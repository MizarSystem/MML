:: Euler's {T}heorem and Small {F}ermat's Theorem
::  by Yoshinori Fujisawa , Yasushi Fuwa and Hidetaka Shimizu
::
:: Received June 10, 1998
:: Copyright (c) 1998 Association of Mizar Users

environ

 vocabularies INT_1, FINSEQ_1, INT_2, ARYTM_3, ABSVALUE, ARYTM_1, NAT_1,
      RELAT_1, FUNCT_1, RFINSEQ, SQUARE_1, BOOLE, CARD_1, EULER_1, FINSET_1,
      FILTER_0, CARD_3, GROUP_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, NUMBERS, XCMPLX_0, XXREAL_0, CARD_1,
      INT_1, INT_2, FINSET_1, NAT_1, NAT_D, FUNCT_1, NEWTON, EULER_1, RFINSEQ,
      RVSUM_1, RELAT_1, FINSEQ_1, FINSEQ_3, TREES_4, WSIERP_1;
 constructors REAL_1, SQUARE_1, NAT_1, NAT_D, NEWTON, RFINSEQ, WSIERP_1,
      EULER_1, SEQM_3;
 registrations FUNCT_1, ORDINAL1, RELSET_1, NUMBERS, XXREAL_0, XREAL_0, NAT_1,
      INT_1, FINSEQ_1, RVSUM_1, WSIERP_1, SEQM_3, NEWTON;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, FUNCT_1, XBOOLE_0;
 theorems INT_2, FINSEQ_1, NAT_1, ABSVALUE, REAL_1, INT_1, FUNCT_1, REAL_2,
      EULER_1, PREPOWER, RFINSEQ, FINSEQ_2, RVSUM_1, FINSEQ_3, FINSET_1,
      NEWTON, RELAT_1, XBOOLE_1, XCMPLX_1, XREAL_1, FINSEQ_5, XXREAL_0,
      ORDINAL1, NAT_D;
 schemes NAT_1, FINSEQ_1;

begin :: Preliminary

reserve a,b,m,x,n,l,xi,xj for Nat,
        t,z for Integer,
        f,F for FinSequence of NAT;

Lm1:a gcd b = a hcf b
proof
   abs(a) = a & abs(b) = b by ABSVALUE:def 1;
  hence thesis by INT_2:def 3;
end;

Lm2:t < 1 iff t <= 0
proof
   t < 1 implies t <= 0
  proof
    assume A1:t < 1;
    assume A2:t > 0;
    then reconsider t as Element of NAT by INT_1:16;
     t >= 1 by A2,NAT_1:14;
    hence contradiction by A1;
  end;
  hence thesis;
end;

Lm3:a <> 0 implies a-1 >= 0
proof
  assume a <> 0;
  then a >= 1 by NAT_1:14;
  then a-1 >= 1-1 by XREAL_1:11;
  hence thesis;
end;

Lm4:1 gcd z = 1
proof
  thus 1 gcd z = abs 1 hcf abs(z) by INT_2:def 3
         .= 1 hcf abs z by ABSVALUE:def 1
         .= 1 by NEWTON:64;
end;

::  Very useful theorem

theorem
  Th1:a,(b qua Integer) are_relative_prime iff a,b are_relative_prime
proof
  thus a,(b qua Integer) are_relative_prime implies
                                    a,b are_relative_prime
  proof
    assume a,(b qua Integer) are_relative_prime;
    then A1:a gcd b = 1 by INT_2:def 4;
     abs(a) = a & abs(b) = b by ABSVALUE:def 1;
    then a hcf b = 1 by A1,INT_2:def 3;
    hence thesis by INT_2:def 6;
  end;
  assume a,b are_relative_prime;
  then A2:a hcf b = 1 by INT_2:def 6;
   abs(a) = a & abs(b) = b by ABSVALUE:def 1;
  then a gcd b = 1 by A2,INT_2:def 3;
  hence thesis by INT_2:def 4;
end;

theorem
  Th2:m > 1 & m*t >= 1 implies t >= 1
proof
  assume A1:m > 1 & m*t >= 1;
  assume t < 1;
  then t <= 0 by Lm2;
  hence contradiction by A1,XREAL_1:133;
end;

Lm5:m > 1 & 1-m <= z & z <= m-1 & m divides z implies z = 0
proof
  assume A1:m > 1 & 1-m <= z & z <= m-1 & m divides z;
  assume A2:z <> 0;
  consider t being Integer such that
A3:z = m*t by A1,INT_1:def 9;
A4:t <> 0 by A2,A3;
    now per cases by A4;
    suppose A5:t > 0;
      then reconsider t as Element of NAT by INT_1:16;
       t >= 1 by A5,NAT_1:14;
      then m*t >= m by NEWTON:45;
      then m*t+1 > m by NAT_1:13;
    hence contradiction by A1,A3,XREAL_1:21;
    end;
    suppose A6:t < 0;
       1 <= m*t+m*1 by A1,A3,XREAL_1:22;
      then A7:1 <= m*(t+1);
      reconsider r = (t+1) as Integer;
       r >= 1 by A1,A7,Th2;
      then 1-1 <= t by XREAL_1:22;
    hence contradiction by A6;
  end;
  end;
  hence contradiction;
end;

canceled 2;

theorem
  Th5:a <> 0 & b <> 0 & m <> 0 &
                     a,m are_relative_prime & b,m are_relative_prime
                                       implies m,a*b mod m are_relative_prime
proof
  assume A1:a <> 0 & b <> 0 & m <> 0 &
                     a,m are_relative_prime & b,m are_relative_prime;
  then a*b,m are_relative_prime by EULER_1:15;
  then A2:a*b hcf m = 1 by INT_2:def 6;
  consider t being Nat such that
A3:a*b = m*t+(a*b mod m) and
   (a*b mod m) < m by A1,NAT_D:def 2;
   a*b <> a*0 by A1,XCMPLX_1:5;
  then A4:a*b+(-t)*m gcd m = a*b gcd m by A1,EULER_1:17;
  A5: a*b gcd m = 1 by A2,Lm1;
   abs(a*b mod m) = a*b mod m & abs(m) = m by ABSVALUE:def 1;
  then (a*b mod m) hcf m = 1 by A3,A4,A5,INT_2:def 3;
  hence thesis by INT_2:def 6;
end;

theorem
  Th6:m > 1 & b <> 0 &
       m,n are_relative_prime & a,m are_relative_prime & n = a*b mod m
           implies m,b are_relative_prime
proof
  assume A1:m > 1 & b <> 0 & m,n are_relative_prime
         & a,m are_relative_prime & n = a*b mod m;
  assume not m,b are_relative_prime;
  then A2:m hcf b <> 1 by INT_2:def 6;
  set k = m hcf b;
  A3:k > 0 by A1,NEWTON:71;
  A4:k divides m & k divides b by NAT_D:def 5;
  then consider km being Nat such that
A5:m = k*km by NAT_D:def 3;
A6:k <> 0 & km <> 0 by A1,A5;
  reconsider km as Element of NAT by ORDINAL1:def 13;
  consider kb being Nat such that
A7:b = k*kb by A4,NAT_D:def 3;
  consider p being Nat such that
A8:a*(k*kb) = (k*km)*p+(a*(k*kb) mod (k*km)) &
          (a*(k*kb) mod (k*km)) < k*km by A1,A5,NAT_D:def 2;
A9:(a*(k*kb) mod (k*km)) = k*((a*kb)-(km*p)) by A8;
  set tm = (a*kb)-(km*p);
A10:tm = 0 implies m gcd n <> 1
  proof
    assume A11: tm = 0;
    A12:0 = abs(0) by ABSVALUE:7;
     k*km = abs(k*km) by ABSVALUE:def 1;
    then k*km gcd 0 = k*km hcf 0 by A12,INT_2:def 3
              .= k*km by NEWTON:65;
    hence thesis by A1,A5,A7,A9,A11;
  end;
A13:tm <> 0 implies m gcd n <> 1
  proof
    assume A14:tm <> 0;
    assume A15:m gcd n = 1;
    A16:k <> -1 by NAT_1:2;
     m gcd n = k*(km gcd tm) by A1,A3,A5,A6,A7,A9,A14,EULER_1:16;
    hence contradiction by A2,A15,A16,INT_1:22;
  end;
  A17:m gcd n = abs(m) hcf abs(n) by INT_2:def 3;
   m = abs(m) & n = abs(n) by ABSVALUE:def 1;
  hence contradiction by A1,A10,A13,A17,INT_2:def 6;
end;

theorem
  Th7: (m mod n) mod n = m mod n
proof
   per cases;
   suppose n <> 0;
   then ex t being Nat st m = n*t+(m mod n) & m mod n < n
    by NAT_D:def 2;
   hence thesis by NAT_D:24;
   end;
   suppose
A1:n = 0;
   hence (m mod n) mod n = 0 by NAT_D:def 2
      .= m mod n by A1,NAT_D:def 2;
 end;
 end;

theorem
   (l+m) mod n = ((l mod n)+(m mod n)) mod n
proof
  thus (l+m) mod n = ((l mod n)+m) mod n by NAT_D:22
               .= ((l mod n)+(m mod n)) mod n by NAT_D:23;
end;

theorem
  Th9: (l*m) mod n = (l*(m mod n)) mod n
proof
  per cases;
  suppose n <> 0;
    then consider t being Nat such that
A1: m = n*t+(m mod n) & (m mod n)<n by NAT_D:def 2;
     (l*m) mod n =((l*t)*n+l*(m mod n)) mod n by A1;
    hence (l*m) mod n = (l*(m mod n)) mod n by NAT_D:21;
   end;
   suppose
A2:n = 0;
   hence (l*m) mod n = 0 by NAT_D:def 2
      .= (l*(m mod n)) mod n by A2,NAT_D:def 2;
end;
end;

theorem
   (l*m) mod n = ((l mod n)*m) mod n by Th9;

theorem
  Th11: (l*m) mod n = ((l mod n)*(m mod n)) mod n
proof
   (l*m) mod n = (l*(m mod n)) mod n by Th9;
  hence thesis by Th9;
end;

begin

::::::::::::::::::::::::::::::::::::::::::::
::                                        ::
::  Function of Element of NAT*(FinSequence of NAT)  ::
::                                        ::
::::::::::::::::::::::::::::::::::::::::::::

definition let f,a;
  redefine func a*f -> FinSequence of NAT;
coherence
  proof
     rng(a*f) c= NAT
    proof
      let x be set;
      assume x in rng (a*f);
      then consider xx being set such that
A1:   xx in dom (a*f) & x = (a*f).xx by FUNCT_1:def 5;
      reconsider xx as Element of NAT by A1;
       (a*f).xx = a*(f.xx) by RVSUM_1:66;
      hence thesis by A1;
    end;
    hence thesis by FINSEQ_1:def 4;
  end;
end;

canceled 13;

theorem
  Th25:for R1,R2 be FinSequence of NAT
         st R1,R2 are_fiberwise_equipotent holds Product R1 = Product R2
proof
  defpred P[Nat] means
    for f,g be FinSequence of NAT st f,g are_fiberwise_equipotent & len f = $1
      holds Product f = Product g;
  A1:P[0]
  proof let f,g be FinSequence of NAT; assume
        f,g are_fiberwise_equipotent & len f = 0;
    then len g = 0 & f = <*>NAT by FINSEQ_1:32,RFINSEQ:16;
    hence thesis by FINSEQ_1:32;
  end;
  A2:for n st P[n] holds P[n+1]
  proof let n; assume
    A3:P[n];
    let f,g be FinSequence of NAT; assume
    A4:f,g are_fiberwise_equipotent & len f = n+1;
    then A5:rng f = rng g & len f = len g by RFINSEQ:1,16;
     0+1<=n+1 by NAT_1:13;
    then A6:n+1 in dom f by A4,FINSEQ_3:27;
    reconsider a = f.(n+1) as Element of NAT;
        a in rng g by A5,A6,FUNCT_1:def 5;
    then consider m being Nat such that
    A7:m in dom g & g.m = a by FINSEQ_2:11;
    reconsider m as Element of NAT by ORDINAL1:def 13;
    A8:g = (g|m)^(g/^m) by RFINSEQ:21;
    A9:1<=m & m<=len g by A7,FINSEQ_3:27;
    then max(0,m-1) = m-1 by FINSEQ_2:4;
    then reconsider m1 = m-1 as Element of NAT by FINSEQ_2:5;
    set gg = g/^m,
        gm = g|m;
    A10:len gm = m by A9,FINSEQ_1:80;
    A11:m = m1+1;
        m in Seg m by A9,FINSEQ_1:3;
    then gm.m = a & m in dom g by A7,RFINSEQ:19;
    then A12:gm = (gm|m1)^<*a*> by A10,A11,RFINSEQ:20;
        m1<=m by A11,NAT_1:11;
    then A13:Seg m1 c= Seg m by FINSEQ_1:7;
    A14:gm|m1 = gm|(Seg m1) by FINSEQ_1:def 15
             .= (g|(Seg m))|(Seg m1) by FINSEQ_1:def 15
             .= g|((Seg m)/\(Seg m1)) by RELAT_1:100
             .= g|(Seg m1) by A13,XBOOLE_1:28
             .= g|m1 by FINSEQ_1:def 15;
    set fn = f|n;
        n<=n+1 by NAT_1:11;
    then A15:len fn = n by A4,FINSEQ_1:80;
    A16:f = fn ^ <*a*> by A4,RFINSEQ:20;
        now let x be set;
          card(f"{x}) = card(g"{x}) by A4,RFINSEQ:def 1;
      then card(fn"{x})+card(<*a*>"{x}) = card(((g|m1)^<*a*>^(g/^m))"{x})
                                           by A8,A12,A14,A16,FINSEQ_3:63
      .= card(((g|m1)^<*a*>)"{x})+card((g/^m)"{x}) by FINSEQ_3:63
      .= card((g|m1)"{x})+card(<*a*>"{x})+card((g/^m)"{x}) by FINSEQ_3:63
      .= card((g|m1)"{x})+card((g/^m)"{x})+card(<*a*>"{x})
      .= card(((g|m1)^(g/^m))"{x})+card(<*a*>"{x}) by FINSEQ_3:63;
      hence card(fn"{x}) = card(((g|m1)^(g/^m))"{x});
    end;
    then fn, (g|m1)^(g/^m) are_fiberwise_equipotent by RFINSEQ:def 1;
    then Product fn = Product((g|m1)^gg) by A3,A15;
    hence Product f = Product((g|m1)^gg)*Product <*a*> by A16,RVSUM_1:127
             .= Product(g|m1)*Product gg*Product <*a*> by RVSUM_1:127
             .= Product(g|m1)*Product <*a*>*Product gg
             .= Product gm*Product gg by A12,A14,RVSUM_1:127
             .= Product g by A8,RVSUM_1:127;
  end;
  A17:for n holds P[n] from NAT_1:sch 2(A1,A2);
  let R1,R2 be FinSequence of NAT; assume
  A18:R1,R2 are_fiberwise_equipotent;
   len R1 = len R1;
  hence thesis by A17,A18;
end;

begin :: Modulus for Finite Sequence of Natural

::::::::::::::::::::::::::::::::::::::::::::::::
::                                            ::
::  Function of (FinSequence of NAT) mod Element of NAT  ::
::                                            ::
::::::::::::::::::::::::::::::::::::::::::::::::

definition let f be FinSequence of NAT,m be Nat;
  func f mod m -> FinSequence of NAT means :Def1:
  len(it) = len(f) & for i being Nat st i in dom f
   holds it.i = (f.i) mod m;
existence
  proof
    deffunc F(Nat) = (f.$1) mod m;
    consider g be FinSequence such that
A1: len g = len f and
A2: for k being Nat st k in Seg len f holds g.k = F(k)
 from FINSEQ_1:sch 2;
     rng g c= NAT
    proof
      let x be set;
      assume x in rng g;
      then consider y being set such that
A3:   y in dom g & x = g.y by FUNCT_1:def 5;
A4:   y in Seg len g by A3,FINSEQ_1:def 3;
      reconsider y as Element of NAT by A3;
       g.y = (f.y) mod m by A1,A2,A4;
      hence x in NAT by A3;
    end;
    then reconsider g as FinSequence of NAT by FINSEQ_1:def 4;
    take g;
    thus len g = len f by A1;
    let k be Nat;
    assume k in dom f;
    then k in Seg len f by FINSEQ_1:def 3;
    hence g.k = (f.k) mod m by A2;
  end;
uniqueness
  proof
    let fa,fb be FinSequence of NAT such that
    A5:len(f) = len(fa) &
          for i being Nat st i in dom f holds fa.i = (f.i) mod m and
    A6:len(f) = len(fb) &
          for i being Nat st i in dom f holds fb.i = (f.i) mod m;
        now let X be set such that
      A7:dom f = X;
      A8: dom f = Seg len f by FINSEQ_1:def 3
           .= dom fa by A5,FINSEQ_1:def 3;
      A9: dom f = Seg len f by FINSEQ_1:def 3
           .= dom fb by A6,FINSEQ_1:def 3;
       for i being Nat st i in X holds fa.i = fb.i
      proof
        given j being Nat such that A10: j in X & fa.j <> fb.j;
         j in X & fa.j <> (f.j) mod m by A6,A7,A10;
        hence contradiction by A5,A7;
      end;
      hence fa = fb by A7,A8,A9,FINSEQ_1:17;
    end;
    hence thesis;
  end;
end;

theorem
   for f be FinSequence of NAT st m <> 0 holds
                          (Product(f mod m)) mod m = (Product f) mod m
proof
  defpred P[Nat] means
    for f be FinSequence of NAT st m <> 0 & len f = $1 holds
         (Product(f mod m)) mod m = (Product f) mod m;
  A1:P[0]
  proof
    let f be FinSequence of NAT;
    assume A2:m <> 0 & len f=0;
    then A3:f = <*> NAT by FINSEQ_1:32;
        len (f mod m) = 0 by A2,Def1;
    hence thesis by A3,FINSEQ_1:32;
  end;
  A4:for n st P[n] holds P[n+1]
  proof
    let n;
    assume A5:P[n];
    let f be FinSequence of NAT;
    assume A6:m <> 0 & len f = n+1;
    reconsider fn = f|n as FinSequence of NAT;
    A7:n <= n+1 by NAT_1:11;
    A8:n <= len f by A6,NAT_1:11;
    A9:len fn = n by A6,A7,FINSEQ_1:80;
    A10: (Product f) mod m = (Product (fn ^ <* f.(n+1) *>)) mod m by A6,
RFINSEQ:20
                    .= ((Product fn)*f.(n+1)) mod m by RVSUM_1:126;
    A11:len (f mod m) = n+1 by A6,Def1;
    A12:(f mod m)|n = fn mod m
    proof
      A13:len((f mod m)|n) = n by A7,A11,FINSEQ_1:80;
      then A14:len((f mod m)|n) = len(fn mod m) by A9,Def1;
          for i being Nat st 1 <= i & i <= len ((f mod m)|n) holds
             ((f mod m)|n).i = (fn mod m).i
      proof
        let i be Nat;
        assume A15:1 <= i & i <= len ((f mod m)|n);
        then A16:i in Seg n by A13,FINSEQ_1:3;
        then A17: ((f mod m)|Seg n).i = (f mod m).i by FUNCT_1:72;
            i <= len f by A8,A13,A15,XXREAL_0:2;
        then A18:i in dom f by A15,FINSEQ_3:27;
        A19: (f|n).i = ((f|Seg n).i) by FINSEQ_1:def 15;
            i in dom fn by A9,A13,A15,FINSEQ_3:27;
        then (fn mod m).i = fn.i mod m by Def1
                         .= f.i mod m by A16,A19,FUNCT_1:72
                         .= (f mod m).i by A18,Def1;
        hence thesis by A17,FINSEQ_1:def 15;
      end;
      hence thesis by A14,FINSEQ_1:18;
    end;
     0+1<=n+1 by NAT_1:13;
    then n+1 in dom f by A6,FINSEQ_3:27;
    then (f mod m).(n+1) = f.(n+1) mod m by Def1;
    then f mod m = (fn mod m) ^ <* f.(n+1) mod m *> by A11,A12,RFINSEQ:20;
    then Product (f mod m) mod m
         = ((Product (fn mod m))*(f.(n+1) mod m)) mod m by RVSUM_1:126
        .= (((Product (fn mod m)) mod m)*((f.(n+1) mod m) mod m)) mod m by Th11
        .= (((Product fn) mod m)*((f.(n+1) mod m) mod m)) mod m by A5,A6,A9
        .= (((Product fn) mod m)*(f.(n+1) mod m)) mod m by Th7;
    hence thesis by A10,Th11;
  end;
  A20:for n holds P[n] from NAT_1:sch 2(A1,A4);
  let f be FinSequence of NAT;
  assume A21:m <> 0;
   len f is Element of NAT;
  hence thesis by A20,A21;
end;

theorem
  Th27:a <> 0 & m > 1 & n <> 0 &
      a*n mod m = n mod m & m,n are_relative_prime implies a mod m = 1
proof
  assume A1:a <> 0 & m > 1 & n <> 0 &
      a*n mod m = n mod m & m,n are_relative_prime;
  then consider k1 be Nat such that
    A2:a*n = m*k1+(a*n mod m) and
        (a*n mod m) < m by NAT_D:def 2;
  consider k2 be Nat such that
    A3:n = m*k2+(n mod m) and
   n mod m < m by A1,NAT_D:def 2;
   (a-1)*n = m*(k1-k2) by A1,A2,A3;
  then A4:m divides (a-1)*n by INT_1:def 9;
  reconsider t = (a-1),m,n as Integer;
      m,n are_relative_prime by A1,Th1;
  then m divides t by A4,INT_2:40;
  then consider tt be Integer such that
    A5:(a-1) = m*tt by INT_1:def 9;
  A6:a = m*tt+1 by A5;
      a-1 >= 0 by A1,Lm3;
  then tt >= 0 by A1,A5,REAL_2:145;
  then reconsider tt,m as Element of NAT by INT_1:16;
      a mod m = (((m*tt) mod m)+1) mod m by A6,NAT_D:22
         .= (0+1) mod m by NAT_D:13
         .= 1 by A1,NAT_D:24;
  hence thesis;
end;

theorem
   (F mod m) mod m = F mod m
proof
  A1:dom ((F mod m) mod m) = Seg(len((F mod m) mod m)) by FINSEQ_1:def 3
                          .= Seg(len(F mod m)) by Def1
                          .= Seg(len F) by Def1
                          .= dom F by FINSEQ_1:def 3;
  A2:dom (F mod m) = Seg(len(F mod m)) by FINSEQ_1:def 3
                  .= Seg(len F) by Def1
                  .= dom F by FINSEQ_1:def 3;
   for x being set st x in dom F holds ((F mod m) mod m).x = (F mod m).x
  proof
    let x be set;
    assume A3:x in dom F;
    then reconsider x as Element of NAT;
        ((F mod m) mod m).x = ((F mod m).x) mod m by A2,A3,Def1
                       .= (F.x mod m) mod m by A3,Def1
                       .= F.x mod m by Th7
                       .= (F mod m).x by A3,Def1;
    hence thesis;
  end;
  hence thesis by A1,A2,FUNCT_1:9;
end;

theorem
  (a*(F mod m)) mod m = (a*F) mod m
proof
  A1:dom ((a*(F mod m)) mod m)
                       = Seg(len((a*(F mod m)) mod m)) by FINSEQ_1:def 3
                      .= Seg(len(a*(F mod m))) by Def1
                      .= Seg(len(F mod m)) by NEWTON:6
                      .= Seg(len F) by Def1
                      .= dom F by FINSEQ_1:def 3;
  A2:dom (a*(F mod m)) = Seg(len(a*(F mod m))) by FINSEQ_1:def 3
                        .= Seg(len(F mod m)) by NEWTON:6
                        .= Seg(len F) by Def1
                        .= dom F by FINSEQ_1:def 3;
  A3:dom ((a*F) mod m) = Seg(len((a*F) mod m)) by FINSEQ_1:def 3
                        .= Seg(len(a*F)) by Def1
                        .= Seg(len F) by NEWTON:6
                        .= dom F by FINSEQ_1:def 3;
  A4:dom (a*F) = Seg(len(a*F)) by FINSEQ_1:def 3
                .= Seg(len F) by NEWTON:6
                .= dom F by FINSEQ_1:def 3;
   for x being set st x in dom F holds
     ((a*(F mod m)) mod m).x = ((a*F) mod m).x
  proof
    let x be set;
    assume A5:x in dom F;
    then reconsider x as Element of NAT;
        ((a*(F mod m)) mod m).x = (a*(F mod m)).x mod m by A2,A5,Def1
                             .= (a*(F mod m).x ) mod m by RVSUM_1:66
                             .= (a*(F.x mod m)) mod m by A5,Def1
                             .= (a*F.x) mod m by Th9
                             .= (a*F).x mod m by RVSUM_1:66
                             .= ((a*F) mod m).x by A4,A5,Def1;
    hence thesis;
  end;
  hence thesis by A1,A3,FUNCT_1:9;
end;

theorem
   for F,G being FinSequence of NAT holds
    (F ^ G) mod m = (F mod m) ^ (G mod m)
proof
  let F,G be FinSequence of NAT;
  A1: dom ((F ^ G) mod m) = Seg(len((F ^ G) mod m)) by FINSEQ_1:def 3
                         .= Seg(len(F ^ G)) by Def1
                         .= dom (F ^ G) by FINSEQ_1:def 3;
  A2: dom ((F mod m) ^ (G mod m))
                  = Seg(len((F mod m) ^ (G mod m))) by FINSEQ_1:def 3
                 .= Seg(len(F mod m)+len(G mod m)) by FINSEQ_1:35
                 .= Seg(len(F)+len(G mod m)) by Def1
                 .= Seg(len(F)+len(G)) by Def1
                 .= Seg(len(F ^ G)) by FINSEQ_1:35
                 .= dom (F ^ G) by FINSEQ_1:def 3;
  A3: dom (F mod m) = Seg(len(F mod m)) by FINSEQ_1:def 3
                   .= Seg(len F) by Def1
                   .= dom F by FINSEQ_1:def 3;
  A4: dom (G mod m) = Seg(len(G mod m)) by FINSEQ_1:def 3
                   .= Seg(len G) by Def1
                   .= dom G by FINSEQ_1:def 3;
   for x being set st x in dom (F ^ G)
                holds ((F ^ G) mod m).x = ((F mod m) ^ (G mod m)).x
  proof
    let x be set;
    assume A5:x in dom (F ^ G);
    then reconsider x as Element of NAT;
        now per cases by A5,FINSEQ_1:38;
      suppose A6: x in dom F;
        A7:((F ^ G) mod m).x = (F ^ G).x mod m by A5,Def1
                             .= F.x mod m by A6,FINSEQ_1:def 7;
            ((F mod m) ^ (G mod m)).x = (F mod m).x by A3,A6,FINSEQ_1:def 7
                                 .= F.x mod m by A6,Def1;
      hence ((F ^ G) mod m).x = ((F mod m) ^ (G mod m)).x by A7;
      end;
      suppose ex n being Nat st n in dom G & x=len F+n;
        then consider n being Element of NAT such that
     A8:n in dom G & x=len F+n;
        A9: len (F mod m) = len F by Def1;
        A10:((F ^ G) mod m).x = (F ^ G).x mod m by A5,Def1
                             .= G.n mod m by A8,FINSEQ_1:def 7;
         ((F mod m) ^ (G mod m)).x = (G mod m).n by A4,A8,A9,FINSEQ_1:def 7
                                 .= G.n mod m by A8,Def1;
        hence ((F ^ G) mod m).x = ((F mod m) ^ (G mod m)).x by A10;
    end;
    end;
    hence thesis;
  end;
  hence thesis by A1,A2,FUNCT_1:9;
end;

theorem
   for F,G being FinSequence of NAT holds
    (a*(F ^ G)) mod m = ((a*F) mod m) ^ ((a*G) mod m)
proof
  let F,G be FinSequence of NAT;
  reconsider FG = F ^ G as FinSequence of NAT;
  A1:dom ((a*(F ^ G)) mod m) = Seg(len((a*FG) mod m)) by FINSEQ_1:def 3
                            .= Seg(len(a*FG)) by Def1
                            .= Seg(len(FG)) by NEWTON:6
                            .= dom (F ^ G) by FINSEQ_1:def 3;
  A2:dom (((a*F) mod m) ^ ((a*G) mod m))
                  = Seg(len(((a*F) mod m) ^ ((a*G) mod m))) by FINSEQ_1:def 3
                 .= Seg(len((a*F) mod m)+len((a*G) mod m)) by FINSEQ_1:35
                 .= Seg(len(a*F)+len((a*G) mod m)) by Def1
                 .= Seg(len(a*F)+len(a*G)) by Def1
                 .= Seg(len(F)+len(a*G)) by NEWTON:6
                 .= Seg(len(F)+len(G)) by NEWTON:6
                 .= Seg(len(F ^ G)) by FINSEQ_1:35
                 .= dom(F ^ G) by FINSEQ_1:def 3;
  A3:dom (a*(F ^ G)) = Seg(len(a*FG)) by FINSEQ_1:def 3
                    .= Seg(len(FG)) by NEWTON:6
                    .= dom (F ^ G) by FINSEQ_1:def 3;
  A4:dom (a*F) = Seg(len(a*F)) by FINSEQ_1:def 3
              .= Seg(len F) by NEWTON:6
              .= dom F by FINSEQ_1:def 3;
  A5:dom ((a*F) mod m) = Seg(len((a*F) mod m)) by FINSEQ_1:def 3
                      .= Seg(len (a*F)) by Def1
                      .= Seg(len(F)) by NEWTON:6
                      .= dom F by FINSEQ_1:def 3;
  A6:dom (a*G) = Seg(len(a*G)) by FINSEQ_1:def 3
              .= Seg(len G) by NEWTON:6
              .= dom G by FINSEQ_1:def 3;
  A7:dom ((a*G) mod m) = Seg(len((a*G) mod m)) by FINSEQ_1:def 3
                      .= Seg(len (a*G)) by Def1
                      .= Seg(len(G)) by NEWTON:6
                      .= dom G by FINSEQ_1:def 3;
      for x being set st x in dom (F ^ G)
             holds ((a*(F ^ G)) mod m).x = (((a*F) mod m) ^ ((a*G) mod m)).x
  proof
    let x be set;
    assume A8:x in dom (F ^ G);
    reconsider H = F ^ G as FinSequence of NAT;
    reconsider x as Element of NAT by A8;
        now per cases by A8,FINSEQ_1:38;
      suppose A9: x in dom F;
        A10:((a*(F ^ G)) mod m).x = ((a*(F ^ G)).x) mod m by A3,A8,Def1
                                 .= (a*H.x) mod m by RVSUM_1:66
                                 .= (a*(F.x)) mod m by A9,FINSEQ_1:def 7;
            (((a*F) mod m) ^ ((a*G) mod m)).x
                                  = ((a*F) mod m).x by A5,A9,FINSEQ_1:def 7
                                 .= ((a*F).x) mod m by A4,A9,Def1
                                 .= (a*(F.x)) mod m by RVSUM_1:66;
      hence ((a*(F ^ G)) mod m).x = (((a*F) mod m) ^ ((a*G) mod m)).x by A10;
      end;
      suppose ex n being Nat st n in dom G & x=len F+n;
        then consider n being Element of NAT such that
          A11:n in dom G & x=len F+n;
        A12: len ((a*F) mod m) = len(a*F) by Def1
                         .= len F by NEWTON:6;
        A13:((a*(F ^ G)) mod m).x = ((a*(F ^ G)).x) mod m by A3,A8,Def1
                                 .= (a*H.x) mod m by RVSUM_1:66
                                 .= (a*G.n) mod m by A11,FINSEQ_1:def 7;
            (((a*F) mod m) ^ ((a*G) mod m)).x
                                  = ((a*G) mod m).n by A7,A11,A12,
FINSEQ_1:def 7
                                 .= ((a*G).n) mod m by A6,A11,Def1
                                 .= (a*G.n) mod m by RVSUM_1:66;
      hence ((a*(F ^ G)) mod m).x = (((a*F) mod m) ^ ((a*G) mod m)).x by A13;
    end;
    end;
    hence thesis;
  end;
  hence thesis by A1,A2,FUNCT_1:9;
end;

:::::::::::::::::::::::::::::::::::
::                               ::
::  Function of (Element of NAT) |^ (Element of NAT)   ::
::                               ::
:::::::::::::::::::::::::::::::::::

theorem
   a <> 0 & m <> 0 & a,m are_relative_prime implies
    for b holds a |^ b,m are_relative_prime
proof
  assume A1:a <> 0 & m <> 0 & a,m are_relative_prime;
  A2:(a |^ 0) hcf m = 1 hcf m by NEWTON:9;
  defpred P[Nat] means (a |^ $1),m are_relative_prime;
    m hcf 1 = 1 by NEWTON:64;
  then A3:P[0] by A2,INT_2:def 6;
  A4:for b holds P[b] implies P[b+1]
  proof
    let b;
    assume A5:(a |^ b),m are_relative_prime;
    A6:(a |^ (b+1)) = (a |^ b)*(a |^ 1) by NEWTON:13
                    .= (a |^ b)*a by NEWTON:10;
        a |^ b <> 0 by A1,PREPOWER:12;
    hence thesis by A1,A5,A6,EULER_1:15;
  end;
    for b holds P[b] from NAT_1:sch 2(A3,A4);
  hence thesis;
end;

begin :: Euler's Theorem and Small Fermat's Theorem
::::::::::::::::::::::::::::::::::::::::::::::::
::                                            ::
::  Euler's Theorem & Small Fermat's Theorem  ::
::                                            ::
::::::::::::::::::::::::::::::::::::::::::::::::

theorem ::Euler's theorem
  Th33:a <> 0 & m > 1 & a,m are_relative_prime implies
    (a |^ Euler m) mod m = 1
proof
  assume A1:a <> 0 & m > 1 & a,m are_relative_prime;
  set X = {k where k is Element of NAT :
   m,k are_relative_prime & k >= 1 & k <= m};
  set Y = {l where l is Element of NAT :
   ex u being Element of NAT st l = a*u mod m & u in X};
::Begin Euler's Lemma1
  A2:x in X implies a*x mod m in X
  proof
    assume x in X;
    then consider t being Element of NAT such that
      A3:t = x and
      A4:m,t are_relative_prime and
      A5:t >= 1 & t <= m;
    A6:a*t,m are_relative_prime by A1,A4,A5,EULER_1:15;
    A7:m,a*t mod m are_relative_prime by A1,A4,A5,Th5;
        a*t mod m <> 0
    proof
      assume a*t mod m = 0;
      then consider s being Nat such that
        A8:a*t = m*s+0 and
              0 < m by A1,NAT_D:def 2;
A9:          s in NAT by ORDINAL1:def 13;
        then s hcf 1 = 1 by NEWTON:64;
      then m*s hcf m*1 = m by A9,EULER_1:6;
      hence contradiction by A1,A6,A8,INT_2:def 6;
    end;
    then A10:a*t mod m >= 1 by NAT_1:14;
     a*t mod m <= m by A1,NAT_D:1;
    hence thesis by A3,A7,A10;
  end;
::End of Euler's Lemma1

::Begin Euler's Lemma2
  A11:xi in X & xj in X & xi <> xj implies a*xi mod m <> a*xj mod m
  proof
    assume A12:xi in X & xj in X & xi <> xj;
    assume A13:a*xi mod m = a*xj mod m;
    set tm = a*xi mod m,sm = a*xj mod m;
    consider t being Element of NAT such that
      A14:t = xi and
          m,t are_relative_prime and
      A15:t >= 1 & t <= m by A12;
    A16:tm = (a*t)-m*((a*t) div m) by A1,A14,NEWTON:77;
    consider s being Element of NAT such that
      A17:s = xj and
          m,s are_relative_prime and
    A18:s >= 1 & s <= m by A12;
 sm = (a*s)-m*((a*s) div m) by A1,A17,NEWTON:77;
    then 0 = a*(t-s)-m*(((a*t) div m)-((a*s) div m)) by A13,A16;
    then A19:m divides a*(t-s) by INT_1:def 9;
    reconsider m,a,c = (t-s) as Integer;
        a,m are_relative_prime by A1,Th1;
    then A20:m divides c by A19,INT_2:40;
        1-m <= c & c <= m-1 by A15,A18,REAL_1:92;
    then t-s = 0 by A1,A20,Lm5;
    hence contradiction by A12,A14,A17;
  end;
::End of Euler's Lemma2

::Begin Euler's Lemma3
  A21:X = Y
  proof
    thus X c= Y
    proof
      let x be set;
      assume x in X;
      then consider xx being Element of NAT such that
        A22:x = xx and
        A23:m,xx are_relative_prime and
        A24:xx >= 1 & xx <= m;
          xx <> m by A1,A23,EULER_1:2;
      then xx < m by A24,XXREAL_0:1;
      then A25:xx mod m = xx by NAT_D:24;
      consider i,j being Integer such that
        A26:i*a+j*m = xx by A1,EULER_1:11;
        A27: i = (i div m)*m+(i mod m) by A1,NEWTON:80;
      A28:i mod m <> 0
      proof
        assume i mod m = 0;
        then A29: 0 = i-(i div m)*m by A1,INT_1:def 8;
        then xx = m*((i div m)*a+j) by A26;
        then A30:((i div m)*a+j) <> 0 & 1 <> 0 by A24;
            m gcd xx = m*1 gcd m*((i div m)*a+j) by A26,A29
                    .= m*(1 gcd ((i div m)*a+j)) by A1,A30,EULER_1:16
                    .= m*1 by Lm4
                    .= m;
        then m hcf xx = m by Lm1;
        hence contradiction by A1,A23,INT_2:def 6;
      end;
          i mod m >= 0 by NEWTON:78;
      then reconsider im = i mod m as Element of NAT by INT_1:16;
      A31:im >= 1 by A28,NAT_1:14;
      A32:im <= m by A1,NEWTON:79;
      reconsider ij = (i mod m)*a+(((i div m)*a)+j)*m as Element of NAT
       by A26,A27;
      A33: ij mod m = im*a mod m by EULER_1:13;
      then m,im are_relative_prime by A1,A23,A25,A26,A27,A28,Th6;
      then im in X by A31,A32;
      hence x in Y by A22,A25,A26,A27,A33;
    end;
    let y be set;
    assume y in Y;
    then consider yy being Element of NAT such that
      A34:y = yy and
      A35:ex u being Element of NAT st yy = a*u mod m & u in X;
    consider uu being Element of NAT such that
      A36:yy = a*uu mod m and
      A37:uu in X by A35;
    thus y in X by A2,A34,A36,A37;
  end;
::End of Euler'S Lemma3

  reconsider FX = Sgm X as FinSequence of NAT;
  reconsider FYY = (a*FX) as FinSequence of NAT;
  reconsider FY = FYY mod m as FinSequence of NAT;

::Begin Euler's Lemma4
  A38:X c= Seg m
  proof
    let x be set;
    assume x in X;
    then consider xx being Element of NAT such that
      A39:x = xx and
          m,xx are_relative_prime and
      A40:xx >= 1 & xx <= m;
    thus thesis by A39,A40,FINSEQ_1:3;
  end;
::End of Euler's Lemma4

  then reconsider X as finite set by FINSET_1:13;
  A41:len(FX) = card X by A38,FINSEQ_3:44
        .= Euler m by EULER_1:def 1;

  A42: dom FY = Seg(len FY) by FINSEQ_1:def 3
        .= Seg(len FYY) by Def1
        .= Seg(len FX) by NEWTON:6;
  then A43:dom FX = dom FY by FINSEQ_1:def 3;
      dom FYY = Seg(len FYY) by FINSEQ_1:def 3
         .= Seg(len FX) by NEWTON:6;
  then A44:dom FX = dom FYY by FINSEQ_1:def 3;

::Begin Euler's Lemma5
  A45:rng FY = Y
  proof
    thus rng FY c= Y
    proof
      let x be set;
      assume x in rng FY;
      then consider y being set such that
        A46:y in dom FY & x = FY.y by FUNCT_1:def 5;
      reconsider y as Element of NAT by A46;
          y in dom FX by A42,A46,FINSEQ_1:def 3;
      then A47: FY.y = (FYY.y) mod m by A44,Def1
               .= (a*FX.y) mod m by RVSUM_1:66;
          FX.y in rng FX by A43,A46,FUNCT_1:def 5;
      then FX.y in X by A38,FINSEQ_1:def 13;
      hence thesis by A46,A47;
    end;
    let y be set;
    assume y in Y;
    then consider yy being Element of NAT such that
      A48:y = yy and
      A49:ex u being Element of NAT st yy = a*u mod m & u in X;
    consider uu being Element of NAT such that
      A50:yy = a*uu mod m and
      A51:uu in X by A49;
        uu in rng FX by A38,A51,FINSEQ_1:def 13;
    then consider xx being set such that
      A52:xx in dom FX & uu = FX.xx by FUNCT_1:def 5;
    reconsider xx as Element of NAT by A52;
        FY.xx = (FYY.xx) mod m by A44,A52,Def1
         .= y by A48,A50,A52,RVSUM_1:66;
    hence y in rng FY by A43,A52,FUNCT_1:def 5;
  end;
::End of Euler's Lemma5

::Begin Euler's Lemma6
  A53: FY is one-to-one
  proof
    let x1,x2 be set;
    assume A54: x1 in dom FY & x2 in dom FY & FY.x1 = FY.x2;
    then A55:x1 in dom FX & x2 in dom FX & FY.x1 = FY.x2 by A42,FINSEQ_1:def 3;
    reconsider x1 as Element of NAT by A54;
    reconsider x2 as Element of NAT by A54;
    A56: FY.x1 = (FYY.x1) mod m by A44,A55,Def1
              .= (a*FX.x1) mod m by RVSUM_1:66;
    A57: FY.x2 = (FYY.x2) mod m by A44,A55,Def1
              .= (a*FX.x2) mod m by RVSUM_1:66;
    A58: FX.x1 in rng FX by A43,A54,FUNCT_1:def 5;
    A59: FX.x2 in rng FX by A43,A54,FUNCT_1:def 5;
    A60:FX is one-to-one by A38,FINSEQ_3:99;
        not(FX.x1 in X & FX.x2 in X & FX.x1 <> FX.x2) by A11,A54,A56,A57;
    hence thesis by A38,A55,A58,A59,A60,FINSEQ_1:def 13,FUNCT_1:def 8;
  end;
::End of Euler's Lemma6

::Begin Euler's Lemma7
   for x be set holds card (FX"{x}) = card (FY"{x})
  proof
    let x be set;
        now per cases;
      suppose A61: x in X;
        then A62: x in rng FX by A38,FINSEQ_1:def 13;
            FX is one-to-one by A38,FINSEQ_3:99;
        then A63: len(FX-{x}) = len(FX)-1 by A62,FINSEQ_3:97;
           A64: len(FX-{x})-len(FX-{x})
                 = len(FX)-card(FX"{x})-len(FX-{x}) by FINSEQ_3:66;
   A65: len(FY-{x}) = len(FY)-1 by A21,A45,A53,A61,FINSEQ_3:97;
         len(FY-{x}) = len(FY)-card(FY"{x}) by FINSEQ_3:66;
        hence thesis by A63,A64,A65;
      end;
      suppose A66: not(x in X);
        then A67: not(x in rng FX) by A38,FINSEQ_1:def 13;
         FY"{x} = {} by A21,A45,A66,FUNCT_1:142;
        hence thesis by A67,FUNCT_1:142;
    end;
    end;
    hence thesis;
  end;
::End of Euler's Lemma7

  then A68:FX,FY are_fiberwise_equipotent by RFINSEQ:def 1;
  then A69:(Product FX) mod m = (Product FY) mod m by Th25;
  A70:len(FX) = len(FY) by A68,RFINSEQ:16;

::Begin Euler's Lemma8
  A71:(Product FY) mod m = (a |^ len(FY)*Product FX) mod m
  proof
    defpred P[Nat] means
       $1 <= len(FY) implies
          (Product (FY|$1)) mod m = (a |^ len(FY|$1)*Product (FX|$1)) mod m;
    A72: P[0]
    proof
       0 <= len FY;
      then len (FY|0) = 0 by FINSEQ_1:80;
      hence thesis by NEWTON:9,RVSUM_1:124;
    end;
    A73: for n st P[n] holds P[n+1]
    proof
      let n;
          now per cases;
        suppose A74: n+1 <= len FY;
          assume A75:P[n];
          A76:n <= n+1 by NAT_1:11;
          then n <= len FY by A74,XXREAL_0:2;
          then A77:len(FY|n) = n by FINSEQ_1:80;
          A78:len (FY|(n+1)) = n+1 by A74,FINSEQ_1:80;
          then A79:FY|(n+1)
                 = (FY|(n+1))|n ^ <* (FY|(n+1)).(n+1) *> by RFINSEQ:20;
           0+1 <= n+1 by NAT_1:13;
          then A80: n+1 in dom FY by A74,FINSEQ_3:27;
          A81:(FY|(n+1))|n = FY|n by A76,FINSEQ_5:80;
          A82: (FY|(n+1)).(n+1) = FY.(n+1) by FINSEQ_3:121;
              len (FX|(n+1)) = n+1 by A70,A74,FINSEQ_1:80;
          then A83:FX|(n+1)
                 = (FX|(n+1))|n ^ <* (FX|(n+1)).(n+1) *> by RFINSEQ:20;
          A84:(FX|(n+1))|n = FX|n by A76,FINSEQ_5:80;
          A85: (FX|(n+1)).(n+1) = FX.(n+1) by FINSEQ_3:121;
              (Product(FY|(n+1))) mod m = (Product(FY|n)*FY.(n+1)) mod m
          by A79,A81,A82,RVSUM_1:126
            .= (((a |^ len(FY|n)*Product(FX|n)) mod m)
                      *(FY.(n+1) mod m)) mod m by A74,A75,A76,Th11,XXREAL_0:2
            .= (((a |^ len(FY|n)*Product(FX|n)) mod m)
                      *((FYY.(n+1) mod m) mod m)) mod m by A43,A44,A80,Def1
            .= (((a |^ len(FY|n)*Product(FX|n)) mod m)
                      *((a*FX).(n+1) mod m)) mod m by Th7
            .= ((a |^ len(FY|n)*Product(FX|n))*(a*FX).(n+1)) mod m by Th11
            .= (a |^ n*Product (FX|n))*(a*(FX.(n+1))) mod m by A77,RVSUM_1:66
            .= (a |^ n*a)*(Product(FX|n)*(FX.(n+1))) mod m
            .= (a |^ len(FY|(n+1)))*(Product(FX|n)*(FX.(n+1))) mod m
               by A78,NEWTON:11
            .= (a |^ len(FY|(n+1)))*(Product(FX|(n+1))) mod m by A83,A84,A85,
RVSUM_1:126;
        hence P[n+1];
        end;
        suppose n+1 > len FY;
        hence thesis;
      end;
      end;
      hence thesis;
    end;
    A86: for n holds P[n] from NAT_1:sch 2(A72,A73);
        FY|len FY = FY|(Seg len FY) by FINSEQ_1:def 15;
    then A87:FY = FY|len FY by FINSEQ_2:23;
        FX|len FX = FX|(Seg len FX) by FINSEQ_1:def 15;
    then FX = FX|len FY by A70,FINSEQ_2:23;
    hence thesis by A86,A87;
  end;

::End of Euler's Lemma8

  A88: len FY = Euler m by A41,A68,RFINSEQ:16;
  A89:a |^ Euler m <> 0 by A1,PREPOWER:12;

::Begin Euler's Lemma9
  A90:Product FX <> 0
  proof
    assume Product FX = 0;
    then consider k being Nat such that
      A91:k in dom FX and
      A92:FX.k = 0 by RVSUM_1:133;
    A93:rng FX = X by A38,FINSEQ_1:def 13;
        FX.k in rng FX by A91,FUNCT_1:def 5;
    then consider p being Element of NAT such that
      A94:FX.k = p and
          m,p are_relative_prime and
      A95:p >= 1 & p <= m by A93;
    thus contradiction by A92,A94,A95;
  end;
::End of Euler's Lemma9

::Begin Euler's Lemma10
   (Product FX),m are_relative_prime
  proof
    defpred P[Nat] means
      $1 <= len FX implies (Product (FX|$1)),m are_relative_prime;
    A96: P[0]
    proof
       (Product (FX|0)) hcf m = 1 by NEWTON:64,RVSUM_1:124;
      hence thesis by INT_2:def 6;
    end;
    A97: for n st P[n] holds P[n+1]
    proof
      let n;
          now per cases;
        suppose A98:len FX >= n+1;
          assume A99:P[n];
          A100: n <= n+1 by NAT_1:11;
          reconsider FF = (FX|(n+1)) as FinSequence of NAT;
          reconsider ff = FF.(n+1) as Element of NAT;
              len FF = n+1 by A98,FINSEQ_1:80;
          then A101:FF = (FF|n) ^ <*ff*> by RFINSEQ:20;
              n <= n+1 by NAT_1:11;
          then A102:FX|n = FF|n by FINSEQ_5:80;
          A103:rng FX = X by A38,FINSEQ_1:def 13;
          A104:(FX|(n+1)).(n+1) = FX.(n+1) by FINSEQ_3:121;
           0+1 <= (n+1) by XREAL_1:8;
          then n+1 in dom FX by A98,FINSEQ_3:27;
          then (FX|(n+1)).(n+1) in rng FX by A104,FUNCT_1:def 5;
          then consider p being Element of NAT such that
            A105:ff = p and
            A106:m,p are_relative_prime and
              p >= 1 & p <= m by A103;
          reconsider a = (Product (FF|n)),b = ff,m' = m as Integer;
          A107:a,m' are_relative_prime by A98,A99,A100,A102,Th1,XXREAL_0:2;
              b,m' are_relative_prime by A105,A106,Th1;
          then a*b,m' are_relative_prime by A107,INT_2:41;
          then (Product ((FF|n) ^ <*ff*>)),m' are_relative_prime by RVSUM_1:126
;
        hence P[n+1] by A101,Th1;
        end;
        suppose len FX < n+1;
        hence thesis;
      end;
      end;
      hence thesis;
    end;
    A108:for n holds P[n] from NAT_1:sch 2(A96,A97);
        FX|len(FX) = FX by FINSEQ_1:79;
    hence thesis by A108;
  end;
  hence thesis by A1,A69,A71,A88,A89,A90,Th27;
end;
::End of Euler's Theorem

theorem ::Small Fermat's theorem
   a <> 0 & m is prime & a,m are_relative_prime implies
    (a |^ m) mod m = a mod m
proof
  assume A1: a<>0 & m is prime & a,m are_relative_prime;
  then A2: m > 1 by INT_2:def 5;
  then m-1 > 1-1 by XREAL_1:11;
  then reconsider mm = m-1 as Element of NAT by INT_1:16;
  A3:mm+1 = m;
   Euler m = m-1 by A1,EULER_1:21;
  then ((a |^ mm) mod m)*a = 1*a by A1,A2,Th33;
  then ((a |^ mm)*a) mod m = a mod m by Th9;
  hence thesis by A3,NEWTON:11;
end;
