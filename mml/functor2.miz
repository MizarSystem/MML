:: Category of Functors between Alternative Categories
::  by Robert Nieszczerzewski
::
:: Received June 12, 1997
:: Copyright (c) 1997 Association of Mizar Users
 
environ

 vocabularies RELAT_2, ALTCAT_1, XBOOLE_0, MSUALG_6, FUNCTOR0, CAT_1, FUNCT_1,
      PZFMISC1, NATTRA_1, PBOOLE, STRUCT_0, SUBSET_1, REALSET1, RELAT_1,
      FUNCT_2, CARD_3, CAT_2, ZFMISC_1, TARSKI, FUNCTOR2;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, FUNCTOR0, MCART_1, DOMAIN_1,
      RELAT_1, STRUCT_0, CARD_3, FUNCT_1, PBOOLE, FUNCT_2, BINOP_1, MULTOP_1,
      ALTCAT_1;
 constructors DOMAIN_1, CARD_3, FUNCTOR0;
 registrations XBOOLE_0, SUBSET_1, RELSET_1, STRUCT_0, ALTCAT_1, FUNCTOR0,
      PBOOLE, FUNCT_1, RELAT_1;
 requirements SUBSET, BOOLE;
 definitions ALTCAT_1, BINOP_1;
 theorems MCART_1, MULTOP_1, FUNCT_2, ZFMISC_1, ALTCAT_1, PBOOLE, BINOP_1,
      TARSKI, ALTCAT_2, FUNCTOR0, CARD_3, XBOOLE_0, PARTFUN1, RELAT_1;
 schemes TARSKI, PBOOLE, MSSUBFAM, XBOOLE_0;

begin

registration
  let A be transitive with_units (non empty AltCatStr), B be with_units (non
  empty AltCatStr);
  cluster -> feasible id-preserving Functor of A, B;
  coherence by FUNCTOR0:def 26;
end;

registration
  let A be transitive with_units (non empty AltCatStr), B be with_units (non
  empty AltCatStr);
  cluster covariant -> Covariant comp-preserving Functor of A, B;
  coherence by FUNCTOR0:def 27;
  cluster Covariant comp-preserving -> covariant Functor of A, B;
  coherence by FUNCTOR0:def 27;
  cluster contravariant -> Contravariant comp-reversing Functor of A, B;
  coherence by FUNCTOR0:def 28;
  cluster Contravariant comp-reversing -> contravariant Functor of A, B;
  coherence by FUNCTOR0:def 28;
end;

canceled;

theorem Th2:
  for A,B being transitive with_units (non empty AltCatStr), F
being covariant Functor of A,B for a being object of A holds F.idm a = idm (F.a
  )
proof
  let A,B be transitive with_units (non empty AltCatStr), F be covariant
  Functor of A,B;
  let a be object of A;
  <^a,a^> <> {} & <^F.a,F.a^> <> {} by ALTCAT_2:def 7;
  hence F.idm a = Morph-Map(F,a,a).idm a by FUNCTOR0:def 16
    .= idm (F.a) by FUNCTOR0:def 21;
end;

begin :: Transformations

definition
  let A,B be transitive with_units (non empty AltCatStr),
      F1,F2 be Functor of A,B;
  pred F1 is_transformable_to F2 means
  :Def1:
  for a being object of A holds <^ F1.a,F2.a^> <> {};
  reflexivity by ALTCAT_2:def 7;
end;

canceled;

theorem Th4:
  for A,B being transitive with_units (non empty AltCatStr), F,F1,
  F2 being Functor of A,B holds F is_transformable_to F1 & F1
  is_transformable_to F2 implies F is_transformable_to F2
proof
  let A,B be transitive with_units (non empty AltCatStr),
  F,F1,F2 be Functor of A,B;
  assume
A1: F is_transformable_to F1 & F1 is_transformable_to F2;
  let a be object of A;
  <^F.a,F1.a^> <> {} & <^F1.a,F2.a^> <> {} by A1,Def1;
  hence thesis by ALTCAT_1:def 4;
end;

definition
  let A,B be transitive with_units (non empty AltCatStr),
  F1,F2 be Functor of A,B;
  assume
A1: F1 is_transformable_to F2;
  mode transformation of F1,F2 -> ManySortedSet of the carrier of A means
    :Def2: for a being object of A holds it.a is Morphism of F1.a,F2.a;
  existence
  proof
    defpred P[object of A,set] means $2 in <^F1.$1,F2.$1^>;
A2: for a being Element of A ex j being set st P[a,j]
    proof
      let a be Element of A;
      reconsider o = a as object of A;
      <^F1.o,F2.o^> <> {} by A1,Def1;
      hence thesis by XBOOLE_0:def 1;
    end;
    consider t being ManySortedSet of the carrier of A such that
A3: for a being Element of A holds P[a,t.a] from PBOOLE:sch 6(A2);
    take t;
    thus thesis by A3;
  end;
end;

definition
  let A,B be transitive with_units (non empty AltCatStr);
  let F be Functor of A,B;
  func idt F -> transformation of F,F means
  :Def3:
  for a being object of A holds it.a = idm (F.a);
  existence
  proof
    defpred P[object of A,set] means $2 = idm (F.$1);
A1: for a being Element of A ex j being set st P[a,j];
    consider t being ManySortedSet of the carrier of A such that
A2: for a being Element of A holds P[a,t.a] from PBOOLE:sch 6(A1);
    for a be object of A holds t.a is Morphism of F.a,F.a
    proof
      let a be Element of A;
      P[a,t.a] by A2;
      hence thesis;
    end;
    then t is transformation of F,F by Def2;
    hence thesis by A2;
  end;
  uniqueness
  proof
    let it1,it2 be transformation of F,F such that
A3: for a being object of A holds it1.a = idm (F.a) and
A4: for a being object of A holds it2.a = idm (F.a);
    now
      let a be set;
      assume a in the carrier of A;
      then reconsider o = a as object of A;
      thus it1.a = idm (F.o) by A3
        .= it2.a by A4;
    end;
    hence it1 = it2 by PBOOLE:3;
  end;
end;

definition
  let A,B be transitive with_units (non empty AltCatStr),
  F1,F2 be Functor of A,B;
  assume
A1: F1 is_transformable_to F2;
  let t be transformation of F1,F2;
  let a be object of A;
  func t!a -> Morphism of F1.a, F2.a means
  :Def4:
  it = t.a;
  existence
  proof
    t.a is Morphism of F1.a,F2.a by A1,Def2;
    hence thesis;
  end;
  correctness;
end;

definition
  let A,B be transitive with_units (non empty AltCatStr),
      F,F1,F2 be Functor of A,B;
  assume
A1: F is_transformable_to F1 & F1 is_transformable_to F2;
  let t1 be transformation of F,F1;
  let t2 be transformation of F1,F2;
  func t2`*`t1 -> transformation of F,F2 means
  :Def5:
  for a being object of A holds it!a = (t2!a) * (t1!a);
  existence
  proof
    defpred P[object of A,set] means $2 = (t2!$1) * (t1!$1);
A2: for a being Element of A ex j being set st P[a,j];
    consider t being ManySortedSet of the carrier of A such that
A3: for a being Element of A holds P[a,t.a] from PBOOLE:sch 6(A2);
A4: F is_transformable_to F2 by A1,Th4;
    for a be object of A holds t.a is Morphism of F.a,F2.a
    proof
      let o be Element of A;
      P[o,t.o] by A3;
      hence thesis;
    end;
    then reconsider t9 = t as transformation of F,F2 by A4,Def2;
    take t9;
    let a be Element of A;
    P[a,t.a] by A3;
    hence thesis by A4,Def4;
  end;
  uniqueness
  proof
    let it1,it2 be transformation of F,F2 such that
A5: for a being object of A holds it1!a = (t2!a)*(t1!a) and
A6: for a being object of A holds it2!a = (t2!a)*(t1!a);
A7: F is_transformable_to F2 by A1,Th4;
    now
      let a be set;
      assume a in the carrier of A;
      then reconsider o = a as object of A;
      thus (it1 qua ManySortedSet of the carrier of A).a = it1!o by A7,Def4
        .= (t2!o)*(t1!o) by A5
        .= it2!o by A6
        .= (it2 qua ManySortedSet of the carrier of A).a by A7,Def4;
    end;
    hence it1 = it2 by PBOOLE:3;
  end;
end;

theorem Th5:
  for A,B being transitive with_units (non empty AltCatStr), F1,F2
being Functor of A,B holds F1 is_transformable_to F2 implies for t1,
  t2 being transformation of F1,F2 st for a being object of A holds t1!a = t2!a
  holds t1 = t2
proof
  let A,B be transitive with_units (non empty AltCatStr), F1,F2 be 
  Functor of A,B;
  assume
A1: F1 is_transformable_to F2;
  let t1,t2 be transformation of F1,F2;
  assume
A2: for a being object of A holds t1!a = t2!a;
  now
    let a be set;
    assume a in the carrier of A;
    then reconsider o = a as object of A;
    thus (t1 qua ManySortedSet of the carrier of A).a = t1!o by A1,Def4
      .= t2!o by A2
      .= (t2 qua ManySortedSet of the carrier of A).a by A1,Def4;
  end;
  hence thesis by PBOOLE:3;
end;

theorem Th6:
  for A,B being transitive with_units (non empty AltCatStr), F
being Functor of A,B holds for a being object of A holds (idt F)!a =
  idm(F.a)
proof
  let A,B be transitive with_units (non empty AltCatStr), F be 
  Functor of A,B;
  let a be object of A;
  thus (idt F)!a = (idt F qua ManySortedSet of the carrier of A).a by Def4
    .= idm (F.a) by Def3;
end;

theorem Th7:
  for A,B being transitive with_units (non empty AltCatStr), F1,F2
  being Functor of A,B holds F1 is_transformable_to F2 implies for t
  being transformation of F1,F2 holds (idt F2)`*`t = t & t`*`(idt F1) = t
proof
  let A,B be transitive with_units (non empty AltCatStr), F1,F2 be 
  Functor of A,B;
  assume
A1: F1 is_transformable_to F2;
  let t be transformation of F1,F2;
  now
    let a be object of A;
A2: <^F1.a,F2.a^> <> {} by A1,Def1;
    thus ((idt F2)`*`t)!a = ((idt F2)!a)*(t!a) by A1,Def5
      .= (idm(F2.a))*(t!a) by Th6
      .= t!a by A2,ALTCAT_1:24;
  end;
  hence (idt F2)`*`t = t by A1,Th5;
  now
    let a be object of A;
A3: <^F1.a,F2.a^> <> {} by A1,Def1;
    thus (t`*`(idt F1))!a = (t!a)*((idt F1)!a) by A1,Def5
      .= (t!a)*(idm(F1.a)) by Th6
      .= t!a by A3,ALTCAT_1:def 19;
  end;
  hence thesis by A1,Th5;
end;

theorem Th8:
  for A,B being category, F,F1,F2,F3 being Functor of A,B
  holds F is_transformable_to F1 & F1 is_transformable_to F2 & F2
  is_transformable_to F3 implies for t1 being transformation of F,F1, t2 being
transformation of F1,F2, t3 being transformation of F2,F3 holds t3`*`t2`*`t1 =
  t3`*`(t2`*`t1)
proof
  let A,B be category, F,F1,F2,F3 be Functor of A,B;
  assume that
A1: F is_transformable_to F1 and
A2: F1 is_transformable_to F2 and
A3: F2 is_transformable_to F3;
  let t1 be transformation of F,F1, t2 be transformation of F1,F2, t3 be
  transformation of F2,F3;
A4: F1 is_transformable_to F3 by A2,A3,Th4;
A5: F is_transformable_to F2 by A1,A2,Th4;
  now
    let a be object of A;
A6: <^F2.a,F3.a^> <> {} by A3,Def1;
A7: <^F.a,F1.a^> <> {} & <^F1.a,F2.a^> <> {} by A1,A2,Def1;
    thus (t3`*`t2`*`t1)!a = ((t3`*`t2)!a)*(t1!a) by A1,A4,Def5
      .= (t3!a)*(t2!a)*(t1!a) by A2,A3,Def5
      .= (t3!a)*((t2!a)*(t1!a)) by A7,A6,ALTCAT_1:25
      .= (t3!a)*((t2`*`t1)!a) by A1,A2,Def5
      .= (t3`*`(t2`*`t1))!a by A3,A5,Def5;
  end;
  hence thesis by A1,A4,Th4,Th5;
end;

begin :: Natural transformations

Lm1: for A,B being transitive with_units (non empty AltCatStr), F,G being
covariant Functor of A,B for a,b being object of A st <^a,b^> <> {} for f being
Morphism of a,b holds (idt F)!b*F.f = F.f*((idt F)!a)
proof
  let A,B be transitive with_units (non empty AltCatStr), F,G be covariant
  Functor of A,B;
  let a,b be object of A such that
A1: <^a,b^> <> {};
  let f be Morphism of a,b;
A2: <^a,a^> <> {} by ALTCAT_2:def 7;
A3: <^b,b^> <> {} by ALTCAT_2:def 7;
  thus (idt F)!b*F.f = idm(F.b)*F.f by Th6
    .= F.idm b*F.f by Th2
    .= F.(idm b*f) by A1,A3,FUNCTOR0:def 24
    .= F.f by A1,ALTCAT_1:24
    .= F.(f*idm a) by A1,ALTCAT_1:def 19
    .= F.f*F.idm a by A1,A2,FUNCTOR0:def 24
    .= F.f*idm(F.a) by Th2
    .= F.f*((idt F)!a) by Th6;
end;

definition
  let A,B be transitive with_units (non empty AltCatStr), F1,F2 be covariant
  Functor of A,B;
  pred F1 is_naturally_transformable_to F2 means
  :Def6:
  F1 is_transformable_to
F2 & ex t being transformation of F1,F2 st for a,b being object of A st <^a,b^>
  <> {} for f being Morphism of a,b holds t!b*F1.f = F2.f*(t!a);
  reflexivity
proof
  let F be covariant Functor of A,B;
  thus F is_transformable_to F;
  take idt F;
  thus thesis by Lm1;
end;
end;

theorem
  for A,B be transitive with_units (non empty AltCatStr), F be
  covariant Functor of A,B holds F is_naturally_transformable_to F;

Lm2: for A,B be category, F,F1,F2 be covariant Functor of A,B holds F
is_transformable_to F1 & F1 is_transformable_to F2 implies for t1 being
transformation of F,F1 st for a,b being object of A st <^a,b^> <> {} for f
being Morphism of a,b holds t1!b*F.f = F1.f*(t1!a) for t2 being transformation
of F1,F2 st for a,b being object of A st <^a,b^> <> {} for f being Morphism of
a,b holds t2!b*F1.f = F2.f*(t2!a) for a,b being object of A st <^a,b^><>{} for
f being Morphism of a,b holds (t2`*`t1)!b*F.f = F2.f*((t2`*`t1)!a)
proof
  let A,B be category, F,F1,F2 be covariant Functor of A,B;
  assume that
A1: F is_transformable_to F1 and
A2: F1 is_transformable_to F2;
  let t1 be transformation of F,F1 such that
A3: for a,b being object of A st <^a,b^> <> {} for f being Morphism of a
  ,b holds t1!b*F.f = F1.f*(t1!a);
  let t2 be transformation of F1,F2 such that
A4: for a,b being object of A st <^a,b^> <> {} for f being Morphism of a
  ,b holds t2!b*F1.f = F2.f*(t2!a);
  let a,b be object of A;
A5: <^F.b,F1.b^> <> {} by A1,Def1;
A6: <^F.a,F1.a^> <> {} by A1,Def1;
A7: <^F1.b,F2.b^> <> {} by A2,Def1;
A8: <^F1.a,F2.a^> <> {} by A2,Def1;
  assume
A9: <^a,b^> <> {};
  then
A10: <^F.a,F.b^> <> {} by FUNCTOR0:def 19;
  let f be Morphism of a,b;
A11: <^F2.a,F2.b^> <> {} by A9,FUNCTOR0:def 19;
A12: <^F1.a,F1.b^> <> {} by A9,FUNCTOR0:def 19;
  thus ((t2`*`t1)!b)*(F.f) = ((t2!b)*(t1!b))*F.f by A1,A2,Def5
    .= t2!b*(t1!b*F.f) by A10,A5,A7,ALTCAT_1:25
    .= t2!b*(F1.f*(t1!a)) by A3,A9
    .= t2!b*F1.f*(t1!a) by A6,A7,A12,ALTCAT_1:25
    .= F2.f*(t2!a)*(t1!a) by A4,A9
    .= F2.f*((t2!a)*(t1!a)) by A6,A8,A11,ALTCAT_1:25
    .= F2.f*((t2`*`t1)!a) by A1,A2,Def5;
end;

theorem Th10:
  for A,B be category, F,F1,F2 be covariant Functor of A,B holds F
is_naturally_transformable_to F1 & F1 is_naturally_transformable_to F2 implies
  F is_naturally_transformable_to F2
proof
  let A,B be category, F,F1,F2 be covariant Functor of A,B;
  assume
A1: F is_transformable_to F1;
  given t1 being transformation of F,F1 such that
A2: for a,b being object of A st <^a,b^> <> {} for f being Morphism of a
  ,b holds t1!b*F.f = F1.f*(t1!a);
  assume
A3: F1 is_transformable_to F2;
  given t2 being transformation of F1,F2 such that
A4: for a,b being object of A st <^a,b^> <> {} for f being Morphism of a
  ,b holds t2!b*F1.f = F2.f*(t2!a);
  thus F is_transformable_to F2 by A1,A3,Th4;
  take t2`*`t1;
  thus thesis by A1,A2,A3,A4,Lm2;
end;

definition
  let A,B be transitive with_units (non empty AltCatStr), F1,F2 be covariant
  Functor of A,B;
  assume
A1: F1 is_naturally_transformable_to F2;
  mode natural_transformation of F1,F2 -> transformation of F1,F2 means
    :Def7:
for a,b being object of A st <^a,b^> <> {} for f being Morphism of a,b holds it
    !b*F1.f = F2.f*(it!a);
  existence by A1,Def6;
end;

definition
  let A,B be transitive with_units (non empty AltCatStr), F be covariant
  Functor of A,B;
  redefine func idt F -> natural_transformation of F,F;
  coherence
  proof
    F is_naturally_transformable_to F & for a,b being object of A st <^a,b
^> <> {} for f being Morphism of a,b holds (idt F)!b*F.f = F.f*((idt F)!a) by
    Lm1;
    hence thesis by Def7;
  end;
end;

definition
  let A,B be category, F,F1,F2 be covariant Functor of A,B such that
A1: F is_naturally_transformable_to F1 & F1 is_naturally_transformable_to F2;
  let t1 be natural_transformation of F,F1;
  let t2 be natural_transformation of F1,F2;
  func t2`*`t1 -> natural_transformation of F,F2 means
  :Def8:
  it = t2`*`t1;
  existence
  proof
A2: F is_naturally_transformable_to F2 by A1,Th10;
A3: ( for a,b being object of A st <^a,b^> <> {} for f being Morphism of a
,b holds t1!b*F.f = F1.f*(t1!a))& for a,b being object of A st <^a,b^> <> {}
    for f being Morphism of a,b holds t2!b*F1.f = F2.f*(t2!a) by A1,Def7;
    F is_transformable_to F1 & F1 is_transformable_to F2 by A1,Def6;
    then
    for a,b being object of A st <^a,b^> <> {} for f being Morphism of a,b
    holds (t2`*`t1)!b*F.f = F2.f*((t2`*`t1)!a) by A3,Lm2;
    then t2`*`t1 is natural_transformation of F,F2 by A2,Def7;
    hence thesis;
  end;
  correctness;
end;

theorem
  for A,B be transitive with_units (non empty AltCatStr), F1,F2 be
covariant Functor of A,B holds F1 is_naturally_transformable_to F2 implies for
t being natural_transformation of F1,F2 holds (idt F2)`*`t = t & t`*`(idt F1) =
  t
proof
  let A,B be transitive with_units (non empty AltCatStr), F1,F2 be covariant
  Functor of A,B;
  assume F1 is_naturally_transformable_to F2;
  then
A1: F1 is_transformable_to F2 by Def6;
  let t be natural_transformation of F1,F2;
  thus (idt F2)`*`t = t by A1,Th7;
  thus thesis by A1,Th7;
end;

theorem
  for A,B be transitive with_units (non empty AltCatStr), F,F1,F2 be
  covariant Functor of A,B holds F is_naturally_transformable_to F1 & F1
is_naturally_transformable_to F2 implies for t1 being natural_transformation of
F,F1 for t2 being natural_transformation of F1,F2 for a being object of A holds
  (t2`*`t1)!a = (t2!a)*(t1!a)
proof
  let A,B be transitive with_units (non empty AltCatStr), F,F1,F2 be covariant
  Functor of A,B;
  assume
A1: F is_naturally_transformable_to F1 & F1 is_naturally_transformable_to F2;
  let t1 be natural_transformation of F,F1;
  let t2 be natural_transformation of F1,F2;
  let a be object of A;
  F is_transformable_to F1 & F1 is_transformable_to F2 by A1,Def6;
  hence thesis by Def5;
end;

theorem
  for A,B be category, F,F1,F2,F3 be covariant Functor of A,B for t be
  natural_transformation of F,F1, t1 be natural_transformation of F1,F2 holds F
  is_naturally_transformable_to F1 & F1 is_naturally_transformable_to F2 & F2
is_naturally_transformable_to F3 implies for t3 being natural_transformation of
  F2,F3 holds t3`*`t1`*`t = t3`*`(t1`*`t)
proof
  let A,B be category, F,F1,F2,F3 be covariant Functor of A,B;
  let t be natural_transformation of F,F1, t1 be natural_transformation of F1,
  F2;
  assume that
A1: F is_naturally_transformable_to F1 and
A2: F1 is_naturally_transformable_to F2 and
A3: F2 is_naturally_transformable_to F3;
A4: F is_naturally_transformable_to F2 by A1,A2,Th10;
  let t3 be natural_transformation of F2,F3;
A5: F2 is_transformable_to F3 by A3,Def6;
A6: F is_transformable_to F1 & F1 is_transformable_to F2 by A1,A2,Def6;
  F1 is_naturally_transformable_to F3 by A2,A3,Th10;
  hence t3`*`t1`*`t = t3`*`t1`*`(t qua transformation of F,F1) by A1,Def8
    .= (t3 qua transformation of F2,F3)`*`t1`*`t by A2,A3,Def8
    .= t3`*`(t1`*`(t qua transformation of F,F1)) by A6,A5,Th8
    .= (t3 qua transformation of F2,F3)`*`(t1`*`t) by A1,A2,Def8
    .= t3 `*`(t1`*`t) by A3,A4,Def8;
end;

begin :: Category of Functors

definition
  let I be set;
  let A,B be ManySortedSet of I;
  func Funcs(A,B) -> set means
  :Def9:
  for x be set holds x in it iff x is
ManySortedFunction of A,B if for i being set st i in I holds B.i = {} implies A
  .i = {} otherwise it = {};
  existence
  proof
    thus (for i being set st i in I holds B.i = {} implies A.i = {}) implies
ex IT being set st for x be set holds x in IT iff x is ManySortedFunction of A,
    B
    proof
      deffunc F(set) = Funcs(A.$1,B.$1);
      assume
A1:   for i being set st i in I holds B.i = {} implies A.i = {};
      consider F being ManySortedSet of I such that
A2:   for i be set st i in I holds F.i = F(i) from PBOOLE:sch 4;
      take product F;
      let x be set;
      thus x in product F implies x is ManySortedFunction of A,B
      proof
        assume x in product F;
        then consider g be Function such that
A3:     x = g and
A4:     dom g = dom F and
A5:     for i be set st i in dom F holds g.i in F.i by CARD_3:def 5;
A6:     for i be set st i in I holds g.i is Function of A.i,B.i
        proof
          let i be set such that
A7:       i in I;
          dom F = I & F.i = Funcs(A.i,B.i) by A2,A7,PARTFUN1:def 4;
          hence thesis by A5,A7,FUNCT_2:121;
        end;
        dom F = I by PARTFUN1:def 4;
        then reconsider g as ManySortedSet of I by A4,PARTFUN1:def 4,RELAT_1:
        def 18;
        g is ManySortedFunction of A,B by A6,PBOOLE:def 18;
        hence thesis by A3;
      end;
      assume
A8:   x is ManySortedFunction of A,B;
      then reconsider g = x as ManySortedSet of I;
A9:   for i be set st i in I holds g.i in Funcs(A.i,B.i)
      proof
        let i be set;
        assume
A10:    i in I;
        then
A11:    B.i = {} implies A.i = {} by A1;
        g.i is Function of A.i, B.i by A8,A10,PBOOLE:def 18;
        hence thesis by A11,FUNCT_2:11;
      end;
A12:  for i be set st i in I holds g.i in F.i
      proof
        let i be set;
        assume
A13:    i in I;
        then F.i = Funcs(A.i,B.i) by A2;
        hence thesis by A9,A13;
      end;
A14:  for i be set st i in dom F holds g.i in F.i
      proof
A15:    dom F = I by PARTFUN1:def 4;
        let i be set;
        assume i in dom F;
        hence thesis by A12,A15;
      end;
      dom g = I by PARTFUN1:def 4;
      then dom g = dom F by PARTFUN1:def 4;
      hence thesis by A14,CARD_3:def 5;
    end;
    thus thesis;
  end;
  uniqueness
  proof
    let it1,it2 be set;
    hereby
      assume for i being set st i in I holds B.i = {} implies A.i = {};
      assume that
A16:  for x being set holds x in it1 iff x is ManySortedFunction of A ,B and
A17:  for x being set holds x in it2 iff x is ManySortedFunction of A ,B;
      now
        let x be set;
        x in it1 iff x is ManySortedFunction of A,B by A16;
        hence x in it1 iff x in it2 by A17;
      end;
      hence it1 = it2 by TARSKI:2;
    end;
    thus thesis;
  end;
  consistency;
end;

definition
  let A,B be transitive with_units (non empty AltCatStr);
  func Funct(A,B) -> set means
  :Def10:
  for x being set holds x in it iff x is covariant strict Functor of A,B;
  existence
  proof
    set A9 = Funcs([:the carrier of A,the carrier of A:], [:the carrier of B,
    the carrier of B:]);
    set sAA = {(the Arrows of B)*f where f is Element of A9: not contradiction
    };
    consider f be Element of A9;
    (the Arrows of B)*f in sAA;
    then reconsider sAA as non empty set;
    defpred R[set,set] means ex f be Covariant bifunction of the carrier of A,
the carrier of B, m be MSUnTrans of f, the Arrows of A, the Arrows of B st $1 =
    [f,m] & $2 = FunctorStr (#f,m#) & $2 is covariant Functor of A,B;
    defpred P[set,set] means ex AA being ManySortedSet of [:the carrier of A,
    the carrier of A:] st $1 = AA & $2 = Funcs(the Arrows of A, AA);
A1: for x,y,z being set st P[x,y] & P[x,z] holds y = z;
    consider XX being set such that
A2: for x being set holds x in XX iff ex y being set st y in sAA & P[y
    ,x] from TARSKI:sch 1(A1);
A3: for x,y,z be set st R[x,y] & R[x,z] holds y = z
    proof
      let x,y,z be set;
      given f be Covariant bifunction of the carrier of A,the carrier of B, m
      be MSUnTrans of f, the Arrows of A, the Arrows of B such that
A4:   x = [f,m] and
A5:   y = FunctorStr (#f,m#) and
      y is covariant Functor of A,B;
      given f1 be Covariant bifunction of the carrier of A,the carrier of B,
      m1 be MSUnTrans of f1, the Arrows of A, the Arrows of B such that
A6:   x = [f1,m1] and
A7:   z = FunctorStr (#f1,m1#) and
      z is covariant Functor of A,B;
      f=f1 by A4,A6,ZFMISC_1:33;
      hence thesis by A4,A5,A6,A7,ZFMISC_1:33;
    end;
    consider X be set such that
A8: for x be set holds x in X iff ex y be set st y in [:A9,union XX:]
    & R[y,x] from TARSKI:sch 1(A3);
    take X;
    let x be set;
    thus x in X implies x is covariant strict Functor of A,B
    proof
      assume x in X;
      then ex y be set st y in [:A9,union XX:] & ex f be Covariant bifunction
of the carrier of A,the carrier of B, m be MSUnTrans of f, the Arrows of A, the
Arrows of B st y = [f,m] & x = FunctorStr (#f,m#) & x is covariant Functor of A
      ,B by A8;
      hence thesis;
    end;
    assume x is covariant strict Functor of A,B;
    then reconsider F = x as covariant strict Functor of A,B;
    reconsider f = the ObjectMap of F as Covariant bifunction of the carrier
    of A,the carrier of B by FUNCTOR0:def 13;
    reconsider m = the MorphMap of F as MSUnTrans of f, the Arrows of A, the
    Arrows of B;
    reconsider y = [f,m] as set;
A9: for o1,o2 be object of A st (the Arrows of A).(o1,o2) <> {} holds (
    the Arrows of B).(f.(o1,o2)) <> {}
    proof
      let o1,o2 be object of A such that
A10:  (the Arrows of A).(o1,o2) <> {};
      (the Arrows of A).(o1,o2) = <^o1,o2^>;
      hence thesis by A10,FUNCTOR0:def 12;
    end;
A11: for o1,o2 be object of A st (the Arrows of A).(o1,o2) <> {} holds (
    the Arrows of B).([F.o1,F.o2]) <> {}
    proof
      let o1,o2 be object of A such that
A12:  (the Arrows of A).(o1,o2) <> {};
      f.(o1,o2) = [F.o1,F.o2] by FUNCTOR0:23;
      hence thesis by A9,A12;
    end;
    y in [:A9,union XX:]
    proof
      set I = [:the carrier of A,the carrier of A:];
      reconsider AA = (the Arrows of B)*f as ManySortedSet of [:the carrier of
      A,the carrier of A:];
A13:  for i be set st i in I & (the Arrows of A).i <> {} holds (the
      Arrows of B).(f.i) <> {}
      proof
        let i be set such that
A14:    i in I and
A15:    (the Arrows of A).i <> {};
        consider o1,o2 be set such that
A16:    o1 in the carrier of A & o2 in the carrier of A and
A17:    i = [o1,o2] by A14,ZFMISC_1:def 2;
        reconsider a1 = o1, a2 = o2 as object of A by A16;
A18:    (the Arrows of B).(f.i) = (the Arrows of B).(f.(o1,o2)) by A17
          .= (the Arrows of B).([F.a1,F.a2]) by FUNCTOR0:23;
        (the Arrows of A).(o1,o2) <> {} by A15,A17;
        hence thesis by A11,A18;
      end;
      for i be set st i in I holds (the Arrows of A).i <> {} implies AA.i <> {}
      proof
        let i be set such that
A19:    i in I;
        assume
A20:    (the Arrows of A).i <> {};
        AA.i = (the Arrows of B).(f.i) by A19,FUNCT_2:21;
        hence thesis by A13,A19,A20;
      end;
      then m is ManySortedFunction of the Arrows of A,AA & for i be set st i
      in I holds AA.i = {} implies (the Arrows of A). i = {} by FUNCTOR0:def 5;
      then
A21:  m in Funcs(the Arrows of A,AA) by Def9;
A22:  f in A9 by FUNCT_2:11;
      then (the Arrows of B)*f in sAA;
      then Funcs(the Arrows of A,AA) in XX by A2;
      then m in union XX by A21,TARSKI:def 4;
      hence thesis by A22,ZFMISC_1:def 2;
    end;
    hence thesis by A8;
  end;
  uniqueness
  proof
    let it1,it2 be set such that
A23: for x being set holds x in it1 iff x is covariant strict Functor
    of A, B and
A24: for x being set holds x in it2 iff x is covariant strict Functor of A,B;
    now
      let x be set;
      x in it1 iff x is covariant strict Functor of A,B by A23;
      hence x in it1 iff x in it2 by A24;
    end;
    hence thesis by TARSKI:2;
  end;
end;

definition
  let A,B be category;
  func Functors(A,B) -> strict non empty transitive AltCatStr means
  the
carrier of it = Funct(A,B) & (for F,G being strict covariant Functor of A,B, x
  being set holds x in (the Arrows of it).(F,G) iff F
is_naturally_transformable_to G & x is natural_transformation of F,G) & for F,G
,H being strict covariant Functor of A,B st F is_naturally_transformable_to G &
  G is_naturally_transformable_to H for t1 being natural_transformation of F,G,
t2 being natural_transformation of G,H ex f being Function st f = (the Comp of
  it).(F,G,H) & f.(t2,t1) = t2`*`t1;
  existence
  proof
    defpred Q[set,set,set] means for f,g,h being strict covariant Functor of A
    ,B st [f,g,h] = $3 for t1 being natural_transformation of f,g for t2 being
natural_transformation of g,h st [t2,t1] = $2 & ex v being Function st v.$2 =
    $1 holds $1 = t2 `*` t1;
    defpred P[set,set] means for f,g being strict covariant Functor of A,B st
[f,g] = $1 for x being set holds x in $2 iff f is_naturally_transformable_to g
    & x is natural_transformation of f,g;
A1: for i being set st i in [:Funct(A,B),Funct(A,B):] ex j being set st P[ i,j]
    proof
      let i be set;
      assume i in [:Funct(A,B),Funct(A,B):];
      then consider f be set,g be set such that
A2:   f in Funct(A,B) & g in Funct(A,B) and
A3:   i = [f,g] by ZFMISC_1:def 2;
      reconsider f, g as strict covariant Functor of A,B by A2,Def10;
      defpred P[object of A,set] means $2 = <^f.$1,g.$1^>;
      defpred P[set] means f is_naturally_transformable_to g & $1 is
      natural_transformation of f,g;
A4:   for a being Element of A ex j being set st P[a,j];
      consider N being ManySortedSet of the carrier of A such that
A5:   for a being Element of A holds P[a,N.a] from PBOOLE:sch 6(A4);
      consider j being set such that
A6:   for x being set holds x in j iff x in product N & P[x] from
      XBOOLE_0:sch 1;
      take j;
      let f9,g9 be strict covariant Functor of A,B such that
A7:   [f9,g9] = i;
      let x be set;
      thus x in j implies f9 is_naturally_transformable_to g9 & x is
      natural_transformation of f9,g9
      proof
        assume
A8:     x in j;
        f9 = f & g9 = g by A3,A7,ZFMISC_1:33;
        hence thesis by A6,A8;
      end;
      thus f9 is_naturally_transformable_to g9 & x is natural_transformation
      of f9,g9 implies x in j
      proof
A9:     f9 = f & g9 = g by A3,A7,ZFMISC_1:33;
        set I = the carrier of A;
        assume that
A10:    f9 is_naturally_transformable_to g9 and
A11:    x is natural_transformation of f9,g9;
        reconsider h = x as ManySortedSet of the carrier of A by A11;
A12:    f9 is_transformable_to g9 by A10,Def6;
A13:    for i9 be set st i9 in I holds h.i9 in N.i9
        proof
          let i9 be set;
          assume i9 in I;
          then reconsider i9 as Element of A;
A14:      P[i9,N.i9] by A5;
          <^f.i9,g.i9^> <> {} & h.i9 is Element of <^f.i9,g.i9^> by
          A11,A12,A9, Def1,Def2;
          hence thesis by A14;
        end;
A15:    for i9 be set st i9 in dom N holds h.i9 in N.i9
        proof
A16:      dom N = I by PARTFUN1:def 4;
          let i9 be set;
          assume i9 in dom N;
          hence thesis by A13,A16;
        end;
        dom h = the carrier of A by PARTFUN1:def 4;
        then dom h = dom N by PARTFUN1:def 4;
        then x in product N by A15,CARD_3:18;
        hence thesis by A6,A10,A11,A9;
      end;
    end;
    consider a be ManySortedSet of [:Funct(A,B),Funct(A,B):] such that
A17: for i being set st i in [:Funct(A,B),Funct(A,B):] holds P[i,a.i]
    from PBOOLE:sch 3(A1);
A18: for o be set st o in [:Funct(A,B),Funct(A,B),Funct(A,B):] holds for x
    be set st x in {|a,a|}.o ex y be set st y in {|a|}.o & Q[y,x,o]
    proof
      let o be set;
      assume o in [:Funct(A,B),Funct(A,B),Funct(A,B):];
      then o in [:[:Funct(A,B),Funct(A,B):],Funct(A,B):] by
      ZFMISC_1:def 3;
      then consider ff,h be set such that
A19:  ff in [:Funct(A,B),Funct(A,B):] and
A20:  h in Funct(A,B) and
A21:  o = [ff,h] by ZFMISC_1:def 2;
      consider f,g be set such that
A22:  f in Funct(A,B) & g in Funct(A,B) and
A23:  ff = [f,g] by A19,ZFMISC_1:def 2;
A24:  o = [f,g,h] by A21,A23,MCART_1:def 3;
      reconsider T = Funct(A,B) as non empty set by A20;
      reconsider i = f, j = g, k = h as Element of T by A20,A22;
A25:  {|a|}.[i,j,k] = {|a|}.(i,j,k) by MULTOP_1:def 1
        .= a.(i,k) by ALTCAT_1:def 5;
A26:  {|a,a|}.[i,j,k] = {|a,a|}.(i,j,k) by MULTOP_1:def 1
        .= [:a.(j,k),a.(i,j):] by ALTCAT_1:def 6;
      for x be set st x in {|a,a|}.o ex y be set st y in {|a|}.o & Q[y,x, o]
      proof
        reconsider i9 = i,j9 = j, k9 = k as strict covariant Functor of A,B by
        Def10;
        let x be set;
A27:    [i9,k9] in [:Funct(A,B),Funct(A,B):] by ZFMISC_1:def 2;
        assume x in {|a,a|}.o;
        then consider x2,x1 be set such that
A28:    x2 in a.(j,k) and
A29:    x1 in a.(i,j) and
A30:    x = [x2,x1] by A24,A26,ZFMISC_1:def 2;
A31:    x2 in a.[j,k] by A28;
        then
A32:    j9 is_naturally_transformable_to k9 by A17;
        reconsider x2 as natural_transformation of j9,k9 by A17,A31;
A33:    x1 in a.[i,j] by A29;
        then reconsider x1 as natural_transformation of i9,j9 by A17;
        reconsider vv = x2 `*` x1 as natural_transformation of i9,k9;
A34:    for f,g,h being strict covariant Functor of A,B st [f,g,h] = o
for t1 being natural_transformation of f,g for t2 being natural_transformation
of g,h st [t2,t1] = x & ex v being Function st v.x = vv holds vv = t2 `*` t1
        proof
          let f,g,h be strict covariant Functor of A,B such that
A35:      [f,g,h] = o;
A36:      j9 = g by A24,A35,MCART_1:28;
          then reconsider x2 as natural_transformation of g,h by A24,A35,
          MCART_1:28;
A37:      i9 = f & k9 = h by A24,A35,MCART_1:28;
          let t1 be natural_transformation of f,g, t2 be
          natural_transformation of g,h such that
A38:      [t2,t1] = x and
          ex v being Function st v.x = vv;
          x2 = t2 by A30,A38,ZFMISC_1:33;
          hence thesis by A30,A38,A36,A37,ZFMISC_1:33;
        end;
        i9 is_naturally_transformable_to j9 by A17,A33;
        then i9 is_naturally_transformable_to k9 by A32,Th10;
        then vv in a.[i9,k9] by A17,A27;
        hence thesis by A24,A25,A34;
      end;
      hence thesis;
    end;
    consider c being ManySortedFunction of {|a,a|},{|a|} such that
A39: for i being set st i in [:Funct(A,B),Funct(A,B),Funct(A,B):]
holds ex v being Function of {|a,a|}.i, {|a|}.i st v = c.i & for x be set st x
    in {|a,a|}.i holds Q[v.x,x,i] from MSSUBFAM:sch 1(A18);
    set F = AltCatStr (#Funct(A,B),a,c #);
A40: Funct(A,B) is non empty
    proof
      consider f be strict covariant Functor of A,B;
      f in Funct(A,B) by Def10;
      hence thesis;
    end;
    F is transitive
    proof
      let o1,o2,o3 be object of F;
      reconsider a1 = o1, a2 = o2, a3 = o3 as strict covariant Functor of A,B
      by A40,Def10;
      assume that
A41:  <^o1,o2^> <> {} and
A42:  <^o2,o3^> <> {};
      consider x being set such that
A43:  x in a.[o2,o3] by A42,XBOOLE_0:def 1;
A44:  [o2,o3] in [:Funct(A,B),Funct(A,B):] by A40,ZFMISC_1:def 2;
      then
A45:  a2 is_naturally_transformable_to a3 by A17,A43;
      reconsider x as natural_transformation of a2,a3 by A17,A44,A43;
      consider y being set such that
A46:  y in a.[o1,o2] by A41,XBOOLE_0:def 1;
A47:  [o1,o2] in [:Funct(A,B),Funct(A,B):] by A40,ZFMISC_1:def 2;
      then reconsider y as natural_transformation of a1,a2 by A17,A46;
      a1 is_naturally_transformable_to a2 by A17,A47,A46;
      then
A48:  a1 is_naturally_transformable_to a3 by A45,Th10;
      x `*` y is natural_transformation of a1,a3 & [o1,o3] in [:Funct(A,B
      ),Funct(A,B):] by A40,ZFMISC_1:def 2;
      hence thesis by A17,A48;
    end;
    then reconsider F as strict non empty transitive AltCatStr by A40;
    take F;
    thus the carrier of F = Funct(A,B);
    thus for f,g being strict covariant Functor of A,B, x being set holds x in
    (the Arrows of F).(f,g) iff f is_naturally_transformable_to g & x is
    natural_transformation of f,g
    proof
      let f,g be strict covariant Functor of A,B;
      let x be set;
      thus x in (the Arrows of F).(f,g) implies f
      is_naturally_transformable_to g & x is natural_transformation of f,g
      proof
        f in Funct(A,B) & g in Funct(A,B) by Def10;
        then
A49:    [f,g] in [:Funct(A,B),Funct(A,B):] by ZFMISC_1:def 2;
        assume x in (the Arrows of F).(f,g);
        hence thesis by A17,A49;
      end;
      thus f is_naturally_transformable_to g & x is natural_transformation of
      f,g implies x in (the Arrows of F).(f,g)
      proof
        f in Funct(A,B) & g in Funct(A,B) by Def10;
        then
A50:    [f,g] in [:Funct(A,B),Funct(A,B):] by ZFMISC_1:106;
        assume
        f is_naturally_transformable_to g & x is natural_transformation of f,g;
        hence thesis by A17,A50;
      end;
    end;
    let f,g,h be strict covariant Functor of A,B such that
A51: f is_naturally_transformable_to g and
A52: g is_naturally_transformable_to h;
    let t1 be natural_transformation of f,g, t2 be natural_transformation of g
    ,h;
A53: f in Funct(A,B) by Def10;
    then reconsider T = Funct(A,B) as non empty set;
A54: g in Funct(A,B) by Def10;
    then
A55: [f,g] in [:Funct(A,B),Funct(A,B):] by A53,ZFMISC_1:106;
A56: h in Funct(A,B) by Def10;
    then [g,h] in [:Funct(A,B),Funct(A,B):] by A54,ZFMISC_1:106;
    then
A57: t2 in a.[g,h] by A17,A52;
    reconsider i = f, j = g, k = h as Element of T by Def10;
A58: {|a,a|}.[i,j,k] = {|a,a|}.(i,j,k) by MULTOP_1:def 1
      .= [:a.(j,k),a.(i,j):] by ALTCAT_1:def 6;
    [:Funct(A,B),Funct(A,B),Funct(A,B):] = [:[:Funct(A,B),Funct(A,B):],
    Funct(A,B) :] & [f,g,h] = [[f,g],h] by MCART_1:def 3,ZFMISC_1:def 3;
    then [f,g,h] in [:Funct(A,B),Funct(A,B),Funct(A,B):] by A56,A55,
    ZFMISC_1:106;
    then consider v be Function of {|a,a|}.[f,g,h], {|a|}.[f,g,h] such that
A59: v = c.[f,g,h] and
A60: for x be set st x in {|a,a|}.[f,g,h] holds Q[v.x,x,[f,g,h]] by
    A39;
    t1 in a.[f,g] by A17,A51,A55;
    then [t2,t1] in {|a,a|}.[f,g,h] by A58,A57,ZFMISC_1:106;
    then
A61: v.(t2,t1) = t2`*`t1 by A60;
    v = c.(f,g,h) by A59,MULTOP_1:def 1;
    hence thesis by A61;
  end;
  uniqueness
  proof
    let C1,C2 be strict non empty transitive AltCatStr such that
A62: the carrier of C1 = Funct(A,B) and
A63: for F,G being strict covariant Functor of A,B, x being set holds
    x in (the Arrows of C1).(F,G) iff F is_naturally_transformable_to G & x is
    natural_transformation of F,G and
A64: for F,G,H being strict covariant Functor of A,B st F
    is_naturally_transformable_to G & G is_naturally_transformable_to H for t1
being natural_transformation of F,G, t2 being natural_transformation of G,H ex
    f being Function st f = (the Comp of C1).(F,G,H) & f.(t2,t1) = t2`*` t1 and
A65: the carrier of C2 = Funct(A,B) and
A66: for F,G being strict covariant Functor of A,B, x being set holds
    x in (the Arrows of C2).(F,G) iff F is_naturally_transformable_to G & x is
    natural_transformation of F,G and
A67: for F,G,H being strict covariant Functor of A,B st F
    is_naturally_transformable_to G & G is_naturally_transformable_to H for t1
being natural_transformation of F,G, t2 being natural_transformation of G,H ex
    f being Function st f = (the Comp of C2).(F,G,H) & f.(t2,t1) = t2`*`t1;
    set R = the carrier of C1, T = the carrier of C2, N = the Arrows of C1, M
    = the Arrows of C2, O = the Comp of C1, P = the Comp of C2;
A68: for i,j being set st i in R & j in T holds N.(i,j) = M.(i,j)
    proof
      let i,j be set such that
A69:  i in R and
A70:  j in T;
      reconsider g = j as strict covariant Functor of A,B by A65,A70,Def10;
      reconsider f = i as strict covariant Functor of A,B by A62,A69,Def10;
      now
        let x be set;
        x in N.(i,j) iff f is_naturally_transformable_to g & x is
        natural_transformation of f,g by A63;
        hence x in N.(i,j) iff x in M.(i,j) by A66;
      end;
      hence thesis by TARSKI:2;
    end;
    then
A71: the Arrows of C1 = the Arrows of C2 by A62,A65,ALTCAT_1:8;
    for i,j,k being set st i in R & j in R & k in R holds O.(i,j,k) = P.
    (i, j, k )
    proof
      let i,j,k be set;
      assume that
A72:  i in R and
A73:  j in R and
A74:  k in R;
      reconsider h = k as strict covariant Functor of A,B by A62,A74,Def10;
      reconsider g = j as strict covariant Functor of A,B by A62,A73,Def10;
      reconsider f = i as strict covariant Functor of A,B by A62,A72,Def10;
      per cases;
      suppose
A75:    N.(i,j) = {} or N.(j,k) = {};
        thus O.(i,j,k) = P.(i,j,k)
        proof
          per cases by A75;
          suppose
A76:        N.(i,j) = {};
            reconsider T as non empty set;
            reconsider i9 = i, j9 = j, k9 = k as Element of T by A62,A65,A72
            ,A73,A74;
            M.(i,j) = {} by A62,A65,A68,A76,ALTCAT_1:8;
            then
A77:        [:M.(j9,k9),M.(i9,j9):] = {} by ZFMISC_1:113;
A78:        {|M,M|}.[i9,j9,k9] = {|M,M|}.(i9,j9,k9) by MULTOP_1:def 1
              .= [:M.(j9,k9),M.(i9,j9):] by ALTCAT_1:def 6;
A79:        {|M|}.[i9,j9,k9] = {|M|}.(i9,j9,k9) by MULTOP_1:def 1
              .= M.(i9,k9) by ALTCAT_1:def 5;
            P.[i9,j9,k9] = P.(i9,j9,k9) by MULTOP_1:def 1;
            then
A80:        P.(i9,j9,k9) is Function of [:M.(j9,k9),M.(i9,j9):], M.(i9,
            k9) by A78,A79,PBOOLE:def 18;
A81:        {|M,M|}.[i9,j9,k9] = {|M,M|}.(i9,j9,k9) by MULTOP_1:def 1
              .= [:M.(j9,k9),M.(i9,j9):] by ALTCAT_1:def 6;
A82:        {|M|}.[i9,j9,k9] = {|M|}.(i9,j9,k9) by MULTOP_1:def 1
              .= M.(i9,k9) by ALTCAT_1:def 5;
            O.[i9,j9,k9] = O.(i9,j9,k9) by MULTOP_1:def 1;
            then O.(i9,j9,k9) is Function of [:M.(j9,k9),M.(i9,j9):], M.(i9,
            k9) by A62,A65,A71,A81,A82,PBOOLE:def 18;
            hence thesis by A77,A80;
          end;
          suppose
A83:        N.(j,k) = {};
            reconsider T as non empty set;
            reconsider i9 = i, j9 = j, k9 = k as Element of T by A62,A65,A72
            ,A73,A74;
            M.(j,k) = {} by A62,A65,A68,A83,ALTCAT_1:8;
            then
A84:        [:M.(j9,k9),M.(i9,j9):] = {} by ZFMISC_1:113;
A85:        {|M,M|}.[i9,j9,k9] = {|M,M|}.(i9,j9,k9) by MULTOP_1:def 1
              .= [:M.(j9,k9),M.(i9,j9):] by ALTCAT_1:def 6;
A86:        {|M|}.[i9,j9,k9] = {|M|}.(i9,j9,k9) by MULTOP_1:def 1
              .= M.(i9,k9) by ALTCAT_1:def 5;
            P.[i9,j9,k9] = P.(i9,j9,k9) by MULTOP_1:def 1;
            then
A87:        P.(i9,j9,k9) is Function of [:M.(j9,k9),M.(i9,j9):], M.(i9,
            k9) by A85,A86,PBOOLE:def 18;
A88:        {|M,M|}.[i9,j9,k9] = {|M,M|}.(i9,j9,k9) by MULTOP_1:def 1
              .= [:M.(j9,k9),M.(i9,j9):] by ALTCAT_1:def 6;
A89:        {|M|}.[i9,j9,k9] = {|M|}.(i9,j9,k9) by MULTOP_1:def 1
              .= M.(i9,k9) by ALTCAT_1:def 5;
            O.[i9,j9,k9] = O.(i9,j9,k9) by MULTOP_1:def 1;
            then O.(i9,j9,k9) is Function of [:M.(j9,k9),M.(i9,j9):], M.(i9,
            k9) by A62,A65,A71,A88,A89,PBOOLE:def 18;
            hence thesis by A84,A87;
          end;
        end;
      end;
      suppose that
A90:    N.(i,j) <> {} and
A91:    N.(j,k) <> {};
        reconsider T as non empty set;
        reconsider i9 = i, j9 = j, k9 = k as Element of T by A62,A65,A72,A73
        ,A74;
A92:    {|M,M|}.[i9,j9,k9] = {|M,M|}.(i9,j9,k9) by MULTOP_1:def 1
          .= [:M.(j9,k9),M.(i9,j9):] by ALTCAT_1:def 6;
A93:    {|M|}.[i9,j9,k9] = {|M|}.(i9,j9,k9) by MULTOP_1:def 1
          .= M.(i9,k9) by ALTCAT_1:def 5;
        P.[i9,j9,k9] = P.(i9,j9,k9) by MULTOP_1:def 1;
        then reconsider
        t2 = P.(i,j,k) as Function of [:M.(j,k),M.(i,j):], M.(i,k)
        by A92,A93,PBOOLE:def 18;
A94:    {|M,M|}.[i9,j9,k9] = {|M,M|}.(i9,j9,k9) by MULTOP_1:def 1
          .= [:M.(j9,k9),M.(i9,j9):] by ALTCAT_1:def 6;
A95:    {|M|}.[i9,j9,k9] = {|M|}.(i9,j9,k9) by MULTOP_1:def 1
          .= M.(i9,k9) by ALTCAT_1:def 5;
        O.[i9,j9,k9] = O.(i9,j9,k9) by MULTOP_1:def 1;
        then reconsider
        t1 = O.(i,j,k) as Function of [:M.(j,k),M.(i,j):], M.(i,k)
        by A62,A65,A71,A94,A95,PBOOLE:def 18;
A96:    M.(j,k) <> {} by A62,A65,A68,A91,ALTCAT_1:8;
A97:    M.(i,j) <> {} by A62,A65,A68,A90,ALTCAT_1:8;
        for a being Element of M.(j,k) for b being Element of M.(i,j)
        holds t1.(a,b) = t2.(a,b)
        proof
          let a be Element of M.(j,k), b be Element of M.(i,j);
          a in M.(j,k) by A96;
          then
A98:      g is_naturally_transformable_to h by A66;
          reconsider a as natural_transformation of g,h by A66,A96;
          b in M.(i,j) by A97;
          then
A99:      f is_naturally_transformable_to g by A66;
          reconsider b as natural_transformation of f,g by A66,A97;
          (ex t19 be Function st t19 = O.(f,g,h) & t19.(a,b) = a `*` b )
& ex t29 be Function st t29 = P.(f,g,h) & t29.(a,b) = a `*` b by A64,A67,A98,
          A99;
          hence thesis;
        end;
        hence thesis by BINOP_1:2;
      end;
    end;
    hence thesis by A62,A65,A71,ALTCAT_1:10;
  end;
end;
