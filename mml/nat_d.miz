:: Divisibility of Natural Numbers
::  by Grzegorz Bancerek
::
:: Received January 3, 2007
:: Copyright (c) 2007 Association of Mizar Users

environ

 vocabularies ARYTM_1, ARYTM_3, NAT_1, INT_1, ARYTM, INT_2, ABSVALUE;
 notations SUBSET_1, NUMBERS, XCMPLX_0, XXREAL_0, NAT_1, INT_1, INT_2;
 constructors XXREAL_0, NAT_1, INT_2, REAL_1;
 registrations XXREAL_0, XREAL_0, NAT_1, INT_1, ORDINAL1, XBOOLE_0;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 theorems REAL_1, ORDINAL1, XCMPLX_1, XREAL_1, XXREAL_0, NAT_1, INT_1, INT_2,
      ABSVALUE;
 schemes NAT_1;

begin

reserve p,k,l,m,n,s,h,i,j,t for Nat;

:: Exact division and rest of division

Lm1: now
  let k,l;
  k div l in NAT by INT_1:16,82;
  hence k div l is Nat by ORDINAL1:def 13;
end;

Lm2: now
  let k,l;
  k mod l in NAT by INT_1:16,84;
  hence k mod l is Nat by ORDINAL1:def 13;
end;

definition
  let k,l be Nat;
  redefine func k div l -> Nat means       :: the exact division
  :Def1:
  ( ex t being Nat st k = l * it + t & t < l ) or it = 0 & l = 0;
  compatibility
  proof
    let r be Nat;
    per cases;
    suppose l = 0;
      hence r = k div l iff
      ( ex t being Nat st k = l * r + t & t < l ) or r = 0 & l = 0
      by INT_1:75;
    end;
    suppose
A1:   l > 0;
      then
A2:   k = l * (k div l) + (k mod l) by INT_1:86;
A3:   k mod l is Nat by Lm2;
      hence r = k div l implies
      (ex t being Nat st k = l * r + t & t < l) or r = 0 & l = 0
      by A1,A2,INT_1:85;
      assume
A4:   ( ex t being Nat st k = l * r + t & t < l ) or r = 0 & l = 0;
A5:   k mod l < l by A1,INT_1:85;
      k div l is Nat by Lm1;
      hence r = k div l by A2,A3,A4,A5,NAT_1:18;
    end;
  end;
  coherence by Lm1;
end;

definition
  let k,l be Nat;
  redefine func k mod l -> Nat means         :: the rest of division
  :Def2:
  ( ex t being Nat st k = l * t + it & it < l ) or it = 0 & l = 0;
  compatibility
  proof
    let r be Nat;
    per cases;
    suppose l = 0;
      hence r = k mod l iff
      ( ex t being Nat st k = l * t + r & r < l ) or r = 0 & l = 0
      by INT_1:def 8;
    end;
    suppose
A1:   l > 0;
      then
A2:   k = l * (k div l) + (k mod l) by INT_1:86;
      hence r = k mod l implies
      ( ex t being Nat st k = l * t + r & r < l ) or r = 0 & l = 0
      by A1,INT_1:85;
      assume
A3:   ( ex t being Nat st k = l * t + r & r < l ) or r = 0 & l = 0;
A4:   k mod l < l by A1,INT_1:85;
      k mod l is Nat by Lm2;
      hence r = k mod l by A2,A3,A4,NAT_1:18;
    end;
  end;
  coherence by Lm2;
end;

definition
  let k,l be Nat;
  redefine func k div l -> Element of NAT;
  coherence
  proof
    k div l is Nat;
    hence thesis by ORDINAL1:def 13;
  end;
  redefine func k mod l -> Element of NAT;
  coherence
  proof
    k mod l is Nat;
    hence thesis by ORDINAL1:def 13;
  end;
end;

theorem Th1:
  0 < i implies j mod i < i
proof
  assume 0 < i;
  then ex t being Nat st j = i * t + (j mod i) & j mod i < i by Def2;
  hence j mod i < i;
end;

theorem
  0 < i implies j = i * (j div i) + (j mod i) by INT_1:86;

:: The divisibility relation

definition
  let k,l be Nat;
  redefine pred k divides l means
  :Def3:
  ex t being Nat st l = k * t;
  compatibility
  proof
    hereby
      assume
A1:   k divides l;
      thus ex t being Nat st l = k * t
      proof
        consider t being Integer such that
A2:     l = k * t by A1,INT_1:def 9;
        per cases;
        suppose 0 < l;
          then 0 <= t by A2,XREAL_1:133;
          then reconsider t as Element of NAT by INT_1:16;
          take t;
          thus l = k * t by A2;
        end;
        suppose
A3:       l = 0;
          take 0;
          thus l = k*0 by A3;
        end;
      end;
    end;
    assume ex t being Nat st l = k * t;
    hence thesis by INT_1:def 9;
  end;
  reflexivity
  proof
    let i;
    i = i * 1;
    hence thesis;
  end;
end;

theorem Th3:
  j divides i iff i = j * (i div j)
proof
  thus j divides i implies i = j * (i div j)
  proof
    assume j divides i;
    then consider k being Nat such that
A1: i = j * k by Def3;
A2: i = j * k + 0 by A1;
    now
      assume
A3:   j <> 0;
A4:   0 <= j;
A5:   i = j * (i div j) + (i mod j) by A3,INT_1:86;
      i mod j < j by A3,A4,Th1;
      hence i = j * (i div j) by A2,A5,NAT_1:18;
    end;
    hence i = j * (i div j) by A1;
  end;
  assume i = j * (i div j);
  hence j divides i by Def3;
end;

theorem
  i divides j & j divides h implies i divides h
proof
  assume i divides j & j divides h;
  then j = i * (j div i) & h = j * (h div j) by Th3;
  then h = i * ((j div i)*(h div j));
  hence i divides h by Def3;
end;

theorem Th5:
  i divides j & j divides i implies i = j
proof
  assume i divides j & j divides i;
  then
A1: j = i * (j div i) & i = j * (i div j) by Th3;
  then
A2: i * 1 = i * ((j div i) * (i div j));
A3: i = 0 implies i = j by A1;
  (j div i) * (i div j) = 1 implies i = j
  proof
    assume (j div i) * (i div j) = 1;
    then j div i = 1 by NAT_1:15;
    hence i = j by A1;
  end;
  hence i = j by A2,A3,XCMPLX_1:5;
end;

theorem Th6:
  i divides 0 & 1 divides i
proof
  0 = i * 0;
  hence i divides 0 by Def3;
  i = i * 1;
  hence thesis by Def3;
end;

theorem
  0 < j & i divides j implies i <= j
proof
  assume
A1: 0 < j & i divides j;
  then
A2: j = i * (j div i) by Th3;
  then j div i <> 0 by A1;
  then consider m such that
A3: j div i = m + 1 by NAT_1:6;
  i * (m + 1) = i + i * m;
  hence i <= j by A2,A3,NAT_1:11;
end;

theorem Th8:
  i divides j & i divides h implies i divides j+h
proof
  assume i divides j & i divides h;
  then j = i * (j div i) & h = i * (h div i) by Th3;
  then j + h = i * ((j div i) + (h div i));
  hence i divides j + h by Def3;
end;

theorem Th9:
  i divides j implies i divides j * h
proof
  assume i divides j;
  then consider l being Nat such that
A1: j = i * l by Def3;
  i * l * h = i * (l * h);
  hence thesis by A1,Def3;
end;

theorem Th10:
  i divides j & i divides j + h implies i divides h
proof
  assume
A1: i divides j & i divides j + h;
  then consider t being Nat such that
A2: j + h = i * t by Def3;
  consider u being Nat such that
A3: j = i * u by A1,Def3;
  now
    assume
A4: i <> 0;
    then
A5: i <> 0 & 0 <= i;
A6: h = i * (h div i) + (h mod i) by A4,INT_1:86;
A7: j + (i * (h div i) + (h mod i)) = i * (u + (h div i)) + (h mod i) by A3;
A8: h mod i < i by A5,Th1;
    j + h = i * t + 0 by A2;
    then h mod i = 0 by A6,A7,A8,NAT_1:18;
    hence thesis by A6,Th3;
  end;
  hence thesis by A2,NAT_1:7;
end;

theorem Th11:
  i divides j & i divides h implies i divides j mod h
proof
  assume
A1: i divides j & i divides h;
A2: now
    assume h = 0;
    then j mod h = 0 by Def2;
    hence thesis by Th6;
  end;
  now
    assume h <> 0;
    then
A3: j = h * (j div h) + (j mod h) by INT_1:86;
    i divides h * (j div h) by A1,Th9;
    hence thesis by A1,A3,Th10;
  end;
  hence thesis by A2;
end;

:: The least common multiple and the greatest common divisor

definition
  let k,n be Nat;
  redefine func k lcm n means
  :Def4:
  k divides it & n divides it &
  for m being Nat st k divides m & n divides m holds it divides m;
  compatibility
  proof
    let IT be Nat;
    thus IT = k lcm n implies k divides IT & n divides IT &
    for m being Nat st k divides m & n divides m holds IT divides m
    by INT_2:def 2;
    assume that
A1: k divides IT & n divides IT and
A2: for m being Nat st k divides m & n divides m holds IT divides m;
    for m being Integer st k divides m & n divides m holds IT divides m
    proof
      let m be Integer;
      m in INT by INT_1:def 2;
      then consider i being Element of NAT such that
A3:   m = i or m = -i by INT_1:def 1;
      assume k divides m & n divides m;
      then k divides i & n divides i by A3,INT_2:14;
      then IT divides i by A2;
      hence IT divides m by A3,INT_2:14;
    end;
    hence IT = k lcm n by A1,INT_2:def 2;
  end;
end;

definition
  let k,n be Nat;
  redefine func k lcm n -> Element of NAT;
  coherence by ORDINAL1:def 13;
end;

notation
  let k,n be Nat;
  synonym k hcf n for k gcd n;
end;

definition
  let k,n be Nat;
  redefine func k hcf n means
  :Def5:
  it divides k & it divides n &
  for m being Nat st m divides k & m divides n holds m divides it;
  compatibility
  proof
    let IT be Nat;
    thus IT = k gcd n implies IT divides k & IT divides n &
    for m being Nat st m divides k & m divides n holds m divides IT
    by INT_2:def 3;
    assume that
A1: IT divides k & IT divides n and
A2: for m being Nat st m divides k & m divides n holds m divides IT;
    for m being Integer st m divides k & m divides n holds m divides IT
    proof
      let m be Integer;
      m in INT by INT_1:def 2;
      then consider i being Element of NAT such that
A3:   m = i or m = -i by INT_1:def 1;
      assume m divides k & m divides n;
      then i divides k & i divides n by A3,INT_2:14;
      then i divides IT by A2;
      hence m divides IT by A3,INT_2:14;
    end;
    hence IT = k gcd n by A1,INT_2:def 3;
  end;
end;

definition
  let k,n be Nat;
  redefine func k hcf n -> Element of NAT;
  coherence by ORDINAL1:def 13;
end;

scheme Euklides { Q(Nat)->Nat, a,b()->Nat } :
  ex n being Element of NAT st Q(n) = a() hcf b() & Q(n + 1) = 0
provided
A1: 0 < b() & b() < a()
and
A2: Q(0) = a() & Q(1) = b()
and
A3: for n being Element of NAT holds Q(n + 2) = Q(n) mod Q(n + 1)
proof
  defpred P[Nat] means ex n being Element of NAT st $1 = Q(n);
A4: ex k being Nat st P[k] by A2;
A5: for k being Nat st k <> 0 & P[k] ex n being Nat st n < k & P[n]
  proof
    let k be Nat;
    assume
A6: k <> 0 & ex n being Element of NAT st k = Q(n);
    then consider n being Element of NAT such that
A7: k = Q(n);
    reconsider Q = Q(n+1) as Element of NAT by ORDINAL1:def 13;
A8: (n = 0 implies Q < k) & ex m st m = n+1 & Q = Q(m) by A1,A2,A7;
    now
      given m such that
A9:   n = m + 1;
A10:  0 <= Q(n);
      reconsider m1=m as Element of NAT by ORDINAL1:def 13;
      Q(m1 + 2) = Q(m1) mod Q(m1 + 1) by A3;
      hence Q(n + 1) < k by A6,A7,A9,A10,Th1;
      take m = n + 1;
      thus Q(n + 1) =Q(m);
    end;
    hence thesis by A8,NAT_1:6;
  end;
A11: P[0] from NAT_1:sch 7(A4,A5);
  defpred Q[Nat] means 0 = Q($1);
A12: ex n being Nat st Q[n] by A11;
  consider k being Nat such that
A13: Q[k] & for n being Nat st Q[n] holds k <= n from NAT_1:sch 5(A12);
  consider n such that
A14: k = n + 1 by A1,A2,A13,NAT_1:6;
  reconsider n as Element of NAT by ORDINAL1:def 13;
  take n;
  defpred PP[Nat] means Q(n) divides Q($1) & Q(n) divides Q($1 + 1);
  PP[n] by A13,A14,Th6;
  then
A15: ex k being Nat st PP[k];
A16: for k being Nat st k <> 0 & PP[k] ex m being Nat st m < k & PP[m]
  proof
    let l be Nat;
    assume
A17: l <> 0 & Q(n) divides Q(l) & Q(n) divides Q(l + 1);
    then consider m such that
A18: l = m + 1 by NAT_1:6;
    reconsider m as Element of NAT by ORDINAL1:def 13;
    take m;
A19: m <= m + 1 by NAT_1:11;
    m <> m + 1;
    hence m < l by A18,A19,REAL_1:def 5;
A20: now
      assume
A21:  Q(m + 1) = 0;
      now
        assume
A22:    m + 1 <> k;
        k <= m + 1 by A13,A21;
        then k < m + 1 by A22,REAL_1:def 5;
        then
A23:    k <= m by NAT_1:13;
        defpred Q[Nat] means k <= $1 implies Q($1) = 0;
A24:    Q[0] by A13,XXREAL_0:1;
A25:    for m st Q[m] holds Q[m+1]
        proof
          let m such that
A26:      k <= m implies Q(m) = 0 and
A27:      k <= m + 1;
          now
            assume k <> m + 1;
            then
A28:        k < m + 1 by A27,REAL_1:def 5;
            then consider l such that
A29:        m = l + 1 by A1,A2,A26,NAT_1:6,13;
            reconsider l as Element of NAT by ORDINAL1:def 13;
            Q(l + 2) = Q(l) mod Q(l + 1) by A3;
            hence Q(m + 1) = 0 by A26,A28,A29,Def2,NAT_1:13;
          end;
          hence Q(m + 1) = 0 by A13;
        end;
        for m holds Q[m] from NAT_1:sch 2(A24,A25);
        then Q(m) = 0 by A23;
        hence Q(n) divides Q(m) by Th6;
      end;
      hence Q(n) divides Q(m) by A14;
    end;
    now
      assume
A30:  Q(m + 1) <> 0;
      Q(m + 2) = Q(m) mod Q(m + 1) by A3;
      then
A31:  Q(m) = Q(m + 1) * (Q(m) div Q(m + 1)) + Q(m + 2) by A30,INT_1:86;
      Q(n) divides Q(m + 1) * (Q(m) div Q(m + 1)) &
      Q(n) divides Q(m + 2) by A17,A18,Th9;
      hence Q(n) divides Q(m) by A31,Th8;
    end;
    hence Q(n) divides Q(m) by A20;
    thus Q(n) divides Q(m + 1) by A17,A18;
  end;
  now
    PP[0] from NAT_1:sch 7(A15,A16);
    hence Q(n) divides a() & Q(n) divides b() by A2;
    let m;
    defpred P1[Nat] means m divides Q($1) & m divides Q($1 + 1);
    assume m divides a() & m divides b();
    then
A32: P1[0] by A2;
A33: for k be Element of NAT st P1[k] holds P1[k+1]
    proof
      let k be Element of NAT;
      assume
A34:  m divides Q(k) & m divides Q(k + 1);
      hence m divides Q(k + 1);
      Q(k + 2) = Q(k) mod Q(k + 1) by A3;
      hence m divides Q(k + 1 + 1) by A34,Th11;
    end;
    for k be Element of NAT holds P1[k] from NAT_1:sch 1(A32,A33);
    hence m divides Q(n);
  end;
  hence Q(n) = a() hcf b() by Def5;
  thus Q(n + 1) = 0 by A13,A14;
end;

theorem
  n mod 2 = 0 or n mod 2 = 1
proof
  assume
A1: n mod 2 <> 0;
  2 = 1 + 1 & n mod 2 < 2 by Th1;
  then n mod 2 <= 1 & n mod 2 >= 1 by A1,NAT_1:13,14;
  hence n mod 2 = 1 by XXREAL_0:1;
end;

theorem
  (k * n) mod k = 0
proof
  per cases;
  suppose k = 0;
    hence thesis by Def2;
  end;
  suppose k <> 0;
    then k * n = k * n + 0 & 0 < k;
    hence thesis by Def2;
  end;
end;

theorem
  k > 1 implies 1 mod k = 1
proof
  assume
A1: k > 1;
  1 = k * 0 + 1;
  hence thesis by A1,Def2;
end;

theorem
  k mod n = 0 & l = k - m * n implies l mod n = 0
proof
  assume that
A1: k mod n = 0 and
A2: l = k - m * n;
  per cases;
  suppose n = 0;
    hence thesis by A1,A2;
  end;
  suppose n <> 0;
    then consider t being Nat such that
A3: k = n * t + 0 and
A4: 0 < n by A1,Def2;
A5: l = n * (t - m) + 0 by A2,A3;
    now
      assume t < m + 0;
      then t - m < 0 by XREAL_1:21;
      then l < n * 0 by A4,A5,XREAL_1:70;
      hence contradiction;
    end;
    then t - m is Element of NAT by NAT_1:21;
    hence thesis by A4,A5,Def2;
  end;
end;

theorem
  n <> 0 & k mod n = 0 & l < n implies k + l mod n = l
proof
  assume that
A1: n <> 0 and
A2: k mod n = 0 and
A3: l < n;
  ex t being Nat st k = n * t + 0 & 0 < n by A1,A2,Def2;
  hence thesis by A3,Def2;
end;

theorem
  k mod n = 0 implies k + l mod n = l mod n
proof
  assume that
A1: k mod n = 0;
  per cases;
  suppose
A2: n = 0;
    hence k + l mod n = 0 by Def2
      .= l mod n by A2,Def2;
  end;
  suppose
A3: n <> 0;
    then consider m being Nat such that
A4: k = n * m + 0 & 0 < n by A1,Def2;
    consider t being Nat such that
A5: l = n * t + (l mod n) & l mod n < n by A3,Def2;
    k + l = n * (m + t) + (l mod n) by A4,A5;
    hence thesis by A5,Def2;
  end;
end;

theorem
  k <> 0 implies (k * n) div k = n
proof
  assume k <> 0;
  then k * n = k * n + 0 & 0 < k;
  hence thesis by Def1;
end;

theorem
  k mod n = 0 implies (k + l) div n = (k div n) + (l div n)
proof
  assume
A1: k mod n = 0;
  per cases;
  suppose
A2: n <> 0;
    then
A3: k = n * (k div n) + 0 by A1,INT_1:86;
A4: ex t being Nat st l = n * t + (l mod n) & l mod n < n by A2,Def2;
    l = n * (l div n) + (l mod n) by A2,INT_1:86;
    then k + l = n * ((k div n) + (l div n)) + (l mod n) by A3;
    hence thesis by A4,Def1;
  end;
  suppose n = 0;
    then (k + l) div n = 0 & k div n = 0 & l div n = 0 by Def1;
    hence thesis;
  end;
end;

begin :: Addenda

:: from AMI_5, 2005.11.16, A.T.

theorem
  k <> 0 implies m * k div k = m
proof
  assume k <> 0;
  then m*k = k * m + 0 & 0 < k or m = 0 & k = 0;
  hence m*k div k = m by Def1;
end;

:: from GR_CY_1, 2005.11.16, A.T.

theorem Th21:
  m mod n = (n*k + m) mod n
proof
  per cases;
  suppose
A1: n > 0;
    m mod n = t implies (n*k + m) mod n = t
    proof
      assume m mod n = t;
      then consider q being Nat such that
A2:   m = n * q + t & t < n by A1,Def2;
      ex p st n*k + m = n*p + t & t < n
      proof
        reconsider p = k+q as Element of NAT by ORDINAL1:def 13;
        take p;
        thus thesis by A2;
      end;
      hence thesis by Def2;
    end;
    hence thesis;
  end;
  suppose n = 0;
    hence thesis;
  end;
end;

theorem Th22:
  (p+s) mod n = ((p mod n)+s) mod n
proof
  per cases;
  suppose n > 0;
    then (p + s) mod n = (n*(p div n) + (p mod n) + s) mod n by INT_1:86
      .=(n*(p div n) + ((p mod n) + s)) mod n
      .=((p mod n) + s) mod n by Th21;
    hence thesis;
  end;
  suppose
A1: n = 0;
    hence (p+s) mod n = 0 by Def2
      .= ((p mod n)+s) mod n by A1,Def2;
  end;
end;

theorem
  (p+s) mod n = (p + ( s mod n)) mod n by Th22;

theorem Th24:
  k < n implies k mod n = k
proof
  assume
A1: k<n;
  ex p st k=n*p + k & k<n
  proof
    k=n*0 + k;
    hence thesis by A1;
  end;
  hence thesis by Def2;
end;

theorem
  n mod n = 0
proof
  per cases;
  suppose
A1: n>0;
    ex p st n=n*p +0 & 0<n
    proof
      n=n*1+0;
      hence thesis by A1;
    end;
    hence thesis by Def2;
  end;
  suppose n = 0;
    hence thesis by Def2;
  end;
end;

theorem
  0 = 0 mod n
proof
  n = 0 or n > 0;
  hence thesis by Def2,Th24;
end;

:: from JORDAN4

theorem
  i < j implies i div j = 0
proof
  assume
A1: i<j;
  per cases by Def1;
  suppose ex j1 being Nat st i=j*(i div j)+j1 & j1<j;
    then consider j1 being Nat such that
A2: i=j*(i div j)+j1 and j1<j;
    assume i div j<>0;
    then consider k such that
A3: i div j = k+1 by NAT_1:6;
    i = j + (j*k+j1) by A2,A3;
    hence contradiction by A1,NAT_1:11;
  end;
  suppose i div j=0 & j=0;
    hence i div j=0;
  end;
end;

:: Moved from SCMP_GCD by AK on 28.12.2006

theorem Th28:
  m > 0 implies n hcf m = m hcf (n mod m)
proof
  set r=n mod m, x=n hcf m, y=m hcf r;
  assume m>0;
  then consider t be Nat such that
A1: n = m * t + r & r < m by Def2;
  reconsider t as Element of NAT by ORDINAL1:def 13;
A2: x divides n & x divides m by Def5;
  then x divides r by Th11;
  then
A3: x divides y by A2,Def5;
A4: y divides m & y divides r by Def5;
  then y divides m*t by Th9;
  then y divides n by A1,A4,Th8;
  then y divides x by A4,Def5;
  hence thesis by A3,Th5;
end;

:: from AMI_3, 2007.06.14, A.T.

scheme INDI{ k,n() -> Element of NAT, P[set] }: P[n()]
provided
A1: P[0]
and
A2: k() > 0
and
A3: for i,j st P[k()*i] & j <> 0 & j <= k() holds P[k()*i+j]
proof
  defpred R[Nat] means P[k()*$1];
A4: R[0] by A1;
A5: now
    let i;
    assume
A6: R[i];
    k()*(i+1) = k()*i + k();
    hence R[i+1] by A2,A3,A6;
  end;
A7: for i holds R[i] from NAT_1:sch 2(A4,A5);
  per cases;
  suppose n() mod k() = 0;
    then n() = k() * (n() div k()) + 0 by A2,INT_1:86;
    hence thesis by A7;
  end;
  suppose
A8: n() mod k() <> 0;
A9: n() = k() * (n() div k()) + (n() mod k()) by A2,INT_1:86;
    n() mod k() <= k() by A2,Th1;
    hence thesis by A3,A7,A8,A9;
  end;
end;

:: moved from INT_2, 2007.11.7, A.T,

theorem
  i*j = (i lcm j)*(i hcf j)
proof
A1: i<>0 & j<>0 implies i*j = (i lcm j)*(i hcf j)
  proof
    assume
A2: i<>0 & j<>0;
A3: i divides i implies i divides i*j by Th9;
    j divides j implies j divides j*i by Th9;
    then (i lcm j) divides i*j by A3,Def4;
    then consider c being Nat such that
A4: i*j = (i lcm j)*c by Def3;
A5: i divides (i lcm j) & j divides (i lcm j) by Def4;
    then consider d being Nat such that
A6: (i lcm j) = i*d by Def3;
    consider e being Nat such that
A7: (i lcm j) = j*e by A5,Def3;
    i*j = i*(c*d) by A4,A6;
    then
A8: j = c*d by A2,XCMPLX_1:5;
    i*j = j*(c*e) by A4,A7;
    then i = c*e by A2,XCMPLX_1:5;
    then
A9: c divides i & c divides j by A8,Def3;
    for f being Nat holds f divides i & f divides j implies f divides c
    proof
      let f be Nat;
      assume
A10:  f divides i & f divides j;
      then consider g being Nat such that
A11:  i = f*g by Def3;
      consider h being Nat such that
A12:  j = f*h by A10,Def3;
A13:  j*g = g*h*f by A12;
      i*h = g*h*f by A11;
      then i divides g*h*f & j divides g*h*f by A13,Def3;
      then (i lcm j) divides g*h*f by Def4;
      then consider z being Nat such that
A14:  g*h*f = (i lcm j)*z by Def3;
A15:  c*(i lcm j) = (g*(h*f))*f by A4,A11,A12
        .= (i lcm j)*z*f by A14
        .= (z*f)*(i lcm j);
      (i lcm j) <> 0 by A2,INT_2:4;
      then c = f*z by A15,XCMPLX_1:5;
      hence thesis by Def3;
    end;
    hence thesis by A4,A9,Def5;
  end;
  i = 0 or j = 0 implies i*j = (i lcm j)*(i hcf j)
  proof
    assume
A16: i = 0 or j = 0;
    then i*j = 0*(i hcf j)
      .= (i lcm j)*(i hcf j) by A16,INT_2:4;
    hence thesis;
  end;
  hence thesis by A1;
end;

:: from from SCMP_GCD, 2007.07.26, A.T.

theorem
  for i,j being Integer st i>=0 & j>0 holds i gcd j= j gcd (i mod j)
proof
  let i,j be Integer;
  assume
A1: i>=0 & j>0;
  then
A2: abs(j)> 0 by ABSVALUE:def 1;
  thus i gcd j = abs(i) hcf abs(j) by INT_2:51
    .=abs(j) hcf (abs(i) mod abs(j)) by A2,Th28
    .=abs(j) hcf abs(abs(i) mod abs(j)) by ABSVALUE:def 1
    .=j gcd (abs(i) mod abs(j)) by INT_2:51
    .=j gcd (i mod j) by A1,INT_2:49;
end;

:: ???, 2007.07.26, A.T.

theorem
  i lcm i = i
proof
  for m being Nat st i divides m & i divides m holds i divides m;
  hence thesis by Def4;
end;

theorem
  i gcd i = i
proof
  for m being Nat st m divides i & m divides i holds m divides i;
  hence thesis by Def5;
end;

