:: Divisibility of Natural Numbers
::  by Grzegorz Bancerek
::
:: Received January 3, 2007
:: Copyright (c) 2007 Association of Mizar Users

environ

 vocabularies ARYTM_1, ARYTM_3, NAT_1, INT_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, REAL_1,
      XXREAL_0, NAT_1, INT_1;
 constructors NUMBERS, XCMPLX_0, XXREAL_0, XREAL_0, ORDINAL2, REAL_1, NAT_1,
      INT_1;
 registrations REAL_1, NUMBERS, ORDINAL2, XREAL_0, ARYTM_3, ZFMISC_1, XBOOLE_0,
      XXREAL_0, ORDINAL1, NAT_1, INT_1;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions REAL_1, INT_1;
 theorems REAL_1, ORDINAL1, XCMPLX_1, XREAL_1, XXREAL_0, NAT_1, INT_1;
 schemes NAT_1;

begin

reserve p,k,l,m,n,s,h,i,j,t for Nat;

:: Exact division and rest of division

Lm3: now let k,l;
  0 <= k div l by INT_1:82;
  then k div l in NAT by INT_1:16;
  hence k div l is Nat by ORDINAL1:def 13;
end;

Lm4: now let k,l;
    0 <= k mod l by INT_1:84;
    then k mod l in NAT by INT_1:16;
    hence k mod l is Nat by ORDINAL1:def 13;
  end;

definition let k,l be Nat;
  redefine func k div l -> Nat means       :: the exact division
:Def1:  ( ex t being Nat st k = l * it + t & t < l ) or it = 0 & l = 0;
compatibility
proof
  let r be Nat;
  per cases;
  suppose l = 0;
    hence r = k div l iff
    ( ex t being Nat st k = l * r + t & t < l ) or r = 0 & l = 0
    by INT_1:75;
  end;
  suppose
A1: l > 0;
A2: k = l * (k div l) + (k mod l) by A1,INT_1:86;
A3: k mod l is Nat by Lm4;
A7: k mod l < l by A1,INT_1:85; 
    hence r = k div l implies
    ( ex t being Nat st k = l * r + t & t < l ) or r = 0 & l = 0 by A2,A3;
    assume
A4: ( ex t being Nat st k = l * r + t & t < l ) or r = 0 & l = 0;
    k div l is Nat by Lm3;
    hence r = k div l by A2,A3,A7,A4,NAT_1:18;
  end;
end;
coherence by Lm3;
end;

definition let k,l be Nat;
  redefine func k mod l -> Nat means         :: the rest of division
:Def2: ( ex t being Nat st k = l * t + it & it < l ) or it = 0 & l = 0;
compatibility
proof
  let r be Nat;
  per cases;
  suppose l = 0;
    hence r = k mod l iff
    ( ex t being Nat st k = l * t + r & r < l ) or r = 0 & l = 0
    by INT_1:def 8;
  end;
  suppose
A1: l > 0;
A2: k = l * (k div l) + (k mod l) by A1,INT_1:86;
A7: k mod l < l by A1,INT_1:85; 
    hence r = k mod l implies
    ( ex t being Nat st k = l * t + r & r < l ) or r = 0 & l = 0 by A2;
    assume
A4: ( ex t being Nat st k = l * t + r & r < l ) or r = 0 & l = 0;
    k mod l is Nat by Lm4;
    hence r = k mod l by A2,A7,A4,NAT_1:18;
  end;
end;
coherence by Lm4;
end;

definition let k,l be Nat;
  redefine func k div l -> Element of NAT;
  coherence
  proof
    k div l is Nat;
    hence thesis by ORDINAL1:def 13;
  end;
  redefine func k mod l -> Element of NAT;
  coherence
  proof
    k mod l is Nat;
    hence thesis by ORDINAL1:def 13;
  end;
end;

theorem Th46:
  0 < i implies j mod i < i
   proof assume 0 < i;
     then ex t being Nat st j = i * t + (j mod i) & j mod i < i by Def2;
    hence j mod i < i;
   end;

theorem Th47:
  0 < i implies j = i * (j div i) + (j mod i) by INT_1:86;

:: The divisibility relation

definition let k,l be Nat;
  redefine pred k divides l means
:Def3:  ex t being Nat st l = k * t;
  compatibility
  proof
    hereby
      assume
A0:   k divides l;
      thus ex t being Nat st l = k * t
      proof
      consider t being Integer such that
A1:   l = k * t by A0,INT_1:def 9;
      per cases;
      suppose 0 < l;
      then 0 <= t by A1,XREAL_1:133;
      then reconsider t as Element of NAT by INT_1:16;
      take t;
      thus l = k * t by A1;
      end;
      suppose
A2:     l = 0;
        take 0;
        thus l = k*0 by A2;
      end;
      end;
    end;
    assume ex t being Nat st l = k * t;
    hence thesis by INT_1:def 9;
  end;
  reflexivity
   proof let i;
    i = i * 1;
    hence thesis;
   end;
end;

theorem Th49:
  j divides i iff i = j * (i div j)
   proof
     thus j divides i implies i = j * (i div j)
     proof assume j divides i;
       then consider k being Nat such that
A1:    i = j * k by Def3;
A2:    i = j * k + 0 by A1;
       now assume
A3:      j <> 0;
A4:      0 <= j;
then A5: i = j * (i div j) + (i mod j) by A3,Th47;
         i mod j < j by A3,A4,Th46;
         hence i = j * (i div j) by A2,A5,NAT_1:18;
       end;
       hence i = j * (i div j) by A1;
     end;
     assume i = j * (i div j);
     hence j divides i by Def3;
   end;

theorem
  i divides j & j divides h implies i divides h
  proof assume i divides j & j divides h;
    then j = i * (j div i) & h = j * (h div j) by Th49;
    then h = i * ((j div i)*(h div j));
    hence i divides h by Def3;
   end;

theorem Th52:
  i divides j & j divides i implies i = j
   proof
    assume i divides j & j divides i;
then A1:   j = i * (j div i) & i = j * (i div j) by Th49;
then A2:     i * 1 = i * ((j div i) * (i div j));
A3:   i = 0 implies i = j by A1;
        (j div i) * (i div j) = 1 implies i = j
      proof assume (j div i) * (i div j) = 1;
        then j div i = 1 by NAT_1:15;
       hence i = j by A1;
      end;
    hence i = j by A2,A3,XCMPLX_1:5;
   end;

theorem Th53:
  i divides 0 & 1 divides i
  proof
   0 = i * 0;
   hence i divides 0 by Def3;
   i = i * 1;
   hence thesis by Def3;
  end;

theorem Th54:
  0 < j & i divides j implies i <= j
   proof assume
A1:   0 < j & i divides j;
then A2:   j = i * (j div i) by Th49;
     then j div i <> 0 by A1;
    then consider m such that
A3:   j div i = m + 1 by NAT_1:6;
        i * (m + 1) = i + i * m;
    hence i <= j by A2,A3,NAT_1:11;
   end;

theorem Th55:
  i divides j & i divides h implies i divides j+h
   proof
    assume i divides j & i divides h;
   then j = i * (j div i) & h = i * (h div i) by Th49;
     then j + h = i * ((j div i) + (h div i));
    hence i divides j + h by Def3;
   end;

theorem Th56:
  i divides j implies i divides j * h
  proof assume i divides j;
   then consider l being Nat such that
A1:  j = i * l by Def3;
     i * l * h = i * (l * h);
     hence thesis by A1,Def3;
  end;

theorem Th57:
  i divides j & i divides j + h implies i divides h
  proof assume
A1:  i divides j & i divides j + h;
   then consider t being Nat such that
A2:  j + h = i * t by Def3;
   consider u being Nat such that
A3:  j = i * u by A1,Def3;
       now assume i <> 0;
      then A4: i <> 0 & 0 <= i;
then A5:    h = i * (h div i) + (h mod i) by Th47;
A6:    j + (i * (h div i) + (h mod i)) = i * (u + (h div i)) + (h mod i) by A3;
A7:    h mod i < i by A4,Th46;
         j + h = i * t + 0 by A2;
      then h mod i = 0 by A5,A6,A7,NAT_1:18;
      hence thesis by A5,Th49;
    end;
   hence thesis by A2,NAT_1:7;
  end;

theorem Th58:
  i divides j & i divides h implies i divides j mod h
  proof assume
A1:  i divides j & i divides h;
A2:  now assume h = 0; then j mod h = 0 by Def2;
     hence thesis by Th53;
    end;
       now assume
A3:    h <> 0;
         0 <= h;
then A4:    j = h * (j div h) + (j mod h) by A3,Th47;
         i divides h * (j div h) by A1,Th56;
     hence thesis by A1,A4,Th57;
    end;
   hence thesis by A2;
  end;

:: The least common multiple and the greatest common divisor

definition let k,n be Nat;
  func k lcm n -> Nat means
:Def4:  k divides it & n divides it &
  for m being Nat st k divides m & n divides m holds it divides m;
   existence
    proof
A1:    now assume
A2:      k = 0 or n = 0;
       take w = 0;
       thus k divides w & n divides w by Th53;
       let m be Nat; assume k divides m & n divides m;
       hence w divides m by A2;
      end;
         now assume
A3:      k <> 0 & n <> 0;
      reconsider k1=k, n1=n as Element of NAT by ORDINAL1:def 13;
      A4:      k1 divides k1 * n1 & n1 divides k1 * n1 by Def3;
      defpred P[Nat] means k divides $1 & n divides $1 & k <= $1;
           0 <= n; then 0 + 1 <= n by A3,NAT_1:13;
        then k * 1 <= k * n by NAT_1:4;
then A5:  ex m being Element of NAT st P[m] by A4;
       consider l being Element of NAT such that
A6:      P[l] & for m being Element of NAT st P[m] holds l <= m
          from NAT_1:sch 5(A5);
       take l;
       thus k divides l & n divides l by A6;
       let m be Nat such that
A7:      k divides m & n divides m;
           now A8: 0 <= k;
then A9:        m = l * (m div l) + (m mod l) by A3,A6,Th47;
             l = k * (l div k) & l = n * (l div n) by A6,Th49;
          then l * (m div l) = k * ((l div k) * (m div l)) &
           l * (m div l) = n * ((l div n) * (m div l)) by XCMPLX_1:4;
          then k divides l * (m div l) & n divides l * (m div l) by Def3;
then A10:        k divides m mod l & n divides m mod l by A7,A9,Th57;
             now assume
A11:          m mod l <> 0;
               0 <= m mod l;
            then k <= m mod l by A10,A11,Th54;
           then l <= m mod l by A6,A10;
           hence contradiction by A3,A6,A8,Th46;
          end; hence l divides m by A9,Th49;
        end;
       hence l divides m;
      end;
     hence thesis by A1;
    end;
   uniqueness
    proof let m1,m2 be Nat such that
A12:  k divides m1 & n divides m1 & for m being Nat st k divides m & n divides
 m holds m1 divides m and
A13: k divides m2 & n divides m2 & for m being Nat st k divides m & n divides
 m holds m2 divides m;
  m1 divides m2 & m2 divides m1 by A12,A13;
     hence m1 = m2 by Th52;
    end;
    idempotence;
    commutativity;
end;

definition let k,n be Nat;
  redefine func k lcm n -> Element of NAT;
  coherence by ORDINAL1:def 13;
end;

definition let k,n be Nat;
  func k hcf n -> Nat means
:Def5:  it divides k & it divides n &
    for m being Nat st m divides k & m divides n holds m divides it;
   existence
    proof
       defpred P[Nat] means $1 divides k & $1 divides n;
         1 divides k & 1 divides n by Th53;
then A1:    ex m being Element of NAT st P[m];
A2:    now assume
A3:     k = 0;
       take m = n;
       thus m divides k & m divides n by A3,Th53;
       let l be Nat; assume l divides k & l divides n;
       hence l divides m;
      end;
      now assume
A4:     k <> 0;
        0 <= k;
        then A5: for m being Element of NAT st P[m] holds m <= k by A4,Th54;
       consider m being Element of NAT such that
A6:    P[m] & for l being Element of NAT st P[l] holds l <= m
       from NAT_1:sch 6(A5,A1);
       take m;
       thus m divides k & m divides n by A6;
       let l be Nat;
       assume l divides k & l divides n;
then A7:      l lcm m divides k & l lcm m divides n by A6,Def4;
then A8:      l lcm m <= m by A6;
           m <= l lcm m
         proof
A9:        m divides l lcm m by Def4;
A10:        now assume l lcm m = 0;
             then k = 0 * (k div 0) by A7,Th49;
            hence contradiction by A4;
           end;
              0 <= l lcm m;
          hence thesis by A9,A10,Th54;
         end;
       then l lcm m = m by A8,XXREAL_0:1;
       hence l divides m by Def4;
      end;
     hence thesis by A2;
    end;
   uniqueness
    proof let m1,m2 be Nat such that
A11:  m1 divides k & m1 divides n & for m being Nat st m divides k & m divides
 n holds m divides m1 and
A12: m2 divides k & m2 divides n & for m being Nat st m divides k & m divides
 n holds m divides m2;
  m1 divides m2 & m2 divides m1 by A11,A12;
     hence m1 = m2 by Th52;
    end;
    idempotence;
    commutativity;
end;

definition let k,n be Nat;
  redefine func k hcf n -> Element of NAT;
  coherence by ORDINAL1:def 13;
end;

scheme Euklides { Q(Nat)->Nat, a,b()->Nat } :
  ex n being Element of NAT st Q(n) = a() hcf b() & Q(n + 1) = 0
   provided
A1:   0 < b() & b() < a() and
A2:   Q(0) = a() & Q(1) = b() and
A3:   for n being Element of NAT holds Q(n + 2) = Q(n) mod Q(n + 1)
  proof
     defpred P[Nat] means ex n being Element of NAT st $1 = Q(n);
     a() in NAT by ORDINAL1:def 13;
     then
A4:  ex k being Element of NAT st P[k] by A2;
A5:  for k being Element of NAT st k <> 0 & P[k]
     ex n being Element of NAT st n < k & P[n]
     proof let k be Element of NAT; assume
A6:   k <> 0 & ex n being Element of NAT st k = Q(n);
      then consider n being Element of NAT such that
A7:   k = Q(n);
      reconsider Q = Q(n+1) as Element of NAT by ORDINAL1:def 13;
A8:   (n = 0 implies Q < k) & ex m st m = n+1 & Q = Q(m)
        by A1,A2,A7;
      now given m such that
A9:     n = m + 1;
A10:    0 <= Q(n);
        reconsider m1=m as Element of NAT by ORDINAL1:def 13;
        Q(m1 + 2) = Q(m1) mod Q(m1 + 1) by A3;
        hence Q(n + 1) < k by A6,A7,A9,A10,Th46;
        take m = n + 1;
        thus Q(n + 1) =Q(m);
      end;
      hence thesis by A8,NAT_1:6;
     end;
A11:  P[0] from NAT_1:sch 7(A4,A5);
     defpred Q[Nat] means 0 = Q($1);
A12: ex n being Element of NAT st Q[n] by A11;
   consider k being Element of NAT such that
A13:  Q[k] & for n being Element of NAT st Q[n] holds k <= n
      from NAT_1:sch 5(A12);
   consider n such that
A14:  k = n + 1 by A1,A2,A13,NAT_1:6;
      reconsider n as Element of NAT by ORDINAL1:def 13;
   take n;
   defpred PP[Nat] means
    Q(n) divides Q($1) & Q(n) divides Q($1 + 1);
       PP[n] by A13,A14,Th53;
then A15:  ex k being Element of NAT st PP[k];
A16:  for k being Element of NAT st k <> 0 & PP[k]
      ex m being Element of NAT st m < k & PP[m]
     proof let l be Element of NAT ; assume
A17:    l <> 0 & Q(n) divides Q(l) & Q(n) divides Q(l + 1);
      then consider m such that
A18:    l = m + 1 by NAT_1:6;
        reconsider m as Element of NAT by ORDINAL1:def 13;
      take m;
A19:    m <= m + 1 by NAT_1:11;
        m <> m + 1;
      hence m < l by A18,A19,REAL_1:def 5;
A20:  now assume
A21:    Q(m + 1) = 0;
        now assume
A22:      m + 1 <> k;
          k <= m + 1 by A13,A21;
          then k < m + 1 by A22,REAL_1:def 5;then
A23:      k <= m by NAT_1:13;
          defpred Q[Nat] means k <= $1 implies Q($1) = 0;
A24: Q[0] by A13,XXREAL_0:1;
A26: for m st Q[m] holds Q[m+1]
     proof let m such that
A27:   k <= m implies Q(m) = 0 and
A28:   k <= m + 1;
       now assume k <> m + 1; then A29: k < m + 1 by A28,REAL_1:def 5;
         then consider l such that
A30:     m = l + 1 by A1,A2,A27,NAT_1:6,13;
         reconsider l as Element of NAT by ORDINAL1:def 13;
         Q(l + 2) = Q(l) mod Q(l + 1) by A3;
         hence Q(m + 1) = 0 by A27,A29,A30,Def2,NAT_1:13;
       end;
       hence Q(m + 1) = 0 by A13;
     end;
     for m holds Q[m] from NAT_1:sch 2(A24,A26);
     then Q(m) = 0 by A23;
     hence Q(n) divides Q(m) by Th53;
   end;
   hence Q(n) divides Q(m) by A14;
 end;
 now assume
A31:   Q(m + 1) <> 0;
A32:   0 <= Q(m + 1);
       Q(m + 2) = Q(m) mod Q(m + 1) by A3;then
A33:   Q(m) = Q(m + 1) * (Q(m) div Q(m + 1)) + Q(m + 2) by A31,A32,Th47;
       Q(n) divides Q(m + 1) * (Q(m) div Q(m + 1)) &
       Q(n) divides Q(m + 2) by A17,A18,Th56;
       hence Q(n) divides Q(m) by A33,Th55;
     end;
     hence Q(n) divides Q(m) by A20;
     thus Q(n) divides Q(m + 1) by A17,A18;
   end;
   now
     PP[0] from NAT_1:sch 7(A15,A16);
     hence Q(n) divides a() & Q(n) divides b() by A2;
     let m;
     defpred P1[Nat] means m divides Q($1) & m divides Q($1 + 1);
     assume m divides a() & m divides b();
then A34: P1[0] by A2;
A35: for k be Element of NAT st P1[k] holds P1[k+1]
       proof let k be Element of NAT; assume
A36:   m divides Q(k) & m divides Q(k + 1);
       hence m divides Q(k + 1);
       Q(k + 2) = Q(k) mod Q(k + 1) by A3;
       hence m divides Q(k + 1 + 1) by A36,Th58;
     end;
     for k be Element of NAT holds P1[k] from NAT_1:sch 1(A34,A35);
     hence m divides Q(n);
    end;
   hence Q(n) = a() hcf b() by Def5;
   thus Q(n + 1) = 0 by A13,A14;
  end;

theorem
  n mod 2 = 0 or n mod 2 = 1
 proof
  assume A1: n mod 2 <> 0;
  2 = 1 + 1 & n mod 2 < 2 by Th46;
  then n mod 2 <= 1 & n mod 2 >= 1 by A1,NAT_1:13,14;
  hence n mod 2 = 1 by XXREAL_0:1;
 end;

theorem
  (k * n) mod k = 0
 proof
  per cases;
    suppose k = 0;
     hence thesis by Def2;
    end;
    suppose k <> 0;
    then k * n = k * n + 0 & 0 < k;
    hence thesis by Def2;
   end;
 end;

theorem
  k > 1 implies 1 mod k = 1
 proof
  assume A1: k > 1;
  1 = k * 0 + 1;
  hence thesis by A1,Def2;
 end;

theorem
  k mod n = 0 & l = k - m * n implies l mod n = 0
 proof
  assume that A1: k mod n = 0 and A2: l = k - m * n;
  per cases;
    suppose n = 0;
     hence thesis by A1,A2;
    end;
    suppose n <> 0;
      then consider t being Nat such that
A3:    k = n * t + 0 and A4: 0 < n by A1,Def2;
       A5: l = n * (t - m) + 0 by A2,A3;
       now assume t < m + 0; then
         t - m < 0 by XREAL_1:21;
         then l < n * 0 by A4,A5,XREAL_1:70;
        hence contradiction;
       end;
       then t - m is Element of NAT by NAT_1:21;
    hence thesis by A4,A5,Def2;
   end;
 end;

theorem
  n <> 0 & k mod n = 0 & l < n implies k + l mod n = l
 proof
   assume that A1: n <> 0 and A2: k mod n = 0 and A3: l < n;
   ex t being Nat st k = n * t + 0 & 0 < n by A1,A2,Def2;
  hence thesis by A3,Def2;
 end;

theorem
  k mod n = 0 implies k + l mod n = l mod n
 proof
   assume that A1: k mod n = 0;
   per cases;
    suppose
A2:  n = 0;
     hence k + l mod n = 0 by Def2
          .= l mod n by A2,Def2;
    end;
    suppose A3: n <> 0;
     then consider m being Nat such that
A4:  k = n * m + 0 & 0 < n by A1,Def2;
     consider t being Nat such that
A5:  l = n * t + (l mod n) & l mod n < n by A3,Def2;
     k + l = n * (m + t) + (l mod n) by A4,A5;
     hence thesis by A5,Def2;
   end;
 end;

theorem
  k <> 0 implies (k * n) div k = n
  proof
    assume k <> 0;
    then k * n = k * n + 0 & 0 < k;
    hence thesis by Def1;
 end;

theorem
  k mod n = 0 implies (k + l) div n = (k div n) + (l div n)
 proof assume A1: k mod n = 0;
   per cases;
   suppose A2: n <> 0;
    A3: 0 <= n;
   then A4: k = n * (k div n) + 0 by A1,A2,Th47;
A5: ex t being Nat st l = n * t + (l mod n) & l mod n < n
      by A2,Def2;
   l = n * (l div n) + (l mod n) by A2,A3,Th47;
   then k + l = n * ((k div n) + (l div n)) + (l mod n) by A4;
  hence thesis by A5,Def1;
  end;
  suppose n = 0;
  then (k + l) div n = 0 & k div n = 0 & l div n = 0 by Def1;
  hence thesis;
  end;
 end;

begin :: Addenda

:: from AMI_5, 2005.11.16, A.T.

theorem
  k <> 0 implies m * k div k = m
  proof
   assume k <> 0;
   then m*k = k * m + 0 & 0 < k or m = 0 & k = 0;
   hence m*k div k = m by Def1;
  end;

:: from GR_CY_1, 2005.11.16, A.T.

theorem Th73:
  m mod n = (n*k + m) mod n
 proof
  per cases;
  suppose A1: n > 0;
       m mod n = t implies (n*k + m) mod n = t
   proof
    assume m mod n = t;
    then consider q being Nat such that A2: m = n * q + t & t < n by A1,Def2;
        ex p st n*k + m = n*p + t & t < n
      proof
        reconsider p = k+q as Element of NAT by ORDINAL1:def 13;
        take p;
       thus thesis by A2;
      end;
    hence thesis by Def2;
   end;
 hence thesis;
 end;
 suppose n = 0;
 hence thesis;
end;
end;

theorem Th74:
  (p+s) mod n = ((p mod n)+s) mod n
 proof
  per cases;
  suppose n > 0;
  then (p + s) mod n = (n*(p div n) + (p mod n) + s) mod n by Th47
                .=(n*(p div n) + ((p mod n) + s)) mod n
                .=((p mod n) + s) mod n by Th73;
   hence thesis;
   end;
   suppose
A1:  n = 0;
   hence (p+s) mod n = 0 by Def2
     .= ((p mod n)+s) mod n by A1,Def2;
 end;
 end;

theorem
  (p+s) mod n = (p + ( s mod n)) mod n by Th74;

theorem Th76:
  k < n implies k mod n = k
 proof
   assume A1:k<n;
    ex p st k=n*p + k & k<n
   proof
      k=n*0 + k;
     hence thesis by A1;
   end;
  hence thesis by Def2;
 end;

theorem
  n mod n = 0
 proof
  per cases;
  suppose A1:n>0;
      ex p st n=n*p +0 & 0<n
   proof
        n=n*1+0;
    hence thesis by A1;
   end;
  hence thesis by Def2;
  end;
  suppose n = 0;
  hence thesis by Def2;
 end;
 end;

theorem
  0 = 0 mod n
  proof
    n = 0 or n > 0;
    hence thesis by Th76,Def2;
  end;

:: from JORDAN4

theorem
  i < j implies i div j = 0
proof assume A1: i<j;
 per cases by Def1;
  suppose ex j1 being Nat st i=j*(i div j)+j1 & j1<j;
   then consider j1 being Nat such that
A2: i=j*(i div j)+j1 and
    j1<j;
   assume i div j<>0;
     then consider k such that
A3:   i div j = k+1 by NAT_1:6;
    i = j + (j*k+j1) by A2,A3;
   hence contradiction by A1,NAT_1:11;
  end;
  suppose i div j=0 & j=0;
   hence i div j=0;
  end;
end;

:: Moved from SCMP_GCD by AK on 28.12.2006

theorem
  m > 0 implies n hcf m = m hcf (n mod m)
proof
   set r=n mod m,
       x=n hcf m,
       y=m hcf r;
   assume m>0;
   then consider t be Nat such that
A1: n = m * t + r & r < m by Def2;
    reconsider t as Element of NAT by ORDINAL1:def 13;
A2: x divides n & x divides m by Def5;
    then x divides r by Th58;
then A3: x divides y by A2,Def5;
A4: y divides m & y divides r by Def5;
then y divides m*t by Th56;
    then y divides n by A1,A4,Th55;
    then y divides x by A4,Def5;
   hence thesis by A3,Th52;
end;
