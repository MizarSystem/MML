:: On the Simple Closed Curve Property of the Circle and the
:: Fashoda Meet Theorem for It
::  by Yatsuka Nakamura
::
:: Received August 20, 2001
:: Copyright (c) 2001-2012 Association of Mizar Users
::           (Stowarzyszenie Uzytkownikow Mizara, Bialystok, Poland).
:: This code can be distributed under the GNU General Public Licence
:: version 3.0 or later, or the Creative Commons Attribution-ShareAlike
:: License version 3.0 or later, subject to the binding interpretation
:: detailed in file COPYING.interpretation.
:: See COPYING.GPL and COPYING.CC-BY-SA for the full text of these
:: licenses, or see http://www.gnu.org/licenses/gpl.html and
:: http://creativecommons.org/licenses/by-sa/3.0/.

environ

 vocabularies NUMBERS, XREAL_0, ORDINAL1, SQUARE_1, ARYTM_3, XXREAL_0, CARD_1,
      RELAT_1, STRUCT_0, EUCLID, PRE_TOPC, COMPLEX1, MCART_1, FUNCT_1,
      XBOOLE_0, TARSKI, SUBSET_1, ORDINAL2, RCOMP_1, SUPINF_2, ARYTM_1,
      TOPMETR, REAL_1, XXREAL_1, JORDAN2C, FUNCT_4, PARTFUN1, PCOMPS_1,
      METRIC_1, TOPS_2, TOPREAL2, TOPREAL1, VALUED_1, BORSUK_1, JGRAPH_3,
      FUNCT_2;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0,
      REAL_1, RLVECT_1, EUCLID, RELAT_1, TOPS_2, FUNCT_1, RELSET_1, PARTFUN1,
      FUNCT_2, DOMAIN_1, STRUCT_0, TOPREAL1, TOPMETR, COMPTS_1, METRIC_1,
      RCOMP_1, SQUARE_1, PCOMPS_1, PSCOMP_1, PRE_TOPC, FUNCT_4, TOPREAL2,
      XXREAL_0;
 constructors FUNCT_4, REAL_1, SQUARE_1, RCOMP_1, TOPS_2, COMPTS_1, TOPMETR,
      TOPREAL1, TOPREAL2, PSCOMP_1, SEQ_1, FUNCSDOM, PCOMPS_1;
 registrations XBOOLE_0, FUNCT_1, RELSET_1, FUNCT_2, NUMBERS, XXREAL_0,
      XREAL_0, SQUARE_1, MEMBERED, STRUCT_0, PRE_TOPC, BORSUK_1, EUCLID,
      TOPMETR, TOPREAL1, TOPREAL2, BORSUK_3;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, FUNCT_1, XBOOLE_0, SQUARE_1, STRUCT_0;
 theorems TARSKI, RELAT_1, SUBSET_1, FUNCT_1, FUNCT_2, FUNCT_4, TOPS_1, TOPS_2,
      PARTFUN1, PRE_TOPC, REVROT_1, FRECHET, TOPMETR, JORDAN6, EUCLID,
      JGRAPH_1, SQUARE_1, TOPREAL3, PSCOMP_1, METRIC_1, SPPOL_2, JGRAPH_2,
      COMPTS_1, ZFMISC_1, GOBOARD6, TOPREAL1, TOPREAL2, COMPLEX1, XBOOLE_0,
      XBOOLE_1, XREAL_0, TOPRNS_1, XCMPLX_1, XREAL_1, XXREAL_0, XXREAL_1,
      RELSET_1;
 schemes FUNCT_2, DOMAIN_1, JGRAPH_2, CLASSES1;

begin :: Preliminaries

reserve x for real number;

Lm1: x^2+1 > 0
proof
  x^2 >= 0 by XREAL_1:63;
  hence thesis;
end;

Lm2: dom proj1=the carrier of TOP-REAL 2 by FUNCT_2:def 1;

Lm3: dom proj2=the carrier of TOP-REAL 2 by FUNCT_2:def 1;

theorem
  for p being Point of TOP-REAL 2 holds |.p.| = sqrt((p`1)^2+(p`2)^2) &
  |.p.|^2 = (p`1)^2+(p`2)^2 by JGRAPH_1:29,30;

theorem
  for f being Function,B,C being set holds (f|B).:C = f.:(C /\ B)
proof
  let f be Function,B,C be set;
  thus (f|B).:C c=f.:(C /\ B)
  proof
    let x be set;
    assume x in (f|B).:C;
    then consider y being set such that
A1: y in dom (f|B) and
A2: y in C and
A3: x=(f|B).y by FUNCT_1:def 6;
A4: (f|B).y=f.y by A1,FUNCT_1:47;
A5: dom (f|B)=(dom f)/\ B by RELAT_1:61;
    then y in B by A1,XBOOLE_0:def 4;
    then
A6: y in C /\ B by A2,XBOOLE_0:def 4;
    y in dom f by A1,A5,XBOOLE_0:def 4;
    hence thesis by A3,A6,A4,FUNCT_1:def 6;
  end;
  let x be set;
  assume x in f.:(C /\ B);
  then consider y being set such that
A7: y in dom f and
A8: y in C /\ B and
A9: x=f.y by FUNCT_1:def 6;
A10: y in C by A8,XBOOLE_0:def 4;
  y in B by A8,XBOOLE_0:def 4;
  then y in (dom f)/\ B by A7,XBOOLE_0:def 4;
  then
A11: y in dom (f|B) by RELAT_1:61;
  then (f|B).y=f.y by FUNCT_1:47;
  hence thesis by A9,A10,A11,FUNCT_1:def 6;
end;

theorem Th3:
  for X,Y being non empty TopSpace, p0 being Point of X, D being
  non empty Subset of X, E being non empty Subset of Y, f being Function of X,Y
st D`={p0} & E`={f.p0} & X is T_2 & Y is T_2 & (for p being Point of X|D holds
f.p<>f.p0)& f|D is continuous Function of X|D,Y|E & (for V being Subset of Y st
f.p0 in V & V is open ex W being Subset of X st p0 in W & W is open & f.:W c= V
  ) holds f is continuous
proof
  let X,Y be non empty TopSpace, p0 be Point of X, D be non empty Subset of X,
  E be non empty Subset of Y, f be Function of X,Y;
  assume that
A1: D`={p0} and
A2: E`={f.p0} and
A3: X is T_2 and
A4: Y is T_2 and
A5: for p being Point of X|D holds f.p<>f.p0 and
A6: f|D is continuous Function of X|D,Y|E and
A7: for V being Subset of Y st f.p0 in V & V is open ex W being Subset
  of X st p0 in W & W is open & f.:W c= V;
  for p being Point of X,V being Subset of Y st f.p in V & V is open holds
  ex W being Subset of X st p in W & W is open & f.:W c= V
  proof
A8: the carrier of X|D=D by PRE_TOPC:8;
    let p be Point of X,V be Subset of Y;
    assume that
A9: f.p in V and
A10: V is open;
    per cases;
    suppose
      p=p0;
      hence thesis by A7,A9,A10;
    end;
    suppose
A11:  p<>p0;
      then not p in D` by A1,TARSKI:def 1;
      then p in (the carrier of X)\D` by XBOOLE_0:def 5;
      then
A12:  p in D`` by SUBSET_1:def 4;
      then f.p<>f.p0 by A5,A8;
      then consider G1,G2 being Subset of Y such that
A13:  G1 is open and
      G2 is open and
A14:  f.p in G1 and
      f.p0 in G2 and
      G1 misses G2 by A4,PRE_TOPC:def 10;
A15:  [#](X|D)=D by PRE_TOPC:def 5;
      then reconsider p22=p as Point of X|D by A12;
      consider h being Function of X|D,Y|E such that
A16:  h=f|D and
A17:  h is continuous by A6;
A18:  h.p=f.p by A12,A16,FUNCT_1:49;
A19:  [#](Y|E)=E by PRE_TOPC:def 5;
      then reconsider V20=(G1 /\ V) /\ E as Subset of Y|E by XBOOLE_1:17;
      G1 /\ V is open by A10,A13,TOPS_1:11;
      then
A20:  V20 is open by A19,TOPS_2:24;
      f.p<>f.p0 by A5,A12,A15;
      then not f.p in E` by A2,TARSKI:def 1;
      then not f.p in (the carrier of Y) \E by SUBSET_1:def 4;
      then
A21:  h.p22 in E by A18,XBOOLE_0:def 5;
      h.p22 in G1 /\ V by A9,A14,A18,XBOOLE_0:def 4;
      then h.p22 in V20 by A21,XBOOLE_0:def 4;
      then consider W2 being Subset of X|D such that
A22:  p22 in W2 and
A23:  W2 is open and
A24:  h.:W2 c= V20 by A17,A20,JGRAPH_2:10;
      consider W3b being Subset of X such that
A25:  W3b is open and
A26:  W2=W3b /\ [#](X|D) by A23,TOPS_2:24;
      consider H1,H2 being Subset of X such that
A27:  H1 is open and
      H2 is open and
A28:  p in H1 and
A29:  p0 in H2 and
A30:  H1 misses H2 by A3,A11,PRE_TOPC:def 10;
      p22 in W3b by A22,A26,XBOOLE_0:def 4;
      then
A31:  p in H1 /\ W3b by A28,XBOOLE_0:def 4;
      reconsider W3=H1 /\ W3b as Subset of X;
A32:  W3 c= W3b by XBOOLE_1:17;
A33:  f.:W3 c= h.:W2
      proof
        let xx be set;
        assume xx in f.:W3;
        then consider yy being set such that
A34:    yy in dom f and
A35:    yy in W3 and
A36:    xx=f.yy by FUNCT_1:def 6;
        H2 c= H1` by A30,SUBSET_1:23;
        then D` c= H1` by A1,A29,ZFMISC_1:31;
        then W3 c= H1 & H1 c= D by SUBSET_1:12,XBOOLE_1:17;
        then
A37:    W3 c= D by XBOOLE_1:1;
        then
A38:    yy in W2 by A15,A26,A32,A35,XBOOLE_0:def 4;
        dom h=dom f /\ D by A16,RELAT_1:61;
        then
A39:    yy in dom h by A34,A35,A37,XBOOLE_0:def 4;
        then h.yy=f.yy by A16,FUNCT_1:47;
        hence thesis by A36,A39,A38,FUNCT_1:def 6;
      end;
      (G1 /\ V) /\ E c= G1 /\ V by XBOOLE_1:17;
      then G1 /\ V c= V & h.:W2 c= G1 /\ V by A24,XBOOLE_1:1,17;
      then
A40:  h.:W2 c= V by XBOOLE_1:1;
      H1 /\ W3b is open by A25,A27,TOPS_1:11;
      hence thesis by A31,A33,A40,XBOOLE_1:1;
    end;
  end;
  hence thesis by JGRAPH_2:10;
end;

begin :: The Circle is a Simple Closed Curve

reserve p,q for Point of TOP-REAL 2;

definition
  func Sq_Circ -> Function of the carrier of TOP-REAL 2, the carrier of
  TOP-REAL 2 means
  :Def1:
  for p being Point of TOP-REAL 2 holds (p=0.TOP-REAL 2
  implies it.p=p) & ((p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1)& p<>0.
TOP-REAL 2 implies it.p=|[p`1/sqrt(1+(p`2/p`1)^2),p`2/sqrt(1+(p`2/p`1)^2)]|)& (
not(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1)& p<>0.TOP-REAL 2 implies it.p
  =|[p`1/sqrt(1+(p`1/p`2)^2),p`2/sqrt(1+(p`1/p`2)^2)]|);
  existence
  proof
    defpred P[set,set] means (for p being Point of TOP-REAL 2 st p=$1 holds (p
=0.TOP-REAL 2 implies $2=p)& ((p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1)& p
<>0.TOP-REAL 2 implies $2=|[p`1/sqrt(1+(p`2/p`1)^2),p`2/sqrt(1+(p`2/p`1)^2)]|)&
(not(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1)& p<>0.TOP-REAL 2 implies $2=
    |[p`1/sqrt(1+(p`1/p`2)^2),p`2/sqrt(1+(p`1/p`2)^2)]|));
    set BP= the carrier of TOP-REAL 2;
A1: for x being Element of BP ex y being Element of BP st P[x,y]
    proof
      let x be Element of BP;
      set q=x;
      per cases;
      suppose
        q=0.TOP-REAL 2;
        then for p being Point of TOP-REAL 2 st p=x holds (p=0.TOP-REAL 2
implies 0.TOP-REAL 2=p)& ((p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1)& p<>0.
TOP-REAL 2 implies 0.TOP-REAL 2=|[p`1/sqrt(1+(p`2/p`1)^2),p`2/sqrt(1+(p`2/p`1)
^2)]|)& (not(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1)& p<>0.TOP-REAL 2
implies 0.TOP-REAL 2=|[p`1/sqrt(1+(p`1/p`2)^2),p`2/sqrt(1+(p`1/p`2)^2)]|);
        hence thesis;
      end;
      suppose
A2:     (q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2<=-q`1) & q<>0.TOP-REAL 2;
        set r= |[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]|;
        for p being Point of TOP-REAL 2 st p=x holds (p=0.TOP-REAL 2
implies r=p)& ((p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1)& p<>0.TOP-REAL 2
implies r=|[p`1/sqrt(1+(p`2/p`1)^2),p`2/sqrt(1+(p`2/p`1)^2)]|)& (not(p`2<=p`1 &
-p`1<=p`2 or p`2>=p`1 & p`2<=-p`1)& p<>0.TOP-REAL 2 implies r=|[p`1/sqrt(1+(p`1
        /p`2)^2),p`2/sqrt(1+(p`1/p`2)^2)]|) by A2;
        hence thesis;
      end;
      suppose
A3:     not (q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2<=-q`1) & q<>0. TOP-REAL 2;
        set r= |[q`1/sqrt(1+(q`1/q`2)^2),q`2/sqrt(1+(q`1/q`2)^2)]|;
        for p being Point of TOP-REAL 2 st p=x holds (p=0.TOP-REAL 2
implies r=p)& ((p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1)& p<>0.TOP-REAL 2
implies r=|[p`1/sqrt(1+(p`2/p`1)^2),p`2/sqrt(1+(p`2/p`1)^2)]|)& (not(p`2<=p`1 &
-p`1<=p`2 or p`2>=p`1 & p`2<=-p`1)& p<>0.TOP-REAL 2 implies r=|[p`1/sqrt(1+(p`1
        /p`2)^2),p`2/sqrt(1+(p`1/p`2)^2)]|) by A3;
        hence thesis;
      end;
    end;
    ex h being Function of BP, BP st for x being Element of BP holds P[x,h
    .x] from FUNCT_2:sch 3(A1);
    then consider h being Function of BP,BP such that
A4: for x being Element of BP holds for p being Point of TOP-REAL 2 st
p=x holds (p=0.TOP-REAL 2 implies h.x=p)& ((p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 &
p`2<=-p`1)& p<>0.TOP-REAL 2 implies h.x=|[p`1/sqrt(1+(p`2/p`1)^2),p`2/sqrt(1+(p
    `2/p`1)^2)]|)& (not(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1)& p<>0.
TOP-REAL 2 implies h.x=|[p`1/sqrt(1+(p`1/p`2)^2),p`2/sqrt(1+(p`1/p`2)^2)]|);
    for p being Point of TOP-REAL 2 holds (p=0.TOP-REAL 2 implies h.p=p)&
((p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1)& p<>0.TOP-REAL 2 implies h.p=|[
p`1/sqrt(1+(p`2/p`1)^2),p`2/sqrt(1+(p`2/p`1)^2)]|)& (not(p`2<=p`1 & -p`1<=p`2
or p`2>=p`1 & p`2<=-p`1)& p<>0.TOP-REAL 2 implies h.p=|[p`1/sqrt(1+(p`1/p`2)^2)
    ,p`2/sqrt(1+(p`1/p`2)^2)]|) by A4;
    hence thesis;
  end;
  uniqueness
  proof
    let h1,h2 be Function of the carrier of TOP-REAL 2, the carrier of
    TOP-REAL 2;
    assume that
A5: for p being Point of TOP-REAL 2 holds (p=0.TOP-REAL 2 implies h1.
p=p)& ((p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1)& p<>0.TOP-REAL 2 implies
h1.p= |[p`1/sqrt(1+(p`2/p`1)^2),p`2/sqrt(1+(p`2/p`1)^2)]|)& (not(p`2<=p`1 & -p
`1<=p`2 or p`2>=p`1 & p`2<=-p`1)& p<>0.TOP-REAL 2 implies h1.p=|[p`1/sqrt(1+(p
    `1/p`2)^2 ),p`2/sqrt(1+(p`1/p`2)^2)]|) and
A6: for p being Point of TOP-REAL 2 holds (p=0.TOP-REAL 2 implies h2.
p =p)& ((p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1)& p<>0.TOP-REAL 2 implies
h2.p=|[p`1/sqrt(1+(p`2/p`1)^2),p`2/sqrt(1+(p`2/p`1)^2)]|)& (not(p`2<=p`1 & -p`1
<=p`2 or p`2>=p`1 & p`2<=-p`1)& p<>0.TOP-REAL 2 implies h2.p=|[p`1/sqrt(1+(p`1/
    p`2)^2),p`2/sqrt(1+(p`1/p`2)^2)]|);
    for x being set st x in (the carrier of TOP-REAL 2) holds h1.x=h2.x
    proof
      let x be set;
      assume x in the carrier of TOP-REAL 2;
      then reconsider q=x as Point of TOP-REAL 2;
      per cases;
      suppose
A7:     q=0.TOP-REAL 2;
        then h1.q=q by A5;
        hence thesis by A6,A7;
      end;
      suppose
A8:     (q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2<=-q`1)& q<>0.TOP-REAL 2;
        then
        h1.q=|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]| by A5;
        hence thesis by A6,A8;
      end;
      suppose
A9:     not (q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2<=-q`1) & q<>0. TOP-REAL 2;
        then
        h1.q=|[q`1/sqrt(1+(q`1/q`2)^2),q`2/sqrt(1+(q`1/q`2)^2)]| by A5;
        hence thesis by A6,A9;
      end;
    end;
    hence h1=h2 by FUNCT_2:12;
  end;
end;

theorem Th4:
  for p being Point of TOP-REAL 2 st p<>0.TOP-REAL 2 holds ((p`1<=
p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2)implies Sq_Circ.p=|[p`1/sqrt(1+(p`1/p`2
)^2),p`2/sqrt(1+(p`1/p`2)^2)]|) & (not(p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=
  -p`2) implies Sq_Circ.p=|[p`1/sqrt(1+(p`2/p`1)^2),p`2/sqrt(1+(p`2/p`1)^2)]|)
proof
  let p be Point of TOP-REAL 2;
A1: -p`2<p`1 implies --p`2>-p`1 by XREAL_1:24;
  assume
A2: p<>0.TOP-REAL 2;
  hereby
    assume
A3: p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2;
    now
      per cases by A3;
      case
A4:     p`1<=p`2 & -p`2<=p`1;
        now
          assume
A5:       p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1;
A6:       now
            per cases by A5;
            case
              p`2<=p`1 & -p`1<=p`2;
              hence p`1=p`2 or p`1=-p`2 by A4,XXREAL_0:1;
            end;
            case
              p`2>=p`1 & p`2<=-p`1;
              then -p`2>=--p`1 by XREAL_1:24;
              hence p`1=p`2 or p`1=-p`2 by A4,XXREAL_0:1;
            end;
          end;
          now
            per cases by A6;
            case
              p`1=p`2;
              hence
              Sq_Circ.p=|[p`1/sqrt(1+(p`1/p`2)^2),p`2/sqrt(1+(p`1/p`2)^2)
              ]| by A2,A5,Def1;
            end;
            case
A7:           p`1=-p`2;
              then p`1<>0 & -p`1=p`2 by A2,EUCLID:53,54;
              then
A8:           p`2/p`1=-1 by XCMPLX_1:197;
              p`2<>0 by A2,A7,EUCLID:53,54;
              then p`1/p`2=-1 by A7,XCMPLX_1:197;
              hence
              Sq_Circ.p=|[p`1/sqrt(1+(p`1/p`2)^2),p`2/sqrt(1+(p`1/p`2)^2)
              ]| by A2,A5,A8,Def1;
            end;
          end;
          hence Sq_Circ.p=|[p`1/sqrt(1+(p`1/p`2)^2),p`2/sqrt(1+(p`1/p`2)^2)]|;
        end;
        hence Sq_Circ.p=|[p`1/sqrt(1+(p`1/p`2)^2),p`2/sqrt(1+(p`1/p`2)^2)]| by
A2,Def1;
      end;
      case
A9:     p`1>=p`2 & p`1<=-p`2;
        now
          assume
A10:      p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1;
A11:      now
            per cases by A10;
            case
              p`2<=p`1 & -p`1<=p`2;
              then --p`1>=-p`2 by XREAL_1:24;
              hence p`1=p`2 or p`1=-p`2 by A9,XXREAL_0:1;
            end;
            case
              p`2>=p`1 & p`2<=-p`1;
              hence p`1=p`2 or p`1=-p`2 by A9,XXREAL_0:1;
            end;
          end;
          now
            per cases by A11;
            case
              p`1=p`2;
              hence
              Sq_Circ.p=|[p`1/sqrt(1+(p`1/p`2)^2),p`2/sqrt(1+(p`1/p`2)^2)
              ]| by A2,A10,Def1;
            end;
            case
A12:          p`1=-p`2;
              then p`1<>0 & -p`1=p`2 by A2,EUCLID:53,54;
              then
A13:          p`2/p`1=-1 by XCMPLX_1:197;
              p`2<>0 by A2,A12,EUCLID:53,54;
              then p`1/p`2=-1 by A12,XCMPLX_1:197;
              hence
              Sq_Circ.p=|[p`1/sqrt(1+(p`1/p`2)^2),p`2/sqrt(1+(p`1/p`2)^2)
              ]| by A2,A10,A13,Def1;
            end;
          end;
          hence Sq_Circ.p=|[p`1/sqrt(1+(p`1/p`2)^2),p`2/sqrt(1+(p`1/p`2)^2)]|;
        end;
        hence Sq_Circ.p=|[p`1/sqrt(1+(p`1/p`2)^2),p`2/sqrt(1+(p`1/p`2)^2)]| by
A2,Def1;
      end;
    end;
    hence Sq_Circ.p=|[p`1/sqrt(1+(p`1/p`2)^2),p`2/sqrt(1+(p`1/p`2)^2)]|;
  end;
A14: -p`2>p`1 implies --p`2<-p`1 by XREAL_1:24;
  assume not(p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2);
  hence thesis by A2,A1,A14,Def1;
end;

theorem Th5:
  for X being non empty TopSpace, f1 being Function of X,R^1 st f1
is continuous & (for q being Point of X ex r being real number st f1.q=r & r>=0
  ) holds ex g being Function of X,R^1 st (for p being Point of X,r1 being real
  number st f1.p=r1 holds g.p=sqrt(r1)) & g is continuous
proof
  let X being non empty TopSpace,f1 be Function of X,R^1;
  assume that
A1: f1 is continuous and
A2: for q being Point of X ex r being real number st f1.q=r & r>=0;
  defpred P[set,set] means (for r11 being real number st f1.$1=r11 holds $2=
  sqrt(r11));
A3: for x being Element of X ex y being Element of REAL st P[x,y]
  proof
    let x be Element of X;
    reconsider pp=x as Point of X;
    reconsider r1=f1.pp as Real by TOPMETR:17;
    for r11 being real number st f1.x=r11 holds sqrt(r1)=sqrt(r11);
    hence thesis;
  end;
  ex f being Function of the carrier of X,REAL st for x2 being Element of
  X holds P[x2,f.x2] from FUNCT_2:sch 3(A3);
  then consider f being Function of the carrier of X,REAL such that
A4: for x2 being Element of X holds for r11 being real number st f1.x2=
  r11 holds f.x2=sqrt(r11);
  reconsider g0=f as Function of X,R^1 by TOPMETR:17;
  for p being Point of X,V being Subset of R^1 st g0.p in V & V is open
  holds ex W being Subset of X st p in W & W is open & g0.:W c= V
  proof
    let p be Point of X,V be Subset of R^1;
    reconsider r=g0.p as Real by TOPMETR:17;
    reconsider r1=f1.p as Real by TOPMETR:17;
    assume g0.p in V & V is open;
    then consider r01 being Real such that
A5: r01>0 and
A6: ].r-r01,r+r01.[ c= V by FRECHET:8;
    set r0=min(r01,1);
A7: r0>0 by A5,XXREAL_0:21;
A8: r0>0 by A5,XXREAL_0:21;
    r0<=r01 by XXREAL_0:17;
    then r-r01<=r-r0 & r+r0<=r+r01 by XREAL_1:6,10;
    then ].r-r0,r+r0.[ c= ].r-r01,r+r01.[ by XXREAL_1:46;
    then
A9: ].r-r0,r+r0.[ c= V by A6,XBOOLE_1:1;
A10: ex r8 being real number st f1.p=r8 & r8>=0 by A2;
A11: r=sqrt(r1) by A4;
    then
A12: r1=r^2 by A10,SQUARE_1:def 2;
A13: r>=0 by A10,A11,SQUARE_1:17,26;
    then
A14: 2*r*r0+r0^2>0+0 by A8,SQUARE_1:12,XREAL_1:8;
    per cases;
    suppose
A15:  r-r0>0;
      set r4=r0*(r-r0);
      reconsider G1=].r1-r4,r1+r4.[ as Subset of R^1 by TOPMETR:17;
A16:  r1<r1+r4 by A8,A15,XREAL_1:29,129;
      then r1-r4<r1 by XREAL_1:19;
      then
A17:  f1.p in G1 by A16,XXREAL_1:4;
      G1 is open by JORDAN6:35;
      then consider W1 being Subset of X such that
A18:  p in W1 & W1 is open and
A19:  f1.:W1 c= G1 by A1,A17,JGRAPH_2:10;
      set W=W1;
A20:  (r-(1/2)*r0)^2>=0 & r0^2>=0 by XREAL_1:63;
      now
        assume r1=0;
        then r=0 by A4,SQUARE_1:17;
        hence contradiction by A7,A15;
      end;
      then 0<r by A10,A11,SQUARE_1:25;
      then
A21:  r0*r>0 by A8,XREAL_1:129;
      then 0+r*r0< r*r0+r*r0 by XREAL_1:8;
      then r0*r-r0*r0< 2*r*r0-r0*r0 by XREAL_1:14;
      then -r4 >-(2*r*r0-r0^2) by XREAL_1:24;
      then r1+-r4 >r^2+-(2*r*r0-r0^2) by A12,XREAL_1:8;
      then sqrt(r1-r4)>sqrt((r-r0)^2) by SQUARE_1:27,XREAL_1:63;
      then
A22:  sqrt(r1-r4)>r-r0 by A15,SQUARE_1:22;
      0+r*r0< r*r0+r*r0 by A21,XREAL_1:8;
      then r0*r+0< 2*r*r0+2*(r0*r0) by A8,XREAL_1:8;
      then r0*r-r0*r0+r0*r0< 2*r*r0+r0*r0+r0*r0;
      then r0*r-r0*r0< 2*r*r0+r0*r0 by XREAL_1:7;
      then r1+r4 <r^2+(2*r*r0+r0^2) by A12,XREAL_1:8;
      then sqrt(r1+r4)<sqrt((r+r0)^2) by A10,A8,A15,SQUARE_1:27;
      then
A23:  r+r0>sqrt(r1+r4) by A13,A7,SQUARE_1:22;
A24:  r1-r4=r^2-(r0*r-r0*r0)by A10,A11,SQUARE_1:def 2
        .=(r-(1/2)*r0)^2+(3/4)*r0^2;
      g0.:W c= ].r-r0,r+r0.[
      proof
        let x be set;
        assume x in g0.:W;
        then consider z being set such that
A25:    z in dom g0 and
A26:    z in W and
A27:    g0.z=x by FUNCT_1:def 6;
        reconsider pz=z as Point of X by A25;
        reconsider aa1=f1.pz as Real by TOPMETR:17;
A28:    ex r9 being real number st f1.pz=r9 & r9>=0 by A2;
        pz in the carrier of X;
        then pz in dom f1 by FUNCT_2:def 1;
        then
A29:    f1.pz in f1.:W1 by A26,FUNCT_1:def 6;
        then aa1<r1+r4 by A19,XXREAL_1:4;
        then sqrt(aa1)<sqrt(r1+r4) by A28,SQUARE_1:27;
        then
A30:    sqrt(aa1)< r+r0 by A23,XXREAL_0:2;
A31:    r1-r4<aa1 by A19,A29,XXREAL_1:4;
A32:    now
          per cases;
          case
            0<=r1-r4;
            then sqrt(r1-r4)<=sqrt(aa1) by A31,SQUARE_1:26;
            hence r-r0< sqrt(aa1) by A22,XXREAL_0:2;
          end;
          case
            0>r1-r4;
            hence contradiction by A24,A20;
          end;
        end;
        x=sqrt(aa1) by A4,A27;
        hence thesis by A30,A32,XXREAL_1:4;
      end;
      hence thesis by A9,A18,XBOOLE_1:1;
    end;
    suppose
A33:  r-r0<=0;
      set r4=(2*r*r0+r0^2)/3;
      reconsider G1=].r1-r4,r1+r4.[ as Subset of R^1 by TOPMETR:17;
      (2*r*r0+r0^2)/3>0 by A14,XREAL_1:139;
      then
A34:  r1<r1+r4 by XREAL_1:29;
      then r1-r4<r1 by XREAL_1:19;
      then
A35:  f1.p in G1 by A34,XXREAL_1:4;
      G1 is open by JORDAN6:35;
      then consider W1 being Subset of X such that
A36:  p in W1 & W1 is open and
A37:  f1.:W1 c= G1 by A1,A35,JGRAPH_2:10;
      set W=W1;
      (2*r*r0+r0^2)/3<(2*r*r0+r0^2) by A14,XREAL_1:221;
      then r1+r4 <r^2+(2*r*r0+r0^2) by A12,XREAL_1:8;
      then sqrt(r1+r4)<=sqrt((r+r0)^2) by A10,A13,A8,SQUARE_1:26;
      then
A38:  r+r0>=sqrt(r1+r4) by A13,A7,SQUARE_1:22;
      g0.:W c= ].r-r0,r+r0.[
      proof
        let x be set;
        assume x in g0.:W;
        then consider z being set such that
A39:    z in dom g0 and
A40:    z in W and
A41:    g0.z=x by FUNCT_1:def 6;
        reconsider pz=z as Point of X by A39;
        reconsider aa1=f1.pz as Real by TOPMETR:17;
A42:    ex r9 being real number st f1.pz=r9 & r9>=0 by A2;
        pz in the carrier of X;
        then pz in dom f1 by FUNCT_2:def 1;
        then
A43:    f1.pz in f1.:W1 by A40,FUNCT_1:def 6;
        then aa1<r1+r4 by A37,XXREAL_1:4;
        then sqrt(aa1)<sqrt(r1+r4) by A42,SQUARE_1:27;
        then
A44:    sqrt(aa1)< r+r0 by A38,XXREAL_0:2;
A45:    r1-r4<aa1 by A37,A43,XXREAL_1:4;
A46:    now
          per cases by A33;
          case
            r-r0=0;
            hence r-r0<sqrt(aa1) by A12,A45,SQUARE_1:17,27;
          end;
          case
            r-r0<0;
            hence r-r0<sqrt(aa1) by A42,SQUARE_1:17,26;
          end;
        end;
        x=sqrt(aa1) by A4,A41;
        hence thesis by A44,A46,XXREAL_1:4;
      end;
      hence thesis by A9,A36,XBOOLE_1:1;
    end;
  end;
  then
A47: g0 is continuous by JGRAPH_2:10;
  for p being Point of X,r11 being real number st f1.p=r11 holds g0.p=sqrt
  (r11) by A4;
  hence thesis by A47;
end;

theorem Th6:
  for X being non empty TopSpace, f1,f2 being Function of X,R^1 st
  f1 is continuous & f2 is continuous & (for q being Point of X holds f2.q<>0)
holds ex g being Function of X,R^1 st (for p being Point of X,r1,r2 being real
  number st f1.p=r1 & f2.p=r2 holds g.p=(r1/r2)^2) & g is continuous
proof
  let X be non empty TopSpace, f1,f2 be Function of X,R^1;
  assume
  f1 is continuous & f2 is continuous & for q being Point of X holds f2.q <>0;
  then consider g2 being Function of X,R^1 such that
A1: for p being Point of X,r1,r2 being real number st f1.p=r1 & f2.p=r2
  holds g2.p=r1/r2 and
A2: g2 is continuous by JGRAPH_2:27;
  consider g3 being Function of X,R^1 such that
A3: for p being Point of X,r1 being real number st g2.p=r1 holds g3.p=r1
  *r1 and
A4: g3 is continuous by A2,JGRAPH_2:22;
  for p being Point of X,r1,r2 being real number st f1.p=r1 & f2.p=r2
  holds g3.p=(r1/r2)^2
  proof
    let p be Point of X,r1,r2 be real number;
    assume f1.p=r1 & f2.p=r2;
    then g2.p=r1/r2 by A1;
    hence thesis by A3;
  end;
  hence thesis by A4;
end;

theorem Th7:
  for X being non empty TopSpace, f1,f2 being Function of X,R^1 st
  f1 is continuous & f2 is continuous & (for q being Point of X holds f2.q<>0)
holds ex g being Function of X,R^1 st (for p being Point of X,r1,r2 being real
  number st f1.p=r1 & f2.p=r2 holds g.p=1+(r1/r2)^2) & g is continuous
proof
  let X be non empty TopSpace, f1,f2 be Function of X,R^1;
  assume
  f1 is continuous & f2 is continuous & for q being Point of X holds f2.q <>0;
  then consider g2 being Function of X,R^1 such that
A1: for p being Point of X,r1,r2 being real number st f1.p=r1 & f2.p=r2
  holds g2.p=(r1/r2)^2 and
A2: g2 is continuous by Th6;
  consider g3 being Function of X,R^1 such that
A3: for p being Point of X,r1 being real number st g2.p=r1 holds g3.p=r1 +1 and
A4: g3 is continuous by A2,JGRAPH_2:24;
  for p being Point of X,r1,r2 being real number st f1.p=r1 & f2.p=r2
  holds g3.p=1+(r1/r2)^2
  proof
    let p be Point of X,r1,r2 be real number;
    assume f1.p=r1 & f2.p=r2;
    then g2.p=(r1/r2)^2 by A1;
    hence thesis by A3;
  end;
  hence thesis by A4;
end;

theorem Th8:
  for X being non empty TopSpace, f1,f2 being Function of X,R^1 st
  f1 is continuous & f2 is continuous & (for q being Point of X holds f2.q<>0)
holds ex g being Function of X,R^1 st (for p being Point of X,r1,r2 being real
  number st f1.p=r1 & f2.p=r2 holds g.p=sqrt(1+(r1/r2)^2)) & g is continuous
proof
  let X be non empty TopSpace, f1,f2 be Function of X,R^1;
  assume
  f1 is continuous & f2 is continuous & for q being Point of X holds f2.q <>0;
  then consider g2 being Function of X,R^1 such that
A1: for p being Point of X,r1,r2 being real number st f1.p=r1 & f2.p=r2
  holds g2.p=1+(r1/r2)^2 and
A2: g2 is continuous by Th7;
  for q being Point of X ex r being real number st g2.q=r & r>=0
  proof
    let q be Point of X;
    reconsider r1=f1.q,r2=f2.q as Real by TOPMETR:17;
    1+(r1/r2)^2>0 by Lm1;
    hence thesis by A1;
  end;
  then consider g3 being Function of X,R^1 such that
A3: for p being Point of X,r1 being real number st g2.p=r1 holds g3.p=
  sqrt( r1) and
A4: g3 is continuous by A2,Th5;
  for p being Point of X,r1,r2 being real number st f1.p=r1 & f2.p=r2
  holds g3.p=sqrt(1+(r1/r2)^2)
  proof
    let p be Point of X,r1,r2 be real number;
    assume f1.p=r1 & f2.p=r2;
    then g2.p=1+(r1/r2)^2 by A1;
    hence thesis by A3;
  end;
  hence thesis by A4;
end;

theorem Th9:
  for X being non empty TopSpace, f1,f2 being Function of X,R^1 st
  f1 is continuous & f2 is continuous & (for q being Point of X holds f2.q<>0)
holds ex g being Function of X,R^1 st (for p being Point of X,r1,r2 being real
  number st f1.p=r1 & f2.p=r2 holds g.p=r1/sqrt(1+(r1/r2)^2)) & g is continuous
proof
  let X be non empty TopSpace,f1,f2 be Function of X,R^1;
  assume that
A1: f1 is continuous and
A2: f2 is continuous & for q being Point of X holds f2.q<>0;
  consider g2 being Function of X,R^1 such that
A3: for p being Point of X,r1,r2 being real number st f1.p=r1 & f2.p=r2
  holds g2.p=sqrt(1+(r1/r2)^2) and
A4: g2 is continuous by A1,A2,Th8;
  for q being Point of X holds g2.q<>0
  proof
    let q be Point of X;
    reconsider r1=f1.q,r2=f2.q as Real by TOPMETR:17;
    sqrt(1+(r1/r2)^2)>0 by Lm1,SQUARE_1:25;
    hence thesis by A3;
  end;
  then consider g3 being Function of X,R^1 such that
A5: for p being Point of X,r1,r0 being real number st f1.p=r1 & g2.p=r0
  holds g3.p=r1/r0 and
A6: g3 is continuous by A1,A4,JGRAPH_2:27;
  for p being Point of X,r1,r2 being real number st f1.p=r1 & f2.p=r2
  holds g3.p=r1/sqrt(1+(r1/r2)^2)
  proof
    let p be Point of X,r1,r2 be real number;
    assume that
A7: f1.p=r1 and
A8: f2.p=r2;
    g2.p=sqrt(1+(r1/r2)^2) by A3,A7,A8;
    hence thesis by A5,A7;
  end;
  hence thesis by A6;
end;

theorem Th10:
  for X being non empty TopSpace, f1,f2 being Function of X,R^1 st
  f1 is continuous & f2 is continuous & (for q being Point of X holds f2.q<>0)
holds ex g being Function of X,R^1 st (for p being Point of X,r1,r2 being real
  number st f1.p=r1 & f2.p=r2 holds g.p=r2/sqrt(1+(r1/r2)^2)) & g is continuous
proof
  let X be non empty TopSpace, f1,f2 be Function of X,R^1;
  assume that
A1: f1 is continuous and
A2: f2 is continuous and
A3: for q being Point of X holds f2.q<>0;
  consider g2 being Function of X,R^1 such that
A4: for p being Point of X,r1,r2 being real number st f1.p=r1 & f2.p=r2
  holds g2.p=sqrt(1+(r1/r2)^2) and
A5: g2 is continuous by A1,A2,A3,Th8;
  for q being Point of X holds g2.q<>0
  proof
    let q be Point of X;
    reconsider r1=f1.q,r2=f2.q as Real by TOPMETR:17;
    sqrt(1+(r1/r2)^2)>0 by Lm1,SQUARE_1:25;
    hence thesis by A4;
  end;
  then consider g3 being Function of X,R^1 such that
A6: for p being Point of X,r2,r0 being real number st f2.p=r2 & g2.p=r0
  holds g3.p=r2/r0 and
A7: g3 is continuous by A2,A5,JGRAPH_2:27;
  for p being Point of X,r1,r2 being real number st f1.p=r1 & f2.p=r2
  holds g3.p=r2/sqrt(1+(r1/r2)^2)
  proof
    let p be Point of X,r1,r2 be real number;
    assume that
A8: f1.p=r1 and
A9: f2.p=r2;
    g2.p=sqrt(1+(r1/r2)^2) by A4,A8,A9;
    hence thesis by A6,A9;
  end;
  hence thesis by A7;
end;

Lm4: for K1 being non empty Subset of TOP-REAL 2 holds for q being Point of (
TOP-REAL 2)|K1 holds (proj2|K1).q=proj2.q
proof
  let K1 be non empty Subset of TOP-REAL 2;
  let q be Point of (TOP-REAL 2)|K1;
  the carrier of (TOP-REAL 2)|K1=K1 & q in the carrier of (TOP-REAL 2)|K1
  by PRE_TOPC:8;
  then q in dom proj2 /\ K1 by Lm3,XBOOLE_0:def 4;
  hence thesis by FUNCT_1:48;
end;

Lm5: for K1 being non empty Subset of TOP-REAL 2 holds proj2|K1 is continuous
Function of (TOP-REAL 2)|K1,R^1
proof
  let K1 be non empty Subset of TOP-REAL 2;
  reconsider g2=proj2|K1 as Function of (TOP-REAL 2)|K1,R^1 by TOPMETR:17;
  for q be Point of (TOP-REAL 2)|K1 holds g2.q=proj2.q by Lm4;
  hence thesis by JGRAPH_2:30;
end;

Lm6: for K1 being non empty Subset of TOP-REAL 2 holds for q being Point of (
TOP-REAL 2)|K1 holds (proj1|K1).q=proj1.q
proof
  let K1 be non empty Subset of TOP-REAL 2;
  let q be Point of (TOP-REAL 2)|K1;
  the carrier of (TOP-REAL 2)|K1=K1 & q in the carrier of (TOP-REAL 2)|K1
  by PRE_TOPC:8;
  then q in dom proj1 /\ K1 by Lm2,XBOOLE_0:def 4;
  hence thesis by FUNCT_1:48;
end;

Lm7: for K1 being non empty Subset of TOP-REAL 2 holds proj1|K1 is continuous
Function of (TOP-REAL 2)|K1,R^1
proof
  let K1 be non empty Subset of TOP-REAL 2;
  reconsider g2=proj1|K1 as Function of (TOP-REAL 2)|K1,R^1 by TOPMETR:17;
  for q be Point of (TOP-REAL 2)|K1 holds g2.q=proj1.q by Lm6;
  hence thesis by JGRAPH_2:29;
end;

theorem Th11:
  for K1 being non empty Subset of TOP-REAL 2, f being Function of
(TOP-REAL 2)|K1,R^1 st (for p being Point of TOP-REAL 2 st p in the carrier of
  (TOP-REAL 2)|K1 holds f.p=p`1/sqrt(1+(p`2/p`1)^2)) & (for q being Point of
  TOP-REAL 2 st q in the carrier of (TOP-REAL 2)|K1 holds q`1<>0 ) holds f is
  continuous
proof
  let K1 be non empty Subset of TOP-REAL 2, f be Function of (TOP-REAL 2)|K1,
  R^1;
  reconsider g1=proj1|K1 as continuous Function of (TOP-REAL 2)|K1,R^1 by Lm7;
  reconsider g2=proj2|K1 as continuous Function of (TOP-REAL 2)|K1,R^1 by Lm5;
  assume that
A1: for p being Point of (TOP-REAL 2) st p in the carrier of (TOP-REAL 2
  )| K1 holds f.p=p`1/sqrt(1+(p`2/p`1)^2) and
A2: for q being Point of TOP-REAL 2 st q in the carrier of (TOP-REAL 2)
  |K1 holds q`1<>0;
A3: the carrier of (TOP-REAL 2)|K1=K1 by PRE_TOPC:8;
  now
    let q be Point of (TOP-REAL 2)|K1;
    q in the carrier of (TOP-REAL 2)|K1;
    then reconsider q2=q as Point of TOP-REAL 2 by A3;
    g1.q=proj1.q by Lm6
      .=q2`1 by PSCOMP_1:def 5;
    hence g1.q<>0 by A2;
  end;
  then consider g3 being Function of (TOP-REAL 2)|K1,R^1 such that
A4: for q being Point of (TOP-REAL 2)|K1,r1,r2 being real number st g2.q
  =r1 & g1.q=r2 holds g3.q=r2/sqrt(1+(r1/r2)^2) and
A5: g3 is continuous by Th10;
A6: for x being set st x in dom f holds f.x=g3.x
  proof
    let x be set;
    assume
A7: x in dom f;
    then reconsider s=x as Point of (TOP-REAL 2)|K1;
    x in the carrier of (TOP-REAL 2)|K1 by A7;
    then x in K1 by PRE_TOPC:8;
    then reconsider r=x as Point of (TOP-REAL 2);
A8: proj2.r=r`2 & proj1.r=r`1 by PSCOMP_1:def 5,def 6;
A9: g2.s=proj2.s & g1.s=proj1.s by Lm4,Lm6;
    f.r=r`1/sqrt(1+(r`2/r`1)^2) by A1,A7;
    hence thesis by A4,A9,A8;
  end;
  dom g3=the carrier of (TOP-REAL 2)|K1 by FUNCT_2:def 1;
  then dom f=dom g3 by FUNCT_2:def 1;
  hence thesis by A5,A6,FUNCT_1:2;
end;

theorem Th12:
  for K1 being non empty Subset of TOP-REAL 2, f being Function of
(TOP-REAL 2)|K1,R^1 st (for p being Point of TOP-REAL 2 st p in the carrier of
  (TOP-REAL 2)|K1 holds f.p=p`2/sqrt(1+(p`2/p`1)^2)) & (for q being Point of
  TOP-REAL 2 st q in the carrier of (TOP-REAL 2)|K1 holds q`1<>0 ) holds f is
  continuous
proof
  let K1 be non empty Subset of TOP-REAL 2, f be Function of (TOP-REAL 2)|K1,
  R^1;
  reconsider g1=proj1|K1 as continuous Function of (TOP-REAL 2)|K1,R^1 by Lm7;
  reconsider g2=proj2|K1 as continuous Function of (TOP-REAL 2)|K1,R^1 by Lm5;
  assume that
A1: for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)|
  K1 holds f.p=p`2/sqrt(1+(p`2/p`1)^2) and
A2: for q being Point of TOP-REAL 2 st q in the carrier of (TOP-REAL 2)
  |K1 holds q`1<>0;
A3: the carrier of (TOP-REAL 2)|K1=K1 by PRE_TOPC:8;
  now
    let q be Point of (TOP-REAL 2)|K1;
    q in the carrier of (TOP-REAL 2)|K1;
    then reconsider q2=q as Point of TOP-REAL 2 by A3;
    g1.q=proj1.q by Lm6
      .=q2`1 by PSCOMP_1:def 5;
    hence g1.q<>0 by A2;
  end;
  then consider g3 being Function of (TOP-REAL 2)|K1,R^1 such that
A4: for q being Point of (TOP-REAL 2)|K1,r1,r2 being real number st g2.q
  =r1 & g1.q=r2 holds g3.q=r1/sqrt(1+(r1/r2)^2) and
A5: g3 is continuous by Th9;
A6: for x being set st x in dom f holds f.x=g3.x
  proof
    let x be set;
    assume
A7: x in dom f;
    then reconsider s=x as Point of (TOP-REAL 2)|K1;
    x in the carrier of (TOP-REAL 2)|K1 by A7;
    then x in K1 by PRE_TOPC:8;
    then reconsider r=x as Point of (TOP-REAL 2);
A8: proj2.r=r`2 & proj1.r=r`1 by PSCOMP_1:def 5,def 6;
A9: g2.s=proj2.s & g1.s=proj1.s by Lm4,Lm6;
    f.r=r`2/sqrt(1+(r`2/r`1)^2) by A1,A7;
    hence thesis by A4,A9,A8;
  end;
  dom g3=the carrier of (TOP-REAL 2)|K1 by FUNCT_2:def 1;
  then dom f=dom g3 by FUNCT_2:def 1;
  hence thesis by A5,A6,FUNCT_1:2;
end;

theorem Th13:
  for K1 being non empty Subset of TOP-REAL 2, f being Function of
(TOP-REAL 2)|K1,R^1 st (for p being Point of TOP-REAL 2 st p in the carrier of
  (TOP-REAL 2)|K1 holds f.p=p`2/sqrt(1+(p`1/p`2)^2)) & (for q being Point of
  TOP-REAL 2 st q in the carrier of (TOP-REAL 2)|K1 holds q`2<>0 ) holds f is
  continuous
proof
  let K1 be non empty Subset of TOP-REAL 2, f be Function of (TOP-REAL 2)|K1,
  R^1;
  reconsider g1=proj1|K1 as continuous Function of (TOP-REAL 2)|K1,R^1 by Lm7;
  reconsider g2=proj2|K1 as continuous Function of (TOP-REAL 2)|K1,R^1 by Lm5;
  assume that
A1: for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)|
  K1 holds f.p=p`2/sqrt(1+(p`1/p`2)^2) and
A2: for q being Point of TOP-REAL 2 st q in the carrier of (TOP-REAL 2)
  |K1 holds q`2<>0;
A3: the carrier of (TOP-REAL 2)|K1=K1 by PRE_TOPC:8;
  now
    let q be Point of (TOP-REAL 2)|K1;
    q in the carrier of (TOP-REAL 2)|K1;
    then reconsider q2=q as Point of TOP-REAL 2 by A3;
    g2.q=proj2.q by Lm4
      .=q2`2 by PSCOMP_1:def 6;
    hence g2.q<>0 by A2;
  end;
  then consider g3 being Function of (TOP-REAL 2)|K1,R^1 such that
A4: for q being Point of (TOP-REAL 2)|K1,r1,r2 being real number st g1.q
  =r1 & g2.q=r2 holds g3.q=r2/sqrt(1+(r1/r2)^2) and
A5: g3 is continuous by Th10;
A6: for x being set st x in dom f holds f.x=g3.x
  proof
    let x be set;
    assume
A7: x in dom f;
    then reconsider s=x as Point of (TOP-REAL 2)|K1;
    x in the carrier of (TOP-REAL 2)|K1 by A7;
    then x in K1 by PRE_TOPC:8;
    then reconsider r=x as Point of (TOP-REAL 2);
A8: proj2.r=r`2 & proj1.r=r`1 by PSCOMP_1:def 5,def 6;
A9: g2.s=proj2.s & g1.s=proj1.s by Lm4,Lm6;
    f.r=r`2/sqrt(1+(r`1/r`2)^2) by A1,A7;
    hence thesis by A4,A9,A8;
  end;
  dom g3=the carrier of (TOP-REAL 2)|K1 by FUNCT_2:def 1;
  then dom f=dom g3 by FUNCT_2:def 1;
  hence thesis by A5,A6,FUNCT_1:2;
end;

theorem Th14:
  for K1 being non empty Subset of TOP-REAL 2, f being Function of
(TOP-REAL 2)|K1,R^1 st (for p being Point of TOP-REAL 2 st p in the carrier of
  (TOP-REAL 2)|K1 holds f.p=p`1/sqrt(1+(p`1/p`2)^2)) & (for q being Point of
  TOP-REAL 2 st q in the carrier of (TOP-REAL 2)|K1 holds q`2<>0 ) holds f is
  continuous
proof
  let K1 be non empty Subset of TOP-REAL 2, f be Function of (TOP-REAL 2)|K1,
  R^1;
  reconsider g1=proj1|K1 as continuous Function of (TOP-REAL 2)|K1,R^1 by Lm7;
  reconsider g2=proj2|K1 as continuous Function of (TOP-REAL 2)|K1,R^1 by Lm5;
  assume that
A1: for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)|
  K1 holds f.p=p`1/sqrt(1+(p`1/p`2)^2) and
A2: for q being Point of TOP-REAL 2 st q in the carrier of (TOP-REAL 2)
  |K1 holds q`2<>0;
A3: the carrier of (TOP-REAL 2)|K1=K1 by PRE_TOPC:8;
  now
    let q be Point of (TOP-REAL 2)|K1;
    q in the carrier of (TOP-REAL 2)|K1;
    then reconsider q2=q as Point of TOP-REAL 2 by A3;
    g2.q=proj2.q by Lm4
      .=q2`2 by PSCOMP_1:def 6;
    hence g2.q<>0 by A2;
  end;
  then consider g3 being Function of (TOP-REAL 2)|K1,R^1 such that
A4: for q being Point of (TOP-REAL 2)|K1,r1,r2 being real number st g1.q
  =r1 & g2.q=r2 holds g3.q=r1/sqrt(1+(r1/r2)^2) and
A5: g3 is continuous by Th9;
A6: for x being set st x in dom f holds f.x=g3.x
  proof
    let x be set;
    assume
A7: x in dom f;
    then reconsider s=x as Point of (TOP-REAL 2)|K1;
    x in the carrier of (TOP-REAL 2)|K1 by A7;
    then x in K1 by PRE_TOPC:8;
    then reconsider r=x as Point of (TOP-REAL 2);
A8: proj2.r=r`2 & proj1.r=r`1 by PSCOMP_1:def 5,def 6;
A9: g2.s=proj2.s & g1.s=proj1.s by Lm4,Lm6;
    f.r=r`1/sqrt(1+(r`1/r`2)^2) by A1,A7;
    hence thesis by A4,A9,A8;
  end;
  dom g3=the carrier of (TOP-REAL 2)|K1 by FUNCT_2:def 1;
  then dom f=dom g3 by FUNCT_2:def 1;
  hence thesis by A5,A6,FUNCT_1:2;
end;

Lm8: 0.REAL 2 = 0.TOP-REAL 2 by EUCLID:66;

Lm9: (1.REAL 2)`2<=(1.REAL 2)`1 & -(1.REAL 2)`1<=(1.REAL 2)`2 or (1.REAL 2) `2
>=(1.REAL 2)`1 & (1.REAL 2)`2<=-(1.REAL 2)`1 by JGRAPH_2:5;

Lm10: (1.REAL 2)<>0.TOP-REAL 2 by Lm8,REVROT_1:19;

Lm11: for K1 being non empty Subset of TOP-REAL 2 holds dom ((proj2)*(Sq_Circ|
K1)) = the carrier of (TOP-REAL 2)|K1
proof
  let K1 be non empty Subset of TOP-REAL 2;
A1: dom (Sq_Circ|K1) c= dom ((proj2)*(Sq_Circ|K1))
  proof
    let x be set;
    assume
A2: x in dom (Sq_Circ|K1);
    then x in dom Sq_Circ /\ K1 by RELAT_1:61;
    then x in dom Sq_Circ by XBOOLE_0:def 4;
    then
A3: Sq_Circ.x in rng Sq_Circ by FUNCT_1:3;
    (Sq_Circ|K1).x=Sq_Circ.x by A2,FUNCT_1:47;
    hence thesis by A2,A3,Lm3,FUNCT_1:11;
  end;
  dom ((proj2)*(Sq_Circ|K1)) c= dom (Sq_Circ|K1) by RELAT_1:25;
  hence dom ((proj2)*(Sq_Circ|K1)) = dom (Sq_Circ|K1) by A1,XBOOLE_0:def 10
    .=dom Sq_Circ /\ K1 by RELAT_1:61
    .=(the carrier of TOP-REAL 2)/\ K1 by FUNCT_2:def 1
    .=K1 by XBOOLE_1:28
    .=the carrier of (TOP-REAL 2)|K1 by PRE_TOPC:8;
end;

Lm12: for K1 being non empty Subset of TOP-REAL 2 holds dom ((proj1)*(Sq_Circ|
K1)) = the carrier of (TOP-REAL 2)|K1
proof
  let K1 be non empty Subset of TOP-REAL 2;
A1: dom (Sq_Circ|K1) c= dom ((proj1)*(Sq_Circ|K1))
  proof
    let x be set;
    assume
A2: x in dom (Sq_Circ|K1);
    then x in dom Sq_Circ /\ K1 by RELAT_1:61;
    then x in dom Sq_Circ by XBOOLE_0:def 4;
    then
A3: Sq_Circ.x in rng Sq_Circ by FUNCT_1:3;
    (Sq_Circ|K1).x=Sq_Circ.x by A2,FUNCT_1:47;
    hence thesis by A2,A3,Lm2,FUNCT_1:11;
  end;
  dom ((proj1)*(Sq_Circ|K1)) c= dom (Sq_Circ|K1) by RELAT_1:25;
  hence dom ((proj1)*(Sq_Circ|K1)) =dom (Sq_Circ|K1) by A1,XBOOLE_0:def 10
    .=dom Sq_Circ /\ K1 by RELAT_1:61
    .=((the carrier of TOP-REAL 2))/\ K1 by FUNCT_2:def 1
    .=K1 by XBOOLE_1:28
    .=the carrier of (TOP-REAL 2)|K1 by PRE_TOPC:8;
end;

Lm13: NonZero TOP-REAL 2 <> {} by JGRAPH_2:9;

theorem Th15:
  for K0,B0 being Subset of TOP-REAL 2,f being Function of (
TOP-REAL 2)|K0,(TOP-REAL 2)|B0 st f=Sq_Circ|K0 & B0=NonZero TOP-REAL 2 & K0={p:
  (p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) & p<>0.TOP-REAL 2} holds f is
  continuous
proof
  let K0,B0 be Subset of TOP-REAL 2,f be Function of (TOP-REAL 2)|K0,(TOP-REAL
  2)|B0;
  assume
A1: f=Sq_Circ|K0 & B0=NonZero TOP-REAL 2 & K0={p:(p`2<=p`1 & -p`1<=p`2
  or p`2>=p`1 & p`2<=-p`1) & p<>0.TOP-REAL 2};
  then 1.REAL 2 in K0 by Lm9,Lm10;
  then reconsider K1=K0 as non empty Subset of TOP-REAL 2;
  dom ((proj1)*(Sq_Circ|K1)) = the carrier of (TOP-REAL 2)|K1 & rng ((
  proj1)*( Sq_Circ|K1)) c= the carrier of R^1 by Lm12,TOPMETR:17;
  then reconsider
  g1=(proj1)*(Sq_Circ|K1) as Function of (TOP-REAL 2)|K1,R^1 by FUNCT_2:2;
  for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)|K1
  holds g1.p=p`1/sqrt(1+(p`2/p`1)^2)
  proof
    let p be Point of TOP-REAL 2;
A2: dom (Sq_Circ|K1)=dom Sq_Circ /\ K1 by RELAT_1:61
      .=((the carrier of TOP-REAL 2))/\ K1 by FUNCT_2:def 1
      .=K1 by XBOOLE_1:28;
A3: the carrier of (TOP-REAL 2)|K1=K1 by PRE_TOPC:8;
    assume
A4: p in the carrier of (TOP-REAL 2)|K1;
    then ex p3 being Point of TOP-REAL 2 st p=p3 &( p3`2<=p3`1 & - p3`1<=p3`2
    or p3`2>=p3`1 & p3`2<=-p3`1)& p3<>0.TOP-REAL 2 by A1,A3;
    then
A5: Sq_Circ.p=|[p`1/sqrt(1+(p`2/p`1)^2), p`2/sqrt(1+(p`2/p`1)^2)]| by Def1;
    (Sq_Circ|K1).p=Sq_Circ.p by A4,A3,FUNCT_1:49;
    then g1.p=(proj1).(|[p`1/sqrt(1+(p`2/p`1)^2), p`2/sqrt(1+(p`2/p`1)^2)]|)
    by A4,A2,A3,A5,FUNCT_1:13
      .=(|[p`1/sqrt(1+(p`2/p`1)^2), p`2/sqrt(1+(p`2/p`1)^2)]|)`1 by
PSCOMP_1:def 5
      .=p`1/sqrt(1+(p`2/p`1)^2) by EUCLID:52;
    hence thesis;
  end;
  then consider f1 being Function of (TOP-REAL 2)|K1,R^1 such that
A6: for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)
  |K1 holds f1.p=p`1/sqrt(1+(p`2/p`1)^2);
  dom ((proj2)*(Sq_Circ|K1)) = the carrier of (TOP-REAL 2)|K1 & rng ((
  proj2)*( Sq_Circ|K1)) c= the carrier of R^1 by Lm11,TOPMETR:17;
  then reconsider
  g2=(proj2)*(Sq_Circ|K1) as Function of (TOP-REAL 2)|K1,R^1 by FUNCT_2:2;
  for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)|K1
  holds g2.p=p`2/sqrt(1+(p`2/p`1)^2)
  proof
    let p be Point of TOP-REAL 2;
A7: dom (Sq_Circ|K1)=dom Sq_Circ /\ K1 by RELAT_1:61
      .=((the carrier of TOP-REAL 2))/\ K1 by FUNCT_2:def 1
      .=K1 by XBOOLE_1:28;
A8: the carrier of (TOP-REAL 2)|K1=K1 by PRE_TOPC:8;
    assume
A9: p in the carrier of (TOP-REAL 2)|K1;
    then ex p3 being Point of TOP-REAL 2 st p=p3 &( p3`2<=p3`1 & - p3`1<=p3`2
    or p3`2>=p3`1 & p3`2<=-p3`1)& p3<>0.TOP-REAL 2 by A1,A8;
    then
A10: Sq_Circ.p =|[p`1/sqrt(1+(p`2/p`1)^2),p`2/sqrt(1+(p`2/p`1)^2)]| by Def1;
    (Sq_Circ|K1).p=Sq_Circ.p by A9,A8,FUNCT_1:49;
    then
    g2.p=(proj2).(|[p`1/sqrt(1+(p`2/p`1)^2),p`2/sqrt(1+(p`2/p`1)^2)]|) by A9,A7
,A8,A10,FUNCT_1:13
      .=(|[p`1/sqrt(1+(p`2/p`1)^2), p`2/sqrt(1+(p`2/p`1)^2)]|)`2 by
PSCOMP_1:def 6
      .=p`2/sqrt(1+(p`2/p`1)^2) by EUCLID:52;
    hence thesis;
  end;
  then consider f2 being Function of (TOP-REAL 2)|K1,R^1 such that
A11: for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)
  |K1 holds f2.p=p`2/sqrt(1+(p`2/p`1)^2);
A12: now
    let q be Point of TOP-REAL 2;
A13: the carrier of (TOP-REAL 2)|K1=K1 by PRE_TOPC:8;
    assume q in the carrier of (TOP-REAL 2)|K1;
    then
A14: ex p3 being Point of TOP-REAL 2 st q=p3 &( p3`2<=p3`1 & - p3`1<=p3`2 or
    p3`2>=p3`1 & p3`2<=-p3`1)& p3<>0.TOP-REAL 2 by A1,A13;
    now
      assume
A15:  q`1=0;
      then q`2=0 by A14;
      hence contradiction by A14,A15,EUCLID:53,54;
    end;
    hence q`1<>0;
  end;
  then
A16: f1 is continuous by A6,Th11;
A17: for x,y,r,s being real number st |[x,y]| in K1 & r=f1.(|[x,y]|) & s=f2.
  (|[x,y]|) holds f.(|[x,y]|)=|[r,s]|
  proof
    let x,y,r,s be real number;
    assume that
A18: |[x,y]| in K1 and
A19: r=f1.(|[x,y]|) & s=f2.(|[x,y]|);
    set p99=|[x,y]|;
A20: ex p3 being Point of TOP-REAL 2 st p99=p3 &( p3`2<=p3`1 & -p3`1<=p3`2
    or p3`2>=p3`1 & p3`2<=-p3`1)& p3<>0.TOP-REAL 2 by A1,A18;
A21: the carrier of (TOP-REAL 2)|K1=K1 by PRE_TOPC:8;
    then
A22: f1.p99=p99`1/sqrt(1+(p99`2/p99`1)^2) by A6,A18;
    (Sq_Circ|K0).(|[x,y]|)=(Sq_Circ).(|[x,y]|) by A18,FUNCT_1:49
      .= |[p99`1/sqrt(1+(p99`2/p99`1)^2), p99`2/sqrt(1+(p99`2/p99`1)^2)]| by
A20,Def1
      .=|[r,s]| by A11,A18,A19,A21,A22;
    hence thesis by A1;
  end;
  f2 is continuous by A12,A11,Th12;
  hence thesis by A1,A16,A17,Lm13,JGRAPH_2:35;
end;

Lm14: (1.REAL 2)`1<=(1.REAL 2)`2 & -(1.REAL 2)`2<=(1.REAL 2)`1 or (1.REAL 2 )
`1>=(1.REAL 2)`2 & (1.REAL 2)`1<=-(1.REAL 2)`2 by JGRAPH_2:5;

Lm15: (1.REAL 2)<>0.TOP-REAL 2 by Lm8,REVROT_1:19;

theorem Th16:
  for K0,B0 being Subset of TOP-REAL 2,f being Function of (
TOP-REAL 2)|K0,(TOP-REAL 2)|B0 st f=Sq_Circ|K0 & B0=NonZero TOP-REAL 2 & K0={p:
  (p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2) & p<>0.TOP-REAL 2} holds f is
  continuous
proof
  let K0,B0 be Subset of TOP-REAL 2,f be Function of (TOP-REAL 2)|K0,(TOP-REAL
  2)|B0;
  assume
A1: f=Sq_Circ|K0 & B0=NonZero TOP-REAL 2 & K0={p:(p`1<=p`2 & -p`2<=p`1
  or p`1>=p`2 & p`1<=-p`2) & p<>0.TOP-REAL 2};
  then 1.REAL 2 in K0 by Lm14,Lm15;
  then reconsider K1=K0 as non empty Subset of TOP-REAL 2;
  dom ((proj2)*(Sq_Circ|K1))=the carrier of (TOP-REAL 2)|K1 & rng ((proj2)
  *( Sq_Circ|K1)) c= the carrier of R^1 by Lm11,TOPMETR:17;
  then reconsider
  g1=(proj2)*(Sq_Circ|K1) as Function of (TOP-REAL 2)|K1,R^1 by FUNCT_2:2;
  for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)|K1
  holds g1.p=p`2/sqrt(1+(p`1/p`2)^2)
  proof
    let p be Point of TOP-REAL 2;
A2: dom (Sq_Circ|K1)=dom Sq_Circ /\ K1 by RELAT_1:61
      .=((the carrier of TOP-REAL 2))/\ K1 by FUNCT_2:def 1
      .=K1 by XBOOLE_1:28;
A3: the carrier of (TOP-REAL 2)|K1=K1 by PRE_TOPC:8;
    assume
A4: p in the carrier of (TOP-REAL 2)|K1;
    then ex p3 being Point of TOP-REAL 2 st p=p3 &( p3`1<=p3`2 & - p3`2<=p3`1
    or p3`1>=p3`2 & p3`1<=-p3`2)& p3<>0.TOP-REAL 2 by A1,A3;
    then
A5: Sq_Circ.p=|[p`1/sqrt(1+(p`1/p`2)^2), p`2/sqrt(1+(p`1/p`2)^2)]| by Th4;
    (Sq_Circ|K1).p=Sq_Circ.p by A4,A3,FUNCT_1:49;
    then
    g1.p=(proj2).(|[p`1/sqrt(1+(p`1/p`2)^2),p`2/sqrt(1+(p`1/p`2)^2)]|) by A4,A2
,A3,A5,FUNCT_1:13
      .=(|[p`1/sqrt(1+(p`1/p`2)^2), p`2/sqrt(1+(p`1/p`2)^2)]|)`2 by
PSCOMP_1:def 6
      .=p`2/sqrt(1+(p`1/p`2)^2) by EUCLID:52;
    hence thesis;
  end;
  then consider f1 being Function of (TOP-REAL 2)|K1,R^1 such that
A6: for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)
  |K1 holds f1.p=p`2/sqrt(1+(p`1/p`2)^2);
  dom ((proj1)*(Sq_Circ|K1))=the carrier of (TOP-REAL 2)|K1 & rng ((proj1)
  *( Sq_Circ|K1)) c= the carrier of R^1 by Lm12,TOPMETR:17;
  then reconsider
  g2=(proj1)*(Sq_Circ|K1) as Function of (TOP-REAL 2)|K1,R^1 by FUNCT_2:2;
  for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)|K1
  holds g2.p=p`1/sqrt(1+(p`1/p`2)^2)
  proof
    let p be Point of TOP-REAL 2;
A7: dom (Sq_Circ|K1)=dom Sq_Circ /\ K1 by RELAT_1:61
      .=((the carrier of TOP-REAL 2))/\ K1 by FUNCT_2:def 1
      .=K1 by XBOOLE_1:28;
A8: the carrier of (TOP-REAL 2)|K1=K1 by PRE_TOPC:8;
    assume
A9: p in the carrier of (TOP-REAL 2)|K1;
    then ex p3 being Point of TOP-REAL 2 st p=p3 &( p3`1<=p3`2 & - p3`2<=p3`1
    or p3`1>=p3`2 & p3`1<=-p3`2)& p3<>0.TOP-REAL 2 by A1,A8;
    then
A10: Sq_Circ.p=|[p`1/sqrt(1+(p`1/p`2)^2), p`2/sqrt(1+(p`1/p`2)^2)]| by Th4;
    (Sq_Circ|K1).p=Sq_Circ.p by A9,A8,FUNCT_1:49;
    then
    g2.p=(proj1).(|[p`1/sqrt(1+(p`1/p`2)^2),p`2/sqrt(1+(p`1/p`2)^2)]|) by A9,A7
,A8,A10,FUNCT_1:13
      .=(|[p`1/sqrt(1+(p`1/p`2)^2), p`2/sqrt(1+(p`1/p`2)^2)]|)`1 by
PSCOMP_1:def 5
      .=p`1/sqrt(1+(p`1/p`2)^2) by EUCLID:52;
    hence thesis;
  end;
  then consider f2 being Function of (TOP-REAL 2)|K1,R^1 such that
A11: for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)
  |K1 holds f2.p=p`1/sqrt(1+(p`1/p`2)^2);
A12: for q being Point of TOP-REAL 2 st q in the carrier of (TOP-REAL 2)|K1
  holds q`2<>0
  proof
    let q be Point of TOP-REAL 2;
A13: the carrier of (TOP-REAL 2)|K1=K1 by PRE_TOPC:8;
    assume q in the carrier of (TOP-REAL 2)|K1;
    then
A14: ex p3 being Point of TOP-REAL 2 st q=p3 &( p3`1<=p3`2 & - p3`2<=p3`1 or
    p3`1>=p3`2 & p3`1<=-p3`2)& p3<>0.TOP-REAL 2 by A1,A13;
    now
      assume
A15:  q`2=0;
      then q`1=0 by A14;
      hence contradiction by A14,A15,EUCLID:53,54;
    end;
    hence thesis;
  end;
  then
A16: f1 is continuous by A6,Th13;
A17: now
    let x,y,s,r be real number;
    assume that
A18: |[x,y]| in K1 and
A19: s=f2.(|[x,y]|) & r=f1.(|[x,y]|);
    set p99=|[x,y]|;
A20: ex p3 being Point of TOP-REAL 2 st p99=p3 &( p3`1<=p3`2 & -p3`2<=p3`1
    or p3`1>=p3`2 & p3`1<=-p3`2)& p3<>0.TOP-REAL 2 by A1,A18;
A21: the carrier of (TOP-REAL 2)|K1=K1 by PRE_TOPC:8;
    then
A22: f1.p99=p99`2/sqrt(1+(p99`1/p99`2)^2) by A6,A18;
    (Sq_Circ|K0).(|[x,y]|)=(Sq_Circ).(|[x,y]|) by A18,FUNCT_1:49
      .= |[p99`1/sqrt(1+(p99`1/p99`2)^2), p99`2/sqrt(1+(p99`1/p99`2)^2)]| by
A20,Th4
      .=|[s,r]| by A11,A18,A19,A21,A22;
    hence f.(|[x,y]|)=|[s,r]| by A1;
  end;
  f2 is continuous by A12,A11,Th14;
  hence thesis by A1,A16,A17,Lm13,JGRAPH_2:35;
end;

scheme
  TopIncl { P[set] } : { p: P[p] & p<>0.TOP-REAL 2 } c= NonZero TOP-REAL 2
proof
  let x be set;
  assume x in { p: P[p] & p<>0.TOP-REAL 2 };
  then
A1: ex p8 being Point of TOP-REAL 2 st x=p8 &( P[p8])& p8<> 0.TOP-REAL 2;
  then not x in {0.TOP-REAL 2} by TARSKI:def 1;
  hence thesis by A1,XBOOLE_0:def 5;
end;

scheme
  TopInter { P[set] } : { p: P[p] & p<>0.TOP-REAL 2 } = { p7 where p7 is Point
  of TOP-REAL 2 : P[p7]} /\ (NonZero TOP-REAL 2) proof
  set B0 = NonZero TOP-REAL 2;
  set K1 = { p7 where p7 is Point of TOP-REAL 2 : P[p7]};
  set K0 = { p: P[p] & p<>0.TOP-REAL 2 };
A1: K1 /\ B0 c= K0
  proof
    let x be set;
    assume
A2: x in K1 /\ B0;
    then x in B0 by XBOOLE_0:def 4;
    then not x in {0.TOP-REAL 2} by XBOOLE_0:def 5;
    then
A3: x <> 0.TOP-REAL 2 by TARSKI:def 1;
    x in K1 by A2,XBOOLE_0:def 4;
    then ex p7 being Point of TOP-REAL 2 st p7=x & P[p7];
    hence thesis by A3;
  end;
  K0 c= K1 /\ B0
  proof
    let x be set;
    assume x in K0;
    then
A4: ex p being Point of TOP-REAL 2 st x=p &( P[p])& p<>0. TOP-REAL 2;
    then not x in {0.TOP-REAL 2} by TARSKI:def 1;
    then
A5: x in B0 by A4,XBOOLE_0:def 5;
    x in K1 by A4;
    hence thesis by A5,XBOOLE_0:def 4;
  end;
  hence thesis by A1,XBOOLE_0:def 10;
end;

theorem Th17:
  for B0 being Subset of TOP-REAL 2,K0 being Subset of (TOP-REAL 2
)|B0,f being Function of ((TOP-REAL 2)|B0)|K0,((TOP-REAL 2)|B0) st f=Sq_Circ|K0
& B0=NonZero TOP-REAL 2 & K0={p:(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1)
  & p<>0.TOP-REAL 2} holds f is continuous & K0 is closed
proof
  reconsider K5={p7 where p7 is Point of TOP-REAL 2:p7`2<=-p7`1 } as closed
  Subset of TOP-REAL 2 by JGRAPH_2:47;
  reconsider K4={p7 where p7 is Point of TOP-REAL 2: p7`1<=p7`2 } as closed
  Subset of TOP-REAL 2 by JGRAPH_2:46;
  reconsider K3={p7 where p7 is Point of TOP-REAL 2: -p7`1<=p7`2 } as closed
  Subset of TOP-REAL 2 by JGRAPH_2:47;
  reconsider K2={p7 where p7 is Point of TOP-REAL 2: p7`2<=p7`1 } as closed
  Subset of TOP-REAL 2 by JGRAPH_2:46;
  defpred P[Point of TOP-REAL 2] means ($1`2<=$1`1 & -$1`1<=$1`2 or $1`2>=$1`1
  & $1`2<=-$1`1);
  let B0 be Subset of TOP-REAL 2,K0 be Subset of (TOP-REAL 2)|B0,f being
  Function of ((TOP-REAL 2)|B0)|K0,((TOP-REAL 2)|B0);
  assume
A1: f=Sq_Circ|K0 & B0=NonZero TOP-REAL 2 & K0={p:(p`2<=p`1 & -p`1<=p`2
  or p`2>=p`1 & p`2<=-p`1) & p<>0.TOP-REAL 2};
  the carrier of (TOP-REAL 2)|B0 = B0 by PRE_TOPC:8;
  then reconsider K1=K0 as Subset of TOP-REAL 2 by XBOOLE_1:1;
  {p:P[p] & p<>0.TOP-REAL 2} c= NonZero TOP-REAL 2 from TopIncl;
  then
A2: ((TOP-REAL 2)|B0)|K0=(TOP-REAL 2)|K1 by A1,PRE_TOPC:7;
  defpred P[Point of TOP-REAL 2] means ($1`2<=$1`1 & -$1`1<=$1`2 or $1`2>=$1`1
  & $1`2<=-$1`1);
  reconsider K1={p7 where p7 is Point of TOP-REAL 2: P[p7]} as Subset of
  TOP-REAL 2 from JGRAPH_2:sch 1;
  defpred P[Point of TOP-REAL 2] means ($1`2<=$1`1 & -$1`1<=$1`2 or $1`2>=$1`1
  & $1`2<=-$1`1);
  {p: P[p] & p<>0.TOP-REAL 2} = {p7 where p7 is Point of TOP-REAL 2: P[p7
  ]} /\ (NonZero TOP-REAL 2) from TopInter;
  then
A3: K0=K1 /\ [#]((TOP-REAL 2)|B0) by A1,PRE_TOPC:def 5;
A4: K2 /\ K3 \/ K4 /\ K5 c= K1
  proof
    let x be set;
    assume
A5: x in K2 /\ K3 \/ K4 /\ K5;
    per cases by A5,XBOOLE_0:def 3;
    suppose
A6:   x in K2 /\ K3;
      then x in K3 by XBOOLE_0:def 4;
      then
A7:   ex p8 being Point of TOP-REAL 2 st p8=x & -p8`1<=p8`2;
      x in K2 by A6,XBOOLE_0:def 4;
      then ex p7 being Point of TOP-REAL 2 st p7=x & p7`2<=(p7`1);
      hence thesis by A7;
    end;
    suppose
A8:   x in K4 /\ K5;
      then x in K5 by XBOOLE_0:def 4;
      then
A9:   ex p8 being Point of TOP-REAL 2 st p8=x & p8`2<= -p8`1;
      x in K4 by A8,XBOOLE_0:def 4;
      then ex p7 being Point of TOP-REAL 2 st p7=x & p7`2>=(p7`1);
      hence thesis by A9;
    end;
  end;
A10: K2 /\ K3 is closed & K4 /\ K5 is closed by TOPS_1:8;
  K1 c= K2 /\ K3 \/ K4 /\ K5
  proof
    let x be set;
    assume x in K1;
    then ex p being Point of TOP-REAL 2 st p=x &( p`2<=p`1 & -p`1 <=p`2 or p`2
    >=p`1 & p`2<=-p`1);
    then x in K2 & x in K3 or x in K4 & x in K5;
    then x in K2 /\ K3 or x in K4 /\ K5 by XBOOLE_0:def 4;
    hence thesis by XBOOLE_0:def 3;
  end;
  then K1=K2 /\ K3 \/ K4 /\ K5 by A4,XBOOLE_0:def 10;
  then K1 is closed by A10,TOPS_1:9;
  hence thesis by A1,A2,A3,Th15,PRE_TOPC:13;
end;

theorem Th18:
  for B0 being Subset of TOP-REAL 2,K0 being Subset of (TOP-REAL 2
)|B0,f being Function of ((TOP-REAL 2)|B0)|K0,((TOP-REAL 2)|B0) st f=Sq_Circ|K0
& B0=NonZero TOP-REAL 2 & K0={p:(p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2)
  & p<>0.TOP-REAL 2} holds f is continuous & K0 is closed
proof
  reconsider K5={p7 where p7 is Point of TOP-REAL 2: p7`1<=-p7`2 } as closed
  Subset of TOP-REAL 2 by JGRAPH_2:48;
  reconsider K4={p7 where p7 is Point of TOP-REAL 2: p7`2<=p7`1 } as closed
  Subset of TOP-REAL 2 by JGRAPH_2:46;
  reconsider K3={p7 where p7 is Point of TOP-REAL 2: -p7`2<=p7`1 } as closed
  Subset of TOP-REAL 2 by JGRAPH_2:48;
  reconsider K2={p7 where p7 is Point of TOP-REAL 2: p7`1<=p7`2 } as closed
  Subset of TOP-REAL 2 by JGRAPH_2:46;
  defpred P[Point of TOP-REAL 2] means ($1`1<=$1`2 & -$1`2<=$1`1 or $1`1>=$1`2
  & $1`1<=-$1`2);
  set b0 = NonZero TOP-REAL 2;
  defpred P0[Point of TOP-REAL 2] means ($1`1<=$1`2 & -$1`2<=$1`1 or $1`1>=$1
  `2 & $1`1<=-$1`2);
  let B0 be Subset of TOP-REAL 2,K0 be Subset of (TOP-REAL 2)|B0,f being
  Function of ((TOP-REAL 2)|B0)|K0,((TOP-REAL 2)|B0);
  assume
A1: f=Sq_Circ|K0 & B0=NonZero TOP-REAL 2 & K0={p:(p`1<=p`2 & -p`2<=p`1
  or p`1>=p`2 & p`1<=-p`2) & p<>0.TOP-REAL 2};
  the carrier of (TOP-REAL 2)|B0= B0 by PRE_TOPC:8;
  then reconsider K1=K0 as Subset of TOP-REAL 2 by XBOOLE_1:1;
  {p:P[p] & p<>0.TOP-REAL 2} c= NonZero TOP-REAL 2 from TopIncl;
  then
A2: ((TOP-REAL 2)|B0)|K0=(TOP-REAL 2)|K1 by A1,PRE_TOPC:7;
  defpred P[Point of TOP-REAL 2] means ($1`1<=$1`2 & -$1`2<=$1`1 or $1`1>=$1`2
  & $1`1<=-$1`2);
  reconsider K1={p7 where p7 is Point of TOP-REAL 2:P[p7]} as Subset of
  TOP-REAL 2 from JGRAPH_2:sch 1;
A3: K2 /\ K3 \/ K4 /\ K5 c= K1
  proof
    let x be set;
    assume
A4: x in K2 /\ K3 \/ K4 /\ K5;
    per cases by A4,XBOOLE_0:def 3;
    suppose
A5:   x in K2 /\ K3;
      then x in K3 by XBOOLE_0:def 4;
      then
A6:   ex p8 being Point of TOP-REAL 2 st p8=x & -p8`2<=p8`1;
      x in K2 by A5,XBOOLE_0:def 4;
      then ex p7 being Point of TOP-REAL 2 st p7=x & p7`1<=(p7`2);
      hence thesis by A6;
    end;
    suppose
A7:   x in K4 /\ K5;
      then x in K5 by XBOOLE_0:def 4;
      then
A8:   ex p8 being Point of TOP-REAL 2 st p8=x & p8`1<= -p8`2;
      x in K4 by A7,XBOOLE_0:def 4;
      then ex p7 being Point of TOP-REAL 2 st p7=x & p7`1>=(p7`2);
      hence thesis by A8;
    end;
  end;
  set k0 = {p:P0[p] & p<>0.TOP-REAL 2};
A9: K2 /\ K3 is closed & K4 /\ K5 is closed by TOPS_1:8;
  K1 c= K2 /\ K3 \/ K4 /\ K5
  proof
    let x be set;
    assume x in K1;
    then ex p being Point of TOP-REAL 2 st p=x &( p`1<=p`2 & -p`2 <=p`1 or p`1
    >=p`2 & p`1<=-p`2);
    then x in K2 & x in K3 or x in K4 & x in K5;
    then x in K2 /\ K3 or x in K4 /\ K5 by XBOOLE_0:def 4;
    hence thesis by XBOOLE_0:def 3;
  end;
  then K1=K2 /\ K3 \/ K4 /\ K5 by A3,XBOOLE_0:def 10;
  then
A10: K1 is closed by A9,TOPS_1:9;
  k0 = {p7 where p7 is Point of TOP-REAL 2:P0[p7]} /\ b0 from TopInter;
  then K0=K1 /\ [#]((TOP-REAL 2)|B0) by A1,PRE_TOPC:def 5;
  hence thesis by A1,A2,A10,Th16,PRE_TOPC:13;
end;

theorem Th19:
  for D being non empty Subset of TOP-REAL 2 st D`={0.TOP-REAL 2}
  holds ex h being Function of (TOP-REAL 2)|D,(TOP-REAL 2)|D st h=Sq_Circ|D & h
  is continuous
proof
  set Y1=|[-1,1]|;
  let D be non empty Subset of TOP-REAL 2;
A1: the carrier of ((TOP-REAL 2)|D)=D by PRE_TOPC:8;
  dom Sq_Circ=(the carrier of (TOP-REAL 2)) by FUNCT_2:def 1;
  then
A2: dom (Sq_Circ|D)=(the carrier of (TOP-REAL 2))/\ D by RELAT_1:61
    .=the carrier of ((TOP-REAL 2)|D) by A1,XBOOLE_1:28;
  assume
A3: D`={0.TOP-REAL 2};
  then
A4: D = {0.TOP-REAL 2}` .=(NonZero TOP-REAL 2) by SUBSET_1:def 4;
A5: {p:(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) & p<>0.TOP-REAL 2} c=
  the carrier of (TOP-REAL 2)|D
  proof
    let x be set;
    assume
    x in {p:(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) & p<>0. TOP-REAL 2};
    then
A6: ex p st x=p &( p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p `1)& p<>0.
    TOP-REAL 2;
    now
      assume not x in D;
      then x in (the carrier of TOP-REAL 2) \ D by A6,XBOOLE_0:def 5;
      then x in D` by SUBSET_1:def 4;
      hence contradiction by A3,A6,TARSKI:def 1;
    end;
    hence thesis by PRE_TOPC:8;
  end;
  1.REAL 2 in {p where p is Point of TOP-REAL 2: (p`2<=p`1 & -p`1<=p`2 or
  p`2>=p`1 & p`2<=-p`1) & p<>0.TOP-REAL 2} by Lm9,Lm10;
  then reconsider K0={p:(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) & p<>0.
  TOP-REAL 2} as non empty Subset of (TOP-REAL 2)|D by A5;
A7: K0=the carrier of ((TOP-REAL 2)|D)|K0 by PRE_TOPC:8;
A8: {p:(p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2) & p<>0.TOP-REAL 2} c=
  the carrier of (TOP-REAL 2)|D
  proof
    let x be set;
    assume
    x in {p:(p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2) & p<>0. TOP-REAL 2};
    then
A9: ex p st x=p &( p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p `2)& p<>0.
    TOP-REAL 2;
    now
      assume not x in D;
      then x in (the carrier of TOP-REAL 2) \ D by A9,XBOOLE_0:def 5;
      then x in D` by SUBSET_1:def 4;
      hence contradiction by A3,A9,TARSKI:def 1;
    end;
    hence thesis by PRE_TOPC:8;
  end;
  Y1`1=-1 & Y1`2=1 by EUCLID:52;
  then
  Y1 in {p where p is Point of TOP-REAL 2: (p`1<=p`2 & -p`2<=p`1 or p`1>=
  p`2 & p`1<=-p`2) & p<>0.TOP-REAL 2} by JGRAPH_2:3;
  then reconsider K1={p:(p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2) & p<>0.
  TOP-REAL 2} as non empty Subset of (TOP-REAL 2)|D by A8;
A10: K1=the carrier of ((TOP-REAL 2)|D)|K1 by PRE_TOPC:8;
A11: D c= K0 \/ K1
  proof
    let x be set;
    assume
A12: x in D;
    then reconsider px=x as Point of TOP-REAL 2;
    not x in {0.TOP-REAL 2} by A4,A12,XBOOLE_0:def 5;
    then (px`2<=px`1 & -px`1<=px`2 or px`2>=px`1 & px`2<=-px`1) & px<>0.
TOP-REAL 2 or (px`1<=px`2 & -px`2<=px`1 or px`1>=px`2 & px`1<=-px`2) & px<>0.
    TOP-REAL 2 by TARSKI:def 1,XREAL_1:26;
    then x in K0 or x in K1;
    hence thesis by XBOOLE_0:def 3;
  end;
A13: the carrier of ((TOP-REAL 2)|D) =[#](((TOP-REAL 2)|D))
    .=(NonZero TOP-REAL 2) by A4,PRE_TOPC:def 5;
A14: the carrier of ((TOP-REAL 2)|D) =D by PRE_TOPC:8;
A15: rng (Sq_Circ|K0) c= the carrier of ((TOP-REAL 2)|D)|K0
  proof
    reconsider K00=K0 as Subset of TOP-REAL 2 by A14,XBOOLE_1:1;
    let y be set;
A16: for q being Point of TOP-REAL 2 st q in the carrier of (TOP-REAL 2)|
    K00 holds q`1<>0
    proof
      let q be Point of TOP-REAL 2;
A17:  the carrier of (TOP-REAL 2)|K00=K0 by PRE_TOPC:8;
      assume q in the carrier of (TOP-REAL 2)|K00;
      then
A18:  ex p3 being Point of TOP-REAL 2 st q=p3 &( p3`2<=p3`1 & - p3`1<=p3`2
      or p3`2>=p3`1 & p3`2<=-p3`1)& p3<>0.TOP-REAL 2 by A17;
      now
        assume
A19:    q`1=0;
        then q`2=0 by A18;
        hence contradiction by A18,A19,EUCLID:53,54;
      end;
      hence thesis;
    end;
    assume y in rng (Sq_Circ|K0);
    then consider x being set such that
A20: x in dom (Sq_Circ|K0) and
A21: y=(Sq_Circ|K0).x by FUNCT_1:def 3;
A22: x in (dom Sq_Circ) /\ K0 by A20,RELAT_1:61;
    then
A23: x in K0 by XBOOLE_0:def 4;
    K0 c= the carrier of TOP-REAL 2 by A14,XBOOLE_1:1;
    then reconsider p=x as Point of TOP-REAL 2 by A23;
    K00=the carrier of ((TOP-REAL 2)|K00) by PRE_TOPC:8;
    then p in the carrier of ((TOP-REAL 2)|K00) by A22,XBOOLE_0:def 4;
    then
A24: p`1<>0 by A16;
A25: ex px being Point of TOP-REAL 2 st x=px &( px`2<=px`1 & - px`1<=px`2
    or px`2>=px`1 & px`2<=-px`1)& px<>0.TOP-REAL 2 by A23;
    then
A26: Sq_Circ.p=|[p`1/sqrt(1+(p`2/p`1)^2), p`2/sqrt(1+(p`2/p`1)^2)]| by Def1;
A27: sqrt(1+(p`2/p`1)^2)>0 by Lm1,SQUARE_1:25;
    then
    p`2/sqrt(1+(p`2/p`1)^2)<=p`1/sqrt(1+(p`2/p`1)^2) & (-p`1)/sqrt(1+(p`2
/p`1)^2)<=p`2/sqrt(1+(p`2/p`1)^2) or p`2/sqrt(1+(p`2/p`1)^2)>=p`1/sqrt(1+(p`2/p
    `1)^2) & p`2/sqrt(1+(p`2/p`1)^2)<=(-p`1)/sqrt(1+(p`2/p`1)^2) by A25,
XREAL_1:72;
    then
A28: p`2/sqrt(1+(p`2/p`1)^2)<=p`1/sqrt(1+(p`2/p`1)^2) & -(p`1/sqrt(1+(p`2/
p`1)^2))<=p`2/sqrt(1+(p`2/p`1)^2) or p`2/sqrt(1+(p`2/p`1)^2)>=p`1/sqrt(1+(p`2/p
    `1)^2) & p`2/sqrt(1+(p`2/p`1)^2)<=-(p`1/sqrt(1+(p`2/p`1)^2)) by
XCMPLX_1:187;
    set p9=|[p`1/sqrt(1+(p`2/p`1)^2),p`2/sqrt(1+(p`2/p`1)^2)]|;
A29: p9`1=p`1/sqrt(1+(p`2/p`1)^2) & p9`2=p`2/sqrt(1+(p`2/p`1)^2) by EUCLID:52;
A30: p9`1=p`1/sqrt(1+(p`2/p`1)^2) by EUCLID:52;
A31: now
      assume p9=0.TOP-REAL 2;
      then 0 *sqrt(1+(p`2/p`1)^2)=p`1/sqrt(1+(p`2/p`1)^2)*sqrt(1+(p`2/p`1)^2)
      by A30,EUCLID:52,54;
      hence contradiction by A24,A27,XCMPLX_1:87;
    end;
    Sq_Circ.p=y by A21,A23,FUNCT_1:49;
    then y in K0 by A31,A26,A28,A29;
    then y in [#](((TOP-REAL 2)|D)|K0) by PRE_TOPC:def 5;
    hence thesis;
  end;
A32: K0 c= the carrier of TOP-REAL 2
  proof
    let z be set;
    assume z in K0;
    then ex p8 being Point of TOP-REAL 2 st p8=z &( p8`2<=p8`1 & - p8`1<=p8`2
    or p8`2>=p8`1 & p8`2<=-p8`1)& p8<>0.TOP-REAL 2;
    hence thesis;
  end;
  dom (Sq_Circ|K0)= dom (Sq_Circ) /\ K0 by RELAT_1:61
    .=((the carrier of TOP-REAL 2)) /\ K0 by FUNCT_2:def 1
    .=K0 by A32,XBOOLE_1:28;
  then reconsider
  f=Sq_Circ|K0 as Function of ((TOP-REAL 2)|D)|K0, (TOP-REAL 2)|D
  by A7,A15,FUNCT_2:2,XBOOLE_1:1;
A33: K1=[#](((TOP-REAL 2)|D)|K1) by PRE_TOPC:def 5;
A34: K1 c= the carrier of TOP-REAL 2
  proof
    let z be set;
    assume z in K1;
    then ex p8 being Point of TOP-REAL 2 st p8=z &( p8`1<=p8`2 & - p8`2<=p8`1
    or p8`1>=p8`2 & p8`1<=-p8`2)& p8<>0.TOP-REAL 2;
    hence thesis;
  end;
A35: rng (Sq_Circ|K1) c= the carrier of ((TOP-REAL 2)|D)|K1
  proof
    reconsider K10=K1 as Subset of TOP-REAL 2 by A34;
    let y be set;
A36: for q being Point of TOP-REAL 2 st q in the carrier of (TOP-REAL 2)|
    K10 holds q`2<>0
    proof
      let q be Point of TOP-REAL 2;
A37:  the carrier of (TOP-REAL 2)|K10=K1 by PRE_TOPC:8;
      assume q in the carrier of (TOP-REAL 2)|K10;
      then
A38:  ex p3 being Point of TOP-REAL 2 st q=p3 &( p3`1<=p3`2 & - p3`2<=p3`1
      or p3`1>=p3`2 & p3`1<=-p3`2)& p3<>0.TOP-REAL 2 by A37;
      now
        assume
A39:    q`2=0;
        then q`1=0 by A38;
        hence contradiction by A38,A39,EUCLID:53,54;
      end;
      hence thesis;
    end;
    assume y in rng (Sq_Circ|K1);
    then consider x being set such that
A40: x in dom (Sq_Circ|K1) and
A41: y=(Sq_Circ|K1).x by FUNCT_1:def 3;
A42: x in (dom Sq_Circ) /\ K1 by A40,RELAT_1:61;
    then
A43: x in K1 by XBOOLE_0:def 4;
    then reconsider p=x as Point of TOP-REAL 2 by A34;
    K10=the carrier of ((TOP-REAL 2)|K10) by PRE_TOPC:8;
    then p in the carrier of ((TOP-REAL 2)|K10) by A42,XBOOLE_0:def 4;
    then
A44: p`2<>0 by A36;
    set p9=|[p`1/sqrt(1+(p`1/p`2)^2),p`2/sqrt(1+(p`1/p`2)^2)]|;
A45: p9`2=p`2/sqrt(1+(p`1/p`2)^2) & p9`1=p`1/sqrt(1+(p`1/p`2)^2) by EUCLID:52;
A46: ex px being Point of TOP-REAL 2 st x=px &( px`1<=px`2 & - px`2<=px`1
    or px`1>=px`2 & px`1<=-px`2)& px<>0.TOP-REAL 2 by A43;
    then
A47: Sq_Circ.p=|[p`1/sqrt(1+(p`1/p`2)^2), p`2/sqrt(1+(p`1/p`2)^2)]| by Th4;
A48: sqrt(1+(p`1/p`2)^2)>0 by Lm1,SQUARE_1:25;
    then
    p`1/sqrt(1+(p`1/p`2)^2)<=p`2/sqrt(1+(p`1/p`2)^2) & (-p`2)/sqrt(1+(p`1
/p`2)^2)<=p`1/sqrt(1+(p`1/p`2)^2) or p`1/sqrt(1+(p`1/p`2)^2)>=p`2/sqrt(1+(p`1/p
    `2)^2) & p`1/sqrt(1+(p`1/p`2)^2)<=(-p`2)/sqrt(1+(p`1/p`2)^2) by A46,
XREAL_1:72;
    then
A49: p`1/sqrt(1+(p`1/p`2)^2)<=p`2/sqrt(1+(p`1/p`2)^2) & -(p`2/sqrt(1+(p`1/
p`2)^2))<=p`1/sqrt(1+(p`1/p`2)^2) or p`1/sqrt(1+(p`1/p`2)^2)>=p`2/sqrt(1+(p`1/p
    `2)^2) & p`1/sqrt(1+(p`1/p`2)^2)<=-(p`2/sqrt(1+(p`1/p`2)^2)) by
XCMPLX_1:187;
A50: p9`2=p`2/sqrt(1+(p`1/p`2)^2) by EUCLID:52;
A51: now
      assume p9=0.TOP-REAL 2;
      then 0 *sqrt(1+(p`1/p`2)^2)=p`2/sqrt(1+(p`1/p`2)^2)*sqrt(1+(p`1/p`2)^2)
      by A50,EUCLID:52,54;
      hence contradiction by A44,A48,XCMPLX_1:87;
    end;
    Sq_Circ.p=y by A41,A43,FUNCT_1:49;
    then y in K1 by A51,A47,A49,A45;
    hence thesis by PRE_TOPC:8;
  end;
  dom (Sq_Circ|K1)= dom (Sq_Circ) /\ K1 by RELAT_1:61
    .=((the carrier of TOP-REAL 2)) /\ K1 by FUNCT_2:def 1
    .=K1 by A34,XBOOLE_1:28;
  then reconsider
  g=Sq_Circ|K1 as Function of ((TOP-REAL 2)|D)|K1, ((TOP-REAL 2)|D)
  by A10,A35,FUNCT_2:2,XBOOLE_1:1;
A52: dom g=K1 by A10,FUNCT_2:def 1;
  g=Sq_Circ|K1;
  then
A53: K1 is closed by A4,Th18;
A54: K0=[#](((TOP-REAL 2)|D)|K0) by PRE_TOPC:def 5;
A55: for x be set st x in ([#](((TOP-REAL 2)|D)|K0)) /\ ([#] (((TOP-REAL 2)
  |D)|K1)) holds f.x = g.x
  proof
    let x be set;
    assume
A56: x in ([#](((TOP-REAL 2)|D)|K0)) /\ [#] (((TOP-REAL 2)|D)|K1);
    then x in K0 by A54,XBOOLE_0:def 4;
    then f.x=Sq_Circ.x by FUNCT_1:49;
    hence thesis by A33,A56,FUNCT_1:49;
  end;
  f=Sq_Circ|K0;
  then
A57: K0 is closed by A4,Th17;
A58: dom f=K0 by A7,FUNCT_2:def 1;
  D= [#]((TOP-REAL 2)|D) by PRE_TOPC:def 5;
  then
A59: ([#](((TOP-REAL 2)|D)|K0)) \/ ([#](((TOP-REAL 2)|D)|K1)) = [#]((
  TOP-REAL 2)|D) by A54,A33,A11,XBOOLE_0:def 10;
A60: f is continuous & g is continuous by A4,Th17,Th18;
  then consider h being Function of (TOP-REAL 2)|D,(TOP-REAL 2)|D such that
A61: h= f+*g and
  h is continuous by A54,A33,A59,A57,A53,A55,JGRAPH_2:1;
  K0=[#](((TOP-REAL 2)|D)|K0) & K1=[#](((TOP-REAL 2)|D)|K1) by PRE_TOPC:def 5;
  then
A62: f tolerates g by A55,A58,A52,PARTFUN1:def 4;
A63: for x being set st x in dom h holds h.x=(Sq_Circ|D).x
  proof
    let x be set;
    assume
A64: x in dom h;
    then reconsider p=x as Point of TOP-REAL 2 by A13,XBOOLE_0:def 5;
    not x in {0.TOP-REAL 2} by A13,A64,XBOOLE_0:def 5;
    then
A65: x <>0.TOP-REAL 2 by TARSKI:def 1;
    x in (the carrier of TOP-REAL 2)\D` by A3,A13,A64;
    then
A66: x in D`` by SUBSET_1:def 4;
    per cases;
    suppose
A67:  x in K0;
A68:  Sq_Circ|D.p=Sq_Circ.p by A66,FUNCT_1:49
        .=f.p by A67,FUNCT_1:49;
      h.p=(g+*f).p by A61,A62,FUNCT_4:34
        .=f.p by A58,A67,FUNCT_4:13;
      hence thesis by A68;
    end;
    suppose
      not x in K0;
      then not (p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) by A65;
      then p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2 by XREAL_1:26;
      then
A69:  x in K1 by A65;
      Sq_Circ|D.p=Sq_Circ.p by A66,FUNCT_1:49
        .=g.p by A69,FUNCT_1:49;
      hence thesis by A61,A52,A69,FUNCT_4:13;
    end;
  end;
  dom h=the carrier of ((TOP-REAL 2)|D) by FUNCT_2:def 1;
  then f+*g=Sq_Circ|D by A61,A2,A63,FUNCT_1:2;
  hence thesis by A54,A33,A59,A57,A60,A53,A55,JGRAPH_2:1;
end;

theorem Th20:
  for D being non empty Subset of TOP-REAL 2 st D=NonZero TOP-REAL
  2 holds D`= {0.TOP-REAL 2}
proof
  let D be non empty Subset of TOP-REAL 2;
  assume
A1: D=NonZero TOP-REAL 2;
A2: D` c= {0.TOP-REAL 2}
  proof
    let x be set;
    assume
A3: x in D`;
    then x in (the carrier of TOP-REAL 2)\D by SUBSET_1:def 4;
    then not x in D by XBOOLE_0:def 5;
    hence thesis by A1,A3,XBOOLE_0:def 5;
  end;
  {0.TOP-REAL 2} c= D`
  proof
    let x be set;
    assume
A4: x in {0.TOP-REAL 2};
    then not x in D by A1,XBOOLE_0:def 5;
    then x in (the carrier of TOP-REAL 2)\D by A4,XBOOLE_0:def 5;
    hence thesis by SUBSET_1:def 4;
  end;
  hence thesis by A2,XBOOLE_0:def 10;
end;

Lm16: the TopStruct of TOP-REAL 2 = TopSpaceMetr Euclid 2 by EUCLID:def 8;

theorem Th21:
  ex h being Function of TOP-REAL 2, TOP-REAL 2 st h=Sq_Circ & h is continuous
proof
  reconsider D=NonZero TOP-REAL 2 as non empty Subset of TOP-REAL 2 by
JGRAPH_2:9;
  reconsider f=Sq_Circ as Function of (TOP-REAL 2),(TOP-REAL 2);
A1: for p being Point of (TOP-REAL 2)|D holds f.p<>f.(0.TOP-REAL 2)
  proof
    let p be Point of (TOP-REAL 2)|D;
A2: [#]((TOP-REAL 2)|D)=D by PRE_TOPC:def 5;
    then reconsider q=p as Point of TOP-REAL 2 by XBOOLE_0:def 5;
    not p in {0.TOP-REAL 2} by A2,XBOOLE_0:def 5;
    then
A3: not p=0.TOP-REAL 2 by TARSKI:def 1;
    per cases;
    suppose
A4:   not(q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2<=-q`1);
then A5:   q`2<>0;
      set q9=|[q`1/sqrt(1+(q`1/q`2)^2),q`2/sqrt(1+(q`1/q`2)^2)]|;
A6:   q9`2=q`2/sqrt(1+(q`1/q`2)^2) by EUCLID:52;
A7:   sqrt(1+(q`1/q`2)^2)>0 by Lm1,SQUARE_1:25;
A8:   now
        assume q9=0.TOP-REAL 2;
        then
        0 *sqrt(1+(q`1/q`2)^2)=q`2/sqrt(1+(q`1/q`2)^2)*sqrt(1+(q`1/q`2)^2
        ) by A6,EUCLID:52,54;
        hence contradiction by A5,A7,XCMPLX_1:87;
      end;
      Sq_Circ.q=|[q`1/sqrt(1+(q`1/q`2)^2),q`2/sqrt(1+(q`1/q`2)^2)]| by A3,A4
,Def1;
      hence thesis by A8,Def1;
    end;
    suppose
A9:   q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2<=-q`1;
A10:  now
        assume
A11:    q`1=0;
        then q`2=0 by A9;
        hence contradiction by A3,A11,EUCLID:53,54;
      end;
      set q9=|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]|;
A12:  q9`1=q`1/sqrt(1+(q`2/q`1)^2) by EUCLID:52;
A13:  sqrt(1+(q`2/q`1)^2)>0 by Lm1,SQUARE_1:25;
A14:  now
        assume q9=0.TOP-REAL 2;
        then
        0 *sqrt(1+(q`2/q`1)^2)=q`1/sqrt(1+(q`2/q`1)^2)*sqrt(1+(q`2/q`1)^2
        ) by A12,EUCLID:52,54;
        hence contradiction by A10,A13,XCMPLX_1:87;
      end;
      Sq_Circ.q=|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]| by A3,A9
,Def1;
      hence thesis by A14,Def1;
    end;
  end;
A15: f.(0.TOP-REAL 2)=0.TOP-REAL 2 by Def1;
A16: for V being Subset of TOP-REAL 2 st f.(0.TOP-REAL 2) in V & V is open
  ex W being Subset of TOP-REAL 2 st 0.TOP-REAL 2 in W & W is open & f.:W c= V
  proof
    reconsider u0=0.TOP-REAL 2 as Point of Euclid 2 by EUCLID:67;
    let V be Subset of (TOP-REAL 2);
    reconsider VV=V as Subset of TopSpaceMetr Euclid 2 by Lm16;
    assume that
A17: f.(0.TOP-REAL 2) in V and
A18: V is open;
    VV is open by A18,Lm16,PRE_TOPC:30;
    then consider r being real number such that
A19: r>0 and
A20: Ball(u0,r) c= V by A15,A17,TOPMETR:15;
    reconsider r as Real by XREAL_0:def 1;
    reconsider W1=Ball(u0,r) as Subset of TOP-REAL 2 by EUCLID:67;
A21: W1 is open by GOBOARD6:3;
A22: f.:W1 c= W1
    proof
      let z be set;
      assume z in f.:W1;
      then consider y being set such that
A23:  y in dom f and
A24:  y in W1 and
A25:  z=f.y by FUNCT_1:def 6;
      z in rng f by A23,A25,FUNCT_1:def 3;
      then reconsider qz=z as Point of TOP-REAL 2;
      reconsider pz=qz as Point of Euclid 2 by EUCLID:67;
      reconsider q=y as Point of TOP-REAL 2 by A23;
      reconsider qy=q as Point of Euclid 2 by EUCLID:67;
      dist(u0,qy)<r by A24,METRIC_1:11;
      then |.(0.TOP-REAL 2) - q.|<r by JGRAPH_1:28;
      then sqrt((((0.TOP-REAL 2) - q)`1)^2+(((0.TOP-REAL 2) - q)`2)^2)<r by
JGRAPH_1:30;
      then sqrt(((0.TOP-REAL 2)`1 - q`1)^2+(((0.TOP-REAL 2) - q)`2)^2)<r by
TOPREAL3:3;
      then
A26:  sqrt(((0.TOP-REAL 2)`1 - q`1)^2+((0.TOP-REAL 2)`2 - q`2)^2)<r by
TOPREAL3:3;
      per cases;
      suppose
        q=0.TOP-REAL 2;
        hence thesis by A24,A25,Def1;
      end;
      suppose
A27:    q<>0.TOP-REAL 2 & (q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2<=-q `1);
A28:    (q`2)^2>=0 by XREAL_1:63;
        (q`2/q`1)^2 >=0 by XREAL_1:63;
        then 1+(q`2/q`1)^2>=1+0 by XREAL_1:7;
        then
A29:    sqrt(1+(q`2/q`1)^2)>=1 by SQUARE_1:18,26;
        then (sqrt(1+(q`2/q`1)^2))^2>=sqrt(1+(q`2/q`1)^2) by XREAL_1:151;
        then
A30:    1<=(sqrt(1+(q`2/q`1)^2))^2 by A29,XXREAL_0:2;
A31:    Sq_Circ.q=|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2 ) ]|
        by A27,Def1;
        then (qz`2)^2=(q`2/sqrt(1+(q`2/q`1)^2))^2 by A25,EUCLID:52
          .=(q`2)^2/(sqrt(1+(q`2/q`1)^2))^2 by XCMPLX_1:76;
        then
A32:    (qz`2)^2<=(q`2)^2/1 by A30,A28,XREAL_1:118;
A33:    (q`1)^2>=0 by XREAL_1:63;
        (qz`1)^2=(q`1/sqrt(1+(q`2/q`1)^2))^2 by A25,A31,EUCLID:52
          .=(q`1)^2/(sqrt(1+(q`2/q`1)^2))^2 by XCMPLX_1:76;
        then (qz`1)^2<=(q`1)^2/1 by A30,A33,XREAL_1:118;
        then
A34:    (qz`1)^2+(qz`2)^2<=(q`1)^2+(q`2)^2 by A32,XREAL_1:7;
        (qz`1)^2>=0 & (qz`2)^2>=0 by XREAL_1:63;
        then
A35:    sqrt((qz`1)^2+(qz`2)^2) <= sqrt((q`1)^2+(q`2)^2) by A34,SQUARE_1:26;
A36:    ((0.TOP-REAL 2) - qz)`2=(0.TOP-REAL 2)`2-qz`2 by TOPREAL3:3
          .= -qz`2 by JGRAPH_2:3;
        ((0.TOP-REAL 2) - qz)`1=(0.TOP-REAL 2)`1-qz`1 by TOPREAL3:3
          .= -qz`1 by JGRAPH_2:3;
        then sqrt((((0.TOP-REAL 2) - qz)`1)^2+(((0.TOP-REAL 2) - qz)`2)^2)<r
        by A26,A36,A35,JGRAPH_2:3,XXREAL_0:2;
        then |.(0.TOP-REAL 2) - qz.|<r by JGRAPH_1:30;
        then dist(u0,pz)<r by JGRAPH_1:28;
        hence thesis by METRIC_1:11;
      end;
      suppose
A37:    q<>0.TOP-REAL 2 & not (q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2 <=-q`1);
A38:    (q`2)^2>=0 by XREAL_1:63;
        (q`1/q`2)^2 >=0 by XREAL_1:63;
        then 1+(q`1/q`2)^2>=1+0 by XREAL_1:7;
        then
A39:    sqrt(1+(q`1/q`2)^2)>=1 by SQUARE_1:18,26;
        then (sqrt(1+(q`1/q`2)^2))^2>=sqrt(1+(q`1/q`2)^2) by XREAL_1:151;
        then
A40:    1<=(sqrt(1+(q`1/q`2)^2))^2 by A39,XXREAL_0:2;
A41:    Sq_Circ.q=|[q`1/sqrt(1+(q`1/q`2)^2),q`2/sqrt(1+(q`1/q`2)^2 ) ]|
        by A37,Def1;
        then (qz`2)^2=(q`2/sqrt(1+(q`1/q`2)^2))^2 by A25,EUCLID:52
          .=(q`2)^2/(sqrt(1+(q`1/q`2)^2))^2 by XCMPLX_1:76;
        then
A42:    (qz`2)^2<=(q`2)^2/1 by A40,A38,XREAL_1:118;
A43:    (q`1)^2>=0 by XREAL_1:63;
        (qz`1)^2=(q`1/sqrt(1+(q`1/q`2)^2))^2 by A25,A41,EUCLID:52
          .=(q`1)^2/(sqrt(1+(q`1/q`2)^2))^2 by XCMPLX_1:76;
        then (qz`1)^2<=(q`1)^2/1 by A40,A43,XREAL_1:118;
        then
A44:    (qz`1)^2+(qz`2)^2<=(q`1)^2+(q`2)^2 by A42,XREAL_1:7;
        (qz`1)^2>=0 & (qz`2)^2>=0 by XREAL_1:63;
        then
A45:    sqrt((qz`1)^2+(qz`2)^2) <= sqrt((q`1)^2+(q`2)^2) by A44,SQUARE_1:26;
A46:    ((0.TOP-REAL 2) - qz)`2=(0.TOP-REAL 2)`2-qz`2 by TOPREAL3:3
          .= -qz`2 by JGRAPH_2:3;
        ((0.TOP-REAL 2) - qz)`1=(0.TOP-REAL 2)`1-qz`1 by TOPREAL3:3
          .= -qz`1 by JGRAPH_2:3;
        then sqrt((((0.TOP-REAL 2) - qz)`1)^2+(((0.TOP-REAL 2) - qz)`2)^2)<r
        by A26,A46,A45,JGRAPH_2:3,XXREAL_0:2;
        then |.(0.TOP-REAL 2) - qz.|<r by JGRAPH_1:30;
        then dist(u0,pz)<r by JGRAPH_1:28;
        hence thesis by METRIC_1:11;
      end;
    end;
    u0 in W1 by A19,GOBOARD6:1;
    hence thesis by A20,A21,A22,XBOOLE_1:1;
  end;
A47: D`= {0.TOP-REAL 2} by Th20;
  then
  ex h being Function of (TOP-REAL 2)|D,(TOP-REAL 2)|D st h=Sq_Circ|D & h
  is continuous by Th19;
  hence thesis by A15,A47,A1,A16,Th3;
end;

theorem Th22:
  Sq_Circ is one-to-one
proof
  let x1,x2 be set;
  assume that
A1: x1 in dom Sq_Circ and
A2: x2 in dom Sq_Circ and
A3: Sq_Circ.x1=Sq_Circ.x2;
  reconsider p2=x2 as Point of TOP-REAL 2 by A2;
  reconsider p1=x1 as Point of TOP-REAL 2 by A1;
  set q=p1,p=p2;
  per cases;
  suppose
A4: q=0.TOP-REAL 2;
    then
A5: Sq_Circ.q=0.TOP-REAL 2 by Def1;
    now
      per cases;
      case
        p=0.TOP-REAL 2;
        hence thesis by A4;
      end;
      case
A6:     p<>0.TOP-REAL 2 & (p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p `1);
        (p`2/p`1)^2 >=0 by XREAL_1:63;
        then 1+(p`2/p`1)^2>=1+0 by XREAL_1:7;
        then
A7:     sqrt(1+(p`2/p`1)^2)>=1 by SQUARE_1:18,26;
A8:     Sq_Circ.p=|[p`1/sqrt(1+(p`2/p`1)^2),p`2/sqrt(1+(p`2/p`1)^2)]| by A6
,Def1;
        then p`2/sqrt(1+(p`2/p`1)^2)=0 by A3,A5,EUCLID:52,JGRAPH_2:3;
        then
A9:     p`2= 0 *sqrt(1+(p`2/p`1)^2) by A7,XCMPLX_1:87
          .=0;
        p`1/sqrt(1+(p`2/p`1)^2)=0 by A3,A5,A8,EUCLID:52,JGRAPH_2:3;
        then p`1= 0 *sqrt(1+(p`2/p`1)^2) by A7,XCMPLX_1:87
          .=0;
        hence contradiction by A6,A9,EUCLID:53,54;
      end;
      case
A10:    p<>0.TOP-REAL 2 & not (p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2 <=-p`1);
        (p`1/p`2)^2 >=0 by XREAL_1:63;
        then 1+(p`1/p`2)^2>=1+0 by XREAL_1:7;
        then
A11:    sqrt(1+(p`1/p`2)^2)>=1 by SQUARE_1:18,26;
        Sq_Circ.p=|[p`1/sqrt(1+(p`1/p`2)^2),p`2/sqrt(1+(p`1/p`2)^2)]| by A10
,Def1;
        then p`2/sqrt(1+(p`1/p`2)^2)=0 by A3,A5,EUCLID:52,JGRAPH_2:3;
        then p`2= 0 *sqrt(1+(p`1/p`2)^2) by A11,XCMPLX_1:87
          .=0;
        hence contradiction by A10;
      end;
    end;
    hence thesis;
  end;
  suppose
A12: q<>0.TOP-REAL 2 & (q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2<=-q`1);
A13: sqrt(1+(q`2/q`1)^2)>0 by Lm1,SQUARE_1:25;
A14: Sq_Circ.q=|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]| by A12,Def1;
A15: (|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]|)`2 = q`2/sqrt(1+
    (q `2/q`1)^2) by EUCLID:52;
A16: 1+(q`2/q`1)^2>0 by Lm1;
A17: (|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]|)`1 = q`1/sqrt(1+
    (q `2/q`1)^2) by EUCLID:52;
    now
      per cases;
      case
A18:    p=0.TOP-REAL 2;
        (q`2/q`1)^2 >=0 by XREAL_1:63;
        then 1+(q`2/q`1)^2>=1+0 by XREAL_1:7;
        then
A19:    sqrt(1+(q`2/q`1)^2)>=1 by SQUARE_1:18,26;
A20:    Sq_Circ.p=0.TOP-REAL 2 by A18,Def1;
        then q`2/sqrt(1+(q`2/q`1)^2)=0 by A3,A14,EUCLID:52,JGRAPH_2:3;
        then
A21:    q`2= 0 *sqrt(1+(q`2/q`1)^2) by A19,XCMPLX_1:87
          .=0;
        q`1/sqrt(1+(q`2/q`1)^2)=0 by A3,A14,A20,EUCLID:52,JGRAPH_2:3;
        then q`1= 0 *sqrt(1+(q`2/q`1)^2) by A19,XCMPLX_1:87
          .=0;
        hence contradiction by A12,A21,EUCLID:53,54;
      end;
      case
A22:    p<>0.TOP-REAL 2 & (p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p `1);
        now
          assume
A23:      p`1=0;
          then p`2=0 by A22;
          hence contradiction by A22,A23,EUCLID:53,54;
        end;
        then
A24:    (p`1)^2>0 by SQUARE_1:12;
A25:    sqrt(1+(p`2/p`1)^2)>0 by Lm1,SQUARE_1:25;
A26:    1+(p`2/p`1)^2>0 by Lm1;
A27:    Sq_Circ.p=|[p`1/sqrt(1+(p`2/p`1)^2),p`2/sqrt(1+(p`2/p`1)^2 )]| by A22
,Def1;
        then
A28:    p
`2/sqrt(1+(p`2/p`1)^2)=q`2/sqrt(1+(q`2/q`1)^2) by A3,A14,A15,EUCLID:52;
        then (p`2)^2/(sqrt(1+(p`2/p`1)^2))^2=(q`2/sqrt(1+(q`2/q`1)^2))^2 by
XCMPLX_1:76;
        then (p`2)^2/(sqrt(1+(p`2/p`1)^2))^2=(q`2)^2/(sqrt(1+(q`2/q`1)^2))^2
        by XCMPLX_1:76;
        then (p`2)^2/(1+(p`2/p`1)^2)=(q`2)^2/(sqrt(1+(q`2/q`1)^2))^2 by A26,
SQUARE_1:def 2;
        then
A29:    (p`2)^2/(1+(p`2/p`1)^2)=(q`2)^2/(1+(q`2/q`1)^2) by A16,SQUARE_1:def 2;
A30:    p`1/sqrt(1+(p`2/p`1)^2)=q`1/sqrt(1+(q`2/q`1)^2) by A3,A14,A17,A27,
EUCLID:52;
        then (p`1)^2/(sqrt(1+(p`2/p`1)^2))^2=(q`1/sqrt(1+(q`2/q`1)^2))^2 by
XCMPLX_1:76;
        then (p`1)^2/(sqrt(1+(p`2/p`1)^2))^2=(q`1)^2/(sqrt(1+(q`2/q`1)^2))^2
        by XCMPLX_1:76;
        then (p`1)^2/(1+(p`2/p`1)^2)=(q`1)^2/(sqrt(1+(q`2/q`1)^2))^2 by A26,
SQUARE_1:def 2;
        then (p`1)^2/(1+(p`2/p`1)^2)=(q`1)^2/(1+(q`2/q`1)^2) by A16,
SQUARE_1:def 2;
        then (p`1)^2/(1+(p`2/p`1)^2)/(p`1)^2=(q`1)^2/(p`1)^2/(1+(q`2/q`1)^2)
        by XCMPLX_1:48;
        then (p`1)^2/(p`1)^2/(1+(p`2/p`1)^2)=(q`1)^2/(p`1)^2/(1+(q`2/q`1)^2)
        by XCMPLX_1:48;
        then 1/(1+(p`2/p`1)^2)=(q`1)^2/(p`1)^2/(1+(q`2/q`1)^2) by A24,
XCMPLX_1:60;
        then
A31:    1/(1+(p`2/p`1)^2)*(1+(q`2/q`1)^2)=(q`1)^2/(p`1)^2 by A16,XCMPLX_1:87;
        now
          assume
A32:      q`1=0;
          then q`2=0 by A12;
          hence contradiction by A12,A32,EUCLID:53,54;
        end;
        then
A33:    (q`1)^2>0 by SQUARE_1:12;
        now
          per cases;
          case
A34:        p`2=0;
            then (q`2)^2=0 by A16,A29,XCMPLX_1:50;
            then
A35:        q`2=0 by XCMPLX_1:6;
            then p=|[q`1,0]|by A3,A14,A27,A34,EUCLID:53,SQUARE_1:18;
            hence thesis by A35,EUCLID:53;
          end;
          case
            p`2<>0;
            then
A36:        (p`2)^2>0 by SQUARE_1:12;
            (p`2)^2/(1+(p`2/p`1)^2)/(p`2)^2=(q`2)^2/(p`2)^2/(1+(q`2/q`1)
            ^2) by A29,XCMPLX_1:48;
            then (p`2)^2/(p`2)^2/(1+(p`2/p`1)^2)=(q`2)^2/(p`2)^2/(1+(q`2/q`1)
            ^2) by XCMPLX_1:48;
            then 1/(1+(p`2/p`1)^2)=(q`2)^2/(p`2)^2/(1+(q`2/q`1)^2) by A36,
XCMPLX_1:60;
            then 1/(1+(p`2/p`1)^2)*(1+(q`2/q`1)^2)=(q`2)^2/(p`2)^2 by A16,
XCMPLX_1:87;
            then (q`1)^2/(q`1)^2/(p`1)^2=(q`2)^2/(p`2)^2/(q`1)^2 by A31,
XCMPLX_1:48;
            then 1/(p`1)^2=(q`2)^2/(p`2)^2/(q`1)^2 by A33,XCMPLX_1:60;
            then 1/(p`1)^2*(p`2)^2=(p`2)^2*((q`2)^2/(p`2)^2)/(q`1)^2 by
XCMPLX_1:74;
            then 1/(p`1)^2*(p`2)^2=(q`2)^2/(q`1)^2 by A36,XCMPLX_1:87;
            then (p`2)^2/(p`1)^2=(q`2)^2/(q`1)^2 by XCMPLX_1:99;
            then (p`2/p`1)^2=(q`2)^2/(q`1)^2 by XCMPLX_1:76;
            then
A37:        (1+(p`2/p`1)^2)=(1+(q`2/q`1)^2) by XCMPLX_1:76;
            then p`2=q`2/sqrt(1+(q`2/q`1)^2)*sqrt(1+(q`2/q`1)^2) by A28,A25,
XCMPLX_1:87;
            then
A38:        p`2=q`2 by A13,XCMPLX_1:87;
            p`1=q`1/sqrt(1+(q`2/q`1)^2)*sqrt(1+(q`2/q`1)^2) by A30,A25,A37,
XCMPLX_1:87;
            then p`1=q`1 by A13,XCMPLX_1:87;
            then p=|[q`1,q`2]|by A38,EUCLID:53;
            hence thesis by EUCLID:53;
          end;
        end;
        hence thesis;
      end;
      case
A39:    p<>0.TOP-REAL 2 & not (p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2 <=-p`1);
A40:    1+(p`1/p`2)^2>0 by Lm1;
A41:    p<>0.TOP-REAL 2 & p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2 by A39,
JGRAPH_2:13;
        p`2<>0 by A39;
        then
A42:    (p`2)^2>0 by SQUARE_1:12;
        (|[p`1/sqrt(1+(p`1/p`2)^2),p`2/sqrt(1+(p`1/p`2)^2)]|)`2 = p`2/
        sqrt(1+(p `1/p`2)^2) by EUCLID:52;
        then
A43:    p`2/sqrt(1+(p`1/p`2)^2)=q`2/sqrt(1+(q`2/q`1)^2) by A3,A14,A15,A39,Def1;
        then (p`2)^2/(sqrt(1+(p`1/p`2)^2))^2=(q`2/sqrt(1+(q`2/q`1)^2))^2 by
XCMPLX_1:76;
        then (p`2)^2/(sqrt(1+(p`1/p`2)^2))^2=(q`2)^2/(sqrt(1+(q`2/q`1)^2))^2
        by XCMPLX_1:76;
        then (p`2)^2/(1+(p`1/p`2)^2)=(q`2)^2/(sqrt(1+(q`2/q`1)^2))^2 by A40,
SQUARE_1:def 2;
        then (p`2)^2/(1+(p`1/p`2)^2)=(q`2)^2/(1+(q`2/q`1)^2) by A16,
SQUARE_1:def 2;
        then (p`2)^2/(1+(p`1/p`2)^2)/(p`2)^2=(q`2)^2/(p`2)^2/(1+(q`2/q`1)^2)
        by XCMPLX_1:48;
        then (p`2)^2/(p`2)^2/(1+(p`1/p`2)^2)=(q`2)^2/(p`2)^2/(1+(q`2/q`1)^2)
        by XCMPLX_1:48;
        then 1/(1+(p`1/p`2)^2)=(q`2)^2/(p`2)^2/(1+(q`2/q`1)^2) by A42,
XCMPLX_1:60;
        then
A44:    1/(1+(p`1/p`2)^2)*(1+(q`2/q`1)^2)=(q`2)^2/(p`2)^2 by A16,XCMPLX_1:87;
A45:    sqrt(1+(p`1/p`2)^2)>0 by Lm1,SQUARE_1:25;
        (|[p`1/sqrt(1+(p`1/p`2)^2),p`2/sqrt(1+(p`1/p`2)^2)]|)`1 = p`1/
        sqrt(1+(p `1/p`2)^2) by EUCLID:52;
        then
A46:    p`1/sqrt(1+(p`1/p`2)^2)=q`1/sqrt(1+(q`2/q`1)^2) by A3,A14,A17,A39,Def1;
        then (p`1)^2/(sqrt(1+(p`1/p`2)^2))^2=(q`1/sqrt(1+(q`2/q`1)^2))^2 by
XCMPLX_1:76;
        then (p`1)^2/(sqrt(1+(p`1/p`2)^2))^2=(q`1)^2/(sqrt(1+(q`2/q`1)^2))^2
        by XCMPLX_1:76;
        then (p`1)^2/(1+(p`1/p`2)^2)=(q`1)^2/(sqrt(1+(q`2/q`1)^2))^2 by A40,
SQUARE_1:def 2;
        then
A47:    (p`1)^2/(1+(p`1/p`2)^2)=(q`1)^2/(1+(q`2/q`1)^2) by A16,SQUARE_1:def 2;
A48:    now
          assume
A49:      q`1=0;
          then q`2=0 by A12;
          hence contradiction by A12,A49,EUCLID:53,54;
        end;
        then
A50:    (q`1)^2>0 by SQUARE_1:12;
        now
          per cases;
          case
            p`1=0;
            then (q`1)^2=0 by A16,A47,XCMPLX_1:50;
            then
A51:        q`1=0 by XCMPLX_1:6;
            then q`2=0 by A12;
            hence contradiction by A12,A51,EUCLID:53,54;
          end;
          case
A52:        p`1<>0;
            set a=q`2/q`1;
            (p`1)^2/(1+(p`1/p`2)^2)/(p`1)^2=(q`1)^2/(p`1)^2/(1+(q`2/q`1)
            ^2) by A47,XCMPLX_1:48;
            then
A53:        (p`1)^2/(p`1)^2/(1+(p`1/p`2)^2)=(q`1)^2/(p`1)^2/(1+(q`2/q`1)
            ^2) by XCMPLX_1:48;
A54:        q`1*a<=q`1 & -q`1<=q`1*a or q`1*a>=q`1 & q`1*a<=-q`1 by A12,A48,
XCMPLX_1:87;
A55:        now
              per cases by A48;
              case
A56:            q`1>0;
                then a*q`1/q`1<=q`1/q`1 & (-q`1)/q`1<=a*q`1/q`1 or a*q`1/q`1
                >=q`1/q`1 & a*q`1/q`1<=(-q`1)/q`1 by A54,XREAL_1:72;
                then
A57:            a<=q`1/q`1 & (-q`1)/q`1<=a or a>=q`1/q`1 & a<=(-q`1)/q`1
                by A56,XCMPLX_1:89;
                q`1/q`1=1 by A56,XCMPLX_1:60;
                hence a<=1 & -1<=a or a>=1 & a<=-1 by A57,XCMPLX_1:187;
              end;
              case
A58:            q`1<0;
                then
A59:            q`1/q`1=1 & (-q`1)/q`1=-1 by XCMPLX_1:60,197;
                a*q`1/q`1>=q`1/q`1 & (-q`1)/q`1>=a*q`1/q`1 or a*q`1/q`1
                <=q`1/q`1 & a*q`1/q`1>=(-q`1)/q`1 by A54,A58,XREAL_1:73;
                hence a<=1 & -1<=a or a>=1 & a<=-1 by A58,A59,XCMPLX_1:89;
              end;
            end;
            (p`1)^2>0 by A52,SQUARE_1:12;
            then 1/(1+(p`1/p`2)^2)=(q`1)^2/(p`1)^2/(1+(q`2/q`1)^2) by A53,
XCMPLX_1:60;
            then 1/(1+(p`1/p`2)^2)*(1+(q`2/q`1)^2)=(q`1)^2/(p`1)^2 by A16,
XCMPLX_1:87;
            then
(q`1)^2/(q`1)^2/(p`1)^2=(q`2)^2/(p`2)^2/(q`1)^2 by A44,XCMPLX_1:48;
            then 1/(p`1)^2=(q`2)^2/(p`2)^2/(q`1)^2 by A50,XCMPLX_1:60;
            then 1/(p`1)^2*(p`2)^2=(p`2)^2*((q`2)^2/(p`2)^2)/(q`1)^2 by
XCMPLX_1:74;
            then 1/(p`1)^2*(p`2)^2=(q`2)^2/(q`1)^2 by A42,XCMPLX_1:87;
            then (p`2)^2/(p`1)^2=(q`2)^2/(q`1)^2 by XCMPLX_1:99;
            then (p`2/p`1)^2=(q`2)^2/(q`1)^2 by XCMPLX_1:76;
            then
A60:        (p`2/p`1)^2=(q`2/q`1)^2 by XCMPLX_1:76;
            then
A61:        p`2/p`1*p`1=a*p`1 or p`2/p`1*p`1=(-a)*p`1 by SQUARE_1:40;
A62:        now
              per cases by A52,A61,XCMPLX_1:87;
              case
A63:            p`2=a*p`1;
                now
                  per cases by A52;
                  case
                    p`1>0;
                    then p`1/p`1<= a*p`1/p`1 & (-(a*p`1))/p`1<=p`1/p`1 or p`1
/p`1>=(a*p`1)/p`1 & p`1/p`1<=(-(a*p`1))/p`1 by A41,A63,XREAL_1:72;
                    then
A64:                1<= a*p`1/p`1 & (-(a*p`1))/p`1<=1 or 1>=(a*p`1)/p`1
                    & 1<=(-(a*p`1))/p`1 by A52,XCMPLX_1:60;
                    (a*p`1)/p`1=a by A52,XCMPLX_1:89;
                    hence 1<=a & -a<=1 or 1>=a & 1<=-a by A64,XCMPLX_1:187;
                  end;
                  case
                    p`1<0;
                    then p`1/p`1>= a*p`1/p`1 & (-(a*p`1))/p`1>=p`1/p`1 or p`1
/p`1<=(a*p`1)/p`1 & p`1/p`1>=(-(a*p`1))/p`1 by A41,A63,XREAL_1:73;
                    then
A65:                1>= a*p`1/p`1 & (-(a*p`1))/p`1>=1 or 1<=(a*p`1)/p`1
                    & 1>=(-(a*p`1))/p`1 by A52,XCMPLX_1:60;
                    (a*p`1)/p`1=a by A52,XCMPLX_1:89;
                    hence 1<=a & -a<=1 or 1>=a & 1<=-a by A65,XCMPLX_1:187;
                  end;
                end;
                then 1<=a & -a<=1 or 1>=a & -1>=--a by XREAL_1:24;
                hence 1<=a or -1>=a;
              end;
              case
A66:            p`2=(-a)*p`1;
                now
                  per cases by A52;
                  case
                    p`1>0;
                    then p`1/p`1<= (-a)*p`1/p`1 & (-((-a)*p`1))/p`1<=p`1/p`1
or p`1/p`1>=((-a)*p`1)/p`1 & p`1/p`1<=(-((-a)*p`1))/p`1 by A41,A66,XREAL_1:72;
                    then 1<= (-a)*p`1/p`1 & (-((-a)*p`1))/p `1<=1 or 1>=((-a)
                    *p`1)/p`1 & 1<=(-((-a)*p`1))/p`1 by A52,XCMPLX_1:60;
                    then
A67:                1<= (-a) & -(((-a)*p`1)/p`1)<=1 or 1>=(-a) & 1<=-(((
                    -a)*p`1)/p`1) by A52,XCMPLX_1:89,187;
                    ((-a)*p`1)/p`1=(-a) by A52,XCMPLX_1:89;
                    hence 1<=a & -a<=1 or 1>=a & 1<=-a by A67;
                  end;
                  case
                    p`1<0;
                    then p`1/p`1>= (-a)*p`1/p`1 & (-((-a)*p`1))/p`1>=p`1/p`1
or p`1/p`1<=((-a)*p`1)/p`1 & p`1/p`1>=(-((-a)*p`1))/p`1 by A41,A66,XREAL_1:73;
                    then 1>= (-a)*p`1/p`1 & (-((-a)*p`1))/p `1>=1 or 1<=((-a)
                    *p`1)/p`1 & 1>=(-((-a)*p`1))/p`1 by A52,XCMPLX_1:60;
                    then
A68:                1>= (-a) & -(((-a)*p`1)/p`1)>=1 or 1<=(-a) & 1>=-(((
                    -a)*p`1)/p`1) by A52,XCMPLX_1:89,187;
                    ((-a)*p`1)/p`1=(-a) by A52,XCMPLX_1:89;
                    hence 1<=a & -a<=1 or 1>=a & 1<=-a by A68;
                  end;
                end;
                then 1<=a & -a<=1 or 1>=a & -1>=--a by XREAL_1:24;
                hence 1<=a or -1>=a;
              end;
            end;
A69:        now
              per cases by A62,A55,XXREAL_0:1;
              case
                a=1;
                then (p`2)^2/(p`1)^2=1 by A60,XCMPLX_1:76;
                then
A70:            (p`2)^2=(p`1)^2 by XCMPLX_1:58;
                (p`1/p`2)^2=(p`1)^2/(p`2)^2 by XCMPLX_1:76;
                hence (p`1/p`2)^2=(q`2/q`1)^2 by A60,A70,XCMPLX_1:76;
              end;
              case
                a=-1;
                then (p`2)^2/(p`1)^2=1 by A60,XCMPLX_1:76;
                then
A71:            (p`2)^2=(p`1)^2 by XCMPLX_1:58;
                (p`1/p`2)^2=(p`1)^2/(p`2)^2 by XCMPLX_1:76;
                hence (p`1/p`2)^2=(q`2/q`1)^2 by A60,A71,XCMPLX_1:76;
              end;
            end;
            then p`2=q`2/sqrt(1+(q`2/q`1)^2)*sqrt(1+(q`2/q`1)^2) by A43,A45,
XCMPLX_1:87;
            then
A72:        p`2=q`2 by A13,XCMPLX_1:87;
            p`1=q`1/sqrt(1+(q`2/q`1)^2)*sqrt(1+(q`2/q`1)^2) by A46,A45,A69,
XCMPLX_1:87;
            then p`1=q`1 by A13,XCMPLX_1:87;
            then p=|[q`1,q`2]| by A72,EUCLID:53;
            hence thesis by EUCLID:53;
          end;
        end;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
  suppose
A73: q<>0.TOP-REAL 2 & not (q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2<=- q`1);
A74: (|[q`1/sqrt(1+(q`1/q`2)^2),q`2/sqrt(1+(q`1/q`2)^2)]|)`2 = q`2/sqrt(1
    +( q`1/q`2)^2) by EUCLID:52;
A75: (|[q`1/sqrt(1+(q`1/q`2)^2),q`2/sqrt(1+(q`1/q`2)^2)]|)`1 = q`1/sqrt(1
    +( q`1/q`2)^2) by EUCLID:52;
A76: 1+(q`1/q`2)^2>0 by Lm1;
A77: sqrt(1+(q`1/q`2)^2)>0 by Lm1,SQUARE_1:25;
A78: Sq_Circ.q=|[q`1/sqrt(1+(q`1/q`2)^2),q`2/sqrt(1+(q`1/q`2)^2)]| by A73,Def1;
A79: q`1<=q`2 & -q`2<=q`1 or q`1>=q`2 & q`1<=-q`2 by A73,JGRAPH_2:13;
    now
      per cases;
      case
A80:    p=0.TOP-REAL 2;
        (q`1/q`2)^2 >=0 by XREAL_1:63;
        then 1+(q`1/q`2)^2>=1+0 by XREAL_1:7;
        then
A81:    sqrt(1+(q`1/q`2)^2)>=1 by SQUARE_1:18,26;
        Sq_Circ.p=0.TOP-REAL 2 by A80,Def1;
        then q`2/sqrt(1+(q`1/q`2)^2)=0 by A3,A78,EUCLID:52,JGRAPH_2:3;
        then q`2= 0 *sqrt(1+(q`1/q`2)^2) by A81,XCMPLX_1:87
          .=0;
        hence contradiction by A73;
      end;
      case
A82:    p<>0.TOP-REAL 2 & (p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=- p`1);
        now
          assume
A83:      p`1=0;
          then p`2=0 by A82;
          hence contradiction by A82,A83,EUCLID:53,54;
        end;
        then
A84:    (p`1)^2>0 by SQUARE_1:12;
A85:    1+(p`2/p`1)^2>0 by Lm1;
A86:    Sq_Circ.p=|[p`1/sqrt(1+(p`2/p`1)^2),p`2/sqrt(1+(p`2/p`1)^2)]| by A82
,Def1;
        then
A87:    p`1/sqrt(1+(p`2/p`1)^2)=q`1/sqrt(1+(q`1/q`2)^2) by A3,A78,A75,EUCLID:52
;
        then (p`1)^2/(sqrt(1+(p`2/p`1)^2))^2=(q`1/sqrt(1+(q`1/q`2)^2))^2 by
XCMPLX_1:76;
        then (p`1)^2/(sqrt(1+(p`2/p`1)^2))^2=(q`1)^2/(sqrt(1+(q`1/q`2)^2))^2
        by XCMPLX_1:76;
        then (p`1)^2/(1+(p`2/p`1)^2)=(q`1)^2/(sqrt(1+(q`1/q`2)^2))^2 by A85,
SQUARE_1:def 2;
        then (p`1)^2/(1+(p`2/p`1)^2)=(q`1)^2/(1+(q`1/q`2)^2) by A76,
SQUARE_1:def 2;
        then (p`1)^2/(1+(p`2/p`1)^2)/(p`1)^2=(q`1)^2/(p`1)^2/(1+(q`1/q`2)^2)
        by XCMPLX_1:48;
        then (p`1)^2/(p`1)^2/(1+(p`2/p`1)^2)=(q`1)^2/(p`1)^2/(1+(q`1/q`2)^2)
        by XCMPLX_1:48;
        then 1/(1+(p`2/p`1)^2)=(q`1)^2/(p`1)^2/(1+(q`1/q`2)^2) by A84,
XCMPLX_1:60;
        then
A88:    1/(1+(p`2/p`1)^2)*(1+(q`1/q`2)^2)=(q`1)^2/(p`1)^2 by A76,XCMPLX_1:87;
A89:    p`2/sqrt(1+(p`2/p`1)^2)=q`2/sqrt(1+(q`1/q`2)^2) by A3,A78,A74,A86,
EUCLID:52;
        then (p`2)^2/(sqrt(1+(p`2/p`1)^2))^2=(q`2/sqrt(1+(q`1/q`2)^2))^2 by
XCMPLX_1:76;
        then (p`2)^2/(sqrt(1+(p`2/p`1)^2))^2=(q`2)^2/(sqrt(1+(q`1/q`2)^2))^2
        by XCMPLX_1:76;
        then (p`2)^2/(1+(p`2/p`1)^2)=(q`2)^2/(sqrt(1+(q`1/q`2)^2))^2 by A85,
SQUARE_1:def 2;
        then
A90:    (p`2)^2/(1+(p`2/p`1)^2)=(q`2)^2/(1+(q`1/q`2)^2) by A76,SQUARE_1:def 2;
A91:    sqrt(1+(p`2/p`1)^2)>0 by Lm1,SQUARE_1:25;
A92:    q`2<>0 by A73;
        then
A93:    (q`2)^2>0 by SQUARE_1:12;
        now
          per cases;
          case
            p`2=0;
            then (q`2)^2=0 by A76,A90,XCMPLX_1:50;
            then q`2=0 by XCMPLX_1:6;
            hence contradiction by A73;
          end;
          case
A94:        p`2<>0;
            set a=q`1/q`2;
            (p`2)^2/(1+(p`2/p`1)^2)/(p`2)^2=(q`2)^2/(p`2)^2/(1+(q`1/q`2)
            ^2) by A90,XCMPLX_1:48;
            then
A95:        (p`2)^2/(p`2)^2/(1+(p`2/p`1)^2)=(q`2)^2/(p`2)^2/(1+(q`1/q`2)
            ^2) by XCMPLX_1:48;
A96:        q`2*a<=q`2 & -q`2<=q`2*a or q`2*a>=q`2 & q`2*a<=-q`2 by A79,A92,
XCMPLX_1:87;
A97:        now
              per cases by A73;
              case
A98:            q`2>0;
                then
A99:            q`2/q`2=1 & (-q`2)/q`2=-1 by XCMPLX_1:60,197;
                a*q`2/q`2<=q`2/q`2 & (-q`2)/q`2<=a*q`2/q`2 or a*q`2/q`2
                >=q`2/q`2 & a*q`2/q`2<=(-q`2)/q`2 by A96,A98,XREAL_1:72;
                hence a<=1 & -1<=a or a>=1 & a<=-1 by A98,A99,XCMPLX_1:89;
              end;
              case
A100:           q`2<0;
                then a*q`2/q`2>=q`2/q`2 & (-q`2)/q`2>=a*q`2/q`2 or a*q`2/q`2
                <=q`2/q`2 & a*q`2/q`2>=(-q`2)/q`2 by A96,XREAL_1:73;
                then a>=q`2/q`2 & (-q`2)/q`2>=a or a<=q`2/q`2 & a>=(-q`2)/q`2
                by A100,XCMPLX_1:89;
                hence a<=1 & -1<=a or a>=1 & a<=-1 by A100,XCMPLX_1:60,197;
              end;
            end;
            (p`2)^2>0 by A94,SQUARE_1:12;
            then 1/(1+(p`2/p`1)^2)=(q`2)^2/(p`2)^2/(1+(q`1/q`2)^2) by A95,
XCMPLX_1:60;
            then 1/(1+(p`2/p`1)^2)*(1+(q`1/q`2)^2)=(q`2)^2/(p`2)^2 by A76,
XCMPLX_1:87;
            then
            (q`2)^2/(q`2)^2/(p`2)^2=(q`1)^2/(p`1)^2/(q`2)^2 by A88,XCMPLX_1:48;
            then 1/(p`2)^2=(q`1)^2/(p`1)^2/(q`2)^2 by A93,XCMPLX_1:60;
            then 1/(p`2)^2*(p`1)^2=(p`1)^2*((q`1)^2/(p`1)^2)/(q`2)^2 by
XCMPLX_1:74;
            then 1/(p`2)^2*(p`1)^2=(q`1)^2/(q`2)^2 by A84,XCMPLX_1:87;
            then (p`1)^2/(p`2)^2=(q`1)^2/(q`2)^2 by XCMPLX_1:99;
            then (p`1/p`2)^2=(q`1)^2/(q`2)^2 by XCMPLX_1:76;
            then
A101:       (p`1/p`2)^2=(q`1/q`2)^2 by XCMPLX_1:76;
            then
A102:       p`1/p`2=q`1/q`2 or p`1/p`2=-q`1/q`2 by SQUARE_1:40;
A103:       now
              per cases by A94,A102,XCMPLX_1:87;
              case
A104:           p`1=a*p`2;
                now
                  per cases by A94;
                  case
                    p`2>0;
                    then p`2/p`2<= a*p`2/p`2 & (-(a*p`2))/p`2<=p`2/p`2 or p`2
/p`2>=(a*p`2)/p`2 & p`2/p`2<=(-(a*p`2))/p`2 by A82,A104,XREAL_1:72;
                    then
A105:               1<= a*p`2/p`2 & (-(a*p`2))/p`2<=1 or 1>=(a*p`2)/p`2
                    & 1<=(-(a*p`2))/p`2 by A94,XCMPLX_1:60;
                    (a*p`2)/p`2=a by A94,XCMPLX_1:89;
                    hence 1<=a & -a<=1 or 1>=a & 1<=-a by A105,XCMPLX_1:187;
                  end;
                  case
                    p`2<0;
                    then p`2/p`2>= a*p`2/p`2 & (-(a*p`2))/p`2>=p`2/p`2 or p`2
/p`2<=(a*p`2)/p`2 & p`2/p`2>=(-(a*p`2))/p`2 by A82,A104,XREAL_1:73;
                    then
A106:               1>= a*p`2/p`2 & (-(a*p`2))/p`2>=1 or 1<=(a*p`2)/p`2
                    & 1>=(-(a*p`2))/p`2 by A94,XCMPLX_1:60;
                    (a*p`2)/p`2=a by A94,XCMPLX_1:89;
                    hence 1<=a & -a<=1 or 1>=a & 1<=-a by A106,XCMPLX_1:187;
                  end;
                end;
                then 1<=a & -a<=1 or 1>=a & -1>=--a by XREAL_1:24;
                hence 1<=a or -1>=a;
              end;
              case
A107:           p`1=(-a)*p`2;
                now
                  per cases by A94;
                  case
                    p`2>0;
                    then p`2/p`2<= (-a)*p`2/p`2 & (-((-a)*p`2))/p`2<=p`2/p`2
or p`2/p`2>=((-a)*p`2)/p`2 & p`2/p`2<=(-((-a)*p`2))/p`2 by A82,A107,XREAL_1:72;
                    then 1<= (-a)*p`2/p`2 & (-((-a)*p`2))/p `2<=1 or 1>=((-a)
                    *p`2)/p`2 & 1<=(-((-a)*p`2))/p`2 by A94,XCMPLX_1:60;
                    then
A108:               1<= (-a) & -(((-a)*p`2)/p`2)<=1 or 1>=(-a) & 1<=-(((
                    -a)*p`2)/p`2) by A94,XCMPLX_1:89,187;
                    ((-a)*p`2)/p`2=(-a) by A94,XCMPLX_1:89;
                    hence 1<=a & -a<=1 or 1>=a & 1<=-a by A108;
                  end;
                  case
                    p`2<0;
                    then p`2/p`2>= (-a)*p`2/p`2 & (-((-a)*p`2))/p`2>=p`2/p`2
or p`2/p`2<=((-a)*p`2)/p`2 & p`2/p`2>=(-((-a)*p`2))/p`2 by A82,A107,XREAL_1:73;
                    then 1>= (-a)*p`2/p`2 & (-((-a)*p`2))/p `2>=1 or 1<=((-a)
                    *p`2)/p`2 & 1>=(-((-a)*p`2))/p`2 by A94,XCMPLX_1:60;
                    then
A109:               1>= -a & -(((-a)*p`2)/p`2)>=1 or 1<=-a & 1>=-(((-a)*
                    p`2)/p`2) by A94,XCMPLX_1:89,187;
                    ((-a)*p`2)/p`2=(-a) by A94,XCMPLX_1:89;
                    hence 1<=a & -a<=1 or 1>=a & 1<=-a by A109;
                  end;
                end;
                then 1<=a & -a<=1 or 1>=a & -1>=--a by XREAL_1:24;
                hence 1<=a or -1>=a;
              end;
            end;
A110:       now
              per cases by A103,A97,XXREAL_0:1;
              case
                a=1;
                then (p`1)^2/(p`2)^2=1 by A101,XCMPLX_1:76;
                then
A111:           (p`1)^2=(p`2)^2 by XCMPLX_1:58;
                (p`2/p`1)^2=(p`2)^2/(p`1)^2 by XCMPLX_1:76;
                hence (p`2/p`1)^2=(q`1/q`2)^2 by A101,A111,XCMPLX_1:76;
              end;
              case
                a=-1;
                then (p`1)^2/(p`2)^2=1 by A101,XCMPLX_1:76;
                then
A112:           (p`1)^2=(p`2)^2 by XCMPLX_1:58;
                (p`2/p`1)^2=(p`2)^2/(p`1)^2 by XCMPLX_1:76;
                hence (p`2/p`1)^2=(q`1/q`2)^2 by A101,A112,XCMPLX_1:76;
              end;
            end;
            then p`1=q`1/sqrt(1+(q`1/q`2)^2)*sqrt(1+(q`1/q`2)^2) by A87,A91,
XCMPLX_1:87;
            then
A113:       p`1=q`1 by A77,XCMPLX_1:87;
            p`2=q`2/sqrt(1+(q`1/q`2)^2)*sqrt(1+(q`1/q`2)^2) by A89,A91,A110,
XCMPLX_1:87;
            then p`2=q`2 by A77,XCMPLX_1:87;
            then p=|[q`1,q`2]|by A113,EUCLID:53;
            hence thesis by EUCLID:53;
          end;
        end;
        hence thesis;
      end;
      case
A114:   p<>0.TOP-REAL 2 & not (p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p `2<=-p`1);
        then p`2<>0;
        then
A115:   (p`2)^2>0 by SQUARE_1:12;
A116:   sqrt(1+(p`1/p`2)^2)>0 by Lm1,SQUARE_1:25;
A117:   1+(p`1/p`2)^2>0 by Lm1;
A118:   Sq_Circ.p=|[p`1/sqrt(1+(p`1/p`2)^2),p`2/sqrt(1+(p`1/p`2)^2)]| by A114
,Def1;
        then
A119:   p`1/sqrt(1+(p`1/p`2)^2)=q`1/sqrt(1+(q`1/q`2)^2) by A3,A78,A75,EUCLID:52
;
        then (p`1)^2/(sqrt(1+(p`1/p`2)^2))^2=(q`1/sqrt(1+(q`1/q`2)^2))^2 by
XCMPLX_1:76;
        then (p`1)^2/(sqrt(1+(p`1/p`2)^2))^2=(q`1)^2/(sqrt(1+(q`1/q`2)^2))^2
        by XCMPLX_1:76;
        then (p`1)^2/(1+(p`1/p`2)^2)=(q`1)^2/(sqrt(1+(q`1/q`2)^2))^2 by A117,
SQUARE_1:def 2;
        then
A120:   (p`1)^2/(1+(p`1/p`2)^2)=(q`1)^2/(1+(q`1/q`2)^2) by A76,SQUARE_1:def 2;
A121:   p`2/sqrt(1+(p`1/p`2)^2)=q`2/sqrt(1+(q`1/q`2)^2) by A3,A78,A74,A118,
EUCLID:52;
        then (p`2)^2/(sqrt(1+(p`1/p`2)^2))^2=(q`2/sqrt(1+(q`1/q`2)^2))^2 by
XCMPLX_1:76;
        then (p`2)^2/(sqrt(1+(p`1/p`2)^2))^2=(q`2)^2/(sqrt(1+(q`1/q`2)^2))^2
        by XCMPLX_1:76;
        then (p`2)^2/(1+(p`1/p`2)^2)=(q`2)^2/(sqrt(1+(q`1/q`2)^2))^2 by A117,
SQUARE_1:def 2;
        then (p`2)^2/(1+(p`1/p`2)^2)=(q`2)^2/(1+(q`1/q`2)^2) by A76,
SQUARE_1:def 2;
        then (p`2)^2/(1+(p`1/p`2)^2)/(p`2)^2=(q`2)^2/(p`2)^2/(1+(q`1/q`2)^2)
        by XCMPLX_1:48;
        then (p`2)^2/(p`2)^2/(1+(p`1/p`2)^2)=(q`2)^2/(p`2)^2/(1+(q`1/q`2)^2)
        by XCMPLX_1:48;
        then 1/(1+(p`1/p`2)^2)=(q`2)^2/(p`2)^2/(1+(q`1/q`2)^2) by A115,
XCMPLX_1:60;
        then
A122:   1/(1+(p`1/p`2)^2)*(1+(q`1/q`2)^2)=(q`2)^2/(p`2)^2 by A76,XCMPLX_1:87;
        q`2<>0 by A73;
        then
A123:   (q`2)^2>0 by SQUARE_1:12;
        now
          per cases;
          case
A124:       p`1=0;
            then (q`1)^2=0 by A76,A120,XCMPLX_1:50;
            then
A125:       q`1=0 by XCMPLX_1:6;
            then p=|[0,q`2]|by A3,A78,A118,A124,EUCLID:53,SQUARE_1:18;
            hence thesis by A125,EUCLID:53;
          end;
          case
            p`1<>0;
            then
A126:       (p`1)^2>0 by SQUARE_1:12;
            (p`1)^2/(1+(p`1/p`2)^2)/(p`1)^2=(q`1)^2/(p`1)^2/(1+(q`1/q`2)
            ^2) by A120,XCMPLX_1:48;
            then (p`1)^2/(p`1)^2/(1+(p`1/p`2)^2)=(q`1)^2/(p`1)^2/(1+(q`1/q`2)
            ^2) by XCMPLX_1:48;
            then 1/(1+(p`1/p`2)^2)=(q`1)^2/(p`1)^2/(1+(q`1/q`2)^2) by A126,
XCMPLX_1:60;
            then 1/(1+(p`1/p`2)^2)*(1+(q`1/q`2)^2)=(q`1)^2/(p`1)^2 by A76,
XCMPLX_1:87;
            then
(q`2)^2/(q`2)^2/(p`2)^2=(q`1)^2/(p`1)^2/(q`2)^2 by A122,XCMPLX_1:48;
            then 1/(p`2)^2=(q`1)^2/(p`1)^2/(q`2)^2 by A123,XCMPLX_1:60;
            then 1/(p`2)^2*(p`1)^2=(p`1)^2*((q`1)^2/(p`1)^2)/(q`2)^2 by
XCMPLX_1:74;
            then 1/(p`2)^2*(p`1)^2=(q`1)^2/(q`2)^2 by A126,XCMPLX_1:87;
            then (p`1)^2/(p`2)^2=(q`1)^2/(q`2)^2 by XCMPLX_1:99;
            then (p`1/p`2)^2=(q`1)^2/(q`2)^2 by XCMPLX_1:76;
            then
A127:       (1+(p`1/p`2)^2)=(1+(q`1/q`2)^2) by XCMPLX_1:76;
            then p`1=q`1/sqrt(1+(q`1/q`2)^2)*sqrt(1+(q`1/q`2)^2) by A119,A116,
XCMPLX_1:87;
            then
A128:       p`1=q`1 by A77,XCMPLX_1:87;
            p`2=q`2/sqrt(1+(q`1/q`2)^2)*sqrt(1+(q`1/q`2)^2) by A121,A116,A127,
XCMPLX_1:87;
            then p`2=q`2 by A77,XCMPLX_1:87;
            then p=|[q`1,q`2]|by A128,EUCLID:53;
            hence thesis by EUCLID:53;
          end;
        end;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
end;

registration
  cluster Sq_Circ -> one-to-one;
  coherence by Th22;
end;

theorem Th23:
  for Kb,Cb being Subset of TOP-REAL 2 st Kb={q: -1=q`1 & -1<=q`2
& q`2<=1 or q`1=1 & -1<=q`2 & q`2<=1 or -1=q`2 & -1<=q`1 & q`1<=1 or 1=q`2 & -1
  <=q`1 & q`1<=1}& Cb={p2 where p2 is Point of TOP-REAL 2: |.p2.|=1} holds
  Sq_Circ.:Kb=Cb
proof
  let Kb,Cb be Subset of TOP-REAL 2;
  assume
A1: Kb={q: -1=q`1 & -1<=q`2 & q`2<=1 or q`1=1 & -1<=q`2 & q`2<=1 or -1=q
`2 & -1<=q`1 & q`1<=1 or 1=q`2 & -1<=q`1 & q`1<=1}& Cb={p2 where p2 is Point of
  TOP-REAL 2: |.p2.|=1};
  thus Sq_Circ.:Kb c= Cb
  proof
    let y be set;
    assume y in Sq_Circ.:Kb;
    then consider x being set such that
    x in dom Sq_Circ and
A2: x in Kb and
A3: y=Sq_Circ.x by FUNCT_1:def 6;
    consider q being Point of TOP-REAL 2 such that
A4: q=x and
A5: -1=q`1 & -1<=q`2 & q`2<=1 or q`1=1 & -1<=q`2 & q`2<=1 or -1=q`2 &
    - 1<=q`1 & q`1<=1 or 1=q`2 & -1<=q`1 & q`1<=1 by A1,A2;
    now
      per cases;
      case
        q=0.TOP-REAL 2;
        hence contradiction by A5,JGRAPH_2:3;
      end;
      case
A6:     q<>0.TOP-REAL 2 & (q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2<=-q `1);
A7:     (|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]|)`1 = q`1/
sqrt(1+(q `2/q `1)^2) & (|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]|)`2
        = q`2/sqrt(1+ (q `2/q`1)^2) by EUCLID:52;
A8:     1+(q`2)^2>0 by Lm1;
A9:     Sq_Circ.q=|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2 )]| by A6
,Def1;
        now
          per cases by A5;
          case
            -1=q`1 & -1<=q`2 & q`2<=1;
            then
            |.(|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]|).|^2 =
((-1)/sqrt(1+(q`2/(-1))^2))^2+(q`2/sqrt(1+(q`2/(-1))^2))^2 by A7,JGRAPH_1:29
              .=(-1)^2/(sqrt(1+(q`2/(-1))^2))^2+(q`2/sqrt(1+(q`2/(-1))^2))^2
            by XCMPLX_1:76
              .=1/(sqrt(1+(-q`2)^2))^2+(q`2)^2/(sqrt(1+(-q`2)^2))^2 by
XCMPLX_1:76
              .=1/(1+(q`2)^2)+(q`2)^2/(sqrt(1+(q`2)^2))^2 by A8,SQUARE_1:def 2
              .=1/(1+(q`2)^2)+(q`2)^2/(1+(q`2)^2) by A8,SQUARE_1:def 2
              .=(1+(q`2)^2)/(1+(q`2)^2) by XCMPLX_1:62
              .=1 by A8,XCMPLX_1:60;
            then |.(|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]|).|=1
            by SQUARE_1:18,22;
            hence ex p2 being Point of TOP-REAL 2 st p2=y & |.p2.|=1 by A3,A4
,A9;
          end;
          case
            q`1=1 & -1<=q`2 & q`2<=1;
            then
            |.(|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]|).|^2 =
(1/sqrt(1+(q`2/(1))^2))^2+(q`2/sqrt(1+(q`2/(1))^2))^2 by A7,JGRAPH_1:29
              .=1^2/(sqrt(1+(q`2/(1))^2))^2+(q`2/sqrt(1+(q`2/(1))^2))^2 by
XCMPLX_1:76
              .=1/(sqrt(1+(q`2/(1))^2))^2+(q`2)^2/(sqrt(1+(q`2/(1))^2))^2 by
XCMPLX_1:76
              .=1/(1+(q`2)^2)+(q`2)^2/(sqrt(1+(q`2)^2))^2 by A8,SQUARE_1:def 2
              .=1/(1+(q`2)^2)+(q`2)^2/(1+(q`2)^2) by A8,SQUARE_1:def 2
              .=(1+(q`2)^2)/(1+(q`2)^2) by XCMPLX_1:62
              .=1 by A8,XCMPLX_1:60;
            then |.(|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]|).|=1
            by SQUARE_1:18,22;
            hence ex p2 being Point of TOP-REAL 2 st p2=y & |.p2.|=1 by A3,A4
,A9;
          end;
          case
A10:        -1=q`2 & -1<=q`1 & q`1<=1;
            then -1<=q`1 & q`1>=1 or -1>=q`1 & 1>=q`1 by A6,XREAL_1:24;
            then
A11:        q`1=1 or q`1=-1 by A10,XXREAL_0:1;
            |.(|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]|).|^2 =
((q`1)/sqrt(1+((-1)/(q`1))^2))^2+((-1)/sqrt(1+((-1)/(q`1))^2))^2 by A7,A10,
JGRAPH_1:29
              .=((q`1)/sqrt(1+((-1)/(q`1))^2))^2+(-1)^2/(sqrt(1+((-1)/(q`1))
            ^2))^2 by XCMPLX_1:76
              .=(q`1)^2/(sqrt(1+((-1)/(q`1))^2))^2+1/(sqrt(1+((-1)/(q`1))^2)
            )^2 by XCMPLX_1:76
              .=1/2+1/(sqrt(2))^2 by A11,SQUARE_1:def 2
              .=1/2+1/2 by SQUARE_1:def 2
              .=1;
            then |.(|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]|).|=1
            by SQUARE_1:18,22;
            hence ex p2 being Point of TOP-REAL 2 st p2=y & |.p2.|=1 by A3,A4
,A9;
          end;
          case
A12:        1=q`2 & -1<=q`1 & q`1<=1;
            then 1<=q`1 & q`1>=-1 or 1>=q`1 & -1>=q`1 by A6,XREAL_1:25;
            then
A13:        q`1=1 or q`1=-1 by A12,XXREAL_0:1;
            |.(|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]|).|^2 =
((q`1)/sqrt(1+((1)/(q`1))^2))^2+((1)/sqrt(1+((1)/(q`1))^2))^2 by A7,A12,
JGRAPH_1:29
              .=((q`1)/sqrt(1+((1)/(q`1))^2))^2+(1)^2/(sqrt(1+((1)/(q`1))^2)
            )^2 by XCMPLX_1:76
              .=1/(sqrt(1+1/1))^2+1/(sqrt(1+1/1))^2 by A13,XCMPLX_1:76
              .=1/2+1/(sqrt(2))^2 by SQUARE_1:def 2
              .=1/2+1/2 by SQUARE_1:def 2
              .=1;
            then |.(|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]|).|=1
            by SQUARE_1:18,22;
            hence ex p2 being Point of TOP-REAL 2 st p2=y & |.p2.|=1 by A3,A4
,A9;
          end;
        end;
        hence ex p2 being Point of TOP-REAL 2 st p2=y & |.p2.|=1;
      end;
      case
A14:    q<>0.TOP-REAL 2 & not(q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2 <=-q`1);
A15:    (|[q`1/sqrt(1+(q`1/q`2)^2),q`2/sqrt(1+(q`1/q`2)^2)]|)`1 = q`1/
sqrt(1+(q `1/q `2)^2) & (|[q`1/sqrt(1+(q`1/q`2)^2),q`2/sqrt(1+(q`1/q`2)^2)]|)`2
        = q`2/sqrt(1+ (q `1/q`2)^2) by EUCLID:52;
A16:    1+(q`1)^2>0 by Lm1;
A17:    Sq_Circ.q=|[q`1/sqrt(1+(q`1/q`2)^2),q`2/sqrt(1+(q`1/q`2)^2 )]| by A14
,Def1;
        now
          per cases by A5;
          case
            -1=q`2 & -1<=q`1 & q`1<=1;
            then |.(|[q`1/sqrt(1+(q`1/q`2)^2),q`2/sqrt(1+(q`1/q`2)^2)]|).| ^2
=(q`1/sqrt(1+(q`1/(-1))^2))^2+((-1)/sqrt(1+(q`1/(-1))^2))^2 by A15,JGRAPH_1:29
              .=(-1)^2/(sqrt(1+(q`1/(-1))^2))^2+(q`1/sqrt(1+(q`1/(-1))^2))^2
            by XCMPLX_1:76
              .=1/(sqrt(1+(-q`1)^2))^2+(q`1)^2/(sqrt(1+(-q`1)^2))^2 by
XCMPLX_1:76
              .=1/(1+(q`1)^2)+(q`1)^2/(sqrt(1+(q`1)^2))^2 by A16,SQUARE_1:def 2
              .=1/(1+(q`1)^2)+(q`1)^2/(1+(q`1)^2) by A16,SQUARE_1:def 2
              .=(1+(q`1)^2)/(1+(q`1)^2) by XCMPLX_1:62
              .=1 by A16,XCMPLX_1:60;
            then |.(|[q`1/sqrt(1+(q`1/q`2)^2),q`2/sqrt(1+(q`1/q`2)^2)]|).|=1
            by SQUARE_1:18,22;
            hence ex p2 being Point of TOP-REAL 2 st p2=y & |.p2.|=1 by A3,A4
,A17;
          end;
          case
            q`2=1 & -1<=q`1 & q`1<=1;
            then |.(|[q`1/sqrt(1+(q`1/q`2)^2),q`2/sqrt(1+(q`1/q`2)^2)]|).| ^2
=((1)/sqrt(1+(q`1/(1))^2))^2+(q`1/sqrt(1+(q`1/(1))^2))^2 by A15,JGRAPH_1:29
              .=1^2/(sqrt(1+(q`1/(1))^2))^2+(q`1/sqrt(1+(q`1/(1))^2))^2 by
XCMPLX_1:76
              .=1/(sqrt(1+(q`1/(1))^2))^2+(q`1)^2/(sqrt(1+(q`1/(1))^2))^2 by
XCMPLX_1:76
              .=1/(1+(q`1)^2)+(q`1)^2/(sqrt(1+(q`1)^2))^2 by A16,SQUARE_1:def 2
              .=1/(1+(q`1)^2)+(q`1)^2/(1+(q`1)^2) by A16,SQUARE_1:def 2
              .=(1+(q`1)^2)/(1+(q`1)^2) by XCMPLX_1:62
              .=1 by A16,XCMPLX_1:60;
            then |.(|[q`1/sqrt(1+(q`1/q`2)^2),q`2/sqrt(1+(q`1/q`2)^2)]|).|=1
            by SQUARE_1:18,22;
            hence ex p2 being Point of TOP-REAL 2 st p2=y & |.p2.|=1 by A3,A4
,A17;
          end;
          case
            -1=q`1 & -1<=q`2 & q`2<=1;
            hence ex p2 being Point of TOP-REAL 2 st p2=y & |.p2.|=1 by A14;
          end;
          case
            1=q`1 & -1<=q`2 & q`2<=1;
            hence ex p2 being Point of TOP-REAL 2 st p2=y & |.p2.|=1 by A14;
          end;
        end;
        hence ex p2 being Point of TOP-REAL 2 st p2=y & |.p2.|=1;
      end;
    end;
    hence thesis by A1;
  end;
  let y be set;
  assume y in Cb;
  then consider p2 being Point of TOP-REAL 2 such that
A18: p2=y and
A19: |.p2.|=1 by A1;
  set q=p2;
  now
    per cases;
    case
      q=0.TOP-REAL 2;
      hence contradiction by A19,TOPRNS_1:23;
    end;
    case
A20:  q<>0.TOP-REAL 2 & (q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2<=-q`1 );
A21:  |.q.|^2=q`1^2+q`2^2 by JGRAPH_1:29;
      set px=|[q`1*sqrt(1+(q`2/q`1)^2),q`2*sqrt(1+(q`2/q`1)^2)]|;
A22:  px`1 = q`1*sqrt(1+(q`2/q`1)^2) by EUCLID:52;
A23:  sqrt(1+(q`2/q`1)^2)>0 by Lm1,SQUARE_1:25;
      then
A24:  q`2=q`2*sqrt(1+(q`2/q`1)^2)/(sqrt(1+(q`2/q`1)^2))by XCMPLX_1:89
        .=px`2/(sqrt(1+(q`2/q`1)^2)) by EUCLID:52;
A25:  px`2 = q`2*sqrt(1+(q`2/q`1)^2) by EUCLID:52;
      then
A26:  px`2/px`1=q`2/q`1 by A22,A23,XCMPLX_1:91;
      then
A27:  px`2/sqrt(1+(px`2/px`1)^2)=q`2 by A25,A23,XCMPLX_1:89;
      q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2*sqrt(1+(q`2/q`1)^2) <= (-q`1
      )*sqrt(1+(q`2/q`1)^2) by A20,A23,XREAL_1:64;
      then
A28:  q`2<=q`1 & (-q`1)*sqrt(1+(q`2/q`1)^2) <= q`2*sqrt(1+(q`2/q`1) ^2)
      or px`2>=px`1 & px`2<=-px`1 by A22,A25,A23,XREAL_1:64;
A29:  1+(px`2/px`1)^2>0 by Lm1;
      q`1=q`1*sqrt(1+(q`2/q`1)^2)/(sqrt(1+(q`2/q`1)^2))by A23,XCMPLX_1:89
        .=px`1/(sqrt(1+(q`2/q`1)^2)) by EUCLID:52;
      then (px`1)^2/(sqrt(1+(px`2/px`1)^2))^2+(px`2/sqrt(1+(px`2/px`1)^2)) ^2
      = 1 by A19,A26,A24,A21,XCMPLX_1:76;
      then
      (px`1)^2/(sqrt(1+(px`2/px`1)^2))^2+(px`2)^2/(sqrt(1+(px`2/px`1)^2))
      ^2=1 by XCMPLX_1:76;
      then (px`1)^2/(1+(px`2/px`1)^2)+(px`2)^2/(sqrt(1+(px`2/px`1)^2))^2=1 by
A29,SQUARE_1:def 2;
      then
      1 *(1+(px`2/px`1)^2)= (1+(px`2/px`1)^2)*((px`1)^2/(1+(px`2/px`1)^2)
      + (px`2)^2/(1+(px`2/px`1)^2)) by A29,SQUARE_1:def 2
        .= (px`1)^2/(1+(px`2/px`1)^2)*(1+(px`2/px`1)^2) +(px`2)^2/(1+(px`2/
      px`1)^2)*(1+(px`2/px`1)^2);
      then (px`1)^2+(px`2)^2/(1+(px`2/px`1)^2)*(1+(px`2/px`1)^2)=1 *(1+(px`2/
      px `1)^2) by A29,XCMPLX_1:87;
      then
A30:  (px`1)^2+(px`2)^2=1 *(1+(px`2/px`1)^2) by A29,XCMPLX_1:87
        .=1+(px`2)^2/(px`1)^2 by XCMPLX_1:76;
A31:  now
        assume that
A32:    px`1=0 and
A33:    px`2=0;
        q`2*sqrt(1+(q`2/q`1)^2)=0 by A33,EUCLID:52;
        then
A34:    q`2=0 by A23,XCMPLX_1:6;
        q`1*sqrt(1+(q`2/q`1)^2)=0 by A32,EUCLID:52;
        then q`1=0 by A23,XCMPLX_1:6;
        hence contradiction by A20,A34,EUCLID:53,54;
      end;
      then not px`1=0 by A22,A25,A23,A28,XREAL_1:64;
      then ((px`1)^2+((px`2)^2-1))*(px`1)^2=(px`2)^2 by A30,XCMPLX_1:6,87;
      then 0= ((px`1)^2-1)*((px`1)^2+(px`2)^2);
      then
A35:  (px`1)^2-1=0 or (px`1)^2+(px`2)^2=0 by XCMPLX_1:6;
      now
        per cases by A31,A35,COMPLEX1:1,SQUARE_1:41;
        case
          px`1=1;
          hence -1=px`1 & -1<=px`2 & px`2<=1 or px`1=1 & -1<=px`2 & px`2<=1 or
-1=px`2 & -1<=px`1 & px`1<=1 or 1=px`2 & -1<=px`1 & px`1<=1 by A22,A25,A23,A28,
XREAL_1:64;
        end;
        case
          px`1=-1;
          hence -1=px`1 & -1<=px`2 & px`2<=1 or px`1=1 & -1<=px`2 & px`2<=1 or
-1=px`2 & -1<=px`1 & px`1<=1 or 1=px`2 & -1<=px`1 & px`1<=1 by A22,A23,A28,
XREAL_1:64;
        end;
      end;
      then
A36:  dom Sq_Circ=the carrier of TOP-REAL 2 & px in Kb by A1,FUNCT_2:def 1;
      px`2<=px`1 & -px`1<=px`2 or px`2>=px`1 & px`2<=-px`1 by A22,A25,A23,A28,
XREAL_1:64;
      then
A37:  Sq_Circ.px=|[px`1/sqrt(1+(px`2/px`1)^2),px`2/sqrt(1+(px`2/px`1 )^2)
      ]| by A31,Def1,JGRAPH_2:3;
      px`1/sqrt(1+(px`2/px`1)^2)=q`1 by A22,A23,A26,XCMPLX_1:89;
      hence ex x being set st x in dom Sq_Circ & x in Kb & y=Sq_Circ.x by A18
,A37,A27,A36,EUCLID:53;
    end;
    case
A38:  q<>0.TOP-REAL 2 & not(q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2<=- q`1);
A39:  |.q.|^2=q`2^2+q`1^2 by JGRAPH_1:29;
      set px=|[q`1*sqrt(1+(q`1/q`2)^2),q`2*sqrt(1+(q`1/q`2)^2)]|;
A40:  sqrt(1+(q`1/q`2)^2)>0 by Lm1,SQUARE_1:25;
A41:  px`1 = q`1*sqrt(1+(q`1/q`2)^2) by EUCLID:52;
      then
A42:  q`1=px`1/(sqrt(1+(q`1/q`2)^2)) by A40,XCMPLX_1:89;
A43:  px`2 = q`2*sqrt(1+(q`1/q`2)^2) by EUCLID:52;
      then
A44:  px`1/px`2=q`1/q`2 by A41,A40,XCMPLX_1:91;
      then
A45:  px`1/sqrt(1+(px`1/px`2)^2)=q`1 by A41,A40,XCMPLX_1:89;
      q`1<=q`2 & -q`2<=q`1 or q`1>=q`2 & q`1<=-q`2 by A38,JGRAPH_2:13;
      then
      q`1<=q`2 & -q`2<=q`1 or q`1>=q`2 & q`1*sqrt(1+(q`1/q`2)^2) <= (-q`2
      )*sqrt(1+(q`1/q`2)^2) by A40,XREAL_1:64;
      then
A46:  q`1<=q`2 & (-q`2)*sqrt(1+(q`1/q`2)^2) <= q`1*sqrt(1+(q`1/q`2) ^2)
      or px`1>=px`2 & px`1<=-px`2 by A43,A41,A40,XREAL_1:64;
A47:  1+(px`1/px`2)^2>0 by Lm1;
      q`2=px`2/(sqrt(1+(q`1/q`2)^2)) by A43,A40,XCMPLX_1:89;
      then (px`2)^2/(sqrt(1+(px`1/px`2)^2))^2+(px`1/sqrt(1+(px`1/px`2)^2)) ^2
      = 1 by A19,A44,A42,A39,XCMPLX_1:76;
      then (px`2)^2/(sqrt(1+(px`1/px`2)^2))^2+(px`1)^2/(sqrt(1+(px`1/px`2)^2)
      ) ^2=1 by XCMPLX_1:76;
      then (px`2)^2/(1+(px`1/px`2)^2)+(px`1)^2/(sqrt(1+(px`1/px`2)^2))^2=1 by
A47,SQUARE_1:def 2;
      then 1 *(1+(px`1/px`2)^2) = (1+(px`1/px`2)^2)*( (px`2)^2/(1+(px`1/px`2)
      ^2) +(px`1)^2/(1+(px`1/px`2)^2)) by A47,SQUARE_1:def 2
        .= (px`2)^2/(1+(px`1/px`2)^2)*(1+(px`1/px`2)^2) +(px`1)^2/(1+(px`1/
      px`2)^2)*(1+(px`1/px`2)^2);
      then (px`2)^2+(px`1)^2/(1+(px`1/px`2)^2)*(1+(px`1/px`2)^2)=1 *(1+(px`1/
      px `2)^2) by A47,XCMPLX_1:87;
      then (px`2)^2+(px`1)^2=1 *(1+(px`1/px`2)^2) by A47,XCMPLX_1:87;
      then
A48:  (px`2)^2+(px`1)^2-1=(px`1)^2/(px`2)^2 by XCMPLX_1:76;
A49:  now
        assume that
A50:    px`2=0 and
        px`1=0;
        q`2=0 by A43,A40,A50,XCMPLX_1:6;
        hence contradiction by A38;
      end;
      then px`2<>0 by A43,A41,A40,A46,XREAL_1:64;
      then ((px`2)^2+((px`1)^2-1))*(px`2)^2=(px`1)^2 by A48,XCMPLX_1:6,87;
      then 0=((px`2)^2-1)*((px`2)^2+(px`1)^2);
      then
A51:  (px`2)^2-1=0 or (px`2)^2+(px`1)^2=0 by XCMPLX_1:6;
      now
        per cases by A49,A51,COMPLEX1:1,SQUARE_1:41;
        case
          px`2=1;
          hence -1=px`2 & -1<=px`1 & px`1<=1 or px`2=1 & -1<=px`1 & px`1<=1 or
-1=px`1 & -1<=px`2 & px`2<=1 or 1=px`1 & -1<=px`2 & px`2<=1 by A43,A41,A40,A46,
XREAL_1:64;
        end;
        case
          px`2=-1;
          hence -1=px`2 & -1<=px`1 & px`1<=1 or px`2=1 & -1<=px`1 & px`1<=1 or
-1=px`1 & -1<=px`2 & px`2<=1 or 1=px`1 & -1<=px`2 & px`2<=1 by A43,A40,A46,
XREAL_1:64;
        end;
      end;
      then
A52:  dom Sq_Circ=the carrier of TOP-REAL 2 & px in Kb by A1,FUNCT_2:def 1;
      px`1<=px`2 & -px`2<=px`1 or px`1>=px`2 & px`1<=-px`2 by A43,A41,A40,A46,
XREAL_1:64;
      then
A53:  Sq_Circ.px=|[px`1/sqrt(1+(px`1/px`2)^2),px`2/sqrt(1+(px`1/px`2 )^2
      )]| by A49,Th4,JGRAPH_2:3;
      px`2/sqrt(1+(px`1/px`2)^2)=q`2 by A43,A40,A44,XCMPLX_1:89;
      hence ex x being set st x in dom Sq_Circ & x in Kb & y=Sq_Circ.x by A18
,A53,A45,A52,EUCLID:53;
    end;
  end;
  hence thesis by FUNCT_1:def 6;
end;

theorem Th24:
  for P,Kb being Subset of TOP-REAL 2,f being Function of (
TOP-REAL 2)|Kb,(TOP-REAL 2)|P st Kb={q: -1=q`1 & -1<=q`2 & q`2<=1 or q`1=1 & -1
<=q`2 & q`2<=1 or -1=q`2 & -1<=q`1 & q`1<=1 or 1=q`2 & -1<=q`1 & q`1<=1} & f is
  being_homeomorphism holds P is being_simple_closed_curve
proof
  set X=(TOP-REAL 2)|R^2-unit_square;
  set b=1,a=0;
  set v= |[1,0]|;
  let P,Kb be Subset of TOP-REAL 2,f be Function of (TOP-REAL 2)|Kb,(TOP-REAL
  2)|P;
  assume
A1: Kb={q: -1=q`1 & -1<=q`2 & q`2<=1 or q`1=1 & -1<=q`2 & q`2<=1 or -1=q
`2 & -1<=q`1 & q`1<=1 or 1=q`2 & -1<=q`1 & q`1<=1} & f is being_homeomorphism;
  v`1=1 & v`2=0 by EUCLID:52;
  then
A2: |[1,0]| in {q: -1=q`1 & -1<=q`2 & q`2<=1 or q`1=1 & -1<=q`2 & q`2<=1 or
  -1=q`2 & -1<=q`1 & q`1<=1 or 1=q`2 & -1<=q`1 & q`1<=1};
  then reconsider Kbb=Kb as non empty Subset of TOP-REAL 2 by A1;
  set A=2/(b-a),B=1-2*b/(b-a),C=2/(b-a),D=1-2*b/(b-a);
  reconsider Kbd=Kbb as non empty Subset of TOP-REAL 2;
  defpred P[set,set] means (for t being Point of TOP-REAL 2 st t=$1 holds $2=
  |[A*(t`1)+B,C*(t`2)+D]|);
A3: for x being set st x in the carrier of TOP-REAL 2 ex y being set st P[x, y]
  proof
    let x be set;
    assume x in the carrier of TOP-REAL 2;
    then reconsider t2=x as Point of TOP-REAL 2;
    reconsider y2=|[A*(t2`1)+B,C*(t2`2)+D]| as set;
    for t being Point of TOP-REAL 2 st t=x holds y2 =|[A*(t`1)+B,C*(t`2)+D ]|;
    hence thesis;
  end;
  ex ff being Function st dom ff=the carrier of TOP-REAL 2 & for x being
set st x in the carrier of TOP-REAL 2 holds P[x,ff.x] from CLASSES1:sch 1(A3);
  then consider ff being Function such that
A4: dom ff=the carrier of TOP-REAL 2 and
A5: for x being set st x in the carrier of TOP-REAL 2 holds for t being
  Point of TOP-REAL 2 st t=x holds ff.x=|[A*(t`1)+B,C*(t`2)+D]|;
A6: for t being Point of TOP-REAL 2 holds ff.t=|[A*(t`1)+B,C*(t`2)+D]| by A5;
  for x being set st x in the carrier of TOP-REAL 2 holds ff.x in the
  carrier of TOP-REAL 2
  proof
    let x be set;
    assume x in the carrier of TOP-REAL 2;
    then reconsider t=x as Point of TOP-REAL 2;
    ff.t=|[A*(t`1)+B,C*(t`2)+D]| by A5;
    hence thesis;
  end;
  then reconsider ff as Function of TOP-REAL 2,TOP-REAL 2 by A4,FUNCT_2:3;
  reconsider f11=ff|(R^2-unit_square) as Function of (TOP-REAL 2)|
  R^2-unit_square,(TOP-REAL 2) by PRE_TOPC:9;
A7: f11 is continuous by A6,JGRAPH_2:43,TOPMETR:7;
  ff is one-to-one
  proof
    let x1,x2 be set;
    assume that
A8: x1 in dom ff & x2 in dom ff and
A9: ff.x1=ff.x2;
    reconsider p1=x1,p2=x2 as Point of TOP-REAL 2 by A8;
A10: ff.x1= |[A*(p1`1)+B,C*(p1`2)+D]| & ff.x2= |[A*(p2`1)+B,C*(p2`2)+D]| by A5;
    then A*(p1`1)+B-B=A*(p2`1)+B-B by A9,SPPOL_2:1;
    then A*(p1`1)/A=p2`1 by XCMPLX_1:89;
    then
A11: p1`1=p2`1 by XCMPLX_1:89;
    C*(p1`2)+D-D=C*(p2`2)+D-D by A9,A10,SPPOL_2:1;
    then C*(p1`2)/C=p2`2 by XCMPLX_1:89;
    hence thesis by A11,TOPREAL3:6,XCMPLX_1:89;
  end;
  then
A12: f11 is one-to-one by FUNCT_1:52;
A13: dom f11=(dom ff)/\ (R^2-unit_square) by RELAT_1:61
    .= R^2-unit_square by A4,XBOOLE_1:28;
A14: Kbd c= rng f11
  proof
    let y be set;
    assume
A15: y in Kbd;
    then reconsider py=y as Point of TOP-REAL 2;
    set t=|[(py`1-B)/2,(py`2-D)/2]|;
A16: ex q st py=q &( -1=q`1 & -1<=q`2 & q`2<=1 or q`1=1 & -1<= q`2 & q`2<=1
    or -1=q`2 & - 1<=q`1 & q`1<=1 or 1=q`2 & -1<=q`1 & q`1<=1) by A1,A15;
    now
      per cases by A16;
      case
A17:    -1=py`1 & -1<=py`2 & py`2<=1;
        then 2-1>=py`2;
        then 2>= py`2+1 by XREAL_1:19;
        then
A18:    2/2 >= (py`2-D)/2 by XREAL_1:72;
        0-1<=py`2 by A17;
        then 0<= py`2+1 by XREAL_1:20;
        hence
        t`1 = 0 & t`2 <= 1 & t`2 >= 0 or t`1 <= 1 & t`1 >= 0 & t`2 = 1 or
t`1 <= 1 & t`1 >= 0 & t`2 = 0 or t`1 = 1 & t`2 <= 1 & t`2 >= 0 by A17,A18,
EUCLID:52;
      end;
      case
A19:    py`1=1 & -1<=py`2 & py`2<=1;
        then 2-1>=py`2;
        then 2>= py`2+1 by XREAL_1:19;
        then
A20:    2/2 >= (py`2-D)/2 by XREAL_1:72;
        0-1<=py`2 by A19;
        then 0<= py`2+1 by XREAL_1:20;
        hence
        t`1 = 0 & t`2 <= 1 & t`2 >= 0 or t`1 <= 1 & t`1 >= 0 & t`2 = 1 or
t`1 <= 1 & t`1 >= 0 & t`2 = 0 or t`1 = 1 & t`2 <= 1 & t`2 >= 0 by A19,A20,
EUCLID:52;
      end;
      case
A21:    -1=py`2 & -1<=py`1 & py`1<=1;
        then 2-1>=py`1;
        then 2>= py`1+1 by XREAL_1:19;
        then
A22:    2/2 >= (py`1-B)/2 by XREAL_1:72;
        0-1<=py`1 by A21;
        then 0<= py`1+1 by XREAL_1:20;
        hence
        t`1 = 0 & t`2 <= 1 & t`2 >= 0 or t`1 <= 1 & t`1 >= 0 & t`2 = 1 or
t`1 <= 1 & t`1 >= 0 & t`2 = 0 or t`1 = 1 & t`2 <= 1 & t`2 >= 0 by A21,A22,
EUCLID:52;
      end;
      case
A23:    1=py`2 & -1<=py`1 & py`1<=1;
        then 2-1>=py`1;
        then 2>= py`1+1 by XREAL_1:19;
        then
A24:    2/2 >= (py`1-B)/2 by XREAL_1:72;
        0-1<=py`1 by A23;
        then 0<= py`1+1 by XREAL_1:20;
        hence
        t`1 = 0 & t`2 <= 1 & t`2 >= 0 or t`1 <= 1 & t`1 >= 0 & t`2 = 1 or
t`1 <= 1 & t`1 >= 0 & t`2 = 0 or t`1 = 1 & t`2 <= 1 & t`2 >= 0 by A23,A24,
EUCLID:52;
      end;
    end;
    then
A25: t in (R^2-unit_square) by TOPREAL1:14;
    t`1=(py`1-B)/2 & t`2=(py`2-D)/2 by EUCLID:52;
    then py=|[A*(t`1)+B,C*(t`2)+D]| by EUCLID:53;
    then py=ff.t by A5
      .=f11.t by A25,FUNCT_1:49;
    hence thesis by A13,A25,FUNCT_1:def 3;
  end;
  rng f11 c= Kbd
  proof
    let y be set;
    assume y in rng f11;
    then consider x being set such that
A26: x in dom f11 and
A27: y=f11.x by FUNCT_1:def 3;
    reconsider t=x as Point of TOP-REAL 2 by A13,A26;
A28: y=ff.t by A13,A26,A27,FUNCT_1:49
      .= |[A*(t`1)+B,C*(t`2)+D]| by A5;
    then reconsider qy=y as Point of TOP-REAL 2;
A29: ex p st t=p &( p`1 = 0 & p`2 <= 1 & p`2 >= 0 or p`1 <= 1 & p`1 >= 0 &
p`2 = 1 or p`1 <= 1 & p`1 >= 0 & p`2 = 0 or p`1 = 1 & p`2 <= 1 & p `2 >= 0) by
A13,A26,TOPREAL1:14;
    now
      per cases by A29;
      suppose
A30:    t`1 = 0 & t`2 <= 1 & t`2 >= 0;
A31:    qy`2=2*(t`2)-1 by A28,EUCLID:52;
        2*1>=2*t`2 by A30,XREAL_1:64;
        then
A32:    1+1-1>=qy`2+1-1 by A31,XREAL_1:9;
        0-1<=qy`2+1-1 by A30,A31,XREAL_1:9;
        hence
        -1=qy`1 & -1<=qy`2 & qy`2<=1 or qy`1=1 & -1<=qy`2 & qy`2<=1 or -1
=qy`2 & -1<=qy`1 & qy`1<=1 or 1=qy`2 & -1<=qy`1 & qy`1<=1 by A28,A30,A32,
EUCLID:52;
      end;
      suppose
A33:    t`1 <= 1 & t`1 >= 0 & t`2 = 1;
A34:    qy`1=2*(t`1)-1 by A28,EUCLID:52;
        2*1>=2*t`1 by A33,XREAL_1:64;
        then
A35:    1+1-1>=qy`1+1-1 by A34,XREAL_1:9;
        0-1<=qy`1+1-1 by A33,A34,XREAL_1:9;
        hence
        -1=qy`1 & -1<=qy`2 & qy`2<=1 or qy`1=1 & -1<=qy`2 & qy`2<=1 or -1
=qy`2 & -1<=qy`1 & qy`1<=1 or 1=qy`2 & -1<=qy`1 & qy`1<=1 by A28,A33,A35,
EUCLID:52;
      end;
      suppose
A36:    t`1 <= 1 & t`1 >= 0 & t`2 = 0;
A37:    qy`1=2*(t`1)-1 by A28,EUCLID:52;
        2*1>=2*t`1 by A36,XREAL_1:64;
        then
A38:    1+1-1>=qy`1+1-1 by A37,XREAL_1:9;
        0-1<=qy`1+1-1 by A36,A37,XREAL_1:9;
        hence
        -1=qy`1 & -1<=qy`2 & qy`2<=1 or qy`1=1 & -1<=qy`2 & qy`2<=1 or -1
=qy`2 & -1<=qy`1 & qy`1<=1 or 1=qy`2 & -1<=qy`1 & qy`1<=1 by A28,A36,A38,
EUCLID:52;
      end;
      suppose
A39:    t`1 = 1 & t`2 <= 1 & t`2 >= 0;
A40:    qy`2=2*(t`2)-1 by A28,EUCLID:52;
        2*1>=2*t`2 by A39,XREAL_1:64;
        then
A41:    1+1-1>=qy`2+1-1 by A40,XREAL_1:9;
        0-1<=qy`2+1-1 by A39,A40,XREAL_1:9;
        hence
        -1=qy`1 & -1<=qy`2 & qy`2<=1 or qy`1=1 & -1<=qy`2 & qy`2<=1 or -1
=qy`2 & -1<=qy`1 & qy`1<=1 or 1=qy`2 & -1<=qy`1 & qy`1<=1 by A28,A39,A41,
EUCLID:52;
      end;
    end;
    hence thesis by A1;
  end;
  then Kbd=rng f11 by A14,XBOOLE_0:def 10;
  then consider f1 being Function of X,((TOP-REAL 2)|Kbd) such that
  f11=f1 and
A42: f1 is being_homeomorphism by A7,A12,JGRAPH_1:46;
  dom f=[#]((TOP-REAL 2)|Kb) by A1,TOPS_2:def 5
    .=Kb by PRE_TOPC:def 5;
  then f.(|[1,0]|) in rng f by A1,A2,FUNCT_1:3;
  then reconsider PP=P as non empty Subset of TOP-REAL 2;
  reconsider g=f as Function of (TOP-REAL 2)|Kbb,(TOP-REAL 2)|PP;
  reconsider g as Function of (TOP-REAL 2)|Kbb,(TOP-REAL 2)|PP;
  reconsider f22=f1 as Function of X,((TOP-REAL 2)|Kbb);
  reconsider h=g*f22 as Function of (TOP-REAL 2)|R^2-unit_square,(TOP-REAL 2)|
  PP;
  h is being_homeomorphism by A1,A42,TOPS_2:57;
  hence thesis by TOPREAL2:def 1;
end;

theorem Th25:
  for Kb being Subset of TOP-REAL 2 st Kb={q: -1=q`1 & -1<=q`2 & q
`2<=1 or q`1=1 & -1<=q`2 & q`2<=1 or -1=q`2 & -1<=q`1 & q`1<=1 or 1=q`2 & -1<=q
  `1 & q`1<=1} holds Kb is being_simple_closed_curve & Kb is compact
proof
  set v= |[1,0]|;
  let Kb be Subset of TOP-REAL 2;
  assume
A1: Kb={q: -1=q`1 & -1<=q`2 & q`2<=1 or q`1=1 & -1<=q`2 & q`2<=1 or -1=q
  `2 & -1<=q`1 & q`1<=1 or 1=q`2 & -1<=q`1 & q`1<=1};
  v`1=1 & v`2=0 by EUCLID:52;
  then
  |[1,0]| in {q: -1=q`1 & -1<=q`2 & q`2<=1 or q`1=1 & -1<=q`2 & q`2<=1 or
  -1=q`2 & -1<=q`1 & q`1<=1 or 1=q`2 & -1<=q`1 & q`1<=1};
  then reconsider Kbd=Kb as non empty Subset of TOP-REAL 2 by A1;
  set P=Kb;
  id ((TOP-REAL 2)|Kbd) is being_homeomorphism;
  hence Kb is being_simple_closed_curve by A1,Th24;
  then consider
  f being Function of (TOP-REAL 2)|R^2-unit_square, (TOP-REAL 2)|P
  such that
A2: f is being_homeomorphism by TOPREAL2:def 1;
  per cases;
  suppose
A3: P is empty;
    Kbd <>{};
    hence thesis by A3;
  end;
  suppose
    P is non empty;
    then reconsider R = P as non empty Subset of TOP-REAL 2;
    f is continuous & rng f = [#]((TOP-REAL 2)|P) by A2,TOPS_2:def 5;
    then (TOP-REAL 2)|R is compact by COMPTS_1:14;
    hence thesis by COMPTS_1:3;
  end;
end;

theorem
  for Cb being Subset of TOP-REAL 2 st Cb={p where p is Point of
  TOP-REAL 2: |.p.|=1} holds Cb is being_simple_closed_curve
proof
  defpred P[Point of TOP-REAL 2] means -1=$1`1 & -1<=$1`2 & $1`2<=1 or $1`1=1
& -1<=$1`2 & $1`2<=1 or -1=$1`2 & -1<=$1`1 & $1`1<=1 or 1=$1`2 & -1<=$1`1 & $1
  `1<=1;
A1: (|[1,0]|)`1=1 & (|[1,0]|)`2=0 by EUCLID:52;
A2: dom Sq_Circ = the carrier of TOP-REAL 2 by FUNCT_2:def 1;
  set v= |[1,0]|;
  let Cb be Subset of TOP-REAL 2;
  assume
A3: Cb={p where p is Point of TOP-REAL 2: |.p.|=1};
  v`1=1 & v`2=0 by EUCLID:52;
  then
A4: |[1,0]| in {q: -1=q`1 & -1<=q`2 & q`2<=1 or q`1=1 & -1<=q`2 & q`2<=1 or
  -1=q`2 & -1<=q`1 & q`1<=1 or 1=q`2 & -1<=q`1 & q`1<=1};
  {q where q is Element of TOP-REAL 2: P[q]} is Subset of TOP-REAL 2 from
  DOMAIN_1:sch 7;
  then reconsider
  Kb= {q: -1=q`1 & -1<=q`2 & q`2<=1 or q`1=1 & -1<=q`2 & q`2<= 1 or
  -1=q`2 & -1<=q`1 & q`1<=1 or 1=q`2 & -1<=q`1 & q`1<=1} as non empty Subset of
  TOP-REAL 2 by A4;
  |.(|[1,0]|).|=sqrt(((|[1,0]|)`1)^2+((|[1,0]|)`2)^2) by JGRAPH_1:30
    .=1 by A1,SQUARE_1:18;
  then |[1,0]| in Cb by A3;
  then reconsider Cbb=Cb as non empty Subset of TOP-REAL 2;
A5: the carrier of (TOP-REAL 2)|Kb=Kb by PRE_TOPC:8;
A6: dom (Sq_Circ|Kb)=(dom Sq_Circ)/\ Kb by RELAT_1:61
    .=the carrier of ((TOP-REAL 2)|Kb) by A5,A2,XBOOLE_1:28;
A7: rng (Sq_Circ|Kb) c= (Sq_Circ|Kb).:(the carrier of ((TOP-REAL 2)|Kb))
  proof
    let u be set;
    assume u in rng (Sq_Circ|Kb);
    then ex z being set st z in dom ((Sq_Circ|Kb)) & u=(Sq_Circ| Kb).z by
FUNCT_1:def 3;
    hence thesis by A6,FUNCT_1:def 6;
  end;
  (Sq_Circ|Kb).: (the carrier of ((TOP-REAL 2)|Kb)) = Sq_Circ.:Kb by A5,
RELAT_1:129
    .= Cb by A3,Th23
    .=the carrier of (TOP-REAL 2)|Cbb by PRE_TOPC:8;
  then reconsider
  f0=Sq_Circ|Kb as Function of (TOP-REAL 2)|Kb, (TOP-REAL 2)|Cbb by A6,A7,
FUNCT_2:2;
  rng (Sq_Circ|Kb) c= the carrier of (TOP-REAL 2);
  then reconsider f00=f0 as Function of (TOP-REAL 2)|Kb,TOP-REAL 2 by A6,
FUNCT_2:2;
A8: f0 is one-to-one & Kb is compact by Th25,FUNCT_1:52;
  rng f0 = (Sq_Circ|Kb).: (the carrier of ((TOP-REAL 2)|Kb)) by RELSET_1:22
    .= Sq_Circ.:Kb by A5,RELAT_1:129
    .= Cb by A3,Th23;
  then
  ex f1 being Function of (TOP-REAL 2)|Kb,(TOP-REAL 2)|Cbb st f00=f1 & f1
  is being_homeomorphism by A8,Th21,JGRAPH_1:46,TOPMETR:7;
  hence thesis by Th24;
end;

begin :: The Fashoda Meet Theorem for the Circle

theorem
  for K0,C0 being Subset of TOP-REAL 2 st K0={p: -1<=p`1 & p`1<=1 & -1<=
  p`2 & p`2<=1} & C0={p1 where p1 is Point of TOP-REAL 2: |.p1.|<=1} holds
  Sq_Circ"(C0) c= K0
proof
  let K0,C0 be Subset of TOP-REAL 2;
  assume
A1: K0={p: -1<=p`1 & p`1<=1 & -1<=p`2 & p`2<=1} & C0={p1 where p1 is
  Point of TOP-REAL 2: |.p1.|<=1};
  let x be set;
  assume
A2: x in Sq_Circ"(C0);
  then reconsider px=x as Point of TOP-REAL 2;
  set q=px;
A3: Sq_Circ.x in C0 by A2,FUNCT_1:def 7;
  now
    per cases;
    case
      q=0.TOP-REAL 2;
      hence -1<=px`1 & px`1<=1 & -1<=px`2 & px`2<=1 by JGRAPH_2:3;
    end;
    case
A4:   q<>0.TOP-REAL 2 & (q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2<=-q`1);
A5:   now
        assume ((px`1)^2+(px`2)^2)=0;
        then px`1=0 & px`2=0 by COMPLEX1:1;
        hence contradiction by A4,EUCLID:53,54;
      end;
A6:   (px`1)^2 >=0 by XREAL_1:63;
A7:   now
        assume
A8:     px`1=0;
        then px`2=0 by A4;
        hence contradiction by A4,A8,EUCLID:53,54;
      end;
A9:   (|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]|)`1 = q`1/sqrt(1
+(q `2/q `1)^2) & (|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]|)`2 = q`2
      /sqrt(1+ (q `2/q`1)^2) by EUCLID:52;
      consider p1 being Point of TOP-REAL 2 such that
A10:  p1=Sq_Circ.q and
A11:  |.p1.|<=1 by A1,A3;
      (|.p1.|)^2<= |.p1.| by A11,SQUARE_1:42;
      then
A12:  (|.p1.|)^2<=1 by A11,XXREAL_0:2;
A13:  1+(q`2/q`1)^2>0 by Lm1;
      Sq_Circ.q=|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]| by A4,Def1;
      then (|.p1.|)^2= (q`1/sqrt(1+(q`2/q`1)^2))^2+(q`2/sqrt(1+(q`2/q`1)^2))
      ^2 by A9,A10,JGRAPH_1:29
        .= (q`1)^2/(sqrt(1+(q`2/q`1)^2))^2+(q`2/sqrt(1+(q`2/q`1)^2))^2 by
XCMPLX_1:76
        .= (q`1)^2/(sqrt(1+(q`2/q`1)^2))^2+(q`2)^2/(sqrt(1+(q`2/q`1)^2))^2
      by XCMPLX_1:76
        .= (q`1)^2/(1+(q`2/q`1)^2)+(q`2)^2/(sqrt(1+(q`2/q`1)^2))^2 by A13,
SQUARE_1:def 2
        .= (q`1)^2/(1+(q`2/q`1)^2)+(q`2)^2/(1+(q`2/q`1)^2) by A13,
SQUARE_1:def 2
        .= ((q`1)^2+(q`2)^2)/(1+(q`2/q`1)^2) by XCMPLX_1:62;
      then
      ((q`1)^2+(q`2)^2)/(1+(q`2/q`1)^2)*(1+(q`2/q`1)^2)<=1 *(1+(q`2/q`1 )
      ^2) by A13,A12,XREAL_1:64;
      then ((q`1)^2+(q`2)^2)<=(1+(q`2/q`1)^2) by A13,XCMPLX_1:87;
      then (px`1)^2+(px`2)^2<=1+(px`2)^2/(px`1)^2 by XCMPLX_1:76;
      then (px`1)^2+(px`2)^2-1<=1+(px`2)^2/(px`1)^2-1 by XREAL_1:9;
      then ((px`1)^2+(px`2)^2-1)*(px`1)^2<=(px`2)^2/(px`1)^2*(px`1)^2 by A6,
XREAL_1:64;
      then (px`1)^2*(px`1)^2+((px`2)^2-1)*(px`1)^2<=(px`2)^2 by A7,XCMPLX_1:6
,87;
      then (px`1)^2*(px`1)^2-(px`1)^2*1+(px`1)^2*(px`2)^2-1 *(px`2)^2<=0 by
XREAL_1:47;
      then
A14:  ((px`1)^2-1)*((px`1)^2+(px`2)^2)<=0;
      (px`2)^2>=0 by XREAL_1:63;
      then
A15:  (px`1)^2-1<=0 by A6,A14,A5,XREAL_1:129;
      then
A16:  px`1<=1 by SQUARE_1:43;
A17:  -1<=px`1 by A15,SQUARE_1:43;
      then q`2<=1 & --q`1>=-q`2 or q`2>=-1 & -q`2>=--q`1 by A4,A16,XREAL_1:24
,XXREAL_0:2;
      then q`2<=1 & 1>=-q`2 or q`2>=-1 & -q`2>=q`1 by A16,XXREAL_0:2;
      then q`2<=1 & -1<=--q`2 or q`2>=-1 & -q`2>=-1 by A17,XREAL_1:24
,XXREAL_0:2;
      hence -1<=px`1 & px`1<=1 & -1<=px`2 & px`2<=1 by A15,SQUARE_1:43
,XREAL_1:24;
    end;
    case
A18:  q<>0.TOP-REAL 2 & not (q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2<= -q`1);
A19:  now
        assume ((px`2)^2+(px`1)^2)=0;
        then px`2=0 by COMPLEX1:1;
        hence contradiction by A18;
      end;
A20:  (px`2)^2 >=0 by XREAL_1:63;
A21:  px`2<>0 by A18;
A22:  (|[q`1/sqrt(1+(q`1/q`2)^2),q`2/sqrt(1+(q`1/q`2)^2)]|)`2 = q`2/sqrt(
1+(q `1/q `2)^2) & (|[q`1/sqrt(1+(q`1/q`2)^2),q`2/sqrt(1+(q`1/q`2)^2)]|)`1 = q
      `1/sqrt(1+ (q `1/q`2)^2) by EUCLID:52;
      consider p1 being Point of TOP-REAL 2 such that
A23:  p1=Sq_Circ.q and
A24:  |.p1.|<=1 by A1,A3;
      (|.p1.|)^2<= |.p1.| by A24,SQUARE_1:42;
      then
A25:  (|.p1.|)^2<=1 by A24,XXREAL_0:2;
A26:  1+(q`1/q`2)^2>0 by Lm1;
      Sq_Circ.q=|[q`1/sqrt(1+(q`1/q`2)^2),q`2/sqrt(1+(q`1/q`2)^2)]| by A18,Def1
;
      then (|.p1.|)^2= (q`1/sqrt(1+(q`1/q`2)^2))^2+(q`2/sqrt(1+(q`1/q`2)^2))
      ^2 by A22,A23,JGRAPH_1:29
        .= (q`2)^2/(sqrt(1+(q`1/q`2)^2))^2+(q`1/sqrt(1+(q`1/q`2)^2))^2 by
XCMPLX_1:76
        .= (q`2)^2/(sqrt(1+(q`1/q`2)^2))^2+(q`1)^2/(sqrt(1+(q`1/q`2)^2))^2
      by XCMPLX_1:76
        .= (q`2)^2/(1+(q`1/q`2)^2)+(q`1)^2/(sqrt(1+(q`1/q`2)^2))^2 by A26,
SQUARE_1:def 2
        .= (q`2)^2/(1+(q`1/q`2)^2)+(q`1)^2/(1+(q`1/q`2)^2) by A26,
SQUARE_1:def 2
        .= ((q`2)^2+(q`1)^2)/(1+(q`1/q`2)^2) by XCMPLX_1:62;
      then
      ((q`2)^2+(q`1)^2)/(1+(q`1/q`2)^2)*(1+(q`1/q`2)^2)<=1 *(1+(q`1/q`2 )
      ^2) by A26,A25,XREAL_1:64;
      then ((q`2)^2+(q`1)^2)<=(1+(q`1/q`2)^2) by A26,XCMPLX_1:87;
      then (px`2)^2+(px`1)^2<=1+(px`1)^2/(px`2)^2 by XCMPLX_1:76;
      then (px`2)^2+(px`1)^2-1<=1+(px`1)^2/(px`2)^2-1 by XREAL_1:9;
      then ((px`2)^2+(px`1)^2-1)*(px`2)^2<=(px`1)^2/(px`2)^2*(px`2)^2 by A20,
XREAL_1:64;
      then (px`2)^2*(px`2)^2+((px`1)^2-1)*(px`2)^2<=(px`1)^2 by A21,XCMPLX_1:6
,87;
      then (px`2)^2*(px`2)^2-(px`2)^2*1+(px`2)^2*(px`1)^2-1 *(px`1)^2<=0 by
XREAL_1:47;
      then
A27:  ((px`2)^2-1)*((px`2)^2+(px`1)^2)<=0;
      (px`1)^2>=0 by XREAL_1:63;
      then
A28:  (px`2)^2-1<=0 by A20,A27,A19,XREAL_1:129;
      then -1<=px`2 & px`2<=1 by SQUARE_1:43;
      then q`1<=1 & 1>=-q`1 or q`1>=-1 & -q`1>=-1 by A18,XXREAL_0:2;
      then q`1<=1 & -1<=--q`1 or q`1>=-1 & q`1<=1 by XREAL_1:24;
      hence -1<=px`1 & px`1<=1 & -1<=px`2 & px`2<=1 by A28,SQUARE_1:43;
    end;
  end;
  hence thesis by A1;
end;

theorem Th28:
  for p holds (p=0.TOP-REAL 2 implies Sq_Circ".p=0.TOP-REAL 2) & (
  (p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) & p<>0.TOP-REAL 2 implies
Sq_Circ".p=|[p`1*sqrt(1+(p`2/p`1)^2),p`2*sqrt(1+(p`2/p`1)^2)]|)& (not(p`2<=p`1
& -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) implies Sq_Circ".p=|[p`1*sqrt(1+(p`1/p`2)
  ^2),p`2*sqrt(1+(p`1/p`2)^2)]|)
proof
  let p;
  set q=p;
  set px=|[q`1*sqrt(1+(q`1/q`2)^2),q`2*sqrt(1+(q`1/q`2)^2)]|;
A1: px`2 = q`2*sqrt(1+(q`1/q`2)^2) by EUCLID:52;
A2: dom Sq_Circ=the carrier of TOP-REAL 2 by FUNCT_2:def 1;
  hereby
    assume
A3: p=0.TOP-REAL 2;
    then Sq_Circ.p=p by Def1;
    hence Sq_Circ".p=0.TOP-REAL 2 by A2,A3,FUNCT_1:34;
  end;
  hereby
A4: dom Sq_Circ=the carrier of TOP-REAL 2 by FUNCT_2:def 1;
    set q=p;
    assume that
A5: p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1 and
A6: p<>0.TOP-REAL 2;
    set px=|[q`1*sqrt(1+(q`2/q`1)^2),q`2*sqrt(1+(q`2/q`1)^2)]|;
A7: px`1 = q`1*sqrt(1+(q`2/q`1)^2) by EUCLID:52;
A8: sqrt(1+(q`2/q`1)^2)>0 by Lm1,SQUARE_1:25;
A9: px`2 = q`2*sqrt(1+(q`2/q`1)^2) by EUCLID:52;
    then
A10: px`2/px`1=q`2/q`1 by A7,A8,XCMPLX_1:91;
    then
A11: px`2/sqrt(1+(px`2/px`1)^2)=q`2 by A9,A8,XCMPLX_1:89;
A12: now
      assume px`1=0 & px`2=0;
      then q`1=0 & q`2=0 by A7,A9,A8,XCMPLX_1:6;
      hence contradiction by A6,EUCLID:53,54;
    end;
    q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2*sqrt(1+(q`2/q`1)^2) <= (-q`1)*
    sqrt(1+(q`2/q`1)^2) by A5,A8,XREAL_1:64;
    then q`2<=q`1 & (-q`1)*sqrt(1+(q`2/q`1)^2) <= q`2*sqrt(1+(q`2/q`1)^2) or
    px`2>=px`1 & px`2<=-px`1 by A7,A9,A8,XREAL_1:64;
    then q`2*sqrt(1+(q`2/q`1)^2) <= q`1*sqrt(1+(q`2/q`1)^2) & -px`1<=px`2 or
    px`2>=px`1 & px`2<=-px`1 by A7,A8,EUCLID:52,XREAL_1:64;
    then
A13: Sq_Circ.px=|[px`1/sqrt(1+(px`2/px`1)^2),px`2/sqrt(1+(px`2/px`1)^2 )
    ]| by A7,A9,A12,Def1,JGRAPH_2:3;
    px`1/sqrt(1+(px`2/px`1)^2)=q`1 by A7,A8,A10,XCMPLX_1:89;
    then q=Sq_Circ.px by A13,A11,EUCLID:53;
    hence (Sq_Circ").p=|[p`1*sqrt(1+(p`2/p`1)^2),p`2*sqrt(1+(p`2/p`1)^2)]| by
A4,FUNCT_1:34;
  end;
A14: dom Sq_Circ=the carrier of TOP-REAL 2 by FUNCT_2:def 1;
A15: sqrt(1+(q`1/q`2)^2)>0 by Lm1,SQUARE_1:25;
A16: px`1 = q`1*sqrt(1+(q`1/q`2)^2) by EUCLID:52;
  then
A17: px`1/px`2=q`1/q`2 by A1,A15,XCMPLX_1:91;
  then
A18: px`1/sqrt(1+(px`1/px`2)^2)=q`1 by A16,A15,XCMPLX_1:89;
  assume
A19: not(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1);
A20: now
    assume that
A21: px`2=0 and
    px`1=0;
    q`2=0 by A1,A15,A21,XCMPLX_1:6;
    hence contradiction by A19;
  end;
  p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2 by A19,JGRAPH_2:13;
  then q`1<=q`2 & -q`2<=q`1 or q`1>=q`2 & q`1*sqrt(1+(q`1/q`2)^2) <= (-q`2)*
  sqrt(1+(q`1/q`2)^2) by A15,XREAL_1:64;
  then q`1<=q`2 & (-q`2)*sqrt(1+(q`1/q`2)^2) <= q`1*sqrt(1+(q`1/q`2)^2) or px
  `1>=px`2 & px`1<=-px`2 by A1,A16,A15,XREAL_1:64;
  then q`1*sqrt(1+(q`1/q`2)^2) <= q`2*sqrt(1+(q`1/q`2)^2) & -px`2<=px`1 or px
  `1>=px`2 & px`1<=-px`2 by A1,A15,EUCLID:52,XREAL_1:64;
  then
A22: Sq_Circ.px=|[px`1/sqrt(1+(px`1/px`2)^2),px`2/sqrt(1+(px`1/px`2)^2) ]|
  by A1,A16,A20,Th4,JGRAPH_2:3;
  px`2/sqrt(1+(px`1/px`2)^2)=q`2 by A1,A15,A17,XCMPLX_1:89;
  then q=Sq_Circ.px by A22,A18,EUCLID:53;
  hence thesis by A14,FUNCT_1:34;
end;

theorem Th29:
  Sq_Circ" is Function of TOP-REAL 2,TOP-REAL 2
proof
A1: the carrier of TOP-REAL 2 c= rng Sq_Circ
  proof
    let y be set;
    assume y in the carrier of TOP-REAL 2;
    then reconsider py=y as Point of TOP-REAL 2;
A2: dom Sq_Circ=the carrier of TOP-REAL 2 by FUNCT_2:def 1;
    now
      per cases;
      case
        py=0.TOP-REAL 2;
        then Sq_Circ.py=py by Def1;
        hence ex x being set st x in dom Sq_Circ & y=Sq_Circ.x by A2;
      end;
      case
A3:     (py`2<=py`1 & -py`1<=py`2 or py`2>=py`1 & py`2<=-py`1) & py<>
        0.TOP-REAL 2;
        set q=py;
        set px=|[q`1*sqrt(1+(q`2/q`1)^2),q`2*sqrt(1+(q`2/q`1)^2)]|;
A4:     sqrt(1+(q`2/q`1)^2)>0 by Lm1,SQUARE_1:25;
A5:     now
          assume that
A6:       px`1=0 and
A7:       px`2=0;
          q`2*sqrt(1+(q`2/q`1)^2)=0 by A7,EUCLID:52;
          then
A8:       q`2=0 by A4,XCMPLX_1:6;
          q`1*sqrt(1+(q`2/q`1)^2)=0 by A6,EUCLID:52;
          then q`1=0 by A4,XCMPLX_1:6;
          hence contradiction by A3,A8,EUCLID:53,54;
        end;
A9:     dom Sq_Circ=the carrier of TOP-REAL 2 by FUNCT_2:def 1;
A10:    px`1 = q`1*sqrt(1+(q`2/q`1)^2) by EUCLID:52;
A11:    px`2 = q`2*sqrt(1+(q`2/q`1)^2) by EUCLID:52;
        then
A12:    px`2/px`1=q`2/q`1 by A10,A4,XCMPLX_1:91;
        then
A13:    px`2/sqrt(1+(px`2/px`1)^2)=q`2 by A11,A4,XCMPLX_1:89;
        q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2*sqrt(1+(q`2/q`1)^2) <= (-q
        `1)*sqrt(1+(q`2/q`1)^2) by A3,A4,XREAL_1:64;
        then q`2<=q`1 & (-q`1)*sqrt(1+(q`2/q`1)^2) <= q`2*sqrt(1+(q`2/q`1)^2)
        or px`2>=px`1 & px`2<=-px`1 by A10,A11,A4,XREAL_1:64;
        then q`2*sqrt(1+(q`2/q`1)^2) <= q`1*sqrt(1+(q`2/q`1)^2) & -px`1<=px`2
        or px`2>=px`1 & px`2<=-px`1 by A10,A4,EUCLID:52,XREAL_1:64;
        then
A14:    Sq_Circ.px=|[px`1/sqrt(1+(px`2/px`1)^2),px`2/sqrt(1+(px`2/ px`1)
        ^2) ]| by A10,A11,A5,Def1,JGRAPH_2:3;
        px`1/sqrt(1+(px`2/px`1)^2)=q`1 by A10,A4,A12,XCMPLX_1:89;
        hence ex x being set st x in dom Sq_Circ & y=Sq_Circ.x by A14,A13,A9,
EUCLID:53;
      end;
      case
A15:    not(py`2<=py`1 & -py`1<=py`2 or py`2>=py`1 & py`2<=-py`1)& py
        <>0.TOP-REAL 2;
        set q=py;
        set px=|[q`1*sqrt(1+(q`1/q`2)^2),q`2*sqrt(1+(q`1/q`2)^2)]|;
A16:    sqrt(1+(q`1/q`2)^2)>0 by Lm1,SQUARE_1:25;
A17:    px`2 = q`2*sqrt(1+(q`1/q`2)^2) by EUCLID:52;
A18:    now
          assume that
A19:      px`2=0 and
          px`1=0;
          q`2=0 by A17,A16,A19,XCMPLX_1:6;
          hence contradiction by A15;
        end;
A20:    px`1 = q`1*sqrt(1+(q`1/q`2)^2) by EUCLID:52;
        then
A21:    px`1/px`2=q`1/q`2 by A17,A16,XCMPLX_1:91;
        then
A22:    px`1/sqrt(1+(px`1/px`2)^2)=q`1 by A20,A16,XCMPLX_1:89;
        py`1<=py`2 & -py`2<=py`1 or py`1>=py`2 & py`1<=-py`2 by A15,JGRAPH_2:13
;
        then
        q`1<=q`2 & -q`2<=q`1 or q`1>=q`2 & q`1*sqrt(1+(q`1/q`2)^2) <= (-q
        `2)*sqrt(1+(q`1/q`2)^2) by A16,XREAL_1:64;
        then q`1<=q`2 & (-q`2)*sqrt(1+(q`1/q`2)^2) <= q`1*sqrt(1+(q`1/q`2)^2)
        or px`1>=px`2 & px`1<=-px`2 by A17,A20,A16,XREAL_1:64;
        then q`1*sqrt(1+(q`1/q`2)^2) <= q`2*sqrt(1+(q`1/q`2)^2) & -px`2<=px`1
        or px`1>=px`2 & px`1<=-px`2 by A17,A16,EUCLID:52,XREAL_1:64;
        then
A23:    Sq_Circ.px=|[px`1/sqrt(1+(px`1/px`2)^2),px`2/sqrt(1+(px`1/ px`2)
        ^2 )]| by A17,A20,A18,Th4,JGRAPH_2:3;
A24:    dom Sq_Circ=the carrier of TOP-REAL 2 by FUNCT_2:def 1;
        px`2/sqrt(1+(px`1/px`2)^2)=q`2 by A17,A16,A21,XCMPLX_1:89;
        hence ex x being set st x in dom Sq_Circ & y=Sq_Circ.x by A23,A22,A24,
EUCLID:53;
      end;
    end;
    hence thesis by FUNCT_1:def 3;
  end;
A25: rng (Sq_Circ")=dom (Sq_Circ) by FUNCT_1:33
    .=the carrier of TOP-REAL 2 by FUNCT_2:def 1;
  dom (Sq_Circ")=rng (Sq_Circ) by FUNCT_1:33;
  then dom (Sq_Circ")=the carrier of TOP-REAL 2 by A1,XBOOLE_0:def 10;
  hence thesis by A25,FUNCT_2:1;
end;

theorem Th30:
  for p being Point of TOP-REAL 2 st p<>0.TOP-REAL 2 holds ((p`1<=
p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2) implies (Sq_Circ").p=|[p`1*sqrt(1+(p`1
/p`2)^2),p`2*sqrt(1+(p`1/p`2)^2)]|) & (not(p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p
`1<=-p`2) implies (Sq_Circ").p=|[p`1*sqrt(1+(p`2/p`1)^2),p`2*sqrt(1+(p`2/p`1)^2
  )]|)
proof
  let p be Point of TOP-REAL 2;
A1: -p`2<p`1 implies --p`2>-p`1 by XREAL_1:24;
  assume
A2: p<>0.TOP-REAL 2;
  hereby
    assume
A3: p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2;
    now
      per cases by A3;
      case
A4:     p`1<=p`2 & -p`2<=p`1;
        now
          assume
A5:       p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1;
A6:       now
            per cases by A5;
            case
              p`2<=p`1 & -p`1<=p`2;
              hence p`1=p`2 or p`1=-p`2 by A4,XXREAL_0:1;
            end;
            case
              p`2>=p`1 & p`2<=-p`1;
              then -p`2>=--p`1 by XREAL_1:24;
              hence p`1=p`2 or p`1=-p`2 by A4,XXREAL_0:1;
            end;
          end;
          now
            per cases by A6;
            case
              p`1=p`2;
              hence
              (Sq_Circ").p =|[p`1*sqrt(1+(p`1/p`2)^2),p`2*sqrt(1+(p`1/p`2
              )^2)]| by A2,A5,Th28;
            end;
            case
              p`1=-p`2;
              then p`1<>0 & -p`1=p`2 by A2,EUCLID:53,54;
              then p`1/p`2=-1 & p`2/p`1=-1 by XCMPLX_1:197,198;
              hence
              (Sq_Circ").p=|[p`1*sqrt(1+(p`1/p`2)^2),p`2*sqrt(1+(p`1/p`2)
              ^2) ]| by A2,A5,Th28;
            end;
          end;
          hence (Sq_Circ").p=|[p`1*sqrt(1+(p`1/p`2)^2),p`2*sqrt(1+(p`1/p`2)^2)
          ]|;
        end;
        hence (Sq_Circ").p=|[p`1*sqrt(1+(p`1/p`2)^2),p`2*sqrt(1+(p`1/p`2)^2)]|
        by Th28;
      end;
      case
A7:     p`1>=p`2 & p`1<=-p`2;
        now
          assume
A8:       p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1;
A9:       now
            per cases by A8;
            case
              p`2<=p`1 & -p`1<=p`2;
              then --p`1>=-p`2 by XREAL_1:24;
              hence p`1=p`2 or p`1=-p`2 by A7,XXREAL_0:1;
            end;
            case
              p`2>=p`1 & p`2<=-p`1;
              hence p`1=p`2 or p`1=-p`2 by A7,XXREAL_0:1;
            end;
          end;
          now
            per cases by A9;
            case
              p`1=p`2;
              hence
              (Sq_Circ").p=|[p`1*sqrt(1+(p`1/p`2)^2),p`2*sqrt(1+(p`1/p`2)
              ^2) ]| by A2,A8,Th28;
            end;
            case
A10:          p`1=-p`2;
              then p`1<>0 & -p`1=p`2 by A2,EUCLID:53,54;
              then
A11:          p`2/p`1=-1 by XCMPLX_1:197;
              p`2<>0 by A2,A10,EUCLID:53,54;
              then p`1/p`2=-1 by A10,XCMPLX_1:197;
              hence
              (Sq_Circ").p=|[p`1*sqrt(1+(p`1/p`2)^2),p`2*sqrt(1+(p`1/p`2)
              ^2) ]| by A2,A8,A11,Th28;
            end;
          end;
          hence (Sq_Circ").p=|[p`1*sqrt(1+(p`1/p`2)^2),p`2*sqrt(1+(p`1/p`2)^2)
          ]|;
        end;
        hence (Sq_Circ").p=|[p`1*sqrt(1+(p`1/p`2)^2),p`2*sqrt(1+(p`1/p`2)^2)]|
        by Th28;
      end;
    end;
    hence (Sq_Circ").p=|[p`1*sqrt(1+(p`1/p`2)^2),p`2*sqrt(1+(p`1/p`2)^2)]|;
  end;
A12: -p`2>p`1 implies --p`2<-p`1 by XREAL_1:24;
  assume not(p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2);
  hence thesis by A2,A1,A12,Th28;
end;

theorem Th31:
  for X being non empty TopSpace, f1,f2 being Function of X,R^1 st
  f1 is continuous & f2 is continuous & (for q being Point of X holds f2.q<>0)
holds ex g being Function of X,R^1 st (for p being Point of X,r1,r2 being real
  number st f1.p=r1 & f2.p=r2 holds g.p=r1*sqrt(1+(r1/r2)^2)) & g is continuous
proof
  let X be non empty TopSpace, f1,f2 be Function of X,R^1;
  assume that
A1: f1 is continuous and
A2: f2 is continuous & for q being Point of X holds f2.q<>0;
  consider g2 being Function of X,R^1 such that
A3: for p being Point of X,r1,r2 being real number st f1.p=r1 & f2.p=r2
  holds g2.p=sqrt(1+(r1/r2)^2) and
A4: g2 is continuous by A1,A2,Th8;
  consider g3 being Function of X,R^1 such that
A5: for p being Point of X,r1,r0 being real number st f1.p=r1 & g2.p=r0
  holds g3.p=r1*r0 and
A6: g3 is continuous by A1,A4,JGRAPH_2:25;
  for p being Point of X,r1,r2 being real number st f1.p=r1 & f2.p=r2
  holds g3.p=r1*sqrt(1+(r1/r2)^2)
  proof
    let p be Point of X,r1,r2 be real number;
    assume that
A7: f1.p=r1 and
A8: f2.p=r2;
    g2.p=sqrt(1+(r1/r2)^2) by A3,A7,A8;
    hence thesis by A5,A7;
  end;
  hence thesis by A6;
end;

theorem Th32:
  for X being non empty TopSpace, f1,f2 being Function of X,R^1 st
f1 is continuous & f2 is continuous & (for q being Point of X holds f2.q<>0) ex
g being Function of X,R^1 st (for p being Point of X,r1,r2 being real number st
  f1.p=r1 & f2.p=r2 holds g.p=r2*sqrt(1+(r1/r2)^2)) & g is continuous
proof
  let X be non empty TopSpace, f1,f2 be Function of X,R^1;
  assume that
A1: f1 is continuous and
A2: f2 is continuous and
A3: for q being Point of X holds f2.q<>0;
  consider g2 being Function of X,R^1 such that
A4: for p being Point of X,r1,r2 being real number st f1.p=r1 & f2.p=r2
  holds g2.p=sqrt(1+(r1/r2)^2) and
A5: g2 is continuous by A1,A2,A3,Th8;
  consider g3 being Function of X,R^1 such that
A6: for p being Point of X,r2,r0 being real number st f2.p=r2 & g2.p=r0
  holds g3.p=r2*r0 and
A7: g3 is continuous by A2,A5,JGRAPH_2:25;
  for p being Point of X,r1,r2 being real number st f1.p=r1 & f2.p=r2
  holds g3.p=r2*sqrt(1+(r1/r2)^2)
  proof
    let p be Point of X,r1,r2 be real number;
    assume that
A8: f1.p=r1 and
A9: f2.p=r2;
    g2.p=sqrt(1+(r1/r2)^2) by A4,A8,A9;
    hence thesis by A6,A9;
  end;
  hence thesis by A7;
end;

theorem Th33:
  for K1 being non empty Subset of TOP-REAL 2, f being Function of
(TOP-REAL 2)|K1,R^1 st (for p being Point of TOP-REAL 2 st p in the carrier of
  (TOP-REAL 2)|K1 holds f.p=p`1*sqrt(1+(p`2/p`1)^2)) & (for q being Point of
  TOP-REAL 2 st q in the carrier of (TOP-REAL 2)|K1 holds q`1<>0 ) holds f is
  continuous
proof
  let K1 be non empty Subset of TOP-REAL 2, f be Function of (TOP-REAL 2)|K1,
  R^1;
  reconsider g1=proj1|K1 as continuous Function of (TOP-REAL 2)|K1,R^1 by Lm7;
  reconsider g2=proj2|K1 as continuous Function of (TOP-REAL 2)|K1,R^1 by Lm5;
  assume that
A1: for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)|
  K1 holds f.p=p`1*sqrt(1+(p`2/p`1)^2) and
A2: for q being Point of TOP-REAL 2 st q in the carrier of (TOP-REAL 2)
  |K1 holds q`1<>0;
A3: the carrier of (TOP-REAL 2)|K1=K1 by PRE_TOPC:8;
  for q being Point of (TOP-REAL 2)|K1 holds g1.q<>0
  proof
    let q be Point of (TOP-REAL 2)|K1;
    q in the carrier of (TOP-REAL 2)|K1;
    then reconsider q2=q as Point of TOP-REAL 2 by A3;
    g1.q=proj1.q by Lm6
      .=q2`1 by PSCOMP_1:def 5;
    hence thesis by A2;
  end;
  then consider g3 being Function of (TOP-REAL 2)|K1,R^1 such that
A4: for q being Point of (TOP-REAL 2)|K1,r1,r2 being real number st g2.q
  =r1 & g1.q=r2 holds g3.q=r2*sqrt(1+(r1/r2)^2) and
A5: g3 is continuous by Th32;
A6: now
    let x be set;
    assume
A7: x in dom f;
    then reconsider s=x as Point of (TOP-REAL 2)|K1;
    x in the carrier of (TOP-REAL 2)|K1 by A7;
    then x in K1 by PRE_TOPC:8;
    then reconsider r=x as Point of (TOP-REAL 2);
A8: proj2.r=r`2 & proj1.r=r`1 by PSCOMP_1:def 5,def 6;
A9: g2.s=proj2.s & g1.s=proj1.s by Lm4,Lm6;
    f.r=r`1*sqrt(1+(r`2/r`1)^2) by A1,A7;
    hence f.x=g3.x by A4,A9,A8;
  end;
  dom g3=the carrier of (TOP-REAL 2)|K1 by FUNCT_2:def 1;
  then dom f=dom g3 by FUNCT_2:def 1;
  hence thesis by A5,A6,FUNCT_1:2;
end;

theorem Th34:
  for K1 being non empty Subset of TOP-REAL 2, f being Function of
(TOP-REAL 2)|K1,R^1 st (for p being Point of TOP-REAL 2 st p in the carrier of
  (TOP-REAL 2)|K1 holds f.p=p`2*sqrt(1+(p`2/p`1)^2)) & (for q being Point of
  TOP-REAL 2 st q in the carrier of (TOP-REAL 2)|K1 holds q`1<>0) holds f is
  continuous
proof
  let K1 be non empty Subset of TOP-REAL 2, f be Function of (TOP-REAL 2)|K1,
  R^1;
  reconsider g1=proj1|K1 as continuous Function of (TOP-REAL 2)|K1,R^1 by Lm7;
  reconsider g2=proj2|K1 as continuous Function of (TOP-REAL 2)|K1,R^1 by Lm5;
  assume that
A1: for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)|
  K1 holds f.p=p`2*sqrt(1+(p`2/p`1)^2) and
A2: for q being Point of TOP-REAL 2 st q in the carrier of (TOP-REAL 2)
  |K1 holds q`1<>0;
A3: the carrier of (TOP-REAL 2)|K1=K1 by PRE_TOPC:8;
  for q being Point of (TOP-REAL 2)|K1 holds g1.q<>0
  proof
    let q be Point of (TOP-REAL 2)|K1;
    q in the carrier of (TOP-REAL 2)|K1;
    then reconsider q2=q as Point of TOP-REAL 2 by A3;
    g1.q=proj1.q by Lm6
      .=q2`1 by PSCOMP_1:def 5;
    hence thesis by A2;
  end;
  then consider g3 being Function of (TOP-REAL 2)|K1,R^1 such that
A4: for q being Point of (TOP-REAL 2)|K1,r1,r2 being real number st g2.q
  =r1 & g1.q=r2 holds g3.q=r1*sqrt(1+(r1/r2)^2) and
A5: g3 is continuous by Th31;
A6: now
    let x be set;
    assume
A7: x in dom f;
    then reconsider s=x as Point of (TOP-REAL 2)|K1;
    x in the carrier of (TOP-REAL 2)|K1 by A7;
    then x in K1 by PRE_TOPC:8;
    then reconsider r=x as Point of (TOP-REAL 2);
A8: proj2.r=r`2 & proj1.r=r`1 by PSCOMP_1:def 5,def 6;
A9: g2.s=proj2.s & g1.s=proj1.s by Lm4,Lm6;
    f.r=r`2*sqrt(1+(r`2/r`1)^2) by A1,A7;
    hence f.x=g3.x by A4,A9,A8;
  end;
  dom g3=the carrier of (TOP-REAL 2)|K1 by FUNCT_2:def 1;
  then dom f=dom g3 by FUNCT_2:def 1;
  hence thesis by A5,A6,FUNCT_1:2;
end;

theorem Th35:
  for K1 being non empty Subset of TOP-REAL 2, f being Function of
(TOP-REAL 2)|K1,R^1 st (for p being Point of TOP-REAL 2 st p in the carrier of
  (TOP-REAL 2)|K1 holds f.p=p`2*sqrt(1+(p`1/p`2)^2)) & (for q being Point of
  TOP-REAL 2 st q in the carrier of (TOP-REAL 2)|K1 holds q`2<>0 ) holds f is
  continuous
proof
  let K1 be non empty Subset of TOP-REAL 2, f be Function of (TOP-REAL 2)|K1,
  R^1;
  reconsider g1=proj1|K1 as continuous Function of (TOP-REAL 2)|K1,R^1 by Lm7;
  reconsider g2=proj2|K1 as continuous Function of (TOP-REAL 2)|K1,R^1 by Lm5;
  assume that
A1: for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)|
  K1 holds f.p=p`2*sqrt(1+(p`1/p`2)^2) and
A2: for q being Point of TOP-REAL 2 st q in the carrier of (TOP-REAL 2)
  |K1 holds q`2<>0;
A3: the carrier of (TOP-REAL 2)|K1=K1 by PRE_TOPC:8;
  for q being Point of (TOP-REAL 2)|K1 holds g2.q<>0
  proof
    let q be Point of (TOP-REAL 2)|K1;
    q in the carrier of (TOP-REAL 2)|K1;
    then reconsider q2=q as Point of TOP-REAL 2 by A3;
    g2.q=proj2.q by Lm4
      .=q2`2 by PSCOMP_1:def 6;
    hence thesis by A2;
  end;
  then consider g3 being Function of (TOP-REAL 2)|K1,R^1 such that
A4: for q being Point of (TOP-REAL 2)|K1,r1,r2 being real number st g1.q
  =r1 & g2.q=r2 holds g3.q=r2*sqrt(1+(r1/r2)^2) and
A5: g3 is continuous by Th32;
A6: now
    let x be set;
    assume
A7: x in dom f;
    then reconsider s=x as Point of (TOP-REAL 2)|K1;
    x in the carrier of (TOP-REAL 2)|K1 by A7;
    then x in K1 by PRE_TOPC:8;
    then reconsider r=x as Point of (TOP-REAL 2);
A8: proj2.r=r`2 & proj1.r=r`1 by PSCOMP_1:def 5,def 6;
A9: g2.s=proj2.s & g1.s=proj1.s by Lm4,Lm6;
    f.r=r`2*sqrt(1+(r`1/r`2)^2) by A1,A7;
    hence f.x=g3.x by A4,A9,A8;
  end;
  dom g3=the carrier of (TOP-REAL 2)|K1 by FUNCT_2:def 1;
  then dom f=dom g3 by FUNCT_2:def 1;
  hence thesis by A5,A6,FUNCT_1:2;
end;

theorem Th36:
  for K1 being non empty Subset of TOP-REAL 2, f being Function of
(TOP-REAL 2)|K1,R^1 st (for p being Point of TOP-REAL 2 st p in the carrier of
  (TOP-REAL 2)|K1 holds f.p=p`1*sqrt(1+(p`1/p`2)^2)) & (for q being Point of
  TOP-REAL 2 st q in the carrier of (TOP-REAL 2)|K1 holds q`2<>0 ) holds f is
  continuous
proof
  let K1 be non empty Subset of TOP-REAL 2, f be Function of (TOP-REAL 2)|K1,
  R^1;
  reconsider g1=proj1|K1 as continuous Function of (TOP-REAL 2)|K1,R^1 by Lm7;
  reconsider g2=proj2|K1 as continuous Function of (TOP-REAL 2)|K1,R^1 by Lm5;
  assume that
A1: for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)|
  K1 holds f.p=p`1*sqrt(1+(p`1/p`2)^2) and
A2: for q being Point of TOP-REAL 2 st q in the carrier of (TOP-REAL 2)
  |K1 holds q`2<>0;
A3: the carrier of (TOP-REAL 2)|K1=K1 by PRE_TOPC:8;
  for q being Point of (TOP-REAL 2)|K1 holds g2.q<>0
  proof
    let q be Point of (TOP-REAL 2)|K1;
    q in the carrier of (TOP-REAL 2)|K1;
    then reconsider q2=q as Point of TOP-REAL 2 by A3;
    g2.q=proj2.q by Lm4
      .=q2`2 by PSCOMP_1:def 6;
    hence thesis by A2;
  end;
  then consider g3 being Function of (TOP-REAL 2)|K1,R^1 such that
A4: for q being Point of (TOP-REAL 2)|K1,r1,r2 being real number st g1.q
  =r1 & g2.q=r2 holds g3.q=r1*sqrt(1+(r1/r2)^2) and
A5: g3 is continuous by Th31;
A6: now
    let x be set;
    assume
A7: x in dom f;
    then reconsider s=x as Point of (TOP-REAL 2)|K1;
    x in the carrier of (TOP-REAL 2)|K1 by A7;
    then x in K1 by PRE_TOPC:8;
    then reconsider r=x as Point of (TOP-REAL 2);
A8: proj2.r=r`2 & proj1.r=r`1 by PSCOMP_1:def 5,def 6;
A9: g2.s=proj2.s & g1.s=proj1.s by Lm4,Lm6;
    f.r=r`1*sqrt(1+(r`1/r`2)^2) by A1,A7;
    hence f.x=g3.x by A4,A9,A8;
  end;
  dom g3=the carrier of (TOP-REAL 2)|K1 by FUNCT_2:def 1;
  then dom f=dom g3 by FUNCT_2:def 1;
  hence thesis by A5,A6,FUNCT_1:2;
end;

Lm17: for K1 being non empty Subset of TOP-REAL 2 holds (proj2)*((Sq_Circ")|K1
) is Function of (TOP-REAL 2)|K1,R^1
proof
  let K1 be non empty Subset of TOP-REAL 2;
A1: rng ((proj2)*((Sq_Circ")|K1)) c= rng (proj2) by RELAT_1:26;
A2: dom ((Sq_Circ")|K1) c= dom ((proj2)*((Sq_Circ")|K1))
  proof
    let x be set;
A3: rng (Sq_Circ") c= the carrier of TOP-REAL 2 by Th29,RELAT_1:def 19;
    assume
A4: x in dom ((Sq_Circ")|K1);
    then x in dom (Sq_Circ") /\ K1 by RELAT_1:61;
    then x in dom (Sq_Circ") by XBOOLE_0:def 4;
    then
A5: (Sq_Circ").x in rng (Sq_Circ") by FUNCT_1:3;
    ((Sq_Circ")|K1).x=(Sq_Circ").x by A4,FUNCT_1:47;
    hence thesis by A4,A5,A3,Lm3,FUNCT_1:11;
  end;
  dom ((proj2)*((Sq_Circ")|K1)) c= dom ((Sq_Circ")|K1) by RELAT_1:25;
  then dom ((proj2)*((Sq_Circ")|K1))=dom ((Sq_Circ")|K1) by A2,XBOOLE_0:def 10
    .=dom (Sq_Circ") /\ K1 by RELAT_1:61
    .=(the carrier of TOP-REAL 2)/\ K1 by Th29,FUNCT_2:def 1
    .=K1 by XBOOLE_1:28
    .=the carrier of (TOP-REAL 2)|K1 by PRE_TOPC:8;
  hence thesis by A1,FUNCT_2:2,TOPMETR:17,XBOOLE_1:1;
end;

Lm18: for K1 being non empty Subset of TOP-REAL 2 holds (proj1)*((Sq_Circ")|K1
) is Function of (TOP-REAL 2)|K1,R^1
proof
  let K1 be non empty Subset of TOP-REAL 2;
A1: rng ((proj1)*((Sq_Circ")|K1)) c= rng (proj1) by RELAT_1:26;
A2: dom ((Sq_Circ")|K1) c= dom ((proj1)*((Sq_Circ")|K1))
  proof
    let x be set;
A3: rng (Sq_Circ") c= the carrier of TOP-REAL 2 by Th29,RELAT_1:def 19;
    assume
A4: x in dom ((Sq_Circ")|K1);
    then x in dom (Sq_Circ") /\ K1 by RELAT_1:61;
    then x in dom (Sq_Circ") by XBOOLE_0:def 4;
    then
A5: (Sq_Circ").x in rng (Sq_Circ") by FUNCT_1:3;
    ((Sq_Circ")|K1).x=(Sq_Circ").x by A4,FUNCT_1:47;
    hence thesis by A4,A5,A3,Lm2,FUNCT_1:11;
  end;
  dom ((proj1)*((Sq_Circ")|K1)) c= dom ((Sq_Circ")|K1) by RELAT_1:25;
  then dom ((proj1)*((Sq_Circ")|K1)) =dom ((Sq_Circ")|K1) by A2,XBOOLE_0:def 10
    .=dom (Sq_Circ") /\ K1 by RELAT_1:61
    .=((the carrier of TOP-REAL 2))/\ K1 by Th29,FUNCT_2:def 1
    .=K1 by XBOOLE_1:28
    .=the carrier of (TOP-REAL 2)|K1 by PRE_TOPC:8;
  hence thesis by A1,FUNCT_2:2,TOPMETR:17,XBOOLE_1:1;
end;

theorem Th37:
  for K0,B0 being Subset of TOP-REAL 2,f being Function of (
TOP-REAL 2)|K0,(TOP-REAL 2)|B0 st f=(Sq_Circ")|K0 & B0=NonZero TOP-REAL 2 & K0=
{p:(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) & p<>0.TOP-REAL 2} holds f is
  continuous
proof
  let K0,B0 be Subset of TOP-REAL 2,f be Function of (TOP-REAL 2)|K0,(TOP-REAL
  2)|B0;
  assume
A1: f=(Sq_Circ")|K0 & B0=NonZero TOP-REAL 2 & K0={p:(p`2<=p`1 & -p`1<=p
  `2 or p`2>=p`1 & p`2<=-p`1) & p<>0.TOP-REAL 2};
  then 1.REAL 2 in K0 by Lm9,Lm10;
  then reconsider K1=K0 as non empty Subset of TOP-REAL 2;
  reconsider g1=(proj1)*((Sq_Circ")|K1) as Function of (TOP-REAL 2)|K1,R^1 by
Lm18;
  for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)|K1
  holds g1.p=p`1*sqrt(1+(p`2/p`1)^2)
  proof
A2: dom ((Sq_Circ")|K1)=dom (Sq_Circ") /\ K1 by RELAT_1:61
      .=((the carrier of TOP-REAL 2))/\ K1 by Th29,FUNCT_2:def 1
      .=K1 by XBOOLE_1:28;
    let p be Point of TOP-REAL 2;
A3: the carrier of (TOP-REAL 2)|K1=K1 by PRE_TOPC:8;
    assume
A4: p in the carrier of (TOP-REAL 2)|K1;
    then ex p3 being Point of TOP-REAL 2 st p=p3 &( p3`2<=p3`1 & - p3`1<=p3`2
    or p3`2>=p3`1 & p3`2<=-p3`1)& p3<>0.TOP-REAL 2 by A1,A3;
    then
A5: (Sq_Circ").p=|[p`1*sqrt(1+(p`2/p`1)^2), p`2*sqrt(1+(p`2/p`1)^2)]| by Th28;
    ((Sq_Circ")|K1).p=(Sq_Circ").p by A4,A3,FUNCT_1:49;
    then g1.p=(proj1).(|[p`1*sqrt(1+(p`2/p`1)^2), p`2*sqrt(1+(p`2/p`1)^2)]|)
    by A4,A2,A3,A5,FUNCT_1:13
      .=(|[p`1*sqrt(1+(p`2/p`1)^2), p`2*sqrt(1+(p`2/p`1)^2)]|)`1 by
PSCOMP_1:def 5
      .=p`1*sqrt(1+(p`2/p`1)^2) by EUCLID:52;
    hence thesis;
  end;
  then consider f1 being Function of (TOP-REAL 2)|K1,R^1 such that
A6: for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)
  |K1 holds f1.p=p`1*sqrt(1+(p`2/p`1)^2);
  reconsider g2=(proj2)*((Sq_Circ")|K1) as Function of (TOP-REAL 2)|K1,R^1 by
Lm17;
  for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)|K1
  holds g2.p=p`2*sqrt(1+(p`2/p`1)^2)
  proof
A7: dom ((Sq_Circ")|K1)=dom (Sq_Circ") /\ K1 by RELAT_1:61
      .=((the carrier of TOP-REAL 2))/\ K1 by Th29,FUNCT_2:def 1
      .=K1 by XBOOLE_1:28;
    let p be Point of TOP-REAL 2;
A8: the carrier of (TOP-REAL 2)|K1=K1 by PRE_TOPC:8;
    assume
A9: p in the carrier of (TOP-REAL 2)|K1;
    then ex p3 being Point of TOP-REAL 2 st p=p3 &( p3`2<=p3`1 & - p3`1<=p3`2
    or p3`2>=p3`1 & p3`2<=-p3`1)& p3<>0.TOP-REAL 2 by A1,A8;
    then
A10: (Sq_Circ").p =|[p`1*sqrt(1+(p`2/p`1)^2),p`2*sqrt(1+(p`2/p`1)^2)]| by Th28;
    ((Sq_Circ")|K1).p=(Sq_Circ").p by A9,A8,FUNCT_1:49;
    then
    g2.p=(proj2).(|[p`1*sqrt(1+(p`2/p`1)^2),p`2*sqrt(1+(p`2/p`1)^2)]|) by A9,A7
,A8,A10,FUNCT_1:13
      .=(|[p`1*sqrt(1+(p`2/p`1)^2), p`2*sqrt(1+(p`2/p`1)^2)]|)`2 by
PSCOMP_1:def 6
      .=p`2*sqrt(1+(p`2/p`1)^2) by EUCLID:52;
    hence thesis;
  end;
  then consider f2 being Function of (TOP-REAL 2)|K1,R^1 such that
A11: for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)
  |K1 holds f2.p=p`2*sqrt(1+(p`2/p`1)^2);
A12: for q being Point of TOP-REAL 2 st q in the carrier of (TOP-REAL 2)|K1
  holds q`1<>0
  proof
    let q be Point of TOP-REAL 2;
A13: the carrier of (TOP-REAL 2)|K1=K1 by PRE_TOPC:8;
    assume q in the carrier of (TOP-REAL 2)|K1;
    then
A14: ex p3 being Point of TOP-REAL 2 st q=p3 &( p3`2<=p3`1 & - p3`1<=p3`2 or
    p3`2>=p3`1 & p3`2<=-p3`1)& p3<>0.TOP-REAL 2 by A1,A13;
    now
      assume
A15:  q`1=0;
      then q`2=0 by A14;
      hence contradiction by A14,A15,EUCLID:53,54;
    end;
    hence thesis;
  end;
  then
A16: f1 is continuous by A6,Th33;
A17: now
    let x,y,r,s be real number;
    assume that
A18: |[x,y]| in K1 and
A19: r=f1.(|[x,y]|) & s=f2.(|[x,y]|);
    set p99=|[x,y]|;
A20: ex p3 being Point of TOP-REAL 2 st p99=p3 &( p3`2<=p3`1 & -p3`1<=p3`2
    or p3`2>=p3`1 & p3`2<=-p3`1)& p3<>0.TOP-REAL 2 by A1,A18;
A21: the carrier of (TOP-REAL 2)|K1=K1 by PRE_TOPC:8;
    then
A22: f1.p99=p99`1*sqrt(1+(p99`2/p99`1)^2) by A6,A18;
    ((Sq_Circ")|K0).(|[x,y]|)=((Sq_Circ")).(|[x,y]|) by A18,FUNCT_1:49
      .= |[p99`1*sqrt(1+(p99`2/p99`1)^2), p99`2*sqrt(1+(p99`2/p99`1)^2)]| by
A20,Th28
      .=|[r,s]| by A11,A18,A19,A21,A22;
    hence f.(|[x,y]|)=|[r,s]| by A1;
  end;
  f2 is continuous by A12,A11,Th34;
  hence thesis by A1,A16,A17,Lm13,JGRAPH_2:35;
end;

theorem Th38:
  for K0,B0 being Subset of TOP-REAL 2,f being Function of (
TOP-REAL 2)|K0,(TOP-REAL 2)|B0 st f=(Sq_Circ")|K0 & B0=NonZero TOP-REAL 2 & K0=
{p:(p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2) & p<>0.TOP-REAL 2} holds f is
  continuous
proof
  let K0,B0 be Subset of TOP-REAL 2,f be Function of (TOP-REAL 2)|K0,(TOP-REAL
  2)|B0;
  assume
A1: f=(Sq_Circ")|K0 & B0=NonZero TOP-REAL 2 & K0={p:(p`1<=p`2 & -p`2<=p
  `1 or p`1>=p`2 & p`1<=-p`2) & p<>0.TOP-REAL 2};
  then 1.REAL 2 in K0 by Lm14,Lm15;
  then reconsider K1=K0 as non empty Subset of TOP-REAL 2;
  reconsider g1=(proj2)*((Sq_Circ")|K1) as Function of (TOP-REAL 2)|K1,R^1 by
Lm17;
  for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)|K1
  holds g1.p=p`2*sqrt(1+(p`1/p`2)^2)
  proof
A2: dom ((Sq_Circ")|K1)=dom (Sq_Circ") /\ K1 by RELAT_1:61
      .=((the carrier of TOP-REAL 2))/\ K1 by Th29,FUNCT_2:def 1
      .=K1 by XBOOLE_1:28;
    let p be Point of TOP-REAL 2;
A3: the carrier of (TOP-REAL 2)|K1=K1 by PRE_TOPC:8;
    assume
A4: p in the carrier of (TOP-REAL 2)|K1;
    then ex p3 being Point of TOP-REAL 2 st p=p3 &( p3`1<=p3`2 & - p3`2<=p3`1
    or p3`1>=p3`2 & p3`1<=-p3`2)& p3<>0.TOP-REAL 2 by A1,A3;
    then
A5: (Sq_Circ").p=|[p`1*sqrt(1+(p`1/p`2)^2), p`2*sqrt(1+(p`1/p`2)^2)]| by Th30;
    ((Sq_Circ")|K1).p=(Sq_Circ").p by A4,A3,FUNCT_1:49;
    then
    g1.p=(proj2).(|[p`1*sqrt(1+(p`1/p`2)^2),p`2*sqrt(1+(p`1/p`2)^2)]|) by A4,A2
,A3,A5,FUNCT_1:13
      .=(|[p`1*sqrt(1+(p`1/p`2)^2), p`2*sqrt(1+(p`1/p`2)^2)]|)`2 by
PSCOMP_1:def 6
      .=p`2*sqrt(1+(p`1/p`2)^2) by EUCLID:52;
    hence thesis;
  end;
  then consider f1 being Function of (TOP-REAL 2)|K1,R^1 such that
A6: for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)
  |K1 holds f1.p=p`2*sqrt(1+(p`1/p`2)^2);
  reconsider g2=(proj1)*((Sq_Circ")|K1) as Function of (TOP-REAL 2)|K1,R^1 by
Lm18;
  for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)|K1
  holds g2.p=p`1*sqrt(1+(p`1/p`2)^2)
  proof
A7: dom ((Sq_Circ")|K1)=dom (Sq_Circ") /\ K1 by RELAT_1:61
      .=((the carrier of TOP-REAL 2))/\ K1 by Th29,FUNCT_2:def 1
      .=K1 by XBOOLE_1:28;
    let p be Point of TOP-REAL 2;
A8: the carrier of (TOP-REAL 2)|K1=K1 by PRE_TOPC:8;
    assume
A9: p in the carrier of (TOP-REAL 2)|K1;
    then ex p3 being Point of TOP-REAL 2 st p=p3 &( p3`1<=p3`2 & - p3`2<=p3`1
    or p3`1>=p3`2 & p3`1<=-p3`2)& p3<>0.TOP-REAL 2 by A1,A8;
    then
A10: (Sq_Circ").p=|[p`1*sqrt(1+(p`1/p`2)^2), p`2*sqrt(1+(p`1/p`2)^2)]| by Th30;
    ((Sq_Circ")|K1).p=(Sq_Circ").p by A9,A8,FUNCT_1:49;
    then
    g2.p=(proj1).(|[p`1*sqrt(1+(p`1/p`2)^2),p`2*sqrt(1+(p`1/p`2)^2)]|) by A9,A7
,A8,A10,FUNCT_1:13
      .=(|[p`1*sqrt(1+(p`1/p`2)^2), p`2*sqrt(1+(p`1/p`2)^2)]|)`1 by
PSCOMP_1:def 5
      .=p`1*sqrt(1+(p`1/p`2)^2) by EUCLID:52;
    hence thesis;
  end;
  then consider f2 being Function of (TOP-REAL 2)|K1,R^1 such that
A11: for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)
  |K1 holds f2.p=p`1*sqrt(1+(p`1/p`2)^2);
A12: for q being Point of TOP-REAL 2 st q in the carrier of (TOP-REAL 2)|K1
  holds q`2<>0
  proof
    let q be Point of TOP-REAL 2;
A13: the carrier of (TOP-REAL 2)|K1=K1 by PRE_TOPC:8;
    assume q in the carrier of (TOP-REAL 2)|K1;
    then
A14: ex p3 being Point of TOP-REAL 2 st q=p3 &( p3`1<=p3`2 & - p3`2<=p3`1 or
    p3`1>=p3`2 & p3`1<=-p3`2)& p3<>0.TOP-REAL 2 by A1,A13;
    now
      assume
A15:  q`2=0;
      then q`1=0 by A14;
      hence contradiction by A14,A15,EUCLID:53,54;
    end;
    hence thesis;
  end;
  then
A16: f1 is continuous by A6,Th35;
A17: for x,y,s,r being real number st |[x,y]| in K1 & s=f2.(|[x,y]|) & r=f1.
  (|[x,y]|) holds f.(|[x,y]|)=|[s,r]|
  proof
    let x,y,s,r be real number;
    assume that
A18: |[x,y]| in K1 and
A19: s=f2.(|[x,y]|) & r=f1.(|[x,y]|);
    set p99=|[x,y]|;
A20: ex p3 being Point of TOP-REAL 2 st p99=p3 &( p3`1<=p3`2 & -p3`2<=p3`1
    or p3`1>=p3`2 & p3`1<=-p3`2)& p3<>0.TOP-REAL 2 by A1,A18;
A21: the carrier of (TOP-REAL 2)|K1=K1 by PRE_TOPC:8;
    then
A22: f1.p99=p99`2*sqrt(1+(p99`1/p99`2)^2) by A6,A18;
    ((Sq_Circ")|K0).(|[x,y]|)=((Sq_Circ")).(|[x,y]|) by A18,FUNCT_1:49
      .= |[p99`1*sqrt(1+(p99`1/p99`2)^2), p99`2*sqrt(1+(p99`1/p99`2)^2)]| by
A20,Th30
      .=|[s,r]| by A11,A18,A19,A21,A22;
    hence thesis by A1;
  end;
  f2 is continuous by A12,A11,Th36;
  hence thesis by A1,A16,A17,Lm13,JGRAPH_2:35;
end;

theorem Th39:
  for B0 being Subset of TOP-REAL 2,K0 being Subset of (TOP-REAL 2
)|B0,f being Function of ((TOP-REAL 2)|B0)|K0,((TOP-REAL 2)|B0) st f=(Sq_Circ")
|K0 & B0=NonZero TOP-REAL 2 & K0={p:(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p
  `1) & p<>0.TOP-REAL 2} holds f is continuous & K0 is closed
proof
  reconsider K5={p7 where p7 is Point of TOP-REAL 2:p7`2<=-p7`1 } as closed
  Subset of TOP-REAL 2 by JGRAPH_2:47;
  reconsider K4={p7 where p7 is Point of TOP-REAL 2: p7`1<=p7`2 } as closed
  Subset of TOP-REAL 2 by JGRAPH_2:46;
  reconsider K3 = {p7 where p7 is Point of TOP-REAL 2:-p7`1<=p7`2 } as closed
  Subset of TOP-REAL 2 by JGRAPH_2:47;
  reconsider K2={p7 where p7 is Point of TOP-REAL 2:p7`2<=p7`1 } as closed
  Subset of TOP-REAL 2 by JGRAPH_2:46;
  defpred P[Point of TOP-REAL 2] means ($1`2<=$1`1 & -$1`1<=$1`2 or $1`2>=$1`1
  & $1`2<=-$1`1);
  set b0 = NonZero TOP-REAL 2;
  defpred P0[Point of TOP-REAL 2] means ($1`2<=$1`1 & -$1`1<=$1`2 or $1`2>=$1
  `1 & $1`2<=-$1`1);
  let B0 be Subset of TOP-REAL 2,K0 be Subset of (TOP-REAL 2)|B0,f being
  Function of ((TOP-REAL 2)|B0)|K0,((TOP-REAL 2)|B0);
  set k0 = {p:P0[p] & p<>0.TOP-REAL 2};
  assume that
A1: f=(Sq_Circ")|K0 and
A2: B0=b0 & K0=k0;
  the carrier of (TOP-REAL 2)|B0 = B0 by PRE_TOPC:8;
  then reconsider K1=K0 as Subset of TOP-REAL 2 by XBOOLE_1:1;
  k0 c= NonZero TOP-REAL 2 from TopIncl;
  then
A3: ((TOP-REAL 2)|B0)|K0=(TOP-REAL 2)|K1 by A2,PRE_TOPC:7;
  reconsider K1={p7 where p7 is Point of TOP-REAL 2: P[p7]} as Subset of
  TOP-REAL 2 from JGRAPH_2:sch 1;
A4: K2 /\ K3 \/ K4 /\ K5 c= K1
  proof
    let x be set;
    assume
A5: x in K2 /\ K3 \/ K4 /\ K5;
    per cases by A5,XBOOLE_0:def 3;
    suppose
A6:   x in K2 /\ K3;
      then x in K3 by XBOOLE_0:def 4;
      then
A7:   ex p8 being Point of TOP-REAL 2 st p8=x & -p8`1<=p8`2;
      x in K2 by A6,XBOOLE_0:def 4;
      then ex p7 being Point of TOP-REAL 2 st p7=x & p7`2<=(p7`1);
      hence thesis by A7;
    end;
    suppose
A8:   x in K4 /\ K5;
      then x in K5 by XBOOLE_0:def 4;
      then
A9:   ex p8 being Point of TOP-REAL 2 st p8=x & p8`2<= -p8`1;
      x in K4 by A8,XBOOLE_0:def 4;
      then ex p7 being Point of TOP-REAL 2 st p7=x & p7`2>=(p7`1);
      hence thesis by A9;
    end;
  end;
A10: K2 /\ K3 is closed & K4 /\ K5 is closed by TOPS_1:8;
  K1 c= K2 /\ K3 \/ K4 /\ K5
  proof
    let x be set;
    assume x in K1;
    then ex p being Point of TOP-REAL 2 st p=x &( p`2<=p`1 & -p`1 <=p`2 or p`2
    >=p`1 & p`2<=-p`1);
    then x in K2 & x in K3 or x in K4 & x in K5;
    then x in K2 /\ K3 or x in K4 /\ K5 by XBOOLE_0:def 4;
    hence thesis by XBOOLE_0:def 3;
  end;
  then K1=K2 /\ K3 \/ K4 /\ K5 by A4,XBOOLE_0:def 10;
  then
A11: K1 is closed by A10,TOPS_1:9;
  k0={p7 where p7 is Point of TOP-REAL 2:P0[p7]} /\ b0 from TopInter;
  then K0=K1 /\ [#]((TOP-REAL 2)|B0) by A2,PRE_TOPC:def 5;
  hence thesis by A1,A2,A3,A11,Th37,PRE_TOPC:13;
end;

theorem Th40:
  for B0 being Subset of TOP-REAL 2,K0 being Subset of (TOP-REAL 2
)|B0,f being Function of ((TOP-REAL 2)|B0)|K0,((TOP-REAL 2)|B0) st f=(Sq_Circ")
|K0 & B0=NonZero TOP-REAL 2 & K0={p:(p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p
  `2) & p<>0.TOP-REAL 2} holds f is continuous & K0 is closed
proof
  reconsider K5={p7 where p7 is Point of TOP-REAL 2:p7`1<=-p7`2 } as closed
  Subset of TOP-REAL 2 by JGRAPH_2:48;
  reconsider K4={p7 where p7 is Point of TOP-REAL 2:p7`2<=p7`1 } as closed
  Subset of TOP-REAL 2 by JGRAPH_2:46;
  reconsider K3={p7 where p7 is Point of TOP-REAL 2:-p7`2<=p7`1 } as closed
  Subset of TOP-REAL 2 by JGRAPH_2:48;
  reconsider K2={p7 where p7 is Point of TOP-REAL 2:p7`1<=p7`2 } as closed
  Subset of TOP-REAL 2 by JGRAPH_2:46;
  defpred P[Point of TOP-REAL 2] means ($1`1<=$1`2 & -$1`2<=$1`1 or $1`1>=$1`2
  & $1`1<=-$1`2);
  defpred P0[Point of TOP-REAL 2] means ($1`1<=$1`2 & -$1`2<=$1`1 or $1`1>=$1
  `2 & $1`1<=-$1`2);
  let B0 be Subset of TOP-REAL 2,K0 be Subset of (TOP-REAL 2)|B0,f being
  Function of ((TOP-REAL 2)|B0)|K0,((TOP-REAL 2)|B0);
  set k0 = {p:P0[p] & p<>0.TOP-REAL 2}, b0=NonZero TOP-REAL 2;
  assume that
A1: f=(Sq_Circ")|K0 and
A2: B0=b0 & K0=k0;
  the carrier of (TOP-REAL 2)|B0= B0 by PRE_TOPC:8;
  then reconsider K1=K0 as Subset of TOP-REAL 2 by XBOOLE_1:1;
  {p:P[p] & p<>0.TOP-REAL 2} c= NonZero TOP-REAL 2 from TopIncl;
  then
A3: ((TOP-REAL 2)|B0)|K0=(TOP-REAL 2)|K1 by A2,PRE_TOPC:7;
  set k1 = {p7 where p7 is Point of TOP-REAL 2: P0[p7]};
A4: K2 /\ K3 is closed & K4 /\ K5 is closed by TOPS_1:8;
  reconsider K1=k1 as Subset of TOP-REAL 2 from JGRAPH_2:sch 1;
A5: K2 /\ K3 \/ K4 /\ K5 c= K1
  proof
    let x be set;
    assume
A6: x in K2 /\ K3 \/ K4 /\ K5;
    per cases by A6,XBOOLE_0:def 3;
    suppose
A7:   x in K2 /\ K3;
      then x in K3 by XBOOLE_0:def 4;
      then
A8:   ex p8 being Point of TOP-REAL 2 st p8=x & -p8`2<=p8`1;
      x in K2 by A7,XBOOLE_0:def 4;
      then ex p7 being Point of TOP-REAL 2 st p7=x & p7`1<=(p7`2);
      hence thesis by A8;
    end;
    suppose
A9:   x in K4 /\ K5;
      then x in K5 by XBOOLE_0:def 4;
      then
A10:  ex p8 being Point of TOP-REAL 2 st p8=x & p8`1<= -p8`2;
      x in K4 by A9,XBOOLE_0:def 4;
      then ex p7 being Point of TOP-REAL 2 st p7=x & p7`1>=(p7`2);
      hence thesis by A10;
    end;
  end;
  K1 c= K2 /\ K3 \/ K4 /\ K5
  proof
    let x be set;
    assume x in K1;
    then ex p being Point of TOP-REAL 2 st p=x &( p`1<=p`2 & -p`2 <=p`1 or p`1
    >=p`2 & p`1<=-p`2);
    then x in K2 & x in K3 or x in K4 & x in K5;
    then x in K2 /\ K3 or x in K4 /\ K5 by XBOOLE_0:def 4;
    hence thesis by XBOOLE_0:def 3;
  end;
  then K1=K2 /\ K3 \/ K4 /\ K5 by A5,XBOOLE_0:def 10;
  then
A11: K1 is closed by A4,TOPS_1:9;
  k0=k1 /\ b0 from TopInter;
  then K0=K1 /\ [#]((TOP-REAL 2)|B0) by A2,PRE_TOPC:def 5;
  hence thesis by A1,A2,A3,A11,Th38,PRE_TOPC:13;
end;

theorem Th41:
  for D being non empty Subset of TOP-REAL 2 st D`={0.TOP-REAL 2}
holds ex h being Function of (TOP-REAL 2)|D,(TOP-REAL 2)|D st h=(Sq_Circ")|D &
  h is continuous
proof
  set Y1=|[-1,1]|;
  set B0 = {0.TOP-REAL 2};
  let D be non empty Subset of TOP-REAL 2;
A1: the carrier of ((TOP-REAL 2)|D) = D by PRE_TOPC:8;
  dom (Sq_Circ")=(the carrier of (TOP-REAL 2)) by Th29,FUNCT_2:def 1;
  then
A2: dom ((Sq_Circ")|D)=(the carrier of (TOP-REAL 2))/\ D by RELAT_1:61
    .=the carrier of ((TOP-REAL 2)|D) by A1,XBOOLE_1:28;
  assume
A3: D`={0.TOP-REAL 2};
  then
A4: D = (B0)` .=(NonZero TOP-REAL 2) by SUBSET_1:def 4;
A5: {p:(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) & p<>0.TOP-REAL 2} c=
  the carrier of (TOP-REAL 2)|D
  proof
    let x be set;
    assume
    x in {p:(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) & p<>0. TOP-REAL 2};
    then
A6: ex p st x=p &( p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p `1)& p<>0.
    TOP-REAL 2;
    now
      assume not x in D;
      then x in (the carrier of TOP-REAL 2) \ D by A6,XBOOLE_0:def 5;
      then x in D` by SUBSET_1:def 4;
      hence contradiction by A3,A6,TARSKI:def 1;
    end;
    hence thesis by PRE_TOPC:8;
  end;
  1.REAL 2 in {p where p is Point of TOP-REAL 2: (p`2<=p`1 & -p`1<=p`2 or
  p`2>=p`1 & p`2<=-p`1) & p<>0.TOP-REAL 2} by Lm9,Lm10;
  then reconsider K0={p:(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) & p<>0.
  TOP-REAL 2} as non empty Subset of (TOP-REAL 2)|D by A5;
A7: K0=the carrier of ((TOP-REAL 2)|D)|K0 by PRE_TOPC:8;
A8: {p:(p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2) & p<>0.TOP-REAL 2} c=
  the carrier of (TOP-REAL 2)|D
  proof
    let x be set;
    assume
    x in {p:(p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2) & p<>0. TOP-REAL 2};
    then
A9: ex p st x=p &( p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p `2)& p<>0.
    TOP-REAL 2;
    now
      assume not x in D;
      then x in (the carrier of TOP-REAL 2) \ D by A9,XBOOLE_0:def 5;
      then x in D` by SUBSET_1:def 4;
      hence contradiction by A3,A9,TARSKI:def 1;
    end;
    hence thesis by PRE_TOPC:8;
  end;
  Y1`1=-1 & Y1`2=1 by EUCLID:52;
  then
  Y1 in {p where p is Point of TOP-REAL 2: (p`1<=p`2 & -p`2<=p`1 or p`1>=
  p`2 & p`1<=-p`2) & p<>0.TOP-REAL 2} by JGRAPH_2:3;
  then reconsider K1={p:(p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2) & p<>0.
  TOP-REAL 2} as non empty Subset of (TOP-REAL 2)|D by A8;
A10: K1=the carrier of ((TOP-REAL 2)|D)|K1 by PRE_TOPC:8;
A11: D c= K0 \/ K1
  proof
    let x be set;
    assume
A12: x in D;
    then reconsider px=x as Point of TOP-REAL 2;
    not x in {0.TOP-REAL 2} by A4,A12,XBOOLE_0:def 5;
    then (px`2<=px`1 & -px`1<=px`2 or px`2>=px`1 & px`2<=-px`1) & px<>0.
TOP-REAL 2 or (px`1<=px`2 & -px`2<=px`1 or px`1>=px`2 & px`1<=-px`2) & px<>0.
    TOP-REAL 2 by TARSKI:def 1,XREAL_1:26;
    then x in K0 or x in K1;
    hence thesis by XBOOLE_0:def 3;
  end;
A13: the carrier of ((TOP-REAL 2)|D)=[#](((TOP-REAL 2)|D))
    .=(NonZero TOP-REAL 2) by A4,PRE_TOPC:def 5;
A14: K0 c= the carrier of TOP-REAL 2
  proof
    let z be set;
    assume z in K0;
    then ex p8 being Point of TOP-REAL 2 st p8=z &( p8`2<=p8`1 & - p8`1<=p8`2
    or p8`2>=p8`1 & p8`2<=-p8`1)& p8<>0.TOP-REAL 2;
    hence thesis;
  end;
A15: rng ((Sq_Circ")|K0) c= the carrier of ((TOP-REAL 2)|D)|K0
  proof
    reconsider K00=K0 as Subset of TOP-REAL 2 by A14;
    let y be set;
A16: for q being Point of TOP-REAL 2 st q in the carrier of (TOP-REAL 2)|
    K00 holds q`1<>0
    proof
      let q be Point of TOP-REAL 2;
A17:  the carrier of (TOP-REAL 2)|K00=K0 by PRE_TOPC:8;
      assume q in the carrier of (TOP-REAL 2)|K00;
      then
A18:  ex p3 being Point of TOP-REAL 2 st q=p3 &( p3`2<=p3`1 & - p3`1<=p3`2
      or p3`2>=p3`1 & p3`2<=-p3`1)& p3<>0.TOP-REAL 2 by A17;
      now
        assume
A19:    q`1=0;
        then q`2=0 by A18;
        hence contradiction by A18,A19,EUCLID:53,54;
      end;
      hence thesis;
    end;
    assume y in rng ((Sq_Circ")|K0);
    then consider x being set such that
A20: x in dom ((Sq_Circ")|K0) and
A21: y=((Sq_Circ")|K0).x by FUNCT_1:def 3;
A22: x in (dom (Sq_Circ")) /\ K0 by A20,RELAT_1:61;
    then
A23: x in K0 by XBOOLE_0:def 4;
    then reconsider p=x as Point of TOP-REAL 2 by A14;
    K00=the carrier of ((TOP-REAL 2)|K00) by PRE_TOPC:8;
    then p in the carrier of ((TOP-REAL 2)|K00) by A22,XBOOLE_0:def 4;
    then
A24: p`1<>0 by A16;
    set p9=|[p`1*sqrt(1+(p`2/p`1)^2),p`2*sqrt(1+(p`2/p`1)^2)]|;
A25: p9`1=p`1*sqrt(1+(p`2/p`1)^2) & p9`2=p`2*sqrt(1+(p`2/p`1)^2) by EUCLID:52;
A26: ex px being Point of TOP-REAL 2 st x=px &( px`2<=px`1 & - px`1<=px`2
    or px`2>=px`1 & px`2<=-px`1)& px<>0.TOP-REAL 2 by A23;
    then
A27: (Sq_Circ").p=|[p`1*sqrt(1+(p`2/p`1)^2), p`2*sqrt(1+(p`2/p`1)^2)]| by Th28;
A28: sqrt(1+(p`2/p`1)^2)>0 by Lm1,SQUARE_1:25;
    then
    p`2*sqrt(1+(p`2/p`1)^2)<=p`1*sqrt(1+(p`2/p`1)^2) & (-p`1)*sqrt(1+(p`2
/p`1)^2)<=p`2*sqrt(1+(p`2/p`1)^2) or p`2*sqrt(1+(p`2/p`1)^2)>=p`1*sqrt(1+(p`2/p
    `1)^2) & p`2*sqrt(1+(p`2/p`1)^2)<=(-p`1)*sqrt(1+(p`2/p`1)^2) by A26,
XREAL_1:64;
    then
A29: p`2*sqrt(1+(p`2/p`1)^2)<=p`1*sqrt(1+(p`2/p`1)^2) & -(p`1*sqrt(1+(p`2/
p`1)^2))<=p`2*sqrt(1+(p`2/p`1)^2) or p`2*sqrt(1+(p`2/p`1)^2)>=p`1*sqrt(1+(p`2/p
    `1)^2) & p`2*sqrt(1+(p`2/p`1)^2)<=-(p`1*sqrt(1+(p`2/p`1)^2));
A30: p9`1=p`1*sqrt(1+(p`2/p`1)^2) by EUCLID:52;
A31: now
      assume p9=0.TOP-REAL 2;
      then 0/sqrt(1+(p`2/p`1)^2)=p`1*sqrt(1+(p`2/p`1)^2)/sqrt(1+(p`2/p`1)^2)
      by A30,EUCLID:52,54;
      hence contradiction by A24,A28,XCMPLX_1:89;
    end;
    (Sq_Circ").p=y by A21,A23,FUNCT_1:49;
    then y in K0 by A31,A27,A29,A25;
    hence thesis by PRE_TOPC:8;
  end;
  dom ((Sq_Circ")|K0)= dom ((Sq_Circ")) /\ K0 by RELAT_1:61
    .=((the carrier of TOP-REAL 2)) /\ K0 by Th29,FUNCT_2:def 1
    .=K0 by A14,XBOOLE_1:28;
  then reconsider
  f=(Sq_Circ")|K0 as Function of ((TOP-REAL 2)|D)|K0, (TOP-REAL 2)|
  D by A7,A15,FUNCT_2:2,XBOOLE_1:1;
A32: K1=[#](((TOP-REAL 2)|D)|K1) by PRE_TOPC:def 5;
A33: K1 c= the carrier of TOP-REAL 2
  proof
    let z be set;
    assume z in K1;
    then ex p8 being Point of TOP-REAL 2 st p8=z &( p8`1<=p8`2 & - p8`2<=p8`1
    or p8`1>=p8`2 & p8`1<=-p8`2)& p8<>0.TOP-REAL 2;
    hence thesis;
  end;
A34: rng ((Sq_Circ")|K1) c= the carrier of ((TOP-REAL 2)|D)|K1
  proof
    reconsider K10=K1 as Subset of TOP-REAL 2 by A33;
    let y be set;
A35: for q being Point of TOP-REAL 2 st q in the carrier of (TOP-REAL 2)|
    K10 holds q`2<>0
    proof
      let q be Point of TOP-REAL 2;
A36:  the carrier of (TOP-REAL 2)|K10=K1 by PRE_TOPC:8;
      assume q in the carrier of (TOP-REAL 2)|K10;
      then
A37:  ex p3 being Point of TOP-REAL 2 st q=p3 &( p3`1<=p3`2 & - p3`2<=p3`1
      or p3`1>=p3`2 & p3`1<=-p3`2)& p3<>0.TOP-REAL 2 by A36;
      now
        assume
A38:    q`2=0;
        then q`1=0 by A37;
        hence contradiction by A37,A38,EUCLID:53,54;
      end;
      hence thesis;
    end;
    assume y in rng ((Sq_Circ")|K1);
    then consider x being set such that
A39: x in dom ((Sq_Circ")|K1) and
A40: y=((Sq_Circ")|K1).x by FUNCT_1:def 3;
A41: x in (dom (Sq_Circ")) /\ K1 by A39,RELAT_1:61;
    then
A42: x in K1 by XBOOLE_0:def 4;
    then reconsider p=x as Point of TOP-REAL 2 by A33;
    K10=the carrier of ((TOP-REAL 2)|K10) by PRE_TOPC:8;
    then p in the carrier of ((TOP-REAL 2)|K10) by A41,XBOOLE_0:def 4;
    then
A43: p`2<>0 by A35;
    set p9=|[p`1*sqrt(1+(p`1/p`2)^2),p`2*sqrt(1+(p`1/p`2)^2)]|;
A44: p9`2=p`2*sqrt(1+(p`1/p`2)^2) & p9`1=p`1*sqrt(1+(p`1/p`2)^2) by EUCLID:52;
A45: ex px being Point of TOP-REAL 2 st x=px &( px`1<=px`2 & - px`2<=px`1
    or px`1>=px`2 & px`1<=-px`2)& px<>0.TOP-REAL 2 by A42;
    then
A46: (Sq_Circ").p=|[p`1*sqrt(1+(p`1/p`2)^2), p`2*sqrt(1+(p`1/p`2)^2)]| by Th30;
A47: sqrt(1+(p`1/p`2)^2)>0 by Lm1,SQUARE_1:25;
    then
    p`1*sqrt(1+(p`1/p`2)^2)<=p`2*sqrt(1+(p`1/p`2)^2) & (-p`2)*sqrt(1+(p`1
/p`2)^2)<=p`1*sqrt(1+(p`1/p`2)^2) or p`1*sqrt(1+(p`1/p`2)^2)>=p`2*sqrt(1+(p`1/p
    `2)^2) & p`1*sqrt(1+(p`1/p`2)^2)<=(-p`2)*sqrt(1+(p`1/p`2)^2) by A45,
XREAL_1:64;
    then
A48: p`1*sqrt(1+(p`1/p`2)^2)<=p`2*sqrt(1+(p`1/p`2)^2) & -(p`2*sqrt(1+(p`1/
p`2)^2))<=p`1*sqrt(1+(p`1/p`2)^2) or p`1*sqrt(1+(p`1/p`2)^2)>=p`2*sqrt(1+(p`1/p
    `2)^2) & p`1*sqrt(1+(p`1/p`2)^2)<=-(p`2*sqrt(1+(p`1/p`2)^2));
A49: p9`2=p`2*sqrt(1+(p`1/p`2)^2) by EUCLID:52;
A50: now
      assume p9=0.TOP-REAL 2;
      then 0/sqrt(1+(p`1/p`2)^2)=p`2*sqrt(1+(p`1/p`2)^2)/sqrt(1+(p`1/p`2)^2)
      by A49,EUCLID:52,54;
      hence contradiction by A43,A47,XCMPLX_1:89;
    end;
    (Sq_Circ").p=y by A40,A42,FUNCT_1:49;
    then y in K1 by A50,A46,A48,A44;
    hence thesis by PRE_TOPC:8;
  end;
  dom ((Sq_Circ")|K1)= dom ((Sq_Circ")) /\ K1 by RELAT_1:61
    .=((the carrier of TOP-REAL 2)) /\ K1 by Th29,FUNCT_2:def 1
    .=K1 by A33,XBOOLE_1:28;
  then reconsider
  g=(Sq_Circ")|K1 as Function of ((TOP-REAL 2)|D)|K1, ((TOP-REAL 2)
  |D) by A10,A34,FUNCT_2:2,XBOOLE_1:1;
A51: dom g=K1 by A10,FUNCT_2:def 1;
  g=(Sq_Circ")|K1;
  then
A52: K1 is closed by A4,Th40;
A53: K0=[#](((TOP-REAL 2)|D)|K0) by PRE_TOPC:def 5;
A54: now
    let x be set;
    assume
A55: x in ([#]((((TOP-REAL 2)|D)|K0))) /\ ([#] ((((TOP-REAL 2)|D)|K1) ));
    then x in K0 by A53,XBOOLE_0:def 4;
    then f.x=(Sq_Circ").x by FUNCT_1:49;
    hence f.x = g.x by A32,A55,FUNCT_1:49;
  end;
  f=(Sq_Circ")|K0;
  then
A56: K0 is closed by A4,Th39;
A57: dom f=K0 by A7,FUNCT_2:def 1;
  D= [#]((TOP-REAL 2)|D) by PRE_TOPC:def 5;
  then
A58: ([#](((TOP-REAL 2)|D)|K0)) \/ [#](((TOP-REAL 2)|D)|K1) = [#]((TOP-REAL
  2)|D) by A53,A32,A11,XBOOLE_0:def 10;
A59: f is continuous & g is continuous by A4,Th39,Th40;
  then consider h being Function of (TOP-REAL 2)|D,(TOP-REAL 2)|D such that
A60: h= f+*g and
  h is continuous by A53,A32,A58,A56,A52,A54,JGRAPH_2:1;
  K0=[#](((TOP-REAL 2)|D)|K0) & K1=[#](((TOP-REAL 2)|D)|K1) by PRE_TOPC:def 5;
  then
A61: f tolerates g by A54,A57,A51,PARTFUN1:def 4;
A62: for x being set st x in dom h holds h.x=((Sq_Circ")|D).x
  proof
    let x be set;
    assume
A63: x in dom h;
    then reconsider p=x as Point of TOP-REAL 2 by A13,XBOOLE_0:def 5;
    not x in {0.TOP-REAL 2} by A13,A63,XBOOLE_0:def 5;
    then
A64: x <>0.TOP-REAL 2 by TARSKI:def 1;
    x in (the carrier of TOP-REAL 2)\D` by A3,A13,A63;
    then
A65: x in D`` by SUBSET_1:def 4;
    per cases;
    suppose
A66:  x in K0;
A67:  (Sq_Circ")|D.p=(Sq_Circ").p by A65,FUNCT_1:49
        .=f.p by A66,FUNCT_1:49;
      h.p=(g+*f).p by A60,A61,FUNCT_4:34
        .=f.p by A57,A66,FUNCT_4:13;
      hence thesis by A67;
    end;
    suppose
      not x in K0;
      then not (p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) by A64;
      then p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2 by XREAL_1:26;
      then
A68:  x in K1 by A64;
      (Sq_Circ")|D.p=(Sq_Circ").p by A65,FUNCT_1:49
        .=g.p by A68,FUNCT_1:49;
      hence thesis by A60,A51,A68,FUNCT_4:13;
    end;
  end;
  dom h=the carrier of ((TOP-REAL 2)|D) by FUNCT_2:def 1;
  then f+*g=(Sq_Circ")|D by A60,A2,A62,FUNCT_1:2;
  hence thesis by A53,A32,A58,A56,A59,A52,A54,JGRAPH_2:1;
end;

theorem Th42:
  ex h being Function of TOP-REAL 2,TOP-REAL 2 st h=Sq_Circ" & h is continuous
proof
  reconsider f=(Sq_Circ") as Function of (TOP-REAL 2),(TOP-REAL 2) by Th29;
  reconsider D=NonZero TOP-REAL 2 as non empty Subset of TOP-REAL 2 by
JGRAPH_2:9;
A1: f.(0.TOP-REAL 2)=0.TOP-REAL 2 by Th28;
A2: for p being Point of (TOP-REAL 2)|D holds f.p<>f.(0.TOP-REAL 2)
  proof
    let p be Point of (TOP-REAL 2)|D;
A3: [#]((TOP-REAL 2)|D)=D by PRE_TOPC:def 5;
    then reconsider q=p as Point of TOP-REAL 2 by XBOOLE_0:def 5;
    not p in {0.TOP-REAL 2} by A3,XBOOLE_0:def 5;
    then
A4: not p=0.TOP-REAL 2 by TARSKI:def 1;
    per cases;
    suppose
A5:   not(q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2<=-q`1);
then A6:   q`2<>0;
      set q9=|[q`1*sqrt(1+(q`1/q`2)^2),q`2*sqrt(1+(q`1/q`2)^2)]|;
A7:   q9`2=q`2*sqrt(1+(q`1/q`2)^2) by EUCLID:52;
A8:   sqrt(1+(q`1/q`2)^2)>0 by Lm1,SQUARE_1:25;
      now
        assume q9=0.TOP-REAL 2;
        then 0 *q`2=q`2*sqrt(1+(q`1/q`2)^2) by A7,EUCLID:52,54;
        then
        0 *sqrt(1+(q`1/q`2)^2)=q`2*sqrt(1+(q`1/q`2)^2)/sqrt(1+(q`1/q`2)^2 );
        hence contradiction by A6,A8,XCMPLX_1:89;
      end;
      hence thesis by A1,A5,Th28;
    end;
    suppose
A9:   q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2<=-q`1;
A10:  now
        assume
A11:    q`1=0;
        then q`2=0 by A9;
        hence contradiction by A4,A11,EUCLID:53,54;
      end;
      set q9=|[q`1*sqrt(1+(q`2/q`1)^2),q`2*sqrt(1+(q`2/q`1)^2)]|;
A12:  q9`1=q`1*sqrt(1+(q`2/q`1)^2) by EUCLID:52;
A13:  sqrt(1+(q`2/q`1)^2)>0 by Lm1,SQUARE_1:25;
      now
        assume q9=0.TOP-REAL 2;
        then
        0/sqrt(1+(q`2/q`1)^2)=q`1*sqrt(1+(q`2/q`1)^2)/sqrt(1+(q`2/q`1)^2)
        by A12,EUCLID:52,54;
        hence contradiction by A10,A13,XCMPLX_1:89;
      end;
      hence thesis by A1,A4,A9,Th28;
    end;
  end;
A14: for V being Subset of TOP-REAL 2 st f.(0.TOP-REAL 2) in V & V is open
  ex W being Subset of TOP-REAL 2 st 0.TOP-REAL 2 in W & W is open & f.:W c= V
  proof
    reconsider u0=0.TOP-REAL 2 as Point of Euclid 2 by EUCLID:67;
    let V be Subset of TOP-REAL 2;
    reconsider VV=V as Subset of TopSpaceMetr Euclid 2 by Lm16;
    assume that
A15: f.(0.TOP-REAL 2) in V and
A16: V is open;
    VV is open by A16,Lm16,PRE_TOPC:30;
    then consider r being real number such that
A17: r>0 and
A18: Ball(u0,r) c= V by A1,A15,TOPMETR:15;
    reconsider r as Real by XREAL_0:def 1;
    reconsider W1=Ball(u0,r), V1=Ball(u0,r/sqrt(2)) as Subset of TOP-REAL 2 by
EUCLID:67;
A19: f.:V1 c= W1
    proof
      let z be set;
A20:  sqrt(2)>0 by SQUARE_1:25;
      assume z in f.:V1;
      then consider y being set such that
A21:  y in dom f and
A22:  y in V1 and
A23:  z=f.y by FUNCT_1:def 6;
      z in rng f by A21,A23,FUNCT_1:def 3;
      then reconsider qz=z as Point of TOP-REAL 2;
      reconsider pz=qz as Point of Euclid 2 by EUCLID:67;
      reconsider q=y as Point of TOP-REAL 2 by A21;
      reconsider qy=q as Point of Euclid 2 by EUCLID:67;
A24:  (q`1)^2 >=0 by XREAL_1:63;
A25:  (q`2)^2>=0 by XREAL_1:63;
      dist(u0,qy)<r/sqrt(2) by A22,METRIC_1:11;
      then |.(0.TOP-REAL 2) - q.|<r/sqrt(2) by JGRAPH_1:28;
      then sqrt((((0.TOP-REAL 2) - q)`1)^2+(((0.TOP-REAL 2) - q)`2)^2) < r/
      sqrt(2) by JGRAPH_1:30;
      then
      sqrt(((0.TOP-REAL 2)`1 - q`1)^2+ (((0.TOP-REAL 2) - q)`2)^2)<r/sqrt
      (2) by TOPREAL3:3;
      then
      sqrt(((0.TOP-REAL 2)`1 - q`1)^2+((0.TOP-REAL 2)`2 - q`2)^2)<r/sqrt(
      2) by TOPREAL3:3;
      then sqrt((q`1)^2+(q`2)^2)*sqrt(2)< r/sqrt(2)*sqrt(2) by A20,JGRAPH_2:3
,XREAL_1:68;
      then sqrt(((q`1)^2+(q`2)^2)*2)< r/sqrt(2)*sqrt(2) by A24,A25,SQUARE_1:29;
      then
A26:  sqrt(((q`1)^2+(q`2)^2)*2)< r by A20,XCMPLX_1:87;
      per cases;
      suppose
        q=0.TOP-REAL 2;
        then z=0.TOP-REAL 2 by A23,Th28;
        hence thesis by A17,GOBOARD6:1;
      end;
      suppose
A27:    q<>0.TOP-REAL 2 & (q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2<=-q `1);
A28:    now
          assume (q`1)^2<=0;
          then (q`1)^2=0 by XREAL_1:63;
          then
A29:      q`1=0 by XCMPLX_1:6;
          then q`2=0 by A27;
          hence contradiction by A27,A29,EUCLID:53,54;
        end;
A30:    (Sq_Circ").q=|[q`1*sqrt(1+(q`2/q`1)^2),q`2*sqrt(1+(q`2/q`1 ) ^2)
        ]| by A27,Th28;
        then qz`1=q`1*sqrt(1+(q`2/q`1)^2) by A23,EUCLID:52;
        then
A31:    (qz`1)^2=(q`1)^2*(sqrt(1+(q`2/q`1)^2))^2;
        qz`2=q`2*sqrt(1+(q`2/q`1)^2) by A23,A30,EUCLID:52;
        then
A32:    (qz`2)^2=(q`2)^2*(sqrt(1+(q`2/q`1)^2))^2;
A33:    1+(q`2/q`1)^2>0 by Lm1;
        now
          per cases by A27;
          case
A34:        q`2<=q`1 & -q`1<=q`2;
            now
              per cases;
              case
                0<=q`2;
                hence (q`2)^2<=(q`1)^2 by A34,SQUARE_1:15;
              end;
              case
A35:            0>q`2;
                --q`1>=-q`2 by A34,XREAL_1:24;
                then (-q`2)^2<=(q`1)^2 by A35,SQUARE_1:15;
                hence (q`2)^2<=(q`1)^2;
              end;
            end;
            hence (q`2)^2<=(q`1)^2;
          end;
          case
A36:        q`2>=q`1 & q`2<=-q`1;
            now
              per cases;
              case
A37:            0>=q`2;
                -q`2<=-q`1 by A36,XREAL_1:24;
                then (-q`2)^2<=(-q`1)^2 by A37,SQUARE_1:15;
                hence (q`2)^2<=(q`1)^2;
              end;
              case
                0<q`2;
                then (q`2)^2<=(-q`1)^2 by A36,SQUARE_1:15;
                hence (q`2)^2<=(q`1)^2;
              end;
            end;
            hence (q`2)^2<=(q`1)^2;
          end;
        end;
        then (q`2)^2/(q`1)^2<=(q`1)^2/(q`1)^2 by A28,XREAL_1:72;
        then (q`2/q`1)^2<=(q`1)^2/(q`1)^2 by XCMPLX_1:76;
        then (q`2/q`1)^2 <=1 by A28,XCMPLX_1:60;
        then
A38:    1+(q`2/q`1)^2<=1+1 by XREAL_1:7;
        then (q`2)^2*(1+(q`2/q`1)^2)<=(q`2)^2*2 by A25,XREAL_1:64;
        then
A39:    (qz`2)^2<=(q`2)^2*2 by A33,A32,SQUARE_1:def 2;
        (q`1)^2*(1+(q`2/q`1)^2)<=(q`1)^2*2 by A24,A38,XREAL_1:64;
        then (qz`1)^2<=(q`1)^2*2 by A33,A31,SQUARE_1:def 2;
        then
A40:    (qz`1)^2+(qz`2)^2<=(q`1)^2*2+(q`2)^2*2 by A39,XREAL_1:7;
        (qz`1)^2>=0 & (qz`2)^2>=0 by XREAL_1:63;
        then
A41:    sqrt((qz`1)^2+(qz`2)^2) <= sqrt((q`1)^2*2+(q`2)^2*2) by A40,SQUARE_1:26
;
A42:    ((0.TOP-REAL 2) - qz)`2=(0.TOP-REAL 2)`2-qz`2 by TOPREAL3:3
          .= -qz`2 by JGRAPH_2:3;
        ((0.TOP-REAL 2) - qz)`1=(0.TOP-REAL 2)`1-qz`1 by TOPREAL3:3
          .= -qz`1 by JGRAPH_2:3;
        then sqrt((((0.TOP-REAL 2) - qz)`1)^2+(((0.TOP-REAL 2) - qz)`2)^2)<r
        by A26,A42,A41,XXREAL_0:2;
        then |.(0.TOP-REAL 2) - qz.|<r by JGRAPH_1:30;
        then dist(u0,pz)<r by JGRAPH_1:28;
        hence thesis by METRIC_1:11;
      end;
      suppose
A43:    q<>0.TOP-REAL 2 & not (q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2 <=-q`1);
A44:    now
          assume (q`2)^2<=0;
          then (q`2)^2=0 by XREAL_1:63;
          then q`2=0 by XCMPLX_1:6;
          hence contradiction by A43;
        end;
        now
          per cases by A43,JGRAPH_2:13;
          case
A45:        q`1<=q`2 & -q`2<=q`1;
            now
              per cases;
              case
                0<=q`1;
                hence (q`1)^2<=(q`2)^2 by A45,SQUARE_1:15;
              end;
              case
A46:            0>q`1;
                --q`2>=-q`1 by A45,XREAL_1:24;
                then (-q`1)^2<=(q`2)^2 by A46,SQUARE_1:15;
                hence (q`1)^2<=(q`2)^2;
              end;
            end;
            hence (q`1)^2<=(q`2)^2;
          end;
          case
A47:        q`1>=q`2 & q`1<=-q`2;
            now
              per cases;
              case
A48:            0>=q`1;
                -q`1<=-q`2 by A47,XREAL_1:24;
                then (-q`1)^2<=(-q`2)^2 by A48,SQUARE_1:15;
                hence (q`1)^2<=(q`2)^2;
              end;
              case
                0<q`1;
                then (q`1)^2<=(-q`2)^2 by A47,SQUARE_1:15;
                hence (q`1)^2<=(q`2)^2;
              end;
            end;
            hence (q`1)^2<=(q`2)^2;
          end;
        end;
        then (q`1)^2/(q`2)^2<=(q`2)^2/(q`2)^2 by A44,XREAL_1:72;
        then (q`1/q`2)^2<=(q`2)^2/(q`2)^2 by XCMPLX_1:76;
        then (q`1/q`2)^2 <=1 by A44,XCMPLX_1:60;
        then
A49:    1+(q`1/q`2)^2<=1+1 by XREAL_1:7;
        then
A50:    (q`2)^2*(1+(q`1/q`2)^2)<=(q`2)^2*2 by A25,XREAL_1:64;
        1+(q`1/q`2)^2>0 by Lm1;
        then
A51:    (sqrt(1+(q`1/q`2)^2))^2=1+(q`1/q`2)^2 by SQUARE_1:def 2;
A52:    (q`1)^2*(1+(q`1/q`2)^2)<=(q`1)^2*2 by A24,A49,XREAL_1:64;
A53:    (Sq_Circ").q=|[q`1*sqrt(1+(q`1/q`2)^2),q`2*sqrt(1+(q`1/q`2 ) ^2)
        ]| by A43,Th28;
        then qz`1=q`1*sqrt(1+(q`1/q`2)^2) by A23,EUCLID:52;
        then
A54:    (qz`1)^2<=(q`1)^2*2 by A52,A51,SQUARE_1:9;
        qz`2=q`2*sqrt(1+(q`1/q`2)^2) by A23,A53,EUCLID:52;
        then (qz`2)^2<=(q`2)^2*2 by A50,A51,SQUARE_1:9;
        then
A55:    (qz`2)^2+(qz`1)^2<=(q`2)^2*2+(q`1)^2*2 by A54,XREAL_1:7;
        (qz`2)^2>=0 & (qz`1)^2>=0 by XREAL_1:63;
        then
A56:    sqrt((qz`2)^2+(qz`1)^2) <= sqrt((q`2)^2*2+(q`1)^2*2) by A55,SQUARE_1:26
;
A57:    ((0.TOP-REAL 2) - qz)`2=(0.TOP-REAL 2)`2-qz`2 by TOPREAL3:3
          .= -qz`2 by JGRAPH_2:3;
        ((0.TOP-REAL 2) - qz)`1=(0.TOP-REAL 2)`1-qz`1 by TOPREAL3:3
          .= -qz`1 by JGRAPH_2:3;
        then sqrt((((0.TOP-REAL 2) - qz)`2)^2+(((0.TOP-REAL 2) - qz)`1)^2)<r
        by A26,A57,A56,XXREAL_0:2;
        then |.(0.TOP-REAL 2) - qz.|<r by JGRAPH_1:30;
        then dist(u0,pz)<r by JGRAPH_1:28;
        hence thesis by METRIC_1:11;
      end;
    end;
A58: V1 is open by GOBOARD6:3;
    sqrt(2)>0 by SQUARE_1:25;
    then u0 in V1 by A17,GOBOARD6:1,XREAL_1:139;
    hence thesis by A18,A58,A19,XBOOLE_1:1;
  end;
A59: D`= {0.TOP-REAL 2} by Th20;
  then ex h being Function of (TOP-REAL 2)|D,(TOP-REAL 2)|D st h=(Sq_Circ")|D
  & h is continuous by Th41;
  hence thesis by A1,A59,A2,A14,Th3;
end;

theorem Th43:
  Sq_Circ is Function of TOP-REAL 2,TOP-REAL 2 & rng Sq_Circ = the
  carrier of TOP-REAL 2 & for f being Function of TOP-REAL 2,TOP-REAL 2 st f=
  Sq_Circ holds f is being_homeomorphism
proof
  thus Sq_Circ is Function of TOP-REAL 2,TOP-REAL 2;
A1: for f being Function of TOP-REAL 2,TOP-REAL 2 st f=Sq_Circ holds rng
  Sq_Circ=the carrier of TOP-REAL 2 & f is being_homeomorphism
  proof
    let f be Function of TOP-REAL 2,TOP-REAL 2;
    assume
A2: f=Sq_Circ;
    reconsider g=f/" as Function of TOP-REAL 2,TOP-REAL 2;
A3: dom f=the carrier of TOP-REAL 2 by FUNCT_2:def 1;
    the carrier of TOP-REAL 2 c= rng f
    proof
      let y be set;
      assume y in the carrier of TOP-REAL 2;
      then reconsider p2=y as Point of TOP-REAL 2;
      set q=p2;
      now
        per cases;
        case
          q=0.TOP-REAL 2;
          then y=Sq_Circ.q by Def1;
          hence ex x being set st x in dom Sq_Circ & y=Sq_Circ.x by A2,A3;
        end;
        case
A4:       q<>0.TOP-REAL 2 & (q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2<=- q`1);
          set px=|[q`1*sqrt(1+(q`2/q`1)^2),q`2*sqrt(1+(q`2/q`1)^2)]|;
A5:       sqrt(1+(q`2/q`1)^2)>0 by Lm1,SQUARE_1:25;
A6:       now
            assume that
A7:         px`1=0 and
A8:         px`2=0;
            q`2*sqrt(1+(q`2/q`1)^2 )=0 by A8,EUCLID:52;
            then
A9:         q`2=0 by A5,XCMPLX_1:6;
            q`1*sqrt(1+(q`2/q`1)^2)=0 by A7,EUCLID:52;
            then q`1=0 by A5,XCMPLX_1:6;
            hence contradiction by A4,A9,EUCLID:53,54;
          end;
A10:      dom Sq_Circ=the carrier of TOP-REAL 2 by FUNCT_2:def 1;
A11:      px`1 = q`1*sqrt(1+(q`2/q`1)^2) by EUCLID:52;
A12:      px`2 = q`2*sqrt(1+(q`2/q`1)^2) by EUCLID:52;
          then
A13:      px`2/px`1=q`2/q`1 by A11,A5,XCMPLX_1:91;
          then
A14:      px`2/sqrt(1+(px`2/px`1)^2)=q`2 by A12,A5,XCMPLX_1:89;
          q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2*sqrt(1+(q`2/q`1)^2) <= (
          -q`1)*sqrt(1+(q`2/q`1)^2) by A4,A5,XREAL_1:64;
          then
          q`2<=q`1 & (-q`1)*sqrt(1+(q`2/q`1)^2) <= q`2*sqrt(1+(q`2/q`1)^2
          ) or px`2>=px`1 & px`2<=-px`1 by A11,A12,A5,XREAL_1:64;
          then q`2*sqrt(1+(q`2/q`1)^2) <= q`1*sqrt(1+(q`2/q`1)^2) & -px`1<=px
          `2 or px`2>=px`1 & px`2<=-px`1 by A11,A5,EUCLID:52,XREAL_1:64;
          then
A15:      Sq_Circ.px=|[px`1/sqrt(1+(px`2/px`1)^2),px`2/sqrt(1+( px`2/px`1
          )^2) ]| by A11,A12,A6,Def1,JGRAPH_2:3;
          px`1/sqrt(1+(px`2/px`1)^2)=q`1 by A11,A5,A13,XCMPLX_1:89;
          hence ex x being set st x in dom Sq_Circ & y=Sq_Circ.x by A15,A14,A10
,EUCLID:53;
        end;
        case
A16:      q<>0.TOP-REAL 2 & not(q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q `2<=-q`1);
          set px=|[q`1*sqrt(1+(q`1/q`2)^2),q`2*sqrt(1+(q`1/q`2)^2)]|;
A17:      sqrt(1+(q`1/q`2)^2)>0 by Lm1,SQUARE_1:25;
A18:      now
            assume that
A19:        px`2=0 and
            px`1=0;
            q`2*sqrt(1+(q`1/q`2)^2)=0 by A19,EUCLID:52;
            then q`2=0 by A17,XCMPLX_1:6;
            hence contradiction by A16;
          end;
A20:      px`2 = q`2*sqrt(1+(q`1/q`2)^2) by EUCLID:52;
A21:      px`1 = q`1*sqrt(1+(q`1/q`2)^2) by EUCLID:52;
          then
A22:      px`1/px`2=q`1/q`2 by A20,A17,XCMPLX_1:91;
          then
A23:      px`1/sqrt(1+(px`1/px`2)^2)=q`1 by A21,A17,XCMPLX_1:89;
          q`1<=q`2 & -q`2<=q`1 or q`1>=q`2 & q`1<=-q`2 by A16,JGRAPH_2:13;
          then
          q`1<=q`2 & -q`2<=q`1 or q`1>=q`2 & q`1*sqrt(1+(q`1/q`2)^2) <= (
          -q`2)*sqrt(1+(q`1/q`2)^2) by A17,XREAL_1:64;
          then
          q`1<=q`2 & (-q`2)*sqrt(1+(q`1/q`2)^2) <= q`1*sqrt(1+(q`1/q`2)^2
          ) or px`1>=px`2 & px`1<=-px`2 by A20,A21,A17,XREAL_1:64;
          then q`1*sqrt(1+(q`1/q`2)^2) <= q`2*sqrt(1+(q`1/q`2)^2) & -px`2<=px
          `1 or px`1>=px`2 & px`1<=-px`2 by A20,A17,EUCLID:52,XREAL_1:64;
          then
A24:      Sq_Circ.px=|[px`1/sqrt(1+(px`1/px`2)^2),px`2/sqrt(1+( px`1/px`2
          )^2 )]| by A20,A21,A18,Th4,JGRAPH_2:3;
A25:      dom Sq_Circ=the carrier of TOP-REAL 2 by FUNCT_2:def 1;
          px`2/sqrt(1+(px`1/px`2)^2)=q`2 by A20,A17,A22,XCMPLX_1:89;
          hence ex x being set st x in dom Sq_Circ & y=Sq_Circ.x by A24,A23,A25
,EUCLID:53;
        end;
      end;
      hence thesis by A2,FUNCT_1:def 3;
    end;
    then
 rng f=the carrier of TOP-REAL 2 by XBOOLE_0:def 10;
     then
A26:  f is onto by FUNCT_2:def 3;
A27: rng f=dom ((f qua Function)") by A2,FUNCT_1:33
      .=dom (f/") by A2,A26,TOPS_2:def 4
      .=[#](TOP-REAL 2) by FUNCT_2:def 1;
    g=Sq_Circ" by A26,A2,TOPS_2:def 4;
    hence thesis by A2,A3,A27,Th21,Th42,TOPS_2:def 5;
  end;
  hence rng Sq_Circ=the carrier of TOP-REAL 2;
  thus thesis by A1;
end;

Lm19: now
  let pz2, pz1 be real number;
  assume ((pz2)^2+(pz1)^2-1)*(pz2)^2<=(pz1)^2;
  then (pz2)^2*(pz2)^2+(pz2)^2*((pz1)^2-1)-(pz1)^2 <=(pz1)^2-(pz1)^2 by
XREAL_1:9;
  hence ((pz2)^2-1)*((pz2)^2+(pz1)^2)<=0;
end;

Lm20: now
  let px1 be real number;
  assume (px1)^2-1=0;
  then (px1-1)*(px1+1)=0;
  then px1-1=0 or px1+1=0 by XCMPLX_1:6;
  hence px1=1 or px1=-1;
end;

theorem
  for f,g being Function of I[01],TOP-REAL 2, C0,KXP,KXN,KYP,KYN being
  Subset of TOP-REAL 2, O,I being Point of I[01] st O=0 & I=1 & f is continuous
one-to-one & g is continuous one-to-one & C0={p: |.p.|<=1}& KXP={q1 where q1 is
Point of TOP-REAL 2: |.q1.|=1 & q1`2<=q1`1 & q1`2>=-q1`1} & KXN={q2 where q2 is
Point of TOP-REAL 2: |.q2.|=1 & q2`2>=q2`1 & q2`2<=-q2`1} & KYP={q3 where q3 is
Point of TOP-REAL 2: |.q3.|=1 & q3`2>=q3`1 & q3`2>=-q3`1} & KYN={q4 where q4 is
Point of TOP-REAL 2: |.q4.|=1 & q4`2<=q4`1 & q4`2<=-q4`1} & f.O in KXN & f.I in
KXP & g.O in KYN & g.I in KYP & rng f c= C0 & rng g c= C0 holds rng f meets rng
  g
proof
A1: dom (Sq_Circ")=the carrier of TOP-REAL 2 by Th29,FUNCT_2:def 1;
  let f,g be Function of I[01],TOP-REAL 2, C0,KXP,KXN,KYP,KYN be Subset of
  TOP-REAL 2, O,I be Point of I[01];
  assume
A2: O=0 & I=1 & f is continuous one-to-one & g is continuous one-to-one
& C0={p: |.p.|<=1}& KXP={q1 where q1 is Point of TOP-REAL 2: |.q1.|=1 & q1`2<=
q1`1 & q1`2>=-q1`1} & KXN={q2 where q2 is Point of TOP-REAL 2: |.q2.|=1 & q2`2
>=q2`1 & q2`2<=-q2`1} & KYP={q3 where q3 is Point of TOP-REAL 2: |.q3.|=1 & q3
  `2>=q3`1 & q3`2>=-q3`1} & KYN={q4 where q4 is Point of TOP-REAL 2: |.q4.|=1 &
q4`2<=q4`1 & q4`2<=-q4`1} & f.O in KXN & f.I in KXP & g.O in KYN & g.I in KYP &
  rng f c= C0 & rng g c= C0;
  then consider p1 being Point of TOP-REAL 2 such that
A3: f.O=p1 and
A4: |.p1.|=1 and
A5: p1`2>=p1`1 and
A6: p1`2<=-p1`1;
  reconsider gg=Sq_Circ"*g as Function of I[01],TOP-REAL 2 by Th29,FUNCT_2:13;
A7: dom g=the carrier of I[01] by FUNCT_2:def 1;
  reconsider ff=Sq_Circ"*f as Function of I[01],TOP-REAL 2 by Th29,FUNCT_2:13;
A8: dom gg=the carrier of I[01] by FUNCT_2:def 1;
A9: dom ff=the carrier of I[01] by FUNCT_2:def 1;
  then
A10: (ff.O)=(Sq_Circ").(f.O) by FUNCT_1:12;
A11: dom f=the carrier of I[01] by FUNCT_2:def 1;
A12: for r being Point of I[01] holds -1<=(ff.r)`1 & (ff.r)`1<=1 & -1<=(gg.
r)`1 & (gg.r)`1<=1 & -1 <=(ff.r)`2 & (ff.r)`2<=1 & -1 <=(gg.r)`2 & (gg.r)`2<=1
  proof
    let r be Point of I[01];
    f.r in rng f by A11,FUNCT_1:3;
    then f.r in C0 by A2;
    then consider p1 being Point of TOP-REAL 2 such that
A13: f.r=p1 and
A14: |.p1.|<=1 by A2;
    g.r in rng g by A7,FUNCT_1:3;
    then g.r in C0 by A2;
    then consider p2 being Point of TOP-REAL 2 such that
A15: g.r=p2 and
A16: |.p2.|<=1 by A2;
A17: (gg.r)=(Sq_Circ").(g.r) by A8,FUNCT_1:12;
A18: now
      per cases;
      case
        p2=0.TOP-REAL 2;
        hence
        -1<=(gg.r)`1 & (gg.r)`1<=1 & -1 <=(gg.r)`2 & (gg.r)`2<=1 by A17,A15
,Th28,JGRAPH_2:3;
      end;
      case
A19:    p2<>0.TOP-REAL 2 & (p2`2<=p2`1 & -p2`1<=p2`2 or p2`2>=p2`1 &
        p2`2<=-p2`1);
        set px=gg.r;
A20:    Sq_Circ".p2=|[p2`1*sqrt(1+(p2`2/p2`1)^2),p2`2*sqrt(1+(p2`2 /p2`1
        )^2 )]| by A19,Th28;
        then
A21:    px`1 = p2`1*sqrt(1+(p2`2/p2`1)^2) by A17,A15,EUCLID:52;
        (|.p2.|)^2<=|.p2.| by A16,SQUARE_1:42;
        then
A22:    (|.p2.|)^2<=1 by A16,XXREAL_0:2;
A23:    (px`2)^2>=0 by XREAL_1:63;
A24:    (px`1)^2 >=0 by XREAL_1:63;
A25:    px`2 = p2`2*sqrt(1+(p2`2/p2`1)^2) by A17,A15,A20,EUCLID:52;
A26:    sqrt(1+(p2`2/p2`1)^2)>0 by Lm1,SQUARE_1:25;
        then p2`2<=p2`1 & -p2`1<=p2`2 or p2`2>=p2`1 & p2`2*sqrt(1+(p2`2/p2`1)
        ^2) <= (-p2`1)*sqrt(1+(p2`2/p2`1)^2) by A19,XREAL_1:64;
        then
A27:    p2`2<=p2`1 & (-p2`1)*sqrt(1+(p2`2/p2`1)^2) <= p2`2*sqrt(1+(p2`2/
        p2`1 )^2) or px`2>=px`1 & px`2<=-px`1 by A21,A25,A26,XREAL_1:64;
        then
A28:    p2`2*sqrt( 1+(p2`2/p2`1)^2) <= p2`1*sqrt(1+(p2`2/p2`1)^2) & -px
        `1<= px `2 or px`2>=px`1 & px`2<=-px`1 by A17,A15,A20,A21,A26,EUCLID:52
,XREAL_1:64;
A29:    now
          assume px`1=0 & px`2=0;
          then p2`1=0 & p2`2=0 by A21,A25,A26,XCMPLX_1:6;
          hence contradiction by A19,EUCLID:53,54;
        end;
        then
A30:    px`1<>0 by A21,A25,A26,A27,XREAL_1:64;
        set q=px;
A31:    (|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]|)`2 = q`2/
        sqrt(1+( q`2/q`1)^2) by EUCLID:52;
A32:    1+(q`2/q`1)^2>0 by Lm1;
A33:    p2=Sq_Circ.px & (|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)
^2)]|)`1 = q `1/sqrt(1+( q`2/q`1)^2) by A17,A15,Th43,EUCLID:52,FUNCT_1:32;
        Sq_Circ.q=|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]| by A21
,A25,A29,A28,Def1,JGRAPH_2:3;
        then (|.p2.|)^2= (q`1/sqrt(1+(q`2/q`1)^2))^2+(q`2/sqrt(1+(q`2/q`1)^2)
        ) ^2 by A33,A31,JGRAPH_1:29
          .= (q`1)^2/(sqrt(1+(q`2/q`1)^2))^2+(q`2/sqrt(1+(q`2/q`1)^2))^2 by
XCMPLX_1:76
          .= (q`1)^2/(sqrt(1+(q`2/q`1)^2))^2+(q`2)^2/(sqrt(1+(q`2/q`1)^2))^2
        by XCMPLX_1:76
          .= (q`1)^2/(1+(q`2/q`1)^2)+(q`2)^2/(sqrt(1+(q`2/q`1)^2))^2 by A32,
SQUARE_1:def 2
          .= (q`1)^2/(1+(q`2/q`1)^2)+(q`2)^2/(1+(q`2/q`1)^2) by A32,
SQUARE_1:def 2
          .= ((q`1)^2+(q`2)^2)/(1+(q`2/q`1)^2) by XCMPLX_1:62;
        then ((q`1)^2+(q`2)^2)/(1+(q`2/q`1)^2)*(1+(q`2/q`1)^2)<=1 *(1+(q`2/q
        `1 )^2) by A32,A22,XREAL_1:64;
        then ((q`1)^2+(q`2)^2)<=(1+(q`2/q`1)^2) by A32,XCMPLX_1:87;
        then (px`1)^2+(px`2)^2<=1+(px`2)^2/(px`1)^2 by XCMPLX_1:76;
        then (px`1)^2+(px`2)^2-1<=(px`2)^2/(px`1)^2 by XREAL_1:20;
        then ((px`1)^2+(px`2)^2-1)*(px`1)^2<=(px`2)^2/(px`1)^2*(px`1)^2 by A24,
XREAL_1:64;
        then ((px`1)^2+(px`2)^2-1)*(px`1)^2<=(px`2)^2 by A30,XCMPLX_1:6,87;
        then
A34:    ((px`1)^2-1)*((px`1)^2+(px`2)^2)<=0 by Lm19;
        ((px`1)^2+(px`2)^2)<>0 by A29,COMPLEX1:1;
        then
A35:    ((px`1)^2-1)<=0 by A24,A34,A23,XREAL_1:129;
        then
A36:    px`1>=-1 by SQUARE_1:43;
A37:    px`1<=1 by A35,SQUARE_1:43;
        then q`2<=1 & --q`1>=-q`2 or q`2>=-1 & q`2<=-q`1 by A21,A25,A28,A36,
XREAL_1:24,XXREAL_0:2;
        then q`2<=1 & q`1>=-q`2 or q`2>=-1 & -q`2>=--q`1 by XREAL_1:24;
        then q`2<=1 & 1>=-q`2 or q`2>=-1 & -q`2>=q`1 by A37,XXREAL_0:2;
        then q`2<=1 & -1<=--q`2 or q`2>=-1 & -q`2>=-1 by A36,XREAL_1:24
,XXREAL_0:2;
        hence
        -1<=(gg.r)`1 & (gg.r)`1<=1 & -1 <=(gg.r)`2 & (gg.r)`2<=1 by A35,
SQUARE_1:43,XREAL_1:24;
      end;
      case
A38:    p2<>0.TOP-REAL 2 & not(p2`2<=p2`1 & -p2`1<=p2`2 or p2`2>=p2
        `1 & p2`2<=-p2`1);
        set pz=gg.r;
A39:    Sq_Circ".p2=|[p2`1*sqrt(1+(p2`1/p2`2)^2),p2`2*sqrt(1+(p2`1 /p2`2
        )^2 )]| by A38,Th28;
        then
A40:    pz`2 = p2`2*sqrt(1+(p2`1/p2`2)^2) by A17,A15,EUCLID:52;
A41:    pz`1 = p2`1*sqrt(1+(p2`1/p2`2)^2) by A17,A15,A39,EUCLID:52;
A42:    sqrt(1+(p2`1/p2`2)^2)>0 by Lm1,SQUARE_1:25;
        p2`1<=p2`2 & -p2`2<=p2`1 or p2`1>=p2`2 & p2`1<=-p2`2 by A38,JGRAPH_2:13
;
        then p2`1<=p2`2 & -p2`2<=p2`1 or p2`1>=p2`2 & p2`1*sqrt(1+(p2`1/p2`2)
        ^2) <= (-p2`2)*sqrt(1+(p2`1/p2`2)^2) by A42,XREAL_1:64;
        then
A43:    p2`1<=p2`2 & (-p2`2)*sqrt(1+(p2`1/p2`2)^2) <= p2`1*sqrt(1+(p2`1/
        p2`2) ^2) or pz`1>=pz`2 & pz`1<=-pz`2 by A40,A41,A42,XREAL_1:64;
        then
A44:    p2`1*sqrt(1+(p2`1/p2`2)^2) <= p2`2*sqrt(1+(p2`1/p2`2)^2) & -pz
        `2<=pz `1 or pz`1>=pz`2 & pz`1<=-pz`2 by A17,A15,A39,A40,A42,EUCLID:52
,XREAL_1:64;
A45:    now
          assume that
A46:      pz`2=0 and
          pz`1=0;
          p2`2=0 by A40,A42,A46,XCMPLX_1:6;
          hence contradiction by A38;
        end;
        then
A47:    pz`2<>0 by A40,A41,A42,A43,XREAL_1:64;
A48:    p2=Sq_Circ.pz & (|[pz`1/sqrt(1+(pz`1/pz`2)^2),pz`2/sqrt(1+(pz`1/
        pz`2)^2)]|) `2 = pz`2/ sqrt(1+(pz`1/pz`2)^2) by A17,A15,Th43,EUCLID:52
,FUNCT_1:32;
A49:    (pz`2)^2 >=0 by XREAL_1:63;
        (|.p2.|)^2<=|.p2.| by A16,SQUARE_1:42;
        then
A50:    (|.p2.|)^2<=1 by A16,XXREAL_0:2;
A51:    (pz`1)^2>=0 by XREAL_1:63;
A52:    (|[pz`1/sqrt(1+(pz`1/pz`2)^2),pz`2/sqrt(1+(pz`1/pz`2)^2)]|)`1 =
        pz`1/ sqrt(1+(pz`1/pz`2)^2) by EUCLID:52;
A53:    1+(pz`1/pz`2)^2>0 by Lm1;
        Sq_Circ.pz=|[pz`1/sqrt(1+(pz`1/pz`2)^2),pz`2/sqrt(1+(pz`1/pz`2)
        ^2)]| by A40,A41,A45,A44,Th4,JGRAPH_2:3;
        then (|.p2.|)^2= (pz`2/sqrt(1+(pz`1/pz`2)^2))^2+(pz`1/sqrt(1+(pz`1/pz
        `2)^2))^2 by A48,A52,JGRAPH_1:29
          .= (pz`2)^2/(sqrt(1+(pz`1/pz`2)^2))^2+(pz`1/sqrt(1+(pz`1/pz`2)^2))
        ^2 by XCMPLX_1:76
          .= (pz`2)^2/(sqrt(1+(pz`1/pz`2)^2))^2 +(pz`1)^2/(sqrt(1+(pz`1/pz`2
        )^2))^2 by XCMPLX_1:76
          .= (pz`2)^2/(1+(pz`1/pz`2)^2)+(pz`1)^2/(sqrt(1+(pz`1/pz`2)^2))^2
        by A53,SQUARE_1:def 2
          .= (pz`2)^2/(1+(pz`1/pz`2)^2)+(pz`1)^2/(1+(pz`1/pz`2)^2) by A53,
SQUARE_1:def 2
          .= ((pz`2)^2+(pz`1)^2)/(1+(pz`1/pz`2)^2) by XCMPLX_1:62;
        then ((pz`2)^2+(pz`1)^2)/(1+(pz`1/pz`2)^2)*(1+(pz`1/pz`2)^2) <=1 *(1+
        (pz`1/pz`2)^2) by A53,A50,XREAL_1:64;
        then ((pz`2)^2+(pz`1)^2)<=(1+(pz`1/pz`2)^2) by A53,XCMPLX_1:87;
        then (pz`2)^2+(pz`1)^2<=1+(pz`1)^2/(pz`2)^2 by XCMPLX_1:76;
        then (pz`2)^2+(pz`1)^2-1<=(pz`1)^2/(pz`2)^2 by XREAL_1:20;
        then ((pz`2)^2+(pz`1)^2-1)*(pz`2)^2<=(pz`1)^2/(pz`2)^2*(pz`2)^2 by A49,
XREAL_1:64;
        then ((pz`2)^2+(pz`1)^2-1)*(pz`2)^2<=(pz`1)^2 by A47,XCMPLX_1:6,87;
        then
A54:    ((pz`2)^2-1)*((pz`2)^2+(pz`1)^2)<=0 by Lm19;
        (pz`2)^2+(pz`1)^2<>0 by A45,COMPLEX1:1;
        then
A55:    (pz`2)^2-1<=0 by A49,A54,A51,XREAL_1:129;
        then
A56:    pz`2>=-1 by SQUARE_1:43;
A57:    pz`2<=1 by A55,SQUARE_1:43;
        then pz`1<=1 & --pz`2>=-pz`1 or pz`1>=-1 & pz`1<=-pz`2 by A40,A41,A44
,A56,XREAL_1:24,XXREAL_0:2;
        then pz`1<=1 & 1>=-pz`1 or pz`1>=-1 & -pz`1>=--pz`2 by A57,XREAL_1:24
,XXREAL_0:2;
        then pz`1<=1 & 1>=-pz`1 or pz`1>=-1 & -pz`1>=-1 by A56,XXREAL_0:2;
        then pz`1<=1 & -1<=--pz`1 or pz`1>=-1 & pz`1<=1 by XREAL_1:24;
        hence
        -1<=(gg.r)`1 & (gg.r)`1<=1 & -1 <=(gg.r)`2 & (gg.r)`2<=1 by A55,
SQUARE_1:43;
      end;
    end;
A58: (ff.r)=(Sq_Circ").(f.r) by A9,FUNCT_1:12;
    now
      per cases;
      case
        p1=0.TOP-REAL 2;
        hence
        -1<=(ff.r)`1 & (ff.r)`1<=1 & -1 <=(ff.r)`2 & (ff.r)`2<=1 by A58,A13
,Th28,JGRAPH_2:3;
      end;
      case
A59:    p1<>0.TOP-REAL 2 & (p1`2<=p1`1 & -p1`1<=p1`2 or p1`2>=p1`1 &
        p1`2<=-p1`1);
        set px=ff.r;
        Sq_Circ".p1=|[p1`1*sqrt(1+(p1`2/p1`1)^2),p1`2*sqrt(1+(p1`2 /p1`1
        )^2 ) ]| by A59,Th28;
        then
A60:    px`1 = p1`1*sqrt(1+(p1`2/p1`1)^2) & px`2 = p1`2*sqrt(1+(p1`2/p1
        `1)^2) by A58,A13,EUCLID:52;
A61:    sqrt(1+(p1`2/p1`1)^2)>0 by Lm1,SQUARE_1:25;
        then p1`2<=p1`1 & -p1`1<=p1`2 or p1`2>=p1`1 & p1`2*sqrt(1+(p1`2/p1`1)
        ^2) <= (-p1`1)*sqrt(1+(p1`2/p1`1)^2) by A59,XREAL_1:64;
        then
A62:    p1`2<=p1`1 & (-p1`1)*sqrt(1+(p1`2/p1`1)^2) <= p1`2*sqrt(1+ (p1`2
        /p1`1 )^2) or px`2>=px`1 & px`2<=-px`1 by A60,A61,XREAL_1:64;
        then
A63:    px`2<=px`1 & -px`1<=px`2 or px`2>=px`1 & px`2<=-px`1 by A60,A61,
XREAL_1:64;
A64:    now
          assume px`1=0 & px`2=0;
          then p1`1=0 & p1`2=0 by A60,A61,XCMPLX_1:6;
          hence contradiction by A59,EUCLID:53,54;
        end;
        then
A65:    px`1<>0 by A60,A61,A62,XREAL_1:64;
        (|.p1.|)^2<=|.p1.| by A14,SQUARE_1:42;
        then
A66:    (|.p1.|)^2<=1 by A14,XXREAL_0:2;
A67:    (px`1)^2 >=0 by XREAL_1:63;
A68:    (px`2)^2>=0 by XREAL_1:63;
        set q=px;
A69:    (|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]|)`2 = q`2/
        sqrt(1+( q`2/q`1)^2) by EUCLID:52;
A70:    1+(q`2/q`1)^2>0 by Lm1;
A71:    p1=Sq_Circ.px & (|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)
^2)]|)`1 = q `1/sqrt(1+( q`2/q`1)^2) by A58,A13,Th43,EUCLID:52,FUNCT_1:32;
        Sq_Circ.q=|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]| by A64
,A63,Def1,JGRAPH_2:3;
        then (|.p1.|)^2= (q`1/sqrt(1+(q`2/q`1)^2))^2+(q`2/sqrt(1+(q`2/q`1)^2)
        ) ^2 by A71,A69,JGRAPH_1:29
          .= (q`1)^2/(sqrt(1+(q`2/q`1)^2))^2+(q`2/sqrt(1+(q`2/q`1)^2))^2 by
XCMPLX_1:76
          .= (q`1)^2/(sqrt(1+(q`2/q`1)^2))^2+(q`2)^2/(sqrt(1+(q`2/q`1)^2))^2
        by XCMPLX_1:76
          .= (q`1)^2/(1+(q`2/q`1)^2)+(q`2)^2/(sqrt(1+(q`2/q`1)^2))^2 by A70,
SQUARE_1:def 2
          .= (q`1)^2/(1+(q`2/q`1)^2)+(q`2)^2/(1+(q`2/q`1)^2) by A70,
SQUARE_1:def 2
          .= ((q`1)^2+(q`2)^2)/(1+(q`2/q`1)^2) by XCMPLX_1:62;
        then ((q`1)^2+(q`2)^2)/(1+(q`2/q`1)^2)*(1+(q`2/q`1)^2)<=1 *(1+(q`2/q
        `1 )^2) by A70,A66,XREAL_1:64;
        then (q`1)^2+(q`2)^2<=1+(q`2/q`1)^2 by A70,XCMPLX_1:87;
        then (px`1)^2+(px`2)^2<=1+(px`2)^2/(px`1)^2 by XCMPLX_1:76;
        then (px`1)^2+(px`2)^2-1<=(px`2)^2/(px`1)^2 by XREAL_1:20;
        then ((px`1)^2+(px`2)^2-1)*(px`1)^2<=(px`2)^2/(px`1)^2*(px`1)^2 by A67,
XREAL_1:64;
        then ((px`1)^2+(px`2)^2-1)*(px`1)^2<=(px`2)^2 by A65,XCMPLX_1:6,87;
        then
A72:    ((px`1)^2-1)*((px`1)^2+(px`2)^2)<=0 by Lm19;
        ((px`1)^2+(px`2)^2)<>0 by A64,COMPLEX1:1;
        then
A73:    (px`1)^2-1<=0 by A67,A72,A68,XREAL_1:129;
        then
A74:    px`1>=-1 by SQUARE_1:43;
A75:    px`1<=1 by A73,SQUARE_1:43;
        then q`2<=1 & --q`1>=-q`2 or q`2>=-1 & q`2<=-q`1 by A63,A74,XREAL_1:24
,XXREAL_0:2;
        then q`2<=1 & q`1>=-q`2 or q`2>=-1 & -q`2>=--q`1 by XREAL_1:24;
        then q`2<=1 & 1>=-q`2 or q`2>=-1 & -q`2>=q`1 by A75,XXREAL_0:2;
        then q`2<=1 & -1<=--q`2 or q`2>=-1 & -q`2>=-1 by A74,XREAL_1:24
,XXREAL_0:2;
        hence
        -1<=(ff.r)`1 & (ff.r)`1<=1 & -1 <=(ff.r)`2 & (ff.r)`2<=1 by A73,
SQUARE_1:43,XREAL_1:24;
      end;
      case
A76:    p1<>0.TOP-REAL 2 & not(p1`2<=p1`1 & -p1`1<=p1`2 or p1`2>=p1
        `1 & p1`2<=-p1`1);
        set pz=ff.r;
A77:    Sq_Circ".p1=|[p1`1*sqrt(1+(p1`1/p1`2)^2),p1`2*sqrt(1+(p1`1 /p1`2
        ) ^2)]| by A76,Th28;
        then
A78:    pz`2 = p1`2*sqrt(1+(p1`1/p1`2)^2) by A58,A13,EUCLID:52;
A79:    pz`1 = p1`1*sqrt(1+(p1`1/p1`2)^2) by A58,A13,A77,EUCLID:52;
A80:    sqrt(1+(p1`1/p1`2)^2)>0 by Lm1,SQUARE_1:25;
        p1`1<=p1`2 & -p1`2<=p1`1 or p1`1>=p1`2 & p1`1<=-p1`2 by A76,JGRAPH_2:13
;
        then p1`1<=p1`2 & -p1`2<=p1`1 or p1`1>=p1`2 & p1`1*sqrt(1+(p1`1/p1`2)
        ^2) <= (-p1`2)*sqrt(1+(p1`1/p1`2)^2) by A80,XREAL_1:64;
        then
A81:    p1`1<=p1`2 & (-p1`2)*sqrt(1+(p1`1/p1`2)^2) <= p1`1*sqrt(1+(p1`1/
        p1`2 )^2) or pz`1>=pz`2 & pz`1<=-pz`2 by A78,A79,A80,XREAL_1:64;
        then
A82:    p1`1*sqrt(1+(p1`1/p1`2)^2) <= p1`2*sqrt(1+(p1`1/p1`2)^2) & -pz
        `2<=pz `1 or pz`1>=pz`2 & pz`1<=-pz`2 by A58,A13,A77,A78,A80,EUCLID:52
,XREAL_1:64;
A83:    now
          assume that
A84:      pz`2=0 and
          pz`1=0;
          p1`2=0 by A78,A80,A84,XCMPLX_1:6;
          hence contradiction by A76;
        end;
        then
A85:    pz`2<>0 by A78,A79,A80,A81,XREAL_1:64;
A86:    p1=Sq_Circ.pz & (|[pz`1/sqrt(1+(pz`1/pz`2)^2),pz`2/sqrt(1+(pz`1/
        pz`2)^2)]|) `2 = pz`2/ sqrt(1+(pz`1/pz`2)^2) by A58,A13,Th43,EUCLID:52
,FUNCT_1:32;
A87:    (pz`2)^2 >=0 by XREAL_1:63;
        (|.p1.|)^2<=|.p1.| by A14,SQUARE_1:42;
        then
A88:    (|.p1.|)^2<=1 by A14,XXREAL_0:2;
A89:    (pz`1)^2>=0 by XREAL_1:63;
A90:    (|[pz`1/sqrt(1+(pz`1/pz`2)^2),pz`2/sqrt(1+(pz`1/pz`2)^2)]|)`1 =
        pz`1/ sqrt(1+(pz`1/pz`2)^2) by EUCLID:52;
A91:    1+(pz`1/pz`2)^2>0 by Lm1;
        Sq_Circ.pz=|[pz`1/sqrt(1+(pz`1/pz`2)^2),pz`2/sqrt(1+(pz`1/pz`2)
        ^2)]| by A78,A79,A83,A82,Th4,JGRAPH_2:3;
        then (|.p1.|)^2= (pz`2/sqrt(1+(pz`1/pz`2)^2))^2+(pz`1/sqrt(1+(pz`1/pz
        `2)^2))^2 by A86,A90,JGRAPH_1:29
          .= (pz`2)^2/(sqrt(1+(pz`1/pz`2)^2))^2+(pz`1/sqrt(1+(pz`1/pz`2)^2))
        ^2 by XCMPLX_1:76
          .= (pz`2)^2/(sqrt(1+(pz`1/pz`2)^2))^2 +(pz`1)^2/(sqrt(1+(pz`1/pz`2
        )^2))^2 by XCMPLX_1:76
          .= (pz`2)^2/(1+(pz`1/pz`2)^2)+(pz`1)^2/(sqrt(1+(pz`1/pz`2)^2))^2
        by A91,SQUARE_1:def 2
          .= (pz`2)^2/(1+(pz`1/pz`2)^2)+(pz`1)^2/(1+(pz`1/pz`2)^2) by A91,
SQUARE_1:def 2
          .= ((pz`2)^2+(pz`1)^2)/(1+(pz`1/pz`2)^2) by XCMPLX_1:62;
        then ((pz`2)^2+(pz`1)^2)/(1+(pz`1/pz`2)^2)*(1+(pz`1/pz`2)^2) <=1 *(1+
        (pz`1/pz`2)^2) by A91,A88,XREAL_1:64;
        then ((pz`2)^2+(pz`1)^2)<=(1+(pz`1/pz`2)^2) by A91,XCMPLX_1:87;
        then (pz`2)^2+(pz`1)^2<=1+(pz`1)^2/(pz`2)^2 by XCMPLX_1:76;
        then (pz`2)^2+(pz`1)^2-1<=(pz`1)^2/(pz`2)^2 by XREAL_1:20;
        then ((pz`2)^2+(pz`1)^2-1)*(pz`2)^2<=(pz`1)^2/(pz`2)^2*(pz`2)^2 by A87,
XREAL_1:64;
        then ((pz`2)^2+(pz`1)^2-1)*(pz`2)^2<=(pz`1)^2 by A85,XCMPLX_1:6,87;
        then
A92:    ((pz`2)^2-1)*((pz`2)^2+(pz`1)^2)<=0 by Lm19;
        ((pz`2)^2+(pz`1)^2)<>0 by A83,COMPLEX1:1;
        then
A93:    (pz`2)^2-1<=0 by A87,A92,A89,XREAL_1:129;
        then
A94:    pz`2>=-1 by SQUARE_1:43;
A95:    pz`2<=1 by A93,SQUARE_1:43;
        then pz`1<=1 & --pz`2>=-pz`1 or pz`1>=-1 & pz`1<=-pz`2 by A78,A79,A82
,A94,XREAL_1:24,XXREAL_0:2;
        then pz`1<=1 & 1>=-pz`1 or pz`1>=-1 & -pz`1>=--pz`2 by A95,XREAL_1:24
,XXREAL_0:2;
        then pz`1<=1 & 1>=-pz`1 or pz`1>=-1 & -pz`1>=-1 by A94,XXREAL_0:2;
        then pz`1<=1 & -1<=--pz`1 or pz`1>=-1 & pz`1<=1 by XREAL_1:24;
        hence
        -1<=(ff.r)`1 & (ff.r)`1<=1 & -1 <=(ff.r)`2 & (ff.r)`2<=1 by A93,
SQUARE_1:43;
      end;
    end;
    hence thesis by A18;
  end;
  set y = the Element of rng ff /\ rng gg;
A96: p1<>0.TOP-REAL 2 by A4,TOPRNS_1:23;
  then
A97: Sq_Circ".p1=|[p1`1*sqrt(1+(p1`2/p1`1)^2),p1`2*sqrt(1+(p1`2/p1`1)^2 )]|
  by A5,A6,Th28;
  (ff.O)`1=-1 & (ff.I)`1=1 & (gg.O)`2=-1 & (gg.I)`2=1
  proof
    set pz=gg.O;
    set py=ff.I;
    set px=ff.O;
    set q=px;
A98: (|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]|)`1 = q`1/sqrt(1+
    (q `2/q`1)^2) by EUCLID:52;
    set pu=gg.I;
A99: (|[py`1/sqrt(1+(py`2/py`1)^2),py`2/sqrt(1+(py`2/py`1)^2)]|)`1 = py`1/
    sqrt(1+(py`2/py`1)^2) by EUCLID:52;
A100: (|[pu`1/sqrt(1+(pu`1/pu`2)^2),pu`2/sqrt(1+(pu`1/pu`2)^2)]|)`2 = pu`2
    / sqrt(1+(pu`1/pu`2)^2) by EUCLID:52;
A101: 1+(pu`1/pu`2)^2>0 by Lm1;
    Sq_Circ".p1=q by A9,A3,FUNCT_1:12;
    then
A102: p1=Sq_Circ.px by Th43,FUNCT_1:32;
    consider p4 being Point of TOP-REAL 2 such that
A103: g.I=p4 and
A104: |.p4.|=1 and
A105: p4`2>=p4`1 and
A106: p4`2>=-p4`1 by A2;
A107: sqrt(1+(p4`1/p4`2)^2)>0 by Lm1,SQUARE_1:25;
A108: -p4`2<=--p4`1 by A106,XREAL_1:24;
    then
A109: p4`1<=p4`2 & (-p4`2)*sqrt(1+(p4`1/p4`2)^2) <= p4`1*sqrt(1+(p4`1/p4`2
    ) ^2 ) or pu`1>=pu`2 & pu`1<=-pu`2 by A105,A107,XREAL_1:64;
A110: (gg.I)=(Sq_Circ").(g.I) by A8,FUNCT_1:12;
    then
A111: p4=Sq_Circ.pu by A103,Th43,FUNCT_1:32;
A112: p4<>0.TOP-REAL 2 by A104,TOPRNS_1:23;
    then
A113: Sq_Circ".p4=|[p4`1*sqrt(1+(p4`1/p4`2)^2),p4`2*sqrt(1+(p4`1/p4`2) ^2
    )]| by A105,A108,Th30;
    then
A114: pu`2 = p4`2*sqrt(1+(p4`1/p4`2)^2) by A110,A103,EUCLID:52;
A115: pu`1 = p4`1*sqrt(1+(p4`1/p4`2)^2) by A110,A103,A113,EUCLID:52;
A116: now
      assume pu`2=0 & pu`1=0;
      then p4`2=0 & p4`1=0 by A114,A115,A107,XCMPLX_1:6;
      hence contradiction by A112,EUCLID:53,54;
    end;
    p4`1* sqrt(1+(p4`1/p4`2)^2) <= p4`2*sqrt(1+(p4`1/p4`2)^2) & -pu`2<=
pu `1 or pu`1>=pu`2 & pu`1<=-pu`2 by A110,A103,A113,A114,A107,A109,EUCLID:52
,XREAL_1:64;
    then
A117: Sq_Circ.pu=|[pu`1/sqrt(1+(pu`1/pu`2)^2),pu`2/sqrt(1+(pu`1/pu`2)^2)]|
    by A114,A115,A116,Th4,JGRAPH_2:3;
    (|[pu`1/sqrt(1+(pu`1/pu`2)^2),pu`2/sqrt(1+(pu`1/pu`2)^2)]|)`1 = pu`1
    / sqrt(1+(pu`1/pu`2)^2) by EUCLID:52;
    then (|.p4.|)^2= (pu`2/sqrt(1+(pu`1/pu`2)^2))^2+(pu`1/sqrt(1+(pu`1/pu`2)
    ^2 ) ) ^2 by A111,A117,A100,JGRAPH_1:29
      .= (pu`2)^2/(sqrt(1+(pu`1/pu`2)^2))^2+(pu`1/sqrt(1+(pu`1/pu`2)^2))^2
    by XCMPLX_1:76
      .= (pu`2)^2/(sqrt(1+(pu`1/pu`2)^2))^2 +(pu`1)^2/(sqrt(1+(pu`1/pu`2)^2)
    )^2 by XCMPLX_1:76
      .= (pu`2)^2/(1+(pu`1/pu`2)^2)+(pu`1)^2/(sqrt(1+(pu`1/pu`2)^2))^2 by A101,
SQUARE_1:def 2
      .= (pu`2)^2/(1+(pu`1/pu`2)^2)+(pu`1)^2/(1+(pu`1/pu`2)^2) by A101,
SQUARE_1:def 2
      .= ((pu`2)^2+(pu`1)^2)/(1+(pu`1/pu`2)^2) by XCMPLX_1:62;
    then ((pu`2)^2+(pu`1)^2)/(1+(pu`1/pu`2)^2)*(1+(pu`1/pu`2)^2)=1 *(1+(pu`1/
    pu `2)^2) by A104;
    then ((pu`2)^2+(pu`1)^2)=(1+(pu`1/pu`2)^2) by A101,XCMPLX_1:87;
    then
A118: (pu`2)^2+(pu`1)^2-1=(pu`1)^2/(pu`2)^2 by XCMPLX_1:76;
    pu`2<>0 by A114,A115,A107,A116,A109,XREAL_1:64;
    then ((pu`2)^2+(pu`1)^2-1)*(pu`2)^2=(pu`1)^2 by A118,XCMPLX_1:6,87;
    then
A119: ((pu`2)^2-1)*((pu`2)^2+(pu`1)^2)=0;
    ((pu`2)^2+(pu`1)^2)<>0 by A116,COMPLEX1:1;
    then
A120: ((pu`2)^2-1)=0 by A119,XCMPLX_1:6;
A121: sqrt(1+(p1`2/p1`1)^2)>0 by Lm1,SQUARE_1:25;
A122: sqrt(1+(pu`1/pu`2)^2)>0 by Lm1,SQUARE_1:25;
A123: now
      assume
A124: pu`2=-1;
      then -p4`1<0 by A106,A111,A117,A100,A122,XREAL_1:141;
      then --p4`1>-0;
      hence contradiction by A105,A111,A117,A122,A124,EUCLID:52;
    end;
A125: 1+(pz`1/pz`2)^2>0 by Lm1;
A126: px`1 = p1`1*sqrt(1+(p1`2/p1`1)^2) & px`2 = p1`2*sqrt(1+(p1`2/p1`1)^2)
    by A10,A3,A97,EUCLID:52;
A127: now
      assume px`1=0 & px`2=0;
      then p1`1=0 & p1`2=0 by A126,A121,XCMPLX_1:6;
      hence contradiction by A96,EUCLID:53,54;
    end;
    p1`2<=p1`1 & -p1`1<=p1`2 or p1`2>=p1`1 & p1`2*sqrt(1+(p1`2/p1`1)^2)
    <= (-p1`1)*sqrt(1+(p1`2/p1`1)^2) by A5,A6,A121,XREAL_1:64;
    then
A128: p1`2<=p1`1 & (-p1`1)*sqrt(1+(p1`2/p1`1)^2) <= p1`2*sqrt(1+(p1`2/ p1`1
    )^2) or px`2>=px`1 & px`2<=-px`1 by A126,A121,XREAL_1:64;
    then px`2<=px`1 & -px`1<=px`2 or px`2>=px`1 & px`2<=-px`1 by A126,A121,
XREAL_1:64;
    then
A129: Sq_Circ.q=|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]| by A127
,Def1,JGRAPH_2:3;
A130: sqrt(1+(q`2/q`1)^2)>0 by Lm1,SQUARE_1:25;
A131: now
      assume
A132: px`1=1;
      -p1`2>=--p1`1 by A6,XREAL_1:24;
      then -p1`2>0 by A102,A129,A98,A130,A132,XREAL_1:139;
      then --p1`2<-0;
      hence contradiction by A5,A102,A129,A130,A132,EUCLID:52;
    end;
    consider p2 being Point of TOP-REAL 2 such that
A133: f.I=p2 and
A134: |.p2.|=1 and
A135: p2`2<=p2`1 and
A136: p2`2>=-p2`1 by A2;
A137: (ff.I)=(Sq_Circ").(f.I) by A9,FUNCT_1:12;
    then
A138: p2=Sq_Circ.py by A133,Th43,FUNCT_1:32;
A139: p2<>0.TOP-REAL 2 by A134,TOPRNS_1:23;
    then
A140: Sq_Circ".p2=|[p2`1*sqrt(1+(p2`2/p2`1)^2),p2`2*sqrt(1+(p2`2/p2`1) ^2 )
    ]| by A135,A136,Th28;
    then
A141: py`1 = p2`1*sqrt(1+(p2`2/p2`1)^2) by A137,A133,EUCLID:52;
A142: sqrt(1+(p2`2/p2`1)^2)>0 by Lm1,SQUARE_1:25;
A143: py`2 = p2`2*sqrt(1+(p2`2/p2`1)^2) by A137,A133,A140,EUCLID:52;
A144: now
      assume py`1=0 & py`2=0;
      then p2`1=0 & p2`2=0 by A141,A143,A142,XCMPLX_1:6;
      hence contradiction by A139,EUCLID:53,54;
    end;
A145: p2`2<=p2`1 & (-p2`1)*sqrt(1+(p2`2/p2`1)^2) <= p2`2*sqrt(1+(p2`2/p2`1
    ) ^2 ) or py`2>=py`1 & py`2<=-py`1 by A135,A136,A142,XREAL_1:64;
    then p2`2*sqrt(1+(p2`2/p2`1)^2) <= p2`1*sqrt(1+(p2`2/p2`1)^2) & -py `1<=
    py `2 or py`2>=py`1 & py`2<=-py`1 by A137,A133,A140,A141,A142,EUCLID:52
,XREAL_1:64;
    then
A146: Sq_Circ.py=|[py`1/sqrt(1+(py`2/py`1)^2),py`2/sqrt(1+(py`2/py`1)^2)]|
    by A141,A143,A144,Def1,JGRAPH_2:3;
A147: sqrt(1+(py`2/py`1)^2)>0 by Lm1,SQUARE_1:25;
A148: now
      assume
A149: py`1=-1;
      -p2`2<=--p2`1 by A136,XREAL_1:24;
      then -p2`2<0 by A138,A146,A99,A147,A149,XREAL_1:141;
      then --p2`2>-0;
      hence contradiction by A135,A138,A146,A147,A149,EUCLID:52;
    end;
A150: 1+(py`2/py`1)^2>0 by Lm1;
    (|[py`1/sqrt(1+(py`2/py`1)^2),py`2/sqrt(1+(py`2/py`1)^2)]|)`2 = py`2/
    sqrt(1+(py`2/py`1)^2) by EUCLID:52;
    then
    (|.p2.|)^2= (py`1/sqrt(1+(py`2/py`1)^2))^2+(py`2/sqrt(1+(py`2/py`1)^2
    ) ) ^2 by A138,A146,A99,JGRAPH_1:29
      .= (py`1)^2/(sqrt(1+(py`2/py`1)^2))^2+(py`2/sqrt(1+(py`2/py`1)^2))^2
    by XCMPLX_1:76
      .= (py`1)^2/(sqrt(1+(py`2/py`1)^2))^2 +(py`2)^2/(sqrt(1+(py`2/py`1)^2)
    )^2 by XCMPLX_1:76
      .= (py`1)^2/(1+(py`2/py`1)^2)+(py`2)^2/(sqrt(1+(py`2/py`1)^2))^2 by A150,
SQUARE_1:def 2
      .= (py`1)^2/(1+(py`2/py`1)^2)+(py`2)^2/(1+(py`2/py`1)^2) by A150,
SQUARE_1:def 2
      .= ((py`1)^2+(py`2)^2)/(1+(py`2/py`1)^2) by XCMPLX_1:62;
    then
    ((py`1)^2+(py`2)^2)/(1+(py`2/py`1)^2)*(1+(py`2/py`1)^2) =1 *(1+(py`2/
    py`1)^2) by A134;
    then ((py`1)^2+(py`2)^2)=(1+(py`2/py`1)^2) by A150,XCMPLX_1:87;
    then
A151: (py`1)^2+(py`2)^2-1=(py`2)^2/(py`1)^2 by XCMPLX_1:76;
    py`1<>0 by A141,A143,A142,A144,A145,XREAL_1:64;
    then ((py`1)^2+(py`2)^2-1)*(py`1)^2=(py`2)^2 by A151,XCMPLX_1:6,87;
    then
A152: ((py`1)^2-1)*((py`1)^2+(py`2)^2)=0;
    ((py`1)^2+(py`2)^2)<>0 by A144,COMPLEX1:1;
    then
A153: ((py`1)^2-1)=0 by A152,XCMPLX_1:6;
A154: (|[pz`1/sqrt(1+(pz`1/pz`2)^2),pz`2/sqrt(1+(pz`1/pz`2)^2)]|)`2 = pz`2/
    sqrt(1+(pz`1/pz`2)^2) by EUCLID:52;
A155: 1+(q`2/q`1)^2>0 by Lm1;
    (|[q`1/sqrt(1+(q`2/q`1)^2),q`2/sqrt(1+(q`2/q`1)^2)]|)`2 = q`2/sqrt(1+
    (q `2/q`1)^2) by EUCLID:52;
    then (|.p1.|)^2= (q`1/sqrt(1+(q`2/q`1)^2))^2+(q`2/sqrt(1+(q`2/q`1)^2)) ^2
    by A102,A129,A98,JGRAPH_1:29
      .= (q`1)^2/(sqrt(1+(q`2/q`1)^2))^2+(q`2/sqrt(1+(q`2/q`1)^2))^2 by
XCMPLX_1:76
      .= (q`1)^2/(sqrt(1+(q`2/q`1)^2))^2+(q`2)^2/(sqrt(1+(q`2/q`1)^2))^2 by
XCMPLX_1:76
      .= (q`1)^2/(1+(q`2/q`1)^2)+(q`2)^2/(sqrt(1+(q`2/q`1)^2))^2 by A155,
SQUARE_1:def 2
      .= (q`1)^2/(1+(q`2/q`1)^2)+(q`2)^2/(1+(q`2/q`1)^2) by A155,SQUARE_1:def 2
      .= ((q`1)^2+(q`2)^2)/(1+(q`2/q`1)^2) by XCMPLX_1:62;
    then
    ((q`1)^2+(q`2)^2)/(1+(q`2/q`1)^2)*(1+(q`2/q`1)^2)=1 *(1+(q`2/q`1) ^2)
    by A4;
    then (q`1)^2+(q`2)^2=1+(q`2/q`1)^2 by A155,XCMPLX_1:87;
    then
A156: (px`1)^2+(px`2)^2-1=(px`2)^2/(px`1)^2 by XCMPLX_1:76;
    px`1<>0 by A126,A121,A127,A128,XREAL_1:64;
    then ((px`1)^2+(px`2)^2-1)*(px`1)^2=(px`2)^2 by A156,XCMPLX_1:6,87;
    then
A157: ((px`1)^2-1)*((px`1)^2+(px`2)^2)=0;
    consider p3 being Point of TOP-REAL 2 such that
A158: g.O=p3 and
A159: |.p3.|=1 and
A160: p3`2<=p3`1 and
A161: p3`2<=-p3`1 by A2;
A162: p3<>0.TOP-REAL 2 by A159,TOPRNS_1:23;
A163: (gg.O)=(Sq_Circ").(g.O) by A8,FUNCT_1:12;
    then
A164: p3=Sq_Circ.pz by A158,Th43,FUNCT_1:32;
A165: -p3`2>=--p3`1 by A161,XREAL_1:24;
    then
A166: Sq_Circ".p3=|[p3`1*sqrt(1+(p3`1/p3`2)^2),p3`2*sqrt(1+(p3`1/p3`2) ^2)
    ]| by A160,A162,Th30;
    then
A167: pz`2 = p3`2*sqrt(1+(p3`1/p3`2)^2) by A163,A158,EUCLID:52;
A168: sqrt(1+(p3`1/p3`2)^2)>0 by Lm1,SQUARE_1:25;
A169: pz`1 = p3`1*sqrt(1+(p3`1/p3`2)^2) by A163,A158,A166,EUCLID:52;
A170: now
      assume pz`2=0 & pz`1=0;
      then p3`2=0 & p3`1=0 by A167,A169,A168,XCMPLX_1:6;
      hence contradiction by A162,EUCLID:53,54;
    end;
    p3`1<=p3`2 & -p3`2<=p3`1 or p3`1>=p3`2 & p3`1*sqrt(1+(p3`1/p3`2)^2)
    <= (-p3`2)*sqrt(1+(p3`1/p3`2)^2) by A160,A165,A168,XREAL_1:64;
    then
A171: p3`1<=p3`2 & (-p3`2)*sqrt(1+(p3`1/p3`2)^2) <= p3`1*sqrt(1+(p3`1/p3 `2
    )^2) or pz`1>=pz`2 & pz`1<=-pz`2 by A167,A169,A168,XREAL_1:64;
    then
    p3`1*sqrt(1+(p3`1/p3`2)^2) <= p3`2*sqrt(1+(p3`1/p3`2)^2) & -pz `2<=pz
`1 or pz`1>=pz`2 & pz`1<=-pz`2 by A163,A158,A166,A167,A168,EUCLID:52,XREAL_1:64
    ;
    then
A172: Sq_Circ.pz=|[pz`1/sqrt(1+(pz`1/pz`2)^2),pz`2/sqrt(1+(pz`1/pz`2)^2)]|
    by A167,A169,A170,Th4,JGRAPH_2:3;
A173: sqrt(1+(pz`1/pz`2)^2)>0 by Lm1,SQUARE_1:25;
A174: now
      assume
A175: pz`2=1;
      then -p3`1>0 by A161,A164,A172,A154,A173,XREAL_1:139;
      then --p3`1<-0;
      hence contradiction by A160,A164,A172,A173,A175,EUCLID:52;
    end;
    (|[pz`1/sqrt(1+(pz`1/pz`2)^2),pz`2/sqrt(1+(pz`1/pz`2)^2)]|)`1 = pz`1/
    sqrt(1+(pz`1/pz`2)^2) by EUCLID:52;
    then (|.p3.|)^2= (pz`2/sqrt(1+(pz`1/pz`2)^2))^2+(pz`1/sqrt(1+(pz`1/pz`2)
    ^2))^2 by A164,A172,A154,JGRAPH_1:29
      .= (pz`2)^2/(sqrt(1+(pz`1/pz`2)^2))^2+(pz`1/sqrt(1+(pz`1/pz`2)^2))^2
    by XCMPLX_1:76
      .= (pz`2)^2/(sqrt(1+(pz`1/pz`2)^2))^2 +(pz`1)^2/(sqrt(1+(pz`1/pz`2)^2)
    )^2 by XCMPLX_1:76
      .= (pz`2)^2/(1+(pz`1/pz`2)^2)+(pz`1)^2/(sqrt(1+(pz`1/pz`2)^2))^2 by A125,
SQUARE_1:def 2
      .= (pz`2)^2/(1+(pz`1/pz`2)^2)+(pz`1)^2/(1+(pz`1/pz`2)^2) by A125,
SQUARE_1:def 2
      .= ((pz`2)^2+(pz`1)^2)/(1+(pz`1/pz`2)^2) by XCMPLX_1:62;
    then ((pz`2)^2+(pz`1)^2)/(1+(pz`1/pz`2)^2)*(1+(pz`1/pz`2)^2)=1 *(1+(pz`1/
    pz `2)^2) by A159;
    then ((pz`2)^2+(pz`1)^2)=(1+(pz`1/pz`2)^2) by A125,XCMPLX_1:87;
    then
A176: (pz`2)^2+(pz`1)^2-1=(pz`1)^2/(pz`2)^2 by XCMPLX_1:76;
    pz`2<>0 by A167,A169,A168,A170,A171,XREAL_1:64;
    then ((pz`2)^2+(pz`1)^2-1)*(pz`2)^2=(pz`1)^2 by A176,XCMPLX_1:6,87;
    then
A177: ((pz`2)^2-1)*((pz`2)^2+(pz`1)^2)=0;
    ((pz`2)^2+(pz`1)^2)<>0 by A170,COMPLEX1:1;
    then
A178: (pz`2)^2-1=0 by A177,XCMPLX_1:6;
    ((px`1)^2+(px`2)^2)<>0 by A127,COMPLEX1:1;
    then ((px`1)^2-1)=0 by A157,XCMPLX_1:6;
    hence thesis by A131,A153,A148,A178,A174,A120,A123,Lm20;
  end;
  then rng ff meets rng gg by A2,A12,Th42,JGRAPH_1:47;
  then
A179: rng ff /\ rng gg <>{} by XBOOLE_0:def 7;
  then y in rng ff by XBOOLE_0:def 4;
  then consider x1 being set such that
A180: x1 in dom ff and
A181: y=ff.x1 by FUNCT_1:def 3;
  x1 in dom f by A180,FUNCT_1:11;
  then
A182: f.x1 in rng f by FUNCT_1:def 3;
  y in rng gg by A179,XBOOLE_0:def 4;
  then consider x2 being set such that
A183: x2 in dom gg and
A184: y=gg.x2 by FUNCT_1:def 3;
A185: gg.x2=Sq_Circ".(g.x2) by A183,FUNCT_1:12;
  x2 in dom g by A183,FUNCT_1:11;
  then
A186: g.x2 in rng g by FUNCT_1:def 3;
  ff.x1=Sq_Circ".(f.x1) by A180,FUNCT_1:12;
  then f.x1=g.x2 by A181,A184,A1,A182,A186,A185,FUNCT_1:def 4;
  then rng f /\ rng g <> {} by A182,A186,XBOOLE_0:def 4;
  hence thesis by XBOOLE_0:def 7;
end;
