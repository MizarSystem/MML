:: The Sum and Product of Finite Sequences of Real Numbers
::  by Czes{\l}aw Byli\'nski
::
:: Received May 11, 1990
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies FINSEQ_1, FINSEQ_2, ARYTM_1, FUNCT_1, RELAT_1, BINOP_1, VECTSP_1,
      SETWISEO, SQUARE_1, ARYTM, FUNCOP_1, FINSEQOP, RLVECT_1, RVSUM_1, CARD_3,
      BOOLE, COMPLEX1, COMPLSP1, SEQ_1, XCMPLX_0, SUBSET_1, NAT_1;
 notations TARSKI, SUBSET_1, ZFMISC_1, ORDINAL1, NUMBERS, XBOOLE_0, XCMPLX_0,
      XXREAL_0, XREAL_0, REAL_1, NAT_1, SQUARE_1, RELAT_1, FUNCT_1, RELSET_1,
      PARTFUN1, BINOP_2, SETWISEO, FUNCT_2, BINOP_1, FUNCOP_1, FINSEQ_1,
      FINSEQ_2, FINSEQ_4, SEQ_1, FINSOP_1, FINSEQOP, SETWOP_2;
 constructors PARTFUN1, BINOP_1, SETWISEO, REAL_1, SQUARE_1, NAT_1, BINOP_2,
      SEQ_1, FINSEQOP, FINSEQ_4, FINSOP_1, SETWOP_2;
 registrations XBOOLE_0, FUNCT_1, ORDINAL1, RELSET_1, FUNCT_2, FUNCOP_1,
      NUMBERS, XCMPLX_0, XREAL_0, SQUARE_1, NAT_1, BINOP_2, MEMBERED, FINSEQ_1,
      SEQ_1, FINSEQ_2;
 requirements NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions SEQ_1, BINOP_1, FINSEQOP, FINSEQ_1, SQUARE_1, FINSEQ_2, TARSKI;
 theorems SQUARE_1, FUNCT_1, FUNCT_2, BINOP_1, FINSEQ_1, FUNCOP_1, FINSEQ_2,
      FINSEQOP, SETWOP_2, RELAT_1, FINSOP_1, XREAL_0, ZFMISC_1, XCMPLX_0,
      XCMPLX_1, BINOP_2, SETWISEO, XREAL_1, SEQ_1, NUMBERS, XBOOLE_1, FINSEQ_3;
 schemes NAT_1, FUNCT_2;

begin :: Auxiliary theorems

Lm1: for a, b being real number holds
   multreal.(a,b) = a * b
  proof
    let a, b be real number;
    reconsider a' = a, b' = b as Element of REAL by XREAL_0:def 1;
     multreal.(a',b') = a' * b' by BINOP_2:def 11;
    hence thesis;
  end;

Lm2: for a, b being complex number holds
   multcomplex.(a,b) = a * b
  proof
    let a, b be complex number;
    reconsider a' = a, b' = b as Element of COMPLEX by XCMPLX_0:def 2;
     multcomplex.(a',b') = a' * b' by BINOP_2:def 5;
    hence thesis;
  end;

registration
  cluster real-yielding FinSequence;
  existence
  proof
    consider f being FinSequence of REAL;
     f is real-yielding;
    hence thesis;
  end;
end;

definition
  let F be real-yielding Relation;
  redefine func rng F -> Subset of REAL;
  coherence by SEQ_1:def 1;
end;

registration
  let f, g be real-yielding Relation;
  cluster f*g -> real-yielding;
  coherence
  proof
     rng (f*g) c= rng g by RELAT_1:45;
    hence rng (f*g) c= REAL by XBOOLE_1:1;
  end;
end;

registration
  let D be non empty set;
  let F be Function of REAL,D; let F1 be real-yielding FinSequence;
  cluster F*F1 -> FinSequence-like;
  coherence
  proof
    consider n1 being Element of NAT such that
A1: dom F1 = Seg n1 by FINSEQ_1:def 2;
    take n1;
A2: dom F = REAL by FUNCT_2:def 1;
     rng F1 c= REAL;
    hence dom(F*F1) = Seg n1 by A2,RELAT_1:46,A1;
  end;
end;

registration let r be real number;
  cluster <*r*> -> real-yielding;
  coherence
  proof
    reconsider p = r as Element of REAL by XREAL_0:def 1;
    reconsider f = <*p*> as FinSequence of REAL;
     f is FinSequence-like;
    hence thesis;
  end;
end;

registration let r1, r2 be real number;
  cluster <*r1, r2*> -> real-yielding;
  coherence
  proof
    reconsider p1 = r1, p2 = r2 as Element of REAL by XREAL_0:def 1;
    reconsider f = <*p1,p2*> as FinSequence of REAL;
     f is FinSequence-like;
    hence thesis;
  end;
end;

registration let r1, r2, r3 be real number;
  cluster <*r1, r2, r3*> -> real-yielding;
  coherence
  proof
    reconsider p1 = r1, p2 = r2, p3 = r3 as Element of REAL
      by XREAL_0:def 1;
    reconsider f = <*p1,p2,p3*> as FinSequence of REAL;
     f is FinSequence-like;
    hence thesis;
  end;
end;

registration let j be Nat, r be real number;
  cluster j |-> r -> real-yielding;
  coherence
  proof
     r in REAL by XREAL_0:def 1;
    then (j |-> r) is FinSequence of REAL by FINSEQ_2:77;
    hence thesis;
  end;
end;

registration let f, g be real-yielding FinSequence;
  cluster f ^ g -> real-yielding;
  coherence
  proof
     rng (f ^ g) = rng f \/ rng g by FINSEQ_1:44;
    hence rng (f ^ g) c= REAL by XBOOLE_1:8;
  end;
end;

reserve i,j,k for Element of NAT,
        x,x1,x2,x3 for Element of REAL,
        r,r1,r2,r3 for real number,
        F,F1,F2 for real-yielding FinSequence,
        R,R1,R2,R3 for Element of i-tuples_on REAL;

canceled 2;

theorem
   0 is_a_unity_wrt addreal by BINOP_2:2,SETWISEO:22;

definition
  redefine func diffreal equals
     addreal*(id REAL,compreal);
  compatibility
   proof let b be BinOp of REAL;
      now let r1,r2 be Element of REAL;
      thus diffreal.(r1,r2) = r1 - r2 by BINOP_2:def 10
          .= r1 + - r2
          .= addreal.(r1,- r2) by BINOP_2:def 9
          .= addreal.(r1,compreal.r2) by BINOP_2:def 7
          .= (addreal*(id REAL,compreal)).(r1,r2) by FINSEQOP:87;
     end;
    hence thesis by BINOP_1:2;
   end;
  correctness;
end;

definition
  func sqrreal -> UnOp of REAL means
:Def2: for r holds it.r = r^2;
  existence
 proof
  deffunc F(Element of REAL) = $1^2;
  consider G being Function of REAL,REAL such that
A1: for x being Element of REAL holds G.x = F(x) from FUNCT_2:sch 4;
  take G; let r;  r in REAL by XREAL_0:def 1; hence G.r = (r)^2 by A1;
 end;
  uniqueness
 proof let G1,G2 be UnOp of REAL such that
A2: (for r holds G1.r = r^2) & for r holds G2.r = r^2;
      now let x;   G1.(x) = (x)^2 & G2.(x) = (x)^2 & x = x by A2;
   hence G1.x = G2.x;
  end;
  hence thesis by FUNCT_2:113;
 end;
end;

canceled 9;

theorem
   1 is_a_unity_wrt multreal by BINOP_2:7,SETWISEO:22;

canceled 2;

theorem Th16:
  multreal is_distributive_wrt addreal
 proof
   now let x1,x2,x3;
   thus multreal.(x1,addreal.(x2,x3))
      = multreal.(x1,x2+x3) by BINOP_2:def 9
     .= x1*(x2+x3) by BINOP_2:def 11
     .= x1*x2+x1*x3
     .= addreal.(x1*x2,x1*x3) by BINOP_2:def 9
     .= addreal.(multreal.(x1,x2),x1*x3) by BINOP_2:def 11
     .= addreal.(multreal.(x1,x2),multreal.(x1,x3)) by BINOP_2:def 11;
   thus multreal.(addreal.(x1,x2),x3)
      = multreal.(x1+x2,x3) by BINOP_2:def 9
     .= (x1+x2)*x3 by BINOP_2:def 11
     .= x1*x3+x2*x3
     .= addreal.(x1*x3,x2*x3) by BINOP_2:def 9
     .= addreal.(multreal.(x1,x3),x2*x3) by BINOP_2:def 11
     .= addreal.(multreal.(x1,x3),multreal.(x2,x3)) by BINOP_2:def 11;
  end;
  hence thesis by BINOP_1:23;
 end;

theorem Th17:
  sqrreal is_distributive_wrt multreal
 proof let x1,x2;
   thus sqrreal.(multreal.(x1,x2))
      = sqrreal.(x1*x2) by BINOP_2:def 11
     .= (x1*x2)^2 by Def2
     .= x1^2*x2^2
     .= multreal.(x1^2,x2^2) by BINOP_2:def 11
     .= multreal.(sqrreal.x1,x2^2) by Def2
     .= multreal.(sqrreal.x1,sqrreal.x2) by Def2;
 end;

definition let x be real number;
  func x multreal -> UnOp of REAL equals
   multreal[;](x,id REAL);
 coherence
   proof
     reconsider y = x as Real by XREAL_0:def 1;
      multreal[;](y,id REAL) is UnOp of REAL;
     hence thesis;
   end;
end;

Lm3:
 (multreal[;](r,id REAL)).r1 = r*r1
 proof
  reconsider a=r, s=r1 as Element of REAL by XREAL_0:def 1;
  thus (multreal[;](r,id REAL)).r1 = multreal.(a,(id REAL).s) by FUNCOP_1:66
  .= multreal.(a,s) by FUNCT_1:35
  .= r*r1 by BINOP_2:def 11;
 end;

canceled;

theorem
   (r multreal).r1 = r*r1 by Lm3;

theorem
   r multreal is_distributive_wrt addreal
  proof
     r in REAL by XREAL_0:def 1;
    hence thesis by Th16,FINSEQOP:55;
  end;

theorem Th21:
  compreal is_an_inverseOp_wrt addreal
 proof let x;
   thus addreal.(x,compreal.x) = x+compreal.x by BINOP_2:def 9
   .= x+-x by BINOP_2:def 7
   .= the_unity_wrt addreal by BINOP_2:2;
   thus addreal.(compreal.x,x) = compreal.x+x by BINOP_2:def 9
   .= -x+x by BINOP_2:def 7
   .= the_unity_wrt addreal by BINOP_2:2;
 end;

theorem Th22:
  addreal has_an_inverseOp by Th21,FINSEQOP:def 2;

theorem Th23:
  the_inverseOp_wrt addreal = compreal by Th21,Th22,FINSEQOP:def 3;

theorem
   compreal is_distributive_wrt addreal by Th22,Th23,FINSEQOP:67;

:: Some Functors on the i-tuples on Real

Lm4: F is FinSequence of REAL
     proof
       thus rng F c= REAL;
     end;

Lm5: now let F1,F2;
  thus F1 + F2 is FinSequence-like
  proof
A1: dom (F1+F2) = dom F1 /\ dom F2 by SEQ_1:def 3;
    consider n1 being Element of NAT such that
A2: dom F1 = Seg n1 by FINSEQ_1:def 2;
    consider n2 being Element of NAT such that
A3: dom F2 = Seg n2 by FINSEQ_1:def 2;
    per cases;
    suppose n1 <= n2;
      then A4:   Seg n1 c= Seg n2 by FINSEQ_1:7;
      take n1;
      thus dom(F1+F2) = Seg n1 by A2,A3,A4,A1,XBOOLE_1:28;
    end;
    suppose n2 <= n1;
      then A5:   Seg n2 c= Seg n1 by FINSEQ_1:7;
      take n2;
      thus dom(F1+F2) = Seg n2 by A2,A3,A5,A1,XBOOLE_1:28;
    end;
  end;
end;

definition let F1,F2;
  redefine func F1 + F2 -> FinSequence of REAL equals
   addreal.:(F1,F2);
  coherence
  proof
     F1+F2 is FinSequence-like by Lm5;
    hence thesis by Lm4;
  end;
  compatibility
  proof
    let F be FinSequence of REAL;
A1: dom (F1+F2) = dom F1 /\ dom F2 by SEQ_1:def 3;
    reconsider F3=F1,F4=F2 as FinSequence of REAL by Lm4;
 dom addreal = [:REAL,REAL:] by FUNCT_2:def 1;
    then [:rng F3, rng F4:] c= dom addreal by ZFMISC_1:119;
then A2: dom(addreal.:(F1,F2)) = dom F1 /\ dom F2 by FUNCOP_1:84;
    thus F = F1+F2 implies F = addreal.:(F1,F2)
    proof
      assume
A3:   F = F1+F2;
       for z being set st z in dom(addreal.:(F1,F2))
      holds F.z = addreal.(F1.z,F2.z)
      proof let z be set;
        assume z in dom(addreal.:(F1,F2));
        hence F.z = F1.z + F2.z by A1,A3,A2,SEQ_1:def 3
        .= addreal.(F1.z,F2.z) by BINOP_2:def 9;
     end;
     hence thesis by A1,A3,A2,FUNCOP_1:27;
   end;
   assume
A4: F = addreal.:(F1,F2);
    now let c be set;
     assume c in dom F;
     hence F.c = addreal.(F1.c,F2.c) by A4,FUNCOP_1:28
     .= F1.c + F2.c by BINOP_2:def 9;
   end;
   hence thesis by A2,A4,SEQ_1:def 3;
  end;
 commutativity
  proof let F be FinSequence of REAL;
    let F1,F2;
   assume
A5:  F = addreal.:(F1,F2);
     reconsider F1,F2 as FinSequence of REAL by Lm4;
A6:  dom addreal = [:REAL,REAL:] by FUNCT_2:def 1;
then A7:  [:rng F2, rng F1:] c= dom addreal by ZFMISC_1:119;
      [:rng F1, rng F2:] c= dom addreal by A6,ZFMISC_1:119;
then A8:  dom(addreal.:(F1,F2)) = dom F1 /\ dom F2 by FUNCOP_1:84
          .= dom(addreal.:(F2,F1)) by A7,FUNCOP_1:84;
        for z being set st z in dom(addreal.:(F2,F1))
       holds F.z = addreal.(F2.z,F1.z)
     proof let z be set such that
A9:    z in dom(addreal.:(F2,F1));
      thus F.z = addreal.(F1.z,F2.z) by A5,A8,A9,FUNCOP_1:28
           .= F1.z + F2.z by BINOP_2:def 9
           .= addreal.(F2.z,F1.z) by BINOP_2:def 9;
     end;
   hence thesis by A5,A8,FUNCOP_1:27;
  end;
end;

definition let i,R1,R2;
  redefine func R1 + R2 -> Element of i-tuples_on REAL;
  coherence by FINSEQ_2:140;
end;

canceled 2;

theorem Th27:
  (R1+R2).j = R1.j + R2.j
 proof per cases;
  suppose
A1:  not j in Seg i;
then A2:  not j in dom R1 by FINSEQ_2:144;
A3:  not j in dom R2 by A1,FINSEQ_2:144;
      not j in dom(R1+R2) by A1,FINSEQ_2:144;
   hence (R1+R2).j = 0+0 by FUNCT_1:def 4
       .= R1.j + 0 by A2,FUNCT_1:def 4
       .= R1.j + R2.j by A3,FUNCT_1:def 4;
  end;
  suppose j in Seg i;
   then j in dom (R1 + R2) by FINSEQ_2:144;
  hence thesis by SEQ_1:def 3;
 end;
 end;

theorem
   <*>REAL + F = <*>REAL
  proof
     F is FinSequence of REAL by Lm4;
    hence thesis by FINSEQ_2:87;
  end;

theorem
   <*r1*> + <*r2*> = <*r1+r2*>
 proof
   reconsider s1=r1, s2=r2 as Element of REAL by XREAL_0:def 1;
    <*s1*> + <*s2*> = <*addreal.(s1,s2)*> by FINSEQ_2:88
   .= <*r1+r2*> by BINOP_2:def 9;
   hence thesis;
 end;

theorem
   (i|->r1) + (i|->r2) = i|->(r1+r2)
 proof
   reconsider s1=r1, s2=r2 as Element of REAL by XREAL_0:def 1;
    (i|->s1) + (i|->s2) = i|->(addreal.(s1,s2)) by FINSEQOP:18
   .= i|->(r1+r2) by BINOP_2:def 9;
   hence thesis;
 end;

canceled;

theorem Th32:
  R1 + (R2 + R3) = R1 + R2 + R3 by FINSEQOP:29;

theorem
   R + (i|->(0 qua Real)) = R by BINOP_2:2,FINSEQOP:57;

Lm6: now
  let F;
  thus -F is FinSequence-like
  proof
    consider n being Element of NAT such that
A1: dom F = Seg n by FINSEQ_1:def 2;
    take n;
    thus thesis by A1,SEQ_1:def 7;
  end;
end;

canceled;

theorem Th35:
  for i being set holds (-F).i = -F.i
 proof
   let i be set;
    per cases;
  suppose
A1: not i in dom -F;
then A2: not i in dom F by SEQ_1:def 7;
   thus (-F).i = -0 by A1,FUNCT_1:def 4
      .= -F.i by A2,FUNCT_1:def 4;
  end;
  suppose i in dom -F;
    hence thesis by SEQ_1:def 7;
  end;
end;

definition let F;
  redefine func -F -> FinSequence of REAL equals
   compreal*F;
  coherence
  proof
     -F is FinSequence-like by Lm6;
    hence thesis by Lm4;
  end;
  compatibility
  proof
    let F1 be FinSequence of REAL;
A1: dom(-F) = dom F by SEQ_1:def 7;
A2: rng F c= REAL;
     dom compreal = REAL by FUNCT_2:def 1;
    then A3: dom(compreal*F) = dom F by A2,RELAT_1:46;
    thus F1 = -F implies F1 = compreal*F
    proof
      assume
A4:   F1 = -F;
       now
        let c be set;
        assume
A5:     c in dom F1;
        hence F1.c = -(F.c) by A4,SEQ_1:def 7
        .= compreal.(F.c) by BINOP_2:def 7
        .= (compreal*F).c by A1,A4,A5,FUNCT_1:23;
      end;
      hence thesis by A1,A3,A4,FUNCT_1:9;
    end;
    assume
A6: F1 = compreal*F;
     now
      let c be set;
      assume
A7:   c in dom F1;
      thus (-F).c = -(F.c) by Th35
      .= compreal.(F.c) by BINOP_2:def 7
      .= (compreal*F).c by A6,A7,FUNCT_1:22;
    end;
    hence thesis by A1,A3,A6,FUNCT_1:9;
  end;
end;

definition let i,R;
  redefine func -R -> Element of i-tuples_on REAL;
  coherence by FINSEQ_2:133;
end;

theorem
   (-R).j = -(R.j) by Th35;

theorem
   -(<*>REAL) = <*>REAL by FINSEQ_2:38;

theorem
   -<*r*> = <*-r*>
 proof
   reconsider s=r as Element of REAL by XREAL_0:def 1;
    -<*s*> = <*compreal.s*> by FINSEQ_2:39
   .= <*-r*> by BINOP_2:def 7;
   hence thesis;
 end;

theorem Th39:
  -(i|->r) = i|->-r
 proof
   reconsider s=r as Element of REAL by XREAL_0:def 1;
    -(i|->s) = i|->(compreal.s) by FINSEQOP:17
   .= i|->-r by BINOP_2:def 7;
   hence thesis;
 end;

theorem
   R + -R = (i|->0) by Th22,Th23,BINOP_2:2,FINSEQOP:77;

theorem
   R1 + R2 = (i|->0) implies R1 = -R2 by Th22,Th23,BINOP_2:2,FINSEQOP:78;

canceled;

theorem
   -R1 = -R2 implies R1 = R2
  proof assume -R1 = -R2;
    hence R1 = --R2 .= R2;
  end;

theorem
   R1 + R = R2 + R implies R1 = R2
 proof assume R1 + R = R2 + R;
  then R1 + (R + -R)= (R2 + R)+-R by FINSEQOP:29;
  then R1 + (R + -R)= R2 + (R + -R) & R + -R = (i|->0)
  by FINSEQOP:29,77,Th22,Th23,BINOP_2:2;
  then R1 = R2 + (i|->(0 qua Real)) by BINOP_2:2,FINSEQOP:57;
  hence R1 = R2 by BINOP_2:2,FINSEQOP:57;
 end;

theorem Th45:
  -(R1 + R2) = -R1 + -R2
 proof
    (R1 + R2) + (-R1 + -R2) = R1 + R2 + -R1 + -R2 by FINSEQOP:29
   .= R2 + (R1 + -R1) + -R2 by Th32
   .= R2 + (i|->(0 qua Real)) + -R2 by Th22,Th23,BINOP_2:2,FINSEQOP:77
   .= R2 + -R2 by BINOP_2:2,FINSEQOP:57
   .= (i|->0) by Th22,Th23,BINOP_2:2,FINSEQOP:77;
   hence thesis by Th22,Th23,BINOP_2:2,FINSEQOP:78;
 end;

Lm7: now let F1,F2;
  thus F1 - F2 is FinSequence-like
  proof
A1: dom (F1-F2) = dom F1 /\ dom F2 by SEQ_1:def 4;
    consider n1 being Element of NAT such that
A2: dom F1 = Seg n1 by FINSEQ_1:def 2;
    consider n2 being Element of NAT such that
A3: dom F2 = Seg n2 by FINSEQ_1:def 2;
    per cases;
    suppose n1 <= n2;
      then A4:   Seg n1 c= Seg n2 by FINSEQ_1:7;
      take n1;
      thus dom(F1-F2) = Seg n1 by A2,A3,A4,A1,XBOOLE_1:28;
    end;
    suppose n2 <= n1;
      then A5:   Seg n2 c= Seg n1 by FINSEQ_1:7;
      take n2;
      thus dom(F1-F2) = Seg n2 by A2,A3,A5,A1,XBOOLE_1:28;
    end;
  end;
end;

definition let F1,F2;
  redefine func F1 - F2 -> FinSequence of REAL equals
   diffreal.:(F1,F2);
  coherence
  proof
     F1 - F2 is FinSequence-like by Lm7;
    hence thesis by Lm4;
  end;
  compatibility
  proof
    let F be FinSequence of REAL;
A1: dom (F1-F2) = dom F1 /\ dom F2 by SEQ_1:def 4;
    reconsider F3=F1,F4=F2 as FinSequence of REAL by Lm4;
 dom diffreal = [:REAL,REAL:] by FUNCT_2:def 1;
    then [:rng F3, rng F4:] c= dom diffreal by ZFMISC_1:119;
then A2: dom(diffreal.:(F1,F2)) = dom F1 /\ dom F2 by FUNCOP_1:84;
    thus F = F1-F2 implies F = diffreal.:(F1,F2)
    proof
      assume
A3:   F = F1-F2;
       for z being set st z in dom(diffreal.:(F1,F2))
      holds F.z = diffreal.(F1.z,F2.z)
      proof let z be set;
        assume z in dom(diffreal.:(F1,F2));
        hence F.z = F1.z - F2.z by A1,A3,A2,SEQ_1:def 4
        .= diffreal.(F1.z,F2.z) by BINOP_2:def 10;
     end;
     hence thesis by A1,A3,A2,FUNCOP_1:27;
   end;
   assume
A4: F = diffreal.:(F1,F2);
    now let c be set;
     assume c in dom F;
     hence F.c = diffreal.(F1.c,F2.c) by A4,FUNCOP_1:28
     .= F1.c - F2.c by BINOP_2:def 10;
   end;
   hence thesis by A2,A4,SEQ_1:def 4;
  end;
end;

definition let i,R1,R2;
  redefine func R1 - R2 -> Element of i-tuples_on REAL;
  coherence by FINSEQ_2:140;
end;

canceled 2;

theorem
   (R1-R2).j = R1.j - R2.j
 proof per cases;
  suppose
A1:  not j in Seg i;
then A2:  not j in dom R1 by FINSEQ_2:144;
A3:  not j in dom R2 by A1,FINSEQ_2:144;
      not j in dom(R1-R2) by A1,FINSEQ_2:144;
   hence (R1-R2).j = 0-0 by FUNCT_1:def 4
       .= R1.j - 0 by A2,FUNCT_1:def 4
       .= R1.j - R2.j by A3,FUNCT_1:def 4;
  end;
  suppose j in Seg i;
  then j in dom (R1 - R2) by FINSEQ_2:144;
  hence thesis by SEQ_1:def 4;
 end;
 end;

theorem
   <*>REAL - F = <*>REAL & F - <*>REAL = <*>REAL
  proof
     F is FinSequence of REAL by Lm4;
    hence thesis by FINSEQ_2:87;
  end;

theorem
   <*r1*> - <*r2*> = <*r1-r2*>
 proof
   reconsider s1=r1, s2=r2 as Element of REAL by XREAL_0:def 1;
    <*s1*> - <*s2*> = <*diffreal.(s1,s2)*> by FINSEQ_2:88
   .= <*r1-r2*> by BINOP_2:def 10;
   hence thesis;
 end;

theorem
   (i|->r1) - (i|->r2) = i|->(r1-r2)
 proof
   reconsider s1=r1, s2=r2 as Element of REAL by XREAL_0:def 1;
    (i|->s1) - (i|->s2) = i|->(diffreal.(s1,s2)) by FINSEQOP:18
   .= i|->(r1-r2) by BINOP_2:def 10;
   hence thesis;
 end;

theorem Th52:
  R1 - R2 = R1 + - R2 by FINSEQOP:89;

theorem
   R - (i|->(0 qua Real)) = R
 proof
  thus R - (i|->(0 qua Real)) = R + - (i|->(0 qua Real)) by FINSEQOP:89
  .= R + (i|->(-0 qua Real)) by Th39 .= R by BINOP_2:2,FINSEQOP:57;
 end;

theorem
   (i|->(0 qua Real)) - R = -R
 proof thus (i|->(0 qua Real)) - R = (i|->(0 qua Real)) + -R by FINSEQOP:89
   .= - R by BINOP_2:2,FINSEQOP:57;
 end;

theorem
   R1 - -R2 = R1 + R2
 proof
   thus R1 - -R2 = R1 + --R2 by FINSEQOP:89 .= R1 + R2;
 end;

theorem
   -(R1 - R2) = R2 - R1
 proof
   thus -(R1 - R2) = -(R1 + -R2) by FINSEQOP:89
                  .= -R1 + --R2 by Th45
                  .= R2 - R1 by Th52;
 end;

theorem Th57:
  -(R1 - R2) = -R1 + R2
 proof
   thus -(R1 - R2) = -(R1+ -R2) by FINSEQOP:89
                  .= -R1 +--R2 by Th45
                  .= -R1 + R2;
 end;

theorem Th58:
  R - R = (i|->0)
 proof thus R - R = R + - R by FINSEQOP:89
   .= (i|->0) by Th22,Th23,BINOP_2:2,FINSEQOP:77; end;

theorem
   R1 - R2 = (i|->0) implies R1 = R2
 proof assume R1 - R2 = (i|->0);
   then R1 + - R2 = (i|->0) by FINSEQOP:89;
   then R1 = --R2 by Th22,Th23,BINOP_2:2,FINSEQOP:78;
   hence thesis;
 end;

theorem
   R1 - R2 - R3 = R1 - (R2 + R3)
 proof
   thus R1 - R2 - R3 = R1 - R2 + - R3 by FINSEQOP:89
                    .= R1 + - R2 + - R3 by FINSEQOP:89
                    .= R1 + (- R2 + - R3) by FINSEQOP:29
                    .= R1 + -(R2 + R3) by Th45
                    .= R1 - (R2 + R3) by FINSEQOP:89;
 end;

theorem Th61:
  R1 + (R2 - R3) = R1 + R2 - R3
 proof
   thus R1 + (R2 - R3) = R1 + (R2 + - R3) by FINSEQOP:89
                      .= R1 + R2 + - R3 by FINSEQOP:29
                      .= R1 + R2 - R3 by FINSEQOP:89;
 end;

theorem
   R1 - (R2 - R3) = R1 - R2 + R3
 proof
   thus R1 - (R2 - R3) = R1 + - (R2 - R3) by FINSEQOP:89
                      .= R1 + (- R2 + R3) by Th57
                      .= R1 + - R2 + R3 by FINSEQOP:29
                      .= R1 - R2 + R3 by FINSEQOP:89;
 end;

theorem
   R1 = R1 + R - R
 proof
   thus R1 = R1 + (i|->(0 qua Real)) by BINOP_2:2,FINSEQOP:57
          .= R1 + (R - R) by Th58
          .= R1 + R - R by Th61;
 end;

theorem
   R1 = R1 - R + R
 proof
   thus R1 = R1 + (i|->(0 qua Real)) by BINOP_2:2,FINSEQOP:57
          .= R1 + (-R + R) by Th22,Th23,BINOP_2:2,FINSEQOP:77
          .= R1 + -R + R by FINSEQOP:29
          .= R1 - R + R by FINSEQOP:89;
 end;

notation
  let F,r;
  synonym r*F for r(#)F;
end;

Lm8: now
  let F,r;
  thus r*F is FinSequence-like
  proof
    consider n being Element of NAT such that
A1: dom F = Seg n by FINSEQ_1:def 2;
    take n;
    thus thesis by A1,SEQ_1:def 6;
  end;
end;

canceled;

theorem Th66:
  for i being set holds
  (r*F).i = r*(F.i) :: poprawic definicje SEQ_1
 proof
  let i be set;
   per cases;
  suppose
A1: not i in dom (r*F);
    then A2:  not i in dom F by SEQ_1:def 6;
   thus (r*F).i = r*0 by A1,FUNCT_1:def 4
      .= r*(F.i) by A2,FUNCT_1:def 4;
  end;
  suppose i in dom (r*F);
  hence (r*F).i = r*(F.i) by SEQ_1:def 6;
 end;
 end;

definition let F,r;
  redefine func r*F -> FinSequence of REAL equals
   (r multreal)*F;
  coherence
  proof
     r*F is FinSequence-like by Lm8;
    hence thesis by Lm4;
  end;
  compatibility
  proof
    let F1 be FinSequence of REAL;
A1: dom(r*F) = dom F by SEQ_1:def 6;
A2: rng F c= REAL;
     dom(r multreal) = REAL by FUNCT_2:def 1;
    then A3: dom((r multreal)*F) = dom F by A2,RELAT_1:46;
    thus F1 = r*F implies F1 = (r multreal)*F
    proof
      assume
A4:   F1 = r*F;
       now
        let c be set;
        assume
A5:     c in dom F1;
        hence F1.c = r*(F.c) by A4,SEQ_1:def 6
        .= (r multreal).(F.c) by Lm3
        .= ((r multreal)*F).c by A1,A4,A5,FUNCT_1:23;
      end;
      hence thesis by A1,A3,A4,FUNCT_1:9;
    end;
    assume
A6: F1 = (r multreal)*F;
     now
      let c be set;
      assume
A7:   c in dom F1;
      thus (r*F).c = r*(F.c) by Th66
      .= (r multreal).(F.c) by Lm3
      .= ((r multreal)*F).c by A6,A7,FUNCT_1:22;
    end;
    hence thesis by A1,A3,A6,FUNCT_1:9;
  end;
end;

definition let i,R,r;
  redefine func r*R -> Element of i-tuples_on REAL;
  coherence by FINSEQ_2:133;
end;

theorem
   (r*R).j = r*(R.j) by Th66;

theorem
   r*(<*>REAL) = <*>REAL by FINSEQ_2:38;

theorem
   r*<*r1*> = <*r*r1*>
 proof
   reconsider s=r, s1=r1 as Element of REAL by XREAL_0:def 1;
    s*<*s1*> = <*(multreal[;](s,id REAL)).s1*> by FINSEQ_2:39
   .= <*r*r1*> by Lm3;
   hence thesis;
 end;

theorem Th70:
  r1*(i|->r2) = i|->(r1*r2)
 proof
   reconsider s1=r1, s2=r2 as Element of REAL by XREAL_0:def 1;
    s1*(i|->s2) = i|->((multreal[;](s1,id REAL)).s2) by FINSEQOP:17
   .= i|->(r1*r2) by Lm3;
   hence thesis;
 end;

theorem Th71:
  (r1*r2)*R = r1*(r2*R)
 proof
   reconsider s1=r1, s2=r2 as Element of REAL by XREAL_0:def 1;
   thus (r1*r2)*R
      = multreal[;](multreal.(s1,s2),id REAL)*R by BINOP_2:def 11
     .= multreal[;](s1,multreal[;](s2,id REAL))*R by FUNCOP_1:77
     .= (multreal[;](s1,id REAL)*(multreal [;] (s2,id REAL)))*R
     by FUNCOP_1:69
     .= r1*(r2*R) by RELAT_1:55;
 end;

theorem
   (r1 + r2)*R = r1*R + r2*R
 proof
   reconsider s1=r1, s2=r2 as Element of REAL by XREAL_0:def 1;
   thus (r1 + r2)*R
     = multreal[;](addreal.(s1,s2),id REAL)*R by BINOP_2:def 9
    .= addreal.:(multreal[;](s1,id REAL),multreal[;](s2,id REAL))*R
        by Th16,FINSEQOP:36
    .= r1*R + r2*R by FUNCOP_1:31;
 end;

theorem
   r*(R1+R2) = r*R1 + r*R2
 proof
    r in REAL by XREAL_0:def 1;
  then r multreal is_distributive_wrt addreal by Th16,FINSEQOP:55;
  hence r*(R1+R2) = r*R1 + r*R2 by FINSEQOP:52;
 end;

theorem
   1*R = R
 proof
A1: rng R c= REAL;
   thus 1*R = (id REAL)*R by BINOP_2:7,FINSEQOP:45
           .= R by A1,RELAT_1:79;
 end;

theorem
   0*R = (i|->0)
 proof
A1:  rng R c= REAL;
  thus 0*R = multreal[;](0,(id REAL)*R) by FUNCOP_1:44
          .= multreal[;](0,R) by A1,RELAT_1:79
          .= i|->0 by Th16,Th22,BINOP_2:2,FINSEQOP:80;
 end;

theorem Th76:
  (-1)*R = -R
 proof
    compreal.1 = -1 by BINOP_2:def 7;
   hence (-1)*R = -R by Th16,Th22,Th23,BINOP_2:7,FINSEQOP:72;
 end;

definition let F;
  func sqr F -> FinSequence of REAL equals
   sqrreal*F;
  coherence by Lm4;
end;

theorem Th77:
  dom(sqr F) = dom F
  proof
     dom sqrreal = REAL by FUNCT_2:def 1;
    then rng F c= dom sqrreal;
    hence dom F = dom sqr F by RELAT_1:46;
  end;

theorem Th78:
  (sqr F).i = (F.i)^2
 proof per cases;
  suppose
A1:  not i in dom sqr F;
then A2:  not i in dom F by Th77;
   thus (sqr F).i = 0^2 by A1,FUNCT_1:def 4
      .= (F.i)^2 by A2,FUNCT_1:def 4;
  end;
  suppose
A3:  i in dom sqr F;
  set r = F.i;
   (sqr F).i = sqrreal.r by A3,FUNCT_1:22;
  hence thesis by Def2;
 end;
 end;

definition let i,R;
  redefine func sqr R -> Element of i-tuples_on REAL;
  coherence by FINSEQ_2:133;
end;

theorem
   (sqr R).j = (R.j)^2 by Th78;

canceled;

theorem
   sqr <*r*> = <*r^2*>
 proof
   reconsider s=r as Element of REAL by XREAL_0:def 1;
    sqr <*s*> = <*sqrreal.s*> by FINSEQ_2:39
   .= <*r^2*> by Def2;
   hence thesis;
 end;

theorem
   sqr(i |-> r) = i |-> r^2
 proof
  reconsider s=r as Element of REAL by XREAL_0:def 1;
   sqr(i |-> s) = i |-> (sqrreal.s) by FINSEQOP:17
  .= i |-> r^2 by Def2;
  hence thesis;
 end;

theorem Th83:
  sqr -R = sqr R
 proof
    now let j; assume j in Seg i;
   set r = R.j, r' = (-R).j;
   thus (sqr -R).j = (r')^2 by Th78
                  .= (-r)^2 by Th35
                  .= r^2
                  .= (sqr R).j by Th78;
  end;
  hence thesis by FINSEQ_2:139;
 end;

theorem Th84:
  sqr (r*R) = r^2* sqr R
 proof
A1: rng R c= REAL;
A2: rng(sqrreal*R) c= REAL;
   reconsider s=r as Element of REAL by XREAL_0:def 1;
  thus sqr (r*R) = sqrreal*(multreal[;](r,(id REAL)*R)) by FUNCOP_1:44
  .= sqrreal*(multreal[;](r,R)) by A1,RELAT_1:79
  .= multreal[;](sqrreal.s,sqrreal*R) by Th17,FINSEQOP:53
  .= multreal[;](sqrreal.r,(id REAL)*(sqrreal*R)) by A2,RELAT_1:79
  .= multreal[;](sqrreal.r,id REAL)*(sqrreal*R) by FUNCOP_1:44
  .= r^2* sqr R by Def2;
end;

notation let F1,F2;
  synonym mlt(F1,F2) for F1(#)F2;
end;

Lm9: now let F1,F2;
  thus mlt(F1,F2) is FinSequence-like
  proof
A1: dom mlt(F1,F2) = dom F1 /\ dom F2 by SEQ_1:def 5;
    consider n1 being Element of NAT such that
A2: dom F1 = Seg n1 by FINSEQ_1:def 2;
    consider n2 being Element of NAT such that
A3: dom F2 = Seg n2 by FINSEQ_1:def 2;
    per cases;
    suppose n1 <= n2;
      then A4:   Seg n1 c= Seg n2 by FINSEQ_1:7;
      take n1;
      thus dom mlt(F1,F2) = Seg n1 by A2,A3,A4,A1,XBOOLE_1:28;
    end;
    suppose n2 <= n1;
      then A5:   Seg n2 c= Seg n1 by FINSEQ_1:7;
      take n2;
      thus dom mlt(F1,F2) = Seg n2 by A2,A3,A5,A1,XBOOLE_1:28;
    end;
  end;
end;

definition let F1,F2;
  redefine func mlt(F1,F2) -> FinSequence of REAL equals
   multreal.:(F1,F2);
  coherence
  proof
     mlt(F1,F2) is FinSequence-like by Lm9;
    hence thesis by Lm4;
  end;
  compatibility
  proof
    let F be FinSequence of REAL;
A1: dom mlt(F1,F2) = dom F1 /\ dom F2 by SEQ_1:def 5;
    reconsider F3=F1,F4=F2 as FinSequence of REAL by Lm4;
 dom multreal = [:REAL,REAL:] by FUNCT_2:def 1;
    then [:rng F3, rng F4:] c= dom multreal by ZFMISC_1:119;
then A2: dom(multreal.:(F1,F2)) = dom F1 /\ dom F2 by FUNCOP_1:84;
    thus F = mlt(F1,F2) implies F = multreal.:(F1,F2)
    proof
      assume
A3:   F = mlt(F1,F2);
       for z being set st z in dom(multreal.:(F1,F2))
      holds F.z = multreal.(F1.z,F2.z)
      proof let z be set;
        assume z in dom(multreal.:(F1,F2));
        hence F.z = F1.z * F2.z by A1,A3,A2,SEQ_1:def 5
        .= multreal.(F1.z,F2.z) by BINOP_2:def 11;
     end;
     hence thesis by A1,A3,A2,FUNCOP_1:27;
   end;
   assume
A4: F = multreal.:(F1,F2);
    now let c be set;
     assume c in dom F;
     hence F.c = multreal.(F1.c,F2.c) by A4,FUNCOP_1:28
     .= F1.c * F2.c by BINOP_2:def 11;
   end;
   hence thesis by A2,A4,SEQ_1:def 5;
  end;
  commutativity
  proof let F be FinSequence of REAL; let F1,F2;
   assume
A5:  F = multreal.:(F1,F2);
A6:  dom multreal = [:REAL,REAL:] by FUNCT_2:def 1;
    reconsider F3=F1,F4=F2 as FinSequence of REAL by Lm4;
A7:  [:rng F4, rng F3:] c= dom multreal by A6,ZFMISC_1:119;
      [:rng F3, rng F4:] c= dom multreal by A6,ZFMISC_1:119;
    then A8:  dom(multreal.:(F1,F2)) = dom F1 /\ dom F2 by FUNCOP_1:84
          .= dom(multreal.:(F2,F1)) by A7,FUNCOP_1:84;
      for z being set st z in dom(multreal.:(F2,F1))
       holds F.z = multreal.(F2.z,F1.z)
     proof let z be set such that
A9:    z in dom(multreal.:(F2,F1));
       set F1z = F1.z, F2z =F2.z;
      thus F.z = multreal.(F1.z,F2.z) by A5,A8,A9,FUNCOP_1:28
           .= F1z * F2z by BINOP_2:def 11
           .= multreal.(F2.z,F1.z) by BINOP_2:def 11;
     end;
   hence F = multreal.:(F2,F1) by A5,A8,FUNCOP_1:27;
  end;
end;

canceled;

theorem Th86:
  i in dom mlt(F1,F2) implies mlt(F1,F2).i = F1.i * F2.i
 proof assume that
A1: i in dom mlt(F1,F2);
  set r1 = F1.i, r2 = F2.i;
   mlt(F1,F2).i = multreal.(r1,r2) by A1,FUNCOP_1:28;
  hence thesis by BINOP_2:def 11;
 end;

definition let i,R1,R2;
  redefine func mlt(R1,R2) -> Element of i-tuples_on REAL;
  coherence by FINSEQ_2:140;
end;

theorem Th87:
  mlt(R1,R2).j = R1.j * R2.j
 proof per cases;
  suppose
A1:  not j in Seg i;
then A2:  not j in dom R2 by FINSEQ_2:144;
      not j in dom mlt(R1,R2) by A1,FINSEQ_2:144;
   hence mlt(R1,R2).j = R1.j *0 by FUNCT_1:def 4
       .= R1.j * R2.j by A2,FUNCT_1:def 4;
  end;
  suppose j in Seg i;
  then j in dom mlt(R1,R2) by FINSEQ_2:144;
  hence thesis by Th86;
 end;
 end;

theorem
   mlt(<*>REAL,F) = <*>REAL
  proof
     F is FinSequence of REAL by Lm4;
    hence thesis by FINSEQ_2:87;
  end;

theorem
   mlt(<*r1*>,<*r2*>) = <*r1*r2*>
 proof
  reconsider s1=r1, s2=r2 as Element of REAL by XREAL_0:def 1;
   mlt(<*s1*>,<*s2*>) = <*multreal.(s1,s2)*> by FINSEQ_2:88
  .= <*r1*r2*> by BINOP_2:def 11;
  hence thesis;
 end;

canceled 2;

theorem Th92:
  mlt(i|->r,R) = r*R
 proof
  reconsider s=r as Element of REAL by XREAL_0:def 1;
   mlt(i|->s,R) = multreal[;](s,R) by FINSEQOP:21
  .= r*R by FINSEQOP:23;
  hence thesis;
 end;

theorem
   mlt(i|->r1,i|->r2) = i|->(r1*r2)
 proof
   reconsider s1=r1, s2=r2 as Element of REAL by XREAL_0:def 1;
    mlt(i|->s1,i|->s2) = s1*(i|->s2) by Th92
   .= i|->(r1*r2) by Th70;
   hence thesis;
 end;

theorem Th94:
  r*mlt(R1,R2) = mlt(r*R1,R2)
  proof
     r in REAL by XREAL_0:def 1;
    hence thesis by FINSEQOP:27;
  end;

canceled;

theorem
   r*R = mlt(i|->r,R) by Th92;

theorem Th97:
  sqr(R) = mlt(R,R)
 proof
A1:  len R = i by FINSEQ_2:109;
then A2:  len(sqrreal*R) = i by FINSEQ_2:37;
A3:  len(multreal.:(R,R)) = i by A1,FINSEQ_2:86;
  now let j;
    assume
A4:  j in Seg i;
then A5:  j in dom(sqrreal*R) by A2,FINSEQ_1:def 3;
A6:  j in dom(multreal.:(R,R)) by A3,A4,FINSEQ_1:def 3;
    set r = R.j;
    thus (sqrreal*R).j = sqrreal.r by A5,FUNCT_1:22
                      .= r^2 by Def2
                      .= multreal.(r,r) by BINOP_2:def 11
                      .= (multreal.:(R,R)).j by A6,FUNCOP_1:28;
   end;
  hence sqr R = mlt(R,R) by A2,A3,FINSEQ_2:10;
 end;

theorem Th98:
  sqr(R1 + R2) = sqr R1 + 2*mlt(R1,R2) + sqr R2
 proof
   now let j; assume j in Seg i;
   set r1r2 = (R1 + R2).j, r1 = R1.j, r2 = R2.j,
              r1'2 = (sqr R1).j, r2'2 = (sqr R2).j,
              r1r2a = mlt(R1,R2).j,2r1r2 = (2*mlt(R1,R2)).j,
              r1'22r1r2 = (sqr R1 + 2*mlt(R1,R2)).j;
   thus (sqr(R1 + R2)).j = r1r2^2 by Th78
                        .= (r1 + r2)^2 by Th27
                        .= r1^2+2*r1*r2+r2^2
                        .= r1'2+2*(r1*r2)+r2^2 by Th78
                        .= r1'2+2*(r1*r2)+r2'2 by Th78
                        .= r1'2+2*(r1r2a)+r2'2 by Th87
                        .= r1'2+2r1r2+r2'2 by Th66
                        .= r1'22r1r2+r2'2 by Th27
                        .= (sqr R1 + 2*mlt(R1,R2) + sqr R2).j by Th27;
  end;
  hence thesis by FINSEQ_2:139;
 end;

theorem Th99:
  sqr(R1 - R2) = sqr R1 - 2*mlt(R1,R2) + sqr R2
 proof
   thus sqr(R1 - R2) = sqr(R1 + -R2) by FINSEQOP:89
                    .= sqr R1 + 2*mlt(R1,-R2) + sqr -R2 by Th98
                    .= sqr R1 + 2*mlt(R1,-R2) + sqr R2 by Th83
                    .= sqr R1 + 2*mlt(R1,(-1)*R2) + sqr R2 by Th76
                    .= sqr R1 + 2*((-1)*mlt(R1,R2)) + sqr R2 by Th94
                    .= sqr R1 + ((-1)*2)*mlt(R1,R2) + sqr R2 by Th71
                    .= sqr R1 + (-1)*(2*mlt(R1,R2)) + sqr R2 by Th71
                    .= sqr R1 + -(2*mlt(R1,R2)) + sqr R2 by Th76
                    .= sqr R1 - 2*mlt(R1,R2) + sqr R2 by FINSEQOP:89;
 end;

theorem
   sqr mlt(R1,R2) = mlt(sqr R1,sqr R2) by Th17,FINSEQOP:52;

:: Finite sum of Finite Sequence of Real Numbers

definition let F be Relation;
  attr F is complex-yielding means :Def10:
    rng F c= COMPLEX;
end;

registration
  cluster -> complex-yielding FinSequence of COMPLEX;
  coherence
  proof
    let f be FinSequence of COMPLEX;
     rng f c= COMPLEX;
    hence thesis by Def10;
  end;
  cluster real-yielding -> complex-yielding FinSequence;
  coherence
  proof
    let f be FinSequence;
    assume f is real-yielding;
    then rng f c= REAL by SEQ_1:def 1;
    then rng f c= COMPLEX by XBOOLE_1:1,NUMBERS:11;
    hence thesis by Def10;
  end;
  cluster real-yielding complex-yielding FinSequence;
  existence
  proof
    consider f being FinSequence of REAL;
     f is real-yielding;
    hence thesis;
  end;
end;

definition let F be complex-yielding FinSequence;
  func Sum F -> complex number means :Def11:
    ex f being FinSequence of COMPLEX st f = F &
    it = addcomplex $$ f;
  existence
  proof
     rng F c= COMPLEX by Def10;
    then reconsider f = F as FinSequence of COMPLEX by FINSEQ_1:def 4;
    take addcomplex $$ f;
    thus thesis;
  end;
  uniqueness;
end;

registration let F be real-yielding FinSequence;
  cluster Sum F -> real;
  coherence
  proof
    consider f being FinSequence of COMPLEX such that
A1: f = F & Sum F = addcomplex $$ f by Def11;
A2: rng f c= REAL by A1,SEQ_1:def 1;
    then reconsider f' = f as FinSequence of REAL by FINSEQ_1:def 4;
    set mc = addcomplex, mr = addreal;
A3: mc $$ f = mc $$ (dom f,[#](f,the_unity_wrt mc)) by SETWOP_2:def 2;
    consider n being Element of NAT such that
A4: dom f = Seg n by FINSEQ_1:def 2;
    defpred P[Element of NAT] means
      mc $$ (Seg $1,[#](f,the_unity_wrt mc)) is real;
     Seg 0 = {}.NAT by FINSEQ_1:4;
then A5: P[0] by SETWISEO:40,BINOP_2:1;
    set g = [#](f,the_unity_wrt mc);
A6: for k being Element of NAT st P[k] holds P[k+1]
    proof
      let k be Element of NAT;
      assume
A7:   P[k];
   g.(k+1) is real
      proof
        per cases;
        suppose
A8:       k+1 in dom f;
          then g.(k+1) = f.(k+1) by SETWOP_2:22;
          then g.(k+1) in rng f by A8,FUNCT_1:12;
          hence thesis by A2,XREAL_0:def 1;
        end;
        suppose not k+1 in dom f;
          hence thesis by BINOP_2:1,SETWOP_2:22;
        end;
      end;
      then reconsider a = g.(k+1), b = mc $$(Seg k,g) as real number by A7;
       not (k + 1) in Seg k by FINSEQ_3:9;
      then mc $$ (Seg k \/ {.k+1.},g) = mc.(mc $$(Seg k,g),g.(k+1))
        by SETWOP_2:4
        .= b + a by BINOP_2:def 3;
      hence thesis by FINSEQ_1:11;
    end;
     for n being Element of NAT holds P[n] from NAT_1:sch 1(A5,A6);
    hence thesis by A1,A4,A3;
  end;
end;

theorem Th101:
  for F being FinSequence of REAL holds
    Sum F = addreal $$ F
  proof
    let F be FinSequence of REAL;
     rng F c= COMPLEX by XBOOLE_1:1,NUMBERS:11;
    then reconsider f = F as FinSequence of COMPLEX by FINSEQ_1:def 4;
    set g = addreal, h = addcomplex;
    consider n being Element of NAT such that
A1: dom f = Seg n by FINSEQ_1:def 2;
A2: g $$ F = g $$ (Seg n,[#](F,the_unity_wrt g)) by A1,SETWOP_2:def 2;
A3: h $$ f = h $$ (Seg n,[#](f,the_unity_wrt h)) by A1,SETWOP_2:def 2;
    defpred P[Element of NAT] means
      g $$ (Seg $1,[#](F,the_unity_wrt g)) =
        h $$ (Seg $1,[#](f,the_unity_wrt h));
A4: Seg 0 = {}.NAT by FINSEQ_1:4;
A5: P[0]
    proof
       g $$ (Seg 0,[#](F,the_unity_wrt g)) =
           the_unity_wrt h by BINOP_2:1,2,SETWISEO:40,A4
        .= h $$ (Seg 0,[#](f,the_unity_wrt h)) by A4,SETWISEO:40;
      hence thesis;
    end;
A6: for k being Element of NAT st P[k] holds P[k+1]
    proof
      let k be Element of NAT;
      assume
A7:   P[k];
      set i = [#](F,the_unity_wrt g);
      set j = [#](f,the_unity_wrt h);
A8:   i.(k+1) = j.(k+1)
      proof
        per cases;
        suppose
A9:       k+1 in dom f;
          then j.(k+1) = F.(k+1) by SETWOP_2:22
                 .= i.(k+1) by A9,SETWOP_2:22;
          hence thesis;
        end;
        suppose
A10:       not k+1 in dom f;
          then j.(k+1) = the_unity_wrt h by SETWOP_2:22
                 .= i.(k+1) by A10,SETWOP_2:22,BINOP_2:1,2;
          hence thesis;
        end;
      end;
      reconsider a = i.(k+1), b = g $$(Seg k,i) as real number;
A11:   not (k + 1) in Seg k by FINSEQ_3:9;
       g $$ (Seg (k+1),i) = g $$ (Seg k \/ {.k+1.},i) by FINSEQ_1:11
        .= g.(g $$(Seg k,i),i.(k+1)) by A11,SETWOP_2:4
        .= g $$(Seg k,i) + (i.(k+1)) by BINOP_2:def 9
        .= h.(h $$(Seg k,j),j.(k+1)) by BINOP_2:def 3,A7,A8
        .= h $$ (Seg k \/ {.k+1.},j) by A11,SETWOP_2:4
        .= h $$ (Seg (k+1),j) by FINSEQ_1:11;
      hence thesis;
    end;
     for k being Element of NAT holds P[k] from NAT_1:sch 1(A5,A6);
    then g $$ F = h $$ f by A2,A3;
    hence thesis by Def11;
  end;

definition let F be FinSequence of COMPLEX;
  redefine func Sum F -> Element of COMPLEX equals
   addcomplex $$ F;
  coherence by XCMPLX_0:def 2;
  compatibility by Def11;
end;

definition let F be FinSequence of REAL;
  redefine func Sum F -> Element of REAL equals
   addreal $$ F;
  coherence by XREAL_0:def 1;
  compatibility by Th101;
end;

theorem Th102:
  Sum(<*> REAL) = 0 by BINOP_2:2,FINSOP_1:11;

theorem
   Sum <*r*> = r
  proof
   reconsider r as Element of REAL by XREAL_0:def 1;
    Sum <*r*> = r by FINSOP_1:12;
   hence thesis;
 end;

theorem Th104:
  Sum(F^<*r*>) = Sum F + r
 proof
   reconsider F1=F as FinSequence of REAL by Lm4;
   reconsider s=r as Element of REAL by XREAL_0:def 1;
   thus Sum(F^<*r*>) = Sum(F1^<*s*>)
   .= addreal.(addreal $$ F1,s) by FINSOP_1:5
   .= Sum F1 + r by BINOP_2:def 9
   .= Sum F + r;
 end;

theorem Th105:
  Sum(F1^F2) = Sum F1 + Sum F2
 proof
   reconsider F3=F1,F4=F2 as FinSequence of REAL by Lm4;
   thus Sum(F1^F2) = Sum(F3^F4)
   .= addreal.(addreal $$ F3,addreal $$ F4) by FINSOP_1:6
   .= Sum F3 + Sum F4 by BINOP_2:def 9
   .= Sum F1 + Sum F2;
 end;

theorem
   Sum(<*r*>^F) = r + Sum F
 proof
   reconsider s=r as Element of REAL by XREAL_0:def 1;
   thus Sum(<*r*>^F) = Sum <*s*> + Sum F by Th105
  .= r + Sum F by FINSOP_1:12;
 end;

theorem Th107:
  Sum<*r1,r2*> = r1 + r2
 proof
   reconsider s1=r1 as Element of REAL by XREAL_0:def 1;
   thus Sum<*r1,r2*> = Sum<*s1*> + r2 by Th104
                  .= r1 + r2 by FINSOP_1:12;
 end;

theorem
   Sum<*r1,r2,r3*> = r1 + r2 + r3
 proof
   thus Sum<*r1,r2,r3*> = Sum<*r1,r2*> + r3 by Th104
                     .= r1 + r2 + r3 by Th107;
 end;

theorem
   for R being Element of 0-tuples_on REAL holds Sum R = 0
    by Th102,FINSEQ_2:113;

theorem Th110:
  Sum(i |-> r) = i*r
 proof
   reconsider r as Element of REAL by XREAL_0:def 1;
   defpred P[Element of NAT] means Sum($1 |->r) = $1*r;
A1: P[0] by Th102,FINSEQ_2:113;
A2:  for i st P[i] holds P[(i+1)]
   proof let i such that
A3:   Sum(i |-> r) = i*r;
    thus Sum((i+1) |-> r) = Sum((i |-> r)^<*r*>) by FINSEQ_2:74
                       .= i*r + 1*r by A3,Th104
                       .= (i+1)*r;
   end;
    for i holds P[i] from NAT_1:sch 1(A1,A2);
  hence thesis;
 end;

theorem Th111:
  Sum(i |-> (0 qua Real)) = 0
 proof
   thus Sum(i |-> (0 qua Real)) = i*0 by Th110 .= 0;
 end;

theorem Th112:
  (for j st j in Seg i holds R1.j <= R2.j) implies Sum R1 <= Sum R2
 proof
   defpred P[Element of NAT] means
     for R1,R2 being Element of $1-tuples_on REAL
      st for j st j in Seg $1 holds R1.j <= R2.j holds
       Sum R1 <= Sum R2;
A1: P[0]
    proof let R1,R2 be Element of 0-tuples_on REAL;
      Sum R1 = 0 & Sum R2 = 0 by Th102,FINSEQ_2:113;
     hence thesis;
    end;
A2: for i st P[i] holds P[i+1]
   proof let i such that
A3:   for R1,R2 being Element of i-tuples_on REAL
        st for j st j in Seg i holds R1.j <= R2.j
       holds Sum R1 <= Sum R2;
    let R1,R2 be Element of (i+1)-tuples_on REAL such that
A4:   for j st j in Seg (i+1) holds R1.j <= R2.j;
    consider R1' being (Element of i-tuples_on REAL), x1 such that
A5:   R1 = R1'^<*x1*> by FINSEQ_2:137;
    consider R2' being (Element of i-tuples_on REAL), x2 such that
A6:   R2 = R2'^<*x2*> by FINSEQ_2:137;
    set n = i+1;
        n in Seg n & R1.n = x1 & R2.n = x2 by A5,A6,FINSEQ_1:6,FINSEQ_2:136;
then A7:   x1 <= x2 by A4;
        for j st j in Seg i holds R1'.j <= R2'.j
     proof let j such that
A8:   j in Seg i;
       Seg len R1' = dom R1' & Seg len R2' = dom R2' &
      len R1' = i & len R2' = i by FINSEQ_1:def 3,FINSEQ_2:109;
      then j in Seg n & R1'.j = R1.j & R2'.j = R2.j
        by A5,A6,A8,FINSEQ_1:def 7,FINSEQ_2:9;
      hence thesis by A4;
     end;
    then Sum R1' <= Sum R2' & Sum R1 = Sum R1' + x1 & Sum R2 = Sum R2' + x2
      by A3,A5,A6,Th104;
    hence Sum R1 <= Sum R2 by A7,XREAL_1:9;
   end;
     for i holds P[i] from NAT_1:sch 1(A1,A2);
  hence thesis;
 end;

theorem Th113:
  (for j st j in Seg i holds R1.j <= R2.j) &
    (ex j st j in Seg i & R1.j < R2.j)
      implies Sum R1 < Sum R2
 proof
   defpred P[Element of NAT] means
     for R1,R2 be Element of $1-tuples_on REAL st
      (for j st j in Seg $1 holds R1.j <= R2.j) &
       (ex j st j in Seg $1 & R1.j < R2.j)
       holds Sum R1 < Sum R2;
A1: P[0] by FINSEQ_1:4;
A2: for i st P[i] holds P[i+1]
   proof
   now let i such that
A3: P[i];
     let R1,R2 be Element of (i+1)-tuples_on REAL such that
A4:    for j st j in Seg (i+1) holds R1.j <= R2.j;
     given j such that
A5:    j in Seg (i+1) & R1.j < R2.j;
     consider R1' being (Element of i-tuples_on REAL), x1 such that
A6:    R1 = R1'^<*x1*> by FINSEQ_2:137;
     consider R2' being (Element of i-tuples_on REAL), x2 such that
A7:    R2 = R2'^<*x2*> by FINSEQ_2:137;
A8:    (i+1) in Seg (i+1) & R1.(i+1) = x1 & R2.(i+1) = x2
        by A6,A7,FINSEQ_1:6,FINSEQ_2:136;
A9:    for j st j in Seg i holds R1'.j <= R2'.j
      proof let j such that
A10:   j in Seg i;
        Seg len R1' = dom R1' & Seg len R2' = dom R2' &
       len R1' = i & len R2' = i by FINSEQ_1:def 3,FINSEQ_2:109;
       then j in Seg (i+1) & R1'.j = R1.j & R2'.j = R2.j
         by A6,A7,A10,FINSEQ_1:def 7,FINSEQ_2:9;
       hence thesis by A4;
      end;
       now per cases by A5,FINSEQ_2:8;
      suppose
A11:   j in Seg i;
        Seg len R1' = dom R1' & Seg len R2' = dom R2' &
       len R1' = i & len R2' = i by FINSEQ_1:def 3,FINSEQ_2:109;
       then R1'.j = R1.j & R2'.j = R2.j by A6,A7,A11,FINSEQ_1:def 7;
       then Sum R1' < Sum R2' & Sum R1 = Sum R1' + x1 &
       Sum R2 = Sum R2' + x2 & x1 <= x2
         by A3,A4,A5,A6,A7,A8,A9,A11,Th104;
       hence Sum R1 < Sum R2 by XREAL_1:10;
      end;
      suppose
A12:   j = i+1;
        Sum R1' <= Sum R2' & Sum R1 = Sum R1' + x1 & Sum R2 = Sum R2' + x2
         by A6,A7,A9,Th104,Th112;
       hence Sum R1 < Sum R2 by A5,A8,A12,XREAL_1:10;
      end;
      end;
     hence Sum R1 < Sum R2;
   end;
   hence thesis;
  end;
    for i holds P[i] from NAT_1:sch 1(A1,A2);
   hence thesis;
 end;

theorem Th114:
  (for i st i in dom F holds 0 <= F.i) implies 0 <= Sum F
 proof assume
A1:  for i st i in dom F holds 0 <= F.i;
  set i = len F;
  set R1 = i|->(0 qua Real);
  reconsider F1 = F as FinSequence of REAL by Lm4;
  reconsider R2 = F1 as Element of i-tuples_on REAL by FINSEQ_2:110;
A2: Seg len F = dom F by FINSEQ_1:def 3;
    now let j; assume
A3: j in Seg i;
   then R1.j = 0 by FUNCOP_1:13;
   hence R1.j <= R2.j by A1,A2,A3;
  end;
  then Sum R1 <= Sum R2 by Th112;
  hence thesis by Th111;
 end;

theorem Th115:
   (for i st i in dom F holds 0 <= F.i) &
    (ex i st i in dom F & 0 < F.i)
     implies 0 < Sum F
 proof assume
A1:   for i st i in dom F holds 0 <= F.i;
  set i = len F, R1 = i|->(0 qua Real);
  reconsider F1 = F as FinSequence of REAL by Lm4;
  reconsider R2 = F1 as Element of i-tuples_on REAL by FINSEQ_2:110;
A2: Seg len F = dom F by FINSEQ_1:def 3;
A3: now let j; assume
A4:  j in Seg i;
   then R1.j = 0 by FUNCOP_1:13;
   hence R1.j <= R2.j by A1,A2,A4;
  end;
  given j such that
A5:  j in dom F and
A6:  0 < F.j;
      R1.j = 0 by A2,A5,FUNCOP_1:13;
  then Sum R1 < Sum R2 by A2,A3,A5,A6,Th113;
  hence thesis by Th111;
 end;

theorem Th116:
  0 <= Sum sqr F
 proof
   now let i such that
    i in dom sqr F;
   set r = (sqr F).i;
   set x = F.i;
    0 <= x^2 by XREAL_1:65;
   hence 0 <= r by Th78;
  end;
  hence thesis by Th114;
end;

theorem Th117:
  Sum(r*F) = r*(Sum F)
 proof
   reconsider s=r as Element of REAL by XREAL_0:def 1;
   set rM = multreal[;](s,id REAL);
    reconsider F1=F as FinSequence of REAL by Lm4;
  thus Sum (r*F) = rM.(addreal $$ F1) by Th16,Th22,SETWOP_2:41
    .= r*(Sum F1) by Lm3
    .= r*(Sum F);
 end;

theorem
   Sum -F = -(Sum F)
 proof
   reconsider F1=F as FinSequence of REAL by Lm4;
   thus Sum -F = compreal.(addreal $$ F1) by Th22,Th23,SETWOP_2:42
   .= -(Sum F1) by BINOP_2:def 7
   .= -(Sum F);
 end;

theorem Th119:
  Sum(R1 + R2) = Sum R1 + Sum R2
 proof
  thus Sum(R1 + R2) = addreal.(addreal$$R1,addreal$$R2) by SETWOP_2:46
    .= Sum R1 + Sum R2 by BINOP_2:def 9;
 end;

theorem Th120:
  Sum(R1 - R2) = Sum R1 - Sum R2
 proof
  thus Sum(R1 - R2)
     = diffreal.(addreal$$R1,addreal$$R2) by Th22,Th23,SETWOP_2:48
    .= Sum R1 - Sum R2 by BINOP_2:def 10;
 end;

theorem
   Sum sqr R = 0 implies R = i |-> 0
 proof assume
A1:  Sum sqr R = 0;
A2:  len R = i by FINSEQ_2:109;
A3:  len(i |-> 0) = i by FINSEQ_2:109;
then A4:  Seg i = dom R & Seg i = dom(i |-> 0) by A2,FINSEQ_1:def 3;
  assume R <> i |-> 0;
  then consider j such that
A5:   j in dom R and
A6:   R.j <> (i |-> 0).j by A2,A3,A4,FINSEQ_2:10;
A7:   dom sqr R = Seg len sqr R & dom R = Seg len R by FINSEQ_1:def 3;
A8: now let k such that
      k in dom sqr R;
     set r = (sqr R).k;
     set x = R.k;
      0 <= x^2 by XREAL_1:65; hence 0 <= r by Th78;
    end;
A9:  j in dom sqr R by A5,A7,FINSEQ_2:37;
  set x = R.j,x' = (sqr R).j;
   x <> 0 by A2,A5,A6,A7,FUNCOP_1:13;
  then 0 < x^2 by SQUARE_1:74;
  then 0 < x' by Th78;
  hence thesis by A1,A8,A9,Th115;
 end;

theorem
   (Sum mlt(R1,R2))^2 <= (Sum sqr R1)*(Sum sqr R2)
 proof
  defpred P[Element of NAT] means
    for R1,R2 being Element of $1-tuples_on REAL
      holds (Sum mlt(R1,R2))^2 <= (Sum sqr R1)*(Sum sqr R2);
A1: P[0]
    proof let R1,R2 be Element of 0-tuples_on REAL;
       sqr R1 = <*>REAL & mlt(R1,R2) = <*>REAL by FINSEQ_2:113;
      hence thesis by Th102;
    end;
A2: for i st P[i] holds P[i+1]
    proof let i such that
A3:   for R1,R2 being Element of i-tuples_on REAL
        holds (Sum mlt(R1,R2))^2 <= (Sum sqr R1)*(Sum sqr R2);
    let R1,R2 be Element of (i+1)-tuples_on REAL;
    consider R1' being (Element of i-tuples_on REAL), x1 such that
A4: R1 = R1'^<*x1*> by FINSEQ_2:137;
    consider R2' being (Element of i-tuples_on REAL), x2 such that
A5: R2 = R2'^<*x2*> by FINSEQ_2:137;
     (Sum mlt(R1',R2'))^2 + 0 <= (Sum sqr R1')*(Sum sqr R2') by A3;
then A6: 0 <= (Sum sqr R1')*(Sum sqr R2') - (Sum mlt(R1',R2'))^2 by XREAL_1:21;
A7:   (Sum sqr R1' + x1^2)*(Sum sqr R2' + x2^2)
        = (Sum sqr R1')*(Sum sqr R2')+(x1^2*(Sum sqr R2')+(Sum
 sqr R1')*x2^2)+x1^2*x2^2
       .= (Sum sqr R1')*(Sum sqr R2')+(Sum(x1^2*sqr R2')+(Sum
 sqr R1')*x2^2)+x1^2*x2^2 by Th117
       .= (Sum sqr R1')*(Sum sqr R2')+(Sum sqr(x1*R2')+x2^2*(Sum
 sqr R1'))+x1^2*x2^2 by Th84
       .= (Sum sqr R1')*(Sum sqr R2')+(Sum sqr(x1*R2')+Sum
(x2^2*sqr R1'))+x1^2*x2^2 by Th117
       .= (Sum sqr R1')*(Sum sqr R2')+(Sum sqr(x1*R2')+Sum
 sqr(x2*R1'))+x1^2*x2^2 by Th84;
        mlt(R1'^<*x1*>,R2'^<*x2*>)
      = (multreal.:(R1',R2'))^<*multreal.(x1,x2)*> by FINSEQOP:11
     .= (mlt(R1',R2'))^<*x1*x2*> by BINOP_2:def 11;
then A8:  Sum mlt(R1'^<*x1*>,R2'^<*x2*>) = Sum mlt(R1',R2') + x1*x2 by Th104;
A9:   2*(x1*x2)*Sum mlt(R1',R2')
        = 2*((x1*x2)*Sum mlt(R1',R2'))
       .= 2*Sum((x1*x2)*mlt(R1',R2')) by Th117
       .= 2*Sum(x1*(x2*mlt(R1',R2'))) by Th71
       .= 2*Sum(x1*mlt(R2',x2*R1')) by Th94
       .= 2*Sum(mlt(x1*R2',x2*R1')) by FINSEQOP:27;
A10:  -(Sum mlt(R1',R2')+x1*x2)^2
        = -(x1*x2)^2+-(2*(x1*x2)*Sum mlt(R1',R2')+(Sum mlt(R1',R2'))^2)
       .= -x1^2*x2^2+(-(Sum mlt(R1',R2'))^2+ -2*Sum(mlt(x1*R2',x2*R1')))
              by A9;
A11:  Sum sqr(x1*R2')+Sum sqr(x2*R1') + -2*Sum(mlt(x1*R2',x2*R1'))
       = Sum sqr(x1*R2')-2*Sum(mlt(x1*R2',x2*R1'))+Sum sqr(x2*R1')
      .= Sum sqr(x1*R2')-Sum(2*mlt(x1*R2',x2*R1'))+Sum sqr(x2*R1') by Th117
      .= Sum(sqr(x1*R2')-2*mlt(x1*R2',x2*R1'))+Sum sqr(x2*R1') by Th120
      .= Sum(sqr (x1*R2')-2*mlt(x1*R2',x2*R1')+sqr(x2*R1')) by Th119;
    Sum sqr (R^<*r*>) = Sum sqr R + r^2
 proof
   reconsider s=r as Element of REAL by XREAL_0:def 1;
    sqr(R^<*s*>) = (sqrreal*R)^<*sqrreal.s*> by FINSEQOP:9
               .= (sqr R)^<*r^2*> by Def2;
   hence Sum sqr (R^<*r*>) = Sum sqr R + r^2 by Th104;
 end;
    then (Sum sqr R1' + x1^2) = Sum sqr R1 &
    (Sum sqr R2' + x2^2) = Sum sqr R2 by A4,A5;
    then A12:  (Sum sqr R1)*(Sum sqr R2) - (Sum mlt(R1,R2))^2
       = (Sum sqr R1' + x1^2)*(Sum sqr R2' + x2^2)
           + -(Sum mlt(R1',R2')+x1*x2)^2 by A4,A5,A8
      .= (Sum sqr R1')*(Sum sqr R2')+-(Sum mlt(R1',R2'))^2 +
         ((Sum sqr(x1*R2')+Sum sqr(x2*R1')) +- 2*Sum(mlt(x1*R2',x2*R1'))) by A7
,A10
      .= (Sum sqr R1')*(Sum sqr R2')-(Sum mlt(R1',R2'))^2 + Sum
 sqr (x1*R2'-x2*R1')
          by A11,Th99;
        0 <= Sum sqr (x1*R2'-x2*R1') by Th116;
    then 0 + 0 <= (Sum sqr R1)*(Sum sqr R2) - (Sum
 mlt(R1,R2))^2 by A6,A12,XREAL_1:9;
    then (Sum mlt(R1,R2))^2 + 0 <= (Sum sqr R1)*(Sum sqr R2) by XREAL_1:21;
    hence (Sum mlt(R1,R2))^2 <= (Sum sqr R1)*(Sum sqr R2);
   end;
    for i holds P[i] from NAT_1:sch 1(A1,A2);
   hence thesis;
 end;

:: The Product of Finite Sequences of Real Numbers

definition let F be complex-yielding FinSequence;
  func Product F -> complex number means :Def14:
    ex f being FinSequence of COMPLEX st f = F &
    it = multcomplex $$ f;
  existence
  proof
     rng F c= COMPLEX by Def10;
    then reconsider f = F as FinSequence of COMPLEX by FINSEQ_1:def 4;
    take multcomplex $$ f;
    thus thesis;
  end;
  uniqueness;
end;

registration let F be real-yielding FinSequence;
  cluster Product F -> real;
  coherence
  proof
    consider f being FinSequence of COMPLEX such that
A1: f = F & Product F = multcomplex $$ f by Def14;
A2: rng f c= REAL by A1,SEQ_1:def 1;
    then reconsider f' = f as FinSequence of REAL by FINSEQ_1:def 4;
    set mc = multcomplex, mr = multreal;
A3: mc $$ f = mc $$ (dom f,[#](f,the_unity_wrt mc)) by SETWOP_2:def 2;
    consider n being Element of NAT such that
A4: dom f = Seg n by FINSEQ_1:def 2;
    defpred P[Element of NAT] means
      mc $$ (Seg $1,[#](f,the_unity_wrt mc)) is real;
     Seg 0 = {}.NAT by FINSEQ_1:4;
then A5: P[0] by SETWISEO:40,BINOP_2:6;
    set g = [#](f,the_unity_wrt mc);
A6: for k being Element of NAT st P[k] holds P[k+1]
    proof
      let k be Element of NAT;
      assume
A7:   P[k];
   g.(k+1) is real
      proof
        per cases;
        suppose
A8:       k+1 in dom f;
          then g.(k+1) = f.(k+1) by SETWOP_2:22;
          then g.(k+1) in rng f by A8,FUNCT_1:12;
          hence thesis by A2,XREAL_0:def 1;
        end;
        suppose not k+1 in dom f;
          hence thesis by BINOP_2:6,SETWOP_2:22;
        end;
      end;
      then reconsider a = g.(k+1), b = mc $$(Seg k,g) as real number by A7;
       not (k + 1) in Seg k by FINSEQ_3:9;
      then mc $$ (Seg k \/ {.k+1.},g) = mc.(mc $$(Seg k,g),g.(k+1))
        by SETWOP_2:4
        .= b * a by BINOP_2:def 5;
      hence thesis by FINSEQ_1:11;
    end;
     for n being Element of NAT holds P[n] from NAT_1:sch 1(A5,A6);
    hence thesis by A1,A4,A3;
  end;
end;

theorem Th123:
  for F being FinSequence of REAL holds
    Product F = multreal $$ F
  proof
    let F be FinSequence of REAL;
     rng F c= COMPLEX by XBOOLE_1:1,NUMBERS:11;
    then reconsider f = F as FinSequence of COMPLEX by FINSEQ_1:def 4;
    set g = multreal, h = multcomplex;
    consider n being Element of NAT such that
A1: dom f = Seg n by FINSEQ_1:def 2;
A2: g $$ F = g $$ (Seg n,[#](F,the_unity_wrt g)) by A1,SETWOP_2:def 2;
A3: h $$ f = h $$ (Seg n,[#](f,the_unity_wrt h)) by A1,SETWOP_2:def 2;
    defpred P[Element of NAT] means
      g $$ (Seg $1,[#](F,the_unity_wrt g)) =
        h $$ (Seg $1,[#](f,the_unity_wrt h));
A4: Seg 0 = {}.NAT by FINSEQ_1:4;
A5: P[0]
    proof
       g $$ (Seg 0,[#](F,the_unity_wrt g)) =
           the_unity_wrt h by BINOP_2:6,7,SETWISEO:40,A4
        .= h $$ (Seg 0,[#](f,the_unity_wrt h)) by A4,SETWISEO:40;
      hence thesis;
    end;
A6: for k being Element of NAT st P[k] holds P[k+1]
    proof
      let k be Element of NAT;
      assume
A7:   P[k];
      set i = [#](F,the_unity_wrt g);
      set j = [#](f,the_unity_wrt h);
A8:   i.(k+1) = j.(k+1)
      proof
        per cases;
        suppose
A9:       k+1 in dom f;
          then j.(k+1) = F.(k+1) by SETWOP_2:22
                 .= i.(k+1) by A9,SETWOP_2:22;
          hence thesis;
        end;
        suppose
A10:       not k+1 in dom f;
          then j.(k+1) = the_unity_wrt h by SETWOP_2:22
                 .= i.(k+1) by A10,SETWOP_2:22,BINOP_2:7,6;
          hence thesis;
        end;
      end;
      reconsider a = i.(k+1), b = g $$(Seg k,i) as real number;
A11:   not (k + 1) in Seg k by FINSEQ_3:9;
       g $$ (Seg (k+1),i) = g $$ (Seg k \/ {.k+1.},i) by FINSEQ_1:11
        .= g.(g $$(Seg k,i),i.(k+1)) by A11,SETWOP_2:4
        .= g $$(Seg k,i) * (i.(k+1)) by BINOP_2:def 11
        .= h.(h $$(Seg k,j),j.(k+1)) by BINOP_2:def 5,A7,A8
        .= h $$ (Seg k \/ {.k+1.},j) by A11,SETWOP_2:4
        .= h $$ (Seg (k+1),j) by FINSEQ_1:11;
      hence thesis;
    end;
     for k being Element of NAT holds P[k] from NAT_1:sch 1(A5,A6);
    then g $$ F = h $$ f by A2,A3;
    hence thesis by Def14;
  end;

definition let F be FinSequence of COMPLEX;
  redefine func Product F -> Element of COMPLEX equals
   multcomplex $$ F;
  coherence by XCMPLX_0:def 2;
  compatibility by Def14;
end;

definition let F be FinSequence of REAL;
  redefine func Product F -> Element of REAL equals
   multreal $$ F;
  coherence by XREAL_0:def 1;
  compatibility by Th123;
end;

registration
  cluster empty -> complex-yielding FinSequence;
  coherence
  proof
    let F be FinSequence;
    assume F is empty;
    then rng F c= COMPLEX by XBOOLE_1:2,RELAT_1:60;
    hence thesis by Def10;
  end;
end;

Lm10:
  for F being empty FinSequence holds
    Product F = 1
  proof
     Product <*>REAL = 1 by BINOP_2:7,FINSOP_1:11;
    hence thesis;
  end;

theorem Th124:
  Product <*>REAL = 1 by Lm10;

registration let r be complex number;
  cluster <*r*> -> complex-yielding;
  coherence
  proof
    reconsider p = r as Element of COMPLEX by XCMPLX_0:def 2;
    reconsider f = <*p*> as FinSequence of COMPLEX;
     f is FinSequence-like;
    hence thesis;
  end;
end;

registration let r1, r2 be complex number;
  cluster <*r1, r2*> -> complex-yielding;
  coherence
  proof
    reconsider p1 = r1, p2 = r2 as Element of COMPLEX by XCMPLX_0:def 2;
    reconsider f = <*p1,p2*> as FinSequence of COMPLEX;
     f is FinSequence-like;
    hence thesis;
  end;
end;

registration let r1, r2, r3 be complex number;
  cluster <*r1, r2, r3*> -> complex-yielding;
  coherence
  proof
    reconsider p1 = r1, p2 = r2, p3 = r3 as Element of COMPLEX
      by XCMPLX_0:def 2;
    reconsider f = <*p1,p2,p3*> as FinSequence of COMPLEX;
     f is FinSequence-like;
    hence thesis;
  end;
end;

theorem Th125:
  for r being complex number holds
    Product <*r*> = r
  proof
    let r be complex number;
    reconsider r' = r as Element of COMPLEX by XCMPLX_0:def 2;
    reconsider F = <*r'*> as FinSequence of COMPLEX;
     multcomplex $$ F = r by FINSOP_1:12;
    hence thesis by Def14;
  end;

registration let f, g be complex-yielding FinSequence;
  cluster f ^ g -> complex-yielding;
  coherence
  proof
A1: rng f c= COMPLEX & rng g c= COMPLEX by Def10;
     rng (f ^ g) = rng f \/ rng g by FINSEQ_1:44;
    then rng (f ^ g) c= COMPLEX by A1,XBOOLE_1:8;
    hence thesis by Def10;
  end;
end;

theorem Th126:
  for F being complex-yielding FinSequence, r being complex number holds
    Product (F^<*r*>) = Product F * r
  proof
    let F be complex-yielding FinSequence, r be complex number;
    reconsider p = r as Element of COMPLEX by XCMPLX_0:def 2;
A1: rng F c= COMPLEX by Def10;
     rng (F^<*p*>) c= COMPLEX by Def10;
    then reconsider Fr = F^<*r*>, Ff = F as FinSequence of COMPLEX
      by A1,FINSEQ_1:def 4;
    thus Product (F^<*r*>) = multcomplex $$ Fr by Def14
                 .= multcomplex.(Product Ff,p) by FINSOP_1:5
                 .= Product F * r by BINOP_2:def 5;
  end;

theorem Th127:
  for F1, F2 being complex-yielding FinSequence holds
    Product (F1^F2) = Product F1 * Product F2
 proof
  let F1, F2 be complex-yielding FinSequence;
A1: rng F1 c= COMPLEX & rng F2 c= COMPLEX by Def10;
     rng (F1^F2) c= COMPLEX by Def10;
    then reconsider FF = F1^F2, f1 = F1, f2 = F2 as FinSequence of COMPLEX
      by A1,FINSEQ_1:def 4;
  thus Product (F1^F2) = multcomplex $$ FF by Def14
    .= multcomplex.(Product f1,Product f2) by FINSOP_1:6
    .= Product F1 * Product F2 by Lm2;
 end;

theorem
   Product (<*r*>^F) = r * Product F
  proof thus Product (<*r*>^F) = Product <*r*> * Product F by Th127
    .= r * Product F by Th125;
  end;

theorem Th129:
  for r1, r2 being complex number holds
    Product <*r1,r2*> = r1 * r2
 proof
   let r1, r2 be complex number;
   thus Product <*r1,r2*> = Product <*r1*> * r2 by Th126
                  .= r1 * r2 by Th125;
 end;

theorem
   for r1, r2, r3 being complex number holds
    Product <*r1,r2,r3*> = r1 * r2 * r3
 proof
   let r1, r2, r3 be complex number;
   thus Product <*r1,r2,r3*> = Product <*r1,r2*> * r3 by Th126
                     .= r1 * r2 * r3 by Th129;
 end;

theorem
   for R being Element of 0-tuples_on REAL holds Product R = 1
    by Th124,FINSEQ_2:113;

theorem
   Product (i|->1) = 1
  proof
     Product (i |-> the_unity_wrt multreal) = the_unity_wrt multreal
    by SETWOP_2:35;
    hence thesis by BINOP_2:7;
  end;

Lm11:
  for p being complex-yielding FinSequence
    st len p <> 0 ex q being complex-yielding FinSequence,
       d being complex number st p = q^<*d*>
 proof let p be complex-yielding FinSequence;
  assume
A1:  len p <> 0;
  then p <> {} by FINSEQ_1:25;
  then consider q being FinSequence,d being set such that
A2:  p = q^<*d*> by FINSEQ_1:63;
      rng p c= COMPLEX by Def10;
then A3:  p is FinSequence of COMPLEX by FINSEQ_1:def 4;
A4:  for i st i in dom q holds q.i in COMPLEX
   proof let i; assume i in dom q;
    then p.i = q.i & i in dom p by A2,FINSEQ_2:18,FINSEQ_1:def 7;
    hence thesis by A3,FINSEQ_2:13;
   end;
A5:  len p in Seg len p by A1,FINSEQ_1:5;
        len p = len q + 1 by A2,FINSEQ_2:19;
  then len p in dom p & p.(len p) = d by A2,A5,FINSEQ_1:59,def 3;
  then d is Element of COMPLEX &
       q is FinSequence of COMPLEX by A3,A4,FINSEQ_2:13,FINSEQ_2:14;
  hence thesis by A2;
 end;

theorem
   for F being complex-yielding FinSequence holds
    (ex k st k in dom F & F.k = 0) iff Product F = 0
 proof
   let F be complex-yielding FinSequence;
   defpred P[Element of NAT] means
    for F being complex-yielding FinSequence st
      len F = $1 holds (ex k st k in
        Seg $1 & F.k = 0) iff Product F = 0;
A1:  P[0] by Th124,FINSEQ_1:4,32;
A2: for i st P[i] holds P[i+1]
     proof let i such that
A3:   for F being complex-yielding FinSequence st
        len F = i holds (ex k st k in Seg i & F.k = 0) iff
          Product F = 0;
      let F be complex-yielding FinSequence; assume
A4:   len F = i+1;
      then consider F' being complex-yielding FinSequence,
           x being complex number such that
A5:   F = F'^<*x*> by Lm11;
A6:   len F = len F' + 1 by A5,FINSEQ_2:19;
A7:   Product F = Product F' * x by A5,Th126;
      thus (ex k st k in Seg (i+1) & F.k = 0) implies Product F = 0
      proof given k such that
A8:     k in Seg (i+1) and
A9:    F.k = 0;
         now per cases by A8,FINSEQ_2:8;
         suppose
A10:      k in Seg i;
           Seg len F' = dom F' by FINSEQ_1:def 3;
          then F'.k = F.k by A5,A4,A6,A10,FINSEQ_1:def 7;
          then Product F' = 0 by A3,A4,A6,A9,A10;
          hence thesis by A7;
         end;
         suppose k = i+1;
          then x = 0 by A4,A5,A6,A9,FINSEQ_1:59;
          hence thesis by A7;
        end;
        end;
        hence thesis;
       end;
      assume
A11:  Product F = 0;
      per cases by A7,A11,XCMPLX_1:6;
       suppose Product F' = 0;
        then consider k such that
A12:    k in Seg i & F'.k = 0 by A3,A4,A6;
         Seg len F' = dom F' by FINSEQ_1:def 3;
        then k in Seg (i+1) & F.k = 0
        by A5,A4,A6,A12,FINSEQ_1:def 7,FINSEQ_2:9;
        hence thesis;
       end;
       suppose x = 0;
        then i+1 in Seg(i+1) & F.(i+1) = 0 by A4,A5,A6,FINSEQ_1:6,59;
        hence thesis;
     end;
     end;
A13: Seg len F = dom F by FINSEQ_1:def 3;
      for i holds P[i] from NAT_1:sch 1(A1,A2);
     hence thesis by A13;
  end;

theorem
   Product ((i+j)|->r) = (Product (i|->r))*(Product (j|->r))
 proof
   reconsider s=r as Element of REAL by XREAL_0:def 1;
    Product ((i+j)|->s) = multreal.(multreal$$(i|->s),multreal$$(j|->s))
   by SETWOP_2:37
   .= (Product (i|->s))*(Product (j|->s)) by BINOP_2:def 11;
   hence thesis;
 end;

theorem
   Product ((i*j)|->r) = Product (j |-> (Product (i|->r)))
  proof
   reconsider r as Element of REAL by XREAL_0:def 1;
    Product ((i*j)|->r) = Product (j |-> (Product (i|->r)))
   by SETWOP_2:38;
   hence thesis;
 end;

theorem
   Product (i|->(r1*r2)) = (Product (i|->r1))*(Product (i|->r2))
 proof
   reconsider s1=r1, s2=r2 as Element of REAL by XREAL_0:def 1;
    Product (i|->(s1*s2)) =
   multreal$$(i|->multreal.(s1,s2)) by BINOP_2:def 11
   .= multreal.(multreal$$(i|->s1),multreal$$(i|->s2)) by SETWOP_2:47
   .= (Product (i|->s1))*(Product (i|->s2)) by BINOP_2:def 11;
   hence thesis;
 end;

theorem Th137:
  Product mlt(R1,R2) = Product R1 * Product R2
 proof
   thus Product (mlt(R1,R2)) =
   multreal.(multreal$$R1,multreal$$R2) by SETWOP_2:46
    .= Product R1 * Product R2 by Lm1;
 end;

theorem
   Product (r*R) = Product (i|->r) * Product R
 proof
   reconsider s=r as Element of REAL by XREAL_0:def 1;
   thus Product (r*R) = Product mlt(i|->s,R) by Th92
    .= Product (i|->r) * Product R by Th137;
 end;

theorem
   Product sqr R = (Product R)^2
 proof
   thus Product sqr R = Product (mlt(R,R)) by Th97
               .= (Product R)^2 by Th137;
 end;

begin :: Addenda

:: 2006.07.13, A.T.

reserve z,z1,z2 for Element of COMPLEX;

definition let i be Nat, z be Element of COMPLEX;
 redefine func i |-> z -> FinSequence of COMPLEX;
 coherence
  proof
   let x be set;
   assume x in rng (i |-> z);
   then consider y be set such that A1: y in dom (i |-> z) and
    A2: x = (i |-> z).y by FUNCT_1:def 5;
   A3: y in Seg i by A1,FUNCOP_1:19;
   then reconsider y as Element of NAT;
   reconsider r1 = (i |-> z).y as complex number by A3,FUNCOP_1:13;
    r1 = x by A2;
   hence x in COMPLEX by XCMPLX_0:def 2;
  end;
end;

theorem
   for F being FinSequence of COMPLEX holds
    Product F = multcomplex $$ F
  proof
    let F be FinSequence of COMPLEX;
    thus thesis;
  end;

theorem
   Product ((i+j)|->z) = (Product (i|->z))*(Product (j|->z))
 proof
   thus Product ((i+j)|->z) =
   multcomplex.(multcomplex$$(i|->z),multcomplex$$(j|->z)) by SETWOP_2:37
     .= (Product (i|->z))*(Product (j|->z)) by BINOP_2:def 5;
 end;

theorem
   Product ((i*j)|->z) = Product (j |-> (Product (i|->z))) by SETWOP_2:38;

theorem
   Product (i|->(z1*z2)) = (Product (i|->z1))*(Product (i|->z2))
 proof
   thus Product (i|->(z1*z2)) =
   multcomplex$$(i|->multcomplex.(z1,z2)) by BINOP_2:def 5
     .= multcomplex.(multcomplex$$(i|->z1),multcomplex$$(i|->z2))
                by SETWOP_2:47
     .= (Product (i|->z1))*(Product (i|->z2)) by BINOP_2:def 5;
 end;
