:: The {F}ashoda Meet Theorem for Continuous Mappings
::  by Yatsuka Nakamura , Andrzej Trybulec and Artur Korni{\l}owicz
::
:: Received September 14, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies FINSEQ_1, SQUARE_1, ABSVALUE, ARYTM_1, FUNCT_1, FINSEQ_4, BOOLE,
      RELAT_1, EUCLID, TOPREAL1, PRE_TOPC, TARSKI, MCART_1, GOBOARD1, GOBOARD4,
      COMPLEX1, METRIC_1, PCOMPS_1, COMPTS_1, WEIERSTR, ARYTM_3, BORSUK_1,
      ORDINAL2, RCOMP_1, TBSP_1, SUBSET_1, PSCOMP_1, LATTICES, ARYTM, UNIFORM1,
      TOPMETR, INT_1, CONNSP_1, RELAT_2, GRAPH_1, TOPREALA, JGRAPH_6, JORDAN1,
      BORSUK_6, ASYMPT_0;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XXREAL_0,
      XREAL_0, COMPLEX1, REAL_1, FUNCT_1, RELSET_1, FUNCT_2, FINSEQ_1, NAT_1,
      STRUCT_0, BINARITH, FINSEQ_4, TOPREAL1, PRE_TOPC, GOBOARD1, GOBOARD4,
      TOPMETR, PCOMPS_1, COMPTS_1, WEIERSTR, METRIC_1, TBSP_1, RCOMP_1,
      UNIFORM1, INT_1, CONNSP_1, SQUARE_1, PSCOMP_1, EUCLID, JGRAPH_1,
      BORSUK_1, BORSUK_2, JORDAN1, JGRAPH_6, TOPREALA, TOPALG_2;
 constructors REAL_1, BINARITH, GOBOARD5, GOBOARD4, WEIERSTR, TBSP_1, RCOMP_1,
      SQUARE_1, PSCOMP_1, FINSEQ_4, TOPRNS_1, GOBOARD1, JORDAN7, COMPLEX1,
      NAT_1, UNIFORM1, CONNSP_1, TOPREALA, JGRAPH_6, JORDAN1, XXREAL_0;
 registrations STRUCT_0, RELSET_1, EUCLID, PRE_TOPC, BORSUK_1, XREAL_0,
      FINSEQ_1, MEMBERED, BORSUK_2, INT_1, RELAT_1, TOPALG_2, REAL_1, JORDAN5A,
      XXREAL_0;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, XBOOLE_0, JORDAN1, TOPALG_2, EUCLID, TOPREALA;
 theorems TARSKI, FUNCT_1, FINSEQ_1, NAT_1, REAL_1, REAL_2, SPPOL_1, FINSEQ_4,
      BINARITH, RELAT_1, FINSEQ_3, TOPREAL1, GOBOARD1, GOBOARD4, TOPREAL3,
      SQUARE_1, EUCLID, UNIFORM1, WEIERSTR, PRE_TOPC, TBSP_1, HEINE, COMPTS_1,
      TOPMETR, PSCOMP_1, BORSUK_1, RCOMP_1, FUNCT_2, XBOOLE_0, XBOOLE_1,
      TOPRNS_1, XCMPLX_1, JGRAPH_1, INT_1, CAT_3, XREAL_0, CONNSP_1, JORDAN2C,
      XREAL_1, STRUCT_0, BORSUK_2, TSEP_1, JGRAPH_6, JORDAN5B, FINSEQ_2,
      XXREAL_0, JORDAN1, TOPS_2;
 schemes FINSEQ_1, NAT_1;

begin

 reserve x, y for set;
 reserve i, j, n for Nat;
 reserve p, p1, p2 for Point of TOP-REAL n;
 reserve a, b, c, d for real number;

Lm1: I[01]=TopSpaceMetr(Closed-Interval-MSpace(0,1)) by TOPMETR:27,def 8;
Lm2:for x being set,f being FinSequence st 1<=len f holds
 (f^<*x*>).1=f.1 & (<*x*>^f).(len f +1)=f.len f
proof let x be set,f be FinSequence;assume A1:1<=len f;
 then A2:1 in dom f by FINSEQ_3:27;
A3:len f in dom f by A1,FINSEQ_3:27;
 (<*x*>^f).(len f +1)=(<*x*>^f).(len <*x*>+len f) by FINSEQ_1:56
 .=f.len f by A3,FINSEQ_1:def 7;
 hence thesis by A2,FINSEQ_1:def 7;
end;

Lm3:for f being FinSequence of REAL st
 (for k being Nat st 1<=k & k<len f holds f/.k<f/.(k+1)) holds
 f is increasing
proof let f be FinSequence of REAL;
 assume A1:for k being Nat st 1<=k & k<len f holds f/.k<f/.(k+1);
  now let i,j;
    now assume A2:i in dom f & j in dom f & i<j;
    then A3:1<=i & i<=len f by FINSEQ_3:27;
    defpred P[Nat] means i+(1+$1)<=len f implies f/.i<f/.(i+(1+$1));
    A4:1<=j & j<=len f by A2,FINSEQ_3:27;
    then i<len f by A2,XXREAL_0:2;
    then A5: P[0] by A1,A3;
    A6:for k being Nat st P[k] holds P[(k+1)]
    proof let k be Nat;assume
A7:  i+(1+k)<=len f implies f/.i<f/.(i+(1+k));
     now assume A8:i+(1+(k+1))<=len f;
       A9:1<=i+1 by NAT_1:29;   i+1<=i+1+k by NAT_1:29;
       then A10: 1<=i+1+k by A9,XXREAL_0:2;
       1+k<1+(k+1) by NAT_1:38;
       then A11: i+(1+k)<i+(1+(k+1)) by XREAL_1:8;
       then A12:i+(1+k)<len f by A8,XXREAL_0:2;
       i+(1+(k+1))=i+(1+k)+1;
       then f/.(i+(1+k))<f/.(i+(1+(k+1))) by A1,A10,A12;
      hence f/.i<f/.(i+(1+(k+1))) by A7,A8,A11,XXREAL_0:2;
     end;
    hence i+(1+(k+1))<=len f implies f/.i<f/.(i+(1+(k+1)));
   end;
  A13:for k being Nat holds P[k] from NAT_1:sch 1(A5,A6);
  i+1<=j by A2,NAT_1:38;
  then j-'(i+1)=j-(i+1) by BINARITH:50;
  then i+(1+(j-'(i+1))) = j;
 then A14:f/.i<f/.j by A4,A13;
 f/.i=f.i by A2,FINSEQ_4:def 4;
 hence f.i<f.j by A2,A14,FINSEQ_4:def 4;
 end;
 hence i in dom f & j in dom f & i<j implies f.i<f.j;
 end;
hence thesis by GOBOARD1:def 1;
end;

registration
  let a, b, c, d;
  cluster closed_inside_of_rectangle(a,b,c,d) -> convex;
  coherence
  proof
    set P = closed_inside_of_rectangle(a,b,c,d);
A1: P = {p where p is Point of TOP-REAL 2:
    a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d} by JGRAPH_6:def 2;
    let w1, w2 be Point of TOP-REAL 2 such that
A2: w1 in P & w2 in P;
    let x be set such that
A3: x in LSeg(w1,w2);
A4: ex p3 being Point of TOP-REAL 2 st
    p3 = w1 & a <= p3`1 & p3`1 <= b & c <= p3`2 & p3`2 <= d by A1,A2;
A5: ex p4 being Point of TOP-REAL 2 st
    p4 = w2 & a <= p4`1 & p4`1 <= b & c <= p4`2 & p4`2 <= d by A1,A2;
    x in {(1-l)*w1 + l*w2 where l is Real: 0 <= l & l <= 1}
    by A3,TOPREAL1:def 3;
    then consider l being Real such that
A6: x = (1-l)*w1 + l*w2 & 0 <= l & l <= 1;
    set w = (1-l)*w1 + l*w2;
A7: w = |[((1-l)*w1)`1+(l*w2)`1,((1-l)*w1)`2+ (l*w2)`2]| by EUCLID:59;
    (1-l)*w1=|[ (1-l)*w1`1 ,(1-l)*w1`2 ]| & l*w2=|[ l*w2`1 ,l*w2`2 ]|
      by EUCLID:61;
    then ((1-l)*w1)`1= (1-l)*w1`1 & ((1-l)*w1)`2= (1-l)*w1`2
    & (l*w2)`1=l*w2`1 & (l*w2)`2=l*w2`2 by EUCLID:56;
    then w`1=(1-l)* w1`1+ l* w2`1 & w`2=(1-l)* w1`2+ l* w2`2 by A7,EUCLID:56;
    then a <= w`1 & w`1 <= b & c <= w`2 & w`2 <= d
      by A4,A5,A6,XREAL_1:175,176;
    hence thesis by A1,A6;
  end;
end;

registration
  let a, b, c, d;
  cluster Trectangle(a,b,c,d) -> convex;
  coherence
  proof
    set T = Trectangle(a,b,c,d);
    the carrier of T = closed_inside_of_rectangle(a,b,c,d) by JORDAN1:1;
    hence [#]T is convex Subset of TOP-REAL 2 by PRE_TOPC:12;
  end;
end;

theorem Th1:
  for e being positive (real number),
      g being continuous Function of I[01],TOP-REAL n
  ex h being FinSequence of REAL
   st h.1=0 & h.len h=1 & 5<=len h & rng h c= the carrier of I[01]
   & h is increasing
   & (for i being Nat,Q being Subset of I[01],
    W being Subset of Euclid n
    st 1<=i & i<len h & Q=[.h/.i,h/.(i+1).] & W=g.:(Q) holds diameter(W)<e)
    proof
      let e be positive (real number),
      g be continuous Function of I[01],TOP-REAL n;
      reconsider e as positive Real by XREAL_0:def 1;
     A1:dom g=the carrier of I[01] by FUNCT_2:def 1;
     consider f being Function of
      Closed-Interval-MSpace(0,1),Euclid n such that A2: f=g by UNIFORM1:11;
     A3:f is uniformly_continuous by A2,UNIFORM1:9;
     A4:e/2<e by XREAL_1:218;
     A5:e/2>0 by XREAL_1:217;
     then consider s1 being Real such that
A6:  0<s1 and
A7:  for u1,u2 being Element of
     (Closed-Interval-MSpace(0,1)) st dist(u1,u2) < s1 holds
       dist((f/.u1),(f/.u2)) < e/2 by A3,UNIFORM1:def 1;
      set s=min(s1,1/2);
      A8:0<=s by A6,SQUARE_1:39;
      A9: s<>0 by A6,XXREAL_0:15;
      then A10:0<s by A6,SQUARE_1:39;
      A11:s<=s1 by XXREAL_0:17;
     A12: for u1,u2 being Element of
     (Closed-Interval-MSpace(0,1))
      st dist(u1,u2) < s holds dist((f/.u1),(f/.u2)) < e/2
      proof let u1,u2 be Element of
     (Closed-Interval-MSpace(0,1));
      assume dist(u1,u2) < s;
       then dist(u1,u2) < s1 by A11,XXREAL_0:2;
      hence dist((f/.u1),(f/.u2)) < e/2 by A7;
      end;
      s<=1/2 by XXREAL_0:17;
      then A13:2/s>=2/(1/2) by A8,A9,REAL_2:201;
      A14:s/2>0 by A8,A9,XREAL_1:217;
      2/s>0 by A8,A9,REAL_2:127;
      then reconsider j= [/ (2/s) \] as Nat by INT_1:80;
      A15: 2/s <= [/ (2/s) \] by INT_1:def 5;
      A16:2/s<=j by INT_1:def 5;
      0<j by A10,A15,REAL_2:127;
      then A17: 0+1<=j by NAT_1:38;
      then A18:1 in Seg j by FINSEQ_1:3;
      A19:4<=j by A13,A16,XXREAL_0:2;
      2/s-j<=0 by A15,REAL_2:106;
      then 1+(2/s-j)<=1+0 by XREAL_1:8;
      then A20: s/2*(1+(2/s-j))<=s/2*1 by A14,XREAL_1:66;
    defpred P[Nat,set] means $2=s/2*($1-1);
    A21:for k being Nat,y1,y2 being set st
    k in Seg j &  P[k,y1]& P[k,y2] holds y1=y2;
    A22:for k being Nat st k in Seg j ex x st P[k,x];
    consider p being FinSequence such that
     A23: dom p = Seg j & for k being Nat st k in Seg j holds
     P[k,p.k] from FINSEQ_1:sch 1(A21,A22);
    A24:p.1=s/2*(1-1) by A18,A23 .=0;
    A25: Seg len p=Seg j by A23,FINSEQ_1:def 3;
    A26:len p=j by A23,FINSEQ_1:def 3;
    [/ (2/s) \] < 2/s+1 by INT_1:def 5;
    then [/ (2/s) \] -1 < 2/s+1-1 by XREAL_1:11;
    then A27:s/2*(j-1)<s/2*(2/s) by A14,XREAL_1:70;
    A28:s/2*(2/s)=(2*s)/(2*s) by XCMPLX_1:77;
    2*s<>0 by A9;
    then A29:(2*s)/(2*s)=1 by XCMPLX_1:60;
    then A30: 1-s/2*(j-1)=s/2*(1+(2/s- [/ (2/s) \])) by A28;
A31: for i being Nat,r1,r2 being Real st 1<=i & i<len p & r1=p.i & r2=p.(i+1)
      holds r1<r2 & r2-r1=s/2
    proof let i be Nat,r1,r2 be Real;assume
      A32: 1<=i & i<len p & r1=p.i & r2=p.(i+1);
      then A33:i in Seg j by A25,FINSEQ_1:3;
      A34:i<i+1 by NAT_1:38;
      1<i+1 & i+1<=len p by A32,NAT_1:38;
      then A35:(i+1) in Seg j by A25,FINSEQ_1:3;
      A36:r1=s/2*(i-1) by A23,A32,A33;
      A37:r2=s/2*(i+1-1) by A23,A32,A35;
      i-1<i+1-1 by A34,XREAL_1:11;
      hence r1<r2 by A14,A36,A37,XREAL_1:70;
      r2-r1=s/2*(i)-s/2*(i-1) by A23,A32,A33,A37;
     hence thesis;
    end;
A38:for i being Nat,r1 being Real st 1<=i & i<=len p & r1=p.i
      holds r1<1
    proof let i be Nat,r1 be Real;assume
      A39: 1<=i & i<=len p & r1=p.i;
      then i in Seg j by A25,FINSEQ_1:3;
      then A40:r1=s/2*(i-1) by A23,A39;
      i-1<=j-1 by A26,A39,XREAL_1:11;
      then (s/2)*(i-1)<=(s/2)*(j-1) by A14,XREAL_1:66;
     hence thesis by A27,A28,A29,A40,XXREAL_0:2;
    end;
    A41:for r1 being Real st r1=p.len p holds 1-r1<=s/2
    proof let r1 be Real;assume
A42:  r1=p.len p;
      len p in Seg j by A17,A26,FINSEQ_1:3;
     hence thesis by A20,A23,A26,A30,A42;
    end;
     rng (p^<*1*>) c= REAL
     proof let y;assume y in rng (p^<*1*>);
       then consider x such that
       A43: x in dom (p^<*1*>) & y=(p^<*1*>).x by FUNCT_1:def 5;
       A44:dom (p^<*1*>)=Seg len (p^<*1*>) by FINSEQ_1:def 3;
       reconsider nx=x as Nat by A43;
       A45:1<=nx & nx<=len (p^<*1*>) by A43,A44,FINSEQ_1:3;
       A46: len (p^<*1*>)=len p+1 by FINSEQ_2:19;
       then A47:1<=nx & nx<=len p+1 by A43,A44,FINSEQ_1:3;
       per cases;
       suppose nx<len p+1;
         then A48:nx<=len p by NAT_1:38;
         then nx in Seg j by A25,A47,FINSEQ_1:3;
         then A49: p.nx=s/2*(nx-1) by A23;
         y=p.nx by A43,A45,A48,FINSEQ_1:85;
        hence y in REAL by A49;
       end;
       suppose nx>=len p+1;
         then nx=len p+1 by A45,A46,XXREAL_0:1;
         then y=1 by A43,FINSEQ_1:59;
        hence y in REAL;
       end;
     end;
    then reconsider h1=p^<*1*> as FinSequence of REAL by FINSEQ_1:def 4;
    A50:len h1 =len p+1 by FINSEQ_2:19;
    A51: 4+1<=len p+1 by A19,A26,XREAL_1:8;
    A52:h1.1=0 by A17,A24,A26,Lm2;
    A53:h1.len h1=1 by A50,FINSEQ_1:59;
    A54:rng p c=[.0,1.]
     proof let y be set;assume y in rng p;
       then consider x being set such that
       A55: x in dom p & y=p.x by FUNCT_1:def 5;
       A56:x in Seg len p by A55,FINSEQ_1:def 3;
       reconsider nx=x as Nat by A55;
       A57:1<=nx & nx<=len p by A56,FINSEQ_1:3;
       A58:p.nx=s/2*(nx-1) by A23,A55;
       then reconsider ry=p.nx as Real;
       A59:ry<1 by A38,A57;
       A60:s/2>=0 by A8,REAL_2:125;
       nx-1>=1-1 by A57,XREAL_1:11;
       then ry>=0 by A58,A60,REAL_2:121;
       then y in {rs where rs is Real:0<=rs & rs<=1} by A55,A59;
      hence y in [.0,1.] by RCOMP_1:def 1;
     end;
    A61:rng <*1*> ={1} by FINSEQ_1:55;
    1 in {r where r is Real:0<=r & r<=1};
    then A62:1 in [.0,1.] by RCOMP_1:def 1;
     {1} c= [.0,1.]
     proof let x be set;assume x in {1};
      hence x in [.0,1.] by A62,TARSKI:def 1;
     end;
    then A63:[.0,1.] \/ {1} =[.0,1.] by XBOOLE_1:12;
    rng h1 =rng p \/ {1} by A61,FINSEQ_1:44;
    then A64: rng h1 c=[.0,1.] \/ {1} by A54,XBOOLE_1:13;
    Closed-Interval-TSpace(0,1)
        =TopSpaceMetr(Closed-Interval-MSpace(0,1)) by TOPMETR:def 8;
    then A65: the carrier of I[01] =the carrier of Closed-Interval-MSpace(0,1)
                      by TOPMETR:16,27
       .= [.0,1.] by TOPMETR:14;
    A66:for i being Nat st 1<=i & i<len h1 holds h1/.i<h1/.(i+1)
     proof let i be Nat;assume
       A67: 1<=i & i<len h1;
       then A68:i<=len p by A50,NAT_1:38;
       A69:i+1<=len h1 by A67,NAT_1:38;
       A70:1<i+1 by A67,NAT_1:38;
       per cases;
       suppose A71:i<len p;
         A72:h1.i=h1/.i by A67,FINSEQ_4:24;
         A73:h1.i=p.i by A67,A71,FINSEQ_1:85;
         A74:i+1<=len p by A71,NAT_1:38;
         A75:h1.(i+1)=h1/.(i+1) by A69,A70,FINSEQ_4:24;
        h1.(i+1)=p.(i+1) by A70,A74,FINSEQ_1:85;
        hence h1/.i<h1/.(i+1) by A31,A67,A71,A72,A73,A75;
       end;
       suppose i>=len p;
        then A76:i=len p by A68,XXREAL_0:1;
        A77:h1/.(i+1)=h1.(i+1) by A69,A70,FINSEQ_4:24
                  .=1 by A76,FINSEQ_1:59;
        h1/.i=h1.i by A67,FINSEQ_4:24.=p.i by A67,A68,FINSEQ_1:85;
        hence h1/.i<h1/.(i+1) by A38,A67,A68,A77;
       end;
     end;
    then A78:h1 is increasing by Lm3;
    A79:for i being Nat st 1<=i & i<len h1 holds h1/.(i+1)-h1/.i<=s/2
     proof let i be Nat;assume
       A80: 1<=i & i<len h1;
       then A81:i<=len p by A50,NAT_1:38;
       A82:i+1<=len h1 by A80,NAT_1:38;
       A83:1<i+1 by A80,NAT_1:38;
       per cases;
       suppose A84:i<len p;
         A85:h1.i=h1/.i by A80,FINSEQ_4:24;
         A86:h1.i=p.i by A80,A84,FINSEQ_1:85;
         A87:i+1<=len p by A84,NAT_1:38;
         A88:h1.(i+1)=h1/.(i+1) by A82,A83,FINSEQ_4:24;
        h1.(i+1)=p.(i+1) by A83,A87,FINSEQ_1:85;
        hence h1/.(i+1)-h1/.i<=s/2 by A31,A80,A84,A85,A86,A88;
       end;
       suppose i>=len p;
         then A89:i=len p by A81,XXREAL_0:1;
         A90:h1/.(i+1)=h1.(i+1) by A82,A83,FINSEQ_4:24
                  .=1 by A89,FINSEQ_1:59;
        h1/.i=h1.i by A80,FINSEQ_4:24.=p.i by A80,A81,FINSEQ_1:85;
        hence h1/.(i+1)-h1/.i<=s/2 by A41,A89,A90;
       end;
     end;
     for i being Nat,Q being Subset of I[01],
      W being Subset of Euclid n
      st 1<=i & i<len h1 & Q=[.h1/.i,h1/.(i+1).] & W=g.:(Q) holds
      diameter(W)<e
     proof let i be Nat,Q be Subset of I[01],
       W be Subset of Euclid n;
       assume A91:1<=i & i<len h1 & Q=[.h1/.i,h1/.(i+1).] & W=g.:(Q);
        then h1/.i<h1/.(i+1) by A66;
        then A92:Q<>{} by A91,RCOMP_1:15;
        consider x1 being Element of Q;
        x1 in Q by A92;
        then A93: g.x1 in W by A1,A91,FUNCT_1:def 12;
        A94:for x,y being Point of Euclid n st
        x in W & y in W holds dist(x,y)<=e/2
         proof let x,y be Point of Euclid n;
           assume A95: x in W & y in W;
           then consider x3 being set such that
       A96:x3 in dom g & x3 in Q & x=g.x3 by A91,FUNCT_1:def 12;
           reconsider x3 as Element of
             Closed-Interval-MSpace(0,1) by A96,Lm1,TOPMETR:16;
           consider y3 being set such that
       A97:y3 in dom g & y3 in Q & y=g.y3 by A91,A95,FUNCT_1:def 12;
           reconsider y3 as Element of
             Closed-Interval-MSpace(0,1) by A97,Lm1,TOPMETR:16;
           A98:f.x3=(f/.x3) by CAT_3:def 1;
           A99:f.y3=(f/.y3) by CAT_3:def 1;
           reconsider r3=x3 as Real by A91,A96;
           reconsider s3=y3 as Real by A91,A97;
           A100:abs(r3-s3)<=h1/.(i+1)-h1/.i by A91,A96,A97,UNIFORM1:14;
           h1/.(i+1)-h1/.i<=s/2 by A79,A91;
           then abs(r3-s3)<=s/2 by A100,XXREAL_0:2;
           then A101:dist(x3,y3)<=s/2 by HEINE:1;
           s/2<s by A8,A9,XREAL_1:218;
           then dist(x3,y3)<s by A101,XXREAL_0:2;
          hence dist(x,y)<=e/2 by A2,A12,A96,A97,A98,A99;
         end;
        then W is bounded by A5,TBSP_1:def 9;
        then diameter(W)<=e/2 by A93,A94,TBSP_1:def 10;
      hence diameter(W)<e by A4,XXREAL_0:2;
     end;
   hence thesis by A50,A51,A52,A53,A63,A64,A65,A78;
  end;

theorem Th2:
  for P being Subset of TOP-REAL n st P c= LSeg(p1,p2) & p1 in P & p2 in P
  & P is connected holds P = LSeg(p1,p2)
  proof
    let P be Subset of TOP-REAL n;
  assume A1: P c= LSeg(p1,p2) & p1 in P & p2 in P
   & P is connected;
   reconsider L=LSeg(p1,p2) as non empty Subset of TOP-REAL n by A1;
  now assume not LSeg(p1,p2) c= P;then
   consider x0 being set such that
   A2: x0 in LSeg(p1,p2) & not x0 in P by TARSKI:def 3;
   reconsider p0=x0 as Point of TOP-REAL n by A2;
A3: p1 in LSeg(p1,p2) by TOPREAL1:6;then
   A4: LSeg(p1,p0) c= L by A2,TOPREAL1:12;
A5: LSeg(p1,p0)\{p0} c= LSeg(p1,p0) by XBOOLE_1:36;
   A6: the carrier of ((TOP-REAL n)|L)=[#]((TOP-REAL n)|L) by PRE_TOPC:12
                                 .= L by PRE_TOPC:def 10;then
   reconsider R1=LSeg(p1,p0)\{p0} as Subset of (TOP-REAL n)|L
     by A5,XBOOLE_1:1,A4;
A7: p2 in LSeg(p1,p2) by TOPREAL1:6;then
   A8: LSeg(p0,p2) c= L by A2,TOPREAL1:12;
   LSeg(p0,p2)\{p0} c= LSeg(p0,p2) by XBOOLE_1:36;then
   reconsider R2=LSeg(p0,p2)\{p0} as Subset of (TOP-REAL n)|L
     by A6,XBOOLE_1:1,A8;
   reconsider PX=P as Subset of (TOP-REAL n)|L by A6,A1;
   reconsider PX1=LSeg(p1,p0) as Subset of (TOP-REAL n)|L
     by A3,A6,A2,TOPREAL1:12;
   reconsider PX2=LSeg(p0,p2) as Subset of (TOP-REAL n)|L
     by A7,A6,A2,TOPREAL1:12;
A9: (TOP-REAL n)|L is_T2 by TOPMETR:3;
   LSeg(p1,p0) is compact by SPPOL_1:28;then
   PX1 is compact by BORSUK_1:42;then
   PX1 is closed by COMPTS_1:16,A9;then
A10: Cl PX1 =PX1 by PRE_TOPC:52;
A11: PX is connected by JORDAN2C:15,A1;
A12: R2 c= PX2 by XBOOLE_1:36;
A13: R1 c= PX1 by XBOOLE_1:36;then
   Cl R1 c= Cl PX1 by PRE_TOPC:49;then
A14: (Cl R1) /\ R2 c= PX1 /\ R2 by A10,XBOOLE_1:27;
A15: PX1 /\ PX2 ={p0} by A2,TOPREAL1:14;
   PX1 /\ R2 =PX1 /\ PX2 \ {p0} by XBOOLE_1:49
   .= {} by A15,XBOOLE_1:37;then
   (Cl R1) /\ R2={} by A14,XBOOLE_1:3;then
A16: (Cl R1) misses R2 by XBOOLE_0:def 7;
   LSeg(p0,p2) is compact by SPPOL_1:28;then
   PX2 is compact by BORSUK_1:42;then
   PX2 is closed by COMPTS_1:16,A9;then
A17: Cl PX2 =PX2 by PRE_TOPC:52;
   R2 c= PX2 by XBOOLE_1:36;then
   Cl R2 c= Cl PX2 by PRE_TOPC:49;then
A18: R1 /\ (Cl R2) c= R1 /\ PX2 by A17,XBOOLE_1:27;
A19: PX1 /\ PX2 ={p0} by A2,TOPREAL1:14;
   R1 /\ PX2 = PX1 /\ PX2 \ {p0} by XBOOLE_1:49
   .= {} by A19,XBOOLE_1:37;then
   R1/\ (Cl R2)={} by A18,XBOOLE_1:3;then
   R1 misses (Cl R2) by XBOOLE_0:def 7;then
A20: R1,R2 are_separated by A16,CONNSP_1:def 1;
A21: {p0} c= LSeg(p1,p0)
    proof let d be set;assume d in {p0};then
     d=p0 by TARSKI:def 1;
     hence d in LSeg(p1,p0) by TOPREAL1:6;
    end;
A22: {p0} c= LSeg(p0,p2)
    proof let d be set;assume d in {p0};then
      d=p0 by TARSKI:def 1;
     hence d in LSeg(p0,p2) by TOPREAL1:6;
    end;
A23: L=LSeg(p1,p0) \/ LSeg(p0,p2) by TOPREAL1:11,A2
    .=(LSeg(p1,p0)\{p0})\/{p0} \/ LSeg(p0,p2) by A21,XBOOLE_1:45
    .=(LSeg(p1,p0)\{p0})\/ ({p0} \/ LSeg(p0,p2)) by XBOOLE_1:4
    .= R1 \/ PX2 by A22,XBOOLE_1:12
    .= R1 \/ ((PX2 \{p0}) \/ {p0}) by XBOOLE_1:45,A22
    .= R1 \/ {p0} \/ R2 by XBOOLE_1:4;
A24: P c= R1 \/ R2
   proof let z be set;assume A25: z in P;then
     z in R1 \/ {p0} or z in R2 by A23,A1,XBOOLE_0:def 2;then
     z in R1 or z in {p0} or z in R2 by XBOOLE_0:def 2;
    hence z in R1 \/ R2 by A25,A2,XBOOLE_0:def 2,TARSKI:def 1;
   end;
A26: now assume P c= R1;then
A27: p2 in R1 by A1;
     p2 in PX2 by TOPREAL1:6;then
     p2 in PX1 /\ PX2 by A27,A13,XBOOLE_0:def 3;
    hence contradiction by A2,A1,A19,TARSKI:def 1;
   end;
   now assume P c= R2;then
A28:  p1 in R2 by A1;
     p1 in PX1 by TOPREAL1:6;then
     p1 in PX1 /\ PX2 by A28,A12,XBOOLE_0:def 3;
    hence contradiction by A2,A1,A19,TARSKI:def 1;
   end;
   hence contradiction by A26,A20,A24,A11,CONNSP_1:17;
 end;
 hence P=LSeg(p1,p2) by XBOOLE_0:def 10,A1;
end;

theorem Th3:
  for g being Path of p1,p2 st rng g c= LSeg(p1,p2) holds
  rng g = LSeg(p1,p2)
  proof
    let g be Path of p1,p2;
    assume
A1: rng g c= LSeg(p1,p2);
A2: p1=g.0 & p2=g.1 by BORSUK_2:def 4;
  now assume A3: not LSeg(p1,p2) c= rng g;
    [#]I[01] is connected by CONNSP_1:28;then
    A4: g.:([#]I[01]) is connected by TOPS_2:75;
    0 in the carrier of I[01] by BORSUK_1:86;then
    A5: 0 in dom g by FUNCT_2:def 1;
    the carrier of I[01]=[#]I[01] by PRE_TOPC:def 3;then
    A6: p1 in g.:([#]I[01]) by A2,A5,FUNCT_1:def 12;
    1 in the carrier of I[01] by BORSUK_1:86;then
    A7: 1 in dom g by FUNCT_2:def 1;
    the carrier of I[01]=[#]I[01] by PRE_TOPC:def 3;then
    A8: p2 in g.:([#]I[01]) by A2,A7,FUNCT_1:def 12;
    A9: g.:([#]I[01]) c= rng g
    proof let y be set;assume y in g.:([#]I[01]);then
A10:   ex x being set st
     x in dom g & x in [#]I[01] & y=g.x by FUNCT_1:def 12;
     thus y in rng g by A10,FUNCT_1:def 5;
    end;then
    g.:([#]I[01]) c= LSeg(p1,p2) by A1,XBOOLE_1:1;
   hence contradiction by A9,A3,A6,A8,A4,Th2;
 end;
 hence rng g=LSeg(p1,p2) by A1,XBOOLE_0:def 10;
end;

:: Goboard Theorem in continuous case
theorem Th4:
  for P, Q being non empty Subset of TOP-REAL 2,
  p1, p2, q1, q2 being Point of TOP-REAL 2,
  f being Path of p1,p2, g being Path of q1,q2
  st rng f = P & rng g = Q &
  (for p being Point of TOP-REAL 2 st p in P holds p1`1<=p`1 & p`1<= p2`1) &
  (for p being Point of TOP-REAL 2 st p in Q holds p1`1<=p`1 & p`1<= p2`1) &
  (for p being Point of TOP-REAL 2 st p in P holds q1`2<=p`2 & p`2<= q2`2) &
  (for p being Point of TOP-REAL 2 st p in Q holds q1`2<=p`2 & p`2<= q2`2)
  holds P meets Q
  proof
    let P,Q be non empty Subset of TOP-REAL 2,
    p1,p2,q1,q2 be Point of TOP-REAL 2,
    f be Path of p1,p2,
    g be Path of q1,q2;
    assume that
A1: rng f = P and
A2: rng g = Q and
A3: for p being Point of TOP-REAL 2 st p in P holds p1`1<=p`1 & p`1<= p2`1
  and
A4: for p being Point of TOP-REAL 2 st p in Q holds p1`1<=p`1 & p`1<= p2`1
  and
A5: for p being Point of TOP-REAL 2 st p in P holds q1`2<=p`2 & p`2<= q2`2
  and
A6: for p being Point of TOP-REAL 2 st p in Q holds q1`2<=p`2 & p`2<= q2`2;
A7: f.0 = p1 & f.1 = p2 by BORSUK_2:def 4;
A8: g.0 = q1 & g.1 = q2 by BORSUK_2:def 4;
   A9:TOP-REAL 2=TopSpaceMetr(Euclid 2) by EUCLID:def 8;
   reconsider P'=P,Q'=Q as Subset of TopSpaceMetr(Euclid 2) by EUCLID:def 8;
   A10: [#](I[01]) is compact by COMPTS_1:10;
   f.:(the carrier of I[01])=rng f by FUNCT_2:45;then
A11: f.:([#](I[01]))=rng f by PRE_TOPC:def 3;
   g.:(the carrier of I[01])=rng g by FUNCT_2:45;then
A12: g.:([#](I[01]))=rng g by PRE_TOPC:def 3;
   A13: 0 in the carrier of I[01] by BORSUK_1:86;then
   0 in dom f by FUNCT_2:def 1;then
   A14: p1 in P' by A1,A7,FUNCT_1:def 5;
   0 in dom g by FUNCT_2:def 1,A13;then
   A15: q1 in Q' by A2,A8,FUNCT_1:def 5;
   A16:P' <> {} & P' is compact & Q' <> {}
   & Q' is compact by A2,A11,A12,A9,A1,A10,WEIERSTR:14;
   assume A17: P /\ Q= {};
     then P misses Q by XBOOLE_0:def 7;
     then A18:min_dist_min(P',Q')>0 by A16,JGRAPH_1:55;
     set e=min_dist_min(P',Q')/5;
A19: e>0/5 by A18,REAL_1:73;
     then consider h being FinSequence of REAL such that
     A20: h.1=0 & h.len h=1 & 5<=len h & rng h c= the carrier of I[01]
     & h is increasing
     &(for i being Nat,R being Subset of I[01],
     W being Subset of Euclid 2
      st 1<=i & i<len h & R=[.h/.i,h/.(i+1).] & W=f.:(R) holds
     diameter(W)<e) by Th1;
     deffunc F(Nat)=f.(h.$1);
     ex h1' being FinSequence st len h1'=len h &
     for i st i in Seg len h holds h1'.i=F(i) from FINSEQ_1:sch 2;
     then consider h1' being FinSequence such that
     A21:len h1'=len h &
     for i st i in Seg len h holds h1'.i=f.(h.i);
     rng h1' c= the carrier of TOP-REAL 2
      proof let y;assume y in rng h1';
        then consider x such that
        A22:x in dom h1' & y=h1'.x by FUNCT_1:def 5;
        A23:dom h1'=Seg len h1' by FINSEQ_1:def 3;
        reconsider i=x as Nat by A22;
        A24: h1'.i=f.(h.i) by A21,A22,A23;
        i in dom h by A21,A22,A23,FINSEQ_1:def 3;
        then A25:h.i in rng h by FUNCT_1:def 5;
        dom f= the carrier of I[01] by FUNCT_2:def 1;
        then h1'.i in rng f by A20,A24,A25,FUNCT_1:def 5;
       hence thesis by A22;
      end;
     then reconsider h1=h1' as FinSequence of TOP-REAL 2 by FINSEQ_1:def 4;
A26: dom h=Seg len h by FINSEQ_1:def 3;
     A27:for i st 1<=i & i+1<=len h1 holds |.h1/.i-h1/.(i+1).|<e
      proof let i;assume A28:1<=i & i+1<=len h1;
        then A29:i<len h1 by NAT_1:38;
        A30:1<i+1 by A28,NAT_1:38;
        then A31:i+1 in Seg len h by A21,A28,FINSEQ_1:3;
        A32: i in Seg len h by A21,A28,A29,FINSEQ_1:3;
        then A33:h1'.i=f.(h.i) by A21;
        then A34:h1/.i=f.(h.i) by A28,A29,FINSEQ_4:24;
        A35:h1'.(i+1)=f.(h.(i+1)) by A21,A31;
        then A36:h1/.(i+1)=f.(h.(i+1)) by A28,A30,FINSEQ_4:24;
        A37:i in dom h by A21,A28,A29,FINSEQ_3:27;
        then A38:h.i in rng h by FUNCT_1:def 5;
A39:    dom f =the carrier of I[01] by FUNCT_2:def 1;
        then h1'.i in rng f by A20,A33,A38,FUNCT_1:def 5; then
A40:    f.(h.i) is Element of TOP-REAL 2 by A21,A32;
        i+1 in dom h by A21,A28,A30,FINSEQ_3:27;
        then h.(i+1) in rng h by FUNCT_1:def 5;
        then h1'.(i+1) in rng f by A20,A35,A39,FUNCT_1:def 5;
        then A41:f.(h.(i+1)) is Element of TOP-REAL 2 by A21,A31;
        A42:h.i=h/.i by A21,A28,A29,FINSEQ_4:24;
        A43:i+1 in dom h by A21,A28,A30,FINSEQ_3:27;
        then A44:h.(i+1) in rng h by FUNCT_1:def 5;
        A45:h.(i+1)=h/.(i+1) by A21,A28,A30,FINSEQ_4:24;
        i<i+1 by NAT_1:38;
        then A46:0<=h/.i & h/.i <= h/.(i+1) & h/.(i+1)<=1
         by A20,A37,A38,A42,A43,A44,A45,BORSUK_1:83,GOBOARD1:def 1
,RCOMP_1:48;
       reconsider W1=P as Subset of Euclid 2 by TOPREAL3:13;
        reconsider Pa=P as Subset of TOP-REAL 2;
        A47: [#](I[01]) is compact by COMPTS_1:10;
        f.:(the carrier of I[01])=rng f by FUNCT_2:45;then
        f.:([#](I[01]))=rng f by PRE_TOPC:def 3;then
        A48:Pa is compact by A1,A47,WEIERSTR:14;
        reconsider R=[.h/.i,h/.(i+1).] as Subset of I[01]
                          by A20,A38,A42,A44,A45,BORSUK_1:83,RCOMP_1:16;
        reconsider w1=f.(h.i),w2=f.(h.(i+1)) as Point of Euclid 2
                          by A40,A41,TOPREAL3:13;
        A49: h.i in R by A42,A46,RCOMP_1:48;
        h.(i+1) in R by A45,A46,RCOMP_1:48;
        then A50:w1 in f.:R & w2 in f.:R by A39,A49,FUNCT_1:def 12;
        reconsider W=f.:R as Subset of Euclid 2 by TOPREAL3:13;
        A51:dom f=[#](I[01]) & rng f=P by A1,A39,PRE_TOPC:def 3;
        [#](I[01])=[.0,1.] by BORSUK_1:83,PRE_TOPC:12;
        then P=f.:([.0,1.]) by A51,RELAT_1:146;
        then A52:W c= W1 by BORSUK_1:83,RELAT_1:156;
        0 in the carrier of I[01] by BORSUK_1:86;then
        f.0 in rng f by A39,FUNCT_1:def 5;then
        A53: W-bound Pa <= p1`1 & p1`1 <= E-bound Pa &
        S-bound Pa <= p1`2 & p1`2 <= N-bound Pa by A7,A1,A48,PSCOMP_1:71;
        set r1=((E-bound Pa) - (W-bound Pa))+((N-bound Pa) - (S-bound Pa))+1;
               (W-bound Pa)<=(E-bound Pa) by A53,XXREAL_0:2;
        then A54:0<=((E-bound Pa) - (W-bound Pa)) by XREAL_1:50;
               (S-bound Pa)<=(N-bound Pa) by A53,XXREAL_0:2;
        then 0<=((N-bound Pa) - (S-bound Pa)) by XREAL_1:50;
        then A55:((E-bound Pa) - (W-bound Pa))+((N-bound Pa) - (S-bound Pa))
            >=0+0 by A54,XREAL_1:9;
         A56: r1> ((E-bound Pa) - (W-bound Pa))+((N-bound Pa) - (S-bound Pa))
                   by XREAL_1:31;
         for x,y being Point of Euclid 2
         st x in W1 & y in W1 holds dist(x,y)<=r1
         proof let x,y be Point of Euclid 2;
           assume A57: x in W1 & y in W1;
           then reconsider pw1=x,pw2=y as Point of TOP-REAL 2;
           A58:dist(x,y)=|.pw1-pw2.| by JGRAPH_1:45;
           A59:|.pw1-pw2.|<=abs(pw1`1-pw2`1)+abs(pw1`2-pw2`2) by JGRAPH_1:49;
           A60:W-bound Pa <= pw1`1 & pw1`1 <= E-bound Pa &
           S-bound Pa <= pw1`2 & pw1`2 <= N-bound Pa by A48,A57,PSCOMP_1:71;
           A61:W-bound Pa <= pw2`1 & pw2`1 <= E-bound Pa &
                 S-bound Pa <= pw2`2 & pw2`2 <= N-bound Pa
                            by A48,A57,PSCOMP_1:71;
          then A62:abs(pw1`1-pw2`1)<=(E-bound Pa) - (W-bound Pa)
                           by A60,JGRAPH_1:31;
               abs(pw1`2-pw2`2)<=(N-bound Pa) - (S-bound Pa)
                           by A60,A61,JGRAPH_1:31;
           then A63:abs(pw1`1-pw2`1)+abs(pw1`2-pw2`2)
           <=((E-bound Pa) - (W-bound Pa))+((N-bound Pa) - (S-bound Pa))
                         by A62,XREAL_1:9;
               ((E-bound Pa) - (W-bound Pa))+((N-bound Pa) - (S-bound Pa))
           <= ((E-bound Pa) - (W-bound Pa))+((N-bound Pa) - (S-bound Pa))
            +1 by XREAL_1:31;
           then abs(pw1`1-pw2`1)+abs(pw1`2-pw2`2)<=r1 by A63,XXREAL_0:2;
          hence thesis by A58,A59,XXREAL_0:2;
         end;
        then W1 is bounded by A55,A56,TBSP_1:def 9;
        then A64:W is bounded by A52,TBSP_1:21;
        A65: diameter W<e by A20,A21,A28,A29;
        dist(w1,w2)<=diameter(W) by A50,A64,TBSP_1:def 10;
        then dist(w1,w2)<e by A65,XXREAL_0:2;
       hence thesis by A34,A36,JGRAPH_1:45;
      end;
     A66:len h1>=1 by A20,A21,XXREAL_0:2;
     consider hb being FinSequence of REAL such that
     A67: hb.1=0 & hb.len hb=1 & 5<=len hb & rng hb c= the carrier of I[01]
     & hb is increasing & (for i being Nat,R being Subset of I[01],
     W being Subset of Euclid 2
      st 1<=i & i<len hb & R=[.hb/.i,hb/.(i+1).] & W=g.:(R) holds
     diameter(W)<e) by A19,Th1;
     A68: 1<=len hb by A67,XXREAL_0:2;
     then A69:1 in dom hb by FINSEQ_3:27;
     A70:len hb in dom hb by A68,FINSEQ_3:27;
     deffunc F(Nat)=g.(hb.$1);
     ex h2' being FinSequence st len h2'=len hb &
     for i st i in Seg len hb holds h2'.i=F(i) from FINSEQ_1:sch 2;
     then consider h2' being FinSequence such that
     A71:len h2'=len hb &
     for i st i in Seg len hb holds h2'.i=g.(hb.i);
     rng h2' c= the carrier of TOP-REAL 2
      proof let y;assume y in rng h2';
        then consider x such that
        A72:x in dom h2' & y=h2'.x by FUNCT_1:def 5;
        A73:dom h2'=Seg len h2' by FINSEQ_1:def 3;
        reconsider i=x as Nat by A72;
        A74: h2'.i=g.(hb.i) by A71,A72,A73;
        i in dom hb by A71,A72,A73,FINSEQ_1:def 3;
        then A75:hb.i in rng hb by FUNCT_1:def 5;
        dom g = the carrier of I[01] by FUNCT_2:def 1;
        then h2'.i in rng g by A67,A74,A75,FUNCT_1:def 5;
        hence thesis by A72;
      end;
     then reconsider h2=h2' as FinSequence of TOP-REAL 2 by FINSEQ_1:def 4;
     A76:dom hb=Seg len hb by FINSEQ_1:def 3;
A77: for i st i in dom hb holds h2/.i=g.(hb.i)
      proof let i;assume
A78:   i in dom hb;
       then A79:h2.i=g.(hb.i) by A71,A76;
       1<=i & i<=len hb by A76,A78,FINSEQ_1:3;
       hence thesis by A71,A79,FINSEQ_4:24;
      end;
     A80:len h2>=1 by A67,A71,XXREAL_0:2;
     A81:for i st i in dom h1 holds
 (h1/.1)`1<=(h1/.i)`1 & (h1/.i)`1<=(h1/.len h1)`1
  proof let i;assume A82:i in dom h1;
     then A83: i in Seg len h by A21,FINSEQ_1:def 3;
     then A84:i in dom h by FINSEQ_1:def 3;
     h1.i=f.(h.i) by A21,A83;
     then A85:h1/.i=f.(h.i) by A82,FINSEQ_4:def 4;
     1 in dom h by A21,A66,FINSEQ_3:27;
     then h1.1=f.(h.1) by A21,A26;
     then A86:h1/.1=f.(h.1) by A66,FINSEQ_4:24;
     len h in dom h by A21,A66,FINSEQ_3:27;
     then h1.len h1=f.(h.len h) by A21,A26;
     then A87:h1/.len h1=f.(h.len h) by A66,FINSEQ_4:24;
     A88:h.i in rng h by A84,FUNCT_1:def 5;
     dom f =the carrier of I[01] by FUNCT_2:def 1;
     then h1/.i in rng f by A20,A85,A88,FUNCT_1:def 5;
     hence thesis by A7,A3,A1,A20,A86,A87;
  end;
A89: for i st i in dom X_axis h1 holds
  (X_axis h1).1<=(X_axis h1).i & (X_axis h1).i<=(X_axis h1).len h1
  proof let i;assume i in dom X_axis h1;
     then i in Seg len (X_axis h1) by FINSEQ_1:def 3;
     then i in Seg len h1 by A66,JGRAPH_1:59;
     then A90:i in dom h1 by FINSEQ_1:def 3;
     then A91:(X_axis h1).i = (h1/.i)`1 by JGRAPH_1:61;
     A92:(X_axis h1).1=(h1/.1)`1 by A66,JGRAPH_1:59;
     (X_axis h1).len h1=(h1/.len h1)`1 by A66,JGRAPH_1:59;
    hence thesis by A81,A90,A91,A92;
  end;
 A93:for i st i in dom h1 holds
 q1`2<=(h1/.i)`2 & (h1/.i)`2<=q2`2
  proof let i;assume A94:i in dom h1;
    then A95: i in Seg len h1 by FINSEQ_1:def 3;
    then A96:i in dom h by A21,FINSEQ_1:def 3;
    h1.i=f.(h.i) by A21,A95;
    then A97:h1/.i=f.(h.i) by A94,FINSEQ_4:def 4;
    A98:h.i in rng h by A96,FUNCT_1:def 5;
    dom f =the carrier of I[01] by FUNCT_2:def 1;
    then h1/.i in rng f by A20,A97,A98,FUNCT_1:def 5;
    hence thesis by A1,A5;
  end;
  for i st i in dom (Y_axis h1) holds
   q1`2<=(Y_axis h1).i & (Y_axis h1).i<=q2`2
  proof let i;assume i in dom (Y_axis h1);
    then i in Seg len (Y_axis h1) by FINSEQ_1:def 3;
    then i in Seg len h1 by A66,JGRAPH_1:60;
    then A99:i in dom h1 by FINSEQ_1:def 3;
    then (Y_axis h1).i = (h1/.i)`2 by JGRAPH_1:61;
    hence thesis by A93,A99;
  end;
 then A100:X_axis(h1) lies_between (X_axis(h1)).1, (X_axis(h1)).(len h1) &
 Y_axis(h1) lies_between q1`2,q2`2 by A89,GOBOARD4:def 1;
 A101:for i st i in dom h2 holds
 (h2/.1)`2<=(h2/.i)`2 & (h2/.i)`2<=(h2/.len h2)`2
  proof let i;assume i in dom h2;
   then i in Seg len h2 by FINSEQ_1:def 3;
   then A102:i in dom hb by A71,FINSEQ_1:def 3;
   then A103:h2/.i=g.(hb.i) by A77;
   1 in Seg len hb by A71,A80,FINSEQ_1:3;
   then 1 in dom hb by FINSEQ_1:def 3;
   then A104:h2/.1=g.(hb.1) by A77;
   len hb in Seg len hb by A71,A80,FINSEQ_1:3;
   then len hb in dom hb by FINSEQ_1:def 3;
   then A105:h2/.len h2=g.(hb.len hb) by A71,A77;
   A106:hb.i in rng hb by A102,FUNCT_1:def 5;
   dom g =the carrier of I[01] by FUNCT_2:def 1;
   then h2/.i in rng g by A67,A103,A106,FUNCT_1:def 5;
   hence thesis by A2,A8,A6,A67,A104,A105;
  end;
 A107: for i st i in dom (Y_axis(h2)) holds
  (Y_axis(h2)).1<=(Y_axis(h2)).i & (Y_axis(h2)).i <=(Y_axis(h2)).(len h2)
  proof let i;assume i in dom (Y_axis(h2));
    then i in Seg len (Y_axis(h2)) by FINSEQ_1:def 3;
    then i in Seg len h2 by A80,JGRAPH_1:60;
    then A108:i in dom h2 by FINSEQ_1:def 3;
    then A109:(Y_axis(h2)).i = (h2/.i)`2 by JGRAPH_1:61;
    A110:(Y_axis(h2)).1=(h2/.1)`2 by A80,JGRAPH_1:60;
    (Y_axis(h2)).len h2=(h2/.len h2)`2 by A80,JGRAPH_1:60;
    hence thesis by A101,A108,A109,A110;
  end;
 A111:for i st i in dom h2 holds
 p1`1<=(h2/.i)`1 & (h2/.i)`1<=p2`1
  proof let i;assume i in dom h2;
     then i in Seg len h2 by FINSEQ_1:def 3;
     then A112:i in dom hb by A71,FINSEQ_1:def 3;
     then A113:h2/.i=g.(hb.i) by A77;
     A114:hb.i in rng hb by A112,FUNCT_1:def 5;
     dom g =the carrier of I[01] by FUNCT_2:def 1;
     then h2/.i in rng g by A67,A113,A114,FUNCT_1:def 5;
     hence thesis by A2,A4;
  end;
  for i st i in dom (X_axis(h2)) holds
   p1`1<= (X_axis(h2)).i & (X_axis(h2)).i<=p2`1
  proof let i;assume i in dom (X_axis(h2));
    then i in Seg len (X_axis(h2)) by FINSEQ_1:def 3;
    then i in Seg len h2 by A80,JGRAPH_1:59;
    then A115:i in dom h2 by FINSEQ_1:def 3;
    then (X_axis(h2)).i = (h2/.i)`1 by JGRAPH_1:61;
    hence thesis by A111,A115;
  end;
 then A116:X_axis(h2) lies_between p1`1, p2`1 &
 Y_axis(h2) lies_between (Y_axis(h2)).1, (Y_axis(h2)).(len h2)
                                  by A107,GOBOARD4:def 1;
 A117:for i st 1<=i & i+1<=len h2 holds
   |. (h2/.i)-(h2/.(i+1)) .|<e
      proof let i;assume A118:1<=i & i+1<=len h2;
        then A119:i<len h2 by NAT_1:38;
        A120:1<i+1 by A118,NAT_1:38;
        then A121:i+1 in Seg len hb by A71,A118,FINSEQ_1:3;
        i in Seg len hb by A71,A118,A119,FINSEQ_1:3;
        then A122:h2'.i=g.(hb.i) by A71;
        then A123:h2/.i=g.(hb.i) by A118,A119,FINSEQ_4:24;
        A124:h2'.(i+1)=g.(hb.(i+1)) by A71,A121;
        h2.(i+1)=h2/.(i+1) by A118,A120,FINSEQ_4:24;
        then A125:h2/.(i+1)=g.(hb.(i+1)) by A71,A121;
        i in Seg len hb by A71,A118,A119,FINSEQ_1:3;
        then A126:i in dom hb by FINSEQ_1:def 3;
        then A127:hb.i in rng hb by FUNCT_1:def 5;
        A128: dom g =the carrier of I[01] by FUNCT_2:def 1;
        then A129:h2'.i in rng g by A67,A122,A127,FUNCT_1:def 5;
        i+1 in Seg len hb by A71,A118,A120,FINSEQ_1:3;
        then i+1 in dom hb by FINSEQ_1:def 3;
        then hb.(i+1) in rng hb by FUNCT_1:def 5;
        then h2'.(i+1) in rng g by A67,A124,A128,FUNCT_1:def 5;
   then A130:g.(hb.(i+1)) is Element of TOP-REAL 2 by A71,A121;
        A131:hb.i=hb/.i by A71,A118,A119,FINSEQ_4:24;
        i+1 in Seg len hb by A71,A118,A120,FINSEQ_1:3;
        then A132:i+1 in dom hb by FINSEQ_1:def 3;
        then A133:hb.(i+1) in rng hb by FUNCT_1:def 5;
        A134:hb.(i+1)=hb/.(i+1) by A71,A118,A120,FINSEQ_4:24;
        i<i+1 by NAT_1:38;
        then A135:0<=hb/.i & hb/.i <= hb/.(i+1) & hb/.(i+1)<=1
          by A67,A126,A127,A131,A132,A133,A134,BORSUK_1:83,GOBOARD1:def 1
,RCOMP_1:48;
       reconsider W1=Q as Subset of Euclid 2 by TOPREAL3:13;
        reconsider Qa=Q as Subset of TOP-REAL 2;
        A136: [#](I[01]) is compact by COMPTS_1:10;
        g.:(the carrier of I[01])=rng g by FUNCT_2:45;then
        g.:([#](I[01]))=rng g by PRE_TOPC:def 3;then
        A137:Qa is compact by A2,A136,WEIERSTR:14;
        reconsider Qa as non empty Subset of TOP-REAL 2;
        reconsider R=[.hb/.i,hb/.(i+1).] as Subset of I[01]
           by A67,A127,A131,A133,A134,BORSUK_1:83,RCOMP_1:16;
        reconsider w1=g.(hb.i),w2=g.(hb.(i+1)) as Point of Euclid 2
                                   by A122,A129,A130,TOPREAL3:13;
        A138: hb.i in R by A131,A135,RCOMP_1:48;
        hb.(i+1) in R by A134,A135,RCOMP_1:48;
        then A139:w1 in g.:(R) & w2 in g.:(R) by A128,A138,FUNCT_1:def 12;
        reconsider W=g.:(R) as Subset of Euclid 2 by TOPREAL3:13;
A140:   dom g=[#](I[01]) & rng g=Q by A2,PRE_TOPC:def 3,A128;
        [#](I[01])=[.0,1.] by BORSUK_1:83,PRE_TOPC:12;
        then Q=g.:([.0,1.]) by A140,RELAT_1:146;
        then A141:W c= W1 by BORSUK_1:83,RELAT_1:156;
        0 in the carrier of I[01] by BORSUK_1:86;then
        g.0 in rng g by A128,FUNCT_1:def 5;then
A142:   W-bound Qa <= q1`1 & q1`1 <= E-bound Qa &
        S-bound Qa <= q1`2 & q1`2 <= N-bound Qa by A2,A8,A137,PSCOMP_1:71;
        set r1=
           ((E-bound Qa) - (W-bound Qa))+((N-bound Qa) - (S-bound Qa))+1;
               (W-bound Qa)<=(E-bound Qa) by A142,XXREAL_0:2;
           then A143:0<=((E-bound Qa) - (W-bound Qa)) by XREAL_1:50;
               (S-bound Qa)<=(N-bound Qa) by A142,XXREAL_0:2;
           then 0<=((N-bound Qa) - (S-bound Qa)) by XREAL_1:50;
        then A144:((E-bound Qa) - (W-bound Qa))+((N-bound Qa) - (S-bound Qa))
            >=0+0 by A143,XREAL_1:9;
         A145: r1> ((E-bound Qa) - (W-bound Qa))+((N-bound Qa) - (S-bound Qa))
                             by XREAL_1:31;
            for x,y being Point of Euclid 2
         st x in W1 & y in W1 holds dist(x,y)<=r1
         proof let x,y be Point of Euclid 2;
           assume A146: x in W1 & y in W1;
           then reconsider pw1=x,pw2=y as Point of TOP-REAL 2;
           A147:dist(x,y)=|.pw1-pw2.| by JGRAPH_1:45;
           A148:|.pw1-pw2.|<=abs(pw1`1-pw2`1)+abs(pw1`2-pw2`2) by JGRAPH_1:49;
           A149:W-bound Qa <= pw1`1 & pw1`1 <= E-bound Qa &
                 S-bound Qa <= pw1`2 & pw1`2 <= N-bound Qa
                              by A137,A146,PSCOMP_1:71;
           A150:W-bound Qa <= pw2`1 & pw2`1 <= E-bound Qa &
                 S-bound Qa <= pw2`2 & pw2`2 <= N-bound Qa
                              by A137,A146,PSCOMP_1:71;
           then A151:abs(pw1`1-pw2`1)<=(E-bound Qa) - (W-bound Qa)
                           by A149,JGRAPH_1:31;
           abs(pw1`2-pw2`2)<=(N-bound Qa) - (S-bound Qa)
                          by A149,A150,JGRAPH_1:31;
           then A152:abs(pw1`1-pw2`1)+abs(pw1`2-pw2`2)
           <=((E-bound Qa) - (W-bound Qa))+((N-bound Qa) - (S-bound Qa))
                         by A151,XREAL_1:9;
               ((E-bound Qa) - (W-bound Qa))+((N-bound Qa) - (S-bound Qa))
           <=
           ((E-bound Qa) - (W-bound Qa))+((N-bound Qa) - (S-bound Qa))
            +1 by XREAL_1:31;
           then abs(pw1`1-pw2`1)+abs(pw1`2-pw2`2)<=r1 by A152,XXREAL_0:2;
          hence thesis by A147,A148,XXREAL_0:2;
         end;
        then W1 is bounded by A144,A145,TBSP_1:def 9;
        then A153:W is bounded by A141,TBSP_1:21;
        A154: diameter(W)<e by A67,A71,A118,A119;
        dist(w1,w2)<=diameter(W) by A139,A153,TBSP_1:def 10;
        then dist(w1,w2)<e by A154,XXREAL_0:2;
       hence thesis by A123,A125,JGRAPH_1:45;
      end;
     consider f2 being FinSequence of TOP-REAL 2 such that
     A155: f2 is special &
     f2.1=h1.1 & f2.len f2=h1.len h1 & len f2>=len h1 &
     X_axis(f2) lies_between (X_axis(h1)).1, (X_axis(h1)).(len h1) &
     Y_axis(f2) lies_between q1`2,q2`2 &
     (for j st j in dom f2 holds
     ex k being Nat st k in dom h1 & |.(f2/.j - h1/.k).|<e)
     &(for j st 1<=j & j+1<=len f2 holds |.f2/.j - f2/.(j+1).|<e)
                                  by A19,A27,A66,A100,JGRAPH_1:56;
     A156:len f2>=1 by A66,A155,XXREAL_0:2;
     consider g2 being FinSequence of TOP-REAL 2 such that
     A157: g2 is special &
     g2.1=h2.1 & g2.len g2=h2.len h2 & len g2>=len h2 &
     Y_axis(g2) lies_between (Y_axis(h2)).1, (Y_axis(h2)).(len h2) &
     X_axis(g2) lies_between p1`1,p2`1 &
     (for j st j in dom g2 holds
     ex k being Nat st k in dom h2 & |.((g2/.j) - h2/.k).|<e)&
     (for j st 1<=j & j+1<=len g2 holds |.(g2/.j) - g2/.(j+1).|<e)
               by A19,A80,A116,A117,JGRAPH_1:57;
     A158:len g2>=1 by A80,A157,XXREAL_0:2;
     A159:1 in dom h by A21,A66,FINSEQ_3:27;
     A160:len h in dom h by A21,A66,FINSEQ_3:27;
     A161: h1.1=f.(h.1) by A21,A26,A159;
     A162:h1.1=h1/.1 by A66,FINSEQ_4:24;
     A163:h1.len h1=h1/.len h1 by A66,FINSEQ_4:24;
     A164:f2.1=f2/.1 by A156,FINSEQ_4:24;
     A165:f2.len f2=f2/.len f2 by A156,FINSEQ_4:24;
     A166:X_axis(f2).1=p1`1
     by A7,A20,A155,A156,A161,A164,JGRAPH_1:59;
     (h1/.len h1)=p2 by A7,A20,A21,A26,A160,A163;
     then A167:X_axis(f2).len f2=p2`1 by A155,A156,A163,A165,JGRAPH_1:59;
     A168:(h2/.1)=q1 by A8,A67,A69,A77;
     A169:h2.1=h2/.1 by A68,A71,FINSEQ_4:24;
     A170:h2.len h2=h2/.len h2 by A68,A71,FINSEQ_4:24;
     g2/.1=g2.1 by A158,FINSEQ_4:24;
     then A171:Y_axis(g2).1=q1`2 by A157,A158,A168,A169,JGRAPH_1:60;
     A172:g2/.len g2=g2.len g2 by A158,FINSEQ_4:24;
     (g2.len g2)=q2 by A8,A67,A70,A71,A77,A157,A170;
     then A173:Y_axis(g2).len g2=q2`2 by A158,A172,JGRAPH_1:60;
     A174:(X_axis f2).1=(h1/.1)`1 by A155,A156,A162,A164,JGRAPH_1:59
     .=(X_axis h1).1 by A66,JGRAPH_1:59;
     A175:X_axis(f2).len f2=(h1/.len h1)`1 by A155,A156,A163,A165,JGRAPH_1:59
     .=X_axis(h1).len h1 by A66,JGRAPH_1:59;
     g2.1=g2/.1 by A158,FINSEQ_4:24;
     then A176:(Y_axis(g2)).1=(h2/.1)`2 by A157,A158,A169,JGRAPH_1:60
     .=(Y_axis(h2)).1 by A80,JGRAPH_1:60;
         g2.len g2=g2/.len g2 by A158,FINSEQ_4:24;
     then A177: Y_axis(g2).len g2=(h2/.len h2)`2 by A157,A158,A170,JGRAPH_1:60
     .=Y_axis(h2).len h2 by A80,JGRAPH_1:60;
     5<=len f2 by A20,A21,A155,XXREAL_0:2;
     then A178:2<=len f2 by XXREAL_0:2;
     5<=len g2 by A67,A71,A157,XXREAL_0:2;
     then A179:2<=len g2 by XXREAL_0:2;
     A180:1<=len h by A20,XXREAL_0:2;
     then 1 in dom h by FINSEQ_3:27;
     then A181:h1/.1=f.(h.1) by A21,A26,A162;
     A182: len h in dom h by A180,FINSEQ_3:27;
    now per cases;
    case A183: p1=p2;
      A184: for q being Point of TOP-REAL 2 st q in Q holds q`1=p1`1
       proof let q be Point of TOP-REAL 2;
        assume q in Q;then p1`1<=q`1 & q`1<= p2`1 by A4;
        hence q`1=p1`1 by A183,XXREAL_0:1;
       end;
       0 in the carrier of I[01] by BORSUK_1:86;then
       0 in dom g by FUNCT_2:def 1;then
       A185: q1 in Q by A2,A8,FUNCT_1:12;
       1 in the carrier of I[01] by BORSUK_1:86;then
       1 in dom g by FUNCT_2:def 1;then
       A186: q2 in Q by A2,A8,FUNCT_1:12;
       0 in the carrier of I[01] by BORSUK_1:86;then
       0 in dom f by FUNCT_2:def 1;then
A187:  p1 in P by A7,A1,FUNCT_1:12;
      A188: now assume A189: q1`2=q2`2;
         q1`2<=p1`2 & p1`2<=q2`2 by A5,A187;then
         A190: p1`2=q1`2 by A189,XXREAL_0:1;
         q1`1=p1`1 by A184,A185;then
         q1=p1 by A190,TOPREAL3:11;
        hence contradiction by A17,A185,A187,XBOOLE_0:def 3;
      end;
      A191: Q c= LSeg(q1,q2)
       proof let z be set;assume
         A192: z in Q;then
         reconsider qz=z as Point of TOP-REAL 2;
A193:      p1`1<=qz`1 & qz`1<= p2`1 by A192,A4;
         p1`1<=q1`1 & q1`1<= p2`1 by A185,A4;then
         A194: p1`1=q1`1 by XXREAL_0:1,A183;
         p1`1<=q2`1 & q2`1<= p2`1 by A186,A4;then
         A195: p1`1=q2`1 by XXREAL_0:1,A183;
         A196: q1`2<=qz`2 & qz`2<=q2`2 by A192,A6;then
         q2`2>=q1`2 by XXREAL_0:2;then
         q2`2>q1`2 by A188,REAL_1:def 5;then
         A197: q2`2-q1`2>0 by XREAL_1:52;
         A198: qz`2-q1`2>=0 by XREAL_1:50,A196;
         set ln=(qz`2-q1`2)/(q2`2-q1`2);
         A199: ln>=0 by A197,A198,REAL_2:125;
         qz`2-q1`2<=q2`2-q1`2 by A196,REAL_1:92;then
         ln<= (q2`2-q1`2)/(q2`2-q1`2) by A197,REAL_1:73;then
         A200: ln <= 1 by A197,XCMPLX_1:60;
         A201: q2`2-q1`2 <>0 by A188;
         1-ln=(q2`2-q1`2)/(q2`2-q1`2)-(qz`2-q1`2)/(q2`2-q1`2)
                               by XCMPLX_1:60,A201
            .=((q2`2-q1`2)-(qz`2-q1`2))/(q2`2-q1`2) by XCMPLX_1:121
            .=(q2`2-qz`2)/(q2`2-q1`2);then
         A202: (1-ln)*q1`2=(q2`2-qz`2)*q1`2/(q2`2-q1`2) by XCMPLX_1:75
                   .=(q2`2*q1`2-qz`2*q1`2)/(q2`2-q1`2);
         A203: ln*q2`2=(qz`2-q1`2)*q2`2/(q2`2-q1`2) by XCMPLX_1:75
               .=(qz`2*q2`2-q1`2*q2`2)/(q2`2-q1`2);
         A204: ((1-ln)*q1+ln*q2)`1= ((1-ln)*q1)`1+(ln*q2)`1 by TOPREAL3:7
                           .=(1-ln)*q1`1+(ln*q2)`1 by TOPREAL3:9
                           .=(1-ln)*p1`1+(ln)*p1`1 by A194,A195,TOPREAL3:9
                           .=qz`1 by A193,XXREAL_0:1,A183;
          ((1-ln)*q1+ln*q2)`2=((1-ln)*q1)`2+(ln*q2)`2 by TOPREAL3:7
                           .=(1-ln)*q1`2+(ln*q2)`2 by TOPREAL3:9
                           .=(1-ln)*q1`2+ln*q2`2 by TOPREAL3:9
                           .=((q2`2*q1`2-qz`2*q1`2)+(qz`2*q2`2-q1`2*q2`2))/
                              (q2`2-q1`2) by XCMPLX_1:63,A202,A203
                           .=qz`2*(q2`2-q1`2)/(q2`2-q1`2)
                           .=qz`2*((q2`2-q1`2)/(q2`2-q1`2)) by XCMPLX_1:75
                           .=qz`2*1 by XCMPLX_1:60,A201
                           .=qz`2;then
          (1-ln)*q1+ln*q2=qz by A204,TOPREAL3:11;then
          z in { (1-lambda)*q1 + lambda*q2 where lambda is Real
                         : 0 <= lambda & lambda <= 1 } by A199,A200;
        hence z in LSeg(q1,q2) by TOPREAL1:def 3;
       end;
      A205: p1 in LSeg(q1,q2)
       proof
         0 in the carrier of I[01] by BORSUK_1:86;then
         0 in dom g by FUNCT_2:def 1;then
         q1 in Q by A2,A8,FUNCT_1:12;then
         p1`1<=q1`1 & q1`1<= p2`1 by A4;then
         A206: p1`1=q1`1 by XXREAL_0:1,A183;
         1 in the carrier of I[01] by BORSUK_1:86;then
         1 in dom g by FUNCT_2:def 1;then
         g.1 in rng g by FUNCT_1:12;then
          p1`1<=q2`1 & q2`1<= p2`1 by A2,A8,A4;then
         A207: p1`1=q2`1 by XXREAL_0:1,A183;
         0 in the carrier of I[01] by BORSUK_1:86;then
         0 in dom f by FUNCT_2:def 1;then
         f.0 in rng f by FUNCT_1:12;then
         A208: q1`2<=p1`2 & p1`2<=q2`2 by A7,A1,A5;then
         q2`2>=q1`2 by XXREAL_0:2;then
         q2`2>q1`2 by A188,REAL_1:def 5;then
         A209: q2`2-q1`2>0 by XREAL_1:52;
         A210: p1`2-q1`2>=0 by XREAL_1:50,A208;
         set ln=(p1`2-q1`2)/(q2`2-q1`2);
         A211: ln>=0 by A209,A210,REAL_2:125;
         p1`2-q1`2<=q2`2-q1`2 by A208,REAL_1:92;then
         ln<= (q2`2-q1`2)/(q2`2-q1`2) by A209,REAL_1:73;then
         A212: ln <= 1 by A209,XCMPLX_1:60;
         A213: q2`2-q1`2 <>0 by A188;
         1-ln=(q2`2-q1`2)/(q2`2-q1`2)-(p1`2-q1`2)/(q2`2-q1`2)
                               by XCMPLX_1:60,A213
            .=((q2`2-q1`2)-(p1`2-q1`2))/(q2`2-q1`2) by XCMPLX_1:121
            .=(q2`2-p1`2)/(q2`2-q1`2);then
         A214: (1-ln)*q1`2=(q2`2-p1`2)*q1`2/(q2`2-q1`2) by XCMPLX_1:75
                   .=(q2`2*q1`2-p1`2*q1`2)/(q2`2-q1`2);
         A215: ln*q2`2=(p1`2-q1`2)*q2`2/(q2`2-q1`2) by XCMPLX_1:75
               .=(p1`2*q2`2-q1`2*q2`2)/(q2`2-q1`2);
         A216: ((1-ln)*q1+ln*q2)`1= ((1-ln)*q1)`1+(ln*q2)`1 by TOPREAL3:7
                           .=(1-ln)*q1`1+(ln*q2)`1 by TOPREAL3:9
                           .=(1-ln)*p1`1+(ln)*p1`1 by A206,A207,TOPREAL3:9
                           .=p1`1;
          ((1-ln)*q1+ln*q2)`2=((1-ln)*q1)`2+(ln*q2)`2 by TOPREAL3:7
                           .=(1-ln)*q1`2+(ln*q2)`2 by TOPREAL3:9
                           .=(1-ln)*q1`2+ln*q2`2 by TOPREAL3:9
                           .=((q2`2*q1`2-p1`2*q1`2)+(p1`2*q2`2-q1`2*q2`2))/
                              (q2`2-q1`2) by XCMPLX_1:63,A214,A215
                           .=p1`2*(q2`2-q1`2)/(q2`2-q1`2)
                           .=p1`2*((q2`2-q1`2)/(q2`2-q1`2)) by XCMPLX_1:75
                           .=p1`2*1 by XCMPLX_1:60,A213
                           .=p1`2;then
         (1-ln)*q1+ln*q2=p1 by A216,TOPREAL3:11;then
         p1 in { (1-lambda)*q1 + lambda*q2 where lambda is Real
                         : 0 <= lambda & lambda <= 1 } by A211,A212;
        hence p1 in LSeg(q1,q2) by TOPREAL1:def 3;
       end;
      p1 in Q by A2,A205,A191,Th3;
     hence contradiction by A14,A17,XBOOLE_0:def 3;
    end;
    case A217:p1<>p2;
    A218: now assume A219: q1=q2;
      A220: for p being Point of TOP-REAL 2 st p in P holds p`2=q1`2
       proof let p be Point of TOP-REAL 2;
        assume p in P;then q1`2<=p`2 & p`2<= q2`2 by A5;
        hence p`2=q1`2 by A219,XXREAL_0:1;
       end;
       0 in the carrier of I[01] by BORSUK_1:86;then
       0 in dom f by FUNCT_2:def 1;then
       A221: p1 in P by A7,A1,FUNCT_1:12;
       1 in the carrier of I[01] by BORSUK_1:86;then
       1 in dom f by FUNCT_2:def 1;then
       A222: p2 in P by A7,A1,FUNCT_1:12;
       0 in the carrier of I[01] by BORSUK_1:86;then
       0 in dom g by FUNCT_2:def 1;then
       A223: q1 in Q by A2,A8,FUNCT_1:12;
A224:   now assume A225: p1`1=p2`1;
         p1`1<=q1`1 & q1`1<=p2`1 by A4,A223;then
         A226: q1`1=p1`1 by A225,XXREAL_0:1;
         p1`2=q1`2 by A220,A221;then
         p1=q1 by A226,TOPREAL3:11;
        hence contradiction by A17,A221,A223,XBOOLE_0:def 3;
      end;
      A227: P c= LSeg(p1,p2)
       proof let z be set;assume
         A228: z in P;then
         reconsider pz=z as Point of TOP-REAL 2;
A229:      q1`2<=pz`2 & pz`2<= q2`2 by A228,A5;
         q1`2<=p1`2 & p1`2<= q2`2 by A221,A5;then
         A230: q1`2=p1`2 by XXREAL_0:1,A219;
          q1`2<=p2`2 & p2`2<= q2`2 by A222,A5;then
         A231: q1`2=p2`2 by XXREAL_0:1,A219;
         A232: p1`1<=pz`1 & pz`1<=p2`1 by A228,A3;then
         p2`1>=p1`1 by XXREAL_0:2;then
         p2`1>p1`1 by A224,REAL_1:def 5;then
         A233: p2`1-p1`1>0 by XREAL_1:52;
         A234: pz`1-p1`1>=0 by XREAL_1:50,A232;
         set ln=(pz`1-p1`1)/(p2`1-p1`1);
         A235: ln>=0 by A233,A234,REAL_2:125;
         pz`1-p1`1<=p2`1-p1`1 by A232,REAL_1:92;then
         ln<= (p2`1-p1`1)/(p2`1-p1`1) by A233,REAL_1:73;then
         A236: ln <= 1 by A233,XCMPLX_1:60;
         A237: p2`1-p1`1 <>0 by A224;
         1-ln=(p2`1-p1`1)/(p2`1-p1`1)-(pz`1-p1`1)/(p2`1-p1`1)
                               by XCMPLX_1:60,A237
            .=((p2`1-p1`1)-(pz`1-p1`1))/(p2`1-p1`1) by XCMPLX_1:121
            .=(p2`1-pz`1)/(p2`1-p1`1);then
         A238: (1-ln)*p1`1=(p2`1-pz`1)*p1`1/(p2`1-p1`1) by XCMPLX_1:75
                   .=(p2`1*p1`1-pz`1*p1`1)/(p2`1-p1`1);
         A239: ln*p2`1=(pz`1-p1`1)*p2`1/(p2`1-p1`1) by XCMPLX_1:75
               .=(pz`1*p2`1-p1`1*p2`1)/(p2`1-p1`1);
         A240: ((1-ln)*p1+ln*p2)`2= ((1-ln)*p1)`2+(ln*p2)`2 by TOPREAL3:7
                           .=(1-ln)*p1`2+(ln*p2)`2 by TOPREAL3:9
                           .=(1-ln)*q1`2+(ln)*q1`2 by A230,A231,TOPREAL3:9
                           .=pz`2 by A229,XXREAL_0:1,A219;
          ((1-ln)*p1+ln*p2)`1=((1-ln)*p1)`1+(ln*p2)`1 by TOPREAL3:7
                           .=(1-ln)*p1`1+(ln*p2)`1 by TOPREAL3:9
                           .=(1-ln)*p1`1+ln*p2`1 by TOPREAL3:9
                           .=((p2`1*p1`1-pz`1*p1`1)+(pz`1*p2`1-p1`1*p2`1))/
                              (p2`1-p1`1) by XCMPLX_1:63,A238,A239
                           .=pz`1*(p2`1-p1`1)/(p2`1-p1`1)
                           .=pz`1*((p2`1-p1`1)/(p2`1-p1`1)) by XCMPLX_1:75
                           .=pz`1*1 by XCMPLX_1:60,A237
                           .=pz`1;then
          (1-ln)*p1+ln*p2=pz by A240,TOPREAL3:11;then
         z in { (1-lambda)*p1 + lambda*p2 where lambda is Real
                         : 0 <= lambda & lambda <= 1 } by A235,A236;
        hence z in LSeg(p1,p2) by TOPREAL1:def 3;
       end;
      A241: q1 in LSeg(p1,p2)
       proof
         0 in the carrier of I[01] by BORSUK_1:86;then
         0 in dom f by FUNCT_2:def 1;then
         f.0 in rng f by FUNCT_1:12;then
         q1`2<=p1`2 & p1`2<= q2`2 by A7,A1,A5;then
         A242: q1`2=p1`2 by XXREAL_0:1,A219;
         1 in the carrier of I[01] by BORSUK_1:86;then
         1 in dom f by FUNCT_2:def 1;then
         f.1 in rng f by FUNCT_1:12;then
         q1`2<=p2`2 & p2`2<= q2`2 by A7,A1,A5;then
         A243: q1`2=p2`2 by XXREAL_0:1,A219;
         0 in the carrier of I[01] by BORSUK_1:86;then
         0 in dom g by FUNCT_2:def 1;then
         g.0 in rng g by FUNCT_1:12;then
         A244: p1`1<=q1`1 & q1`1<=p2`1 by A2,A8,A4;then
         p2`1>=p1`1 by XXREAL_0:2;then
         p2`1>p1`1 by A224,REAL_1:def 5;then
         A245: p2`1-p1`1>0 by XREAL_1:52;
         A246: q1`1-p1`1>=0 by XREAL_1:50,A244;
         set ln=(q1`1-p1`1)/(p2`1-p1`1);
         A247: ln>=0 by A245,A246,REAL_2:125;
         q1`1-p1`1<=p2`1-p1`1 by A244,REAL_1:92;then
         ln<= (p2`1-p1`1)/(p2`1-p1`1) by A245,REAL_1:73;then
         A248: ln <= 1 by A245,XCMPLX_1:60;
         A249: p2`1-p1`1 <>0 by A224;
         1-ln=(p2`1-p1`1)/(p2`1-p1`1)-(q1`1-p1`1)/(p2`1-p1`1)
                               by XCMPLX_1:60,A249
            .=((p2`1-p1`1)-(q1`1-p1`1))/(p2`1-p1`1) by XCMPLX_1:121
            .=(p2`1-q1`1)/(p2`1-p1`1);then
         A250: (1-ln)*p1`1=(p2`1-q1`1)*p1`1/(p2`1-p1`1) by XCMPLX_1:75
                   .=(p2`1*p1`1-q1`1*p1`1)/(p2`1-p1`1);
         A251: ln*p2`1=(q1`1-p1`1)*p2`1/(p2`1-p1`1) by XCMPLX_1:75
               .=(q1`1*p2`1-p1`1*p2`1)/(p2`1-p1`1);
         A252: ((1-ln)*p1+ln*p2)`2= ((1-ln)*p1)`2+(ln*p2)`2 by TOPREAL3:7
                           .=(1-ln)*p1`2+(ln*p2)`2 by TOPREAL3:9
                           .=(1-ln)*q1`2+(ln)*q1`2 by A242,A243,TOPREAL3:9;
         ((1-ln)*p1+ln*p2)`1=((1-ln)*p1)`1+(ln*p2)`1 by TOPREAL3:7
                           .=(1-ln)*p1`1+(ln*p2)`1 by TOPREAL3:9
                           .=(1-ln)*p1`1+ln*p2`1 by TOPREAL3:9
                           .=((p2`1*p1`1-q1`1*p1`1)+(q1`1*p2`1-p1`1*p2`1))/
                              (p2`1-p1`1) by XCMPLX_1:63,A250,A251
                           .=q1`1*(p2`1-p1`1)/(p2`1-p1`1)
                           .=q1`1*((p2`1-p1`1)/(p2`1-p1`1)) by XCMPLX_1:75
                           .=q1`1*1 by XCMPLX_1:60,A249;then
         (1-ln)*p1+ln*p2=q1 by A252,TOPREAL3:11;then
         q1 in { (1-lambda)*p1 + lambda*p2 where lambda is Real
                         : 0 <= lambda & lambda <= 1 } by A247,A248;
        hence q1 in LSeg(p1,p2) by TOPREAL1:def 3;
       end;
      q1 in P by A1,A241,A227,Th3;
     hence contradiction by A17,A15,XBOOLE_0:def 3;
    end;
A253: f2/.1<>f2/.len f2 by A7,A217,A20,A21,A26,A155,A162,A164,A165
,A181,A182;
     A254:1<=len hb by A67,XXREAL_0:2;
     then 1 in dom hb by FINSEQ_3:27;
     then A255:h2/.1=g.(hb.1) by A77;
     len hb in dom hb by A254,FINSEQ_3:27;
     then g2.1<>g2.len g2
     by A8,A218,A67,A71,A77,A157,A169,A170,A255;
     then L~f2 meets L~g2 by A155,A157,A164,A165,A166,A167,A171,A173,A174,A175
,A176,A177,A178,A179,A253,JGRAPH_1:30;
     then consider s being set such that
     A256:s in L~f2 & s in L~g2 by XBOOLE_0:3;
     reconsider ps=s as Point of TOP-REAL 2 by A256;
     ps in union{ LSeg(f2,i) : 1 <= i & i+1 <= len f2 }
          by A256,TOPREAL1:def 6;
     then consider x such that
     A257: ps in x & x in
     { LSeg(f2,i) : 1 <= i & i+1 <= len f2 } by TARSKI:def 4;
     consider i such that
     A258: x=LSeg(f2,i) &(1 <= i & i+1 <= len f2) by A257;
     A259:LSeg(f2,i)=LSeg(f2/.i,f2/.(i+1)) by A258,TOPREAL1:def 5;
     i<len f2 by A258,NAT_1:38;
     then i in dom f2 by A258,FINSEQ_3:27;
     then consider k being Nat such that
     A260: k in dom h1 & |.f2/.i - h1/.k.|<e by A155;
     A261:|.ps-f2/.i.|<=|.f2/.i-f2/.(i+1).| by A257,A258,A259,JGRAPH_1:53;
     |.f2/.i-f2/.(i+1).|<e by A155,A258;
     then A262:|.ps-f2/.i.|<e by A261,XXREAL_0:2;
     A263:|.ps-h1/.k.|<=|.ps-f2/.i.|+|.f2/.i-h1/.k.| by TOPRNS_1:35;
     |.ps-f2/.i.|+|.f2/.i-h1/.k.|<e+e by A260,A262,XREAL_1:10;
     then A264:|.ps-h1/.k.|<e+e by A263,XXREAL_0:2;
     A265:k in Seg len h1 by A260,FINSEQ_1:def 3;
     then A266:k in dom h by A21,FINSEQ_1:def 3;
     1<=k & k<=len h1 by A265,FINSEQ_1:3;
     then h1.k=h1/.k by FINSEQ_4:24;
     then A267:h1/.k=f.(h.k) by A21,A265;
     A268:h.k in rng h by A266,FUNCT_1:def 5;
     dom f=the carrier of I[01] by FUNCT_2:def 1;
     then A269: h1/.k in P by A1,A20,A267,A268,FUNCT_1:def 5;
     reconsider p11=h1/.k as Point of TOP-REAL 2;
     A270: |.(p11-ps).|<e+e by A264,TOPRNS_1:28;
     ps in union{ LSeg(g2,j) : 1 <= j & j+1 <= len g2 }
       by A256,TOPREAL1:def 6;
     then consider y such that
     A271: ps in y &
     y in { LSeg(g2,j) : 1 <= j & j+1 <= len g2 } by TARSKI:def 4;
     consider j such that
     A272:y=LSeg(g2,j) &(1 <= j & j+1 <= len g2) by A271;
     A273: LSeg(g2,j)=LSeg((g2/.j),g2/.(j+1)) &
      ps in LSeg(g2,j) & y=LSeg(g2,j) &(1 <= j & j+1 <= len g2)
                             by A271,A272,TOPREAL1:def 5;
      j<len g2 & LSeg(g2,j)=LSeg((g2/.j),g2/.(j+1)) &
      ps in LSeg(g2,j) & y=LSeg(g2,j) & (1 <= j & j+1 <= len g2)
        by A271,A272,NAT_1:38,TOPREAL1:def 5;
     then j in dom g2 by FINSEQ_3:27;
     then consider k' being Nat such that
     A274: k' in dom h2 & |.((g2/.j) - h2/.k').|<e by A157;
     A275:|.ps-(g2/.j).|<=|.(g2/.j)-g2/.(j+1).| by A273,JGRAPH_1:53;
     |.(g2/.j)-g2/.(j+1).|<e by A157,A272;
     then A276:|.ps-(g2/.j).|<e by A275,XXREAL_0:2;
     A277:|.ps-h2/.k'.|<=|.ps-(g2/.j).|+|.(g2/.j)-h2/.k'.| by TOPRNS_1:35;
     |.ps-(g2/.j).|+|.(g2/.j)-(h2/.k').|<e+e by A274,A276,XREAL_1:10;
     then A278:|.ps-(h2/.k').|<e+e by A277,XXREAL_0:2;
     k' in Seg len h2 by A274,FINSEQ_1:def 3;
     then k' in dom hb by A71,FINSEQ_1:def 3;
     then A279:hb.k' in rng hb &
     h2/.k'=g.(hb.k') & k' in dom hb by A77,FUNCT_1:def 5;
     dom g =the carrier of I[01] by FUNCT_2:def 1;
     then A280:Q<>{}(TOP-REAL 2) &
     h2/.k' in rng g & hb.k' in dom g by A67,A279,FUNCT_1:def 5;
     reconsider q11=h2/.k' as Point of TOP-REAL 2;
     reconsider x1=p11,x2=q11 as Point of Euclid 2 by EUCLID:25;
     min_dist_min(P',Q')<=dist(x1,x2) by A2,A16,A269,A280,WEIERSTR:40;
     then A281:|.(p11-q11).|<= |.(p11-ps).|+|.(ps-q11).|
       & min_dist_min(P',Q')<=|.(p11-q11).| by JGRAPH_1:45,TOPRNS_1:35;
     |.(p11-ps).|+|.(ps-q11).| <e+e+(e+e) by A270,A278,XREAL_1:10;
     then |.(p11-q11).|<e+e+e+e by A281,XXREAL_0:2;
     then min_dist_min(P',Q')<4*e by A281,XXREAL_0:2;
     then 4*e-5*e>0 by XREAL_1:52; then (4-5)*e/e>0 by A19,REAL_2:127;
     then 4-5>0 by A19,XCMPLX_1:90;
    hence contradiction;
     end;
     end;
    hence contradiction;
end;

:: Fashoda Meet Theorem
theorem Th5:
  for f,g being continuous Function of I[01],TOP-REAL 2,
  O,I being Point of I[01] st O=0 & I=1 &
  (f.O)`1=a & (f.I)`1=b &
  (g.O)`2=c & (g.I)`2=d &
  (for r being Point of I[01] holds
    a <=(f.r)`1 & (f.r)`1<=b & a <=(g.r)`1 & (g.r)`1<=b &
    c <=(f.r)`2 & (f.r)`2<=d & c <=(g.r)`2 & (g.r)`2<=d)
   holds rng f meets rng g
   proof
     let f,g be continuous Function of I[01],TOP-REAL 2,
         O,I be Point of I[01];
   assume A1: O=0 & I=1 &
     (f.O)`1=a & (f.I)`1=b & (g.O)`2=c & (g.I)`2=d &
     (for r being Point of I[01] holds
      a<=(f.r)`1 & (f.r)`1<=b & a<=(g.r)`1 & (g.r)`1<=b &
      c <=(f.r)`2 & (f.r)`2<=d & c <=(g.r)`2 & (g.r)`2<=d);
      A2:dom f= the carrier of I[01] by FUNCT_2:def 1
      .=[#](I[01]) by PRE_TOPC:12;
      A3:[#](I[01]) =[.0,1.] by BORSUK_1:83,PRE_TOPC:12;
      then 0 in dom f by A2,RCOMP_1:48;
     then reconsider P=rng f as non empty Subset of TOP-REAL 2
                        by FUNCT_1:12;
          dom g= the carrier of I[01] by FUNCT_2:def 1
      .=[#](I[01]) by PRE_TOPC:12;
      then 0 in dom g by A3,RCOMP_1:48;
     then reconsider Q=rng g as non empty Subset of TOP-REAL 2
                       by FUNCT_1:12;
     A4: dom f=the carrier of I[01] by FUNCT_2:def 1;
     A5: dom g=the carrier of I[01] by FUNCT_2:def 1;
     the carrier of ((TOP-REAL 2)|P)=[#]((TOP-REAL 2)|P) by PRE_TOPC:def 3
                                   .=rng f by PRE_TOPC:def 10;then
     reconsider f1=f as Function of I[01],((TOP-REAL 2)|P) by FUNCT_2:3,A4;
     the carrier of ((TOP-REAL 2)|Q)=[#]((TOP-REAL 2)|Q) by PRE_TOPC:def 3
                                   .=rng g by PRE_TOPC:def 10;then
     reconsider g1=g as Function of I[01],((TOP-REAL 2)|Q) by FUNCT_2:3,A5;
     reconsider p1=f1.O as Point of TOP-REAL 2 by A1;
     reconsider p2=f1.I as Point of TOP-REAL 2 by A1;
     reconsider q1=g1.O as Point of TOP-REAL 2 by A1;
     reconsider q2=g1.I as Point of TOP-REAL 2 by A1;
A6:  f is Path of p1,p2 & g is Path of q1,q2 by A1,BORSUK_2:def 4;
     A7:for p being Point of TOP-REAL 2 st p in P holds p1`1<=p`1 & p`1<= p2`1
      proof let p be Point of TOP-REAL 2;
        assume p in P; then ex x st x in dom f1 & p=f1.x by FUNCT_1:def 5;
        hence thesis by A1;
      end;
A8:  for p being Point of TOP-REAL 2 st p in Q holds p1`1<=p`1 & p`1<= p2`1
      proof let p be Point of TOP-REAL 2;
        assume p in Q; then ex x st x in dom g1 & p=g1.x by FUNCT_1:def 5;
        hence thesis by A1;
      end;
A9:  for p being Point of TOP-REAL 2 st p in P holds q1`2<=p`2 & p`2<= q2`2
      proof let p be Point of TOP-REAL 2;
        assume p in P; then ex x st x in dom f1 & p=f1.x by FUNCT_1:def 5;
        hence thesis by A1;
      end;
      for p being Point of TOP-REAL 2 st p in Q holds q1`2<=p`2 & p`2<= q2`2
      proof let p be Point of TOP-REAL 2;
        assume p in Q; then ex x st x in dom g1 & p=g1.x by FUNCT_1:def 5;
        hence thesis by A1;
      end;
   hence thesis by A7,A8,A9,A6,Th4;
 end;

theorem
  for ar, br, cr, dr being Point of Trectangle(a,b,c,d)
  for h being Path of ar,br, v being Path of dr,cr
  for Ar, Br, Cr, Dr being Point of TOP-REAL 2
  st Ar`1 = a & Br`1 = b & Cr`2 = c & Dr`2 = d &
  ar = Ar & br = Br & cr = Cr & dr = Dr
  ex s, t being Point of I[01] st h.s = v.t
  proof
    let ar, br, cr, dr be Point of Trectangle(a,b,c,d);
    let h be Path of ar,br;
    let v be Path of dr,cr;
    let Ar, Br, Cr, Dr be Point of TOP-REAL 2 such that
A1: Ar`1 = a & Br`1 = b & Cr`2 = c & Dr`2 = d and
A2: ar = Ar & br = Br & cr = Cr & dr = Dr;
    set TR = Trectangle(a,b,c,d);
    per cases;
    suppose TR is empty;
      then the carrier of TR is empty by STRUCT_0:def 1;
      then
A3:   h = {} & v = {} by FUNCT_2:def 1;
      take 1[01], 1[01];
A4:   not 1[01] in dom {};
      hence h.1[01] = {} by A3,FUNCT_1:def 4
      .= v.1[01] by A4,A3,FUNCT_1:def 4;
    end;
    suppose TR is non empty;
      then reconsider TR = Trectangle(a,b,c,d)
        as non empty convex SubSpace of TOP-REAL 2;
      reconsider ar, br, cr, dr as Point of TR;
      reconsider h as Path of ar,br;
      reconsider v as Path of dr,cr;
      the carrier of TR is Subset of TOP-REAL 2 by TSEP_1:1;
      then reconsider f = h, g = -v as Function
 of I[01],TOP-REAL 2 by FUNCT_2:9;
A5:   f is continuous & g is continuous by TOPMETR:7;
A6:   h.0 = ar & h.1 = br & (-v).0 = cr & (-v).1 = dr by BORSUK_2:def 4;
      for r being Point of I[01] holds
      a <= (f.r)`1 & (f.r)`1 <= b & a <= (g.r)`1 & (g.r)`1 <= b &
      c <= (f.r)`2 & (f.r)`2 <= d & c <= (g.r)`2 & (g.r)`2 <= d
      proof
        let r be Point of I[01];
A7:     the carrier of TR = closed_inside_of_rectangle(a,b,c,d)
          by JORDAN1:1
        .= {p where p is Point of TOP-REAL 2:
        a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d} by JGRAPH_6:def 2;
        h.r in the carrier of TR; then
A8:     ex p being Point of TOP-REAL 2 st
        h.r = p & a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d by A7;
        (-v).r in the carrier of TR;
        then ex p being Point of TOP-REAL 2 st
        (-v).r = p & a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d by A7;
        hence thesis by A8;
      end;
      then rng f meets rng g by A1,A2,A5,A6,BORSUK_1:def 17,def 18,Th5;
      then consider y being set such that
A9:   y in rng f and
A10:   y in rng g by XBOOLE_0:3;
      consider s being set such that
A11:   s in dom f and
A12:  f.s = y by A9,FUNCT_1:def 5;
      consider t being set such that
A13:  t in dom g and
A14:  g.t = y by A10,FUNCT_1:def 5;
      reconsider s, t as Point of I[01] by A11,A13;
      reconsider t1 = 1-t as Point of I[01] by JORDAN5B:4;
      take s, t1;
A15:  dr,cr are_connected by BORSUK_2:def 3;
      h.s = g.t by A12,A14
      .= v.t1 by A15,BORSUK_2:def 6;
      hence thesis;
    end;
  end;
