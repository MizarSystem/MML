:: The {F}ashoda Meet Theorem for Continuous Mappings
::  by Yatsuka Nakamura , Andrzej Trybulec and Artur Korni{\l}owicz
::
:: Received September 14, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies FINSEQ_1, SQUARE_1, ABSVALUE, ARYTM_1, FUNCT_1, FINSEQ_4, BOOLE,
      RELAT_1, EUCLID, TOPREAL1, PRE_TOPC, TARSKI, MCART_1, GOBOARD1, GOBOARD4,
      COMPLEX1, METRIC_1, PCOMPS_1, COMPTS_1, WEIERSTR, ARYTM_3, BORSUK_1,
      ORDINAL2, RCOMP_1, TBSP_1, SUBSET_1, PSCOMP_1, LATTICES, ARYTM, UNIFORM1,
      TOPMETR, INT_1, CONNSP_1, RELAT_2, GRAPH_1, TOPREALA, JGRAPH_6, JORDAN1,
      BORSUK_6, ASYMPT_0;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0,
      COMPLEX1, REAL_1, RELAT_1, FUNCT_1, RELSET_1, FUNCT_2, FINSEQ_1, NAT_1,
      STRUCT_0, BINARITH, FINSEQ_4, TOPREAL1, PRE_TOPC, GOBOARD1, GOBOARD4,
      TOPMETR, PCOMPS_1, COMPTS_1, WEIERSTR, METRIC_1, TBSP_1, RCOMP_1,
      UNIFORM1, INT_1, CONNSP_1, SQUARE_1, PSCOMP_1, EUCLID, JGRAPH_1,
      BORSUK_1, BORSUK_2, JORDAN1, JGRAPH_6, TOPREALA, TOPALG_2;
 constructors REAL_1, BINARITH, GOBOARD5, GOBOARD4, WEIERSTR, TBSP_1, RCOMP_1,
      SQUARE_1, PSCOMP_1, FINSEQ_4, TOPRNS_1, GOBOARD1, JORDAN7, COMPLEX1,
      NAT_1, UNIFORM1, CONNSP_1, TOPREALA, JGRAPH_6, JORDAN1;
 registrations STRUCT_0, RELSET_1, EUCLID, PRE_TOPC, BORSUK_1, XREAL_0,
      FINSEQ_1, MEMBERED, BORSUK_2, INT_1, FUNCT_1, RELAT_1, TOPALG_2,
      ASYMPT_0, JORDAN5A;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, XBOOLE_0, JORDAN1, TOPALG_2;
 theorems TARSKI, FUNCT_1, FINSEQ_1, NAT_1, REAL_1, REAL_2, SPPOL_1, FINSEQ_4,
      BINARITH, RELAT_1, FINSEQ_3, TOPREAL1, GOBOARD1, GOBOARD4, TOPREAL3,
      SQUARE_1, EUCLID, UNIFORM1, WEIERSTR, JORDAN5A, PRE_TOPC, TBSP_1, HEINE,
      COMPTS_1, TOPMETR, PSCOMP_1, TOPREAL5, BORSUK_1, RCOMP_1, FUNCT_2,
      SCMFSA_7, XBOOLE_0, XBOOLE_1, TOPRNS_1, XCMPLX_1, JGRAPH_1, INT_1, CAT_3,
      XREAL_0, TREAL_1, CONNSP_1, JORDAN2C, XREAL_1, STRUCT_0, BORSUK_2,
      TSEP_1, TOPREALA, JGRAPH_6, JORDAN5B, JORDAN3, FINSEQ_2;
 schemes FINSEQ_1, NAT_1;

begin

 reserve x, y for set;
 reserve i, j, n for Nat;
 reserve p, p1, p2 for Point of TOP-REAL n;
 reserve a, b, c, d for real number;

Lm2: I[01]=TopSpaceMetr(Closed-Interval-MSpace(0,1)) by TOPMETR:27,def 8;
Lm5:for x being set,f being FinSequence st 1<=len f holds
 (f^<*x*>).1=f.1 & (<*x*>^f).(len f +1)=f.len f
proof let x be set,f be FinSequence;assume A1:1<=len f;
 then A2:1 in dom f by FINSEQ_3:27;
A3:len f in dom f by A1,FINSEQ_3:27;
 (<*x*>^f).(len f +1)=(<*x*>^f).(len <*x*>+len f) by FINSEQ_1:56
 .=f.len f by A3,FINSEQ_1:def 7;
 hence thesis by A2,FINSEQ_1:def 7;
end;

Lm7:for f being FinSequence of REAL st
 (for k being Nat st 1<=k & k<len f holds f/.k<f/.(k+1)) holds
 f is increasing
proof let f be FinSequence of REAL;
 assume A1:for k being Nat st 1<=k & k<len f holds f/.k<f/.(k+1);
  now let i,j;
    now assume A2:i in dom f & j in dom f & i<j;
    then A3:1<=i & i<=len f by FINSEQ_3:27;
    defpred P[Nat] means i+(1+$1)<=len f implies f/.i<f/.(i+(1+$1));
    A4:1<=j & j<=len f by A2,FINSEQ_3:27;
    then i<len f by A2,XREAL_1:2;
    then A5: P[0] by A1,A3;
    A6:for k being Nat st P[k] holds P[(k+1)]
    proof let k be Nat;assume
A7:  i+(1+k)<=len f implies f/.i<f/.(i+(1+k));
     now assume A8:i+(1+(k+1))<=len f;
       A9:1<=i+1 by NAT_1:29;   i+1<=i+1+k by NAT_1:29;
       then A10: 1<=i+1+k by A9,XREAL_1:2;
       1+k<1+(k+1) by NAT_1:38;
       then A11: i+(1+k)<i+(1+(k+1)) by XREAL_1:8;
       then A12:i+(1+k)<len f by A8,XREAL_1:2;
       i+(1+(k+1))=i+(1+k)+1;
       then f/.(i+(1+k))<f/.(i+(1+(k+1))) by A1,A10,A12;
      hence f/.i<f/.(i+(1+(k+1))) by A7,A8,A11,XREAL_1:2;
     end;
    hence i+(1+(k+1))<=len f implies f/.i<f/.(i+(1+(k+1)));
   end;
  A13:for k being Nat holds P[k] from NAT_1:sch 1(A5,A6);
  i+1<=j by A2,NAT_1:38;
  then j-'(i+1)=j-(i+1) by BINARITH:50;
  then i+(1+(j-'(i+1))) = j;
 then A14:f/.i<f/.j by A4,A13;
 f/.i=f.i by A2,FINSEQ_4:def 4;
 hence f.i<f.j by A2,A14,FINSEQ_4:def 4;
 end;
 hence i in dom f & j in dom f & i<j implies f.i<f.j;
 end;
hence thesis by GOBOARD1:def 1;
end;

registration
  let a, b, c, d;
  cluster closed_inside_of_rectangle(a,b,c,d) -> convex;
  coherence
  proof
    set P = closed_inside_of_rectangle(a,b,c,d);
A1: P = {p where p is Point of TOP-REAL 2:
    a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d} by JGRAPH_6:def 2;
    let w1, w2 be Point of TOP-REAL 2 such that
A2: w1 in P & w2 in P;
    let x be set such that
A3: x in LSeg(w1,w2);
A4: ex p3 being Point of TOP-REAL 2 st
    p3 = w1 & a <= p3`1 & p3`1 <= b & c <= p3`2 & p3`2 <= d by A1,A2;
A6: ex p4 being Point of TOP-REAL 2 st
    p4 = w2 & a <= p4`1 & p4`1 <= b & c <= p4`2 & p4`2 <= d by A1,A2;
    x in {(1-l)*w1 + l*w2 where l is Real: 0 <= l & l <= 1}
    by A3,TOPREAL1:def 3;
    then consider l being Real such that
A8: x = (1-l)*w1 + l*w2 & 0 <= l & l <= 1;
    set w = (1-l)*w1 + l*w2;
A9: w = |[((1-l)*w1)`1+(l*w2)`1,((1-l)*w1)`2+ (l*w2)`2]| by EUCLID:59;
    (1-l)*w1=|[ (1-l)*w1`1 ,(1-l)*w1`2 ]| & l*w2=|[ l*w2`1 ,l*w2`2 ]|
      by EUCLID:61;
    then ((1-l)*w1)`1= (1-l)*w1`1 & ((1-l)*w1)`2= (1-l)*w1`2
    & (l*w2)`1=l*w2`1 & (l*w2)`2=l*w2`2 by EUCLID:56;
    then w`1=(1-l)* w1`1+ l* w2`1 & w`2=(1-l)* w1`2+ l* w2`2 by A9,EUCLID:56;
    then a <= w`1 & w`1 <= b & c <= w`2 & w`2 <= d
      by A4,A6,A8,XREAL_1:175,176;
    hence thesis by A1,A8;
  end;
end;

registration
  let a, b, c, d;
  cluster Trectangle(a,b,c,d) -> convex;
  coherence
  proof
    set T = Trectangle(a,b,c,d);
    the carrier of T = closed_inside_of_rectangle(a,b,c,d) by TOPREALA:53;
    hence [#]T is convex Subset of TOP-REAL 2 by PRE_TOPC:12;
  end;
end;

theorem UU10:
  for e being positive (real number),
      g being continuous Function of I[01],TOP-REAL n
  ex h being FinSequence of REAL
   st h.1=0 & h.len h=1 & 5<=len h & rng h c= the carrier of I[01]
   & h is increasing
   & (for i being Nat,Q being Subset of I[01],
    W being Subset of Euclid n
    st 1<=i & i<len h & Q=[.h/.i,h/.(i+1).] & W=g.:(Q) holds diameter(W)<e)
    proof
      let e be positive (real number),
      g be continuous Function of I[01],TOP-REAL n;
      reconsider e as positive Real by XREAL_0:def 1;
     A2:dom g=the carrier of I[01] by FUNCT_2:def 1;
     consider f being Function of
      Closed-Interval-MSpace(0,1),Euclid n such that A3: f=g by UNIFORM1:11;
     A4:f is uniformly_continuous by A3,UNIFORM1:9;
     A5:e/2<e by XREAL_1:218;
     A6:e/2>0 by XREAL_1:217;
     then consider s1 being Real such that
A7:  0<s1 and
A8:  for u1,u2 being Element of
     (Closed-Interval-MSpace(0,1)) st dist(u1,u2) < s1 holds
       dist((f/.u1),(f/.u2)) < e/2 by A4,UNIFORM1:def 1;
      set s=min(s1,1/2);
      A9:0<=s by A7,SQUARE_1:39;
      A10: s<>0 by A7,SQUARE_1:38;
      then A11:0<s by A7,SQUARE_1:39;
      A12:s<=s1 by SQUARE_1:35;
     A13: for u1,u2 being Element of
     (Closed-Interval-MSpace(0,1))
      st dist(u1,u2) < s holds dist((f/.u1),(f/.u2)) < e/2
      proof let u1,u2 be Element of
     (Closed-Interval-MSpace(0,1));
      assume dist(u1,u2) < s;
       then dist(u1,u2) < s1 by A12,XREAL_1:2;
      hence dist((f/.u1),(f/.u2)) < e/2 by A8;
      end;
      s<=1/2 by SQUARE_1:35;
      then A14:2/s>=2/(1/2) by A9,A10,REAL_2:201;
      A15:s/2>0 by A9,A10,XREAL_1:217;
      2/s>0 by A9,A10,REAL_2:127;
      then reconsider j= [/ (2/s) \] as Nat by INT_1:80;
      A16: 2/s <= [/ (2/s) \] by INT_1:def 5;
      A17:2/s<=j by INT_1:def 5;
      0<j by A11,A16,REAL_2:127;
      then A18: 0+1<=j by NAT_1:38;
      then A19:1 in Seg j by FINSEQ_1:3;
      A20:4<=j by A14,A17,XREAL_1:2;
      2/s-j<=0 by A16,REAL_2:106;
      then 1+(2/s-j)<=1+0 by XREAL_1:8;
      then A21: s/2*(1+(2/s-j))<=s/2*1 by A15,XREAL_1:66;
    defpred P[Nat,set] means $2=s/2*($1-1);
    A22:for k being Nat,y1,y2 being set st
    k in Seg j &  P[k,y1]& P[k,y2] holds y1=y2;
    A23:for k being Nat st k in Seg j ex x st P[k,x];
    consider p being FinSequence such that
     A24: dom p = Seg j & for k being Nat st k in Seg j holds
     P[k,p.k] from FINSEQ_1:sch 1(A22,A23);
    A25:p.1=s/2*(1-1) by A19,A24 .=0;
    A26: Seg len p=Seg j by A24,FINSEQ_1:def 3;
    A27:len p=j by A24,FINSEQ_1:def 3;
    [/ (2/s) \] < 2/s+1 by INT_1:def 5;
    then [/ (2/s) \] -1 < 2/s+1-1 by XREAL_1:11;
    then A28:s/2*(j-1)<s/2*(2/s) by A15,XREAL_1:70;
    A29:s/2*(2/s)=(2*s)/(2*s) by XCMPLX_1:77;
    2*s<>0 by A10,XCMPLX_1:6;
    then A30:(2*s)/(2*s)=1 by XCMPLX_1:60;
    then A31: 1-s/2*(j-1)=s/2*(1+(2/s- [/ (2/s) \])) by A29;
A32: for i being Nat,r1,r2 being Real st 1<=i & i<len p & r1=p.i & r2=p.(i+1)
      holds r1<r2 & r2-r1=s/2
    proof let i be Nat,r1,r2 be Real;assume
      A33: 1<=i & i<len p & r1=p.i & r2=p.(i+1);
      then A34:i in Seg j by A26,FINSEQ_1:3;
      A35:i<i+1 by NAT_1:38;
      1<i+1 & i+1<=len p by A33,NAT_1:38;
      then A36:(i+1) in Seg j by A26,FINSEQ_1:3;
      A37:r1=s/2*(i-1) by A24,A33,A34;
      A38:r2=s/2*(i+1-1) by A24,A33,A36;
      i-1<i+1-1 by A35,XREAL_1:11;
      hence r1<r2 by A15,A37,A38,XREAL_1:70;
      r2-r1=s/2*(i)-s/2*(i-1) by A24,A33,A34,A38;
     hence thesis;
    end;
A39:for i being Nat,r1 being Real st 1<=i & i<=len p & r1=p.i
      holds r1<1
    proof let i be Nat,r1 be Real;assume
      A40: 1<=i & i<=len p & r1=p.i;
      then i in Seg j by A26,FINSEQ_1:3;
      then A41:r1=s/2*(i-1) by A24,A40;
      i-1<=j-1 by A27,A40,XREAL_1:11;
      then (s/2)*(i-1)<=(s/2)*(j-1) by A15,XREAL_1:66;
     hence thesis by A28,A29,A30,A41,XREAL_1:2;
    end;
    A42:for r1 being Real st r1=p.len p holds 1-r1<=s/2
    proof let r1 be Real;assume
A43:  r1=p.len p;
      len p in Seg j by A18,A27,FINSEQ_1:3;
     hence thesis by A21,A24,A27,A31,A43;
    end;
     rng (p^<*1*>) c= REAL
     proof let y;assume y in rng (p^<*1*>);
       then consider x such that
       A44: x in dom (p^<*1*>) & y=(p^<*1*>).x by FUNCT_1:def 5;
       A45:dom (p^<*1*>)=Seg len (p^<*1*>) by FINSEQ_1:def 3;
       reconsider nx=x as Nat by A44;
       A46:1<=nx & nx<=len (p^<*1*>) by A44,A45,FINSEQ_1:3;
       A47: len (p^<*1*>)=len p+1 by FINSEQ_2:19;
       then A48:1<=nx & nx<=len p+1 by A44,A45,FINSEQ_1:3;
       per cases;
       suppose nx<len p+1;
         then A49:nx<=len p by NAT_1:38;
         then nx in Seg j by A26,A48,FINSEQ_1:3;
         then A50: p.nx=s/2*(nx-1) by A24;
         y=p.nx by A44,A46,A49,SCMFSA_7:9;
        hence y in REAL by A50;
       end;
       suppose nx>=len p+1;
         then nx=len p+1 by A46,A47,XREAL_1:1;
         then y=1 by A44,JORDAN3:16;
        hence y in REAL;
       end;
     end;
    then reconsider h1=p^<*1*> as FinSequence of REAL by FINSEQ_1:def 4;
    A51:len h1 =len p+1 by FINSEQ_2:19;
    A52: 4+1<=len p+1 by A20,A27,XREAL_1:8;
    A53:h1.1=0 by A18,A25,A27,Lm5;
    A54:h1.len h1=1 by A51,JORDAN3:16;
    A55:rng p c=[.0,1.]
     proof let y be set;assume y in rng p;
       then consider x being set such that
       A56: x in dom p & y=p.x by FUNCT_1:def 5;
       A57:x in Seg len p by A56,FINSEQ_1:def 3;
       reconsider nx=x as Nat by A56;
       A58:1<=nx & nx<=len p by A57,FINSEQ_1:3;
       A59:p.nx=s/2*(nx-1) by A24,A56;
       then reconsider ry=p.nx as Real;
       A60:ry<1 by A39,A58;
       A61:s/2>=0 by A9,REAL_2:125;
       nx-1>=1-1 by A58,XREAL_1:11;
       then ry>=0 by A59,A61,REAL_2:121;
       then y in {rs where rs is Real:0<=rs & rs<=1} by A56,A60;
      hence y in [.0,1.] by RCOMP_1:def 1;
     end;
    A62:rng <*1*> ={1} by FINSEQ_1:55;
    1 in {r where r is Real:0<=r & r<=1};
    then A63:1 in [.0,1.] by RCOMP_1:def 1;
     {1} c= [.0,1.]
     proof let x be set;assume x in {1};
      hence x in [.0,1.] by A63,TARSKI:def 1;
     end;
    then A64:[.0,1.] \/ {1} =[.0,1.] by XBOOLE_1:12;
    rng h1 =rng p \/ {1} by A62,FINSEQ_1:44;
    then A65: rng h1 c=[.0,1.] \/ {1} by A55,XBOOLE_1:13;
    Closed-Interval-TSpace(0,1)
        =TopSpaceMetr(Closed-Interval-MSpace(0,1)) by TOPMETR:def 8;
    then A66: the carrier of I[01] =the carrier of Closed-Interval-MSpace(0,1)
                      by TOPMETR:16,27
       .= [.0,1.] by TOPMETR:14;
    A67:for i being Nat st 1<=i & i<len h1 holds h1/.i<h1/.(i+1)
     proof let i be Nat;assume
       A68: 1<=i & i<len h1;
       then A69:i<=len p by A51,NAT_1:38;
       A70:i+1<=len h1 by A68,NAT_1:38;
       A71:1<i+1 by A68,NAT_1:38;
       per cases;
       suppose A72:i<len p;
         A73:h1.i=h1/.i by A68,FINSEQ_4:24;
         A74:h1.i=p.i by A68,A72,SCMFSA_7:9;
         A75:i+1<=len p by A72,NAT_1:38;
         A76:h1.(i+1)=h1/.(i+1) by A70,A71,FINSEQ_4:24;
        h1.(i+1)=p.(i+1) by A71,A75,SCMFSA_7:9;
        hence h1/.i<h1/.(i+1) by A32,A68,A72,A73,A74,A76;
       end;
       suppose i>=len p;
        then A77:i=len p by A69,XREAL_1:1;
        A78:h1/.(i+1)=h1.(i+1) by A70,A71,FINSEQ_4:24
                  .=1 by A77,JORDAN3:16;
        h1/.i=h1.i by A68,FINSEQ_4:24.=p.i by A68,A69,SCMFSA_7:9;
        hence h1/.i<h1/.(i+1) by A39,A68,A69,A78;
       end;
     end;
    then A79:h1 is increasing by Lm7;
    A80:for i being Nat st 1<=i & i<len h1 holds h1/.(i+1)-h1/.i<=s/2
     proof let i be Nat;assume
       A81: 1<=i & i<len h1;
       then A82:i<=len p by A51,NAT_1:38;
       A83:i+1<=len h1 by A81,NAT_1:38;
       A84:1<i+1 by A81,NAT_1:38;
       per cases;
       suppose A85:i<len p;
         A86:h1.i=h1/.i by A81,FINSEQ_4:24;
         A87:h1.i=p.i by A81,A85,SCMFSA_7:9;
         A88:i+1<=len p by A85,NAT_1:38;
         A89:h1.(i+1)=h1/.(i+1) by A83,A84,FINSEQ_4:24;
        h1.(i+1)=p.(i+1) by A84,A88,SCMFSA_7:9;
        hence h1/.(i+1)-h1/.i<=s/2 by A32,A81,A85,A86,A87,A89;
       end;
       suppose i>=len p;
         then A90:i=len p by A82,XREAL_1:1;
         A91:h1/.(i+1)=h1.(i+1) by A83,A84,FINSEQ_4:24
                  .=1 by A90,JORDAN3:16;
        h1/.i=h1.i by A81,FINSEQ_4:24.=p.i by A81,A82,SCMFSA_7:9;
        hence h1/.(i+1)-h1/.i<=s/2 by A42,A90,A91;
       end;
     end;
     for i being Nat,Q being Subset of I[01],
      W being Subset of Euclid n
      st 1<=i & i<len h1 & Q=[.h1/.i,h1/.(i+1).] & W=g.:(Q) holds
      diameter(W)<e
     proof let i be Nat,Q be Subset of I[01],
       W be Subset of Euclid n;
       assume A92:1<=i & i<len h1 & Q=[.h1/.i,h1/.(i+1).] & W=g.:(Q);
        then h1/.i<h1/.(i+1) by A67;
        then A93:Q<>{} by A92,RCOMP_1:15;
        consider x1 being Element of Q;
        x1 in Q by A93;
        then A94: g.x1 in W by A2,A92,FUNCT_1:def 12;
        A95:for x,y being Point of Euclid n st
        x in W & y in W holds dist(x,y)<=e/2
         proof let x,y be Point of Euclid n;
           assume A96: x in W & y in W;
           then consider x3 being set such that
       A97:x3 in dom g & x3 in Q & x=g.x3 by A92,FUNCT_1:def 12;
           reconsider x3 as Element of
             Closed-Interval-MSpace(0,1) by A97,Lm2,TOPMETR:16;
           consider y3 being set such that
       A98:y3 in dom g & y3 in Q & y=g.y3 by A92,A96,FUNCT_1:def 12;
           reconsider y3 as Element of
             Closed-Interval-MSpace(0,1) by A98,Lm2,TOPMETR:16;
           A99:f.x3=(f/.x3) by CAT_3:def 1;
           A100:f.y3=(f/.y3) by CAT_3:def 1;
           reconsider r3=x3 as Real by A92,A97;
           reconsider s3=y3 as Real by A92,A98;
           A101:abs(r3-s3)<=h1/.(i+1)-h1/.i by A92,A97,A98,UNIFORM1:14;
           h1/.(i+1)-h1/.i<=s/2 by A80,A92;
           then abs(r3-s3)<=s/2 by A101,XREAL_1:2;
           then A102:dist(x3,y3)<=s/2 by HEINE:1;
           s/2<s by A9,A10,XREAL_1:218;
           then dist(x3,y3)<s by A102,XREAL_1:2;
          hence dist(x,y)<=e/2 by A3,A13,A97,A98,A99,A100;
         end;
        then W is bounded by A6,TBSP_1:def 9;
        then diameter(W)<=e/2 by A94,A95,TBSP_1:def 10;
      hence diameter(W)<e by A5,XREAL_1:2;
     end;
   hence thesis by A51,A52,A53,A54,A64,A65,A66,A79;
  end;

theorem AA440:
  for P being Subset of TOP-REAL n st P c= LSeg(p1,p2) & p1 in P & p2 in P
  & P is connected holds P = LSeg(p1,p2)
  proof
    let P be Subset of TOP-REAL n;
  assume A1: P c= LSeg(p1,p2) & p1 in P & p2 in P
   & P is connected;
   reconsider L=LSeg(p1,p2) as non empty Subset of TOP-REAL n by A1;
  now assume not LSeg(p1,p2) c= P;then
   consider x0 being set such that
   B1: x0 in LSeg(p1,p2) & not x0 in P by TARSKI:def 3;
   reconsider p0=x0 as Point of TOP-REAL n by B1;
b75: p1 in LSeg(p1,p2) by TOPREAL1:6;then
   B75: LSeg(p1,p0) c= L by B1,TOPREAL1:12;
b2: LSeg(p1,p0)\{p0} c= LSeg(p1,p0) by XBOOLE_1:36;
   B3: the carrier of ((TOP-REAL n)|L)=[#]((TOP-REAL n)|L) by PRE_TOPC:12
                                 .= L by PRE_TOPC:def 10;then
   reconsider R1=LSeg(p1,p0)\{p0} as Subset of (TOP-REAL n)|L
     by b2,XBOOLE_1:1,B75;
b75b: p2 in LSeg(p1,p2) by TOPREAL1:6;then
   B75b: LSeg(p0,p2) c= L by B1,TOPREAL1:12;
   LSeg(p0,p2)\{p0} c= LSeg(p0,p2) by XBOOLE_1:36;then
   reconsider R2=LSeg(p0,p2)\{p0} as Subset of (TOP-REAL n)|L
     by B3,XBOOLE_1:1,B75b;
   reconsider PX=P as Subset of (TOP-REAL n)|L by B3,A1;
   reconsider PX1=LSeg(p1,p0) as Subset of (TOP-REAL n)|L
     by b75,B3,B1,TOPREAL1:12;
   reconsider PX2=LSeg(p0,p2) as Subset of (TOP-REAL n)|L
     by b75b,B3,B1,TOPREAL1:12;
A8: (TOP-REAL n)|L is_T2 by TOPMETR:3;
   LSeg(p1,p0) is compact by SPPOL_1:28;then
   PX1 is compact by BORSUK_1:42;then
   PX1 is closed by COMPTS_1:16,A8;then
A9: Cl PX1 =PX1 by PRE_TOPC:52;
A15: PX is connected by JORDAN2C:15,A1;
A95b: R2 c= PX2 by XBOOLE_1:36;
A95: R1 c= PX1 by XBOOLE_1:36;then
   Cl R1 c= Cl PX1 by PRE_TOPC:49;then
A10: (Cl R1) /\ R2 c= PX1 /\ R2 by A9,XBOOLE_1:27;
A40: PX1 /\ PX2 ={p0} by B1,TOPREAL1:14;
   PX1 /\ R2 =PX1 /\ PX2 \ {p0} by XBOOLE_1:49
   .= {} by A40,XBOOLE_1:37;then
   (Cl R1) /\ R2={} by A10,XBOOLE_1:3;then
A11: (Cl R1) misses R2 by XBOOLE_0:def 7;
   LSeg(p0,p2) is compact by SPPOL_1:28;then
   PX2 is compact by BORSUK_1:42;then
   PX2 is closed by COMPTS_1:16,A8;then
A9: Cl PX2 =PX2 by PRE_TOPC:52;
   R2 c= PX2 by XBOOLE_1:36;then
   Cl R2 c= Cl PX2 by PRE_TOPC:49;then
A10: R1 /\ (Cl R2) c= R1 /\ PX2 by A9,XBOOLE_1:27;
A40: PX1 /\ PX2 ={p0} by B1,TOPREAL1:14;
   R1 /\ PX2 = PX1 /\ PX2 \ {p0} by XBOOLE_1:49
   .= {} by A40,XBOOLE_1:37;then
   R1/\ (Cl R2)={} by A10,XBOOLE_1:3;then
   R1 misses (Cl R2) by XBOOLE_0:def 7;then
A12: R1,R2 are_separated by A11,CONNSP_1:def 1;
A52: {p0} c= LSeg(p1,p0)
    proof let d be set;assume d in {p0};then
     d=p0 by TARSKI:def 1;
     hence d in LSeg(p1,p0) by TOPREAL1:6;
    end;
A52b: {p0} c= LSeg(p0,p2)
    proof let d be set;assume d in {p0};then
      d=p0 by TARSKI:def 1;
     hence d in LSeg(p0,p2) by TOPREAL1:6;
    end;
A66: L=LSeg(p1,p0) \/ LSeg(p0,p2) by TOPREAL1:11,B1
    .=(LSeg(p1,p0)\{p0})\/{p0} \/ LSeg(p0,p2) by A52,XBOOLE_1:45
    .=(LSeg(p1,p0)\{p0})\/ ({p0} \/ LSeg(p0,p2)) by XBOOLE_1:4
    .= R1 \/ PX2 by A52b,XBOOLE_1:12
    .= R1 \/ ((PX2 \{p0}) \/ {p0}) by XBOOLE_1:45,A52b
    .= R1 \/ {p0} \/ R2 by XBOOLE_1:4;
A13: P c= R1 \/ R2
   proof let z be set;assume B0: z in P;then
     z in R1 \/ {p0} or z in R2 by A66,A1,XBOOLE_0:def 2;then
     z in R1 or z in {p0} or z in R2 by XBOOLE_0:def 2;
    hence z in R1 \/ R2 by B0,B1,XBOOLE_0:def 2,TARSKI:def 1;
   end;
A89: now assume P c= R1;then
C3: p2 in R1 by A1;
     p2 in PX2 by TOPREAL1:6;then
     p2 in PX1 /\ PX2 by C3,A95,XBOOLE_0:def 3;
    hence contradiction by B1,A1,A40,TARSKI:def 1;
   end;
   now assume P c= R2;then
C3:  p1 in R2 by A1;
     p1 in PX1 by TOPREAL1:6;then
     p1 in PX1 /\ PX2 by C3,A95b,XBOOLE_0:def 3;
    hence contradiction by B1,A1,A40,TARSKI:def 1;
   end;
   hence contradiction by A89,A12,A13,A15,CONNSP_1:17;
 end;
 hence P=LSeg(p1,p2) by XBOOLE_0:def 10,A1;
end;

theorem AA450:
  for g being Path of p1,p2 st rng g c= LSeg(p1,p2) holds
  rng g = LSeg(p1,p2)
  proof
    let g be Path of p1,p2;
    assume
a2: rng g c= LSeg(p1,p2);
A1: p1=g.0 & p2=g.1 by BORSUK_2:def 4;
  now assume B0: not LSeg(p1,p2) c= rng g;then
    B1: ex x0 being set st
    x0 in LSeg(p1,p2) & not x0 in rng g by TARSKI:def 3;
    [#]I[01] is connected by TREAL_1:22,CONNSP_1:28;then
    B4: g.:([#]I[01]) is connected by TOPREAL5:5;
    0 in the carrier of I[01] by JORDAN5A:2;then
    B9: 0 in dom g by FUNCT_2:def 1;
    the carrier of I[01]=[#]I[01] by PRE_TOPC:def 3;then
    B2: p1 in g.:([#]I[01]) by A1,B9,FUNCT_1:def 12;
    1 in the carrier of I[01] by JORDAN5A:2;then
    B9: 1 in dom g by FUNCT_2:def 1;
    the carrier of I[01]=[#]I[01] by PRE_TOPC:def 3;then
    B3: p2 in g.:([#]I[01]) by A1,B9,FUNCT_1:def 12;
    B5: g.:([#]I[01]) c= rng g
    proof let y be set;assume y in g.:([#]I[01]);then
C1:   ex x being set st
     x in dom g & x in [#]I[01] & y=g.x by FUNCT_1:def 12;
     thus y in rng g by C1,FUNCT_1:def 5;
    end;then
    g.:([#]I[01]) c= LSeg(p1,p2) by a2,XBOOLE_1:1;
   hence contradiction by B5,B0,B2,B3,B4,AA440;
 end;
 hence rng g=LSeg(p1,p2) by a2,XBOOLE_0:def 10;
end;

:: Goboard Theorem in continuous case
theorem Th62:
  for P, Q being non empty Subset of TOP-REAL 2,
  p1, p2, q1, q2 being Point of TOP-REAL 2,
  f being Path of p1,p2, g being Path of q1,q2
  st rng f = P & rng g = Q &
  (for p being Point of TOP-REAL 2 st p in P holds p1`1<=p`1 & p`1<= p2`1) &
  (for p being Point of TOP-REAL 2 st p in Q holds p1`1<=p`1 & p`1<= p2`1) &
  (for p being Point of TOP-REAL 2 st p in P holds q1`2<=p`2 & p`2<= q2`2) &
  (for p being Point of TOP-REAL 2 st p in Q holds q1`2<=p`2 & p`2<= q2`2)
  holds P meets Q
  proof
    let P,Q be non empty Subset of TOP-REAL 2,
    p1,p2,q1,q2 be Point of TOP-REAL 2,
    f be Path of p1,p2,
    g be Path of q1,q2;
    assume that
A1: rng f = P and
a1: rng g = Q and
A2: for p being Point of TOP-REAL 2 st p in P holds p1`1<=p`1 & p`1<= p2`1
  and
A3: for p being Point of TOP-REAL 2 st p in Q holds p1`1<=p`1 & p`1<= p2`1
  and
A4: for p being Point of TOP-REAL 2 st p in P holds q1`2<=p`2 & p`2<= q2`2
  and
A5: for p being Point of TOP-REAL 2 st p in Q holds q1`2<=p`2 & p`2<= q2`2;
a2: f.0 = p1 & f.1 = p2 by BORSUK_2:def 4;
a3: g.0 = q1 & g.1 = q2 by BORSUK_2:def 4;
   A12:TOP-REAL 2=TopSpaceMetr(Euclid 2) by EUCLID:def 8;
   A13: Euclid 2= MetrStruct(#REAL 2,Pitag_dist 2#) by EUCLID:def 7;
   reconsider P'=P,Q'=Q as Subset of TopSpaceMetr(Euclid 2) by EUCLID:def 8;
   I[01] is compact by HEINE:11,TOPMETR:27;then
   A555: [#](I[01]) is compact by COMPTS_1:10;
   f.:(the carrier of I[01])=rng f by FUNCT_2:45;then
A48: f.:([#](I[01]))=rng f by PRE_TOPC:def 3;
   g.:(the carrier of I[01])=rng g by FUNCT_2:45;then
A142: g.:([#](I[01]))=rng g by PRE_TOPC:def 3;
   A121: 0 in the carrier of I[01] by JORDAN5A:2;then
   0 in dom f by FUNCT_2:def 1;then
   A122b: p1 in P' by A1,a2,FUNCT_1:def 5;
   0 in dom g by FUNCT_2:def 1,A121;then
   A122d: q1 in Q' by a1,a3,FUNCT_1:def 5;
   A14:P' <> {} & P' is compact & Q' <> {}
   & Q' is compact by a1,A48,A142,A12,A1,A555,WEIERSTR:14;
   assume A00: P /\ Q= {};
     then P misses Q by XBOOLE_0:def 7;
     then A15:min_dist_min(P',Q')>0 by A14,JGRAPH_1:55;
     set e=min_dist_min(P',Q')/5;
A16: e>0/5 by A15,REAL_1:73;
     then consider h being FinSequence of REAL such that
     A17: h.1=0 & h.len h=1 & 5<=len h & rng h c= the carrier of I[01]
     & h is increasing
     &(for i being Nat,R being Subset of I[01],
     W being Subset of Euclid 2
      st 1<=i & i<len h & R=[.h/.i,h/.(i+1).] & W=f.:(R) holds
     diameter(W)<e) by UU10;
     deffunc F(Nat)=f.(h.$1);
     ex h1' being FinSequence st len h1'=len h &
     for i st i in Seg len h holds h1'.i=F(i) from FINSEQ_1:sch 2;
     then consider h1' being FinSequence such that
     A18:len h1'=len h &
     for i st i in Seg len h holds h1'.i=f.(h.i);
     rng h1' c= the carrier of TOP-REAL 2
      proof let y;assume y in rng h1';
        then consider x such that
        A19:x in dom h1' & y=h1'.x by FUNCT_1:def 5;
        A20:dom h1'=Seg len h1' by FINSEQ_1:def 3;
        reconsider i=x as Nat by A19;
        A21: h1'.i=f.(h.i) by A18,A19,A20;
        i in dom h by A18,A19,A20,FINSEQ_1:def 3;
        then A22:h.i in rng h by FUNCT_1:def 5;
        dom f= the carrier of I[01] by FUNCT_2:def 1;
        then h1'.i in rng f by A17,A21,A22,FUNCT_1:def 5;
       hence thesis by A19;
      end;
     then reconsider h1=h1' as FinSequence of TOP-REAL 2 by FINSEQ_1:def 4;
A24: dom h=Seg len h by FINSEQ_1:def 3;
     A26:for i st 1<=i & i+1<=len h1 holds |.h1/.i-h1/.(i+1).|<e
      proof let i;assume A27:1<=i & i+1<=len h1;
        then A28:i<len h1 by NAT_1:38;
        A29:1<i+1 by A27,NAT_1:38;
        then A30:i+1 in Seg len h by A18,A27,FINSEQ_1:3;
        A31: i in Seg len h by A18,A27,A28,FINSEQ_1:3;
        then A32:h1'.i=f.(h.i) by A18;
        then A33:h1/.i=f.(h.i) by A27,A28,FINSEQ_4:24;
        A34:h1'.(i+1)=f.(h.(i+1)) by A18,A30;
        then A35:h1/.(i+1)=f.(h.(i+1)) by A27,A29,FINSEQ_4:24;
        A36:i in dom h by A18,A27,A28,FINSEQ_3:27;
        then A37:h.i in rng h by FUNCT_1:def 5;
A38:    dom f =the carrier of I[01] by FUNCT_2:def 1;
        then h1'.i in rng f by A17,A32,A37,FUNCT_1:def 5; then
A41:    f.(h.i) is Element of TOP-REAL 2 by A18,A31;
        i+1 in dom h by A18,A27,A29,FINSEQ_3:27;
        then h.(i+1) in rng h by FUNCT_1:def 5;
        then h1'.(i+1) in rng f by A17,A34,A38,FUNCT_1:def 5;
        then A42:f.(h.(i+1)) is Element of TOP-REAL 2 by A18,A30;
        A43:h.i=h/.i by A18,A27,A28,FINSEQ_4:24;
        A44:i+1 in dom h by A18,A27,A29,FINSEQ_3:27;
        then A45:h.(i+1) in rng h by FUNCT_1:def 5;
        A46:h.(i+1)=h/.(i+1) by A18,A27,A29,FINSEQ_4:24;
        i<i+1 by NAT_1:38;
        then A47:0<=h/.i & h/.i <= h/.(i+1) & h/.(i+1)<=1
         by A17,A36,A37,A43,A44,A45,A46,BORSUK_1:83,GOBOARD1:def 1
,RCOMP_1:48;
       reconsider W1=P as Subset of Euclid 2 by TOPREAL3:13;
        reconsider Pa=P as Subset of TOP-REAL 2;
         I[01] is compact by HEINE:11,TOPMETR:27;then
        A555: [#](I[01]) is compact by COMPTS_1:10;
        f.:(the carrier of I[01])=rng f by FUNCT_2:45;then
        f.:([#](I[01]))=rng f by PRE_TOPC:def 3;then
        A48:Pa is compact by A1,A555,WEIERSTR:14;
        reconsider R=[.h/.i,h/.(i+1).] as Subset of I[01]
                          by A17,A37,A43,A45,A46,BORSUK_1:83,RCOMP_1:16;
        reconsider w1=f.(h.i),w2=f.(h.(i+1)) as Point of Euclid 2
                          by A41,A42,TOPREAL3:13;
        A49: h.i in R by A43,A47,RCOMP_1:48;
        h.(i+1) in R by A46,A47,RCOMP_1:48;
        then A50:w1 in f.:R & w2 in f.:R by A38,A49,FUNCT_1:def 12;
        reconsider W=f.:R as Subset of Euclid 2 by TOPREAL3:13;
        A51:dom f=[#](I[01]) & rng f=P by A1,A38,PRE_TOPC:def 3;
        [#](I[01])=[.0,1.] by BORSUK_1:83,PRE_TOPC:12;
        then P=f.:([.0,1.]) by A51,RELAT_1:146;
        then A52:W c= W1 by BORSUK_1:83,RELAT_1:156;
        0 in the carrier of I[01] by JORDAN5A:2;then
        f.0 in rng f by A38,FUNCT_1:def 5;then
        A53: W-bound Pa <= p1`1 & p1`1 <= E-bound Pa &
        S-bound Pa <= p1`2 & p1`2 <= N-bound Pa by a2,A1,A48,PSCOMP_1:71;
        set r1=((E-bound Pa) - (W-bound Pa))+((N-bound Pa) - (S-bound Pa))+1;
               (W-bound Pa)<=(E-bound Pa) by A53,XREAL_1:2;
        then A54:0<=((E-bound Pa) - (W-bound Pa)) by XREAL_1:50;
               (S-bound Pa)<=(N-bound Pa) by A53,XREAL_1:2;
        then 0<=((N-bound Pa) - (S-bound Pa)) by XREAL_1:50;
        then A55:((E-bound Pa) - (W-bound Pa))+((N-bound Pa) - (S-bound Pa))
            >=0+0 by A54,XREAL_1:9;
         A56: r1> ((E-bound Pa) - (W-bound Pa))+((N-bound Pa) - (S-bound Pa))
                   by XREAL_1:31;
         for x,y being Point of Euclid 2
         st x in W1 & y in W1 holds dist(x,y)<=r1
         proof let x,y be Point of Euclid 2;
           assume A57: x in W1 & y in W1;
           then reconsider pw1=x,pw2=y as Point of TOP-REAL 2;
           A58:dist(x,y)=|.pw1-pw2.| by JGRAPH_1:45;
           A59:|.pw1-pw2.|<=abs(pw1`1-pw2`1)+abs(pw1`2-pw2`2) by JGRAPH_1:49;
           A60:W-bound Pa <= pw1`1 & pw1`1 <= E-bound Pa &
           S-bound Pa <= pw1`2 & pw1`2 <= N-bound Pa by A48,A57,PSCOMP_1:71;
           A61:W-bound Pa <= pw2`1 & pw2`1 <= E-bound Pa &
                 S-bound Pa <= pw2`2 & pw2`2 <= N-bound Pa
                            by A48,A57,PSCOMP_1:71;
          then A62:abs(pw1`1-pw2`1)<=(E-bound Pa) - (W-bound Pa)
                           by A60,JGRAPH_1:31;
               abs(pw1`2-pw2`2)<=(N-bound Pa) - (S-bound Pa)
                           by A60,A61,JGRAPH_1:31;
           then A63:abs(pw1`1-pw2`1)+abs(pw1`2-pw2`2)
           <=((E-bound Pa) - (W-bound Pa))+((N-bound Pa) - (S-bound Pa))
                         by A62,XREAL_1:9;
               ((E-bound Pa) - (W-bound Pa))+((N-bound Pa) - (S-bound Pa))
           <= ((E-bound Pa) - (W-bound Pa))+((N-bound Pa) - (S-bound Pa))
            +1 by XREAL_1:31;
           then abs(pw1`1-pw2`1)+abs(pw1`2-pw2`2)<=r1 by A63,XREAL_1:2;
          hence thesis by A58,A59,XREAL_1:2;
         end;
        then W1 is bounded by A55,A56,TBSP_1:def 9;
        then A64:W is bounded by A52,TBSP_1:21;
        A65: diameter W<e by A17,A18,A27,A28;
        dist(w1,w2)<=diameter(W) by A50,A64,TBSP_1:def 10;
        then dist(w1,w2)<e by A65,XREAL_1:2;
       hence thesis by A33,A35,JGRAPH_1:45;
      end;
     A66:len h1>=1 by A17,A18,XREAL_1:2;
     consider hb being FinSequence of REAL such that
     A67: hb.1=0 & hb.len hb=1 & 5<=len hb & rng hb c= the carrier of I[01]
     & hb is increasing & (for i being Nat,R being Subset of I[01],
     W being Subset of Euclid 2
      st 1<=i & i<len hb & R=[.hb/.i,hb/.(i+1).] & W=g.:(R) holds
     diameter(W)<e) by A16,UU10;
     A68: 1<=len hb by A67,XREAL_1:2;
     then A69:1 in dom hb by FINSEQ_3:27;
     A70:len hb in dom hb by A68,FINSEQ_3:27;
     deffunc F(Nat)=g.(hb.$1);
     ex h2' being FinSequence st len h2'=len hb &
     for i st i in Seg len hb holds h2'.i=F(i) from FINSEQ_1:sch 2;
     then consider h2' being FinSequence such that
     A71:len h2'=len hb &
     for i st i in Seg len hb holds h2'.i=g.(hb.i);
     rng h2' c= the carrier of TOP-REAL 2
      proof let y;assume y in rng h2';
        then consider x such that
        A72:x in dom h2' & y=h2'.x by FUNCT_1:def 5;
        A73:dom h2'=Seg len h2' by FINSEQ_1:def 3;
        reconsider i=x as Nat by A72;
        A74: h2'.i=g.(hb.i) by A71,A72,A73;
        i in dom hb by A71,A72,A73,FINSEQ_1:def 3;
        then A75:hb.i in rng hb by FUNCT_1:def 5;
        dom g = the carrier of I[01] by FUNCT_2:def 1;
        then h2'.i in rng g by A67,A74,A75,FUNCT_1:def 5;
        hence thesis by A72;
      end;
     then reconsider h2=h2' as FinSequence of TOP-REAL 2 by FINSEQ_1:def 4;
     A77:dom hb=Seg len hb by FINSEQ_1:def 3;
A78: for i st i in dom hb holds h2/.i=g.(hb.i)
      proof let i;assume
A79:   i in dom hb;
       then A10:h2.i=g.(hb.i) by A71,A77;
       1<=i & i<=len hb by A77,A79,FINSEQ_1:3;
       hence thesis by A71,A10,FINSEQ_4:24;
      end;
     A81:len h2>=1 by A67,A71,XREAL_1:2;
     A82:for i st i in dom h1 holds
 (h1/.1)`1<=(h1/.i)`1 & (h1/.i)`1<=(h1/.len h1)`1
  proof let i;assume A83:i in dom h1;
     then A84: i in Seg len h by A18,FINSEQ_1:def 3;
     then A85:i in dom h by FINSEQ_1:def 3;
     h1.i=f.(h.i) by A18,A84;
     then A86:h1/.i=f.(h.i) by A83,FINSEQ_4:def 4;
     1 in dom h by A18,A66,FINSEQ_3:27;
     then h1.1=f.(h.1) by A18,A24;
     then A87:h1/.1=f.(h.1) by A66,FINSEQ_4:24;
     len h in dom h by A18,A66,FINSEQ_3:27;
     then h1.len h1=f.(h.len h) by A18,A24;
     then A88:h1/.len h1=f.(h.len h) by A66,FINSEQ_4:24;
     A89:h.i in rng h by A85,FUNCT_1:def 5;
     dom f =the carrier of I[01] by FUNCT_2:def 1;
     then h1/.i in rng f by A17,A86,A89,FUNCT_1:def 5;
     hence thesis by a2,A2,A1,A17,A87,A88;
  end;
A91: for i st i in dom X_axis h1 holds
  (X_axis h1).1<=(X_axis h1).i & (X_axis h1).i<=(X_axis h1).len h1
  proof let i;assume i in dom X_axis h1;
     then i in Seg len (X_axis h1) by FINSEQ_1:def 3;
     then i in Seg len h1 by A66,JGRAPH_1:59;
     then A92:i in dom h1 by FINSEQ_1:def 3;
     then A93:(X_axis h1).i = (h1/.i)`1 by JGRAPH_1:61;
     A94:(X_axis h1).1=(h1/.1)`1 by A66,JGRAPH_1:59;
     (X_axis h1).len h1=(h1/.len h1)`1 by A66,JGRAPH_1:59;
    hence thesis by A82,A92,A93,A94;
  end;
 A95:for i st i in dom h1 holds
 q1`2<=(h1/.i)`2 & (h1/.i)`2<=q2`2
  proof let i;assume A96:i in dom h1;
    then A97: i in Seg len h1 by FINSEQ_1:def 3;
    then A98:i in dom h by A18,FINSEQ_1:def 3;
    h1.i=f.(h.i) by A18,A97;
    then A99:h1/.i=f.(h.i) by A96,FINSEQ_4:def 4;
    A100:h.i in rng h by A98,FUNCT_1:def 5;
    dom f =the carrier of I[01] by FUNCT_2:def 1;
    then h1/.i in rng f by A17,A99,A100,FUNCT_1:def 5;
    hence thesis by A1,A4;
  end;
  for i st i in dom (Y_axis h1) holds
   q1`2<=(Y_axis h1).i & (Y_axis h1).i<=q2`2
  proof let i;assume i in dom (Y_axis h1);
    then i in Seg len (Y_axis h1) by FINSEQ_1:def 3;
    then i in Seg len h1 by A66,JGRAPH_1:60;
    then A102:i in dom h1 by FINSEQ_1:def 3;
    then (Y_axis h1).i = (h1/.i)`2 by JGRAPH_1:61;
    hence thesis by A95,A102;
  end;
 then A103:X_axis(h1) lies_between (X_axis(h1)).1, (X_axis(h1)).(len h1) &
 Y_axis(h1) lies_between q1`2,q2`2 by A91,GOBOARD4:def 1;
 A104:for i st i in dom h2 holds
 (h2/.1)`2<=(h2/.i)`2 & (h2/.i)`2<=(h2/.len h2)`2
  proof let i;assume i in dom h2;
   then i in Seg len h2 by FINSEQ_1:def 3;
   then A105:i in dom hb by A71,FINSEQ_1:def 3;
   then A106:h2/.i=g.(hb.i) by A78;
   1 in Seg len hb by A71,A81,FINSEQ_1:3;
   then 1 in dom hb by FINSEQ_1:def 3;
   then A107:h2/.1=g.(hb.1) by A78;
   len hb in Seg len hb by A71,A81,FINSEQ_1:3;
   then len hb in dom hb by FINSEQ_1:def 3;
   then A108:h2/.len h2=g.(hb.len hb) by A71,A78;
   A109:hb.i in rng hb by A105,FUNCT_1:def 5;
   dom g =the carrier of I[01] by FUNCT_2:def 1;
   then h2/.i in rng g by A67,A106,A109,FUNCT_1:def 5;
   hence thesis by a1,a3,A5,A67,A107,A108;
  end;
 A111: for i st i in dom (Y_axis(h2)) holds
  (Y_axis(h2)).1<=(Y_axis(h2)).i & (Y_axis(h2)).i <=(Y_axis(h2)).(len h2)
  proof let i;assume i in dom (Y_axis(h2));
    then i in Seg len (Y_axis(h2)) by FINSEQ_1:def 3;
    then i in Seg len h2 by A81,JGRAPH_1:60;
    then A112:i in dom h2 by FINSEQ_1:def 3;
    then A113:(Y_axis(h2)).i = (h2/.i)`2 by JGRAPH_1:61;
    A114:(Y_axis(h2)).1=(h2/.1)`2 by A81,JGRAPH_1:60;
    (Y_axis(h2)).len h2=(h2/.len h2)`2 by A81,JGRAPH_1:60;
    hence thesis by A104,A112,A113,A114;
  end;
 A115:for i st i in dom h2 holds
 p1`1<=(h2/.i)`1 & (h2/.i)`1<=p2`1
  proof let i;assume i in dom h2;
     then i in Seg len h2 by FINSEQ_1:def 3;
     then A116:i in dom hb by A71,FINSEQ_1:def 3;
     then A117:h2/.i=g.(hb.i) by A78;
     A118:hb.i in rng hb by A116,FUNCT_1:def 5;
     dom g =the carrier of I[01] by FUNCT_2:def 1;
     then h2/.i in rng g by A67,A117,A118,FUNCT_1:def 5;
     hence thesis by a1,A3;
  end;
  for i st i in dom (X_axis(h2)) holds
   p1`1<= (X_axis(h2)).i & (X_axis(h2)).i<=p2`1
  proof let i;assume i in dom (X_axis(h2));
    then i in Seg len (X_axis(h2)) by FINSEQ_1:def 3;
    then i in Seg len h2 by A81,JGRAPH_1:59;
    then A120:i in dom h2 by FINSEQ_1:def 3;
    then (X_axis(h2)).i = (h2/.i)`1 by JGRAPH_1:61;
    hence thesis by A115,A120;
  end;
 then A121:X_axis(h2) lies_between p1`1, p2`1 &
 Y_axis(h2) lies_between (Y_axis(h2)).1, (Y_axis(h2)).(len h2)
                                  by A111,GOBOARD4:def 1;
 A122:for i st 1<=i & i+1<=len h2 holds
   |. (h2/.i)-(h2/.(i+1)) .|<e
      proof let i;assume A123:1<=i & i+1<=len h2;
        then A124:i<len h2 by NAT_1:38;
        A125:1<i+1 by A123,NAT_1:38;
        then A126:i+1 in Seg len hb by A71,A123,FINSEQ_1:3;
        i in Seg len hb by A71,A123,A124,FINSEQ_1:3;
        then A127:h2'.i=g.(hb.i) by A71;
        then A128:h2/.i=g.(hb.i) by A123,A124,FINSEQ_4:24;
        A129:h2'.(i+1)=g.(hb.(i+1)) by A71,A126;
        h2.(i+1)=h2/.(i+1) by A123,A125,FINSEQ_4:24;
        then A130:h2/.(i+1)=g.(hb.(i+1)) by A71,A126;
        i in Seg len hb by A71,A123,A124,FINSEQ_1:3;
        then A131:i in dom hb by FINSEQ_1:def 3;
        then A132:hb.i in rng hb by FUNCT_1:def 5;
        A133: dom g =the carrier of I[01] by FUNCT_2:def 1;
        then A134:h2'.i in rng g by A67,A127,A132,FUNCT_1:def 5;
        i+1 in Seg len hb by A71,A123,A125,FINSEQ_1:3;
        then i+1 in dom hb by FINSEQ_1:def 3;
        then hb.(i+1) in rng hb by FUNCT_1:def 5;
        then h2'.(i+1) in rng g by A67,A129,A133,FUNCT_1:def 5;
   then A136:g.(hb.(i+1)) is Element of TOP-REAL 2 by A71,A126;
        A137:hb.i=hb/.i by A71,A123,A124,FINSEQ_4:24;
        i+1 in Seg len hb by A71,A123,A125,FINSEQ_1:3;
        then A138:i+1 in dom hb by FINSEQ_1:def 3;
        then A139:hb.(i+1) in rng hb by FUNCT_1:def 5;
        A140:hb.(i+1)=hb/.(i+1) by A71,A123,A125,FINSEQ_4:24;
        i<i+1 by NAT_1:38;
        then A141:0<=hb/.i & hb/.i <= hb/.(i+1) & hb/.(i+1)<=1
          by A67,A131,A132,A137,A138,A139,A140,BORSUK_1:83,GOBOARD1:def 1
,RCOMP_1:48;
       reconsider W1=Q as Subset of Euclid 2 by TOPREAL3:13;
        reconsider Qa=Q as Subset of TOP-REAL 2;
        I[01] is compact by HEINE:11,TOPMETR:27;then
        A555: [#](I[01]) is compact by COMPTS_1:10;
        g.:(the carrier of I[01])=rng g by FUNCT_2:45;then
        g.:([#](I[01]))=rng g by PRE_TOPC:def 3;then
        A142:Qa is compact by a1,A555,WEIERSTR:14;
        reconsider Qa as non empty Subset of TOP-REAL 2;
        reconsider R=[.hb/.i,hb/.(i+1).] as Subset of I[01]
           by A67,A132,A137,A139,A140,BORSUK_1:83,RCOMP_1:16;
        reconsider w1=g.(hb.i),w2=g.(hb.(i+1)) as Point of Euclid 2
                                   by A127,A134,A136,TOPREAL3:13;
        A143: hb.i in R by A137,A141,RCOMP_1:48;
        hb.(i+1) in R by A140,A141,RCOMP_1:48;
        then A144:w1 in g.:(R) & w2 in g.:(R) by A133,A143,FUNCT_1:def 12;
        reconsider W=g.:(R) as Subset of Euclid 2 by TOPREAL3:13;
A145:   dom g=[#](I[01]) & rng g=Q by a1,PRE_TOPC:def 3,A133;
        [#](I[01])=[.0,1.] by BORSUK_1:83,PRE_TOPC:12;
        then Q=g.:([.0,1.]) by A145,RELAT_1:146;
        then A146:W c= W1 by BORSUK_1:83,RELAT_1:156;
        0 in the carrier of I[01] by JORDAN5A:2;then
        g.0 in rng g by A133,FUNCT_1:def 5;then
A147:   W-bound Qa <= q1`1 & q1`1 <= E-bound Qa &
        S-bound Qa <= q1`2 & q1`2 <= N-bound Qa by a1,a3,A142,PSCOMP_1:71;
        set r1=
           ((E-bound Qa) - (W-bound Qa))+((N-bound Qa) - (S-bound Qa))+1;
               (W-bound Qa)<=(E-bound Qa) by A147,XREAL_1:2;
           then A148:0<=((E-bound Qa) - (W-bound Qa)) by XREAL_1:50;
               (S-bound Qa)<=(N-bound Qa) by A147,XREAL_1:2;
           then 0<=((N-bound Qa) - (S-bound Qa)) by XREAL_1:50;
        then A149:((E-bound Qa) - (W-bound Qa))+((N-bound Qa) - (S-bound Qa))
            >=0+0 by A148,XREAL_1:9;
         A150: r1> ((E-bound Qa) - (W-bound Qa))+((N-bound Qa) - (S-bound Qa))
                             by XREAL_1:31;
            for x,y being Point of Euclid 2
         st x in W1 & y in W1 holds dist(x,y)<=r1
         proof let x,y be Point of Euclid 2;
           assume A151: x in W1 & y in W1;
           then reconsider pw1=x,pw2=y as Point of TOP-REAL 2;
           A152:dist(x,y)=|.pw1-pw2.| by JGRAPH_1:45;
           A153:|.pw1-pw2.|<=abs(pw1`1-pw2`1)+abs(pw1`2-pw2`2) by JGRAPH_1:49;
           A154:W-bound Qa <= pw1`1 & pw1`1 <= E-bound Qa &
                 S-bound Qa <= pw1`2 & pw1`2 <= N-bound Qa
                              by A142,A151,PSCOMP_1:71;
           A155:W-bound Qa <= pw2`1 & pw2`1 <= E-bound Qa &
                 S-bound Qa <= pw2`2 & pw2`2 <= N-bound Qa
                              by A142,A151,PSCOMP_1:71;
           then A156:abs(pw1`1-pw2`1)<=(E-bound Qa) - (W-bound Qa)
                           by A154,JGRAPH_1:31;
           abs(pw1`2-pw2`2)<=(N-bound Qa) - (S-bound Qa)
                          by A154,A155,JGRAPH_1:31;
           then A157:abs(pw1`1-pw2`1)+abs(pw1`2-pw2`2)
           <=((E-bound Qa) - (W-bound Qa))+((N-bound Qa) - (S-bound Qa))
                         by A156,XREAL_1:9;
               ((E-bound Qa) - (W-bound Qa))+((N-bound Qa) - (S-bound Qa))
           <=
           ((E-bound Qa) - (W-bound Qa))+((N-bound Qa) - (S-bound Qa))
            +1 by XREAL_1:31;
           then abs(pw1`1-pw2`1)+abs(pw1`2-pw2`2)<=r1 by A157,XREAL_1:2;
          hence thesis by A152,A153,XREAL_1:2;
         end;
        then W1 is bounded by A149,A150,TBSP_1:def 9;
        then A158:W is bounded by A146,TBSP_1:21;
        A159: diameter(W)<e by A67,A71,A123,A124;
        dist(w1,w2)<=diameter(W) by A144,A158,TBSP_1:def 10;
        then dist(w1,w2)<e by A159,XREAL_1:2;
       hence thesis by A128,A130,JGRAPH_1:45;
      end;
     consider f2 being FinSequence of TOP-REAL 2 such that
     A160: f2 is special &
     f2.1=h1.1 & f2.len f2=h1.len h1 & len f2>=len h1 &
     X_axis(f2) lies_between (X_axis(h1)).1, (X_axis(h1)).(len h1) &
     Y_axis(f2) lies_between q1`2,q2`2 &
     (for j st j in dom f2 holds
     ex k being Nat st k in dom h1 & |.(f2/.j - h1/.k).|<e)
     &(for j st 1<=j & j+1<=len f2 holds |.f2/.j - f2/.(j+1).|<e)
                                  by A16,A26,A66,A103,JGRAPH_1:56;
     A161:len f2>=1 by A66,A160,XREAL_1:2;
     consider g2 being FinSequence of TOP-REAL 2 such that
     A162: g2 is special &
     g2.1=h2.1 & g2.len g2=h2.len h2 & len g2>=len h2 &
     Y_axis(g2) lies_between (Y_axis(h2)).1, (Y_axis(h2)).(len h2) &
     X_axis(g2) lies_between p1`1,p2`1 &
     (for j st j in dom g2 holds
     ex k being Nat st k in dom h2 & |.((g2/.j) - h2/.k).|<e)&
     (for j st 1<=j & j+1<=len g2 holds |.(g2/.j) - g2/.(j+1).|<e)
               by A16,A81,A121,A122,JGRAPH_1:57;
     A163:len g2>=1 by A81,A162,XREAL_1:2;
     A164:1 in dom h by A18,A66,FINSEQ_3:27;
     A165:len h in dom h by A18,A66,FINSEQ_3:27;
     A166: h1.1=f.(h.1) by A18,A24,A164;
     A167:h1.1=h1/.1 by A66,FINSEQ_4:24;
     A168:h1.len h1=h1/.len h1 by A66,FINSEQ_4:24;
     A169:f2.1=f2/.1 by A161,FINSEQ_4:24;
     A170:f2.len f2=f2/.len f2 by A161,FINSEQ_4:24;
     A171:X_axis(f2).1=p1`1
     by a2,A17,A160,A161,A166,A169,JGRAPH_1:59;
     (h1/.len h1)=p2 by a2,A17,A18,A24,A165,A168;
     then A172:X_axis(f2).len f2=p2`1 by A160,A161,A168,A170,JGRAPH_1:59;
     A173:(h2/.1)=q1 by a3,A67,A69,A78;
     A174:h2.1=h2/.1 by A68,A71,FINSEQ_4:24;
     A175:h2.len h2=h2/.len h2 by A68,A71,FINSEQ_4:24;
     g2/.1=g2.1 by A163,FINSEQ_4:24;
     then A176:Y_axis(g2).1=q1`2 by A162,A163,A173,A174,JGRAPH_1:60;
     A177:g2/.len g2=g2.len g2 by A163,FINSEQ_4:24;
     (g2.len g2)=q2 by a3,A67,A70,A71,A78,A162,A175;
     then A178:Y_axis(g2).len g2=q2`2 by A163,A177,JGRAPH_1:60;
     A179:(X_axis f2).1=(h1/.1)`1 by A160,A161,A167,A169,JGRAPH_1:59
     .=(X_axis h1).1 by A66,JGRAPH_1:59;
     A180:X_axis(f2).len f2=(h1/.len h1)`1 by A160,A161,A168,A170,JGRAPH_1:59
     .=X_axis(h1).len h1 by A66,JGRAPH_1:59;
     g2.1=g2/.1 by A163,FINSEQ_4:24;
     then A181:(Y_axis(g2)).1=(h2/.1)`2 by A162,A163,A174,JGRAPH_1:60
     .=(Y_axis(h2)).1 by A81,JGRAPH_1:60;
         g2.len g2=g2/.len g2 by A163,FINSEQ_4:24;
     then A182: Y_axis(g2).len g2=(h2/.len h2)`2 by A162,A163,A175,JGRAPH_1:60
     .=Y_axis(h2).len h2 by A81,JGRAPH_1:60;
     5<=len f2 by A17,A18,A160,XREAL_1:2;
     then A183:2<=len f2 by XREAL_1:2;
     5<=len g2 by A67,A71,A162,XREAL_1:2;
     then A184:2<=len g2 by XREAL_1:2;
     A185:1<=len h by A17,XREAL_1:2;
     then 1 in dom h by FINSEQ_3:27;
     then A186:h1/.1=f.(h.1) by A18,A24,A167;
     A186b: len h in dom h by A185,FINSEQ_3:27;
    now per cases;
    case S10: p1=p2;
      S11: for q being Point of TOP-REAL 2 st q in Q holds q`1=p1`1
       proof let q be Point of TOP-REAL 2;
        assume q in Q;then p1`1<=q`1 & q`1<= p2`1 by A3;
        hence q`1=p1`1 by S10,XREAL_1:1;
       end;
       0 in the carrier of I[01] by JORDAN5A:2;then
       0 in dom g by FUNCT_2:def 1;then
       S75: q1 in Q by a1,a3,FUNCT_1:12;
       1 in the carrier of I[01] by JORDAN5A:2;then
       1 in dom g by FUNCT_2:def 1;then
       S76: q2 in Q by a1,a3,FUNCT_1:12;
       0 in the carrier of I[01] by JORDAN5A:2;then
       0 in dom f by FUNCT_2:def 1;then
S75b:  p1 in P by a2,A1,FUNCT_1:12;
      T15: now assume U0: q1`2=q2`2;
         q1`2<=p1`2 & p1`2<=q2`2 by A4,S75b;then
         U3: p1`2=q1`2 by U0,XREAL_1:1;
         q1`1=p1`1 by S11,S75;then
         q1=p1 by U3,TOPREAL3:11;
        hence contradiction by A00,S75,S75b,XBOOLE_0:def 3;
      end;
      S20: Q c= LSeg(q1,q2)
       proof let z be set;assume
         T1: z in Q;then
         reconsider qz=z as Point of TOP-REAL 2;
T8:      p1`1<=qz`1 & qz`1<= p2`1 by T1,A3;
         p1`1<=q1`1 & q1`1<= p2`1 by S75,A3;then
         T9: p1`1=q1`1 by XREAL_1:1,S10;
         p1`1<=q2`1 & q2`1<= p2`1 by S76,A3;then
         T10: p1`1=q2`1 by XREAL_1:1,S10;
         T16: q1`2<=qz`2 & qz`2<=q2`2 by T1,A5;then
         q2`2>=q1`2 by XREAL_1:2;then
         q2`2>q1`2 by T15,REAL_1:def 5;then
         T18: q2`2-q1`2>0 by XREAL_1:52;
         T19: qz`2-q1`2>=0 by XREAL_1:50,T16;
         set ln=(qz`2-q1`2)/(q2`2-q1`2);
         T20: ln>=0 by T18,T19,REAL_2:125;
         qz`2-q1`2<=q2`2-q1`2 by T16,REAL_1:92;then
         ln<= (q2`2-q1`2)/(q2`2-q1`2) by T18,REAL_1:73;then
         T21: ln <= 1 by T18,XCMPLX_1:60;
         T2: q2`2-q1`2 <>0 by T15,XCMPLX_1:15;
         1-ln=(q2`2-q1`2)/(q2`2-q1`2)-(qz`2-q1`2)/(q2`2-q1`2)
                               by XCMPLX_1:60,T2
            .=((q2`2-q1`2)-(qz`2-q1`2))/(q2`2-q1`2) by XCMPLX_1:121
            .=(q2`2-qz`2)/(q2`2-q1`2);then
         T4: (1-ln)*q1`2=(q2`2-qz`2)*q1`2/(q2`2-q1`2) by XCMPLX_1:75
                   .=(q2`2*q1`2-qz`2*q1`2)/(q2`2-q1`2);
         T5: ln*q2`2=(qz`2-q1`2)*q2`2/(q2`2-q1`2) by XCMPLX_1:75
               .=(qz`2*q2`2-q1`2*q2`2)/(q2`2-q1`2);
         T12: ((1-ln)*q1+ln*q2)`1= ((1-ln)*q1)`1+(ln*q2)`1 by TOPREAL3:7
                           .=(1-ln)*q1`1+(ln*q2)`1 by TOPREAL3:9
                           .=(1-ln)*p1`1+(ln)*p1`1 by T9,T10,TOPREAL3:9
                           .=qz`1 by T8,XREAL_1:1,S10;
          ((1-ln)*q1+ln*q2)`2=((1-ln)*q1)`2+(ln*q2)`2 by TOPREAL3:7
                           .=(1-ln)*q1`2+(ln*q2)`2 by TOPREAL3:9
                           .=(1-ln)*q1`2+ln*q2`2 by TOPREAL3:9
                           .=((q2`2*q1`2-qz`2*q1`2)+(qz`2*q2`2-q1`2*q2`2))/
                              (q2`2-q1`2) by XCMPLX_1:63,T4,T5
                           .=qz`2*(q2`2-q1`2)/(q2`2-q1`2)
                           .=qz`2*((q2`2-q1`2)/(q2`2-q1`2)) by XCMPLX_1:75
                           .=qz`2*1 by XCMPLX_1:60,T2
                           .=qz`2;then
          (1-ln)*q1+ln*q2=qz by T12,TOPREAL3:11;then
          z in { (1-lambda)*q1 + lambda*q2 where lambda is Real
                         : 0 <= lambda & lambda <= 1 } by T20,T21;
        hence z in LSeg(q1,q2) by TOPREAL1:def 3;
       end;
      S21: p1 in LSeg(q1,q2)
       proof
         0 in the carrier of I[01] by JORDAN5A:2;then
         0 in dom g by FUNCT_2:def 1;then
         q1 in Q by a1,a3,FUNCT_1:12;then
         p1`1<=q1`1 & q1`1<= p2`1 by A3;then
         T9: p1`1=q1`1 by XREAL_1:1,S10;
         1 in the carrier of I[01] by JORDAN5A:2;then
         1 in dom g by FUNCT_2:def 1;then
         g.1 in rng g by FUNCT_1:12;then
          p1`1<=q2`1 & q2`1<= p2`1 by a1,a3,A3;then
         T10: p1`1=q2`1 by XREAL_1:1,S10;
         0 in the carrier of I[01] by JORDAN5A:2;then
         0 in dom f by FUNCT_2:def 1;then
         f.0 in rng f by FUNCT_1:12;then
         T16: q1`2<=p1`2 & p1`2<=q2`2 by a2,A1,A4;then
         q2`2>=q1`2 by XREAL_1:2;then
         q2`2>q1`2 by T15,REAL_1:def 5;then
         T18: q2`2-q1`2>0 by XREAL_1:52;
         T19: p1`2-q1`2>=0 by XREAL_1:50,T16;
         set ln=(p1`2-q1`2)/(q2`2-q1`2);
         T20: ln>=0 by T18,T19,REAL_2:125;
         p1`2-q1`2<=q2`2-q1`2 by T16,REAL_1:92;then
         ln<= (q2`2-q1`2)/(q2`2-q1`2) by T18,REAL_1:73;then
         T21: ln <= 1 by T18,XCMPLX_1:60;
         T2: q2`2-q1`2 <>0 by T15,XCMPLX_1:15;
         1-ln=(q2`2-q1`2)/(q2`2-q1`2)-(p1`2-q1`2)/(q2`2-q1`2)
                               by XCMPLX_1:60,T2
            .=((q2`2-q1`2)-(p1`2-q1`2))/(q2`2-q1`2) by XCMPLX_1:121
            .=(q2`2-p1`2)/(q2`2-q1`2);then
         T4: (1-ln)*q1`2=(q2`2-p1`2)*q1`2/(q2`2-q1`2) by XCMPLX_1:75
                   .=(q2`2*q1`2-p1`2*q1`2)/(q2`2-q1`2);
         T5: ln*q2`2=(p1`2-q1`2)*q2`2/(q2`2-q1`2) by XCMPLX_1:75
               .=(p1`2*q2`2-q1`2*q2`2)/(q2`2-q1`2);
         T12: ((1-ln)*q1+ln*q2)`1= ((1-ln)*q1)`1+(ln*q2)`1 by TOPREAL3:7
                           .=(1-ln)*q1`1+(ln*q2)`1 by TOPREAL3:9
                           .=(1-ln)*p1`1+(ln)*p1`1 by T9,T10,TOPREAL3:9
                           .=p1`1;
          ((1-ln)*q1+ln*q2)`2=((1-ln)*q1)`2+(ln*q2)`2 by TOPREAL3:7
                           .=(1-ln)*q1`2+(ln*q2)`2 by TOPREAL3:9
                           .=(1-ln)*q1`2+ln*q2`2 by TOPREAL3:9
                           .=((q2`2*q1`2-p1`2*q1`2)+(p1`2*q2`2-q1`2*q2`2))/
                              (q2`2-q1`2) by XCMPLX_1:63,T4,T5
                           .=p1`2*(q2`2-q1`2)/(q2`2-q1`2)
                           .=p1`2*((q2`2-q1`2)/(q2`2-q1`2)) by XCMPLX_1:75
                           .=p1`2*1 by XCMPLX_1:60,T2
                           .=p1`2;then
         (1-ln)*q1+ln*q2=p1 by T12,TOPREAL3:11;then
         p1 in { (1-lambda)*q1 + lambda*q2 where lambda is Real
                         : 0 <= lambda & lambda <= 1 } by T20,T21;
        hence p1 in LSeg(q1,q2) by TOPREAL1:def 3;
       end;
      p1 in Q by a1,S21,S20,AA450;
     hence contradiction by A122b,A00,XBOOLE_0:def 3;
    end;
    case A6:p1<>p2;
    A7: now assume S10: q1=q2;
      S11: for p being Point of TOP-REAL 2 st p in P holds p`2=q1`2
       proof let p be Point of TOP-REAL 2;
        assume p in P;then q1`2<=p`2 & p`2<= q2`2 by A4;
        hence p`2=q1`2 by S10,XREAL_1:1;
       end;
       0 in the carrier of I[01] by JORDAN5A:2;then
       0 in dom f by FUNCT_2:def 1;then
       S75: p1 in P by a2,A1,FUNCT_1:12;
       1 in the carrier of I[01] by JORDAN5A:2;then
       1 in dom f by FUNCT_2:def 1;then
       S76: p2 in P by a2,A1,FUNCT_1:12;
       0 in the carrier of I[01] by JORDAN5A:2;then
       0 in dom g by FUNCT_2:def 1;then
       S75b: q1 in Q by a1,a3,FUNCT_1:12;
T15:   now assume U0: p1`1=p2`1;
         p1`1<=q1`1 & q1`1<=p2`1 by A3,S75b;then
         U3: q1`1=p1`1 by U0,XREAL_1:1;
         p1`2=q1`2 by S11,S75;then
         p1=q1 by U3,TOPREAL3:11;
        hence contradiction by A00,S75,S75b,XBOOLE_0:def 3;
      end;
      S20: P c= LSeg(p1,p2)
       proof let z be set;assume
         T1: z in P;then
         reconsider pz=z as Point of TOP-REAL 2;
T8:      q1`2<=pz`2 & pz`2<= q2`2 by T1,A4;
         q1`2<=p1`2 & p1`2<= q2`2 by S75,A4;then
         T9: q1`2=p1`2 by XREAL_1:1,S10;
          q1`2<=p2`2 & p2`2<= q2`2 by S76,A4;then
         T10: q1`2=p2`2 by XREAL_1:1,S10;
         T16: p1`1<=pz`1 & pz`1<=p2`1 by T1,A2;then
         p2`1>=p1`1 by XREAL_1:2;then
         p2`1>p1`1 by T15,REAL_1:def 5;then
         T18: p2`1-p1`1>0 by XREAL_1:52;
         T19: pz`1-p1`1>=0 by XREAL_1:50,T16;
         set ln=(pz`1-p1`1)/(p2`1-p1`1);
         T20: ln>=0 by T18,T19,REAL_2:125;
         pz`1-p1`1<=p2`1-p1`1 by T16,REAL_1:92;then
         ln<= (p2`1-p1`1)/(p2`1-p1`1) by T18,REAL_1:73;then
         T21: ln <= 1 by T18,XCMPLX_1:60;
         T2: p2`1-p1`1 <>0 by T15,XCMPLX_1:15;
         1-ln=(p2`1-p1`1)/(p2`1-p1`1)-(pz`1-p1`1)/(p2`1-p1`1)
                               by XCMPLX_1:60,T2
            .=((p2`1-p1`1)-(pz`1-p1`1))/(p2`1-p1`1) by XCMPLX_1:121
            .=(p2`1-pz`1)/(p2`1-p1`1);then
         T4: (1-ln)*p1`1=(p2`1-pz`1)*p1`1/(p2`1-p1`1) by XCMPLX_1:75
                   .=(p2`1*p1`1-pz`1*p1`1)/(p2`1-p1`1);
         T5: ln*p2`1=(pz`1-p1`1)*p2`1/(p2`1-p1`1) by XCMPLX_1:75
               .=(pz`1*p2`1-p1`1*p2`1)/(p2`1-p1`1);
         T12: ((1-ln)*p1+ln*p2)`2= ((1-ln)*p1)`2+(ln*p2)`2 by TOPREAL3:7
                           .=(1-ln)*p1`2+(ln*p2)`2 by TOPREAL3:9
                           .=(1-ln)*q1`2+(ln)*q1`2 by T9,T10,TOPREAL3:9
                           .=pz`2 by T8,XREAL_1:1,S10;
          ((1-ln)*p1+ln*p2)`1=((1-ln)*p1)`1+(ln*p2)`1 by TOPREAL3:7
                           .=(1-ln)*p1`1+(ln*p2)`1 by TOPREAL3:9
                           .=(1-ln)*p1`1+ln*p2`1 by TOPREAL3:9
                           .=((p2`1*p1`1-pz`1*p1`1)+(pz`1*p2`1-p1`1*p2`1))/
                              (p2`1-p1`1) by XCMPLX_1:63,T4,T5
                           .=pz`1*(p2`1-p1`1)/(p2`1-p1`1)
                           .=pz`1*((p2`1-p1`1)/(p2`1-p1`1)) by XCMPLX_1:75
                           .=pz`1*1 by XCMPLX_1:60,T2
                           .=pz`1;then
          (1-ln)*p1+ln*p2=pz by T12,TOPREAL3:11;then
         z in { (1-lambda)*p1 + lambda*p2 where lambda is Real
                         : 0 <= lambda & lambda <= 1 } by T20,T21;
        hence z in LSeg(p1,p2) by TOPREAL1:def 3;
       end;
      S21: q1 in LSeg(p1,p2)
       proof
         0 in the carrier of I[01] by JORDAN5A:2;then
         0 in dom f by FUNCT_2:def 1;then
         f.0 in rng f by FUNCT_1:12;then
         q1`2<=p1`2 & p1`2<= q2`2 by a2,A1,A4;then
         T9: q1`2=p1`2 by XREAL_1:1,S10;
         1 in the carrier of I[01] by JORDAN5A:2;then
         1 in dom f by FUNCT_2:def 1;then
         f.1 in rng f by FUNCT_1:12;then
         q1`2<=p2`2 & p2`2<= q2`2 by a2,A1,A4;then
         T10: q1`2=p2`2 by XREAL_1:1,S10;
         0 in the carrier of I[01] by JORDAN5A:2;then
         0 in dom g by FUNCT_2:def 1;then
         g.0 in rng g by FUNCT_1:12;then
         T16: p1`1<=q1`1 & q1`1<=p2`1 by a1,a3,A3;then
         p2`1>=p1`1 by XREAL_1:2;then
         p2`1>p1`1 by T15,REAL_1:def 5;then
         T18: p2`1-p1`1>0 by XREAL_1:52;
         T19: q1`1-p1`1>=0 by XREAL_1:50,T16;
         set ln=(q1`1-p1`1)/(p2`1-p1`1);
         T20: ln>=0 by T18,T19,REAL_2:125;
         q1`1-p1`1<=p2`1-p1`1 by T16,REAL_1:92;then
         ln<= (p2`1-p1`1)/(p2`1-p1`1) by T18,REAL_1:73;then
         T21: ln <= 1 by T18,XCMPLX_1:60;
         T2: p2`1-p1`1 <>0 by T15,XCMPLX_1:15;
         1-ln=(p2`1-p1`1)/(p2`1-p1`1)-(q1`1-p1`1)/(p2`1-p1`1)
                               by XCMPLX_1:60,T2
            .=((p2`1-p1`1)-(q1`1-p1`1))/(p2`1-p1`1) by XCMPLX_1:121
            .=(p2`1-q1`1)/(p2`1-p1`1);then
         T4: (1-ln)*p1`1=(p2`1-q1`1)*p1`1/(p2`1-p1`1) by XCMPLX_1:75
                   .=(p2`1*p1`1-q1`1*p1`1)/(p2`1-p1`1);
         T5: ln*p2`1=(q1`1-p1`1)*p2`1/(p2`1-p1`1) by XCMPLX_1:75
               .=(q1`1*p2`1-p1`1*p2`1)/(p2`1-p1`1);
         T12: ((1-ln)*p1+ln*p2)`2= ((1-ln)*p1)`2+(ln*p2)`2 by TOPREAL3:7
                           .=(1-ln)*p1`2+(ln*p2)`2 by TOPREAL3:9
                           .=(1-ln)*q1`2+(ln)*q1`2 by T9,T10,TOPREAL3:9;
         ((1-ln)*p1+ln*p2)`1=((1-ln)*p1)`1+(ln*p2)`1 by TOPREAL3:7
                           .=(1-ln)*p1`1+(ln*p2)`1 by TOPREAL3:9
                           .=(1-ln)*p1`1+ln*p2`1 by TOPREAL3:9
                           .=((p2`1*p1`1-q1`1*p1`1)+(q1`1*p2`1-p1`1*p2`1))/
                              (p2`1-p1`1) by XCMPLX_1:63,T4,T5
                           .=q1`1*(p2`1-p1`1)/(p2`1-p1`1)
                           .=q1`1*((p2`1-p1`1)/(p2`1-p1`1)) by XCMPLX_1:75
                           .=q1`1*1 by XCMPLX_1:60,T2;then
         (1-ln)*p1+ln*p2=q1 by T12,TOPREAL3:11;then
         q1 in { (1-lambda)*p1 + lambda*p2 where lambda is Real
                         : 0 <= lambda & lambda <= 1 } by T20,T21;
        hence q1 in LSeg(p1,p2) by TOPREAL1:def 3;
       end;
      q1 in P by A1,S21,S20,AA450;
     hence contradiction by A00,A122d,XBOOLE_0:def 3;
    end;
A187: f2/.1<>f2/.len f2 by a2,A6,A17,A18,A24,A160,A167,A169,A170
,A186,A186b;
     A188:1<=len hb by A67,XREAL_1:2;
     then 1 in dom hb by FINSEQ_3:27;
     then A189:h2/.1=g.(hb.1) by A78;
     len hb in dom hb by A188,FINSEQ_3:27;
     then g2.1<>g2.len g2
     by a3,A7,A67,A71,A78,A162,A174,A175,A189;
     then L~f2 meets L~g2 by A160,A162,A169,A170,A171,A172,A176,A178,A179,A180
,A181,A182,A183,A184,A187,JGRAPH_1:30;
     then consider s being set such that
     A190:s in L~f2 & s in L~g2 by XBOOLE_0:3;
     reconsider ps=s as Point of TOP-REAL 2 by A190;
     ps in union{ LSeg(f2,i) : 1 <= i & i+1 <= len f2 }
          by A190,TOPREAL1:def 6;
     then consider x such that
     A191: ps in x & x in
     { LSeg(f2,i) : 1 <= i & i+1 <= len f2 } by TARSKI:def 4;
     consider i such that
     A192: x=LSeg(f2,i) &(1 <= i & i+1 <= len f2) by A191;
     A193:LSeg(f2,i)=LSeg(f2/.i,f2/.(i+1)) by A192,TOPREAL1:def 5;
     i<len f2 by A192,NAT_1:38;
     then i in dom f2 by A192,FINSEQ_3:27;
     then consider k being Nat such that
     A194: k in dom h1 & |.f2/.i - h1/.k.|<e by A160;
     A195:|.ps-f2/.i.|<=|.f2/.i-f2/.(i+1).| by A191,A192,A193,JGRAPH_1:53;
     |.f2/.i-f2/.(i+1).|<e by A160,A192;
     then A196:|.ps-f2/.i.|<e by A195,XREAL_1:2;
     A197:|.ps-h1/.k.|<=|.ps-f2/.i.|+|.f2/.i-h1/.k.| by TOPRNS_1:35;
     |.ps-f2/.i.|+|.f2/.i-h1/.k.|<e+e by A194,A196,XREAL_1:10;
     then A198:|.ps-h1/.k.|<e+e by A197,XREAL_1:2;
     A199:k in Seg len h1 by A194,FINSEQ_1:def 3;
     then A200:k in dom h by A18,FINSEQ_1:def 3;
     1<=k & k<=len h1 by A199,FINSEQ_1:3;
     then h1.k=h1/.k by FINSEQ_4:24;
     then A201:h1/.k=f.(h.k) by A18,A199;
     A202:h.k in rng h by A200,FUNCT_1:def 5;
     dom f=the carrier of I[01] by FUNCT_2:def 1;
     then A204: h1/.k in P by A1,A17,A201,A202,FUNCT_1:def 5;
     reconsider p11=h1/.k as Point of TOP-REAL 2;
     A205: |.(p11-ps).|<e+e by A198,TOPRNS_1:28;
     ps in union{ LSeg(g2,j) : 1 <= j & j+1 <= len g2 }
       by A190,TOPREAL1:def 6;
     then consider y such that
     A206: ps in y &
     y in { LSeg(g2,j) : 1 <= j & j+1 <= len g2 } by TARSKI:def 4;
     consider j such that
     A207:y=LSeg(g2,j) &(1 <= j & j+1 <= len g2) by A206;
     A208: LSeg(g2,j)=LSeg((g2/.j),g2/.(j+1)) &
      ps in LSeg(g2,j) & y=LSeg(g2,j) &(1 <= j & j+1 <= len g2)
                             by A206,A207,TOPREAL1:def 5;
      j<len g2 & LSeg(g2,j)=LSeg((g2/.j),g2/.(j+1)) &
      ps in LSeg(g2,j) & y=LSeg(g2,j) & (1 <= j & j+1 <= len g2)
        by A206,A207,NAT_1:38,TOPREAL1:def 5;
     then j in dom g2 by FINSEQ_3:27;
     then consider k' being Nat such that
     A209: k' in dom h2 & |.((g2/.j) - h2/.k').|<e by A162;
     A210:|.ps-(g2/.j).|<=|.(g2/.j)-g2/.(j+1).| by A208,JGRAPH_1:53;
     |.(g2/.j)-g2/.(j+1).|<e by A162,A207;
     then A211:|.ps-(g2/.j).|<e by A210,XREAL_1:2;
     A212:|.ps-h2/.k'.|<=|.ps-(g2/.j).|+|.(g2/.j)-h2/.k'.| by TOPRNS_1:35;
     |.ps-(g2/.j).|+|.(g2/.j)-(h2/.k').|<e+e by A209,A211,XREAL_1:10;
     then A213:|.ps-(h2/.k').|<e+e by A212,XREAL_1:2;
     k' in Seg len h2 by A209,FINSEQ_1:def 3;
     then k' in dom hb by A71,FINSEQ_1:def 3;
     then A214:hb.k' in rng hb &
     h2/.k'=g.(hb.k') & k' in dom hb by A78,FUNCT_1:def 5;
     dom g =the carrier of I[01] by FUNCT_2:def 1;
     then A215:Q<>{}(TOP-REAL 2) &
     h2/.k' in rng g & hb.k' in dom g by A67,A214,FUNCT_1:def 5;
     reconsider q11=h2/.k' as Point of TOP-REAL 2;
     reconsider x1=p11,x2=q11 as Point of Euclid 2 by A13,EUCLID:25;
     min_dist_min(P',Q')<=dist(x1,x2) by a1,A14,A204,A215,WEIERSTR:40;
     then A217:|.(p11-q11).|<= |.(p11-ps).|+|.(ps-q11).|
       & min_dist_min(P',Q')<=|.(p11-q11).| by JGRAPH_1:45,TOPRNS_1:35;
     |.(p11-ps).|+|.(ps-q11).| <e+e+(e+e) by A205,A213,XREAL_1:10;
     then |.(p11-q11).|<e+e+e+e by A217,XREAL_1:2;
     then min_dist_min(P',Q')<4*e by A217,XREAL_1:2;
     then 4*e-5*e>0 by XREAL_1:52; then (4-5)*e/e>0 by A16,REAL_2:127;
     then 4-5>0 by A16,XCMPLX_1:90;
    hence contradiction;
     end;
     end;
    hence contradiction;
end;

:: Fashoda Meet Theorem
theorem fashoda:
  for f,g being continuous Function of I[01],TOP-REAL 2,
  O,I being Point of I[01] st O=0 & I=1 &
  (f.O)`1=a & (f.I)`1=b &
  (g.O)`2=c & (g.I)`2=d &
  (for r being Point of I[01] holds
    a <=(f.r)`1 & (f.r)`1<=b & a <=(g.r)`1 & (g.r)`1<=b &
    c <=(f.r)`2 & (f.r)`2<=d & c <=(g.r)`2 & (g.r)`2<=d)
   holds rng f meets rng g
   proof
     let f,g be continuous Function of I[01],TOP-REAL 2,
         O,I be Point of I[01];
   assume A1: O=0 & I=1 &
     (f.O)`1=a & (f.I)`1=b & (g.O)`2=c & (g.I)`2=d &
     (for r being Point of I[01] holds
      a<=(f.r)`1 & (f.r)`1<=b & a<=(g.r)`1 & (g.r)`1<=b &
      c <=(f.r)`2 & (f.r)`2<=d & c <=(g.r)`2 & (g.r)`2<=d);
      A2:dom f= the carrier of I[01] by FUNCT_2:def 1
      .=[#](I[01]) by PRE_TOPC:12;
      A3:[#](I[01]) =[.0,1.] by BORSUK_1:83,PRE_TOPC:12;
      then 0 in dom f by A2,RCOMP_1:48;
     then reconsider P=rng f as non empty Subset of TOP-REAL 2
                        by FUNCT_1:12;
          dom g= the carrier of I[01] by FUNCT_2:def 1
      .=[#](I[01]) by PRE_TOPC:12;
      then 0 in dom g by A3,RCOMP_1:48;
     then reconsider Q=rng g as non empty Subset of TOP-REAL 2
                       by FUNCT_1:12;
     A43: dom f=the carrier of I[01] by FUNCT_2:def 1;
     A43b: dom g=the carrier of I[01] by FUNCT_2:def 1;
     the carrier of ((TOP-REAL 2)|P)=[#]((TOP-REAL 2)|P) by PRE_TOPC:def 3
                                   .=rng f by PRE_TOPC:def 10;then
     reconsider f1=f as Function of I[01],((TOP-REAL 2)|P) by FUNCT_2:3,A43;
     the carrier of ((TOP-REAL 2)|Q)=[#]((TOP-REAL 2)|Q) by PRE_TOPC:def 3
                                   .=rng g by PRE_TOPC:def 10;then
     reconsider g1=g as Function of I[01],((TOP-REAL 2)|Q) by FUNCT_2:3,A43b;
     reconsider p1=f1.O as Point of TOP-REAL 2 by A1;
     reconsider p2=f1.I as Point of TOP-REAL 2 by A1;
     reconsider q1=g1.O as Point of TOP-REAL 2 by A1;
     reconsider q2=g1.I as Point of TOP-REAL 2 by A1;
b1:  f is Path of p1,p2 & g is Path of q1,q2 by A1,BORSUK_2:def 4;
     A12:for p being Point of TOP-REAL 2 st p in P holds p1`1<=p`1 & p`1<= p2`1
      proof let p be Point of TOP-REAL 2;
        assume p in P; then ex x st x in dom f1 & p=f1.x by FUNCT_1:def 5;
        hence thesis by A1;
      end;
A14:  for p being Point of TOP-REAL 2 st p in Q holds p1`1<=p`1 & p`1<= p2`1
      proof let p be Point of TOP-REAL 2;
        assume p in Q; then ex x st x in dom g1 & p=g1.x by FUNCT_1:def 5;
        hence thesis by A1;
      end;
A16:  for p being Point of TOP-REAL 2 st p in P holds q1`2<=p`2 & p`2<= q2`2
      proof let p be Point of TOP-REAL 2;
        assume p in P; then ex x st x in dom f1 & p=f1.x by FUNCT_1:def 5;
        hence thesis by A1;
      end;
      for p being Point of TOP-REAL 2 st p in Q holds q1`2<=p`2 & p`2<= q2`2
      proof let p be Point of TOP-REAL 2;
        assume p in Q; then ex x st x in dom g1 & p=g1.x by FUNCT_1:def 5;
        hence thesis by A1;
      end;
   hence thesis by A12,A14,A16,b1,Th62;
 end;

theorem
  for ar, br, cr, dr being Point of Trectangle(a,b,c,d)
  for h being Path of ar,br, v being Path of dr,cr
  for Ar, Br, Cr, Dr being Point of TOP-REAL 2
  st Ar`1 = a & Br`1 = b & Cr`2 = c & Dr`2 = d &
  ar = Ar & br = Br & cr = Cr & dr = Dr
  ex s, t being Point of I[01] st h.s = v.t
  proof
    let ar, br, cr, dr be Point of Trectangle(a,b,c,d);
    let h be Path of ar,br;
    let v be Path of dr,cr;
    let Ar, Br, Cr, Dr be Point of TOP-REAL 2 such that
a1: Ar`1 = a & Br`1 = b & Cr`2 = c & Dr`2 = d and
a2: ar = Ar & br = Br & cr = Cr & dr = Dr;
    set TR = Trectangle(a,b,c,d);
    per cases;
    suppose TR is empty;
      then the carrier of TR is empty by STRUCT_0:def 1;
      then
b2:   h = {} & v = {} by FUNCT_2:def 1;
      take 1[01], 1[01];
b1:   not 1[01] in dom {};
      hence h.1[01] = {} by b2,FUNCT_1:def 4
      .= v.1[01] by b1,b2,FUNCT_1:def 4;
    end;
    suppose TR is non empty;
      then reconsider TR = Trectangle(a,b,c,d)
        as non empty convex SubSpace of TOP-REAL 2;
      reconsider ar, br, cr, dr as Point of TR;
      reconsider h as Path of ar,br;
      reconsider v as Path of dr,cr;
      the carrier of TR is Subset of TOP-REAL 2 by TSEP_1:1;
      then reconsider f = h, g = -v as Function
 of I[01],TOP-REAL 2 by FUNCT_2:9;
a3:   f is continuous & g is continuous by TOPMETR:7;
a4:   h.0 = ar & h.1 = br & (-v).0 = cr & (-v).1 = dr by BORSUK_2:def 4;
      for r being Point of I[01] holds
      a <= (f.r)`1 & (f.r)`1 <= b & a <= (g.r)`1 & (g.r)`1 <= b &
      c <= (f.r)`2 & (f.r)`2 <= d & c <= (g.r)`2 & (g.r)`2 <= d
      proof
        let r be Point of I[01];
a5:     the carrier of TR = closed_inside_of_rectangle(a,b,c,d)
          by TOPREALA:53
        .= {p where p is Point of TOP-REAL 2:
        a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d} by JGRAPH_6:def 2;
        h.r in the carrier of TR; then
a6:     ex p being Point of TOP-REAL 2 st
        h.r = p & a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d by a5;
        (-v).r in the carrier of TR;
        then ex p being Point of TOP-REAL 2 st
        (-v).r = p & a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d by a5;
        hence thesis by a6;
      end;
      then rng f meets rng g by a1,a2,a3,a4,BORSUK_1:def 17,def 18,fashoda;
      then consider y being set such that
a7:   y in rng f and
a8:   y in rng g by XBOOLE_0:3;
      consider s being set such that
a9:   s in dom f and
a10:  f.s = y by a7,FUNCT_1:def 5;
      consider t being set such that
a11:  t in dom g and
a12:  g.t = y by a8,FUNCT_1:def 5;
      reconsider s, t as Point of I[01] by a9,a11;
      reconsider t1 = 1-t as Point of I[01] by JORDAN5B:4;
      take s, t1;
a13:  dr,cr are_connected by BORSUK_2:def 3;
      h.s = g.t by a10,a12
      .= v.t1 by a13,BORSUK_2:def 6;
      hence thesis;
    end;
  end;
