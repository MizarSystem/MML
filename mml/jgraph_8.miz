:: The {F}ashoda Meet Theorem for Continuous Mappings
::  by Yatsuka Nakamura , Andrzej Trybulec and Artur Korni{\l}owicz
::
:: Received September 14, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies FINSEQ_1, SQUARE_1, ABSVALUE, ARYTM_1, FUNCT_1, FINSEQ_4, BOOLE,
      RELAT_1, EUCLID, TOPREAL1, PRE_TOPC, TARSKI, MCART_1, GOBOARD1, GOBOARD4,
      COMPLEX1, METRIC_1, PCOMPS_1, COMPTS_1, WEIERSTR, ARYTM_3, BORSUK_1,
      ORDINAL2, RCOMP_1, TBSP_1, SUBSET_1, PSCOMP_1, LATTICES, ARYTM, UNIFORM1,
      TOPMETR, INT_1, CONNSP_1, RELAT_2, GRAPH_1, TOPREALA, JGRAPH_6, JORDAN1,
      BORSUK_6, ASYMPT_0, NAT_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XXREAL_0,
      XREAL_0, COMPLEX1, REAL_1, FUNCT_1, RELSET_1, FUNCT_2, FINSEQ_1, NAT_1,
      NAT_D, STRUCT_0, BINARITH, FINSEQ_4, TOPREAL1, PRE_TOPC, GOBOARD1,
      GOBOARD4, TOPMETR, PCOMPS_1, COMPTS_1, WEIERSTR, METRIC_1, TBSP_1,
      RCOMP_1, UNIFORM1, INT_1, CONNSP_1, SQUARE_1, PSCOMP_1, EUCLID,
      BORSUK_1, BORSUK_2, JORDAN1, JGRAPH_6, TOPREALA, TOPALG_2;
 constructors REAL_1, SQUARE_1, NAT_1, COMPLEX1, RCOMP_1, FINSEQ_4, BINARITH,
      CONNSP_1, COMPTS_1, TBSP_1, TOPRNS_1, TOPREAL1, GOBOARD1, GOBOARD4,
      JORDAN1, PSCOMP_1, WEIERSTR, UNIFORM1, JGRAPH_6, TOPREALA, TOPALG_2,
      NAT_D;
 registrations STRUCT_0, RELSET_1, EUCLID, PRE_TOPC, BORSUK_1, XREAL_0,
      FINSEQ_1, MEMBERED, BORSUK_2, INT_1, RELAT_1, TOPALG_2, REAL_1, JORDAN5A,
      XXREAL_0, ORDINAL1;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, XBOOLE_0, JORDAN1, TOPALG_2, EUCLID, TOPREALA, STRUCT_0;
 theorems TARSKI, FUNCT_1, FINSEQ_1, NAT_1, REAL_1, REAL_2, SPPOL_1, FINSEQ_4,
      BINARITH, RELAT_1, FINSEQ_3, TOPREAL1, GOBOARD1, GOBOARD4, TOPREAL3,
      SQUARE_1, EUCLID, UNIFORM1, WEIERSTR, PRE_TOPC, TBSP_1, HEINE, COMPTS_1,
      TOPMETR, PSCOMP_1, BORSUK_1, RCOMP_1, FUNCT_2, XBOOLE_0, XBOOLE_1,
      TOPRNS_1, XCMPLX_1, JGRAPH_1, INT_1, CAT_3, XREAL_0, CONNSP_1, JORDAN2C,
      XREAL_1, STRUCT_0, BORSUK_2, TSEP_1, JGRAPH_6, JORDAN5B, FINSEQ_2,
      XXREAL_0, TOPS_2;
 schemes FINSEQ_1, NAT_1;

begin

 reserve x, y for set;
 reserve i, j, n for Element of NAT;
 reserve  p1, p2 for Point of TOP-REAL n;
 reserve a, b, c, d for real number;

Lm1: I[01]=TopSpaceMetr(Closed-Interval-MSpace(0,1)) by TOPMETR:27,
def 8;
Lm2:for x being set,f being FinSequence st 1<=len f holds
 (f^<*x*>).1=f.1 & (<*x*>^f).(len f +1)=f.len f
proof let x be set,f be FinSequence;assume A1:1<=len f;
 then A2:1 in dom f by FINSEQ_3:27;
A3:len f in dom f by A1,FINSEQ_3:27;
 (<*x*>^f).(len f +1)=(<*x*>^f).(len <*x*>+len f) by FINSEQ_1:56
 .=f.len f by A3,FINSEQ_1:def 7;
 hence thesis by A2,FINSEQ_1:def 7;
end;

Lm3:for f being FinSequence of REAL st
 (for k being Element of NAT st 1<=k & k<len f holds f/.k<f/.(k+1)) holds
 f is increasing
proof let f be FinSequence of REAL;
 assume A1:for k being Element of NAT st 1<=k & k<len f holds f/.k<f/.(k+1);
  now let i,j;
    now assume A2:i in dom f & j in dom f & i<j;
    then A3:1<=i & i<=len f by FINSEQ_3:27;
    defpred P[Element of NAT] means i+(1+$1)<=len f implies f/.i<f/.(i+(1+$1));
    A4:1<=j & j<=len f by A2,FINSEQ_3:27;
    then i<len f by A2,XXREAL_0:2;
    then A5: P[0] by A1,A3;
    A6:for k being Element of NAT st P[k] holds P[(k+1)]
    proof let k be Element of NAT;assume
A7:  i+(1+k)<=len f implies f/.i<f/.(i+(1+k));
     now assume A8:i+(1+(k+1))<=len f;
       A9:1<=i+1 by NAT_1:11;   i+1<=i+1+k by NAT_1:11;
       then A10: 1<=i+1+k by A9,XXREAL_0:2;
       1+k<1+(k+1) by NAT_1:13;
       then A11: i+(1+k)<i+(1+(k+1)) by XREAL_1:8;
       then A12:i+(1+k)<len f by A8,XXREAL_0:2;
       i+(1+(k+1))=i+(1+k)+1;
       then f/.(i+(1+k))<f/.(i+(1+(k+1))) by A1,A10,A12;
      hence f/.i<f/.(i+(1+(k+1))) by A7,A8,A11,XXREAL_0:2;
     end;
    hence i+(1+(k+1))<=len f implies f/.i<f/.(i+(1+(k+1)));
   end;
  A13:for k being Element of NAT holds P[k] from NAT_1:sch 1(A5,A6);
  i+1<=j by A2,NAT_1:13;
  then j-'(i+1)=j-(i+1) by BINARITH:50;
  then i+(1+(j-'(i+1))) = j;
 then A14:f/.i<f/.j by A4,A13;
 f/.i=f.i by A2,FINSEQ_4:def 4;
 hence f.i<f.j by A2,A14,FINSEQ_4:def 4;
 end;
 hence i in dom f & j in dom f & i<j implies f.i<f.j;
 end;
hence thesis by GOBOARD1:def 1;
end;

registration
  let a, b, c, d;
  cluster closed_inside_of_rectangle(a,b,c,d) -> convex;
  coherence
  proof
    set P = closed_inside_of_rectangle(a,b,c,d);
A1: P = {p where p is Point of TOP-REAL 2:
    a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d} by JGRAPH_6:def 2;
    let w1, w2 be Point of TOP-REAL 2 such that
A2: w1 in P & w2 in P;
    let x be set such that
A3: x in LSeg(w1,w2);
A4: ex p3 being Point of TOP-REAL 2 st
    p3 = w1 & a <= p3`1 & p3`1 <= b & c <= p3`2 & p3`2 <= d by A1,A2;
A5: ex p4 being Point of TOP-REAL 2 st
    p4 = w2 & a <= p4`1 & p4`1 <= b & c <= p4`2 & p4`2 <= d by A1,A2;
    x in {(1-l)*w1 + l*w2 where l is Real: 0 <= l & l <= 1}
    by A3,TOPREAL1:def 3;
    then consider l being Real such that
A6: x = (1-l)*w1 + l*w2 & 0 <= l & l <= 1;
    set w = (1-l)*w1 + l*w2;
A7: w = |[((1-l)*w1)`1+(l*w2)`1,((1-l)*w1)`2+ (l*w2)`2]| by EUCLID:59;
    (1-l)*w1=|[ (1-l)*w1`1 ,(1-l)*w1`2 ]| & l*w2=|[ l*w2`1 ,l*w2`2 ]|
      by EUCLID:61;
    then ((1-l)*w1)`1= (1-l)*w1`1 & ((1-l)*w1)`2= (1-l)*w1`2
    & (l*w2)`1=l*w2`1 & (l*w2)`2=l*w2`2 by EUCLID:56;
    then w`1=(1-l)* w1`1+ l* w2`1 & w`2=(1-l)* w1`2+ l* w2`2 by A7,EUCLID:56;
    then a <= w`1 & w`1 <= b & c <= w`2 & w`2 <= d
      by A4,A5,A6,XREAL_1:175,176;
    hence thesis by A1,A6;
  end;
end;

registration
  let a, b, c, d;
  cluster Trectangle(a,b,c,d) -> convex;
  coherence
  proof
    set T = Trectangle(a,b,c,d);
    the carrier of T = closed_inside_of_rectangle(a,b,c,d) by PRE_TOPC:29;
    hence [#]T is convex Subset of TOP-REAL 2;
  end;
end;

theorem Th1:
  for e being positive (real number),
      g being continuous Function of I[01],TOP-REAL n
  ex h being FinSequence of REAL
   st h.1=0 & h.len h=1 & 5<=len h & rng h c= the carrier of I[01]
   & h is increasing
   & (for i being Element of NAT,Q being Subset of I[01],
    W being Subset of Euclid n
    st 1<=i & i<len h & Q=[.h/.i,h/.(i+1).] & W=g.:(Q) holds diameter(W)<e)
    proof
      let e be positive (real number),
      g be continuous Function of I[01],TOP-REAL n;
      reconsider e as positive Real by XREAL_0:def 1;
     A1:dom g=the carrier of I[01] by FUNCT_2:def 1;
     consider f being Function of
      Closed-Interval-MSpace(0,1),Euclid n such that A2: f=g by UNIFORM1:11;
     A3:f is uniformly_continuous by A2,UNIFORM1:9;
     A4:e/2<e by XREAL_1:218;
     A5:e/2>0 by XREAL_1:217;
     then consider s1 being Real such that
A6:  0<s1 and
A7:  for u1,u2 being Element of
     (Closed-Interval-MSpace(0,1)) st dist(u1,u2) < s1 holds
       dist((f/.u1),(f/.u2)) < e/2 by A3,UNIFORM1:def 1;
      set s=min(s1,1/2);
      A8:0<=s by A6,SQUARE_1:39;
      A9: s<>0 by A6,XXREAL_0:15;
      then A10:0<s by A6,SQUARE_1:39;
      A11:s<=s1 by XXREAL_0:17;
     A12: for u1,u2 being Element of
     (Closed-Interval-MSpace(0,1))
      st dist(u1,u2) < s holds dist((f/.u1),(f/.u2)) < e/2
      proof let u1,u2 be Element of
     (Closed-Interval-MSpace(0,1));
      assume dist(u1,u2) < s;
       then dist(u1,u2) < s1 by A11,XXREAL_0:2;
      hence dist((f/.u1),(f/.u2)) < e/2 by A7;
      end;
      s<=1/2 by XXREAL_0:17;
      then A13:2/s>=2/(1/2) by A8,A9,REAL_2:201;
      A14:s/2>0 by A8,A9,XREAL_1:217;
      2/s>0 by A8,A9,REAL_2:127;
      then reconsider j= [/ (2/s) \] as Element of NAT by INT_1:80;
      A15: 2/s <= [/ (2/s) \] by INT_1:def 5;
      A16:2/s<=j by INT_1:def 5;
      0<j by A10,A15,REAL_2:127;
      then A17: 0+1<=j by NAT_1:13;
      then A18:1 in Seg j by FINSEQ_1:3;
      A19:4<=j by A13,A16,XXREAL_0:2;
      2/s-j<=0 by A15,REAL_2:106;
      then 1+(2/s-j)<=1+0 by XREAL_1:8;
      then A20: s/2*(1+(2/s-j))<=s/2*1 by A14,XREAL_1:66;
    defpred P[Element of NAT,set] means $2=s/2*($1-1);
    A21:for k being Element of NAT,y1,y2 being set st
    k in Seg j &  P[k,y1]& P[k,y2] holds y1=y2;
    A22:for k being Element of NAT st k in Seg j ex x st P[k,x];
    consider p being FinSequence such that
     A23: dom p = Seg j & for k being Element of NAT st k in Seg j holds
     P[k,p.k] from FINSEQ_1:sch 1(A21,A22);
    A24:p.1=s/2*(1-1) by A18,A23 .=0;
    A25: Seg len p=Seg j by A23,FINSEQ_1:def 3;
    A26:len p=j by A23,FINSEQ_1:def 3;
    [/ (2/s) \] < 2/s+1 by INT_1:def 5;
    then [/ (2/s) \] -1 < 2/s+1-1 by XREAL_1:11;
    then A27:s/2*(j-1)<s/2*(2/s) by A14,XREAL_1:70;
    A28:s/2*(2/s)=(2*s)/(2*s) by XCMPLX_1:77;
    2*s<>0 by A9;
    then A29:(2*s)/(2*s)=1 by XCMPLX_1:60;
    then A30: 1-s/2*(j-1)=s/2*(1+(2/s- [/ (2/s) \])) by A28;
A31: for i being Element of NAT,r1,r2 being Real
 st 1<=i & i<len p & r1=p.i & r2=p.(i+1)
      holds r1<r2 & r2-r1=s/2
    proof let i be Element of NAT,r1,r2 be Real;assume
      A32: 1<=i & i<len p & r1=p.i & r2=p.(i+1);
      then A33:i in Seg j by A25,FINSEQ_1:3;
      A34:i<i+1 by NAT_1:13;
      1<i+1 & i+1<=len p by A32,NAT_1:13;
      then A35:(i+1) in Seg j by A25,FINSEQ_1:3;
      A36:r1=s/2*(i-1) by A23,A32,A33;
      A37:r2=s/2*(i+1-1) by A23,A32,A35;
      i-1<i+1-1 by A34,XREAL_1:11;
      hence r1<r2 by A14,A36,A37,XREAL_1:70;
      r2-r1=s/2*(i)-s/2*(i-1) by A23,A32,A33,A37;
     hence thesis;
    end;
A38:for i being Element of NAT,r1 being Real st 1<=i & i<=len p & r1=p.i
      holds r1<1
    proof let i be Element of NAT,r1 be Real;assume
      A39: 1<=i & i<=len p & r1=p.i;
      then i in Seg j by A25,FINSEQ_1:3;
      then A40:r1=s/2*(i-1) by A23,A39;
      i-1<=j-1 by A26,A39,XREAL_1:11;
      then (s/2)*(i-1)<=(s/2)*(j-1) by A14,XREAL_1:66;
     hence thesis by A27,A28,A29,A40,XXREAL_0:2;
    end;
    A41:for r1 being Real st r1=p.len p holds 1-r1<=s/2
    proof let r1 be Real;assume
A42:  r1=p.len p;
      len p in Seg j by A17,A26,FINSEQ_1:3;
     hence thesis by A20,A23,A26,A30,A42;
    end;
     rng (p^<*1*>) c= REAL
     proof let y;assume y in rng (p^<*1*>);
       then consider x such that
       A43: x in dom (p^<*1*>) & y=(p^<*1*>).x by FUNCT_1:def 5;
       A44:dom (p^<*1*>)=Seg len (p^<*1*>) by FINSEQ_1:def 3;
       reconsider nx=x as Element of NAT by A43;
       A45:1<=nx & nx<=len (p^<*1*>) by A43,A44,FINSEQ_1:3;
       A46: len (p^<*1*>)=len p+1 by FINSEQ_2:19;
       then A47:1<=nx & nx<=len p+1 by A43,A44,FINSEQ_1:3;
       per cases;
       suppose nx<len p+1;
         then A48:nx<=len p by NAT_1:13;
         then nx in Seg j by A25,A47,FINSEQ_1:3;
         then A49: p.nx=s/2*(nx-1) by A23;
         y=p.nx by A43,A45,A48,FINSEQ_1:85;
        hence y in REAL by A49;
       end;
       suppose nx>=len p+1;
         then nx=len p+1 by A45,A46,XXREAL_0:1;
         then y=1 by A43,FINSEQ_1:59;
        hence y in REAL;
       end;
     end;
    then reconsider h1=p^<*1*> as FinSequence of REAL by FINSEQ_1:def 4;
    A50:len h1 =len p+1 by FINSEQ_2:19;
    A51: 4+1<=len p+1 by A19,A26,XREAL_1:8;
    A52:h1.1=0 by A17,A24,A26,Lm2;
    A53:h1.len h1=1 by A50,FINSEQ_1:59;
    A54:rng p c=[.0,1.]
     proof let y be set;assume y in rng p;
       then consider x being set such that
       A55: x in dom p & y=p.x by FUNCT_1:def 5;
       A56:x in Seg len p by A55,FINSEQ_1:def 3;
       reconsider nx=x as Element of NAT by A55;
       A57:1<=nx & nx<=len p by A56,FINSEQ_1:3;
       A58:p.nx=s/2*(nx-1) by A23,A55;
       then reconsider ry=p.nx as Real;
       A59:ry<1 by A38,A57;
       A60:s/2>=0 by A8,REAL_2:125;
       nx-1>=1-1 by A57,XREAL_1:11;
       then ry>=0 by A58,A60,REAL_2:121;
       then y in {rs where rs is Real:0<=rs & rs<=1} by A55,A59;
      hence y in [.0,1.] by RCOMP_1:def 1;
     end;
    A61:rng <*1*> ={1} by FINSEQ_1:55;
    1 in {r where r is Real:0<=r & r<=1};
    then A62:1 in [.0,1.] by RCOMP_1:def 1;
     {1} c= [.0,1.]
     proof let x be set;assume x in {1};
      hence x in [.0,1.] by A62,TARSKI:def 1;
     end;
    then A63:[.0,1.] \/ {1} =[.0,1.] by XBOOLE_1:12;
    rng h1 =rng p \/ {1} by A61,FINSEQ_1:44;
    then A64: rng h1 c=[.0,1.] \/ {1} by A54,XBOOLE_1:13;
    Closed-Interval-TSpace(0,1)
        =TopSpaceMetr(Closed-Interval-MSpace(0,1)) by TOPMETR:def 8;
    then A65: the carrier of I[01] =the carrier of Closed-Interval-MSpace(0,1)
                      by TOPMETR:16,27
       .= [.0,1.] by TOPMETR:14;
    A66:for i being Element of NAT st 1<=i & i<len h1 holds h1/.i<h1/.(i+1)
     proof let i be Element of NAT;assume
       A67: 1<=i & i<len h1;
       then A68:i<=len p by A50,NAT_1:13;
       A69:i+1<=len h1 by A67,NAT_1:13;
       A70:1<i+1 by A67,NAT_1:13;
       per cases;
       suppose A71:i<len p;
         A72:h1.i=h1/.i by A67,FINSEQ_4:24;
         A73:h1.i=p.i by A67,A71,FINSEQ_1:85;
         A74:i+1<=len p by A71,NAT_1:13;
         A75:h1.(i+1)=h1/.(i+1) by A69,A70,FINSEQ_4:24;
        h1.(i+1)=p.(i+1) by A70,A74,FINSEQ_1:85;
        hence h1/.i<h1/.(i+1) by A31,A67,A71,A72,A73,A75;
       end;
       suppose i>=len p;
        then A76:i=len p by A68,XXREAL_0:1;
        A77:h1/.(i+1)=h1.(i+1) by A69,A70,FINSEQ_4:24
                  .=1 by A76,FINSEQ_1:59;
        h1/.i=h1.i by A67,FINSEQ_4:24.=p.i by A67,A68,FINSEQ_1:85;
        hence h1/.i<h1/.(i+1) by A38,A67,A68,A77;
       end;
     end;
    then A78:h1 is increasing by Lm3;
A79:for i being Element of NAT st 1<=i & i<len h1 holds h1/.(i+1)-h1/.i<=s/2
     proof let i be Element of NAT;assume
       A80: 1<=i & i<len h1;
       then A81:i<=len p by A50,NAT_1:13;
       A82:i+1<=len h1 by A80,NAT_1:13;
       A83:1<i+1 by A80,NAT_1:13;
       per cases;
       suppose A84:i<len p;
         A85:h1.i=h1/.i by A80,FINSEQ_4:24;
         A86:h1.i=p.i by A80,A84,FINSEQ_1:85;
         A87:i+1<=len p by A84,NAT_1:13;
         A88:h1.(i+1)=h1/.(i+1) by A82,A83,FINSEQ_4:24;
        h1.(i+1)=p.(i+1) by A83,A87,FINSEQ_1:85;
        hence h1/.(i+1)-h1/.i<=s/2 by A31,A80,A84,A85,A86,A88;
       end;
       suppose i>=len p;
         then A89:i=len p by A81,XXREAL_0:1;
         A90:h1/.(i+1)=h1.(i+1) by A82,A83,FINSEQ_4:24
                  .=1 by A89,FINSEQ_1:59;
        h1/.i=h1.i by A80,FINSEQ_4:24.=p.i by A80,A81,FINSEQ_1:85;
        hence h1/.(i+1)-h1/.i<=s/2 by A41,A89,A90;
       end;
     end;
     for i being Element of NAT,Q being Subset of I[01],
      W being Subset of Euclid n
      st 1<=i & i<len h1 & Q=[.h1/.i,h1/.(i+1).] & W=g.:(Q) holds
      diameter(W)<e
     proof let i be Element of NAT,Q be Subset of I[01],
       W be Subset of Euclid n;
       assume A91:1<=i & i<len h1 & Q=[.h1/.i,h1/.(i+1).] & W=g.:(Q);
        then h1/.i<h1/.(i+1) by A66;
        then A92:Q<>{} by A91,RCOMP_1:15;
        consider x1 being Element of Q;
        x1 in Q by A92;
        then A93: g.x1 in W by A1,A91,FUNCT_1:def 12;
        A94:for x,y being Point of Euclid n st
        x in W & y in W holds dist(x,y)<=e/2
         proof let x,y be Point of Euclid n;
           assume A95: x in W & y in W;
           then consider x3 being set such that
       A96:x3 in dom g & x3 in Q & x=g.x3 by A91,FUNCT_1:def 12;
           reconsider x3 as Element of
             Closed-Interval-MSpace(0,1) by A96,Lm1,TOPMETR:16;
           consider y3 being set such that
       A97:y3 in dom g & y3 in Q & y=g.y3 by A91,A95,FUNCT_1:def 12;
           reconsider y3 as Element of
             Closed-Interval-MSpace(0,1) by A97,Lm1,TOPMETR:16;
           A98:f.x3=(f/.x3) by CAT_3:def 1;
           A99:f.y3=(f/.y3) by CAT_3:def 1;
           reconsider r3=x3 as Real by A91,A96;
           reconsider s3=y3 as Real by A91,A97;
           A100:abs(r3-s3)<=h1/.(i+1)-h1/.i by A91,A96,A97,UNIFORM1:14;
           h1/.(i+1)-h1/.i<=s/2 by A79,A91;
           then abs(r3-s3)<=s/2 by A100,XXREAL_0:2;
           then A101:dist(x3,y3)<=s/2 by HEINE:1;
           s/2<s by A8,A9,XREAL_1:218;
           then dist(x3,y3)<s by A101,XXREAL_0:2;
          hence dist(x,y)<=e/2 by A2,A12,A96,A97,A98,A99;
         end;
        then W is bounded by A5,TBSP_1:def 9;
        then diameter(W)<=e/2 by A93,A94,TBSP_1:def 10;
      hence diameter(W)<e by A4,XXREAL_0:2;
     end;
   hence thesis by A50,A51,A52,A53,A63,A64,A65,A78;
  end;

theorem Th2:
  for P being Subset of TOP-REAL n st P c= LSeg(p1,p2) & p1 in P & p2 in P
  & P is connected holds P = LSeg(p1,p2)
  proof
    let P be Subset of TOP-REAL n;
  assume A1: P c= LSeg(p1,p2) & p1 in P & p2 in P
   & P is connected;
   reconsider L=LSeg(p1,p2) as non empty Subset of TOP-REAL n by A1;
  now assume not LSeg(p1,p2) c= P;then
   consider x0 being set such that
   A2: x0 in LSeg(p1,p2) & not x0 in P by TARSKI:def 3;
   reconsider p0=x0 as Point of TOP-REAL n by A2;
A3: p1 in LSeg(p1,p2) by TOPREAL1:6;then
   A4: LSeg(p1,p0) c= L by A2,TOPREAL1:12;
A5: LSeg(p1,p0)\{p0} c= LSeg(p1,p0) by XBOOLE_1:36;
   A6: the carrier of ((TOP-REAL n)|L)=[#]((TOP-REAL n)|L)
                                 .= L by PRE_TOPC:def 10;then
   reconsider R1=LSeg(p1,p0)\{p0} as Subset of (TOP-REAL n)|L
     by A5,XBOOLE_1:1,A4;
A7: p2 in LSeg(p1,p2) by TOPREAL1:6;then
   A8: LSeg(p0,p2) c= L by A2,TOPREAL1:12;
   LSeg(p0,p2)\{p0} c= LSeg(p0,p2) by XBOOLE_1:36;then
   reconsider R2=LSeg(p0,p2)\{p0} as Subset of (TOP-REAL n)|L
     by A6,XBOOLE_1:1,A8;
   reconsider PX=P as Subset of (TOP-REAL n)|L by A6,A1;
   reconsider PX1=LSeg(p1,p0) as Subset of (TOP-REAL n)|L
     by A3,A6,A2,TOPREAL1:12;
   reconsider PX2=LSeg(p0,p2) as Subset of (TOP-REAL n)|L
     by A7,A6,A2,TOPREAL1:12;
A9: (TOP-REAL n)|L is_T2 by TOPMETR:3;
   LSeg(p1,p0) is compact by SPPOL_1:28;then
   PX1 is compact by BORSUK_1:42;then
   PX1 is closed by COMPTS_1:16,A9;then
A10: Cl PX1 =PX1 by PRE_TOPC:52;
A11: PX is connected by JORDAN2C:15,A1;
A12: R2 c= PX2 by XBOOLE_1:36;
A13: R1 c= PX1 by XBOOLE_1:36;then
   Cl R1 c= Cl PX1 by PRE_TOPC:49;then
A14: (Cl R1) /\ R2 c= PX1 /\ R2 by A10,XBOOLE_1:27;
A15: PX1 /\ PX2 ={p0} by A2,TOPREAL1:14;
   PX1 /\ R2 =PX1 /\ PX2 \ {p0} by XBOOLE_1:49
   .= {} by A15,XBOOLE_1:37;then
   (Cl R1) /\ R2={} by A14,XBOOLE_1:3;then
A16: (Cl R1) misses R2 by XBOOLE_0:def 7;
   LSeg(p0,p2) is compact by SPPOL_1:28;then
   PX2 is compact by BORSUK_1:42;then
   PX2 is closed by COMPTS_1:16,A9;then
A17: Cl PX2 =PX2 by PRE_TOPC:52;
   R2 c= PX2 by XBOOLE_1:36;then
   Cl R2 c= Cl PX2 by PRE_TOPC:49;then
A18: R1 /\ (Cl R2) c= R1 /\ PX2 by A17,XBOOLE_1:27;
A19: PX1 /\ PX2 ={p0} by A2,TOPREAL1:14;
   R1 /\ PX2 = PX1 /\ PX2 \ {p0} by XBOOLE_1:49
   .= {} by A19,XBOOLE_1:37;then
   R1/\ (Cl R2)={} by A18,XBOOLE_1:3;then
   R1 misses (Cl R2) by XBOOLE_0:def 7;then
A20: R1,R2 are_separated by A16,CONNSP_1:def 1;
A21: {p0} c= LSeg(p1,p0)
    proof let d be set;assume d in {p0};then
     d=p0 by TARSKI:def 1;
     hence d in LSeg(p1,p0) by TOPREAL1:6;
    end;
A22: {p0} c= LSeg(p0,p2)
    proof let d be set;assume d in {p0};then
      d=p0 by TARSKI:def 1;
     hence d in LSeg(p0,p2) by TOPREAL1:6;
    end;
A23: L=LSeg(p1,p0) \/ LSeg(p0,p2) by TOPREAL1:11,A2
    .=(LSeg(p1,p0)\{p0})\/{p0} \/ LSeg(p0,p2) by A21,XBOOLE_1:45
    .=(LSeg(p1,p0)\{p0})\/ ({p0} \/ LSeg(p0,p2)) by XBOOLE_1:4
    .= R1 \/ PX2 by A22,XBOOLE_1:12
    .= R1 \/ ((PX2 \{p0}) \/ {p0}) by XBOOLE_1:45,A22
    .= R1 \/ {p0} \/ R2 by XBOOLE_1:4;
A24: P c= R1 \/ R2
   proof let z be set;assume A25: z in P;then
     z in R1 \/ {p0} or z in R2 by A23,A1,XBOOLE_0:def 2;then
     z in R1 or z in {p0} or z in R2 by XBOOLE_0:def 2;
    hence z in R1 \/ R2 by A25,A2,XBOOLE_0:def 2,TARSKI:def 1;
   end;
A26: now assume P c= R1;then
A27: p2 in R1 by A1;
     p2 in PX2 by TOPREAL1:6;then
     p2 in PX1 /\ PX2 by A27,A13,XBOOLE_0:def 3;
    hence contradiction by A2,A1,A19,TARSKI:def 1;
   end;
   now assume P c= R2;then
A28:  p1 in R2 by A1;
     p1 in PX1 by TOPREAL1:6;then
     p1 in PX1 /\ PX2 by A28,A12,XBOOLE_0:def 3;
    hence contradiction by A2,A1,A19,TARSKI:def 1;
   end;
   hence contradiction by A26,A20,A24,A11,CONNSP_1:17;
 end;
 hence P=LSeg(p1,p2) by XBOOLE_0:def 10,A1;
end;

theorem Th3:
  for g being Path of p1,p2 st rng g c= LSeg(p1,p2) holds
  rng g = LSeg(p1,p2)
  proof
    let g be Path of p1,p2;
    assume
A1: rng g c= LSeg(p1,p2);
A2: p1=g.0 & p2=g.1 by BORSUK_2:def 4;
  now assume A3: not LSeg(p1,p2) c= rng g;
    [#]I[01] is connected by CONNSP_1:28;then
    A4: g.:([#]I[01]) is connected by TOPS_2:75;
    0 in the carrier of I[01] by BORSUK_1:86;then
    A5: 0 in dom g by FUNCT_2:def 1;
    A6: p1 in g.:([#]I[01]) by A2,A5,FUNCT_1:def 12;
    1 in the carrier of I[01] by BORSUK_1:86;then
    A7: 1 in dom g by FUNCT_2:def 1;
    A8: p2 in g.:([#]I[01]) by A2,A7,FUNCT_1:def 12;
    A9: g.:([#]I[01]) c= rng g
    proof let y be set;assume y in g.:([#]I[01]);then
A10:   ex x being set st
     x in dom g & x in [#]I[01] & y=g.x by FUNCT_1:def 12;
     thus y in rng g by A10,FUNCT_1:def 5;
    end;then
    g.:([#]I[01]) c= LSeg(p1,p2) by A1,XBOOLE_1:1;
   hence contradiction by A9,A3,A6,A8,A4,Th2;
 end;
 hence rng g=LSeg(p1,p2) by A1,XBOOLE_0:def 10;
end;

:: Goboard Theorem in continuous case
theorem Th4:
  for P, Q being non empty Subset of TOP-REAL 2,
  p1, p2, q1, q2 being Point of TOP-REAL 2,
  f being Path of p1,p2, g being Path of q1,q2
  st rng f = P & rng g = Q &
  (for p being Point of TOP-REAL 2 st p in P holds p1`1<=p`1 & p`1<= p2`1) &
  (for p being Point of TOP-REAL 2 st p in Q holds p1`1<=p`1 & p`1<= p2`1) &
  (for p being Point of TOP-REAL 2 st p in P holds q1`2<=p`2 & p`2<= q2`2) &
  (for p being Point of TOP-REAL 2 st p in Q holds q1`2<=p`2 & p`2<= q2`2)
  holds P meets Q
  proof
    let P,Q be non empty Subset of TOP-REAL 2,
    p1,p2,q1,q2 be Point of TOP-REAL 2,
    f be Path of p1,p2,
    g be Path of q1,q2;
    assume that
A1: rng f = P and
A2: rng g = Q and
A3: for p being Point of TOP-REAL 2 st p in P holds p1`1<=p`1 & p`1<= p2`1
  and
A4: for p being Point of TOP-REAL 2 st p in Q holds p1`1<=p`1 & p`1<= p2`1
  and
A5: for p being Point of TOP-REAL 2 st p in P holds q1`2<=p`2 & p`2<= q2`2
  and
A6: for p being Point of TOP-REAL 2 st p in Q holds q1`2<=p`2 & p`2<= q2`2;
A7: f.0 = p1 & f.1 = p2 by BORSUK_2:def 4;
A8: g.0 = q1 & g.1 = q2 by BORSUK_2:def 4;
   reconsider P'=P,Q'=Q as Subset of TopSpaceMetr(Euclid 2);
   A9: [#](I[01]) is compact by COMPTS_1:10;
   f.:(the carrier of I[01])=rng f by FUNCT_2:45;then
A10: f.:([#](I[01]))=rng f;
   g.:(the carrier of I[01])=rng g by FUNCT_2:45;then
A11: g.:([#](I[01]))=rng g;
   A12: 0 in the carrier of I[01] by BORSUK_1:86;then
   0 in dom f by FUNCT_2:def 1;then
   A13: p1 in P' by A1,A7,FUNCT_1:def 5;
   0 in dom g by FUNCT_2:def 1,A12;then
   A14: q1 in Q' by A2,A8,FUNCT_1:def 5;
   A15:P' <> {} & P' is compact & Q' <> {}
   & Q' is compact by A2,A10,A11,A1,A9,WEIERSTR:14;
   assume A16: P /\ Q= {};
     then P misses Q by XBOOLE_0:def 7;
     then A17:min_dist_min(P',Q')>0 by A15,JGRAPH_1:55;
     set e=min_dist_min(P',Q')/5;
A18: e>0/5 by A17,REAL_1:73;
     then consider h being FinSequence of REAL such that
     A19: h.1=0 & h.len h=1 & 5<=len h & rng h c= the carrier of I[01]
     & h is increasing
     &(for i being Element of NAT,R being Subset of I[01],
     W being Subset of Euclid 2
      st 1<=i & i<len h & R=[.h/.i,h/.(i+1).] & W=f.:(R) holds
     diameter(W)<e) by Th1;
     deffunc F(Element of NAT)=f.(h.$1);
     ex h1' being FinSequence st len h1'=len h &
     for i st i in Seg len h holds h1'.i=F(i) from FINSEQ_1:sch 2;
     then consider h1' being FinSequence such that
     A20:len h1'=len h &
     for i st i in Seg len h holds h1'.i=f.(h.i);
     rng h1' c= the carrier of TOP-REAL 2
      proof let y;assume y in rng h1';
        then consider x such that
        A21:x in dom h1' & y=h1'.x by FUNCT_1:def 5;
        A22:dom h1'=Seg len h1' by FINSEQ_1:def 3;
        reconsider i=x as Element of NAT by A21;
        A23: h1'.i=f.(h.i) by A20,A21,A22;
        i in dom h by A20,A21,A22,FINSEQ_1:def 3;
        then A24:h.i in rng h by FUNCT_1:def 5;
        dom f= the carrier of I[01] by FUNCT_2:def 1;
        then h1'.i in rng f by A19,A23,A24,FUNCT_1:def 5;
       hence thesis by A21;
      end;
     then reconsider h1=h1' as FinSequence of TOP-REAL 2 by FINSEQ_1:def 4;
A25: dom h=Seg len h by FINSEQ_1:def 3;
     A26:for i st 1<=i & i+1<=len h1 holds |.h1/.i-h1/.(i+1).|<e
      proof let i;assume A27:1<=i & i+1<=len h1;
        then A28:i<len h1 by NAT_1:13;
        A29:1<i+1 by A27,NAT_1:13;
        then A30:i+1 in Seg len h by A20,A27,FINSEQ_1:3;
        A31: i in Seg len h by A20,A27,A28,FINSEQ_1:3;
        then A32:h1'.i=f.(h.i) by A20;
        then A33:h1/.i=f.(h.i) by A27,A28,FINSEQ_4:24;
        A34:h1'.(i+1)=f.(h.(i+1)) by A20,A30;
        then A35:h1/.(i+1)=f.(h.(i+1)) by A27,A29,FINSEQ_4:24;
        A36:i in dom h by A20,A27,A28,FINSEQ_3:27;
        then A37:h.i in rng h by FUNCT_1:def 5;
A38:    dom f =the carrier of I[01] by FUNCT_2:def 1;
        then h1'.i in rng f by A19,A32,A37,FUNCT_1:def 5; then
A39:    f.(h.i) is Element of TOP-REAL 2 by A20,A31;
        i+1 in dom h by A20,A27,A29,FINSEQ_3:27;
        then h.(i+1) in rng h by FUNCT_1:def 5;
        then h1'.(i+1) in rng f by A19,A34,A38,FUNCT_1:def 5;
        then A40:f.(h.(i+1)) is Element of TOP-REAL 2 by A20,A30;
        A41:h.i=h/.i by A20,A27,A28,FINSEQ_4:24;
        A42:i+1 in dom h by A20,A27,A29,FINSEQ_3:27;
        then A43:h.(i+1) in rng h by FUNCT_1:def 5;
        A44:h.(i+1)=h/.(i+1) by A20,A27,A29,FINSEQ_4:24;
        i<i+1 by NAT_1:13;
        then A45:0<=h/.i & h/.i <= h/.(i+1) & h/.(i+1)<=1
         by A19,A36,A37,A41,A42,A43,A44,BORSUK_1:83,GOBOARD1:def 1
,RCOMP_1:48;
       reconsider W1=P as Subset of Euclid 2 by TOPREAL3:13;
        reconsider Pa=P as Subset of TOP-REAL 2;
        A46: [#](I[01]) is compact by COMPTS_1:10;
        f.:(the carrier of I[01])=rng f by FUNCT_2:45;then
        f.:([#](I[01]))=rng f;then
        A47:Pa is compact by A1,A46,WEIERSTR:14;
        reconsider R=[.h/.i,h/.(i+1).] as Subset of I[01]
                          by A19,A37,A41,A43,A44,BORSUK_1:83,RCOMP_1:16;
        reconsider w1=f.(h.i),w2=f.(h.(i+1)) as Point of Euclid 2
                          by A39,A40,TOPREAL3:13;
        A48: h.i in R by A41,A45,RCOMP_1:48;
        h.(i+1) in R by A44,A45,RCOMP_1:48;
        then A49:w1 in f.:R & w2 in f.:R by A38,A48,FUNCT_1:def 12;
        reconsider W=f.:R as Subset of Euclid 2 by TOPREAL3:13;
        A50:dom f=[#](I[01]) & rng f=P by A1,A38;
        [#](I[01])=[.0,1.] by BORSUK_1:83;
        then P=f.:([.0,1.]) by A50,RELAT_1:146;
        then A51:W c= W1 by BORSUK_1:83,RELAT_1:156;
        0 in the carrier of I[01] by BORSUK_1:86;then
        f.0 in rng f by A38,FUNCT_1:def 5;then
        A52: W-bound Pa <= p1`1 & p1`1 <= E-bound Pa &
        S-bound Pa <= p1`2 & p1`2 <= N-bound Pa by A7,A1,A47,PSCOMP_1:71;
        set r1=((E-bound Pa) - (W-bound Pa))+((N-bound Pa) - (S-bound Pa))+1;
               (W-bound Pa)<=(E-bound Pa) by A52,XXREAL_0:2;
        then A53:0<=((E-bound Pa) - (W-bound Pa)) by XREAL_1:50;
               (S-bound Pa)<=(N-bound Pa) by A52,XXREAL_0:2;
        then 0<=((N-bound Pa) - (S-bound Pa)) by XREAL_1:50;
        then A54:((E-bound Pa) - (W-bound Pa))+((N-bound Pa) - (S-bound Pa))
            >=0+0 by A53,XREAL_1:9;
         A55: r1> ((E-bound Pa) - (W-bound Pa))+((N-bound Pa) - (S-bound Pa))
                   by XREAL_1:31;
         for x,y being Point of Euclid 2
         st x in W1 & y in W1 holds dist(x,y)<=r1
         proof let x,y be Point of Euclid 2;
           assume A56: x in W1 & y in W1;
           then reconsider pw1=x,pw2=y as Point of TOP-REAL 2;
           A57:dist(x,y)=|.pw1-pw2.| by JGRAPH_1:45;
           A58:|.pw1-pw2.|<=abs(pw1`1-pw2`1)+abs(pw1`2-pw2`2) by JGRAPH_1:49;
           A59:W-bound Pa <= pw1`1 & pw1`1 <= E-bound Pa &
           S-bound Pa <= pw1`2 & pw1`2 <= N-bound Pa by A47,A56,PSCOMP_1:71;
           A60:W-bound Pa <= pw2`1 & pw2`1 <= E-bound Pa &
                 S-bound Pa <= pw2`2 & pw2`2 <= N-bound Pa
                            by A47,A56,PSCOMP_1:71;
          then A61:abs(pw1`1-pw2`1)<=(E-bound Pa) - (W-bound Pa)
                           by A59,JGRAPH_1:31;
               abs(pw1`2-pw2`2)<=(N-bound Pa) - (S-bound Pa)
                           by A59,A60,JGRAPH_1:31;
           then A62:abs(pw1`1-pw2`1)+abs(pw1`2-pw2`2)
           <=((E-bound Pa) - (W-bound Pa))+((N-bound Pa) - (S-bound Pa))
                         by A61,XREAL_1:9;
               ((E-bound Pa) - (W-bound Pa))+((N-bound Pa) - (S-bound Pa))
           <= ((E-bound Pa) - (W-bound Pa))+((N-bound Pa) - (S-bound Pa))
            +1 by XREAL_1:31;
           then abs(pw1`1-pw2`1)+abs(pw1`2-pw2`2)<=r1 by A62,XXREAL_0:2;
          hence thesis by A57,A58,XXREAL_0:2;
         end;
        then W1 is bounded by A54,A55,TBSP_1:def 9;
        then A63:W is bounded by A51,TBSP_1:21;
        A64: diameter W<e by A19,A20,A27,A28;
        dist(w1,w2)<=diameter(W) by A49,A63,TBSP_1:def 10;
        then dist(w1,w2)<e by A64,XXREAL_0:2;
       hence thesis by A33,A35,JGRAPH_1:45;
      end;
     A65:len h1>=1 by A19,A20,XXREAL_0:2;
     consider hb being FinSequence of REAL such that
     A66: hb.1=0 & hb.len hb=1 & 5<=len hb & rng hb c= the carrier of I[01]
     & hb is increasing & (for i being Element of NAT,R being Subset of I[01],
     W being Subset of Euclid 2
      st 1<=i & i<len hb & R=[.hb/.i,hb/.(i+1).] & W=g.:(R) holds
     diameter(W)<e) by A18,Th1;
     A67: 1<=len hb by A66,XXREAL_0:2;
     then A68:1 in dom hb by FINSEQ_3:27;
     A69:len hb in dom hb by A67,FINSEQ_3:27;
     deffunc F(Element of NAT)=g.(hb.$1);
     ex h2' being FinSequence st len h2'=len hb &
     for i st i in Seg len hb holds h2'.i=F(i) from FINSEQ_1:sch 2;
     then consider h2' being FinSequence such that
     A70:len h2'=len hb &
     for i st i in Seg len hb holds h2'.i=g.(hb.i);
     rng h2' c= the carrier of TOP-REAL 2
      proof let y;assume y in rng h2';
        then consider x such that
        A71:x in dom h2' & y=h2'.x by FUNCT_1:def 5;
        A72:dom h2'=Seg len h2' by FINSEQ_1:def 3;
        reconsider i=x as Element of NAT by A71;
        A73: h2'.i=g.(hb.i) by A70,A71,A72;
        i in dom hb by A70,A71,A72,FINSEQ_1:def 3;
        then A74:hb.i in rng hb by FUNCT_1:def 5;
        dom g = the carrier of I[01] by FUNCT_2:def 1;
        then h2'.i in rng g by A66,A73,A74,FUNCT_1:def 5;
        hence thesis by A71;
      end;
     then reconsider h2=h2' as FinSequence of TOP-REAL 2 by FINSEQ_1:def 4;
     A75:dom hb=Seg len hb by FINSEQ_1:def 3;
A76: for i st i in dom hb holds h2/.i=g.(hb.i)
      proof let i;assume
A77:   i in dom hb;
       then A78:h2.i=g.(hb.i) by A70,A75;
       1<=i & i<=len hb by A75,A77,FINSEQ_1:3;
       hence thesis by A70,A78,FINSEQ_4:24;
      end;
     A79:len h2>=1 by A66,A70,XXREAL_0:2;
     A80:for i st i in dom h1 holds
 (h1/.1)`1<=(h1/.i)`1 & (h1/.i)`1<=(h1/.len h1)`1
  proof let i;assume A81:i in dom h1;
     then A82: i in Seg len h by A20,FINSEQ_1:def 3;
     then A83:i in dom h by FINSEQ_1:def 3;
     h1.i=f.(h.i) by A20,A82;
     then A84:h1/.i=f.(h.i) by A81,FINSEQ_4:def 4;
     1 in dom h by A20,A65,FINSEQ_3:27;
     then h1.1=f.(h.1) by A20,A25;
     then A85:h1/.1=f.(h.1) by A65,FINSEQ_4:24;
     len h in dom h by A20,A65,FINSEQ_3:27;
     then h1.len h1=f.(h.len h) by A20,A25;
     then A86:h1/.len h1=f.(h.len h) by A65,FINSEQ_4:24;
     A87:h.i in rng h by A83,FUNCT_1:def 5;
     dom f =the carrier of I[01] by FUNCT_2:def 1;
     then h1/.i in rng f by A19,A84,A87,FUNCT_1:def 5;
     hence thesis by A7,A3,A1,A19,A85,A86;
  end;
A88: for i st i in dom X_axis h1 holds
  (X_axis h1).1<=(X_axis h1).i & (X_axis h1).i<=(X_axis h1).len h1
  proof let i;assume i in dom X_axis h1;
     then i in Seg len (X_axis h1) by FINSEQ_1:def 3;
     then i in Seg len h1 by A65,JGRAPH_1:59;
     then A89:i in dom h1 by FINSEQ_1:def 3;
     then A90:(X_axis h1).i = (h1/.i)`1 by JGRAPH_1:61;
     A91:(X_axis h1).1=(h1/.1)`1 by A65,JGRAPH_1:59;
     (X_axis h1).len h1=(h1/.len h1)`1 by A65,JGRAPH_1:59;
    hence thesis by A80,A89,A90,A91;
  end;
 A92:for i st i in dom h1 holds
 q1`2<=(h1/.i)`2 & (h1/.i)`2<=q2`2
  proof let i;assume A93:i in dom h1;
    then A94: i in Seg len h1 by FINSEQ_1:def 3;
    then A95:i in dom h by A20,FINSEQ_1:def 3;
    h1.i=f.(h.i) by A20,A94;
    then A96:h1/.i=f.(h.i) by A93,FINSEQ_4:def 4;
    A97:h.i in rng h by A95,FUNCT_1:def 5;
    dom f =the carrier of I[01] by FUNCT_2:def 1;
    then h1/.i in rng f by A19,A96,A97,FUNCT_1:def 5;
    hence thesis by A1,A5;
  end;
  for i st i in dom (Y_axis h1) holds
   q1`2<=(Y_axis h1).i & (Y_axis h1).i<=q2`2
  proof let i;assume i in dom (Y_axis h1);
    then i in Seg len (Y_axis h1) by FINSEQ_1:def 3;
    then i in Seg len h1 by A65,JGRAPH_1:60;
    then A98:i in dom h1 by FINSEQ_1:def 3;
    then (Y_axis h1).i = (h1/.i)`2 by JGRAPH_1:61;
    hence thesis by A92,A98;
  end;
 then A99:X_axis(h1) lies_between (X_axis(h1)).1, (X_axis(h1)).(len h1) &
 Y_axis(h1) lies_between q1`2,q2`2 by A88,GOBOARD4:def 2;
 A100:for i st i in dom h2 holds
 (h2/.1)`2<=(h2/.i)`2 & (h2/.i)`2<=(h2/.len h2)`2
  proof let i;assume i in dom h2;
   then i in Seg len h2 by FINSEQ_1:def 3;
   then A101:i in dom hb by A70,FINSEQ_1:def 3;
   then A102:h2/.i=g.(hb.i) by A76;
   1 in Seg len hb by A70,A79,FINSEQ_1:3;
   then 1 in dom hb by FINSEQ_1:def 3;
   then A103:h2/.1=g.(hb.1) by A76;
   len hb in Seg len hb by A70,A79,FINSEQ_1:3;
   then len hb in dom hb by FINSEQ_1:def 3;
   then A104:h2/.len h2=g.(hb.len hb) by A70,A76;
   A105:hb.i in rng hb by A101,FUNCT_1:def 5;
   dom g =the carrier of I[01] by FUNCT_2:def 1;
   then h2/.i in rng g by A66,A102,A105,FUNCT_1:def 5;
   hence thesis by A2,A8,A6,A66,A103,A104;
  end;
 A106: for i st i in dom (Y_axis(h2)) holds
  (Y_axis(h2)).1<=(Y_axis(h2)).i & (Y_axis(h2)).i <=(Y_axis(h2)).(len h2)
  proof let i;assume i in dom (Y_axis(h2));
    then i in Seg len (Y_axis(h2)) by FINSEQ_1:def 3;
    then i in Seg len h2 by A79,JGRAPH_1:60;
    then A107:i in dom h2 by FINSEQ_1:def 3;
    then A108:(Y_axis(h2)).i = (h2/.i)`2 by JGRAPH_1:61;
    A109:(Y_axis(h2)).1=(h2/.1)`2 by A79,JGRAPH_1:60;
    (Y_axis(h2)).len h2=(h2/.len h2)`2 by A79,JGRAPH_1:60;
    hence thesis by A100,A107,A108,A109;
  end;
 A110:for i st i in dom h2 holds
 p1`1<=(h2/.i)`1 & (h2/.i)`1<=p2`1
  proof let i;assume i in dom h2;
     then i in Seg len h2 by FINSEQ_1:def 3;
     then A111:i in dom hb by A70,FINSEQ_1:def 3;
     then A112:h2/.i=g.(hb.i) by A76;
     A113:hb.i in rng hb by A111,FUNCT_1:def 5;
     dom g =the carrier of I[01] by FUNCT_2:def 1;
     then h2/.i in rng g by A66,A112,A113,FUNCT_1:def 5;
     hence thesis by A2,A4;
  end;
  for i st i in dom (X_axis(h2)) holds
   p1`1<= (X_axis(h2)).i & (X_axis(h2)).i<=p2`1
  proof let i;assume i in dom (X_axis(h2));
    then i in Seg len (X_axis(h2)) by FINSEQ_1:def 3;
    then i in Seg len h2 by A79,JGRAPH_1:59;
    then A114:i in dom h2 by FINSEQ_1:def 3;
    then (X_axis(h2)).i = (h2/.i)`1 by JGRAPH_1:61;
    hence thesis by A110,A114;
  end;
 then A115:X_axis(h2) lies_between p1`1, p2`1 &
 Y_axis(h2) lies_between (Y_axis(h2)).1, (Y_axis(h2)).(len h2)
                                  by A106,GOBOARD4:def 2;
 A116:for i st 1<=i & i+1<=len h2 holds
   |. (h2/.i)-(h2/.(i+1)) .|<e
      proof let i;assume A117:1<=i & i+1<=len h2;
        then A118:i<len h2 by NAT_1:13;
        A119:1<i+1 by A117,NAT_1:13;
        then A120:i+1 in Seg len hb by A70,A117,FINSEQ_1:3;
        i in Seg len hb by A70,A117,A118,FINSEQ_1:3;
        then A121:h2'.i=g.(hb.i) by A70;
        then A122:h2/.i=g.(hb.i) by A117,A118,FINSEQ_4:24;
        A123:h2'.(i+1)=g.(hb.(i+1)) by A70,A120;
        h2.(i+1)=h2/.(i+1) by A117,A119,FINSEQ_4:24;
        then A124:h2/.(i+1)=g.(hb.(i+1)) by A70,A120;
        i in Seg len hb by A70,A117,A118,FINSEQ_1:3;
        then A125:i in dom hb by FINSEQ_1:def 3;
        then A126:hb.i in rng hb by FUNCT_1:def 5;
        A127: dom g =the carrier of I[01] by FUNCT_2:def 1;
        then A128:h2'.i in rng g by A66,A121,A126,FUNCT_1:def 5;
        i+1 in Seg len hb by A70,A117,A119,FINSEQ_1:3;
        then i+1 in dom hb by FINSEQ_1:def 3;
        then hb.(i+1) in rng hb by FUNCT_1:def 5;
        then h2'.(i+1) in rng g by A66,A123,A127,FUNCT_1:def 5;
   then A129:g.(hb.(i+1)) is Element of TOP-REAL 2 by A70,A120;
        A130:hb.i=hb/.i by A70,A117,A118,FINSEQ_4:24;
        i+1 in Seg len hb by A70,A117,A119,FINSEQ_1:3;
        then A131:i+1 in dom hb by FINSEQ_1:def 3;
        then A132:hb.(i+1) in rng hb by FUNCT_1:def 5;
        A133:hb.(i+1)=hb/.(i+1) by A70,A117,A119,FINSEQ_4:24;
        i<i+1 by NAT_1:13;
        then A134:0<=hb/.i & hb/.i <= hb/.(i+1) & hb/.(i+1)<=1
          by A66,A125,A126,A130,A131,A132,A133,BORSUK_1:83,GOBOARD1:def 1
,RCOMP_1:48;
       reconsider W1=Q as Subset of Euclid 2 by TOPREAL3:13;
        reconsider Qa=Q as Subset of TOP-REAL 2;
        A135: [#](I[01]) is compact by COMPTS_1:10;
        g.:(the carrier of I[01])=rng g by FUNCT_2:45;then
        g.:([#](I[01]))=rng g;then
        A136:Qa is compact by A2,A135,WEIERSTR:14;
        reconsider Qa as non empty Subset of TOP-REAL 2;
        reconsider R=[.hb/.i,hb/.(i+1).] as Subset of I[01]
           by A66,A126,A130,A132,A133,BORSUK_1:83,RCOMP_1:16;
        reconsider w1=g.(hb.i),w2=g.(hb.(i+1)) as Point of Euclid 2
                                   by A121,A128,A129,TOPREAL3:13;
        A137: hb.i in R by A130,A134,RCOMP_1:48;
        hb.(i+1) in R by A133,A134,RCOMP_1:48;
        then A138:w1 in g.:(R) & w2 in g.:(R) by A127,A137,FUNCT_1:def 12;
        reconsider W=g.:(R) as Subset of Euclid 2 by TOPREAL3:13;
A139:   dom g=[#](I[01]) & rng g=Q by A2,A127;
        [#](I[01])=[.0,1.] by BORSUK_1:83;
        then Q=g.:([.0,1.]) by A139,RELAT_1:146;
        then A140:W c= W1 by BORSUK_1:83,RELAT_1:156;
        0 in the carrier of I[01] by BORSUK_1:86;then
        g.0 in rng g by A127,FUNCT_1:def 5;then
A141:   W-bound Qa <= q1`1 & q1`1 <= E-bound Qa &
        S-bound Qa <= q1`2 & q1`2 <= N-bound Qa by A2,A8,A136,PSCOMP_1:71;
        set r1=
           ((E-bound Qa) - (W-bound Qa))+((N-bound Qa) - (S-bound Qa))+1;
               (W-bound Qa)<=(E-bound Qa) by A141,XXREAL_0:2;
           then A142:0<=((E-bound Qa) - (W-bound Qa)) by XREAL_1:50;
               (S-bound Qa)<=(N-bound Qa) by A141,XXREAL_0:2;
           then 0<=((N-bound Qa) - (S-bound Qa)) by XREAL_1:50;
        then A143:((E-bound Qa) - (W-bound Qa))+((N-bound Qa) - (S-bound Qa))
            >=0+0 by A142,XREAL_1:9;
         A144: r1> ((E-bound Qa) - (W-bound Qa))+((N-bound Qa) - (S-bound Qa))
                             by XREAL_1:31;
            for x,y being Point of Euclid 2
         st x in W1 & y in W1 holds dist(x,y)<=r1
         proof let x,y be Point of Euclid 2;
           assume A145: x in W1 & y in W1;
           then reconsider pw1=x,pw2=y as Point of TOP-REAL 2;
           A146:dist(x,y)=|.pw1-pw2.| by JGRAPH_1:45;
           A147:|.pw1-pw2.|<=abs(pw1`1-pw2`1)+abs(pw1`2-pw2`2) by JGRAPH_1:49;
           A148:W-bound Qa <= pw1`1 & pw1`1 <= E-bound Qa &
                 S-bound Qa <= pw1`2 & pw1`2 <= N-bound Qa
                              by A136,A145,PSCOMP_1:71;
           A149:W-bound Qa <= pw2`1 & pw2`1 <= E-bound Qa &
                 S-bound Qa <= pw2`2 & pw2`2 <= N-bound Qa
                              by A136,A145,PSCOMP_1:71;
           then A150:abs(pw1`1-pw2`1)<=(E-bound Qa) - (W-bound Qa)
                           by A148,JGRAPH_1:31;
           abs(pw1`2-pw2`2)<=(N-bound Qa) - (S-bound Qa)
                          by A148,A149,JGRAPH_1:31;
           then A151:abs(pw1`1-pw2`1)+abs(pw1`2-pw2`2)
           <=((E-bound Qa) - (W-bound Qa))+((N-bound Qa) - (S-bound Qa))
                         by A150,XREAL_1:9;
               ((E-bound Qa) - (W-bound Qa))+((N-bound Qa) - (S-bound Qa))
           <=
           ((E-bound Qa) - (W-bound Qa))+((N-bound Qa) - (S-bound Qa))
            +1 by XREAL_1:31;
           then abs(pw1`1-pw2`1)+abs(pw1`2-pw2`2)<=r1 by A151,XXREAL_0:2;
          hence thesis by A146,A147,XXREAL_0:2;
         end;
        then W1 is bounded by A143,A144,TBSP_1:def 9;
        then A152:W is bounded by A140,TBSP_1:21;
        A153: diameter(W)<e by A66,A70,A117,A118;
        dist(w1,w2)<=diameter(W) by A138,A152,TBSP_1:def 10;
        then dist(w1,w2)<e by A153,XXREAL_0:2;
       hence thesis by A122,A124,JGRAPH_1:45;
      end;
     consider f2 being FinSequence of TOP-REAL 2 such that
     A154: f2 is special &
     f2.1=h1.1 & f2.len f2=h1.len h1 & len f2>=len h1 &
     X_axis(f2) lies_between (X_axis(h1)).1, (X_axis(h1)).(len h1) &
     Y_axis(f2) lies_between q1`2,q2`2 &
     (for j st j in dom f2 holds
     ex k being Element of NAT st k in dom h1 & |.(f2/.j - h1/.k).|<e)
     &(for j st 1<=j & j+1<=len f2 holds |.f2/.j - f2/.(j+1).|<e)
                                  by A18,A26,A65,A99,JGRAPH_1:56;
     A155:len f2>=1 by A65,A154,XXREAL_0:2;
     consider g2 being FinSequence of TOP-REAL 2 such that
     A156: g2 is special &
     g2.1=h2.1 & g2.len g2=h2.len h2 & len g2>=len h2 &
     Y_axis(g2) lies_between (Y_axis(h2)).1, (Y_axis(h2)).(len h2) &
     X_axis(g2) lies_between p1`1,p2`1 &
     (for j st j in dom g2 holds
     ex k being Element of NAT st k in dom h2 & |.((g2/.j) - h2/.k).|<e)&
     (for j st 1<=j & j+1<=len g2 holds |.(g2/.j) - g2/.(j+1).|<e)
               by A18,A79,A115,A116,JGRAPH_1:57;
     A157:len g2>=1 by A79,A156,XXREAL_0:2;
     A158:1 in dom h by A20,A65,FINSEQ_3:27;
     A159:len h in dom h by A20,A65,FINSEQ_3:27;
     A160: h1.1=f.(h.1) by A20,A25,A158;
     A161:h1.1=h1/.1 by A65,FINSEQ_4:24;
     A162:h1.len h1=h1/.len h1 by A65,FINSEQ_4:24;
     A163:f2.1=f2/.1 by A155,FINSEQ_4:24;
     A164:f2.len f2=f2/.len f2 by A155,FINSEQ_4:24;
     A165:X_axis(f2).1=p1`1
     by A7,A19,A154,A155,A160,A163,JGRAPH_1:59;
     (h1/.len h1)=p2 by A7,A19,A20,A25,A159,A162;
     then A166:X_axis(f2).len f2=p2`1 by A154,A155,A162,A164,JGRAPH_1:59;
     A167:(h2/.1)=q1 by A8,A66,A68,A76;
     A168:h2.1=h2/.1 by A67,A70,FINSEQ_4:24;
     A169:h2.len h2=h2/.len h2 by A67,A70,FINSEQ_4:24;
     g2/.1=g2.1 by A157,FINSEQ_4:24;
     then A170:Y_axis(g2).1=q1`2 by A156,A157,A167,A168,JGRAPH_1:60;
     A171:g2/.len g2=g2.len g2 by A157,FINSEQ_4:24;
     (g2.len g2)=q2 by A8,A66,A69,A70,A76,A156,A169;
     then A172:Y_axis(g2).len g2=q2`2 by A157,A171,JGRAPH_1:60;
     A173:(X_axis f2).1=(h1/.1)`1 by A154,A155,A161,A163,JGRAPH_1:59
     .=(X_axis h1).1 by A65,JGRAPH_1:59;
     A174:X_axis(f2).len f2=(h1/.len h1)`1 by A154,A155,A162,A164,JGRAPH_1:59
     .=X_axis(h1).len h1 by A65,JGRAPH_1:59;
     g2.1=g2/.1 by A157,FINSEQ_4:24;
     then A175:(Y_axis(g2)).1=(h2/.1)`2 by A156,A157,A168,JGRAPH_1:60
     .=(Y_axis(h2)).1 by A79,JGRAPH_1:60;
         g2.len g2=g2/.len g2 by A157,FINSEQ_4:24;
     then A176: Y_axis(g2).len g2=(h2/.len h2)`2 by A156,A157,A169,JGRAPH_1:60
     .=Y_axis(h2).len h2 by A79,JGRAPH_1:60;
     5<=len f2 by A19,A20,A154,XXREAL_0:2;
     then A177:2<=len f2 by XXREAL_0:2;
     5<=len g2 by A66,A70,A156,XXREAL_0:2;
     then A178:2<=len g2 by XXREAL_0:2;
     A179:1<=len h by A19,XXREAL_0:2;
     then 1 in dom h by FINSEQ_3:27;
     then A180:h1/.1=f.(h.1) by A20,A25,A161;
     A181: len h in dom h by A179,FINSEQ_3:27;
    now per cases;
    case A182: p1=p2;
      A183: for q being Point of TOP-REAL 2 st q in Q holds q`1=p1`1
       proof let q be Point of TOP-REAL 2;
        assume q in Q;then p1`1<=q`1 & q`1<= p2`1 by A4;
        hence q`1=p1`1 by A182,XXREAL_0:1;
       end;
       0 in the carrier of I[01] by BORSUK_1:86;then
       0 in dom g by FUNCT_2:def 1;then
       A184: q1 in Q by A2,A8,FUNCT_1:12;
       1 in the carrier of I[01] by BORSUK_1:86;then
       1 in dom g by FUNCT_2:def 1;then
       A185: q2 in Q by A2,A8,FUNCT_1:12;
       0 in the carrier of I[01] by BORSUK_1:86;then
       0 in dom f by FUNCT_2:def 1;then
A186:  p1 in P by A7,A1,FUNCT_1:12;
      A187: now assume A188: q1`2=q2`2;
         q1`2<=p1`2 & p1`2<=q2`2 by A5,A186;then
         A189: p1`2=q1`2 by A188,XXREAL_0:1;
         q1`1=p1`1 by A183,A184;then
         q1=p1 by A189,TOPREAL3:11;
        hence contradiction by A16,A184,A186,XBOOLE_0:def 3;
      end;
      A190: Q c= LSeg(q1,q2)
       proof let z be set;assume
         A191: z in Q;then
         reconsider qz=z as Point of TOP-REAL 2;
A192:      p1`1<=qz`1 & qz`1<= p2`1 by A191,A4;
         p1`1<=q1`1 & q1`1<= p2`1 by A184,A4;then
         A193: p1`1=q1`1 by XXREAL_0:1,A182;
         p1`1<=q2`1 & q2`1<= p2`1 by A185,A4;then
         A194: p1`1=q2`1 by XXREAL_0:1,A182;
         A195: q1`2<=qz`2 & qz`2<=q2`2 by A191,A6;then
         q2`2>=q1`2 by XXREAL_0:2;then
         q2`2>q1`2 by A187,REAL_1:def 5;then
         A196: q2`2-q1`2>0 by XREAL_1:52;
         A197: qz`2-q1`2>=0 by XREAL_1:50,A195;
         set ln=(qz`2-q1`2)/(q2`2-q1`2);
         A198: ln>=0 by A196,A197,REAL_2:125;
         qz`2-q1`2<=q2`2-q1`2 by A195,REAL_1:92;then
         ln<= (q2`2-q1`2)/(q2`2-q1`2) by A196,REAL_1:73;then
         A199: ln <= 1 by A196,XCMPLX_1:60;
         A200: q2`2-q1`2 <>0 by A187;
         1-ln=(q2`2-q1`2)/(q2`2-q1`2)-(qz`2-q1`2)/(q2`2-q1`2)
                               by XCMPLX_1:60,A200
            .=((q2`2-q1`2)-(qz`2-q1`2))/(q2`2-q1`2) by XCMPLX_1:121
            .=(q2`2-qz`2)/(q2`2-q1`2);then
         A201: (1-ln)*q1`2=(q2`2-qz`2)*q1`2/(q2`2-q1`2) by XCMPLX_1:75
                   .=(q2`2*q1`2-qz`2*q1`2)/(q2`2-q1`2);
         A202: ln*q2`2=(qz`2-q1`2)*q2`2/(q2`2-q1`2) by XCMPLX_1:75
               .=(qz`2*q2`2-q1`2*q2`2)/(q2`2-q1`2);
         A203: ((1-ln)*q1+ln*q2)`1= ((1-ln)*q1)`1+(ln*q2)`1 by TOPREAL3:7
                           .=(1-ln)*q1`1+(ln*q2)`1 by TOPREAL3:9
                           .=(1-ln)*p1`1+(ln)*p1`1 by A193,A194,TOPREAL3:9
                           .=qz`1 by A192,XXREAL_0:1,A182;
          ((1-ln)*q1+ln*q2)`2=((1-ln)*q1)`2+(ln*q2)`2 by TOPREAL3:7
                           .=(1-ln)*q1`2+(ln*q2)`2 by TOPREAL3:9
                           .=(1-ln)*q1`2+ln*q2`2 by TOPREAL3:9
                           .=((q2`2*q1`2-qz`2*q1`2)+(qz`2*q2`2-q1`2*q2`2))/
                              (q2`2-q1`2) by XCMPLX_1:63,A201,A202
                           .=qz`2*(q2`2-q1`2)/(q2`2-q1`2)
                           .=qz`2*((q2`2-q1`2)/(q2`2-q1`2)) by XCMPLX_1:75
                           .=qz`2*1 by XCMPLX_1:60,A200
                           .=qz`2;then
          (1-ln)*q1+ln*q2=qz by A203,TOPREAL3:11;then
          z in { (1-lambda)*q1 + lambda*q2 where lambda is Real
                         : 0 <= lambda & lambda <= 1 } by A198,A199;
        hence z in LSeg(q1,q2) by TOPREAL1:def 3;
       end;
      A204: p1 in LSeg(q1,q2)
       proof
         0 in the carrier of I[01] by BORSUK_1:86;then
         0 in dom g by FUNCT_2:def 1;then
         q1 in Q by A2,A8,FUNCT_1:12;then
         p1`1<=q1`1 & q1`1<= p2`1 by A4;then
         A205: p1`1=q1`1 by XXREAL_0:1,A182;
         1 in the carrier of I[01] by BORSUK_1:86;then
         1 in dom g by FUNCT_2:def 1;then
         g.1 in rng g by FUNCT_1:12;then
          p1`1<=q2`1 & q2`1<= p2`1 by A2,A8,A4;then
         A206: p1`1=q2`1 by XXREAL_0:1,A182;
         0 in the carrier of I[01] by BORSUK_1:86;then
         0 in dom f by FUNCT_2:def 1;then
         f.0 in rng f by FUNCT_1:12;then
         A207: q1`2<=p1`2 & p1`2<=q2`2 by A7,A1,A5;then
         q2`2>=q1`2 by XXREAL_0:2;then
         q2`2>q1`2 by A187,REAL_1:def 5;then
         A208: q2`2-q1`2>0 by XREAL_1:52;
         A209: p1`2-q1`2>=0 by XREAL_1:50,A207;
         set ln=(p1`2-q1`2)/(q2`2-q1`2);
         A210: ln>=0 by A208,A209,REAL_2:125;
         p1`2-q1`2<=q2`2-q1`2 by A207,REAL_1:92;then
         ln<= (q2`2-q1`2)/(q2`2-q1`2) by A208,REAL_1:73;then
         A211: ln <= 1 by A208,XCMPLX_1:60;
         A212: q2`2-q1`2 <>0 by A187;
         1-ln=(q2`2-q1`2)/(q2`2-q1`2)-(p1`2-q1`2)/(q2`2-q1`2)
                               by XCMPLX_1:60,A212
            .=((q2`2-q1`2)-(p1`2-q1`2))/(q2`2-q1`2) by XCMPLX_1:121
            .=(q2`2-p1`2)/(q2`2-q1`2);then
         A213: (1-ln)*q1`2=(q2`2-p1`2)*q1`2/(q2`2-q1`2) by XCMPLX_1:75
                   .=(q2`2*q1`2-p1`2*q1`2)/(q2`2-q1`2);
         A214: ln*q2`2=(p1`2-q1`2)*q2`2/(q2`2-q1`2) by XCMPLX_1:75
               .=(p1`2*q2`2-q1`2*q2`2)/(q2`2-q1`2);
         A215: ((1-ln)*q1+ln*q2)`1= ((1-ln)*q1)`1+(ln*q2)`1 by TOPREAL3:7
                           .=(1-ln)*q1`1+(ln*q2)`1 by TOPREAL3:9
                           .=(1-ln)*p1`1+(ln)*p1`1 by A205,A206,TOPREAL3:9
                           .=p1`1;
          ((1-ln)*q1+ln*q2)`2=((1-ln)*q1)`2+(ln*q2)`2 by TOPREAL3:7
                           .=(1-ln)*q1`2+(ln*q2)`2 by TOPREAL3:9
                           .=(1-ln)*q1`2+ln*q2`2 by TOPREAL3:9
                           .=((q2`2*q1`2-p1`2*q1`2)+(p1`2*q2`2-q1`2*q2`2))/
                              (q2`2-q1`2) by XCMPLX_1:63,A213,A214
                           .=p1`2*(q2`2-q1`2)/(q2`2-q1`2)
                           .=p1`2*((q2`2-q1`2)/(q2`2-q1`2)) by XCMPLX_1:75
                           .=p1`2*1 by XCMPLX_1:60,A212
                           .=p1`2;then
         (1-ln)*q1+ln*q2=p1 by A215,TOPREAL3:11;then
         p1 in { (1-lambda)*q1 + lambda*q2 where lambda is Real
                         : 0 <= lambda & lambda <= 1 } by A210,A211;
        hence p1 in LSeg(q1,q2) by TOPREAL1:def 3;
       end;
      p1 in Q by A2,A204,A190,Th3;
     hence contradiction by A13,A16,XBOOLE_0:def 3;
    end;
    case A216:p1<>p2;
    A217: now assume A218: q1=q2;
      A219: for p being Point of TOP-REAL 2 st p in P holds p`2=q1`2
       proof let p be Point of TOP-REAL 2;
        assume p in P;then q1`2<=p`2 & p`2<= q2`2 by A5;
        hence p`2=q1`2 by A218,XXREAL_0:1;
       end;
       0 in the carrier of I[01] by BORSUK_1:86;then
       0 in dom f by FUNCT_2:def 1;then
       A220: p1 in P by A7,A1,FUNCT_1:12;
       1 in the carrier of I[01] by BORSUK_1:86;then
       1 in dom f by FUNCT_2:def 1;then
       A221: p2 in P by A7,A1,FUNCT_1:12;
       0 in the carrier of I[01] by BORSUK_1:86;then
       0 in dom g by FUNCT_2:def 1;then
       A222: q1 in Q by A2,A8,FUNCT_1:12;
A223:   now assume A224: p1`1=p2`1;
         p1`1<=q1`1 & q1`1<=p2`1 by A4,A222;then
         A225: q1`1=p1`1 by A224,XXREAL_0:1;
         p1`2=q1`2 by A219,A220;then
         p1=q1 by A225,TOPREAL3:11;
        hence contradiction by A16,A220,A222,XBOOLE_0:def 3;
      end;
      A226: P c= LSeg(p1,p2)
       proof let z be set;assume
         A227: z in P;then
         reconsider pz=z as Point of TOP-REAL 2;
A228:      q1`2<=pz`2 & pz`2<= q2`2 by A227,A5;
         q1`2<=p1`2 & p1`2<= q2`2 by A220,A5;then
         A229: q1`2=p1`2 by XXREAL_0:1,A218;
          q1`2<=p2`2 & p2`2<= q2`2 by A221,A5;then
         A230: q1`2=p2`2 by XXREAL_0:1,A218;
         A231: p1`1<=pz`1 & pz`1<=p2`1 by A227,A3;then
         p2`1>=p1`1 by XXREAL_0:2;then
         p2`1>p1`1 by A223,REAL_1:def 5;then
         A232: p2`1-p1`1>0 by XREAL_1:52;
         A233: pz`1-p1`1>=0 by XREAL_1:50,A231;
         set ln=(pz`1-p1`1)/(p2`1-p1`1);
         A234: ln>=0 by A232,A233,REAL_2:125;
         pz`1-p1`1<=p2`1-p1`1 by A231,REAL_1:92;then
         ln<= (p2`1-p1`1)/(p2`1-p1`1) by A232,REAL_1:73;then
         A235: ln <= 1 by A232,XCMPLX_1:60;
         A236: p2`1-p1`1 <>0 by A223;
         1-ln=(p2`1-p1`1)/(p2`1-p1`1)-(pz`1-p1`1)/(p2`1-p1`1)
                               by XCMPLX_1:60,A236
            .=((p2`1-p1`1)-(pz`1-p1`1))/(p2`1-p1`1) by XCMPLX_1:121
            .=(p2`1-pz`1)/(p2`1-p1`1);then
         A237: (1-ln)*p1`1=(p2`1-pz`1)*p1`1/(p2`1-p1`1) by XCMPLX_1:75
                   .=(p2`1*p1`1-pz`1*p1`1)/(p2`1-p1`1);
         A238: ln*p2`1=(pz`1-p1`1)*p2`1/(p2`1-p1`1) by XCMPLX_1:75
               .=(pz`1*p2`1-p1`1*p2`1)/(p2`1-p1`1);
         A239: ((1-ln)*p1+ln*p2)`2= ((1-ln)*p1)`2+(ln*p2)`2 by TOPREAL3:7
                           .=(1-ln)*p1`2+(ln*p2)`2 by TOPREAL3:9
                           .=(1-ln)*q1`2+(ln)*q1`2 by A229,A230,TOPREAL3:9
                           .=pz`2 by A228,XXREAL_0:1,A218;
          ((1-ln)*p1+ln*p2)`1=((1-ln)*p1)`1+(ln*p2)`1 by TOPREAL3:7
                           .=(1-ln)*p1`1+(ln*p2)`1 by TOPREAL3:9
                           .=(1-ln)*p1`1+ln*p2`1 by TOPREAL3:9
                           .=((p2`1*p1`1-pz`1*p1`1)+(pz`1*p2`1-p1`1*p2`1))/
                              (p2`1-p1`1) by XCMPLX_1:63,A237,A238
                           .=pz`1*(p2`1-p1`1)/(p2`1-p1`1)
                           .=pz`1*((p2`1-p1`1)/(p2`1-p1`1)) by XCMPLX_1:75
                           .=pz`1*1 by XCMPLX_1:60,A236
                           .=pz`1;then
          (1-ln)*p1+ln*p2=pz by A239,TOPREAL3:11;then
         z in { (1-lambda)*p1 + lambda*p2 where lambda is Real
                         : 0 <= lambda & lambda <= 1 } by A234,A235;
        hence z in LSeg(p1,p2) by TOPREAL1:def 3;
       end;
      A240: q1 in LSeg(p1,p2)
       proof
         0 in the carrier of I[01] by BORSUK_1:86;then
         0 in dom f by FUNCT_2:def 1;then
         f.0 in rng f by FUNCT_1:12;then
         q1`2<=p1`2 & p1`2<= q2`2 by A7,A1,A5;then
         A241: q1`2=p1`2 by XXREAL_0:1,A218;
         1 in the carrier of I[01] by BORSUK_1:86;then
         1 in dom f by FUNCT_2:def 1;then
         f.1 in rng f by FUNCT_1:12;then
         q1`2<=p2`2 & p2`2<= q2`2 by A7,A1,A5;then
         A242: q1`2=p2`2 by XXREAL_0:1,A218;
         0 in the carrier of I[01] by BORSUK_1:86;then
         0 in dom g by FUNCT_2:def 1;then
         g.0 in rng g by FUNCT_1:12;then
         A243: p1`1<=q1`1 & q1`1<=p2`1 by A2,A8,A4;then
         p2`1>=p1`1 by XXREAL_0:2;then
         p2`1>p1`1 by A223,REAL_1:def 5;then
         A244: p2`1-p1`1>0 by XREAL_1:52;
         A245: q1`1-p1`1>=0 by XREAL_1:50,A243;
         set ln=(q1`1-p1`1)/(p2`1-p1`1);
         A246: ln>=0 by A244,A245,REAL_2:125;
         q1`1-p1`1<=p2`1-p1`1 by A243,REAL_1:92;then
         ln<= (p2`1-p1`1)/(p2`1-p1`1) by A244,REAL_1:73;then
         A247: ln <= 1 by A244,XCMPLX_1:60;
         A248: p2`1-p1`1 <>0 by A223;
         1-ln=(p2`1-p1`1)/(p2`1-p1`1)-(q1`1-p1`1)/(p2`1-p1`1)
                               by XCMPLX_1:60,A248
            .=((p2`1-p1`1)-(q1`1-p1`1))/(p2`1-p1`1) by XCMPLX_1:121
            .=(p2`1-q1`1)/(p2`1-p1`1);then
         A249: (1-ln)*p1`1=(p2`1-q1`1)*p1`1/(p2`1-p1`1) by XCMPLX_1:75
                   .=(p2`1*p1`1-q1`1*p1`1)/(p2`1-p1`1);
         A250: ln*p2`1=(q1`1-p1`1)*p2`1/(p2`1-p1`1) by XCMPLX_1:75
               .=(q1`1*p2`1-p1`1*p2`1)/(p2`1-p1`1);
         A251: ((1-ln)*p1+ln*p2)`2= ((1-ln)*p1)`2+(ln*p2)`2 by TOPREAL3:7
                           .=(1-ln)*p1`2+(ln*p2)`2 by TOPREAL3:9
                           .=(1-ln)*q1`2+(ln)*q1`2 by A241,A242,TOPREAL3:9;
         ((1-ln)*p1+ln*p2)`1=((1-ln)*p1)`1+(ln*p2)`1 by TOPREAL3:7
                           .=(1-ln)*p1`1+(ln*p2)`1 by TOPREAL3:9
                           .=(1-ln)*p1`1+ln*p2`1 by TOPREAL3:9
                           .=((p2`1*p1`1-q1`1*p1`1)+(q1`1*p2`1-p1`1*p2`1))/
                              (p2`1-p1`1) by XCMPLX_1:63,A249,A250
                           .=q1`1*(p2`1-p1`1)/(p2`1-p1`1)
                           .=q1`1*((p2`1-p1`1)/(p2`1-p1`1)) by XCMPLX_1:75
                           .=q1`1*1 by XCMPLX_1:60,A248;then
         (1-ln)*p1+ln*p2=q1 by A251,TOPREAL3:11;then
         q1 in { (1-lambda)*p1 + lambda*p2 where lambda is Real
                         : 0 <= lambda & lambda <= 1 } by A246,A247;
        hence q1 in LSeg(p1,p2) by TOPREAL1:def 3;
       end;
      q1 in P by A1,A240,A226,Th3;
     hence contradiction by A16,A14,XBOOLE_0:def 3;
    end;
A252: f2/.1<>f2/.len f2 by A7,A216,A19,A20,A25,A154,A161,A163,A164
,A180,A181;
     A253:1<=len hb by A66,XXREAL_0:2;
     then 1 in dom hb by FINSEQ_3:27;
     then A254:h2/.1=g.(hb.1) by A76;
     len hb in dom hb by A253,FINSEQ_3:27;
     then g2.1<>g2.len g2
     by A8,A217,A66,A70,A76,A156,A168,A169,A254;
     then L~f2 meets L~g2 by A154,A156,A163,A164,A165,A166,A170,A172,A173,A174
,A175,A176,A177,A178,A252,JGRAPH_1:30;
     then consider s being set such that
     A255:s in L~f2 & s in L~g2 by XBOOLE_0:3;
     reconsider ps=s as Point of TOP-REAL 2 by A255;
     ps in union{ LSeg(f2,i) : 1 <= i & i+1 <= len f2 }
          by A255,TOPREAL1:def 6;
     then consider x such that
     A256: ps in x & x in
     { LSeg(f2,i) : 1 <= i & i+1 <= len f2 } by TARSKI:def 4;
     consider i such that
     A257: x=LSeg(f2,i) &(1 <= i & i+1 <= len f2) by A256;
     A258:LSeg(f2,i)=LSeg(f2/.i,f2/.(i+1)) by A257,TOPREAL1:def 5;
     i<len f2 by A257,NAT_1:13;
     then i in dom f2 by A257,FINSEQ_3:27;
     then consider k being Element of NAT such that
     A259: k in dom h1 & |.f2/.i - h1/.k.|<e by A154;
     A260:|.ps-f2/.i.|<=|.f2/.i-f2/.(i+1).| by A256,A257,A258,JGRAPH_1:53;
     |.f2/.i-f2/.(i+1).|<e by A154,A257;
     then A261:|.ps-f2/.i.|<e by A260,XXREAL_0:2;
     A262:|.ps-h1/.k.|<=|.ps-f2/.i.|+|.f2/.i-h1/.k.| by TOPRNS_1:35;
     |.ps-f2/.i.|+|.f2/.i-h1/.k.|<e+e by A259,A261,XREAL_1:10;
     then A263:|.ps-h1/.k.|<e+e by A262,XXREAL_0:2;
     A264:k in Seg len h1 by A259,FINSEQ_1:def 3;
     then A265:k in dom h by A20,FINSEQ_1:def 3;
     1<=k & k<=len h1 by A264,FINSEQ_1:3;
     then h1.k=h1/.k by FINSEQ_4:24;
     then A266:h1/.k=f.(h.k) by A20,A264;
     A267:h.k in rng h by A265,FUNCT_1:def 5;
     dom f=the carrier of I[01] by FUNCT_2:def 1;
     then A268: h1/.k in P by A1,A19,A266,A267,FUNCT_1:def 5;
     reconsider p11=h1/.k as Point of TOP-REAL 2;
     A269: |.(p11-ps).|<e+e by A263,TOPRNS_1:28;
     ps in union{ LSeg(g2,j) : 1 <= j & j+1 <= len g2 }
       by A255,TOPREAL1:def 6;
     then consider y such that
     A270: ps in y &
     y in { LSeg(g2,j) : 1 <= j & j+1 <= len g2 } by TARSKI:def 4;
     consider j such that
     A271:y=LSeg(g2,j) &(1 <= j & j+1 <= len g2) by A270;
     A272: LSeg(g2,j)=LSeg((g2/.j),g2/.(j+1)) &
      ps in LSeg(g2,j) & y=LSeg(g2,j) &(1 <= j & j+1 <= len g2)
                             by A270,A271,TOPREAL1:def 5;
      j<len g2 & LSeg(g2,j)=LSeg((g2/.j),g2/.(j+1)) &
      ps in LSeg(g2,j) & y=LSeg(g2,j) & (1 <= j & j+1 <= len g2)
        by A270,A271,NAT_1:13,TOPREAL1:def 5;
     then j in dom g2 by FINSEQ_3:27;
     then consider k' being Element of NAT such that
     A273: k' in dom h2 & |.((g2/.j) - h2/.k').|<e by A156;
     A274:|.ps-(g2/.j).|<=|.(g2/.j)-g2/.(j+1).| by A272,JGRAPH_1:53;
     |.(g2/.j)-g2/.(j+1).|<e by A156,A271;
     then A275:|.ps-(g2/.j).|<e by A274,XXREAL_0:2;
     A276:|.ps-h2/.k'.|<=|.ps-(g2/.j).|+|.(g2/.j)-h2/.k'.| by TOPRNS_1:35;
     |.ps-(g2/.j).|+|.(g2/.j)-(h2/.k').|<e+e by A273,A275,XREAL_1:10;
     then A277:|.ps-(h2/.k').|<e+e by A276,XXREAL_0:2;
     k' in Seg len h2 by A273,FINSEQ_1:def 3;
     then k' in dom hb by A70,FINSEQ_1:def 3;
     then A278:hb.k' in rng hb &
     h2/.k'=g.(hb.k') & k' in dom hb by A76,FUNCT_1:def 5;
     dom g =the carrier of I[01] by FUNCT_2:def 1;
     then A279:Q<>{}(TOP-REAL 2) &
     h2/.k' in rng g & hb.k' in dom g by A66,A278,FUNCT_1:def 5;
     reconsider q11=h2/.k' as Point of TOP-REAL 2;
     reconsider x1=p11,x2=q11 as Point of Euclid 2 by EUCLID:25;
     min_dist_min(P',Q')<=dist(x1,x2) by A2,A15,A268,A279,WEIERSTR:40;
     then A280:|.(p11-q11).|<= |.(p11-ps).|+|.(ps-q11).|
       & min_dist_min(P',Q')<=|.(p11-q11).| by JGRAPH_1:45,TOPRNS_1:35;
     |.(p11-ps).|+|.(ps-q11).| <e+e+(e+e) by A269,A277,XREAL_1:10;
     then |.(p11-q11).|<e+e+e+e by A280,XXREAL_0:2;
     then min_dist_min(P',Q')<4*e by A280,XXREAL_0:2;
     then 4*e-5*e>0 by XREAL_1:52; then (4-5)*e/e>0 by A18,REAL_2:127;
     then 4-5>0 by A18,XCMPLX_1:90;
    hence contradiction;
     end;
     end;
    hence contradiction;
end;

:: Fashoda Meet Theorem
theorem Th5:
  for f,g being continuous Function of I[01],TOP-REAL 2,
  O,I being Point of I[01] st O=0 & I=1 &
  (f.O)`1=a & (f.I)`1=b &
  (g.O)`2=c & (g.I)`2=d &
  (for r being Point of I[01] holds
    a <=(f.r)`1 & (f.r)`1<=b & a <=(g.r)`1 & (g.r)`1<=b &
    c <=(f.r)`2 & (f.r)`2<=d & c <=(g.r)`2 & (g.r)`2<=d)
   holds rng f meets rng g
   proof
     let f,g be continuous Function of I[01],TOP-REAL 2,
         O,I be Point of I[01];
   assume A1: O=0 & I=1 &
     (f.O)`1=a & (f.I)`1=b & (g.O)`2=c & (g.I)`2=d &
     (for r being Point of I[01] holds
      a<=(f.r)`1 & (f.r)`1<=b & a<=(g.r)`1 & (g.r)`1<=b &
      c <=(f.r)`2 & (f.r)`2<=d & c <=(g.r)`2 & (g.r)`2<=d);
      A2:dom f=[#](I[01]) by FUNCT_2:def 1;
      A3:[#](I[01]) =[.0,1.] by BORSUK_1:83;
      then 0 in dom f by A2,RCOMP_1:48;
     then reconsider P=rng f as non empty Subset of TOP-REAL 2
                        by FUNCT_1:12;
          dom g=[#](I[01]) by FUNCT_2:def 1;
      then 0 in dom g by A3,RCOMP_1:48;
     then reconsider Q=rng g as non empty Subset of TOP-REAL 2
                       by FUNCT_1:12;
     A4: dom f=the carrier of I[01] by FUNCT_2:def 1;
     A5: dom g=the carrier of I[01] by FUNCT_2:def 1;
     the carrier of ((TOP-REAL 2)|P)=[#]((TOP-REAL 2)|P)
                                   .=rng f by PRE_TOPC:def 10;then
     reconsider f1=f as Function of I[01],((TOP-REAL 2)|P) by FUNCT_2:3,A4;
     the carrier of ((TOP-REAL 2)|Q)=[#]((TOP-REAL 2)|Q)
                                   .=rng g by PRE_TOPC:def 10;then
     reconsider g1=g as Function of I[01],((TOP-REAL 2)|Q) by FUNCT_2:3,A5;
     reconsider p1=f1.O as Point of TOP-REAL 2 by A1;
     reconsider p2=f1.I as Point of TOP-REAL 2 by A1;
     reconsider q1=g1.O as Point of TOP-REAL 2 by A1;
     reconsider q2=g1.I as Point of TOP-REAL 2 by A1;
A6:  f is Path of p1,p2 & g is Path of q1,q2 by A1,BORSUK_2:def 4;
     A7:for p being Point of TOP-REAL 2 st p in P holds p1`1<=p`1 & p`1<= p2`1
      proof let p be Point of TOP-REAL 2;
        assume p in P; then ex x st x in dom f1 & p=f1.x by FUNCT_1:def 5;
        hence thesis by A1;
      end;
A8:  for p being Point of TOP-REAL 2 st p in Q holds p1`1<=p`1 & p`1<= p2`1
      proof let p be Point of TOP-REAL 2;
        assume p in Q; then ex x st x in dom g1 & p=g1.x by FUNCT_1:def 5;
        hence thesis by A1;
      end;
A9:  for p being Point of TOP-REAL 2 st p in P holds q1`2<=p`2 & p`2<= q2`2
      proof let p be Point of TOP-REAL 2;
        assume p in P; then ex x st x in dom f1 & p=f1.x by FUNCT_1:def 5;
        hence thesis by A1;
      end;
      for p being Point of TOP-REAL 2 st p in Q holds q1`2<=p`2 & p`2<= q2`2
      proof let p be Point of TOP-REAL 2;
        assume p in Q; then ex x st x in dom g1 & p=g1.x by FUNCT_1:def 5;
        hence thesis by A1;
      end;
   hence thesis by A7,A8,A9,A6,Th4;
 end;

theorem
  for ar, br, cr, dr being Point of Trectangle(a,b,c,d)
  for h being Path of ar,br, v being Path of dr,cr
  for Ar, Br, Cr, Dr being Point of TOP-REAL 2
  st Ar`1 = a & Br`1 = b & Cr`2 = c & Dr`2 = d &
  ar = Ar & br = Br & cr = Cr & dr = Dr
  ex s, t being Point of I[01] st h.s = v.t
  proof
    let ar, br, cr, dr be Point of Trectangle(a,b,c,d);
    let h be Path of ar,br;
    let v be Path of dr,cr;
    let Ar, Br, Cr, Dr be Point of TOP-REAL 2 such that
A1: Ar`1 = a & Br`1 = b & Cr`2 = c & Dr`2 = d and
A2: ar = Ar & br = Br & cr = Cr & dr = Dr;
    set TR = Trectangle(a,b,c,d);
    per cases;
    suppose TR is empty;
      then the carrier of TR is empty by STRUCT_0:def 1;
      then
A3:   h = {} & v = {} by FUNCT_2:def 1;
      take 1[01], 1[01];
      thus h.1[01] = v.1[01] by  A3;
    end;
    suppose TR is non empty;
      then reconsider TR = Trectangle(a,b,c,d)
        as non empty convex SubSpace of TOP-REAL 2;
      reconsider ar, br, cr, dr as Point of TR;
      reconsider h as Path of ar,br;
      reconsider v as Path of dr,cr;
      the carrier of TR is Subset of TOP-REAL 2 by TSEP_1:1;
      then reconsider f = h, g = -v as Function
 of I[01],TOP-REAL 2 by FUNCT_2:9;
A4:   f is continuous & g is continuous by TOPMETR:7;
A5:   h.0 = ar & h.1 = br & (-v).0 = cr & (-v).1 = dr by BORSUK_2:def 4;
      for r being Point of I[01] holds
      a <= (f.r)`1 & (f.r)`1 <= b & a <= (g.r)`1 & (g.r)`1 <= b &
      c <= (f.r)`2 & (f.r)`2 <= d & c <= (g.r)`2 & (g.r)`2 <= d
      proof
        let r be Point of I[01];
A6:     the carrier of TR = closed_inside_of_rectangle(a,b,c,d)
          by PRE_TOPC:29
        .= {p where p is Point of TOP-REAL 2:
        a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d} by JGRAPH_6:def 2;
        h.r in the carrier of TR; then
A7:     ex p being Point of TOP-REAL 2 st
        h.r = p & a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d by A6;
        (-v).r in the carrier of TR;
        then ex p being Point of TOP-REAL 2 st
        (-v).r = p & a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d by A6;
        hence thesis by A7;
      end;
      then rng f meets rng g by A1,A2,A4,A5,BORSUK_1:def 17,
def 18,Th5;
      then consider y being set such that
A8:   y in rng f and
A9:   y in rng g by XBOOLE_0:3;
      consider s being set such that
A10:   s in dom f and
A11:  f.s = y by A8,FUNCT_1:def 5;
      consider t being set such that
A12:  t in dom g and
A13:  g.t = y by A9,FUNCT_1:def 5;
      reconsider s, t as Point of I[01] by A10,A12;
      reconsider t1 = 1-t as Point of I[01] by JORDAN5B:4;
      take s, t1;
A14:  dr,cr are_connected by BORSUK_2:def 3;
      h.s = v.t1 by  A11,A13,A14,BORSUK_2:def 6;
      hence thesis;
    end;
  end;
