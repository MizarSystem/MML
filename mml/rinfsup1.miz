:: Inferior Limit and Superior Limit of Sequences of Real Numbers
::  by Bo Zhang , Hiroshi Yamazaki and Yatsuka Nakamura
:: 
:: Received April 29, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies SEQ_1, ARYTM_3, RELAT_1, SEQ_2, FUNCT_1, ARYTM_1, ORDINAL2,
      ARYTM, ABSVALUE, BOOLE, RINFSUP1, SEQM_3, SQUARE_1, PROB_1, LATTICES,
      SUPINF_2, PARTFUN3, FRECHET2;
 notations ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0, ARYTM_3, REAL_1, NAT_1,
      ASYMPT_0, RELAT_1, FUNCT_1, COMPLEX1, TARSKI, SEQM_3, SEQ_4, XBOOLE_0,
      SUBSET_1, SEQ_2, ABSVALUE, RELSET_1, ORDINAL2, FUNCT_2, SQUARE_1,
      LIMFUNC1, MEMBERED, PSCOMP_1, SEQ_1, PARTFUN3;
 constructors REAL_1, SEQ_1, ABSVALUE, NAT_1, PARTFUN1, MEMBERED, XBOOLE_0,
      COMPLEX1, PSCOMP_1, SEQ_2, SETFAM_1, FUNCT_1, FUNCT_2, FUNCOP_1, SEQM_3,
      ASYMPT_0, ARYTM_3, SQUARE_1, LIMFUNC1, RFUNCT_2, PARTFUN3;
 registrations SEQ_1, SUBSET_1, XREAL_0, RELSET_1, ARYTM_3, MEMBERED, ZFMISC_1,
      PSCOMP_1, XBOOLE_0, NUMBERS, ORDINAL2, SETFAM_1, FUNCT_1, FUNCT_2,
      PARTFUN1;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, XBOOLE_0, PARTFUN3;
 theorems FUNCT_1, REAL_1, FUNCT_2, SEQ_1, SEQ_2, SQUARE_1, NAT_1, TARSKI,
      XBOOLE_0, AXIOMS, XREAL_0, SEQ_4, PSCOMP_1, SEQM_3, SETLIM_1, LIMFUNC1,
      SPRECT_1, PREPOWER, TOPMETR3, COMPLSP1, REAL_2, RFUNCT_2, PARTFUN3;
 schemes FUNCT_2, NAT_1;

begin

 reserve n,m,k,k1,k2 for Nat;
 reserve r,r1,r2,s,t,p for real number;
 reserve seq,seq1,seq2 for Real_Sequence;
 reserve x,y for set;

LM1: for s st 0 < s & r1 <= r2 holds r1 < r2 + s & r1 - s < r2
  proof let s such that
A0: 0<s;
    assume r1 <= r2;
    then r1 + 0 < r2 + s by A0,REAL_1:67;
    hence thesis by REAL_1:84;
  end;

theorem Th1:
  s - r < t & s + r > t iff abs(t-s) < r
proof
 thus s - r < t & s + r > t implies abs(t-s) < r
 proof
  assume A0: s - r < t & s + r > t;
   -r + s < t by A0;
   then A2: -r < t - s by REAL_1:86;
   t - s < r by A0,REAL_1:84;
   hence abs(t - s) < r by A2,SEQ_2:9;
  end;
 assume A3: abs(t-s) < r;
  -r < t - s by A3,SEQ_2:9;
  then A5: s + -r < t by REAL_1:86;
  t - s < r by A3,SEQ_2:9;
 hence thesis by A5,REAL_1:84;
end;

definition 
  let seq be Real_Sequence;
  func sup seq -> Real equals :def1:
    sup rng seq;
  coherence;
end;

definition 
  let seq be Real_Sequence;
  func inf seq -> Real equals :def2:
    inf rng seq;
  coherence;
end;

theorem Th6:
  seq1+seq2-seq2=seq1
proof
  for n holds (seq1+seq2-seq2).n = seq1.n
  proof let n;
    (seq1+seq2-seq2).n = (seq1+(seq2-seq2)).n by SEQ_1:39
                    .= seq1.n + (seq2-seq2).n by SEQ_1:11
                    .= seq1.n + (seq2+-seq2).n by SEQ_1:15
                    .= seq1.n + (seq2.n +(-seq2).n) by SEQ_1:11
                    .= seq1.n + (seq2.n +-seq2.n) by SEQ_1:14
                    .= seq1.n;
    hence thesis;
  end;then
  for x st x in NAT holds (seq1+seq2-seq2).x=seq1.x;
  hence thesis by SEQ_1:8;
end;

theorem Th7:
  r in rng seq iff -r in rng(-seq)
proof
  thus r in rng seq implies -r in rng(-seq)
  proof assume r in rng seq;then
    consider n such that
B1: r=seq.n by RFUNCT_2:9;
    -r=(-seq).n by B1,SEQ_1:14;
    hence thesis by RFUNCT_2:9;
  end;
  assume -r in rng(-seq);then
  consider n such that
A1: -r=(-seq).n by RFUNCT_2:9;
  r=-(-seq).n by A1;then
  r= --seq.n by SEQ_1:14;
  hence thesis by RFUNCT_2:9;
end;

theorem Th8:
  rng (-seq) = - rng seq
proof
  thus rng (-seq) c= - rng seq
    proof
      let x;
      assume
E1:   x in rng(-seq);then
      reconsider r = x as Real;
      -r in rng -(-seq) by E1,Th7;then
      -r in rng seq by SEQ_1:36;then
      --r in - rng seq by PSCOMP_1:14;
      hence thesis;
    end;
    let x;
    assume
C1: x in - rng seq;then
    reconsider r = x as Real;
    -r in --rng seq by C1,PSCOMP_1:14;then
    --r in rng (-seq) by Th7;
    hence thesis;
  end;

theorem Th9:
  seq is bounded_above iff rng seq is bounded_above
proof
A00: rng seq is bounded_above implies seq is bounded_above
  proof assume rng seq is bounded_above;
  then consider t such that
A1: for r st r in rng seq holds r<=t by SEQ_4:def 1;
  now let n;
    seq.n in rng seq by FUNCT_2:6;
    then seq.n <= t by A1;
   hence seq.n < t+1 by LM1;
  end;
 hence thesis by SEQ_2:def 3;
 end;
 seq is bounded_above implies rng seq is bounded_above 
 proof
 assume seq is bounded_above;
 then consider t such that
A2: for n holds seq.n<t by SEQ_2:def 3;
   now let r; 
    assume r in rng seq;
    then ex n being set st n in dom seq & seq.n = r by FUNCT_1:def 5;
   hence r<=t by A2;
   end;
  hence thesis by SEQ_4:def 1; 
 end; 
 hence thesis by A00;
end;

theorem Th10:
  seq is bounded_below iff rng seq is bounded_below
proof
A00: rng seq is bounded_below implies seq is bounded_below 
  proof assume rng seq is bounded_below;
  then consider t such that
A1: for r st r in rng seq holds t<=r by SEQ_4:def 2;
  now let n;
    seq.n in rng seq by FUNCT_2:6;
    then t <= seq.n by A1;
   hence t - 1 < seq.n by LM1;
  end;
 hence thesis by SEQ_2:def 4;
 end;
 seq is bounded_below implies rng seq is bounded_below
 proof
 assume seq is bounded_below;
 then consider t such that
A2: for n holds t<seq.n by SEQ_2:def 4;
   now let r; 
    assume r in rng seq;
    then ex n being set st n in dom seq & seq.n = r by FUNCT_1:def 5;
   hence t<=r by A2;
   end;
  hence thesis by SEQ_4:def 2;
 end;
 hence thesis by A00;
end;

theorem Th11:
  seq is bounded_above implies ( r = sup seq iff
    ((for n holds seq.n <= r) & (for s st 0<s holds ex k st r-s < seq.k)))
proof assume seq is bounded_above;
  then A1: rng seq is bounded_above & rng seq <> {} by Th9,SETLIM_1:8;
   set R = rng seq;
A00: r = sup seq implies 
 (for n holds seq.n <= r) & (for s st 0<s holds ex k st r-s<seq.k)
 proof
  assume r = sup seq;
  then B1: r = sup rng seq by def1;
 B00: now let n;
    seq.n in R by FUNCT_2:6;
    hence seq.n <= r by B1,A1,SEQ_4:def 4;
   end;
 now let s;
    assume 0<s; 
    then consider r2 such that 
B3: r2 in R & r-s<r2 by B1,A1,SEQ_4:def 4;
    consider k such that
B4: r2 = seq.k by B3,SETLIM_1:7;
    take k;
    thus r-s < seq.k by B3,B4;
   end;
  hence thesis by B00;
 end;
 ((for n holds seq.n <= r) & (for s st 0<s holds ex k st r-s<seq.k)) 
  implies r = sup seq
 proof assume 
   C1: (for n holds seq.n <= r) & (for s st 0<s holds ex k st r-s<seq.k);
C00: now let r1;
     assume r1 in R;
     then ex n being set st n in dom seq & seq.n = r1 by FUNCT_1:def 5;
     hence r1 <= r by C1;
    end;
 now let s;
     assume 0<s; 
     then consider k such that
 C3: r-s< seq.k by C1;
     seq.k in R by FUNCT_2:6;
     then consider r2 such that 
     C4: r2 in R & r2 = seq.k;
     take r2;
     thus r2 in R & r-s < r2 by C3,C4;
   end;
  then r = sup R by A1,C00,SEQ_4:def 4;
  hence r = sup seq by def1;
 end;
 hence thesis by A00;
end;

theorem Th12:
  seq is bounded_below implies (r = inf seq iff
    ((for n holds r <= seq.n) & (for s st 0<s holds ex k st seq.k < r+s)))
proof assume seq is bounded_below;
  then A1: rng seq is bounded_below & rng seq <> {} by Th10,SETLIM_1:8;
  set R = rng seq;
A00: r = inf seq implies
 (for n holds r <= seq.n) & (for s st 0<s holds ex k st seq.k<r+s)
 proof
  assume r = inf seq;
  then B1: r = inf rng seq by def2;
B00: now let n;
    seq.n in R by FUNCT_2:6;
    hence r <= seq.n by B1,A1,SEQ_4:def 5;
   end;
 now let s;
    assume 0<s; 
    then consider r2 such that 
    B3: r2 in R & r2<r+s by B1,A1,SEQ_4:def 5;
    consider k such that 
    B4: r2 = seq.k by B3,SETLIM_1:7; 
    take k;
    thus seq.k<r+s by B3,B4;
   end;
  hence thesis by B00;
 end;
 ((for n holds r <= seq.n) & (for s st 0<s holds ex k st seq.k<r+s)) 
  implies r = inf seq
 proof assume
   C1: (for n holds r <= seq.n) & (for s st 0<s holds ex k st seq.k<r+s);
C00: now let r1;
     assume r1 in R;
     then ex n being set st n in dom seq & seq.n = r1 by FUNCT_1:def 5;
     hence r <= r1 by C1;
    end;
 now let s;
     assume 0<s;
     then consider k such that
 C3: seq.k < r+s by C1;
     seq.k in R by FUNCT_2:6;
     then consider r2 such that 
     C4: r2 in R & r2 = seq.k;
     take r2;
     thus r2 in R & r2 <r+s by C3,C4;
   end;
  then r = inf R by A1,C00,SEQ_4:def 5;
  hence r = inf seq by def2;
 end;
 hence thesis by A00;
end;

theorem Th13:
  (for n holds seq.n <= r) iff (seq is bounded_above & sup seq <= r)
proof
 thus (for n holds seq.n <= r) implies seq is bounded_above & sup seq <= r
 proof
 assume
 A1:for n holds seq.n <= r;
  now let m;
   seq.m <= r by A1;
   hence seq.m < r + 1 by LM1;
  end;
  hence A2: seq is bounded_above by SEQ_2:def 3;
  now assume A3: r < sup seq;
   set r1= (sup seq) - r;
   r1 > 0 by A3,SQUARE_1:11;
   then consider k such that 
   A4: (sup seq) - r1 < seq.k by A2,Th11;
  thus contradiction by A1,A4;
  end;
  hence thesis;
  end;
  assume B0: seq is bounded_above & sup seq <= r;
  now let n;
  seq.n <= sup seq by B0,Th11;
  hence seq.n <= r by B0,AXIOMS:22;
 end;
 hence thesis;
end;

theorem Th14:
  (for n holds r <= seq.n) iff (seq is bounded_below & r <= inf seq)
proof
 thus (for n holds r <= seq.n) implies (seq is bounded_below & r <= inf seq)
 proof
 assume
 A1:for n holds r <= seq.n;
  now let m;
   r <= seq.m by A1;
   hence r -1 < seq.m by LM1;
  end;
  hence A2: seq is bounded_below by SEQ_2:def 4;
  now assume A3: r > inf seq;
   set r1=r - (inf seq);
   r1 > 0 by A3,SQUARE_1:11;
   then consider k such that
   A4: (seq.k < (inf seq) + r1) by A2,Th12;
   thus contradiction by A1,A4;
  end;
  hence thesis;
 end;
 assume B0: seq is bounded_below & r <= inf seq;
  now let n;
  inf seq <= seq.n by B0,Th12;
  hence r <= seq.n by B0,AXIOMS:22;
 end;
 hence thesis;
end;

theorem Th15:
  seq is bounded_above iff -seq is bounded_below
proof set seq1=-seq;
  thus seq is bounded_above implies -seq is bounded_below
  proof assume seq is bounded_above;then
    consider t such that
B1: for n holds seq.n<t by SEQ_2:def 3;
    for n holds seq1.n > -t
    proof let n;
C1:   seq1.n=-seq.n by SEQ_1:14;
      seq.n < t by B1;
      hence thesis by C1,REAL_1:50;
    end;
    hence thesis by SEQ_2:def 4;
  end;
  assume seq1 is bounded_below;then
    consider t such that
A1: for n holds seq1.n>t by SEQ_2:def 4;
    for n holds seq.n < -t
    proof let n;
      seq1.n=-seq.n by SEQ_1:14;then
  D1: -seq1.n = seq.n;
      seq1.n > t by A1;
      hence thesis by D1,REAL_1:50;
    end;
   hence thesis by SEQ_2:def 3;
  end;

theorem Th16:
  seq is bounded_below iff -seq is bounded_above
proof
  --seq = seq by SEQ_1:36;
  hence thesis by Th15;
end;

theorem Th17:
  seq is bounded_above implies sup seq = -inf (-seq)
proof assume seq is bounded_above;then
  rng seq is non empty bounded_above by Th9,SETLIM_1:8;then
  sup rng seq = - inf -(rng seq) by PSCOMP_1:18
             .= - inf rng -seq by Th8;then
  sup seq = - inf rng -seq by def1;
  hence thesis by def2;
end;

theorem Th18:
  seq is bounded_below implies inf seq = -sup (-seq)
proof assume seq is bounded_below;then
    rng seq is non empty bounded_below by Th10,SETLIM_1:8;then
    inf rng seq = - sup -(rng seq) by PSCOMP_1:17
               .= - sup rng -seq by Th8;then
    inf seq = - sup rng -seq by def2;
    hence thesis by def1;
end;

theorem Th19:
  seq1 is bounded_below & seq2 is bounded_below implies
    inf (seq1 + seq2) >= inf seq1 + inf seq2
proof assume
A0: seq1 is bounded_below & seq2 is bounded_below;
    for n holds (seq1 + seq2).n >= inf seq1 + inf seq2
    proof let n;
     B1: (seq1 + seq2).n = seq1.n + seq2.n by SEQ_1:11;
     seq1.n >= inf seq1 & seq2.n >= inf seq2 by A0,Th12;
     hence thesis by B1,REAL_1:55;
    end;
  hence thesis by Th14;
end;

theorem Th20:
  seq1 is bounded_above & seq2 is bounded_above implies
    sup (seq1 + seq2) <= sup seq1 + sup seq2
proof assume
A0: seq1 is bounded_above & seq2 is bounded_above;
    for n holds (seq1 + seq2).n <= sup seq1 + sup seq2
    proof let n;
     B1: (seq1 + seq2).n = seq1.n + seq2.n by SEQ_1:11;
     seq1.n <= sup seq1 & seq2.n <= sup seq2 by A0,Th11;
     hence thesis by B1,REAL_1:55;
    end; 
  hence thesis by Th13;
end;

notation
  let f be Real_Sequence;
  synonym f is nonnegative for f is nonnegative-yielding;
end;

definition
  let f be Real_Sequence;
  redefine attr f is nonnegative means :def3:
    for n holds f.n >= 0;
  compatibility
  proof
    hereby assume A1: f is nonnegative;
      let n;
      f.n in rng f by RFUNCT_2:10;
      hence f.n >= 0 by A1,PARTFUN3:def 4;
    end;
    assume
A4: for n holds f.n >= 0;
    let r be real number;
    assume r in rng f; then
    consider x being Nat such that
A3: r = f.x by RFUNCT_2:9;
    thus thesis by A3,A4;
  end;
end;

theorem Th21:
  seq is nonnegative implies seq ^\k is nonnegative
proof assume
A0: seq is nonnegative;
    for n holds (seq ^\k).n >= 0
    proof let n;
      (seq ^\k).n = seq.(n+k) by SEQM_3:def 9;
      hence thesis by A0,def3; 
    end;
    hence thesis by def3;
end;

theorem Th22:
  seq is bounded_below nonnegative implies inf seq >= 0
proof assume
A0: seq is bounded_below nonnegative;
    for n holds seq.n >= 0 by A0,def3;
    hence thesis by Th14;
end;

theorem
  seq is bounded_above nonnegative implies sup seq >= 0
proof
  assume seq is bounded_above nonnegative;then
  seq.0 <= sup seq & seq.0 >= 0 by Th11,def3;
  hence thesis;
end;

theorem Th24:
  seq1 is bounded_below nonnegative &
  seq2 is bounded_below nonnegative implies
    (seq1(#)seq2) is bounded_below &
    inf (seq1 (#) seq2) >= (inf seq1) * (inf seq2)
proof assume
A0: seq1 is bounded_below nonnegative &
    seq2 is bounded_below nonnegative;
    for n holds (seq1 (#) seq2).n >= (inf seq1) * (inf seq2)
    proof let n;
     B1: (seq1 (#) seq2).n = seq1.n * seq2.n by SEQ_1:12;
     B2: seq1.n >= inf seq1 & seq2.n >= inf seq2 by A0,Th12;
     inf seq1 >= 0 & inf seq2 >= 0 by A0,Th22;
     hence thesis by B1,B2,SEQ_4:7;
    end; 
  hence thesis by Th14;
end;

theorem Th25:
  seq1 is bounded_above nonnegative &
  seq2 is bounded_above nonnegative implies
    (seq1(#)seq2) is bounded_above &
    sup (seq1 (#) seq2) <= (sup seq1) * (sup seq2)
proof assume
A0: seq1 is bounded_above nonnegative &
    seq2 is bounded_above nonnegative;
    for n holds (seq1 (#) seq2).n <= (sup seq1) * (sup seq2)
    proof let n;
     B1: (seq1 (#) seq2).n = seq1.n * seq2.n by SEQ_1:12;
     B2: seq1.n <= sup seq1 & seq2.n <= sup seq2 by A0,Th11;
     seq1.n >= 0 & seq2.n >= 0 by A0,def3;
     hence thesis by B1,B2,SEQ_4:7;
    end; 
  hence thesis by Th13;
end;

theorem Th26:
  seq is non-decreasing bounded_above implies seq is bounded
proof
  assume seq is non-decreasing bounded_above;then
  seq is bounded_below bounded_above by LIMFUNC1:26;
  hence thesis by SEQ_2:def 5;
end;

theorem Th27:
  seq is non-increasing bounded_below implies seq is bounded
proof
  assume seq is non-increasing bounded_below;then
  seq is bounded_below bounded_above by LIMFUNC1:26;
  hence thesis by SEQ_2:def 5;
end;

theorem Th28:
  seq is non-decreasing bounded_above implies
    lim seq = sup seq
proof
 assume A0: seq is non-decreasing bounded_above;then
   A1: seq is bounded & seq is convergent by Th26,SEQ_4:51;
   then seq is bounded_above & seq is bounded_below by SEQ_2:def 5;
   then for n holds seq.n <= sup seq by Th11;
   then A4: lim seq <= sup seq by A1,PREPOWER:3;
   for n holds seq.n <= lim seq by A0,SEQ_4:54;
   then sup seq <= lim seq by Th13;
   hence lim seq = sup seq by A4,AXIOMS:21;
end;

theorem Th29:
  seq is non-increasing bounded_below implies
    lim seq = inf seq
proof
 assume A0: seq is non-increasing bounded_below;then
   A1: seq is bounded convergent by Th27,SEQ_4:52;
   then seq is bounded_above bounded_below by SEQ_2:def 5;
   then for n holds inf seq <= seq.n by Th12;
   then A4: inf seq <= lim seq by A1,PREPOWER:2;
   for n holds lim seq <= seq.n by A0,SEQ_4:55;
   then lim seq <= inf seq by Th14;
   hence lim seq = inf seq by A4,AXIOMS:21;
end;

theorem Th30:
  seq is bounded_above implies seq ^\k is bounded_above
proof
  seq ^\ k is subsequence of seq by SEQM_3:47;
  hence thesis by SEQM_3:56;
end;

theorem Th31:
  seq is bounded_below implies seq ^\k is bounded_below
proof 
  seq ^\k is subsequence of seq by SEQM_3:47;
  hence thesis by SEQM_3:57;
end;

theorem 
  seq is bounded implies seq ^\k is bounded
proof
  seq ^\ k is subsequence of seq by SEQM_3:47;
  hence thesis by SEQM_3:58;
end;

theorem Th33:
  for seq, n holds {seq.k: n <= k} is Subset of REAL
 proof
  let seq, n;
  set Y = {seq.k: n <= k};
  now let x;
   assume x in Y;
   then consider z1 be set such that
   A0: z1 = x & z1 in Y;
   consider k1 being Nat such that
   A1: z1 = seq.k1 & n <= k1 by A0;
   thus x in REAL by A0,A1;
   end;
  hence thesis by TARSKI:def 3;
 end;

theorem Th34:
  rng (seq ^\ k) = {seq.n: k <= n}
proof
   set seq1 = seq ^\ k;
    C1: dom seq1 = NAT by FUNCT_2:def 1;
    set Z = {seq.m : k <= m};
     now let x;
      assume x in Z;
       then consider k1 being Nat such that
       CC0: x=seq.k1 & k <= k1;
       consider k2 being Nat such that
       CC1: k1 = k + k2 by CC0,NAT_1:28;
       x = seq1.k2 by CC0,CC1,SEQM_3:def 9;
      hence x in rng seq1 by FUNCT_2:6;
     end;then
C00: Z c= rng seq1 by TARSKI:def 3;
    now let y;
    assume y in rng seq1;
    then consider m1 be set such that 
    DD1: m1 in NAT & y = seq1.m1 by C1,FUNCT_1:def 5;
    reconsider m1 as Nat by DD1;
    y = seq.(k+m1) by SEQM_3:def 9,DD1;
    hence y in Z by SETLIM_1:4;
   end;then 
   rng seq1 c= Z by TARSKI:def 3;
  hence thesis by C00,XBOOLE_0:def 10;
 end;

theorem Th35:
  seq is bounded_above implies (for n for R being Subset of REAL st
    R = {seq.k : n <= k} holds R is bounded_above)
proof 
  assume A00: seq is bounded_above;
  let n; let R be Subset of REAL;
  assume A0: R = {seq.k : n <= k};
  set seq1 = seq ^\n;
  seq1 is bounded_above by A00,Th30;
  then rng seq1 is bounded_above by Th9;
  hence R is bounded_above by A0,Th34;
end;

theorem Th36:
  seq is bounded_below implies (for n for R being Subset of REAL st
    R = {seq.k : n <= k} holds R is bounded_below)
proof
  assume A00: seq is bounded_below;
  let n; let R be Subset of REAL;
  assume A0: R = {seq.k : n <= k};
  set seq1 = seq ^\n;
  seq1 is bounded_below by A00,Th31;
  then rng seq1 is bounded_below by Th10;
  hence R is bounded_below by A0,Th34;
end;

theorem Th37:
  seq is bounded implies (for n for R being Subset of REAL st
    R = {seq.k : n <= k} holds R is bounded)
proof 
  assume seq is bounded;
  then A01: seq is bounded_above & seq is bounded_below by SEQ_2:def 5;
  let n; let R be Subset of REAL;
   assume R = {seq.k : n <= k};
   then R is bounded_above & R is bounded_below by A01,Th35,Th36;
  hence R is bounded by SEQ_4:def 3;
end;

theorem Th38:
  seq is non-decreasing implies for n
    for R being Subset of REAL st R = {seq.k : n <= k} holds
      inf R = seq.n
proof
   assume A0: seq is non-decreasing;
   then A1: seq is bounded_below by LIMFUNC1:26;
   let n;
   let R be Subset of REAL;
   assume A3: R = {seq.k : n <= k};
AA0: R <> {} & R is bounded_below by A3,SETLIM_1:3,A1,Th36;
A00: for r st r in R holds seq.n <=r
    proof 
      now let r;
       assume r in {seq.k : n <= k};
       then consider r1 being real number such that
       B0: r = r1 & r1 in {seq.k : n <= k};
       consider k1 such that
       B1: r1 = seq.k1 & n <= k1 by B0;
       ex k st n + k =k1 by B1,NAT_1:28;then
       consider k2 being Nat such that
       B2: r1=seq.(n + k2) by B1; 
       thus seq.n <= r by A0,SEQM_3:11,B2,B0;
      end;
     hence thesis by A3;
    end;
   for s st 0<s ex r st r in R & r< seq.n + s
    proof 
    now let s;
     assume C0: 0 < s;
     C1: seq.n in {seq.k : n <= k};
     reconsider r = seq.n as Real;
     r < seq.n +s by C0,REAL_1:69;
     hence ex r st r in {seq.k : n <= k} & r< seq.n + s by C1;
    end;
    hence thesis by A3;
    end;
  hence thesis by AA0,A00,SEQ_4:def 5;
end;

theorem Th39:
  seq is non-increasing implies
    for n for R being Subset of REAL st R = {seq.k : n <= k}
    holds sup R = seq.n
proof
   assume A0: seq is non-increasing;
   then A1: seq is bounded_above by LIMFUNC1:26;
   let n;
   let R be Subset of REAL;
   assume A3: R = {seq.k : n <= k};
AA0: R <> {} & R is bounded_above by A3,SETLIM_1:3,A1,Th35;
A00: (for r st r in R holds r <= seq.n)
    proof 
      now let r;
       assume r in {seq.k : n <= k};
       then consider r1 being real number such that
       B0: r = r1 & r1 in {seq.k : n <= k};
       consider k1 such that
       B1: r1 = seq.k1 & n <= k1 by B0;
       ex k st n + k =k1 by B1,NAT_1:28; 
       then consider k2 being Nat such that
       B2: r1=seq.(n + k2) by B1;
       thus r <= seq.n by B0,A0,SEQM_3:13,B2;
      end;
     hence thesis by A3;
    end;
  for s st 0<s ex r st r in R & seq.n - s < r
    proof 
    now let s;
     assume C0: 0 < s;
     C1: seq.n in {seq.k : n <= k};
     reconsider r = seq.n as Real;
     seq.n - s < r by C0,SQUARE_1:29;
     hence ex r st r in {seq.k : n <= k} & seq.n - s < r by C1;
    end;
    hence thesis by A3;
    end;
  hence thesis by AA0,A00,SEQ_4:def 4;
end;

theorem Th40:
  for seq holds
    ex f being Function of NAT,REAL st
    for n for Y being Subset of REAL st Y = {seq.k: n <= k} holds f.n = sup Y
  proof
   let seq be Real_Sequence;
   defpred P[Element of NAT, Element of REAL] means
    for Y being Subset of REAL st Y = {seq.k: $1 <= k} holds $2 = sup Y;
A1:for n being Element of NAT ex y being Element of REAL st P[n,y]
    proof
     let n be Element of NAT;
     reconsider Y={seq.k: n <= k} as Subset of REAL by Th33;
     reconsider y = sup Y as Element of REAL;
     for Y being Subset of REAL st Y = {seq.k: n <= k} holds y = sup Y;
     hence thesis;
    end;
  ex f being Function of NAT,REAL st
  for n being Nat holds P[n, f.n] from FUNCT_2:sch 3(A1);
  hence thesis;
 end;

theorem Th41:
  for seq holds
   ex f being Function of NAT,REAL st
  for n for Y being Subset of REAL st Y = {seq.k: n <= k} holds f.n = inf Y
  proof
   let seq be Real_Sequence;
   defpred P[Element of NAT, Element of REAL] means
    for Y being Subset of REAL st Y = {seq.k: $1 <= k} holds $2 = inf Y;
A1:for n being Element of NAT ex y being Element of REAL st P[n,y]
    proof
     let n be Element of NAT;
     reconsider Y={seq.k: n <= k} as Subset of REAL by Th33;
     reconsider y = inf Y as Element of REAL;
     for Y being Subset of REAL st Y = {seq.k: n <= k} holds y = inf Y;
     hence thesis;
    end;
 ex f being Function of NAT,REAL st
  for n being Nat holds P[n, f.n] from FUNCT_2:sch 3(A1);
  hence thesis;
 end;

definition
  let seq be Real_Sequence;
  func inferior_realsequence seq -> Real_Sequence means :def4:
  for n for Y being Subset of REAL st Y = {seq.k : n <= k} holds it.n = inf Y;
existence by Th41;
uniqueness
proof let seq1,seq2 be Real_Sequence such that
    A1: for n for Y being Subset of REAL 
        st Y = {seq.k : n <= k} holds seq1.n = inf Y and
    A2: for m for Y being Subset of REAL
        st Y = {seq.k : m <= k} holds seq2.m = inf Y;
    A3: for y st y in NAT holds seq1.y = seq2.y
       proof 
        let y; 
        assume y in NAT;
        then reconsider y as Nat;
        reconsider Y = {seq.k: y <= k} as Subset of REAL by Th33;
        seq1.y = inf Y by A1;
       hence thesis by A2;
       end;
      NAT = dom seq1 & NAT = dom seq2 by FUNCT_2:def 1;
      hence thesis by A3,FUNCT_1:9;
    end;
end;

definition
  let seq be Real_Sequence;
  func superior_realsequence seq -> Real_Sequence means :def5:
  for n for Y being Subset of REAL st Y = {seq.k : n <= k} holds it.n = sup Y;
existence by Th40;
uniqueness
proof let seq1,seq2 be Real_Sequence such that
    A1: for n for Y being Subset of REAL 
        st Y = {seq.k : n <= k} holds seq1.n = sup Y and
    A2: for m for Y being Subset of REAL
        st Y = {seq.k : m <= k} holds seq2.m = sup Y;
    A3: for y st y in NAT holds seq1.y = seq2.y
       proof 
        let y; 
        assume y in NAT;
        then reconsider y as Nat;
        reconsider Y = {seq.k: y <= k} as Subset of REAL by Th33;
        seq1.y = sup Y by A1;
       hence thesis by A2;
       end;
      NAT = dom seq1 & NAT = dom seq2 by FUNCT_2:def 1;
      hence thesis by A3,FUNCT_1:9;
    end;
end;

theorem Th110:
  (inferior_realsequence seq).n = inf (seq ^\n)
proof reconsider Y = {seq.k: n <= k} as Subset of REAL by Th33;
  (inferior_realsequence seq).n = inf Y by def4
                                .= inf rng (seq ^\ n) by Th34;
  hence thesis by def2;
end;

theorem Th42:
  (superior_realsequence seq).n = sup (seq ^\n)
proof reconsider Y = {seq.k: n <= k} as Subset of REAL by Th33;
  (superior_realsequence seq).n = sup Y by def5
                                .= sup rng (seq ^\ n) by Th34;
  hence thesis by def1;
end;

theorem Th43:
  seq is bounded_below implies (inferior_realsequence seq).0 = inf seq
proof
 reconsider Y1 = {seq.k : 0 <= k} as Subset of REAL by Th33;
 (inferior_realsequence seq).0 = inf Y1 by def4
                .= inf rng seq by SETLIM_1:9
                .= inf seq by def2;
 hence thesis;
end;

theorem Th44:
  seq is bounded_above implies (superior_realsequence seq).0 = sup seq
proof
 reconsider Y1 = {seq.k : 0 <= k} as Subset of REAL by Th33;
 (superior_realsequence seq).0 = sup Y1 by def5
                .= sup rng seq by SETLIM_1:9
                .= sup seq by def1;
 hence thesis;
end;

theorem Th45:
  seq is bounded_below implies (r = (inferior_realsequence seq).n iff
    (for k holds r <= seq.(n+k)) & for s st 0<s ex k st seq.(n+k)<r+s)
proof assume 
 A0: seq is bounded_below;
     reconsider Y1 = {seq.k : n <= k} as Subset of REAL by Th33;
 A1: Y1 is non empty bounded_below by A0,Th36,SETLIM_1:3;
   thus r = (inferior_realsequence seq).n implies
      (for k holds r<=seq.(n+k)) & for s st 0<s ex k st seq.(n+k)<r+s
      proof assume r = (inferior_realsequence seq).n;then
      B1: r = inf Y1 by def4;
      B2: for k holds r<=seq.(n+k)
          proof let k;
           seq.(n+k) in Y1 by SETLIM_1:4;
          hence thesis by A1,B1,SEQ_4:def 5;       
          end;
          for s st 0<s ex k st seq.(n+k)<r+s
          proof let s such that
       C0: 0<s;
           consider r1 such that
       C1: r1 in Y1 & r1<r+s by A1,B1,C0,SEQ_4:def 5;
           consider k1 such that
       C2: r1=seq.k1 & n <= k1 by C1;
           consider k2 being Nat such that
       C3: k1 = n + k2 by C2,NAT_1:28;
        thus thesis by C1,C2,C3;
        end;
       hence thesis by B2;
      end;
      assume that
      D0: for k holds r <= seq.(n+k) and
      D1: for s st 0<s ex k st seq.(n+k)<r+s;
      D2: for r1 st r1 in Y1 holds r <= r1 
          proof let r1 such that
          E1: r1 in Y1;
              consider k1 such that
          E2: r1=seq.k1 & n <= k1 by E1;
              consider k2 being Nat such that
          E3: k1 = n + k2 by E2,NAT_1:28;
             thus thesis by D0,E2,E3;
          end;
          for s st 0<s ex r1 st r1 in Y1 & r1<r+s
          proof let s such that
          F0: 0<s;
              consider k such that
          F1: seq.(n+k)<r+s by F0,D1;
              n + k >= n by NAT_1:29;then
              seq.(n+k) in Y1;then
              consider r1 such that
          F2: r1 in Y1 & r1 = seq.(n+k);
             thus thesis by F1,F2;
          end;then
          r=inf Y1 by D2,A1,SEQ_4:def 5;
          hence thesis by def4;
end;

theorem Th46:
  seq is bounded_above implies (r = (superior_realsequence seq).n iff
    (for k holds seq.(n+k) <= r) & for s st 0<s ex k st r-s<seq.(n+k))
proof assume 
 A0: seq is bounded_above;
     reconsider Y1 = {seq.k : n <= k} as Subset of REAL by Th33;
 A1: Y1 is non empty bounded_above by A0,Th35,SETLIM_1:3;
     thus r = (superior_realsequence seq).n implies
      (for k holds seq.(n+k) <= r) & for s st 0<s ex k st r-s<seq.(n+k)
      proof assume r = (superior_realsequence seq).n;then
      B1: r = sup Y1 by def5;
      B2: for k holds seq.(n+k) <= r
          proof let k;
           seq.(n+k) in Y1 by SETLIM_1:4;
          hence thesis by A1,B1,SEQ_4:def 4;       
          end;
          for s st 0<s ex k st r-s<seq.(n+k)
          proof let s such that
       C0: 0<s;
           consider r1 such that
       C1: r1 in Y1 & r-s<r1 by A1,B1,C0,SEQ_4:def 4;
           consider k1 such that
       C2: r1=seq.k1 & n <= k1 by C1;
           consider k2 being Nat such that
       C3: k1 = n + k2 by C2,NAT_1:28;
        thus thesis by C1,C2,C3;
        end;
       hence thesis by B2;
      end;
      assume that
      D0: for k holds seq.(n+k) <= r and
      D1: for s st 0<s ex k st r-s<seq.(n+k);
      D2: for r1 st r1 in Y1 holds r1 <= r
          proof let r1 such that
          E1: r1 in Y1;
              consider k1 such that
          E2: r1=seq.k1 & n <= k1 by E1;
              consider k2 being Nat such that
          E3: k1 = n + k2 by E2,NAT_1:28;
             thus thesis by D0,E2,E3;
          end;
          for s st 0<s ex r1 st r1 in Y1 & r-s<r1
          proof let s such that
          F0: 0<s;
              consider k such that
          F1: r-s<seq.(n+k) by F0,D1;
              n + k >= n by NAT_1:29;then
              seq.(n+k) in Y1;then
              consider r1 such that
          F2: r1 in Y1 & r1 = seq.(n+k);
             thus thesis by F1,F2;
          end;then
          r=sup Y1 by D2,A1,SEQ_4:def 4;
          hence thesis by def5;
end;

theorem Th47:
  seq is bounded_below implies
    ((for k holds r <= seq.(n+k)) iff r <= (inferior_realsequence seq).n)
 proof assume A0: seq is bounded_below;
  reconsider Y1 = {seq.k : n <= k} as Subset of REAL by Th33;
  set seq1 = seq ^\n;
  A3: rng seq1 = Y1 by Th34;
  A4: seq1 is bounded_below by A0,Th31;
  thus ((for k holds r <= seq.(n+k)) implies 
   r <= (inferior_realsequence seq).n)
  proof assume B0: for k holds r <= seq.(n+k);
    now let n1 being Nat;
     seq1.n1 in rng seq1 by FUNCT_2:6;
     then consider r1 such that
     C1: seq1.n1= r1 & r1 in Y1 by A3;
     consider k1 being Nat such that
     C2: r1 = seq.k1 & n <= k1 by C1;
     consider k2 being Nat such that
     C3: k1 = n + k2 by C2,NAT_1:28;
    thus r <= seq1.n1 by C1,C2,C3,B0;
    end;
   then r <= inf seq1 by Th14;
   then r <= inf rng seq1 by def2;
   then r <= inf Y1 by Th34;
  hence r <= (inferior_realsequence seq).n by def4;
  end;
  assume r <= (inferior_realsequence seq).n;
  then r <= inf Y1 by def4;
   then r <= inf rng seq1 by Th34;
   then A5: r <= inf seq1 by def2;
   now let m1 be Nat;
     D0: n <= n+m1 by NAT_1:29;
     seq.(n+m1) in Y1 by D0;
     then seq.(n+m1) in rng seq1 by Th34;
     then consider k being set such that 
     D1: k in dom seq1 & seq1.k = seq.(n+m1) by FUNCT_1:def 5;
     reconsider k as Nat by D1;
     thus r <= seq.(n+m1) by D1,A5,A4,Th14;
   end;
 hence thesis;
end; 

theorem Th48:
seq is bounded_below implies
((for m st n <= m holds r <= seq.m) iff r <= (inferior_realsequence seq).n)
proof assume 
 A0: seq is bounded_below;
      thus ((for m st n<=m holds r <= seq.m) implies
       r <= (inferior_realsequence seq).n)
      proof assume 
      B0: for m st n<=m holds r <= seq.m;
      now let k;
        n <= n+k by NAT_1:29;
        hence r <= seq.(n+k) by B0;
      end;
      hence thesis by A0,Th47;
      end;
      assume 
      B1: r <= (inferior_realsequence seq).n;
      now let m such that
     C0: n<=m;
         consider k such that
     C1: m = n + k by C0,NAT_1:28;
         thus r <= seq.m by C1,B1,A0,Th47;
      end;
    hence thesis;
end;

theorem Th49:
seq is bounded_above implies
((for k holds seq.(n+k) <= r) iff (superior_realsequence seq).n <= r)
 proof assume A0: seq is bounded_above;
  reconsider Y1 = {seq.k : n <= k} as Subset of REAL by Th33;
  set seq1 = seq ^\n;
  A3: rng seq1 = Y1 by Th34;
  A4: seq1 is bounded_above by A0,Th30;
  thus ((for k holds seq.(n+k) <= r) implies 
    (superior_realsequence seq).n <= r)
  proof assume B0: (for k holds seq.(n+k) <= r);
    now let n1 being Nat;
     seq1.n1 in rng seq1 by FUNCT_2:6;
     then consider r1 such that
     C1: seq1.n1= r1 & r1 in Y1 by A3;
    consider k1 being Nat such that
     C2: r1 = seq.k1 & n <= k1 by C1;
    consider k2 being Nat such that
     C3: k1 = n + k2 by C2,NAT_1:28;
    thus seq1.n1 <= r by C1,C2,C3,B0;
   end;
   then sup seq1 <= r by Th13;
   then sup rng seq1 <= r by def1;
   then sup Y1 <= r by Th34;
  hence (superior_realsequence seq).n <= r by def5;
  end;
  assume (superior_realsequence seq).n <= r;then
    sup Y1 <= r by def5;then
    sup rng seq1 <= r by Th34;then
  A5: sup seq1 <= r by def1;
   now let m1 being Nat;
     D0: n <= n+m1 by NAT_1:29;
         seq.(n+m1) in Y1 by D0;then
         seq.(n+m1) in rng seq1 by Th34;then
         consider k being set such that 
     D1: k in dom seq1 & seq1.k = seq.(n+m1) by FUNCT_1:def 5;
         reconsider k as Nat by D1;
      thus seq.(n+m1) <= r by A4,A5,Th13,D1;
    end;
 hence thesis;
end; 

theorem Th50:
seq is bounded_above implies
((for m st n<=m holds seq.m <= r) iff (superior_realsequence seq).n <= r)
proof assume 
 A0: seq is bounded_above;
      thus ((for m st n<=m holds seq.m <= r) implies
       (superior_realsequence seq).n <= r)
      proof assume 
      B0: for m st n<=m holds seq.m <= r;
      now let k;
        n <= n+k by NAT_1:29;
        hence seq.(n+k) <= r by B0;
      end;
      hence thesis by A0,Th49;
      end;
      assume 
      B1: (superior_realsequence seq).n <= r;
      now let m such that
       C0: n<=m;
           consider k such that
       C1: m = n + k by C0,NAT_1:28;
        thus seq.m <= r by C1,B1,A0,Th49;   
      end;
    hence thesis;
end;

theorem Th51:
seq is bounded_below implies
 (inferior_realsequence seq).n = min((inferior_realsequence seq).(n+1),seq.n)
proof
 assume A0: seq is bounded_below;
  reconsider Y1 = {seq.k : n <= k} as Subset of REAL by Th33;
A1: (inferior_realsequence seq).n = inf Y1 by def4;
  reconsider Y2 = {seq.k : n+1 <= k} as Subset of REAL by Th33;
A2: (inferior_realsequence seq).(n+1) = inf Y2 by def4;
  reconsider Y3 = {seq.n} as Subset of REAL;
A3: Y1 <> {} & Y2 <> {} & Y3 <> {} by SETLIM_1:3;
A4: Y1 is bounded_below & Y2 is bounded_below by A0,Th36;
A5: Y3 is bounded_below
  proof
       consider t such that
       B00: for m holds t<seq.m by A0,SEQ_2:def 4;
      now
       take t;
       let r; assume r in Y3;
       then r = seq.n by TARSKI:def 1;
       hence t <= r by B00;
      end;
    hence thesis by SEQ_4:def 2;
   end;
   (inferior_realsequence seq).n = inf (Y2 \/ Y3) by A1,SETLIM_1:5
             .= min(inf Y2,inf Y3) by A3,A4,A5,SPRECT_1:52
             .= min((inferior_realsequence seq).(n+1),seq.n) by A2,SEQ_4:22;
 hence thesis;
end;

theorem Th52:
seq is bounded_above implies 
 (superior_realsequence seq).n = max((superior_realsequence seq).(n+1),seq.n)
proof
 assume A0: seq is bounded_above;
  reconsider Y1 = {seq.k : n <= k} as Subset of REAL by Th33;
A1: (superior_realsequence seq).n = sup Y1 by def5;
  reconsider Y2 = {seq.k : n+1 <= k} as Subset of REAL by Th33;
A2: (superior_realsequence seq).(n+1) = sup Y2 by def5;
  reconsider Y3 = {seq.n} as Subset of REAL;
A3: Y1 <> {} & Y2 <> {} & Y3 <> {} by SETLIM_1:3;
A4: Y1 is bounded_above & Y2 is bounded_above by A0,Th35;
A5: Y3 is bounded_above
  proof
    consider t such that
B00:for m holds seq.m<t by A0,SEQ_2:def 3;
    now
      take t;
      let r; assume r in Y3;
      then B0: r = seq.n by TARSKI:def 1;
      thus r <=t by B00,B0;
    end;
    hence thesis by SEQ_4:def 1;
   end;
  (superior_realsequence seq).n = sup (Y2 \/ Y3) by A1,SETLIM_1:5
             .= max(sup Y2,sup Y3) by A3,A4,A5,SPRECT_1:53
             .= max((superior_realsequence seq).(n+1),seq.n) by A2,SEQ_4:22;
 hence thesis;
end;

theorem Th53:
  seq is bounded_below implies (inferior_realsequence seq).n <=
    (inferior_realsequence seq).(n+1)
proof
  assume A0: seq is bounded_below;
  min((inferior_realsequence seq).(n+1),seq.n) <=
    (inferior_realsequence seq).(n+1) by SQUARE_1:35;
  hence thesis by A0,Th51;
end;

theorem Th54:
  seq is bounded_above implies (superior_realsequence seq).(n+1) <=
    (superior_realsequence seq).n
proof
  assume A0: seq is bounded_above;
  (superior_realsequence seq).(n+1) <=
    max((superior_realsequence seq).(n+1),seq.n) by SQUARE_1:46;
  hence thesis by A0,Th52;
end;

theorem Th55:
  seq is bounded_below implies inferior_realsequence seq is non-decreasing
proof
  assume seq is bounded_below;then
  (inferior_realsequence seq).n <= (inferior_realsequence seq).(n+1) by Th53;
  hence thesis by SEQM_3:def 13;
end;

theorem Th56:
  seq is bounded_above implies superior_realsequence seq is non-increasing
proof
  assume seq is bounded_above;then
  (superior_realsequence seq).(n+1) <= (superior_realsequence seq).n by Th54;
  hence thesis by SEQM_3:def 14;
end;

theorem
  seq is bounded implies
    (inferior_realsequence seq).n <= (superior_realsequence seq).n
proof
  assume A0: seq is bounded;
  reconsider Y1 = {seq.k : n <= k} as Subset of REAL by Th33;
A1: (superior_realsequence seq).n = sup Y1 by def5;
A2: (inferior_realsequence seq).n = inf Y1 by def4;
  Y1 <> {} & Y1 is bounded by SETLIM_1:3,A0,Th37;
  hence thesis by A1,A2,SEQ_4:24;
end;

theorem Th58:
  seq is bounded implies
    (inferior_realsequence seq).n <= inf (superior_realsequence seq)
proof
  assume A0: seq is bounded;
  reconsider Y2 = {seq.k2 : n <= k2} as Subset of REAL by Th33;
  A1 : (inferior_realsequence seq).n = inf Y2 by def4;
  now let m;
    reconsider Y1 = {seq.k1 : m <= k1} as Subset of REAL by Th33;
    A2: (superior_realsequence seq).m = sup Y1 by def5;
    Y1 is bounded & Y2 is bounded by A0,Th37;
    then A4: Y1 is bounded_above & Y2 is bounded_below by SEQ_4:def 3;
    n <= n + m  & m <= n + m by NAT_1:29;
    then seq.(n+m) in Y1 & seq.(n+m) in Y2;
    then seq.(n+m) <= sup Y1 & inf Y2 <= seq.(n+m)
     by A4,SEQ_4:def 4,SEQ_4:def 5;
    hence inf Y2 <= (superior_realsequence seq).m by A2,AXIOMS:22;
  end;
  hence thesis by Th14,A1;
end;

theorem Th59:
  seq is bounded implies
    sup(inferior_realsequence seq) <= (superior_realsequence seq).n
proof
   assume A0: seq is bounded;
   reconsider Y2 = {seq.k2 : n <= k2} as Subset of REAL by Th33;
   B1 : (superior_realsequence seq).n = sup Y2 by def5;
   now let m;
    reconsider Y1 = {seq.k1 : m <= k1} as Subset of REAL by Th33;
    B2: (inferior_realsequence seq).m = inf Y1 by def4;
    Y1 is bounded & Y2 is bounded by A0,Th37;
    then B4: Y1 is bounded_below & Y2 is bounded_above by SEQ_4:def 3;
    n <= n + m  & m <= n + m by NAT_1:29;
    then seq.(n+m) in Y1 & seq.(n+m) in Y2;
    then seq.(n+m) >= inf Y1 & sup Y2 >= seq.(n+m)
     by B4,SEQ_4:def 4,SEQ_4:def 5;
    hence sup Y2 >= (inferior_realsequence seq).m by B2,AXIOMS:22;
   end;
  hence thesis by Th13,B1; 
end;

theorem Th60:
  seq is bounded implies sup(inferior_realsequence seq) <=
    inf(superior_realsequence seq)
proof
  set seq1 = (inferior_realsequence seq);
  set r1 = sup seq1;
  set r = inf(superior_realsequence seq);
  assume seq is bounded;
  then seq1.n <= r by Th58;
  hence thesis by Th13;
end;

theorem Th61:
  seq is bounded implies superior_realsequence seq is bounded &
    inferior_realsequence seq is bounded
proof assume A0: seq is bounded;
  then seq is bounded_above & seq is bounded_below by SEQ_2:def 5;
  then superior_realsequence seq is non-increasing &
  inferior_realsequence seq is non-decreasing by Th56,Th55;
  then A1: superior_realsequence seq is bounded_above &
   inferior_realsequence seq is bounded_below by LIMFUNC1:26;
  now let m;
   (inferior_realsequence seq).m <= inf (superior_realsequence seq)
    by A0,Th58;
   hence (inferior_realsequence seq).m <
    (inf (superior_realsequence seq)) +1 by LM1;
  end;
  then A3: (inferior_realsequence seq) is bounded_above by SEQ_2:def 3;
  now let m;
    sup(inferior_realsequence seq) <= (superior_realsequence seq).m
      by A0,Th59;
  hence (sup(inferior_realsequence seq)) - 1 <
    (superior_realsequence seq).m by LM1;
  end;
  then (superior_realsequence seq) is bounded_below by SEQ_2:def 4;
  hence thesis by A1,A3,SEQ_2:def 5;
end;

theorem Th62:
  seq is bounded implies inferior_realsequence seq is convergent &
    lim inferior_realsequence seq = sup inferior_realsequence seq
proof
  assume A0: seq is bounded;then 
  seq is bounded_above & seq is bounded_below by SEQ_2:def 5;then 
  A1: inferior_realsequence seq is non-decreasing by Th55;
  inferior_realsequence seq is bounded by A0,Th61;then
  inferior_realsequence seq is bounded_above by SEQ_2:def 5;
  hence thesis by A1,Th28,SEQ_4:51;
end;

theorem Th63:
  seq is bounded implies superior_realsequence seq is convergent &
    lim superior_realsequence seq = inf superior_realsequence seq
proof
  assume A0: seq is bounded;then
  seq is bounded_above & seq is bounded_below by SEQ_2:def 5;then
  A1: superior_realsequence seq is non-increasing by Th56;
  superior_realsequence seq is bounded by A0,Th61;
  then superior_realsequence seq is bounded_below by SEQ_2:def 5;
  hence thesis by A1,Th29,SEQ_4:52;
end;

theorem Th64:
  seq is bounded_below implies
    (inferior_realsequence seq).n = - (superior_realsequence(-seq)).n
proof assume seq is bounded_below;then
A1: -seq is bounded_above & seq ^\n is bounded_below by Th31,Th16;
    (inferior_realsequence seq).n = inf (seq ^\n) by Th110
                                  .= - sup -(seq ^\n) by A1,Th18
                                  .= - sup ((-seq) ^\n) by SEQM_3:38;
   hence thesis by Th42;
end;

theorem Th65:
  seq is bounded_above implies
    (superior_realsequence seq).n = - (inferior_realsequence(-seq)).n
proof assume seq is bounded_above;then
A1: -seq is bounded_below & seq ^\n is bounded_above by Th30,Th15;
    (superior_realsequence seq).n = sup (seq ^\n) by Th42
                                  .= - inf -(seq ^\n) by A1,Th17
                                  .= - inf ((-seq) ^\n) by SEQM_3:38;
   hence thesis by Th110;
end;

theorem Th66:
  seq is bounded_below implies
    (inferior_realsequence seq) = - (superior_realsequence(-seq))
proof assume seq is bounded_below;then
  (inferior_realsequence seq).n = - (superior_realsequence(-seq)).n by Th64;
  hence thesis by SEQ_1:14;
end;

theorem Th67:
  seq is bounded_above implies
    (superior_realsequence seq) = - (inferior_realsequence(-seq))
proof assume seq is bounded_above;then
  (superior_realsequence seq).n = - (inferior_realsequence(-seq)).n by Th65;
  hence thesis by SEQ_1:14;
end;

theorem
  seq is non-decreasing implies seq.n <= (inferior_realsequence seq).(n+1)
proof
  assume A0: seq is non-decreasing;
  reconsider Y1 = {seq.k : n+1 <= k} as Subset of REAL by Th33;
A2: (inferior_realsequence seq).(n+1) = inf Y1 by def4;
  inf Y1 = seq.(n+1) by A0,Th38;
  hence thesis by A2,A0,SEQM_3:def 13;
end;

Th69:
  seq is non-decreasing implies (inferior_realsequence seq).n = seq.n
proof
  assume A0: seq is non-decreasing;
  reconsider Y1 = {seq.k : n <= k} as Subset of REAL by Th33;
  (inferior_realsequence seq).n = inf Y1 by def4;
  hence thesis by A0,Th38;
end;

theorem 
  seq is non-decreasing implies inferior_realsequence seq = seq
proof
  assume seq is non-decreasing;
  then (inferior_realsequence seq).n = seq.n by Th69;
  hence thesis by FUNCT_2:113;
end;

theorem Th71:
  seq is non-decreasing bounded_above implies
    seq.n <= (superior_realsequence seq).(n+1)
proof
  assume A0: seq is non-decreasing bounded_above;
  then A1: seq.n <= seq.(n+1) by SEQM_3:def 13;
  reconsider Y1 = {seq.k : n+1 <= k} as Subset of REAL by Th33;
  A2: (superior_realsequence seq).(n+1) = sup Y1 by def5;
  A3: Y1 <> {} & Y1 is bounded_above by SETLIM_1:3,A0,Th35;
  seq.(n+1) in Y1;
  then seq.(n+1) <= sup Y1 by A3,SEQ_4:def 4;
  hence thesis by A2,A1,AXIOMS:22;
end;

theorem Th72:
  seq is non-decreasing bounded_above implies
    (superior_realsequence seq).n = (superior_realsequence seq).(n+1)
proof
  assume A0: seq is non-decreasing bounded_above;
  then seq.n <= (superior_realsequence seq).(n+1) by Th71;
  then max((superior_realsequence seq).(n+1),seq.n)
       = (superior_realsequence seq).(n+1) by SQUARE_1:def 2;
  hence thesis by A0,Th52;
end;

theorem Th73:
  seq is non-decreasing bounded_above implies
    (superior_realsequence seq).n = sup seq &
    (superior_realsequence seq) is constant
proof
  assume A00: seq is non-decreasing bounded_above;
  defpred P[Nat] means (superior_realsequence seq).$1 = sup seq;
  for n being Nat holds (superior_realsequence seq).n = sup seq
  proof
   A01: P[0] by A00,Th44;
   A02: for k st P[k] holds P[k+1] by A00,Th72;
   thus for k holds P[k] from NAT_1:sch 1(A01,A02);
  end;
  hence thesis by SEQM_3:def 6;
end;

theorem
  seq is non-decreasing bounded_above implies
    inf (superior_realsequence seq) = sup seq
proof
  assume A0: seq is non-decreasing bounded_above;then
  (superior_realsequence seq) is constant by Th73;
  then consider r1 such that
  A1: rng (superior_realsequence seq)={r1} by SEQM_3:15;
  r1 in rng (superior_realsequence seq) by A1,TARSKI:def 1;
  then ex n st r1 = (superior_realsequence seq).n by RFUNCT_2:9;then
  rng (superior_realsequence seq)= {sup seq} by A1,A0,Th73;then
  inf (superior_realsequence seq) = inf {sup seq} by def2
                          .= sup seq by SEQ_4:22;
  hence thesis;
end;

theorem
  seq is non-increasing implies (superior_realsequence seq).(n+1) <= seq.n
proof
 assume A0: seq is non-increasing;
 reconsider Y1 = {seq.k : n+1 <= k} as Subset of REAL by Th33;
 A2: (superior_realsequence seq).(n+1) = sup Y1 by def5;
 sup Y1 = seq.(n+1) by A0,Th39;
 hence thesis by A2,A0,SEQM_3:def 14;
end;

Th76:
  seq is non-increasing implies (superior_realsequence seq).n = seq.n
proof
 assume A0: seq is non-increasing;
 reconsider Y1 = {seq.k : n <= k} as Subset of REAL by Th33;
 A2: (superior_realsequence seq).n = sup Y1 by def5;
 thus thesis by A2,A0,Th39;
end;

theorem
  seq is non-increasing implies superior_realsequence seq = seq
proof
  assume seq is non-increasing;
  then (superior_realsequence seq).n = seq.n by Th76;
  hence thesis by FUNCT_2:113;
end;

theorem Th78:
  seq is non-increasing bounded_below implies
    (inferior_realsequence seq).(n+1) <= seq.n
proof
 assume A0: seq is non-increasing bounded_below;
 then A1: seq.(n+1) <= seq.n by SEQM_3:def 14;
 reconsider Y1 = {seq.k : n+1 <= k} as Subset of REAL by Th33;
 A2: (inferior_realsequence seq).(n+1) = inf Y1 by def4;
 A3: Y1 <> {} & Y1 is bounded_below by SETLIM_1:3,A0,Th36;
 seq.(n+1) in Y1;
 then inf Y1 <= seq.(n+1) by A3,SEQ_4:def 5;
 hence thesis by A2,A1,AXIOMS:22;
end;

theorem Th79:
  seq is non-increasing bounded_below implies
    (inferior_realsequence seq).n = (inferior_realsequence seq).(n+1)
proof
  assume A0: seq is non-increasing bounded_below;
  then (inferior_realsequence seq).(n+1) <= seq.n by Th78;
  then min((inferior_realsequence seq).(n+1),seq.n)
       = (inferior_realsequence seq).(n+1) by SQUARE_1:def 1;
  hence thesis by A0,Th51;
end;

theorem Th80:
  seq is non-increasing & seq is bounded_below implies
    (inferior_realsequence seq).n = inf seq &
      (inferior_realsequence seq) is constant
proof
  assume A00: seq is non-increasing & seq is bounded_below;
  defpred P[Nat] means (inferior_realsequence seq).$1 = inf seq;
  for n being Nat holds (inferior_realsequence seq).n = inf seq
  proof
A01: P[0] by A00,Th43;
A02: for k st P[k] holds P[k+1] by A00,Th79;
     thus for k holds P[k] from NAT_1:sch 1(A01,A02);
  end;
  hence thesis by SEQM_3:def 6;
end;

theorem 
  seq is non-increasing bounded_below implies
    sup inferior_realsequence seq = inf seq
proof
  assume A0: seq is non-increasing bounded_below;then
  inferior_realsequence seq is constant by Th80;
  then consider r1 such that
A1: rng inferior_realsequence seq={r1} by SEQM_3:15;
   r1 in rng inferior_realsequence seq by A1,TARSKI:def 1;then
A2: ex n st r1 = (inferior_realsequence seq).n by RFUNCT_2:9;
   sup inferior_realsequence seq = sup rng inferior_realsequence seq by def1
                          .= sup {inf seq} by A2,A1,A0,Th80
                          .= inf seq by SEQ_4:22; 
  hence thesis;
end;

theorem Th82:
seq1 is bounded & seq2 is bounded & (for n holds seq1.n <= seq2.n) implies
 ((for n holds (superior_realsequence seq1).n <=
   (superior_realsequence seq2).n) &
 (for n holds (inferior_realsequence seq1).n <=
   (inferior_realsequence seq2).n))
proof assume that 
A0: seq1 is bounded & seq2 is bounded and
A1: for n holds seq1.n <= seq2.n;
    now let n;
     reconsider Y1 = {seq1.k1 : n <= k1} as Subset of REAL by Th33;
     reconsider Y2 = {seq2.k2 : n <= k2} as Subset of REAL by Th33;
B1: (superior_realsequence seq1).n = sup Y1 &
       (superior_realsequence seq2).n = sup Y2 by def5;
B2: (inferior_realsequence seq1).n = inf Y1 &
       (inferior_realsequence seq2).n = inf Y2 by def4;
B3: Y1 <> {} & Y2 <> {} by SETLIM_1:3;
B4: Y1 is bounded & Y2 is bounded by A0,Th37;then
B5: Y1 is bounded_above & Y1 is bounded_below by SEQ_4:def 3;
B6: Y2 is bounded_above & Y2 is bounded_below by B4,SEQ_4:def 3;
B7: now let r;
       assume r in Y1;
       then consider k such that
C0:    r = seq1.k & n <= k;
C1:    r <= seq2.k by A1,C0;
       seq2.k in Y2 by C0;then
       seq2.k <= sup Y2 by B6,SEQ_4:def 4;
       hence r <= sup Y2 by C1,AXIOMS:22;
      end;
      now let r;
       assume r in Y2;
       then consider k such that
D0:    r = seq2.k & n <= k;
D1:    seq1.k <= r by A1,D0;
       seq1.k in Y1 by D0;then
       inf Y1 <= seq1.k by B5,SEQ_4:def 5;
       hence inf Y1 <= r by D1,AXIOMS:22;
      end;then
     for r be Real st r in Y2 holds inf Y1 <= r;
    hence (superior_realsequence seq1).n <= (superior_realsequence seq2).n &
      (inferior_realsequence seq1).n <= (inferior_realsequence seq2).n
        by COMPLSP1:84,B2,B7,B1,B3,TOPMETR3:1;
    end;
   hence thesis;
end;

theorem 
seq1 is bounded_below & seq2 is bounded_below implies
 (inferior_realsequence(seq1+seq2)).n >= 
 (inferior_realsequence seq1).n + (inferior_realsequence seq2).n
 proof assume seq1 is bounded_below & seq2 is bounded_below;then
    seq1 ^\n is bounded_below & seq2 ^\n is bounded_below by Th31;then
    inf(seq1 ^\n + seq2 ^\n) >= inf(seq1 ^\n) + inf(seq2 ^\n) by Th19;then
A2: inf((seq1+seq2) ^\n) >= inf(seq1 ^\n) + inf(seq2 ^\n) by SEQM_3:37;
    (inferior_realsequence seq1).n = inf(seq1 ^\n) &
     (inferior_realsequence seq2).n = inf(seq2 ^\n) by Th110;
hence thesis by A2,Th110;
end;

theorem 
  seq1 is bounded_above & seq2 is bounded_above implies
   (superior_realsequence(seq1+seq2)).n <=
   (superior_realsequence seq1).n + (superior_realsequence seq2).n
proof assume seq1 is bounded_above & seq2 is bounded_above;then
    seq1 ^\n is bounded_above & seq2 ^\n is bounded_above by Th30;then
    sup(seq1 ^\n + seq2 ^\n) <= sup(seq1 ^\n) + sup(seq2 ^\n) by Th20;then
A2: sup((seq1+seq2) ^\n) <= sup(seq1 ^\n) + sup(seq2 ^\n) by SEQM_3:37;
    (superior_realsequence seq1).n = sup(seq1 ^\n) &
     (superior_realsequence seq2).n = sup(seq2 ^\n) by Th42;
  hence thesis by A2,Th42;
end;

theorem 
seq1 is bounded_below nonnegative &
seq2 is bounded_below nonnegative implies
 (inferior_realsequence(seq1(#)seq2)).n >=
 (inferior_realsequence seq1).n * (inferior_realsequence seq2).n
 proof assume seq1 is bounded_below nonnegative &
    seq2 is bounded_below nonnegative;then
    seq1 ^\n is bounded_below nonnegative &
    seq2 ^\n is bounded_below nonnegative by Th31,Th21;then
    inf((seq1 ^\n)(#)(seq2 ^\n))>=inf(seq1 ^\n) * inf(seq2 ^\n) by Th24;then
A2: inf((seq1 (#) seq2) ^\n) >= inf(seq1 ^\n) * inf(seq2 ^\n) by SEQM_3:42;
    (inferior_realsequence seq1).n = inf(seq1 ^\n) &
     (inferior_realsequence seq2).n = inf(seq2 ^\n) by Th110;
hence thesis by A2,Th110;
end;

theorem Th86:
seq1 is bounded_below nonnegative &
seq2 is bounded_below nonnegative implies
 (inferior_realsequence(seq1(#)seq2)).n >= 
 (inferior_realsequence seq1).n * (inferior_realsequence seq2).n
 proof assume seq1 is bounded_below nonnegative &
    seq2 is bounded_below nonnegative;then
    seq1 ^\n is bounded_below nonnegative &
    seq2 ^\n is bounded_below nonnegative by Th31,Th21;then
    inf((seq1 ^\n)(#)(seq2 ^\n))>=inf(seq1 ^\n) * inf(seq2 ^\n) by Th24;then
A2: inf((seq1 (#) seq2) ^\n) >= inf(seq1 ^\n) * inf(seq2 ^\n) by SEQM_3:42;
    (inferior_realsequence seq1).n = inf(seq1 ^\n) &
     (inferior_realsequence seq2).n = inf(seq2 ^\n) by Th110;
  hence thesis by A2,Th110;
end;

theorem Th87:
  seq1 is bounded_above nonnegative &
  seq2 is bounded_above nonnegative implies
 (superior_realsequence(seq1(#)seq2)).n <=
 (superior_realsequence seq1).n * (superior_realsequence seq2).n
 proof assume seq1 is bounded_above nonnegative &
    seq2 is bounded_above nonnegative;then
    seq1 ^\n is bounded_above nonnegative &
    seq2 ^\n is bounded_above nonnegative by Th30,Th21;then
    sup((seq1 ^\n)(#)(seq2 ^\n))<=sup(seq1 ^\n) * sup(seq2 ^\n) by Th25;then
A2: sup((seq1 (#) seq2) ^\n) <= sup(seq1 ^\n) * sup(seq2 ^\n) by SEQM_3:42;
    (superior_realsequence seq1).n = sup(seq1 ^\n) &
     (superior_realsequence seq2).n = sup(seq2 ^\n) by Th42;
  hence thesis by A2,Th42;
end;

definition
  let seq be Real_Sequence;
  func lim_sup seq -> Element of REAL equals
:def6:  inf superior_realsequence seq;
  coherence;
end;

definition
  let seq be Real_Sequence;
  func lim_inf seq -> Element of REAL equals
:def7:  sup inferior_realsequence seq;
  coherence;
end;

theorem Th88:
  seq is bounded implies (lim_inf seq <= r iff
    (for s st 0<s holds (for n ex k st seq.(n+k)<r+s)))
proof set seq1 = inferior_realsequence seq;assume
A0: seq is bounded;then
A1: seq is bounded_below bounded_above by SEQ_2:def 5;
    seq1 is bounded by A0,Th61;then
A2: seq1 is bounded_below bounded_above by SEQ_2:def 5;
    thus lim_inf seq <= r implies 
     (for s st 0<s holds (for n ex k st seq.(n+k)<r+s))
     proof assume lim_inf seq <= r;then
      C1: sup seq1 <= r by def7;
          let s such that 
      C2: 0<s;
          for n ex k st seq.(n+k)<r+s
          proof let n;
            consider k such that
        E1: seq.(n+k) < seq1.n+s by A1,C2,Th45;
            seq1.n <= r by C1,A2,Th13;then
            seq.(n+k)+seq1.n < r+(seq1.n+s) by E1,REAL_1:67;then
            seq.(n+k) < r+(seq1.n+s)-seq1.n by REAL_1:86;
            hence thesis;
          end;
      hence thesis;
     end;
    assume
A3: for s st 0<s holds (for n ex k st seq.(n+k)<r+s);
    for s st 0<s holds lim_inf seq<=r+s
    proof let s such that
    G0: 0<s;
        for n holds seq1.n <= r+s
        proof let n;
          consider k such that
      H0: seq.(n+k)<r+s by G0,A3;
          seq1.n <= seq.(n+k) by A1,Th45;
          hence thesis by H0,AXIOMS:22;
        end;then
        sup seq1 <= r+s by Th13;
      hence thesis by def7;
    end;
  hence thesis by REAL_2:181;
end;

theorem Th89:
  seq is bounded implies (r <= lim_inf seq iff
  (for s st 0<s holds ex n st for k holds r-s<seq.(n+k)))
proof set seq1 = inferior_realsequence seq;assume
A0: seq is bounded;then
A1: seq is bounded_below bounded_above by SEQ_2:def 5;
    seq1 is bounded by A0,Th61;then
A2: seq1 is bounded_below bounded_above by SEQ_2:def 5;
    thus r <= lim_inf seq implies 
     (for s st 0<s holds (ex n st for k holds r-s<seq.(n+k)))
     proof assume r <= lim_inf seq;then
      C1: r <= sup seq1 by def7;
          let s such that
          D0: 0<s;
              ex n st for k holds r-s<seq.(n+k)
              proof 
               consider n1 be Nat such that
           F0: seq1.n1 > sup seq1-s by A2,D0,Th11;
               seq1.n1 + sup seq1 > r + (sup seq1-s) by F0,C1,REAL_1:67;then
               seq1.n1 + sup seq1 > r - s + sup seq1;then
           F1: (seq1.n1 + sup seq1) - sup seq1 > r-s by REAL_1:86;
               now let k;
                seq1.n1 <= seq.(n1+k) by A1,Th45;then
                (r-s) + seq1.n1 < seq.(n1+k) + seq1.n1 by F1,REAL_1:67;then
                (r-s) + seq1.n1 - seq1.n1 < seq.(n1+k) by REAL_1:84;
               hence r-s<seq.(n1+k);
               end;  
          hence thesis;
         end;
      hence thesis;
    end;
    assume
A3: for s st 0<s holds ex n st for k holds r-s<seq.(n+k);
    for s st 0<s holds r-s<=lim_inf seq
    proof let s such that
    I0: 0<s;
        consider n1 be Nat such that
    I2: for k holds r-s<seq.(n1+k) by I0,A3;
        for k holds r-s<=seq.(n1+k) by I2;then
    I3: r-s <= seq1.n1 by A1,Th47;
        seq1.n1 <= sup seq1 by A2,Th11;then
        r-s<=sup seq1 by I3,AXIOMS:22;
      hence thesis by def7;
    end;
 hence thesis by REAL_2:182;
end;

theorem 
  seq is bounded implies (r = lim_inf seq iff
    (for s st 0<s holds ((for n ex k st seq.(n+k)<r+s) &
    (ex n st for k holds r-s<seq.(n+k)))))
proof assume 
A0: seq is bounded;
    thus r = lim_inf seq implies 
    (for s st 0<s holds ((for n ex k st seq.(n+k)<r+s) & 
    (ex n st for k holds r-s<seq.(n+k)))) by A0,Th88,Th89;
    assume
A3: for s st 0<s holds ((for n ex k st seq.(n+k)<r+s) & 
    (ex n st for k holds r-s<seq.(n+k)));then
    for s st 0<s holds (for n ex k st seq.(n+k)<r+s);then
A4: lim_inf seq <= r by A0,Th88;
    for s st 0<s holds (ex n st for k holds r-s<seq.(n+k)) by A3;then
    r <= lim_inf seq by A0,Th89;
 hence thesis by A4,AXIOMS:21;
end;

theorem Th91:
  seq is bounded implies (r <= lim_sup seq iff
    (for s st 0<s holds (for n ex k st seq.(n+k)>r-s)))
proof set seq1 = superior_realsequence seq;assume
A0: seq is bounded;then
A1: seq is bounded_below bounded_above by SEQ_2:def 5;
    seq1 is bounded by A0,Th61;then
A2: seq1 is bounded_below bounded_above by SEQ_2:def 5;
    thus r <= lim_sup seq implies 
     (for s st 0<s holds (for n ex k st seq.(n+k)>r-s))
     proof assume r <= lim_sup seq;then
      C1: r <= inf seq1 by def6;
          let s such that 
      C2: 0<s;
          for n ex k st seq.(n+k)>r-s
          proof let n;
              consider k such that
          E1: seq.(n+k) > seq1.n-s by A1,C2,Th46;
              seq1.n >= r by C1,A2,Th14;then
              seq1.n+seq.(n+k) > r+(seq1.n-s) by E1,REAL_1:67;then
              seq.(n+k) > r+seq1.n-s-seq1.n by REAL_1:84;
           hence thesis;
          end;
      hence thesis;
     end;
    assume 
A3:for s st 0<s holds (for n ex k st seq.(n+k)>r-s);
    for s st 0<s holds lim_sup seq>=r-s
    proof let s such that
    G0: 0<s;
        for n holds r-s <= seq1.n 
        proof let n;
            consider k such that
        H0: r-s<seq.(n+k) by G0,A3;
            seq.(n+k) <= seq1.n by A1,Th46;
        hence thesis by H0,AXIOMS:22;
        end;then
        r-s <= inf seq1 by Th14;
    hence thesis by def6;
    end;
 hence thesis by REAL_2:182;     
end; 
 
theorem Th92:
  seq is bounded implies (lim_sup seq <= r iff
    (for s st 0<s holds (ex n st for k holds seq.(n+k)<r+s)))
proof set seq1 = superior_realsequence seq;assume
A0: seq is bounded;then
A1: seq is bounded_below bounded_above by SEQ_2:def 5;
    seq1 is bounded by A0,Th61;then
A2: seq1 is bounded_below bounded_above by SEQ_2:def 5;
    thus lim_sup seq <= r implies 
     (for s st 0<s holds (ex n st for k holds seq.(n+k)<r+s))
     proof assume
          lim_sup seq <= r;then
      C1: inf seq1 <= r by def6;
          for s st 0<s holds (ex n st for k holds seq.(n+k)<r+s)
          proof let s such that
          D0: 0<s;
              ex n st for k holds seq.(n+k)<r+s
              proof 
               consider n1 be Nat such that
           F0: seq1.n1 < inf seq1+s by A2,D0,Th12;
               seq1.n1 + inf seq1 < r + (inf seq1+s) by F0,C1,REAL_1:67;then
               seq1.n1 + inf seq1 < r + s + inf seq1;then
           F1: (seq1.n1 + inf seq1) - inf seq1 < r+s by REAL_1:84;
               now let k;
                seq.(n1+k) <= seq1.n1 by A1,Th46;then 
                seq.(n1+k) + seq1.n1 < (r+s) + seq1.n1 by F1,REAL_1:67;then
                seq.(n1+k) + seq1.n1 - seq1.n1 < (r+s) by REAL_1:84;
               hence seq.(n1+k) < r+s;
               end;  
              hence thesis;
             end;
          hence thesis;
         end;
     hence thesis;
    end;
    assume
A3: for s st 0<s holds (ex n st for k holds seq.(n+k)<r+s);
    for s st 0<s holds lim_sup seq<=r+s
    proof let s such that
    I0: 0<s;
        consider n1 be Nat such that
    I2: for k holds seq.(n1+k)<r+s by I0,A3;
        for k holds seq.(n1+k)<=r+s by I2;then
    I3: seq1.n1<=r+s by A1,Th49;
        inf seq1 <= seq1.n1 by A2,Th12;then
        inf seq1 <= r+s by I3,AXIOMS:22;
      hence thesis by def6;
    end;
  hence thesis by REAL_2:181;
end;

theorem 
  seq is bounded implies (r = lim_sup seq iff
    (for s st 0<s holds ((for n holds ex k st seq.(n+k)>r-s) &
    (ex n st for k holds seq.(n+k)<r+s))))
proof assume 
A0: seq is bounded;
    thus r = lim_sup seq implies
    (for s st 0<s holds ((for n holds ex k st seq.(n+k)>r-s) & 
    (ex n st for k holds seq.(n+k)<r+s))) by A0,Th91,Th92;
    assume
A3: for s st 0<s holds ((for n holds ex k st seq.(n+k)>r-s) &
    (ex n st for k holds seq.(n+k)<r+s));then
    for s st 0<s holds (for n holds ex k st seq.(n+k)>r-s);then
A4: r <= lim_sup seq by A0,Th91;
    for s st 0<s holds (ex n st for k holds seq.(n+k)<r+s) by A3;then
    lim_sup seq <= r by A0,Th92;
 hence thesis by A4,AXIOMS:21;
end;

theorem
  seq is bounded implies lim_inf seq <= lim_sup seq
proof
  assume A0: seq is bounded;
  A2: sup(inferior_realsequence seq) = lim_inf seq by def7;
  inf(superior_realsequence seq) = lim_sup seq by def6;
  hence thesis by A0,Th60,A2;
end;

theorem Th95:
  seq is bounded & lim_sup seq = lim_inf seq iff seq is convergent
proof
 thus seq is bounded & lim_sup seq = lim_inf seq implies seq is convergent
 proof assume that 
  A0: seq is bounded & lim_sup seq = lim_inf seq;
  A1: seq is bounded_above & seq is bounded_below by A0,SEQ_2:def 5;
  A2: superior_realsequence seq is bounded &
      inferior_realsequence seq is bounded by A0,Th61;then
  A3: superior_realsequence seq is bounded_above bounded_below
      by SEQ_2:def 5;
  A4: inferior_realsequence seq is bounded_above bounded_below
      by A2,SEQ_2:def 5;
  A5: superior_realsequence seq is non-increasing &
      inferior_realsequence seq is non-decreasing by A1,Th56,Th55;
  A6: lim_sup seq = inf superior_realsequence seq &
      lim_inf seq = sup inferior_realsequence seq by def6,def7;
   reconsider r= inf superior_realsequence seq as real number;
   for s st 0<s ex n st for m st n<=m holds abs (seq.m-r) < s
    proof
       let s such that 
   C0: 0 < s;
       consider k1 be Nat such that
   C1: r - s < (inferior_realsequence seq).k1 by C0,A0,A4,A6,Th11;
       consider k2 be Nat such that
   C2: (superior_realsequence seq).k2 < r + s by C0,A3,Th12;
       reconsider k = max(k1,k2) as Nat;
       k1 <= k & k2 <= k by SQUARE_1:46;then
   C3: (inferior_realsequence seq).k1 <= (inferior_realsequence seq).k &
        (superior_realsequence seq).k <= (superior_realsequence seq).k2
        by A5,SEQM_3:12,SEQM_3:14;
      ex n st for m st n<=m holds abs(seq.m-r) < s
       proof
          take k;
          let m such that 
      D0: k <= m;
          consider k3 being Nat such that
      D1: m = k + k3 by D0,NAT_1:28;
          (inferior_realsequence seq).k <= seq.m &
          seq.m <= (superior_realsequence seq).k by A1,D1,Th46,Th45;then
          (inferior_realsequence seq).k1 <= seq.m &
          seq.m <= (superior_realsequence seq).k2 by C3,AXIOMS:22;
          then r - s < seq.m & seq.m < r + s by C1,C2,AXIOMS:22;
       hence abs(seq.m - r) < s by Th1;
      end;
     hence thesis;
    end;
  hence seq is convergent by SEQ_2:def 6;
 end;
 assume
  AA0: seq is convergent;then
  AA1: seq is bounded by SEQ_2:27;then
  AA2: seq is bounded_above & seq is bounded_below by SEQ_2:def 5;
  AA3: superior_realsequence seq is bounded &
       inferior_realsequence seq is bounded by AA1,Th61;then
  AA4: superior_realsequence seq is bounded_above bounded_below
         by SEQ_2:def 5;
  AA5: inferior_realsequence seq is bounded_above bounded_below
         by AA3,SEQ_2:def 5;
  AA6: lim_sup seq = inf superior_realsequence seq &
       lim_inf seq = sup inferior_realsequence seq by def6,def7;
       consider r such that
  AA7: for p st 0 < p ex n st for m st n<=m holds abs(seq.m-r) < p
       by AA0,SEQ_2:def 6;
  AA8: for p st 0 < p ex n st r - p <= (inferior_realsequence seq).n &
       (superior_realsequence seq).n <= r + p
       proof let p such that
        B0: 0 < p; 
            consider n such that
        B1: for m st n<=m holds abs(seq.m-r) < p by B0,AA7;
        B2: for m st n<=m holds r - p <= seq.m & seq.m <= r + p
            proof let m such that
         C0: n<=m;
             abs(seq.m-r) < p by C0,B1;
             hence thesis by Th1;
            end;then
            for m st n<=m holds r - p <= seq.m;then 
        B3: r - p <= (inferior_realsequence seq).n by AA2,Th48;
            for m st n<=m holds seq.m <= r + p by B2;then
            (superior_realsequence seq).n <= r + p by AA2,Th50;
         hence thesis by B3;
       end;
  AA9: for p holds 0 < p implies r - p <= sup inferior_realsequence seq &
       inf superior_realsequence seq <= r + p
       proof let p such that 
         B0: 0 < p;
             consider n such that
         B1: r - p <= (inferior_realsequence seq).n &
             (superior_realsequence seq).n <= r + p by B0,AA8;
             (inferior_realsequence seq).n <= sup inferior_realsequence seq &
             inf superior_realsequence seq <= (superior_realsequence seq).n
              by AA4,AA5,Th11,Th12;
          hence thesis by B1,AXIOMS:22;
       end;
       reconsider r as Real by XREAL_0:def 1;
       for p holds 0 < p implies r <= sup inferior_realsequence seq + p
       proof let p;assume 0 < p;then
         r - p <= sup inferior_realsequence seq by AA9;
        hence thesis by REAL_1:86;
       end;then
 AA10: r <= sup inferior_realsequence seq by COMPLSP1:83;
       for p holds 0 < p implies inf superior_realsequence seq <= r + p
       by AA9;then 
 AA11: inf superior_realsequence seq <= r by COMPLSP1:83;
       sup (inferior_realsequence seq) <= inf (superior_realsequence seq)
       by AA1,Th60;then 
       r <= sup inferior_realsequence seq &
       sup inferior_realsequence seq <= r &
       r <= inf superior_realsequence seq &
       inf superior_realsequence seq <= r by AA10,AA11,AXIOMS:22;then
       r = sup inferior_realsequence seq & r = inf superior_realsequence seq
       by AXIOMS:21;
   hence thesis by AA0,AA6,SEQ_2:27;
end;

theorem
  seq is convergent implies lim seq = lim_sup seq & lim seq = lim_inf seq
proof assume
 A0: seq is convergent;then 
 A1: seq is bounded by SEQ_2:27;then
 A2: seq is bounded_above & seq is bounded_below by SEQ_2:def 5;
 A3: superior_realsequence seq is bounded &
     inferior_realsequence seq is bounded by A1,Th61;then
 A4: superior_realsequence seq is bounded_above &
     superior_realsequence seq is bounded_below by SEQ_2:def 5;
 A5: inferior_realsequence seq is bounded_above &
     inferior_realsequence seq is bounded_below by A3,SEQ_2:def 5;
 A6: lim_sup seq = inf superior_realsequence seq &
     lim_inf seq = sup inferior_realsequence seq by def6,def7;
     reconsider r= lim seq as real number;
 A7: for p holds 0 < p implies r - p <= sup inferior_realsequence seq &
     inf superior_realsequence seq <= r + p
     proof
      let p such that 
  B0: 0 < p;
      consider k such that
  B1: for m st k <= m holds abs(seq.m-r)<p by B0,A0,SEQ_2:def 7;
  B2: for m st k <= m holds r - p <= seq.m & seq.m <= r + p
      proof
       let m such that
   C0: k <= m;
       abs(seq.m - r) < p by C0,B1;
      hence thesis by Th1;
      end;then 
      for m st k <= m holds r - p <= seq.m;then
  B3: r - p <= (inferior_realsequence seq).k by A2,Th48;
      for m st k <= m holds seq.m <= r + p by B2;then 
  B5: (superior_realsequence seq).k <= r + p by A2,Th50;
      (inferior_realsequence seq).k <= sup inferior_realsequence seq &
      inf superior_realsequence seq <= (superior_realsequence seq).k
      by A4,A5,Th11,Th12;
      hence r - p <= sup inferior_realsequence seq &
      inf superior_realsequence seq <= r + p by B3,B5,AXIOMS:22;
     end;
     for p holds 0 < p implies r <= sup inferior_realsequence seq + p
     proof
      let p;assume 0 < p;then
      r - p <= sup inferior_realsequence seq by A7;
     hence thesis by REAL_1:86;
     end;then
 A8: r <= sup inferior_realsequence seq by COMPLSP1:83;
     for p holds 0 < p implies inf superior_realsequence seq <= r + p by A7;
     then 
 A9: inf superior_realsequence seq <= r by COMPLSP1:83;
     sup(inferior_realsequence seq) <= inf(superior_realsequence seq)
     by A1,Th60;then 
     r <= sup inferior_realsequence seq &
     sup inferior_realsequence seq <= r &
     r <= inf superior_realsequence seq &
     inf superior_realsequence seq <= r by A8,A9,AXIOMS:22;
  hence thesis by A6,AXIOMS:21;
end;

theorem Th97:
  seq is bounded implies
    ((lim_sup -seq = - lim_inf seq) & (lim_inf -seq = - lim_sup seq))
proof assume 
A0: seq is bounded;then
A1: seq is bounded_below bounded_above & -seq is bounded
      by SEQ_2:def 5,SEQM_3:70;
    superior_realsequence seq is bounded &
    inferior_realsequence seq is bounded by A0,Th61;then
A3: superior_realsequence seq is bounded_below bounded_above &
    inferior_realsequence seq is bounded_above bounded_below
      by SEQ_2:def 5;
A4: - lim_inf seq = - sup inferior_realsequence seq by def7
                 .= -- inf - inferior_realsequence seq by A3,Th17
                 .= inf --superior_realsequence(-seq) by A1,Th66
                 .= inf superior_realsequence(-seq) by SEQ_1:36
                 .= lim_sup -seq by def6;
    - lim_sup seq = - inf superior_realsequence seq by def6
                 .= -- sup - superior_realsequence seq by A3,Th18
                 .= sup --inferior_realsequence(-seq) by A1,Th67
                 .= sup inferior_realsequence(-seq) by SEQ_1:36
                 .= lim_inf -seq by def7;
  hence thesis by A4;
end;

theorem
seq1 is bounded & seq2 is bounded & (for n holds seq1.n <= seq2.n) implies
 lim_sup seq1 <= lim_sup seq2 & lim_inf seq1 <= lim_inf seq2
proof assume 
  A0: seq1 is bounded & seq2 is bounded & for n holds seq1.n <= seq2.n;then
  A1: superior_realsequence seq1 is convergent &
      inferior_realsequence seq1 is convergent &
      superior_realsequence seq2 is convergent &
      inferior_realsequence seq2 is convergent by Th63,Th62;
  A2: (for n holds (superior_realsequence seq1).n <=
   (superior_realsequence seq2).n) &
      (for n holds (inferior_realsequence seq1).n <=
   (inferior_realsequence seq2).n) by A0,Th82;
      seq1 is bounded_above bounded_below &
      seq2 is bounded_above bounded_below by A0,SEQ_2:def 5;then
  A3: superior_realsequence seq1 is non-increasing &
      inferior_realsequence seq1 is non-decreasing &
      superior_realsequence seq2 is non-increasing &
      inferior_realsequence seq2 is non-decreasing by Th56,Th55;
      superior_realsequence seq1 is bounded &
      inferior_realsequence seq1 is bounded &
      superior_realsequence seq2 is bounded &
      inferior_realsequence seq2 is bounded by A0,Th61;then
      superior_realsequence seq1 is bounded_below &
      inferior_realsequence seq1 is bounded_above &
      superior_realsequence seq2 is bounded_below &
      inferior_realsequence seq2 is bounded_above by SEQ_2:def 5;then
  A5: lim superior_realsequence seq1 = inf superior_realsequence seq1 &
       lim inferior_realsequence seq1 = sup inferior_realsequence seq1 &
       lim superior_realsequence seq2 = inf superior_realsequence seq2 &
       lim inferior_realsequence seq2 = sup inferior_realsequence seq2
        by A3,Th28,Th29;
  A6: lim_sup seq1 = inf superior_realsequence seq1 &
       lim_sup seq2 = inf superior_realsequence seq2 by def6;
       lim_inf seq1 = sup inferior_realsequence seq1 &
       lim_inf seq2 = sup inferior_realsequence seq2 by def7;
 hence thesis by A6,A5,A1,A2,SEQ_2:32;
end;

theorem
seq1 is bounded & seq2 is bounded implies
 ((lim_inf seq1 + lim_inf seq2 <= lim_inf (seq1+seq2)) &
 (lim_inf (seq1+seq2) <= lim_inf seq1 + lim_sup seq2) &
 (lim_inf (seq1+seq2) <= lim_sup seq1 + lim_inf seq2) &
 (lim_inf seq1 + lim_sup seq2 <= lim_sup (seq1+seq2)) &
 (lim_sup seq1 + lim_inf seq2 <= lim_sup (seq1+seq2)) &
 (lim_sup (seq1+seq2) <= lim_sup seq1 + lim_sup seq2) &
 (seq1 is convergent or seq2 is convergent implies 
   (lim_inf (seq1+seq2) = lim_inf seq1 + lim_inf seq2) &
   (lim_sup (seq1+seq2) = lim_sup seq1 + lim_sup seq2)))
proof 
A00: for seq1,seq2 st (seq1 is bounded & seq2 is bounded) holds
   (lim_inf seq1 + lim_inf seq2 <= lim_inf (seq1+seq2))
   proof let seq1,seq2;
       set r1 = lim_inf seq1, r2 = lim_inf seq2; assume
  AA0: seq1 is bounded & seq2 is bounded;then
  AA1: (seq1+seq2) is bounded by SEQM_3:71;
       for s st 0<s holds ex n st for k holds
        r1+r2-s < (seq1+seq2).(n+k)
       proof let s such that
      BA0: 0<s;
      BA1: 0<s/2 by BA0,SEQ_2:3;
           consider n1 being Nat such that
      BA2: for k holds r1-s/2 < seq1.(n1+k) by AA0,BA1,Th89;
           consider n2 being Nat such that
      BA3: for k holds r2-s/2 < seq2.(n2+k) by AA0,BA1,Th89;
           for k holds r1+r2-s < (seq1+seq2).(n1+n2+k) 
           proof let k;
        CC1: r1-s/2 < seq1.(n1+(n2+k)) by BA2;
             r2-s/2 < seq2.(n2+(n1+k)) by BA3;then
             (r1-s/2)+(r2-s/2) < seq1.(n1+n2+k) + seq2.(n1+n2+k)
               by CC1,REAL_1:67;
             hence thesis by SEQ_1:11;
           end;
        hence thesis;
       end;
     hence thesis by AA1,Th89;
   end;
A01: for seq1,seq2 st (seq1 is bounded & seq2 is bounded) holds 
   (lim_inf (seq1+seq2) <= lim_inf seq1 + lim_sup seq2)
   proof let seq1,seq2;assume 
  AB0: seq1 is bounded & seq2 is bounded;then
  AB1: seq1+seq2 is bounded & -seq1 is bounded & -seq2 is bounded
        by SEQM_3:70,SEQM_3:71;
       lim_inf ((seq1+seq2)+(-seq2)) >= lim_inf (seq1+seq2) + lim_inf (-seq2) 
        by AB1,A00;then
       lim_inf (seq1+seq2+-seq2) >= lim_inf (seq1+seq2) +- lim_sup seq2 
        by AB0,Th97;then
       lim_inf (seq1+seq2-seq2) >= lim_inf (seq1+seq2) - lim_sup seq2 
        by SEQ_1:15;then
       lim_inf seq1 >= lim_inf (seq1+seq2) - lim_sup seq2 by Th6;
     hence thesis by REAL_1:86;    
   end;
A02: for seq1,seq2 st seq1 is bounded & seq2 is bounded holds
   lim_sup (seq1+seq2) <= lim_sup seq1 + lim_sup seq2
   proof let seq1,seq2;
       set r1 = lim_sup seq1, r2 = lim_sup seq2; assume
  AC0: seq1 is bounded & seq2 is bounded;then
  AC1: (seq1+seq2) is bounded by SEQM_3:71;
       for s st 0<s holds ex n st for k holds
       (seq1+seq2).(n+k) < r1+r2+s
       proof let s such that
      BB0: 0<s;
      BB1: 0<s/2 by BB0,SEQ_2:3;
           consider n1 being Nat such that
      BB2: for k holds seq1.(n1+k) < r1+s/2 by AC0,BB1,Th92;
           consider n2 being Nat such that
      BB3: for k holds seq2.(n2+k) < r2+s/2 by AC0,BB1,Th92;
           for k holds (seq1+seq2).(n1+n2+k) < r1+r2+s
           proof let k;
        CC1: seq1.(n1+(n2+k)) < r1+s/2 by BB2;
             seq2.(n2+(n1+k)) < r2+s/2 by BB3;then
             seq1.(n1+n2+k) + seq2.(n1+n2+k) < (r1+s/2)+(r2+s/2)
               by CC1,REAL_1:67;
             hence thesis by SEQ_1:11;
           end;
        hence thesis;
       end;
     hence thesis by AC1,Th92;
   end;
A03: for seq1,seq2 st seq1 is bounded & seq2 is bounded holds
   lim_inf seq1 + lim_sup seq2 <= lim_sup (seq1+seq2)
   proof let seq1,seq2;assume 
  AD0: seq1 is bounded & seq2 is bounded;then
  AD1: seq1+seq2 is bounded & -seq1 is bounded & -seq2 is bounded 
        by SEQM_3:70,SEQM_3:71;
       lim_sup ((seq1+seq2)+(-seq1)) <= lim_sup (-seq1) + lim_sup (seq1+seq2)  
        by AD1,A02;then
       lim_sup (seq1+seq2+-seq1) <= -lim_inf seq1 + lim_sup (seq1+seq2) 
        by AD0,Th97;then
       lim_sup (seq2+seq1-seq1) <= lim_sup (seq1+seq2) - lim_inf seq1 
        by SEQ_1:15;then
       lim_sup seq2 <= lim_sup (seq1+seq2) - lim_inf seq1 by Th6;
       hence thesis by REAL_1:84; 
   end;
    assume 
A0: seq1 is bounded & seq2 is bounded;then
A1: lim_inf seq1 + lim_inf seq2 <= lim_inf (seq1+seq2) by A00;
A2: lim_inf (seq1+seq2) <= lim_inf seq1 + lim_sup seq2 &
    lim_inf (seq1+seq2) <= lim_sup seq1 + lim_inf seq2 by A0,A01;
A3: lim_inf seq1 + lim_sup seq2 <= lim_sup (seq1+seq2) &
    lim_sup seq1 + lim_inf seq2 <= lim_sup (seq1+seq2) by A0,A03;
A4: lim_sup (seq1+seq2) <= lim_sup seq1 + lim_sup seq2 by A0,A02;
    seq1 is convergent or seq2 is convergent implies
    lim_inf (seq1+seq2) = lim_inf seq1 + lim_inf seq2 &
    lim_sup (seq1+seq2) = lim_sup seq1 + lim_sup seq2
    proof assume 
    B0: seq1 is convergent or seq2 is convergent;
       per cases by B0;
        suppose seq1 is convergent;
         then lim_inf seq1 = lim_sup seq1 by Th95;
        hence thesis by A1,A2,A3,A4,AXIOMS:21;
        end;
        suppose seq2 is convergent;
         then lim_inf seq2 = lim_sup seq2 by Th95;
        hence thesis by A1,A2,A3,A4,AXIOMS:21;
        end;
    end;
 hence thesis by A0,A00,A01,A02,A03;
end;

theorem
seq1 is bounded nonnegative &
seq2 is bounded nonnegative implies
 (lim_inf seq1) * (lim_inf seq2) <= lim_inf(seq1(#)seq2) &
 lim_sup(seq1(#)seq2) <= (lim_sup seq1) * (lim_sup seq2)
 proof
A00: for seq1,seq2 st seq1 is bounded nonnegative &
   seq2 is bounded nonnegative holds
   (lim_inf seq1) * (lim_inf seq2) <= lim_inf(seq1(#)seq2)
    proof let seq1,seq2;assume
  AA0: seq1 is bounded nonnegative &
       seq2 is bounded nonnegative;then
  AA1: seq1 is bounded_below & seq2 is bounded_below by SEQ_2:def 5;
       set seq3 = inferior_realsequence seq1,
           seq4 = inferior_realsequence seq2,
           seq5 = inferior_realsequence(seq1(#)seq2);
  AA2: seq3 is bounded & seq4 is bounded &
       seq3 is convergent & seq4 is convergent by AA0,Th61,Th62;then
  AA3: (seq3(#)seq4) is convergent by SEQ_2:28;
  AA4: seq1(#)seq2 is bounded by AA0,SEQM_3:72;then
  AA5: seq5 is bounded & seq5 is convergent by Th61,Th62;
       for n holds seq5.n >= (seq3(#)seq4).n
       proof let n;
         seq5.n >= seq3.n * seq4.n by AA0,AA1,Th86;
         hence thesis by SEQ_1:12;
       end;then
  AA6: lim seq5 >= lim (seq3(#)seq4) by AA5,AA3,SEQ_2:32;
  AA7: sup seq5 = lim seq5 & sup seq3 = lim seq3 & sup seq4 =lim seq4
         by AA0,AA4,Th62;
       lim_inf seq1 = sup seq3 & lim_inf seq2 = sup seq4 &
       lim_inf (seq1(#)seq2) = sup seq5 by def7;
     hence thesis by AA7,AA6,AA2,SEQ_2:29;
    end;    
  for seq1,seq2 st seq1 is bounded & seq1 is nonnegative &
   seq2 is bounded & seq2 is nonnegative holds 
   lim_sup(seq1(#)seq2) <= (lim_sup seq1) * (lim_sup seq2)
    proof let seq1,seq2;assume
  AB0: seq1 is bounded nonnegative &
       seq2 is bounded nonnegative;then
  AB1: seq1 is bounded_above & seq2 is bounded_above by SEQ_2:def 5;
       set seq3 = superior_realsequence seq1,
           seq4 = superior_realsequence seq2,
           seq5 = superior_realsequence(seq1(#)seq2);
  AB2: seq3 is bounded & seq4 is bounded &
       seq3 is convergent & seq4 is convergent by AB0,Th61,Th63;then
  AB3: (seq3(#)seq4) is convergent by SEQ_2:28;
  AB4: seq1(#)seq2 is bounded by AB0,SEQM_3:72;then
  AB5: seq5 is bounded & seq5 is convergent by Th61,Th63;
       for n holds seq5.n <= (seq3(#)seq4).n
       proof let n;
         seq5.n <= seq3.n * seq4.n by AB0,AB1,Th87;
         hence thesis by SEQ_1:12;
       end;then
  AB6: lim seq5 <= lim (seq3(#)seq4) by AB5,AB3,SEQ_2:32;
  AB7: inf seq5 = lim seq5 & inf seq3 = lim seq3 & inf seq4 =lim seq4 
         by AB0,AB4,Th63;
       lim_sup seq1 = inf seq3 & lim_sup seq2 = inf seq4 &
       lim_sup (seq1(#)seq2) = inf seq5 by def6;
     hence thesis by AB6,AB7,AB2,SEQ_2:29;
    end; 
  hence thesis by A00;
 end;
