:: Replacement of Subtrees in a Tree
::  by Oleg Okhotnikov
::
:: Received October 1, 1995
:: Copyright (c) 1995 Association of Mizar Users

environ

 vocabularies TREES_1, FINSEQ_1, NUMBERS, SUBSET_1, ORDINAL4, XBOOLE_0, TARSKI,
      RELAT_1, XXREAL_0, ARYTM_3, TREES_2, FUNCT_1, TREES_A;
 notations TARSKI, XBOOLE_0, SUBSET_1, NUMBERS, XXREAL_0, NAT_1, RELAT_1,
      FUNCT_1, FINSEQ_1, TREES_1, TREES_2;
 constructors XXREAL_0, NAT_1, MEMBERED, TREES_2, RELSET_1, FINSEQ_2;
 registrations XBOOLE_0, RELSET_1, MEMBERED, FINSEQ_1, TREES_2;
 requirements NUMERALS, BOOLE, SUBSET;
 definitions TARSKI, XBOOLE_0, TREES_1, TREES_2, RELAT_1;
 theorems TARSKI, FUNCT_1, FINSEQ_1, TREES_1, TREES_2, XBOOLE_0, XBOOLE_1,
      RELAT_1;
 schemes TREES_2;

begin

reserve T, T1 for Tree,
  P for AntiChain_of_Prefixes of T,
  p1 for FinSequence,
  p, q, r, s, p9 for FinSequence of NAT,
  x, Z for set,
  t for Element of T,
  k, n for Element of NAT;

theorem Th1:
  for p,q,r,s being FinSequence st p^q = s^r holds p,s are_c=-comparable
proof
  let p,q,r,s be FinSequence;
  assume
A1: p^q = s^r;
  A2: p is_a_prefix_of s^r & s is_a_prefix_of p^q by A1,TREES_1:8;
  thus thesis by A1,A2,TREES_2:1;
end;

definition
  let T,T1;
  let P such that
A1: P<>{};
  func tree(T,P,T1) -> Tree means
  :Def1:
  q in it iff
  (q in T & for p st p in P holds not p is_a_proper_prefix_of q)
  or ex p,r st p in P & r in T1 & q = p^r;
  existence
  proof
    reconsider P9 = P as Subset of T by TREES_1:def 14;
A2: now
      let x;
      assume
A3:   x in P9;
      reconsider x9 = x as FinSequence by A3,TREES_1:def 13;
      reconsider x9 as Element of T by A3;
A4:   now
        let p such that
A5:     p in P;
        per cases;
        suppose
A6:       p <> x9;
A7:       not p,x9 are_c=-comparable by A3,A5,A6,TREES_1:def 13;
          thus not p is_a_proper_prefix_of x9 by A7,XBOOLE_1:104;
        end;
        suppose
A8:       p = x9;
          thus not p is_a_proper_prefix_of x9 by A8;
        end;
      end;
      thus x in {t : for p st p in P holds not p is_a_proper_prefix_of t}
      by A4;
    end;
A9: P c= {t : for p st p in P holds not p is_a_proper_prefix_of t}
    by A2,TARSKI:def 3;
    reconsider Y = {t : for p st p in P holds not p is_a_proper_prefix_of
    t} as non empty set by A1,A9,XBOOLE_1:3;
    consider Z such that
A10: Z = {p^s where p is Element of T, s is Element of T1 : p in P};
    reconsider X = Y \/ Z as non empty set;
A11: x in {t : for p st p in P holds not p is_a_proper_prefix_of t} implies
    x is FinSequence of NAT & x in NAT* & x in T
    proof
      assume
      A12:  x in {t : for p st p in P holds not p is_a_proper_prefix_of t};
      A13:  ex
 t st x = t & for p st p in P holds not p is_a_proper_prefix_of t by A12;
      thus x is FinSequence of NAT by A13;
      thus thesis by A13,FINSEQ_1:def 11;
    end;
A14: X is Tree-like
    proof
      thus X c= NAT*
      proof
        let x;
        assume
A15:    x in X;
        A16:    x
 in {t : for p st p in P holds not p is_a_proper_prefix_of t } or
        x in {p^s where p is Element of T, s is Element of T1 : p in P}
        by A10,A15,XBOOLE_0:def 3;
A17:    now
          assume
          A18:      x
 in {p^s where p is Element of T, s is Element of T1 : p in
          P };
A19:      ex p being Element of T st ex s being Element of T1 st
          x = p^s & p in P by A18;
          thus x is FinSequence of NAT by A19;
        end;
        thus thesis by A11,A16,A17,FINSEQ_1:def 11;
      end;
      thus for q st q in X holds ProperPrefixes q c= X
      proof
        let q such that
A20:    q in X;
A21:    now
          assume
A22:      q in {t : for p st p in P holds not p is_a_proper_prefix_of t};
A23:      ex t st q = t &
          for p st p in P holds not p is_a_proper_prefix_of t by A22;
A24:      ProperPrefixes q c= T by A23,TREES_1:def 5;
          thus ProperPrefixes q c= X
          proof
            let x;
            assume
A25:        x in ProperPrefixes q;
            consider p1 such that
A26:        x = p1 and
A27:        p1 is_a_proper_prefix_of q by A25,TREES_1:def 4;
            reconsider p1 as Element of T by A24,A25,A26;
A28:        for p st p in P holds not p is_a_proper_prefix_of p1
            proof
              let p such that
A29:          p in P;
A30:          ex t st q = t & for p st p in P holds not p
              is_a_proper_prefix_of t by A22;
              thus thesis by A27,A29,A30,XBOOLE_1:56;
            end;
A31:        x in { s1 where s1 is Element of T : for p st p in P holds
            not p is_a_proper_prefix_of s1 } by A26,A28;
            thus thesis by A31,XBOOLE_0:def 3;
          end;
        end;
A32:    now
          assume
A33:      q in Z;
          consider p being Element of T, s being Element of T1 such that
A34:      q = p^s and
A35:      p in P by A10,A33;
          thus ProperPrefixes q c= X
          proof
            let x;
            assume
A36:        x in ProperPrefixes q;
            reconsider r = x as FinSequence by A36,TREES_1:35;
A37:        r is_a_proper_prefix_of p^s by A34,A36,TREES_1:36;
A38:        r is_a_prefix_of p^s by A37,XBOOLE_0:def 8;
            consider r1 being FinSequence such that
A39:        p^s = r^r1 by A38,TREES_1:8;
A40:        now
              assume
A41:          len p <= len r;
              consider r2 being FinSequence such that
A42:          p^r2 = r by A39,A41,FINSEQ_1:64;
A43:          p^s = p^(r2^r1) by A39,A42,FINSEQ_1:45;
A44:          s = r2^r1 by A43,FINSEQ_1:46;
A45:          s|(dom r2) = r2 by A44,FINSEQ_1:33;
A46:          s|Seg(len r2) = r2 by A45,FINSEQ_1:def 3;
              reconsider r2 as FinSequence of NAT by A46,FINSEQ_1:23;
A47:          r2 is_a_prefix_of s by A46,TREES_1:def 1;
              reconsider r2 as Element of T1 by A47,TREES_1:45;
A48:          r = p^r2 by A42;
A49:          r in { w^v where w is Element of T,
              v is Element of T1 : w in P } by A35,A48;
              thus r in X by A10,A49,XBOOLE_0:def 3;
            end;
A50:        now
              assume
A51:          len r <= len p;
A52:          ex r2 being FinSequence st r^r2 = p by A39,A51,FINSEQ_1:64;
A53:          p|(dom r) = r by A52,FINSEQ_1:33;
A54:          p|Seg(len r) = r by A53,FINSEQ_1:def 3;
              reconsider r3 = r as FinSequence of NAT by A54,FINSEQ_1:23;
A55:          r3 is_a_prefix_of p by A54,TREES_1:def 1;
              reconsider r3 as Element of T by A55,TREES_1:45;
A56:          for p9 st p9 in P holds not p9 is_a_proper_prefix_of r3
              proof
                let p9;
                assume
A57:            p9 in P;
                assume
A58:            p9 is_a_proper_prefix_of r3;
A59:            p9 is_a_prefix_of r3 by A58,XBOOLE_0:def 8;
A60:            p9 is_a_prefix_of p by A55,A59,XBOOLE_1:1;
A61:            p,p9 are_c=-comparable by A60,XBOOLE_0:def 9;
                per cases;
                suppose
A62:              p<>p9;
                  thus contradiction by A35,A57,A61,A62,TREES_1:def 13;
                end;
                suppose
A63:              p=p9;
                  thus contradiction by A55,A58,A63,XBOOLE_0:def 8;
                end;
              end;
A64:          r3 in { t : for p9 st p9 in P holds
              not p9 is_a_proper_prefix_of t } by A56;
              thus r in X by A64,XBOOLE_0:def 3;
            end;
            thus thesis by A40,A50;
          end;
        end;
        thus thesis by A20,A21,A32,XBOOLE_0:def 3;
      end;
      let q,k,n such that
A65:  q^<*k*> in X and
A66:  n <= k;
A67:  now
        assume
A68:    q^<*k*> in
        { t : for p st p in P holds not p is_a_proper_prefix_of t };
A69:    ex s being Element of T st
        q^<*k*> = s & for p st p in P holds not p is_a_proper_prefix_of s
        by A68;
        reconsider u = q^<*n*> as Element of T by A66,A69,TREES_1:def 5;
A70:    now
          let p such that
A71:      p in P;
          assume
A72:      p is_a_proper_prefix_of u;
A73:      p is_a_prefix_of q by A72,TREES_1:32;
A74:      ex s being Element of T st q^<*k*> = s & for p st p in
          P holds not p is_a_proper_prefix_of s by A68;
          thus contradiction by A71,A73,A74,TREES_1:31;
        end;
A75:    q^<*n*> in {t : for p st p in P holds not p
        is_a_proper_prefix_of t} by A70;
        thus q^<*n*> in X by A75,XBOOLE_0:def 3;
      end;
A76:  now
        assume
A77:    q^<*k*> in Z;
        consider p being Element of T, s being Element of T1 such that
A78:    q^<*k*> = p^s and
A79:    p in P by A10,A77;
A80:    now
          assume
A81:      len q <= len p;
          consider r being FinSequence such that
A82:      q^r = p by A78,A81,FINSEQ_1:64;
A83:      q^<*k*> = q^(r^s) by A78,A82,FINSEQ_1:45;
A84:      <*k*> = r^s by A83,FINSEQ_1:46;
A85:      now
            assume
A86:        r = <*k*>;
            reconsider s9 = q^<*n*> as Element of T by A66,A82,A86,
TREES_1:def 5;
A87:        now
              let p9 such that
A88:          p9 in P;
              assume
A89:          p9 is_a_proper_prefix_of s9;
A90:          p9 is_a_prefix_of s9 by A89,XBOOLE_0:def 8;
A91:          len p = len q + len <*k*> by A82,A86,FINSEQ_1:35
                .= len q + 1 by FINSEQ_1:57
                .= len q + len <*n*> by FINSEQ_1:57
                .= len s9 by FINSEQ_1:35;
              per cases;
              suppose
A92:            p9 = p;
                thus contradiction by A89,A90,A91,A92,TREES_1:15;
              end;
              suppose
A93:            p9 <> p;
                A94:            q
 is_a_prefix_of p & p9 is_a_prefix_of q by A82,A89,TREES_1:8,32;
A95:            p9 is_a_prefix_of p by A94,XBOOLE_1:1;
A96:            p,p9 are_c=-comparable by A95,XBOOLE_0:def 9;
                thus contradiction by A79,A88,A93,A96,TREES_1:def 13;
              end;
            end;
            thus q^<*n*> in { t : for p st p in P holds
            not p is_a_proper_prefix_of t } by A87;
          end;
A97:      now
            assume that
A98:        s = <*k*> and
A99:        r = {};
A100:       s = <*>NAT^s by FINSEQ_1:47;
A101:       <*>NAT^<*n*> in T1 by A66,A98,A100,TREES_1:def 5;
            reconsider t = <*n*> as Element of T1 by A101,FINSEQ_1:47;
A102:       q^<*n*> = p^t by A82,A99,FINSEQ_1:47;
            thus q^<*n*> in Z by A10,A79,A102;
          end;
          thus q^<*n*> in X by A84,A85,A97,FINSEQ_1:109,XBOOLE_0:def 3;
        end;
A103:   now
          assume
A104:     len p <= len q;
          consider r being FinSequence such that
A105:     p^r = q by A78,A104,FINSEQ_1:64;
A106:     p^(r^<*k*>) = p^s by A78,A105,FINSEQ_1:45;
A107:     r^<*k*> = s by A106,FINSEQ_1:46;
A108:     s|dom r = r by A107,FINSEQ_1:33;
A109:     s|Seg len r = r by A108,FINSEQ_1:def 3;
          reconsider r as FinSequence of NAT by A109,FINSEQ_1:23;
          reconsider t = r^<*n*> as Element of T1 by A66,A107,TREES_1:def 5;
A110:     q^<*n*> = p^t by A105,FINSEQ_1:45;
A111:     q^<*n*> in { p9^v where p9 is Element of T,
          v is Element of T1 : p9 in P } by A79,A110;
          thus q^<*n*> in X by A10,A111,XBOOLE_0:def 3;
        end;
        thus q^<*n*> in X by A80,A103;
      end;
      thus q^<*n*> in X by A65,A67,A76,XBOOLE_0:def 3;
    end;
    reconsider X as Tree by A14;
    take X;
    let q;
    thus q in X implies
    (q in T & for p st p in P holds not p is_a_proper_prefix_of q)
    or ex p,r st p in P & r in T1 & q = p^r
    proof
      assume
A112: q in X;
A113: now
        assume
A114:   q in Y;
A115:   ex s being Element of T st q = s &
        for p st p in P holds not p is_a_proper_prefix_of s by A114;
        thus thesis by A115;
      end;
A116: now
        assume
A117:   q in Z;
A118:   ex p being Element of T st ex s being Element of T1 st
        q = p^s & p in P by A10,A117;
        thus ex p,r st p in P & r in T1 & q = p^r by A118;
      end;
      thus thesis by A112,A113,A116,XBOOLE_0:def 3;
    end;
    assume
A119: (q in T & for p st p in P holds not p is_a_proper_prefix_of q)
    or ex p,r st p in P & r in T1 & q = p^r;
A120: (q in T & for p st p in P holds not p is_a_proper_prefix_of q) implies
    q in {t : for p st p in P holds not p is_a_proper_prefix_of t};
A121: now
      given p,r such that
A122: p in P & r in T1 & q = p^r;
A123: P c= T by TREES_1:def 14;
      thus q in Z by A10,A122,A123;
    end;
    thus thesis by A119,A120,A121,XBOOLE_0:def 3;
  end;
  uniqueness
  proof
    let S1,S2 be Tree such that
A124: q in S1 iff (q in T & for p st p in
    P holds not p is_a_proper_prefix_of q)
    or ex p,r st p in P & r in T1 & q = p^r and
A125: q in S2 iff (q in T & for p st p in
    P holds not p is_a_proper_prefix_of q)
    or ex p,r st p in P & r in T1 & q = p^r;
    let x be FinSequence of NAT;
    thus x in S1 implies x in S2
    proof
      assume
A126: x in S1;
      reconsider q = x as FinSequence of NAT;
A127: (q in T & for p st p in P holds not p is_a_proper_prefix_of q)
      or ex p,r st p in P & r in T1 & q = p^r by A124,A126;
      thus thesis by A125,A127;
    end;
    assume
A128: x in S2;
    reconsider q = x as FinSequence of NAT;
A129: (q in T & for p st p in P holds not p is_a_proper_prefix_of q)
    or ex p,r st p in P & r in T1 & q = p^r by A125,A128;
    thus thesis by A124,A129;
  end;
end;

theorem Th2:
  P <> {} implies tree(T,P,T1) = {t1 where t1 is Element of T :
  for p st p in P holds not p is_a_proper_prefix_of t1}
  \/ { p^s where p is Element of T, s is Element of T1 : p in P }
proof
  assume
A1: P <> {};
  thus tree(T,P,T1) c=
  {t : for p st p in P holds not p is_a_proper_prefix_of t} \/
  { p^s where p is Element of T, s is Element of T1 : p in P }
  proof
    let x be set;
    assume
A2: x in tree(T,P,T1);
    reconsider q = x as FinSequence of NAT by A2,TREES_1:44;
A3: now
      given p,r such that
A4:   p in P & r in T1 & q = p^r;
A5:   P c= T by TREES_1:def 14;
      thus x in { p9^s where p9 is Element of T,
      s is Element of T1 : p9 in P } by A4,A5;
    end;
    A6: q in T & (for p st p in P holds not p is_a_proper_prefix_of q) implies
    x in { t : for p st p in P holds not p is_a_proper_prefix_of t };
    thus thesis by A1,A2,A3,A6,Def1,XBOOLE_0:def 3;
  end;
  let x be set such that
A7: x in { t : for p st p in P holds not p is_a_proper_prefix_of t } \/
  { p^s where p is Element of T, s is Element of T1 : p in P };
A8: now
    assume
    A9: x in { p^s where p is Element of T, s is Element of T1 : p in P };
A10: ex p being Element of T st
    ex s being Element of T1 st x = p^s & p in P by A9;
    thus thesis by A10,Def1;
  end;
A11: now
    assume
    A12: x in { t : for p st p in P holds not p is_a_proper_prefix_of t };
A13: ex t st x = t & for p st p in P holds not p is_a_proper_prefix_of t
    by A12;
    thus thesis by A1,A13,Def1;
  end;
  thus thesis by A7,A8,A11,XBOOLE_0:def 3;
end;

theorem Th3:
  {t1 where t1 is Element of T :
  for p st p in P holds not p is_a_prefix_of t1} c=
  {t1 where t1 is Element of T :
  for p st p in P holds not p is_a_proper_prefix_of t1}
proof
  let x be set;
  assume
A1: x in {t1 where t1 is Element of T :
  for p st p in P holds not p is_a_prefix_of t1};
  consider t9 being Element of T such that
A2: x = t9 and
A3: for p st p in P holds not p is_a_prefix_of t9 by A1;
A4: now
    let p;
    assume
A5: p in P;
A6: not p is_a_prefix_of t9 by A3,A5;
    thus not p is_a_proper_prefix_of t9 by A6,XBOOLE_0:def 8;
  end;
  thus thesis by A2,A4;
end;

theorem Th4:
  P c= {t1 where t1 is Element of T :
  for p st p in P holds not p is_a_proper_prefix_of t1}
proof
  let x be set;
  assume
A1: x in P;
A2: ex t1 being Element of T st x = t1 &
  for p st p in P holds not p is_a_proper_prefix_of t1
  proof
A3: P c= T by TREES_1:def 14;
    consider t9 being Element of T such that
A4: t9 = x by A1,A3;
A5: now
      let p such that
A6:   p in P;
      per cases;
      suppose
A7:     t9 = p;
        thus not p is_a_proper_prefix_of t9 by A7;
      end;
      suppose
A8:     t9 <> p;
A9:     not t9, p are_c=-comparable by A1,A4,A6,A8,TREES_1:def 13;
        thus not p is_a_proper_prefix_of t9 by A9,XBOOLE_1:104;
      end;
    end;
    thus thesis by A4,A5;
  end;
  thus thesis by A2;
end;

theorem Th5:
  {t1 where t1 is Element of T :
  for p st p in P holds not p is_a_proper_prefix_of t1} \
  {t1 where t1 is Element of T :
  for p st p in P holds not p is_a_prefix_of t1} = P
proof
A1: now
    let x be set;
    assume
A2: x in {t1 where t1 is Element of T :
    for p st p in P holds not p is_a_proper_prefix_of t1} \
    {t1 where t1 is Element of T :
    for p st p in P holds not p is_a_prefix_of t1};
A3: x in {t1 where t1 is Element of T : for p st p in P holds not p
    is_a_proper_prefix_of t1} by A2;
A4: not x in {t1 where t1 is Element of T : for p st p in P holds not p
    is_a_prefix_of t1} by A2,XBOOLE_0:def 5;
    assume
A5: not x in P;
A6: ex t1 being Element of T st x = t1 &
    for p st p in P holds not p is_a_prefix_of t1
    proof
      consider t9 being Element of T such that
A7:   x = t9 and
A8:   for p st p in P holds not p is_a_proper_prefix_of t9 by A3;
A9:   now
        let p;
        assume
A10:    p in P;
A11:    not p is_a_proper_prefix_of t9 by A8,A10;
        per cases by A11,XBOOLE_0:def 8;
        suppose
A12:      not p is_a_prefix_of t9;
          thus not p is_a_prefix_of t9 by A12;
        end;
        suppose
A13:      p = t9;
          thus not p is_a_prefix_of t9 by A5,A7,A10,A13;
        end;
      end;
      thus thesis by A7,A9;
    end;
    thus contradiction by A4,A6;
  end;
  thus {t1 where t1 is Element of T :
  for p st p in P holds not p is_a_proper_prefix_of t1} \
  {t1 where t1 is Element of T :
  for p st p in P holds not p is_a_prefix_of t1} c= P by A1,TARSKI:def 3;
  let x be set;
  assume
A14: x in P;
A15: P c= {t1 where t1 is Element of T :
  for p st p in P holds not p is_a_proper_prefix_of t1} by Th4;
A16: not x in {t1 where t1 is Element of T :
  for p st p in P holds not p is_a_prefix_of t1}
  proof
    assume
A17: x in {t1 where t1 is Element of T :
    for p st p in P holds not p is_a_prefix_of t1};
A18: ex t9 being Element of T st x = t9 & for p st p in P
    holds not p is_a_prefix_of t9 by A17;
    thus contradiction by A14,A18;
  end;
  thus thesis by A14,A15,A16,XBOOLE_0:def 5;
end;

theorem Th6:
  for T, T1, P holds
  P c= { p^s where p is Element of T, s is Element of T1 : p in P }
proof
  let T, T1, P;
A1: now
    let x be set;
    assume
A2: x in P;
A3: P c= T by TREES_1:def 14;
    consider q being Element of T such that
A4: q = x by A2,A3;
A5: <*> NAT in T1 by TREES_1:47;
    consider s9 being Element of T1 such that
A6: s9 = <*> NAT by A5;
A7: q = q^s9 by A6,FINSEQ_1:47;
    thus
    x in { p^s where p is Element of T, s is Element of T1 : p in P }
    by A2,A4,A7;
  end;
  thus thesis
  by A1,TARSKI:def 3;
end;

theorem Th7:
  P <> {} implies tree(T,P,T1) = {t1 where t1 is Element of T :
  for p st p in P holds not p is_a_prefix_of t1}
  \/ { p^s where p is Element of T, s is Element of T1 : p in P }
proof
  assume
A1: P <> {};
A2: tree(T,P,T1) = {t1 where t1 is Element of T :
  for p st p in P holds not p is_a_proper_prefix_of t1}
  \/ { p^s where p is Element of T, s is Element of T1 : p in P } by A1,Th2;
  thus tree(T,P,T1) c= {t1 where t1 is Element of T :
  for p st p in P holds not p is_a_prefix_of t1}
  \/ { p^s where p is Element of T, s is Element of T1 : p in P }
  proof
    let x be set;
    assume
A3: x in tree(T,P,T1);
A4: x in {t1 where t1 is Element of T :
    for p st p in P holds not p is_a_proper_prefix_of t1}
    \/ { p^s where p is Element of T, s is Element of T1 : p in P } by A1,A3
,Th2;
A5: now per cases;
      suppose
A6:     x in P;
A7:     P c= { p^s where p is Element of T, s is Element of T1 : p in P }
        by Th6;
        thus x in {t1 where t1 is Element of T :
        for p st p in P holds not p is_a_prefix_of t1}
        or x in { p^s where p is Element of T, s is Element of T1 : p in P }
        by A6,A7;
      end;
      suppose
A8:     not x in P;
A9:     now per cases by A4,XBOOLE_0:def 3;
          suppose
A10:        x in {t1 where t1 is Element of T :
            for p st p in P holds not p is_a_proper_prefix_of t1};
A11:        x in {t1 where t1 is Element of T :
            for p st p in P holds not p is_a_prefix_of t1}
            proof
              assume
A12:          not x in {t1 where t1 is Element of T :
              for p st p in P holds not p is_a_prefix_of t1};
A13:          {t1 where t1 is Element of T :
              for p st p in P holds not p is_a_proper_prefix_of t1} \
              {t1 where t1 is Element of T :
              for p st p in P holds not p is_a_prefix_of t1} = P by Th5;
              thus contradiction by A8,A10,A12,A13,XBOOLE_0:def 5;
            end;
            thus x in {t1 where t1 is Element of T :
            for p st p in P holds not p is_a_prefix_of t1} or
            x in { p^s where p is Element of T, s is Element of T1 : p in P }
            by A11;
          end;
          suppose
            A14:        x
 in { p^s where p is Element of T, s is Element of T1 : p in P };
            thus x in {t1 where t1 is Element of T :
            for p st p in P holds not p is_a_prefix_of t1} or
            x in { p^s where p is Element of T, s is Element of T1 : p in P }
            by A14;
          end;
        end;
        thus x in {t1 where t1 is Element of T :
        for p st p in P holds not p is_a_prefix_of t1}
        or x in { p^s where p is Element of T, s is Element of T1 : p in P }
        by A9;
      end;
    end;
    thus thesis
    by A5,XBOOLE_0:def 3;
  end;
  thus thesis by A2,Th3,XBOOLE_1:9;
end;

canceled;

theorem
  p in P implies T1 = tree(T,P,T1)|p
proof
  assume
A1: p in P;
A2: ex q,r st q in P & r in T1 & p = q^r
  proof
    consider q such that
A3: q = p;
    consider r such that
A4: r = <*> NAT;
A5: r in T1 by A4,TREES_1:47;
A6: p = q^r by A3,A4,FINSEQ_1:47;
    thus thesis by A1,A3,A5,A6;
  end;
A7: p in tree(T,P,T1) by A2,Def1;
  let x be FinSequence of NAT;
  thus x in T1 implies x in tree(T,P,T1)|p
  proof
    assume
A8: x in T1;
A9: p^x in tree(T,P,T1) by A1,A8,Def1;
    thus thesis by A7,A9,TREES_1:def 9;
  end;
  thus x in tree(T,P,T1)|p implies x in T1
  proof
    assume
A10: x in tree(T,P,T1)|p;
A11: p^x in tree(T,P,T1) by A7,A10,TREES_1:def 9;
A12: now
      assume that p^x in T and
A13:  for r st r in P holds not r is_a_proper_prefix_of p^x;
A14:  not p is_a_proper_prefix_of p^x by A1,A13;
A15:  p is_a_prefix_of p^x by TREES_1:8;
A16:  p^x = p by A14,A15,XBOOLE_0:def 8
        .= p^<*>NAT by FINSEQ_1:47;
A17:  x = {} by A16,FINSEQ_1:46;
      thus thesis by A17,TREES_1:47;
    end;
A18: now
      given s,r such that
A19:  s in P and
A20:  r in T1 and
A21:  p^x = s^r;
A22:  now
        assume
A23:    s <> p;
A24:    not s,p are_c=-comparable by A1,A19,A23,TREES_1:def 13;
        thus contradiction by A21,A24,Th1;
      end;
      thus thesis by A20,A21,A22,FINSEQ_1:46;
    end;
    thus thesis by A1,A11,A12,A18,Def1;
  end;
end;

registration
  let T;
  cluster non empty AntiChain_of_Prefixes of T;
  existence
  proof
    consider w being Element of T;
    consider X being set such that
A1: X = {w};
A2: X is AntiChain_of_Prefixes-like by A1,TREES_1:70;
A3: X c= T
    proof
      let x be set;
      assume
A4:   x in X;
A5:   x = w by A1,A4,TARSKI:def 1;
      thus thesis by A5;
    end;
    reconsider X as AntiChain_of_Prefixes of T by A2,A3,TREES_1:def 14;
    take X;
    thus thesis by A1;
  end;
end;

definition
  let T;
  let t be Element of T;
  redefine func {t} -> AntiChain_of_Prefixes of T;
  correctness by TREES_1:74;
end;

theorem Th10:
  tree(T,{t},T1) = T with-replacement (t,T1)
proof
  let p;
  thus p in tree(T,{t},T1) implies p in T with-replacement (t,T1)
  proof
    assume
A1: p in tree(T,{t},T1);
A2: now
      assume
A3:   p in T & for s st s in {t} holds not s is_a_proper_prefix_of p;
A4:   t in {t} by TARSKI:def 1;
      thus p in T & not t is_a_proper_prefix_of p by A3,A4;
    end;
A5: now
      given s such that
A6:   ex r st s in {t} & r in T1 & p = s^r;
A7:   s = t by A6,TARSKI:def 1;
      thus ex r st r in T1 & p = t^r by A6,A7;
    end;
    thus thesis by A1,A2,A5,Def1,TREES_1:def 12;
  end;
  assume
A8: p in T with-replacement (t,T1);
A9: p in T & not t is_a_proper_prefix_of p implies
  p in T & for s st s in {t} holds not s is_a_proper_prefix_of p
  by TARSKI:def 1;
A10: now
    assume
A11: ex r st r in T1 & p = t^r;
    thus ex s,r st s in {t} & r in T1 & p = s^r
    proof
      take t;
A12:  t in {t} by TARSKI:def 1;
      thus thesis by A11,A12;
    end;
  end;
  thus thesis by A8,A9,A10,Def1,TREES_1:def 12;
end;

reserve T,T1 for DecoratedTree,
  P for AntiChain_of_Prefixes of dom T,
  t for Element of dom T,
  p1, p2, r1, r2 for FinSequence of NAT;

definition
  let T,P,T1;
  assume
A1: P<>{};
  func tree(T,P,T1) -> DecoratedTree means
  :Def2:
  dom it = tree(dom T, P, dom T1) & for q st q in tree(dom T, P, dom T1) holds
  (for p st p in P holds not p is_a_prefix_of q & it.q = T.q)
  or ex p,r st p in P & r in dom T1 & q = p^r & it.q = T1.r;
  existence
  proof
    defpred X[FinSequence,set] means
    (for p st p in P holds not p is_a_prefix_of $1 & $2 = T.$1) or
    ex p,r st p in P & r in dom T1 & $1 = p^r & $2 = T1.r;
A2: for q st q in tree(dom T,P,dom T1) ex x st X[q,x]
    proof
      let q;
      assume
A3:   q in tree(dom T, P, dom T1);
A4:   q in {t where t is Element of dom T :
      for p st p in P holds not p is_a_prefix_of t}
      \/ { p^s where p is Element of dom T,
      s is Element of dom T1 : p in P } by A1,A3,Th7;
A5:   now
        assume
A6:     q in {t where t is Element of dom T :
        for p st p in P holds not p is_a_prefix_of t};
        consider t such that
A7:     q = t & for p st p in P holds not p is_a_prefix_of t by A6;
        take x = T.t;
A8:     for p st p in P holds not p is_a_prefix_of q & x = T.q by A7;
        thus thesis by A8;
      end;
A9:   now
        assume
A10:    q in { p^s where p is Element of dom T,
        s is Element of dom T1 : p in P };
        consider p being Element of dom T, s being Element of dom T1
        such that
A11:    q = p^s & p in P by A10;
        take x = T1.s;
A12:    (for p st p in P holds not p is_a_prefix_of q & x = T.q) or
        ex p,r st p in P & r in dom T1 & q = p^r & x = T1.r by A11;
        thus thesis by A12;
      end;
      thus thesis by A4,A5,A9,XBOOLE_0:def 3;
    end;
    thus ex T0 being DecoratedTree st dom T0 = tree(dom T, P, dom T1) &
    for p st p in tree(dom T, P, dom T1) holds X[p,T0.p]
    from TREES_2:sch 6(A2);
  end;
  uniqueness
  proof
    let D1,D2 be DecoratedTree such that
A13: dom D1 = tree(dom T,P,dom T1) and
A14: for q st q in tree(dom T,P,dom T1) holds
    (for p st p in P holds not p is_a_prefix_of q & D1.q = T.q) or
    ex p,r st p in P & r in dom T1 & q = p^r & D1.q = T1.r and
A15: dom D2 = tree(dom T,P,dom T1) and
A16: for q st q in tree(dom T,P,dom T1) holds
    (for p st p in P holds not p is_a_prefix_of q & D2.q = T.q) or
    ex p,r st p in P & r in dom T1 & q = p^r & D2.q = T1.r;
A17: now
      let q;
      assume that
A18:  q in dom D1 and
A19:  D1.q <> D2.q;
      thus contradiction
      proof
        per cases by A13,A14,A18;
        suppose
A20:      for p st p in P holds not p is_a_prefix_of q & D1.q = T.q;
A21:      now
            per cases by A13,A16,A18;
            suppose
A22:          for p st p in P holds not p is_a_prefix_of q & D2.q = T.q;
              consider x being set such that
A23:          x in P by A1,XBOOLE_0:def 1;
A24:          P c= dom T by TREES_1:def 14;
              reconsider x as Element of dom T by A23,A24;
A25:          ex p9 st p9 = x;
A26:          D1.q = T.q by A20,A23,A25;
              thus contradiction by A19,A22,A23,A25,A26;
            end;
            suppose
              A27:          ex
 p,r st p in P & r in dom T1 & q = p^r & D2.q = T1.r;
              consider p2,r2 such that
A28:          p2 in P
              and r2 in dom T1 and
A29:          q = p2^r2
              and D2.q = T1.r2 by A27;
A30:          not p2 is_a_prefix_of q by A20,A28;
              thus contradiction by A29,A30,TREES_1:8;
            end;
          end;
          thus contradiction by A21;
        end;
        suppose
A31:      ex p,r st p in P & r in dom T1 & q = p^r & D1.q = T1.r;
          consider p1,r1 such that
A32:      p1 in P
          and r1 in dom T1 and
A33:      q = p1^r1 and
A34:      D1.q = T1.r1 by A31;
A35:      now
            per cases by A13,A16,A18;
            suppose
              A36:          for
 p st p in P holds not p is_a_prefix_of q & D2.q = T.q;
A37:          not p1 is_a_prefix_of q by A32,A36;
              thus contradiction by A33,A37,TREES_1:8;
            end;
            suppose
              A38:          ex
 p,r st p in P & r in dom T1 & q = p^r & D2.q = T1.r;
              consider p2,r2 such that
A39:          p2 in P
              and r2 in dom T1 and
A40:          q = p2^r2 and
A41:          D2.q = T1.r2 by A38;
A42:          now
                assume
A43:            p1 <> p2;
A44:            p1,p2 are_c=-comparable by A33,A40,Th1;
                thus contradiction by A32,A39,A43,A44,TREES_1:def 13;
              end;
              thus contradiction by A19,A33,A34,A40,A41,A42,FINSEQ_1:46;
            end;
          end;
          thus contradiction by A35;
        end;
      end;
    end;
    thus thesis by A13,A15,A17,TREES_2:33;
  end;
end;

canceled 2;

theorem Th13:
  P<>{} implies for q st q in dom tree(T,P,T1) holds
  (for p st p in P holds not p is_a_prefix_of q & tree(T,P,T1).q = T.q)
  or ex p,r st p in P & r in dom T1 & q = p^r & tree(T,P,T1).q = T1.r
proof
  assume
A1: P<>{};
  let q;
  assume
A2: q in dom tree(T,P,T1);
A3: q in tree(dom T,P,dom T1) by A1,A2,Def2;
  thus thesis by A3,Def2;
end;

theorem Th14:
  p in dom T implies for q st q in dom (T with-replacement (p,T1)) holds
  not p is_a_prefix_of q & T with-replacement (p,T1).q = T.q
  or ex r st r in dom T1 & q = p^r & T with-replacement (p,T1).q = T1.r
proof
  assume
A1: p in dom T;
  let q;
  assume
A2: q in dom (T with-replacement (p,T1));
A3: q in dom T with-replacement (p,dom T1) by A1,A2,TREES_2:def 12;
  thus thesis by A1,A3,TREES_2:def 12;
end;

theorem Th15:
  P<>{} implies for q st q in dom tree(T,P,T1) &
  q in {t1 where t1 is Element of dom T :
  for p st p in P holds not p is_a_prefix_of t1} holds tree(T,P,T1).q = T.q
proof
  assume
A1: P<>{};
  let q;
  assume that
A2: q in dom tree(T,P,T1) and
A3: q in {t1 where t1 is Element of dom T : for p st p in P holds not p
  is_a_prefix_of t1};
A4: ex t9 being Element of dom T st t9 = q & for p st p in P
  holds not p is_a_prefix_of t9 by A3;
  per cases by A2,Th13;
  suppose
A5: for p st p in P holds not p is_a_prefix_of q & tree(T,P,T1).q = T.q;
    consider x being set such that
A6: x in P by A1,XBOOLE_0:def 1;
A7: P c= dom T by TREES_1:def 14;
    reconsider x as Element of dom T by A6,A7;
A8: ex p9 st p9 = x;
    thus thesis by A5,A6,A8;
  end;
  suppose
    A9: ex p,r st p in P & r in dom T1 & q = p^r & tree(T,P,T1).q = T1.r;
    consider p,r such that
A10: p in P
    and r in dom T1 and
A11: q = p^r
    and tree(T,P,T1).q = T1.r by A9;
A12: not p is_a_prefix_of q by A4,A10;
    thus thesis by A11,A12,TREES_1:8;
  end;
end;

theorem Th16:
  p in dom T implies for q st q in dom (T with-replacement (p,T1)) &
  q in {t1 where t1 is Element of dom T : not p is_a_prefix_of t1}
  holds T with-replacement (p,T1).q = T.q
proof
  assume
A1: p in dom T;
  let q;
  assume that
A2: q in dom (T with-replacement (p,T1)) and
A3: q in {t1 where t1 is Element of dom T : not p is_a_prefix_of t1};
  per cases by A1,A2,Th14;
  suppose
A4: not p is_a_prefix_of q & T with-replacement (p,T1).q = T.q;
    thus thesis by A4;
  end;
  suppose
    A5: ex r st r in dom T1 & q = p^r & T with-replacement (p,T1).q = T1.r;
A6: ex t9 being Element of dom T st q = t9 & not p
    is_a_prefix_of t9 by A3;
    thus thesis by A5,A6,TREES_1:8;
  end;
end;

theorem Th17:
  for q st q in dom tree(T,P,T1) &
  q in {p^s where p is Element of dom T, s is Element of dom T1 : p in P}
  holds ex p9 being Element of dom T, r being Element of dom T1 st
  q = p9^r & p9 in P & tree(T,P,T1).q = T1.r
proof
  let q;
  assume that
A1: q in dom tree(T,P,T1) and
A2: q in {p^s where p is Element of dom T, s is Element of dom T1 : p in
  P};
  per cases by A1,Th13;
  suppose
A3: for p st p in P holds not p is_a_prefix_of q & tree(T,P,T1).q = T.q;
    consider p9 being Element of dom T, r being Element of dom T1 such that
A4: q= p9^r and
A5: p9 in P by A2;
A6: now
      assume tree(T,P,T1).q <> T1.r;
A7:   not p9 is_a_prefix_of q by A3,A5;
      thus contradiction by A4,A7,TREES_1:8;
    end;
    thus thesis by A4,A5,A6;
  end;
  suppose
    A8: ex p,r st p in P & r in dom T1 & q = p^r & tree(T,P,T1).q = T1.r;
    consider p,r such that
A9: p in P and
A10: r in dom T1 and
A11: q = p^r and
A12: tree(T,P,T1).q = T1.r by A8;
    consider p9 being Element of dom T, r9 being Element of dom T1 such that
A13: q = p9^r9 and
A14: p9 in P by A2;
A15: now
      assume
A16:  p <> p9;
A17:  p,p9 are_c=-comparable by A11,A13,Th1;
      thus contradiction by A9,A14,A16,A17,TREES_1:def 13;
    end;
    thus thesis by A9,A10,A11,A12,A15;
  end;
end;

theorem Th18:
  p in dom T implies for q st q in dom (T with-replacement (p,T1)) &
  q in {p^s where s is Element of dom T1 : s = s}
  holds ex r being Element of dom T1 st
  q = p^r & T with-replacement (p,T1).q = T1.r
proof
  assume
A1: p in dom T;
  let q;
  assume that
A2: q in dom (T with-replacement (p,T1)) and
A3: q in {p^s where s is Element of dom T1 : s = s};
  per cases by A1,A2,Th14;
  suppose
A4: not p is_a_prefix_of q & T with-replacement (p,T1).q = T.q;
A5: ex r being Element of dom T1 st q = p^r & r = r by A3;
    thus thesis by A4,A5,TREES_1:8;
  end;
  suppose
    A6: ex r st r in dom T1 & q = p^r & T with-replacement (p,T1).q = T1.r;
    thus thesis by A6;
  end;
end;

theorem
  tree(T,{t},T1) = T with-replacement (t,T1)
proof
A1: dom tree(T,{t},T1) = tree(dom T,{t},dom T1) by Def2
    .= dom T with-replacement (t,dom T1) by Th10
    .= dom (T with-replacement (t,T1)) by TREES_2:def 12;
A2: for q st q in dom tree(T,{t},T1) holds tree(T,{t},T1).q =
  T with-replacement (t,T1).q
  proof
    let q;
    assume
A3: q in dom tree(T,{t},T1);
A4: q in tree(dom T,{t},dom T1) by A3,Def2;
A5: tree(dom T,{t},dom T1) = {t1 where t1 is Element of dom T :
    for p st p in {t} holds not p is_a_prefix_of t1} \/
    { p^s where p is Element of dom T, s is Element of dom T1 : p in {t} }
    by Th7;
    per cases by A4,A5,XBOOLE_0:def 3;
    suppose
A6:   q in {t1 where t1 is Element of dom T :
      for p st p in {t} holds not p is_a_prefix_of t1};
      consider t9 being Element of dom T such that
A7:   q = t9 and
A8:   for p st p in {t} holds not p is_a_prefix_of t9 by A6;
      consider p such that
A9:   p = t;
A10:  p in {t} by A9,TARSKI:def 1;
A11:  not p is_a_prefix_of t9 by A8,A10;
A12:  q in dom (T with-replacement (t,T1)) &
      q in {t1 where t1 is Element of dom T : not p is_a_prefix_of t1}
      implies T with-replacement (t,T1).q = T.q by A9,Th16;
      thus thesis by A1,A3,A6,A7,A11,A12,Th15;
    end;
    suppose
A13:  q in { p9^s where p9 is Element of dom T,
      s is Element of dom T1 : p9 in {t} };
      consider p being Element of dom T, r being Element of dom T1
      such that
A14:  q = p^r and
A15:  p in {t} by A13;
A16:  q in { p^s where s is Element of dom T1 : s = s } by A14;
      consider p1 being Element of dom T, r1 being Element of dom T1 such that
A17:  q = p1^r1 and
A18:  p1 in {t} and
A19:  tree(T,{t},T1).q = T1.r1 by A3,A13,Th17;
A20:  p1 = t by A18,TARSKI:def 1;
A21:  p = t by A15,TARSKI:def 1;
A22:  ex r2 being Element of dom T1 st q = p^r2 & (T
      with-replacement (p,T1)).q = T1.r2 by A1,A3,A16,A21,Th18;
      thus thesis by A17,A19,A20,A21,A22,FINSEQ_1:46;
    end;
  end;
  thus thesis by A1,A2,TREES_2:33;
end;

reserve D for non empty set,
  T,T1 for DecoratedTree of D,
  P for AntiChain_of_Prefixes of dom T;

definition
  let D,T,P,T1;
  assume
A1: P<>{};
  func tree(T,P,T1) -> DecoratedTree of D equals
  tree(T,P,T1);
  coherence
  proof
    set T2 = tree(T,P,T1);
A2: T2 is D-valued
    proof
      let y be set;
      assume
A3:   y in rng T2;
      consider x being set such that
A4:   x in dom T2 and
A5:   y = T2.x by A3,FUNCT_1:def 5;
A6:   x is Element of dom T2 by A4;
      reconsider q = x as FinSequence of NAT by A6;
A7:   dom T2 = tree(dom T,P,dom T1) by A1,Def2;
A8:   dom T2 = {t1 where t1 is Element of dom T :
      for p st p in P holds not p is_a_prefix_of t1}
      \/ { p^s where p is Element of dom T,
      s is Element of dom T1 : p in P } by A1,A7,Th7;
      per cases by A4,A8,XBOOLE_0:def 3;
      suppose
A9:     q in {t1 where t1 is Element of dom T :
        for p st p in P holds not p is_a_prefix_of t1};
A10:    tree(T,P,T1).q = T.q by A1,A4,A9,Th15;
A11:    now
A12:      ex t9 being Element of dom T st q = t9 & for p st p in P
          holds not p is_a_prefix_of t9 by A9;
          thus q in dom T by A12;
        end;
A13:    y in rng T by A5,A10,A11,FUNCT_1:def 5;
A14:    rng T c= D by RELAT_1:def 19;
        thus thesis by A13,A14;
      end;
      suppose
A15:    q in { p^s where p is Element of dom T,
        s is Element of dom T1 : p in P };
A16:    ex p being Element of dom T,r being Element of dom T1 st
        q = p^r & p in P & tree(T,P,T1).q = T1.r by A4,A15,Th17;
        thus thesis by A5,A16;
      end;
    end;
    thus thesis by A2;
  end;
end;

