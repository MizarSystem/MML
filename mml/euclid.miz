:: The Euclidean Space
::  by Agata Darmochwa{\l}
::
:: Received November 21, 1991
:: Copyright (c) 1991 Association of Mizar Users

environ

 vocabularies ARYTM, FINSEQ_2, FINSEQ_1, FUNCT_1, ABSVALUE, ARYTM_1, RVSUM_1,
      COMPLEX1, SQUARE_1, RLVECT_1, RELAT_1, PCOMPS_1, METRIC_1, PRE_TOPC,
      FUNCT_2, ARYTM_3, MCART_1, EUCLID, BOOLE, MONOID_0, ZF_LANG,
      XREAL_0, SUBSET_1, VALUED_0;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0,
      XREAL_0, COMPLEX1, NAT_1, RELAT_1, FUNCT_1,
      VALUED_0, STRUCT_0, METRIC_1, FUNCT_2,
      REAL_1, VALUED_1, SEQ_1, FINSEQ_1, FINSEQ_2, FINSEQOP, FRAENKEL,
      SQUARE_1, RVSUM_1, MONOID_0, PRE_TOPC, PCOMPS_1, TOPMETR, XXREAL_0;
 constructors FRAENKEL, REAL_1, SQUARE_1, BINOP_2, COMPLEX1, FINSEQOP,
      PCOMPS_1, MONOID_0, SEQ_1, TOPMETR;
 registrations XBOOLE_0, FUNCT_1, RELSET_1, NUMBERS, XXREAL_0, XREAL_0,
      BINOP_2, MEMBERED, FINSEQ_2, RVSUM_1, METRIC_1, PCOMPS_1, MONOID_0,
      VALUED_0, VALUED_1, STRUCT_0, TOPMETR;
 requirements NUMERALS, REAL, SUBSET, BOOLE, ARITHM;
 definitions FINSEQ_1, STRUCT_0, PCOMPS_1, SQUARE_1, FINSEQ_2, RVSUM_1,
      MONOID_0, VALUED_1;
 theorems ABSVALUE, BINOP_1, FINSEQ_1, FINSEQ_2, FINSEQOP, FUNCT_1, FUNCT_2,
      PCOMPS_1, RVSUM_1, SQUARE_1, TARSKI, XREAL_0, XCMPLX_1, COMPLEX1,
      XREAL_1, XXREAL_0, FUNCOP_1, ORDINAL1, VALUED_1, RELAT_1, PRE_TOPC,
      TOPMETR;
 schemes FUNCT_2, BINOP_1;

begin

reserve k,j,n for Nat,
  r for real number;

definition
  let n be Nat;
  func REAL n -> FinSequence-DOMAIN of REAL equals

  n-tuples_on REAL;
  coherence;
end;

definition
  func absreal -> Function of REAL,REAL means
  :Def2:
  for r holds it.r = abs r;
  existence
  proof
    deffunc F(Real) = abs $1;
    consider f be Function of REAL,REAL such that
A1: for r be Real holds f.r = F(r) from FUNCT_2:sch 4;
    take f;
    let r;
    r is Real by XREAL_0:def 1;
    hence thesis by A1;
  end;
  uniqueness
  proof
    let f, g be Function of REAL,REAL such that
A2: for r holds f.r = abs r and
A3: for r holds g.r = abs r;
    now
      let x be Element of REAL;
      thus f.x = abs x by A2
        .= g.x by A3;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

definition
  let x be real-valued FinSequence;
  redefine func abs x -> FinSequence of REAL equals

  absreal*x;
  coherence
  proof
    thus rng abs x c= REAL;
  end;
  compatibility
  proof
    set g = absreal*x;
A1: dom abs x = dom x by VALUED_1:def 11;
    dom absreal = REAL by FUNCT_2:def 1;
    then rng x c= dom absreal;
    then
A2: dom g = dom x by RELAT_1:46;
    now
      let a be set;
      assume
A3:   a in dom abs x;
      thus (abs x).a = abs (x . a) by VALUED_1:18
        .= absreal.(x . a) by Def2
        .= g.a by A1,A2,A3,FUNCT_1:22;
    end;
    hence thesis by A1,A2,FUNCT_1:9;
  end;
end;

definition
  let n;
  func 0*n -> real-valued FinSequence equals

  n |-> (0 qua Real);
  coherence;
end;

definition
  let n;
  redefine func 0*n -> Element of REAL n;
  coherence;
end;

reserve x,x1,x2,y,z for Element of REAL n;

definition
  let n,x;
  redefine func -x -> Element of REAL n;
  coherence
  proof
    reconsider n as Element of NAT by ORDINAL1:def 13;
    reconsider R1=x as Element of n-tuples_on REAL;
    -R1 in n-tuples_on REAL;
    hence thesis;
  end;
end;

definition
  let n,x,y;
  redefine func x + y -> Element of REAL n;
  coherence
  proof
    reconsider n as Element of NAT by ORDINAL1:def 13;
    reconsider R1=x,R2=y as Element of n-tuples_on REAL;
    R1 + R2 in n-tuples_on REAL;
    hence thesis;
  end;
  redefine func x - y -> Element of REAL n;
  coherence
  proof
    reconsider n as Element of NAT by ORDINAL1:def 13;
    reconsider R1=x,R2=y as Element of n-tuples_on REAL;
    R1 - R2 in n-tuples_on REAL;
    hence thesis;
  end;
end;

definition
  let n, x;
  let r be real number;
  redefine func r*x -> Element of REAL n;
  coherence
  proof
    reconsider n as Element of NAT by ORDINAL1:def 13;
    reconsider R=x as Element of n-tuples_on REAL;
    r*R in n-tuples_on REAL;
    hence thesis;
  end;
end;

definition
  let n,x;
  redefine func abs x -> Element of n-tuples_on REAL;
  coherence by FINSEQ_2:133;
end;

definition
  let n,x;
  redefine func sqr x -> Element of n-tuples_on REAL;
  coherence by FINSEQ_2:133;
end;

reserve f for real-valued FinSequence;

definition
  let f;
  func |. f .| -> Real equals

  sqrt Sum sqr f;
  coherence;
end;

canceled;

theorem
  len x = n by FINSEQ_2:109;

theorem Th3:
  dom x = Seg n
proof
  len x = n by FINSEQ_2:109;
  hence thesis by FINSEQ_1:def 3;
end;

theorem
  f.k in REAL;

canceled;

theorem Th6:
  r = x .k implies (abs x).k = abs r
proof
  assume that
A1: r = x .k;
  per cases;
  suppose
A2: k in Seg n;
    len abs x = n by FINSEQ_2:109;
    then absreal*x = abs x & k in dom abs x by A2,FINSEQ_1:def 3;
    hence (abs x).k = absreal.r by A1,FUNCT_1:22
      .= abs r by Def2;
  end;
  suppose
A3: not k in Seg n;
    then
A4: not k in dom x by Th3;
A5: not k in dom abs x by A3,Th3;
A6: r = {} by A1,A4,FUNCT_1:def 4;
    (abs x).k = 0 by A5,FUNCT_1:def 4;
    hence thesis by A6,ABSVALUE:def 1;
  end;
end;

Lm1: f is FinSequence of REAL
proof
  thus rng f c= REAL;
end;

Lm2: f is Element of REAL len f
proof
  f is FinSequence of REAL by Lm1;
  then f is Element of REAL* by FINSEQ_1:def 11;
  then f in (len f)-tuples_on REAL;
  hence thesis;
end;

theorem
  abs 0*n = n |-> (0 qua Real)
proof
  reconsider m=n as Element of NAT by ORDINAL1:def 13;
  thus abs 0*n = m |-> absreal.(0 qua Real) by FINSEQOP:17
    .= n |-> abs 0 by Def2
    .= n |-> 0 by ABSVALUE:7;
end;

theorem Th8:
  abs -f = abs f
proof
  set n = len f;
  reconsider x=f as Element of REAL n by Lm2;
  now
    let j such that j in Seg n;
    reconsider r = f.j, r' = (-f).j as Element of REAL;
    thus (abs -x).j = abs r' by Th6
      .= abs (-r) by RVSUM_1:35
      .= abs r by COMPLEX1:138
      .= (abs x).j by Th6;
  end;
  hence thesis by FINSEQ_2:139;
end;

theorem Th9:
  abs(r*f) = abs(r)*(abs f)
proof
  set n = len f;
  reconsider n as Element of NAT;
  reconsider x=f as Element of REAL n by Lm2;
  now
    let j such that j in Seg n;
    reconsider r' = x .j, rr = (r*x).j as Element of REAL;
    reconsider ar = (abs x).j as Real;
    thus (abs(r*x)).j = abs(rr) by Th6
      .= abs(r*r') by RVSUM_1:66
      .= abs(r)*abs(r') by COMPLEX1:151
      .= abs(r)*ar by Th6
      .= (abs(r)*(abs x)).j by RVSUM_1:67;
  end;
  hence thesis by FINSEQ_2:139;
end;

theorem Th10:
  |.0*n.| = 0
proof
  thus |.0*n .| = sqrt Sum (n |-> 0^2) by RVSUM_1:82
    .= sqrt (n*0) by RVSUM_1:110
    .= 0 by SQUARE_1:82;
end;

Lm3: sqr abs f = sqr f
proof
  set n = len f;
  reconsider x=f as Element of REAL n by Lm2;
  now
    let k;
    assume k in Seg n;
    thus (sqr abs x).k = ((abs x).k)^2 by VALUED_1:11
      .= (abs (x .k))^2 by Th6
      .= (x .k)^2 by COMPLEX1:161
      .= (sqr x).k by VALUED_1:11;
  end;
  hence thesis by FINSEQ_2:139;
end;

theorem Th11:
  |. x .| = 0 implies x = 0*n
proof
  assume
A1: |. x .| = 0;
  now
    let j;
    assume
A2: j in Seg n;
    reconsider r = x .j as Element of REAL;
    Sum sqr x = 0 by A1,RVSUM_1:116,SQUARE_1:92;
    then Sum sqr abs x = 0 by Lm3;
    then (abs x).j = (n|->0).j by RVSUM_1:121;
    then abs r = (n|-> 0).j by Th6;
    then abs r = 0 by A2,FUNCOP_1:13;
    then r = 0 by ABSVALUE:7;
    hence x .j = (n|->(0 qua Real)).j by A2,FUNCOP_1:13;
  end;
  hence thesis by FINSEQ_2:139;
end;

theorem Th12:
  |.f.| >= 0
proof
  |.f.| = sqrt Sum sqr f & 0 <= Sum sqr f by RVSUM_1:116;
  hence thesis by SQUARE_1:def 4;
end;

registration
  let f;
  cluster |.f.| -> non negative;
  coherence by Th12;
end;

theorem Th13:
  |.-f.| = |.f.|
proof
  thus |.-f.| = sqrt Sum sqr abs -f by Lm3
    .= sqrt Sum sqr abs f by Th8
    .= |.f.| by Lm3;
end;

theorem Th14:
  |.r*f.| = abs(r)*|.f.|
proof
  set n = len f;
  reconsider x=f as Element of REAL n by Lm2;
A1: 0 <= (abs(r))^2 & 0 <= Sum sqr abs x by RVSUM_1:116,XREAL_1:65;
  thus |.r*f.| = sqrt Sum sqr abs(r*x) by Lm3
    .= sqrt Sum sqr (abs(r)*abs x) by Th9
    .= sqrt Sum ((abs(r))^2 * sqr abs x) by RVSUM_1:84
    .= sqrt ((abs(r))^2 * Sum sqr abs x) by RVSUM_1:117
    .= sqrt (abs(r))^2 * sqrt Sum sqr abs x by A1,SQUARE_1:97
    .= abs(r) * sqrt Sum sqr abs x by COMPLEX1:132,SQUARE_1:89
    .= abs(r)*|.f.| by Lm3;
end;

theorem Th15:
  |.x1 + x2.| <= |.x1.| + |.x2.|
proof
A1: 0 <= Sum sqr x1 & 0 <= Sum sqr x2 &
  0 <= Sum sqr abs x1 & 0 <= Sum sqr abs x2 by RVSUM_1:116;
  then
A2: 0 <= sqrt Sum sqr x1 & 0 <= sqrt Sum sqr x2 &
  0 <= sqrt Sum sqr abs x1 & 0 <= sqrt Sum sqr abs x2 by SQUARE_1:def 4;
A3: Sum sqr (abs x1 + abs x2)
  = Sum (sqr abs x1 + 2*mlt(abs x1,abs x2) + sqr abs x2) by RVSUM_1:98
    .= Sum(sqr abs x1 + 2*mlt(abs x1,abs x2)) + Sum sqr abs x2 by RVSUM_1:119
    .= Sum sqr abs x1 + Sum(2*mlt(abs x1,abs x2)) + Sum sqr abs x2
  by RVSUM_1:119
    .= Sum sqr abs x1 + (2*Sum mlt(abs x1,abs x2))+Sum sqr abs x2
  by RVSUM_1:117;
  k in Seg n implies (sqr abs (x1 + x2)).k <= (sqr (abs x1 + abs x2)).k
  proof
    assume that
A4: k in Seg n;
    set r2 = (sqr (abs x1 + abs x2)).k;
    len (x1+x2) = n by FINSEQ_2:109;
    then
A5: dom (x1+x2) = Seg n by FINSEQ_1:def 3;
    reconsider r11 = x1.k, r22 = x2.k as Element of REAL;
    reconsider r12 = (x1 + x2).k as Element of REAL;
    reconsider abs1 = (abs x1).k, abs2 = (abs x2).k as Real;
    reconsider abs12 = (abs x1 + abs x2).k as Real;
    reconsider abs'12 = (abs (x1 + x2)).k as Real;
    abs(r11 + r22) <= abs(r11) + abs(r22) by COMPLEX1:142;
    then abs(r12) <= abs(r11) + abs(r22) by A4,A5,VALUED_1:def 1;
    then abs(r12) <= abs(r11) + abs2 & len(abs x1 + abs x2) = n
    by Th6,FINSEQ_2:109;
    then abs(r12) <= abs1 + abs2 & dom(abs x1 + abs x2) = Seg n
    by Th6,FINSEQ_1:def 3;
    then abs(r12) >= 0 & abs(r12) <= abs12 by A4,COMPLEX1:132,VALUED_1:def 1;
    then abs'12 <= abs12 & 0 <= abs'12 by Th6;
    then (abs'12)^2 <= (abs12)^2 by SQUARE_1:77;
    then (abs'12)^2 <= r2 by VALUED_1:11;
    hence thesis by VALUED_1:11;
  end;
  then Sum sqr abs (x1 + x2) <=
  Sum sqr abs x1 + (2*Sum mlt(abs x1,abs x2))+Sum sqr abs x2
  by A3,RVSUM_1:112;
  then
A6: Sum sqr (x1 + x2) <=
  Sum sqr abs x1 + (2*Sum mlt(abs x1,abs x2))+Sum sqr abs x2 by Lm3;
A7: k in Seg n implies 0 <= (mlt(abs x1,abs x2)).k
  proof
    assume k in Seg n;
    set r = (mlt(abs x1,abs x2)).k;
    reconsider r1 = x1.k, r2 = x2.k as Element of REAL;
    len mlt(abs x1,abs x2) = n & (abs x1).k = abs(r1) & (abs x2).k = abs(r2)
    by Th6,FINSEQ_2:109;
    then r = abs(r1)*abs(r2) & 0 <= abs(r1) & 0 <= abs(r2)
    by COMPLEX1:132,RVSUM_1:87;
    then 0 * abs(r2) <= r;
    hence thesis;
  end;
  len mlt(abs x1,abs x2) = n by FINSEQ_2:109;
  then dom mlt(abs x1,abs x2) = Seg n & 0 <= (Sum mlt(abs x1,abs x2))^2 &
  (Sum mlt(abs x1,abs x2))^2 <= (Sum sqr abs x1)*(Sum sqr abs x2)
  by FINSEQ_1:def 3,RVSUM_1:122,XREAL_1:65;
  then 0 <= Sum mlt(abs x1,abs x2) &
  sqrt(Sum mlt(abs x1,abs x2))^2 <= sqrt((Sum sqr abs x1)*(Sum sqr abs x2))
  by A7,RVSUM_1:114,SQUARE_1:94;
  then 0 <= 2 & Sum mlt(abs x1,abs x2) <=
  sqrt((Sum sqr abs x1)*(Sum sqr abs x2)) by SQUARE_1:89;
  then Sum sqr abs x1 <= Sum sqr abs x1 &
  2*Sum mlt(abs x1,abs x2) <= 2*sqrt((Sum sqr abs x1)*(Sum sqr abs x2))
  by XREAL_1:66;
  then Sum sqr abs x2 <= Sum sqr abs x2 &
  Sum sqr abs x1+(2*Sum mlt(abs x1,abs x2)) <=
  Sum sqr abs x1+2*sqrt((Sum sqr abs x1)*(Sum sqr abs x2)) by XREAL_1:9;
  then Sum sqr abs x1+(2*Sum mlt(abs x1,abs x2)) + Sum sqr abs x2 <=
  Sum sqr abs x1+2*sqrt((Sum sqr abs x1)*(Sum sqr abs x2)) + Sum sqr abs x2
  by XREAL_1:9;
  then Sum sqr (x1 + x2) <=
  Sum sqr abs x1+2*sqrt((Sum sqr abs x1)*(Sum sqr abs x2)) + Sum
  sqr abs x2 by A6,XXREAL_0:2;
  then Sum sqr (x1 + x2) <=
  Sum sqr abs x1+2*((sqrt Sum sqr abs x1)*(sqrt Sum sqr abs x2)) +
  Sum sqr abs x2 by A1,SQUARE_1:97;
  then Sum sqr abs x1 = (sqrt Sum sqr abs x1)^2 &
  (sqrt Sum sqr abs x2)^2 = Sum sqr abs x2 & Sum sqr (x1 + x2) <=
  Sum sqr abs x1+2*(sqrt Sum sqr abs x1)*(sqrt Sum sqr abs x2) + Sum
  sqr abs x2 by A1,SQUARE_1:def 4;
  then 0 <= Sum sqr (x1 + x2) &
  Sum sqr (x1 + x2) <= ((sqrt Sum sqr abs x1) + (sqrt Sum
  sqr abs x2))^2 by RVSUM_1:116;
  then 0 + 0 <= (sqrt Sum sqr abs x1) + (sqrt Sum sqr abs x2) &
  sqrt Sum sqr (x1 + x2) <=
  sqrt(((sqrt Sum sqr abs x1) + (sqrt Sum sqr abs x2))^2)
  by A2,SQUARE_1:94;
  then sqrt Sum sqr (x1 + x2) <= (sqrt Sum sqr abs x1) +
  (sqrt Sum sqr abs x2) by SQUARE_1:89;
  then sqrt Sum sqr (x1 + x2) <= (sqrt Sum sqr abs x1) +
  (sqrt Sum sqr x2) by Lm3;
  hence thesis by Lm3;
end;

theorem Th16:
  |.x1 - x2.| <= |.x1.| + |.x2.|
proof
  |.x1 - x2.| <= |.x1.| + |.-x2.| by Th15;
  hence thesis by Th13;
end;

theorem
  |.x1.| - |.x2.| <= |.x1 + x2.|
proof
  reconsider R1=x1,R2=x2 as Element of n-tuples_on REAL;
  x1 = R1 + R2 - R2 by RVSUM_1:63;
  then |.x1.| <= |.x1 + x2.| + |.x2.| by Th16;
  hence thesis by XREAL_1:22;
end;

theorem
  |.x1.| - |.x2.| <= |.x1 - x2.|
proof
  reconsider R1=x1,R2=x2 as Element of n-tuples_on REAL;
  x1 = R1 - R2 + R2 by RVSUM_1:64;
  then |.x1.| <= |.x1 - x2.| + |.x2.| by Th15;
  hence thesis by XREAL_1:22;
end;

theorem Th19:
  |.x1 - x2.| = 0 iff x1 = x2
proof
  reconsider R1=x1,R2=x2 as Element of n-tuples_on REAL;
  thus |.x1 - x2.| = 0 implies x1 = x2
  proof
    assume |.x1 - x2.| = 0;
    then R1 - R2 = 0*n by Th11
      .= n |-> 0;
    hence thesis by RVSUM_1:59;
  end;
  assume x1 = x2;
  then R1 - R2 = 0*n by RVSUM_1:58;
  hence thesis by Th10;
end;

registration
  let n,x1;
  cluster |. x1 - x1 .| -> zero;
  coherence by Th19;
end;

theorem
  x1 <> x2 implies |.x1 - x2.| > 0
proof
  assume x1 <> x2;
  then |.x1 - x2.| >= 0 & 0 <> |.x1 - x2.| by Th19;
  hence thesis;
end;

theorem Th21:
  |.x1 - x2.| = |.x2 - x1.|
proof
  reconsider R1=x1,R2=x2 as Element of n-tuples_on REAL;
  thus |.x1 - x2.| = |.-(R2 - R1).| by RVSUM_1:56
    .= |.x2 - x1.| by Th13;
end;

theorem Th22:
  |.x1 - x2.| <= |.x1 - x .| + |.x - x2.|
proof
  reconsider R1=x1,R2=x2,R=x as Element of n-tuples_on REAL;
  |.x1 - x2.| = |.R1 - R + R - R2.| by RVSUM_1:64
    .= |.(x1 - x) + (x - x2).| by RVSUM_1:61;
  hence thesis by Th15;
end;

definition
  let n be Nat;
  func Pitag_dist n -> Function of [:REAL n,REAL n:],REAL means
  :Def6:
  for x,y being Element of REAL n holds it.(x,y) = |.x-y.|;
  existence
  proof
    deffunc F(Element of REAL n, Element of REAL n) = |.$1-$2.|;
    consider f being Function of [:REAL n, REAL n:], REAL such that
A1: for x,y being Element of REAL n holds f.(x,y) = F(x,y) from BINOP_1:sch 4;
    take f;
    let x,y be Element of REAL n;
    thus f.(x,y) = |.x-y.| by A1;
  end;
  uniqueness
  proof
    let f,g be Function of [:REAL n, REAL n:], REAL;
    assume that
A2: for x,y being Element of REAL n holds f.(x,y) = |.x-y.| and
A3: for x,y being Element of REAL n holds g.(x,y) = |.x-y.|;
    reconsider n as Element of NAT by ORDINAL1:def 13;
    reconsider f,g as Function of [:REAL n, REAL n:], REAL;
    now
      let x,y be Element of REAL n;
      thus f.(x,y) = |.x-y.| by A2
        .= g.(x,y) by A3;
    end;
    hence thesis by BINOP_1:2;
  end;
end;

theorem
  sqr(x-y) = sqr(y-x)
proof
  reconsider R1=x,R2=y as Element of n-tuples_on REAL;
  thus sqr(x-y) = sqr R1 - 2*mlt(R1,R2) + sqr R2 by RVSUM_1:99
    .= sqr R2 + (- 2*mlt(R1,R2) + sqr R1)
    .= sqr R2 - 2*mlt(R2,R1) + sqr R1 by FINSEQOP:29
    .= sqr(y-x) by RVSUM_1:99;
end;

theorem Th24:
  for n being Nat holds Pitag_dist n is_metric_of REAL n
proof
  let n be Nat;
  let x,y,z be Element of REAL n;
  thus (Pitag_dist n).(x,y) = 0 iff x=y
  proof
    (Pitag_dist n).(x,y) = |.x-y.| by Def6;
    hence thesis by Th19;
  end;
  thus (Pitag_dist n).(x,y) = |.x-y.| by Def6
    .= |.y-x .| by Th21
    .= (Pitag_dist n).(y,x) by Def6;
  thus (Pitag_dist n).(x,z) <= (Pitag_dist n).(x,y) + (Pitag_dist n).(y,z)
  proof
    (Pitag_dist n).(x,y) = |.x-y.| & (Pitag_dist n).(x,z) = |.x-z.| &
    (Pitag_dist n).(y,z) = |.y-z.| by Def6;
    hence thesis by Th22;
  end;
end;

definition
  let n be Nat;
  func Euclid n -> strict MetrSpace equals

  MetrStruct(#REAL n,Pitag_dist n#);
  coherence
  proof
    SpaceMetr(REAL n, Pitag_dist n) = MetrStruct(#REAL n,Pitag_dist n#)
    by Th24,PCOMPS_1:def 8;
    hence thesis;
  end;
end;

registration
  let n be Nat;
  cluster Euclid n -> non empty;
  coherence;
end;

definition
  let n be Nat;
  func TOP-REAL n -> strict TopSpace equals

  TopSpaceMetr (Euclid n);
  coherence;
end;

registration
  let n be Nat;
  cluster TOP-REAL n -> non empty;
  coherence;
end;

reserve p,p1,p2,p3 for Point of TOP-REAL n,
  x,x1,x2,y,y1,y2 for real number;

theorem
  the carrier of TOP-REAL n = REAL n;

theorem Th26:
  p is Function of Seg n, REAL
proof
  p is Element of Funcs(Seg n,REAL) by FINSEQ_2:111;
  hence thesis;
end;

theorem Th27:
  p is FinSequence of REAL
proof
  p is Function of Seg n, REAL by Th26;
  hence thesis by FINSEQ_2:28;
end;

registration
  let n;
  cluster TOP-REAL n -> constituted-FinSeqs;
  coherence
  proof
    let a be Element of TOP-REAL n;
    thus thesis by Th27;
  end;
end;

registration
  let n;
  cluster -> real-valued Point of TOP-REAL n;
  coherence by Th27;
end;

theorem Th28:
  len p = n
proof
A1: p is Function of Seg n, REAL by Th26;
  Seg len p = dom p by FINSEQ_1:def 3
    .= Seg n by A1,FUNCT_2:def 1;
  hence thesis by FINSEQ_1:8;
end;

notation
  let n;
  synonym 0.REAL n for 0*n;
end;

definition
  let n;
  canceled;
  redefine func 0.REAL n -> Point of TOP-REAL n;
  coherence;
end;

definition
  let n,p1,p2;
  canceled;
  redefine func p1 + p2 -> Point of TOP-REAL n;
  coherence
  proof
    reconsider p1' = p1, p2' = p2 as Element of REAL n;
    p1' + p2' is Point of TOP-REAL n;
    hence thesis;
  end;
  commutativity;
end;

theorem
  for x being Element of REAL n holds sqr abs x = sqr x by Lm3;

theorem
  p1 + p2 + p3 = p1 + (p2 + p3) by FINSEQOP:29;

theorem
  0.REAL n + p = p & p + 0.REAL n = p by RVSUM_1:33;

definition
  let n,p,x;
  canceled;
  redefine func x*p -> Point of TOP-REAL n;
  coherence
  proof
    reconsider p' = p as Element of REAL n;
    x*p' is Point of TOP-REAL n;
    hence thesis;
  end;
end;

theorem Th32:
  x*0.REAL n = 0.REAL n
proof
  |. x*(0*n).| = abs(x)*|.0*n.| by Th14
    .= abs(x)*0 by Th10
    .= 0;
  hence thesis by Th11;
end;

theorem
  1*p = p & 0 * p = 0.REAL n by RVSUM_1:74,75;

theorem
  (x*y)*p = x*(y*p) by RVSUM_1:71;

theorem
  x*p = 0.REAL n implies x = 0 or p = 0.REAL n
proof
  assume
A1: x*p = 0.REAL n & x <> 0;
  thus p = 1*p by RVSUM_1:74
    .= ((1/x)*x)*p by A1,XCMPLX_1:107
    .= (1/x)*(x*p) by RVSUM_1:71
    .= 0.REAL n by A1,Th32;
end;

theorem
  x*(p1 + p2) = x*p1 + x*p2 by RVSUM_1:73;

theorem
  (x + y)*p = x*p + y*p by RVSUM_1:72;

theorem
  x*p1 = x*p2 implies x = 0 or p1 = p2
proof
  assume that
A1: x*p1 = x*p2 and
A2: x <> 0;
  ((1/x)*x)*p1 = (1/x)*(x*p2) by A1,RVSUM_1:71;
  then ((1/x)*x)*p1 = ((1/x)*x)*p2 by RVSUM_1:71;
  then 1*p1 = ((1/x)*x)*p2 by A2,XCMPLX_1:107;
  then 1*p1 = 1*p2 by A2,XCMPLX_1:107;
  hence p1 = 1*p2 by RVSUM_1:74
    .= p2 by RVSUM_1:74;
end;

definition
  let n,p;
  canceled;
  redefine func -p -> Point of TOP-REAL n;
  coherence
  proof
    reconsider p' = p as Element of REAL n;
    -p' is Point of TOP-REAL n;
    hence thesis;
  end;
end;

theorem
  --p = p;

theorem
  p + -p = 0.REAL n by RVSUM_1:40;

theorem
  p1 + p2 = 0.REAL n implies p1 = -p2 & p2 = -p1 by RVSUM_1:41;

theorem
  -(p1 + p2) = -p1 + -p2 by RVSUM_1:45;

theorem
  -p = (-1)*p;

theorem Th44:
  -x*p = (-x)*p & -x*p = x*(-p)
proof
  reconsider r=p as Element of REAL n;
  reconsider r'=r as Element of n-tuples_on REAL;
  thus -x*p = ((-1)*x)*r' by RVSUM_1:71
    .= (-x)*p;
  hence -x*p = (x*(-1))*r
    .= x*(-p) by RVSUM_1:71;
end;

definition
  let n,p1,p2;
  canceled;
  redefine func p1 - p2 -> Point of TOP-REAL n;
  coherence
  proof
    reconsider p1' = p1, p2' = p2 as Element of REAL n;
    p1' - p2' is Point of TOP-REAL n;
    hence thesis;
  end;
end;

theorem
  p1 - p2 = p1 + -p2;

theorem
  p - p = 0.REAL n by RVSUM_1:58;

theorem
  p1 - p2 = 0.REAL n implies p1 = p2 by RVSUM_1:59;

theorem
  -(p1 - p2) = p2 - p1 & -(p1 - p2) = -p1 + p2
proof
  thus -(p1 - p2) = -p1 + --p2 by RVSUM_1:45
    .= p2 - p1;
  hence -(p1 - p2) = -p1 + p2;
end;

theorem Th49:
  p1 + (p2 - p3) = p1 + p2 - p3
proof
  thus p1 + (p2 - p3) = (p1 + p2) + -p3 by FINSEQOP:29
    .= p1 + p2 - p3;
end;

theorem Th50:
  p1 - (p2 + p3) = p1 - p2 - p3
proof
  thus p1 - (p2 + p3) = p1 + (-p2 + -p3) by RVSUM_1:45
    .= p1 - p2 - p3 by FINSEQOP:29;
end;

theorem
  p1 - (p2 - p3) = p1 - p2 + p3
proof
  thus p1 - (p2 - p3) = p1 - p2 - -p3 by Th50
    .= p1 - p2 + p3;
end;

theorem
  p = p + p1 - p1 & p = p - p1 + p1
proof
  thus p = p + 0.REAL n by RVSUM_1:33
    .= p + (p1 - p1) by RVSUM_1:58
    .= p + p1 - p1 by Th49;
  hence p = p + (p1 - p1) by Th49
    .= p + (-p1 + p1)
    .= p - p1 + p1 by FINSEQOP:29;
end;

theorem
  x*(p1 - p2) = x*p1 - x*p2
proof
  thus x*(p1 - p2) = x*p1 + x*(-p2) by RVSUM_1:73
    .= x*p1 - x*p2 by Th44;
end;

theorem
  (x-y)*p = x*p - y*p
proof
  thus (x-y)*p = (x+-y)*p
    .= x*p + (-y)*p by RVSUM_1:72
    .= x*p - y*p by Th44;
end;

reserve p,p1,p2 for Point of TOP-REAL 2;

theorem
  ex x,y being Real st p=<*x,y*> by FINSEQ_2:120;

definition
  let p;
  func p`1 -> Real equals

  p.1;
  coherence;
  func p`2 -> Real equals

  p.2;
  coherence;
end;

notation
  let x,y be real number;
  synonym |[ x,y ]| for <*x,y*>;
end;

definition
  let x,y be real number;
  redefine func |[ x,y ]| -> Point of TOP-REAL 2;
  coherence
  proof
    x is Real & y is Real by XREAL_0:def 1;
    hence <*x,y*> is Point of TOP-REAL 2 by FINSEQ_2:121;
  end;
end;

theorem
  |[x,y]|`1 = x & |[x,y]|`2 = y by FINSEQ_1:61;

theorem Th57:
  p = |[p`1, p`2]|
proof
  consider x,y be Real such that
A1: p = <* x,y *> by FINSEQ_2:120;
  p`1 = x & p`2 = y by A1,FINSEQ_1:61;
  hence thesis by A1;
end;

theorem
  0.REAL 2 = |[0,0]| by FINSEQ_2:75;

theorem Th59:
  p1 + p2 = |[ p1`1 + p2`1, p1`2 + p2`2]|
proof
  reconsider p1'=p1, p2'=p2 as Element of REAL 2;
  len(p1'+p2') = 2 by Th28;
  then
A1: dom(p1'+p2') = Seg 2 & 2 in {1,2} & 1 in {1,2}
  by FINSEQ_1:def 3,TARSKI:def 2;
  then
A2: (p1+p2)`1 = p1`1 + p2`1 by FINSEQ_1:4,VALUED_1:def 1;
  (p1+p2)`2 = p1`2 + p2`2 by A1,FINSEQ_1:4,VALUED_1:def 1;
  hence p1 + p2 = |[ p1`1 + p2`1, p1`2 + p2`2]| by A2,Th57;
end;

theorem
  |[x1, y1]| + |[x2, y2]| = |[ x1 + x2, y1 + y2]|
proof
  |[x1, y1]|`1 = x1 & |[x1, y1]|`2 = y1 &
  |[x2, y2]|`1 = x2 & |[x2, y2]|`2 = y2 by FINSEQ_1:61;
  hence thesis by Th59;
end;

theorem Th61:
  x*p = |[ x*p`1 ,x*p`2 ]|
proof
A1: (x*p)`1 = x*(p`1) by RVSUM_1:66;
  (x*p)`2 = x*(p`2) by RVSUM_1:66;
  hence x*p = |[ x*p`1, x*p`2]| by A1,Th57;
end;

theorem
  x*|[x1,y1]| = |[ x*x1,x*y1 ]|
proof
  |[x1,y1]|`1 = x1 & |[x1,y1]|`2 = y1 by FINSEQ_1:61;
  hence thesis by Th61;
end;

theorem Th63:
  -p = |[ -p`1, -p`2]|
proof
  thus -p = |[ (-1)*p`1, (-1)*p`2]| by Th61
    .= |[ -p`1, -p`2]|;
end;

theorem
  -|[x1,y1]| = |[ -x1, -y1]|
proof
  |[x1,y1]|`1 = x1 & |[x1,y1]|`2 = y1 by FINSEQ_1:61;
  hence thesis by Th63;
end;

theorem Th65:
  p1 - p2 = |[ p1`1 - p2`1, p1`2 - p2`2]|
proof
  -p2 = |[ -p2`1, -p2`2]| by Th63;
  then (-p2)`1 = -p2`1 & (-p2)`2 = -p2`2 by FINSEQ_1:61;
  hence p1 - p2 = |[ p1`1 + -p2`1, p1`2 + -p2`2]| by Th59
    .= |[ p1`1 - p2`1, p1`2 - p2`2]|;
end;

theorem
  |[x1, y1]| - |[x2, y2]| = |[ x1 - x2, y1 - y2]|
proof
  |[x1, y1]|`1 = x1 & |[x1, y1]|`2 = y1 & |[x2, y2]|`1 = x2 & |[x2, y2]|`2 =
  y2 by FINSEQ_1:61;
  hence thesis by Th65;
end;

theorem
  for P being Subset of TOP-REAL n, Q being non empty Subset of Euclid n holds
  P = Q implies (TOP-REAL n)|P = TopSpaceMetr((Euclid n)|Q)
proof
  let P be Subset of (TOP-REAL n), Q be non empty Subset of Euclid n;
  reconsider M = TopSpaceMetr((Euclid n)|Q) as SubSpace of TOP-REAL n
         by TOPMETR:19;
  assume
A1: P = Q;
  the carrier of M = the carrier of (Euclid n)|Q & the carrier of (Euclid n)|Q
  = Q by TOPMETR:def 2;
  then [#](M) = P & [#]((TOP-REAL n)|P) = P by A1,PRE_TOPC:def 10;
  hence thesis by PRE_TOPC:def 10;
end;

