:: Cardinal Arithmetics
::  by Grzegorz Bancerek
::
:: Received March 6, 1990
:: Copyright (c) 1990 Association of Mizar Users
 
environ

 vocabularies ORDINAL1, CARD_1, FUNCT_1, TARSKI, RELAT_1, XBOOLE_0, ZFMISC_1,
      MCART_1, FUNCT_2, ORDINAL2, ORDINAL3, FUNCOP_1, SUBSET_1, NUMBERS,
      ARYTM_3, FINSET_1, XXREAL_0, ARYTM_1, NAT_1, CARD_2;
 notations TARSKI, XBOOLE_0, ENUMSET1, ZFMISC_1, RELAT_1, FUNCT_1, FUNCT_2,
      ORDINAL1, MCART_1, ORDINAL2, ORDINAL3, WELLORD2, CARD_1, NUMBERS,
      XCMPLX_0, NAT_1, FINSET_1, FUNCOP_1, SUBSET_1, XXREAL_0;
 constructors ENUMSET1, WELLORD2, FUNCOP_1, ORDINAL3, XXREAL_0, XREAL_0, NAT_1,
      CARD_1;
 registrations SUBSET_1, ORDINAL1, FINSET_1, XREAL_0, CARD_1, FUNCT_2,
      ZFMISC_1, XBOOLE_0, RELAT_1;
 requirements NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, FUNCT_1, WELLORD2, XBOOLE_0;
 theorems TARSKI, ENUMSET1, FUNCT_1, FUNCT_2, MCART_1, ORDINAL1, ORDINAL2,
      WELLORD2, ORDINAL3, CARD_1, NAT_1, ZFMISC_1, FUNCT_5, RELAT_1, XBOOLE_0,
      XBOOLE_1, XREAL_1, XXREAL_0;
 schemes NAT_1, FUNCT_1, FINSET_1, PARTFUN1, CLASSES1;

begin

reserve A,B for Ordinal,
  K,M,N for Cardinal,
  x,x1,x2,y,y1,y2,z,u,X,Y,Z,X1,X2, Y1,Y2 for set,
  f,g for Function;

canceled;

theorem Th2:
  card X c= card Y iff ex f st X c= f.:Y
proof
  deffunc G(set) = $1;
  thus card X c= card Y implies ex f st X c= f.:Y
  proof
    assume card X c= card Y;
    then consider f such that
A1: dom f = Y & X c= rng f by CARD_1:28;
    take f;
    thus thesis by A1,RELAT_1:146;
  end;
  given f such that
A2: X c= f.:Y;
  defpred C[set] means $1 in dom f;
  deffunc F(set) = f.$1;
  consider g such that
A3: dom g = Y & for x st x in Y holds (C[x] implies g.x = F(x)) & (not C
  [x] implies g.x = G(x)) from PARTFUN1:sch 1;
  X c= rng g
  proof
    let x;
    assume x in X;
    then consider y such that
A4: y in dom f and
A5: y in Y and
A6: x = f.y by A2,FUNCT_1:def 12;
    x = g.y by A3,A4,A5,A6;
    hence thesis by A3,A5,FUNCT_1:def 5;
  end;
  hence thesis by A3,CARD_1:28;
end;

theorem
  card (f.:X) c= card X by Th2;

theorem
  card X in card Y implies Y \ X <> {}
proof
  assume that
A1: card X in card Y and
A2: Y \ X = {};
  Y c= X by A2,XBOOLE_1:37;
  then card Y c= card X by CARD_1:27;
  hence contradiction by A1,CARD_1:14;
end;

theorem
  x in X & X,Y are_equipotent implies Y <> {} & ex x st x in Y
proof
  assume
A1: x in X;
  given f such that
  f is one-to-one and
A2: dom f = X & rng f = Y;
  f.x in Y by A1,A2,FUNCT_1:def 5;
  hence Y <> {};
  take f.x;
  thus thesis by A1,A2,FUNCT_1:def 5;
end;

theorem
  bool X,bool card X are_equipotent & card bool X = card bool card X
proof
  X,card X are_equipotent by CARD_1:def 5;
  then consider f such that
A1: f is one-to-one and
A2: dom f = X and
A3: rng f = card X by WELLORD2:def 4;
  deffunc g(set) = f.:$1;
  consider g such that
A4: dom g = bool X & for x st x in bool X holds g.x = g(x) from FUNCT_1:
  sch 3;
  thus bool X,bool card X are_equipotent
  proof
    take g;
    thus g is one-to-one
    proof
      let x,y;
      assume that
A5:   x in dom g and
A6:   y in dom g and
A7:   g.x = g.y;
A8:   g.x = f.:x & g.y = f.:y by A4,A5,A6;
A9:   y c= x
      proof
        let z;
        assume
A10:    z in y;
        then f.z in f.:y by A2,A4,A6,FUNCT_1:def 12;
        then ex u st u in dom f & u in x & f.z = f.u by A7,A8, FUNCT_1:
        def 12;
        hence thesis by A1,A2,A4,A6,A10,FUNCT_1:def 8;
      end;
      x c= y
      proof
        let z;
        assume
A11:    z in x;
        then f.z in f.:x by A2,A4,A5,FUNCT_1:def 12;
        then ex u st u in dom f & u in y & f.z = f.u by A7,A8, FUNCT_1:
        def 12;
        hence thesis by A1,A2,A4,A5,A11,FUNCT_1:def 8;
      end;
      hence thesis by A9,XBOOLE_0:def 10;
    end;
    thus dom g = bool X by A4;
    thus rng g c= bool card X
    proof
      let x;
      assume x in rng g;
      then consider y such that
A12:  y in dom g and
A13:  x = g.y by FUNCT_1:def 5;
A14:  f.:y c= rng f by RELAT_1:144;
      g.y = f.:y by A4,A12;
      hence thesis by A3,A13,A14;
    end;
    let x;
A15: f"x c= dom f by RELAT_1:167;
    assume x in bool card X;
    then f.:(f"x) = x by A3,FUNCT_1:147;
    then g.(f"x) = x by A2,A4,A15;
    hence thesis by A2,A4,A15,FUNCT_1:def 5;
  end;
  hence thesis by CARD_1:21;
end;

deffunc g(set) = $1`1;

theorem
  Z in Funcs(X,Y) implies Z,X are_equipotent & card Z = card X
proof
  assume Z in Funcs(X,Y);
  then consider f such that
A1: Z = f and
A2: dom f = X and
  rng f c= Y by FUNCT_2:def 2;
  thus Z,X are_equipotent
  proof
    consider g such that
A3: dom g = Z & for x st x in Z holds g.x = g(x) from FUNCT_1:sch 3;
    take g;
    thus g is one-to-one
    proof
      let x,y;
      assume that
A4:   x in dom g and
A5:   y in dom g;
A6:   g.x = x`1 & g.y = y`1 by A3,A4,A5;
      ex x1,x2 being set st [x1,x2] = y by A1,A3,A5,RELAT_1:def 1;
      then
A7:   y = [y`1,y`2] by MCART_1:8;
      ex x1,x2 being set st [x1,x2] = x by A1,A3,A4,RELAT_1:def 1;
      then
A8:   x = [x`1,x`2] by MCART_1:8;
      then x`2 = f.(x`1) by A1,A3,A4,FUNCT_1:8;
      hence thesis by A1,A3,A5,A8,A7,A6,FUNCT_1:8;
    end;
    thus dom g = Z by A3;
    thus rng g c= X
    proof
      let x;
      assume x in rng g;
      then consider y such that
A9:   y in dom g and
A10:  x = g.y by FUNCT_1:def 5;
      ex x1,x2 being set st [x1,x2] = y by A1,A3,A9,RELAT_1:def 1;
      then
A11:  y = [y`1,y`2] by MCART_1:8;
      x = y`1 by A3,A9,A10;
      hence thesis by A1,A2,A3,A9,A11,FUNCT_1:8;
    end;
    let x;
    assume x in X;
    then
A12: [x,f.x] in Z by A1,A2,FUNCT_1:def 4;
    then g.[x,f.x] = [x,f.x]`1 by A3
      .= x by MCART_1:7;
    hence thesis by A3,A12,FUNCT_1:def 5;
  end;
  hence thesis by CARD_1:21;
end;

Lm1: x1 <> x2 implies A+^B,[:A,{x1}:] \/ [:B,{x2}:] are_equipotent & card(A+^B
) = card([:A,{x1}:] \/ [:B,{x2}:])
proof
  defpred C[set] means $1 in A;
  deffunc F(set) = [$1,x1];
  deffunc G(Ordinal) = [$1-^A,x2];
  consider f such that
A1: dom f = A+^B and
A2: for x being Ordinal holds x in A+^B implies (C[x] implies f.x = F(x)
  ) & (not C[x] implies f.x = G(x)) from FINSET_1:sch 1;
  assume
A3: x1 <> x2;
  thus A+^B,[:A,{x1}:] \/ [:B,{x2}:] are_equipotent
  proof
    take f;
    thus f is one-to-one
    proof
      let x,y;
      assume that
A4:   x in dom f & y in dom f and
A5:   f.x = f.y;
      reconsider C1 = x, C2 = y as Ordinal by A1,A4;
      per cases;
      suppose
A6:     x in A & y in A;
A7:     [x,x1]`1 = C1 by MCART_1:7;
        f.C1 = [x,x1] & f.C2 = [y,x1] by A1,A2,A4,A6;
        hence thesis by A5,A7,MCART_1:7;
      end;
      suppose
A8:     not x in A & not y in A;
A9:     [C1-^A,x2]`1 = C1-^A by MCART_1:7;
        f.x = [C1-^A,x2] & f.y = [C2-^A,x2] by A1,A2,A4,A8;
        then
A10:    C1-^A = C2-^A by A5,A9,MCART_1:7;
        A c= C1 by A8,ORDINAL1:26;
        then
A11:    C1 = A+^(C1-^A) by ORDINAL3:def 6;
        A c= C2 by A8,ORDINAL1:26;
        hence thesis by A10,A11,ORDINAL3:def 6;
      end;
      suppose
A12:    x in A & not y in A;
A13:    [x,x1]`2 = x1 by MCART_1:7;
        f.x = [x,x1] & f.y = [C2-^A,x2] by A1,A2,A4,A12;
        hence thesis by A3,A5,A13,MCART_1:7;
      end;
      suppose
A14:    not x in A & y in A;
A15:    [y,x1]`2 = x1 by MCART_1:7;
        f.y = [y,x1] & f.x = [C1-^A,x2] by A1,A2,A4,A14;
        hence thesis by A3,A5,A15,MCART_1:7;
      end;
    end;
    thus dom f = A+^B by A1;
    thus rng f c= [:A,{x1}:] \/ [:B,{x2}:]
    proof
      let x;
A16:  x1 in {x1} by TARSKI:def 1;
A17:  x2 in {x2} by TARSKI:def 1;
      assume x in rng f;
      then consider y such that
A18:  y in dom f and
A19:  x = f.y by FUNCT_1:def 5;
      reconsider C = y as Ordinal by A1,A18;
      per cases;
      suppose
        y in A;
        then x = [C,x1] & [C,x1] in [:A,{x1}:] by A1,A2,A18,A19,A16,
        ZFMISC_1:106;
        hence thesis by XBOOLE_0:def 3;
      end;
      suppose
A20:    not y in A;
        then A c= C by ORDINAL1:26;
        then A+^(C-^A) = C by ORDINAL3:def 6;
        then C-^A in B by A1,A18,ORDINAL3:25;
        then
A21:    [C-^A,x2] in [:B,{x2}:] by A17,ZFMISC_1:106;
        x = [C-^A,x2] by A1,A2,A18,A19,A20;
        hence thesis by A21,XBOOLE_0:def 3;
      end;
    end;
    let x such that
A22: x in [:A,{x1}:] \/ [:B,{x2}:];
A23: now
      assume x in [:B,{x2}:];
      then consider y,z such that
A24:  y in B and
A25:  z in {x2} and
A26:  x = [y,z] by ZFMISC_1:103;
      reconsider y as Ordinal by A24;
A27:  A+^y in A+^B by A24,ORDINAL2:49;
      A c= A+^y by ORDINAL3:27;
      then
A28:  not A+^y in A by ORDINAL1:7;
      A+^y-^A = y & z = x2 by A25,ORDINAL3:65,TARSKI:def 1;
      then x = f.(A+^y) by A2,A26,A27,A28;
      hence thesis by A1,A27,FUNCT_1:def 5;
    end;
    now
      assume x in [:A,{x1}:];
      then consider y,z such that
A29:  y in A and
A30:  z in {x1} and
A31:  x = [y,z] by ZFMISC_1:103;
A32:  A c= A+^B by ORDINAL3:27;
      z = x1 by A30,TARSKI:def 1;
      then x = f.y by A2,A29,A31,A32;
      hence thesis by A1,A29,A32,FUNCT_1:def 5;
    end;
    hence thesis by A22,A23,XBOOLE_0:def 3;
  end;
  hence thesis by CARD_1:21;
end;

deffunc plus(set,set) = [:$1,{0}:] \/ [:$2,{1}:];

Lm2: [:X,Y:],[:Y,X:] are_equipotent & card [:X,Y:] = card [:Y,X:]
proof
  deffunc f(set) = [$1`2,$1`1];
  consider f such that
A1: dom f = [:X,Y:] & for x st x in [:X,Y:] holds f.x = f(x) from
  FUNCT_1:sch 3;
  thus [:X,Y:],[:Y,X:] are_equipotent
  proof
    take f;
    thus f is one-to-one
    proof
      let x,y;
      assume
A2:   x in dom f & y in dom f;
      then
A3:   x = [x`1,x`2] & y = [y`1,y`2] by A1,MCART_1:23;
      assume
A4:   f.x = f.y;
A5:   f.x = [x`2,x`1] & f.y = [y`2,y `1] by A1,A2;
      then x`1 = y`1 by A4,ZFMISC_1:33;
      hence thesis by A3,A5,A4,ZFMISC_1:33;
    end;
    thus dom f = [:X,Y:] by A1;
    thus rng f c= [:Y,X:]
    proof
      let x;
      assume x in rng f;
      then consider y such that
A6:   y in dom f and
A7:   x = f.y by FUNCT_1:def 5;
A8:   y`2 in Y by A1,A6,MCART_1:10;
      x = [y`2,y`1] & y`1 in X by A1,A6,A7,MCART_1:10;
      hence thesis by A8,ZFMISC_1:106;
    end;
    let x;
A9: [x`2,x`1]`1 = x`2 & [x`2,x`1]`2 = x`1 by MCART_1:7;
    assume
A10: x in [:Y,X:];
    then
A11: x = [x`1,x`2] by MCART_1:23;
    x`1 in Y & x`2 in X by A10,MCART_1:10;
    then
A12: [x`2,x`1] in [:X,Y:] by ZFMISC_1:106;
    then f.[x`2,x`1] in rng f by A1,FUNCT_1:def 5;
    hence thesis by A1,A11,A12,A9;
  end;
  hence thesis by CARD_1:21;
end;

definition
  let M,N;
  func M +` N -> Cardinal equals
  card( M +^ N);
  coherence;
  commutativity
  proof
    let C be Cardinal;
    let M,N;
    assume C = card( M +^ N);
    hence C = card plus( N, M) by Lm1
      .= card( N +^ M) by Lm1;
  end;
  func M *` N -> Cardinal equals
  card [:M,N:];
  coherence;
  commutativity by Lm2;
  func exp(M,N) -> Cardinal equals
  card Funcs(N,M);
  coherence;
end;

canceled 3;

theorem
  [:X,Y:],[:Y,X:] are_equipotent & card [:X,Y:] = card [:Y,X:] by Lm2;

theorem Th12:
  [:[:X,Y:],Z:],[:X,[:Y,Z:]:] are_equipotent & card [:[:X,Y:],Z:]
  = card [:X,[:Y,Z:]:]
proof
  deffunc f(set) = [$1`1`1,[$1`1`2,$1`2]];
  consider f such that
A1: dom f = [:[:X,Y:],Z:] & for x st x in [:[:X,Y:],Z:] holds f.x = f(x)
  from FUNCT_1:sch 3;
  thus [:[:X,Y:],Z:],[:X,[:Y,Z:]:] are_equipotent
  proof
    take f;
    thus f is one-to-one
    proof
      let x,y;
      assume that
A2:   x in dom f and
A3:   y in dom f;
      assume
A4:   f.x = f.y;
A5:   x = [x`1,x`2] & y = [y`1,y`2] by A1,A2,A3,MCART_1:23;
      x`1 in [:X,Y:] by A1,A2,MCART_1:10;
      then
A6:   x`1 = [x`1`1,x`1`2 ] by MCART_1:23;
A7:   f.x = [x`1`1,[x`1`2,x`2]] & f.y = [y`1`1,[y`1`2,y`2]] by A1,A2,A3;
      then
A8:   x`1`1 = y`1`1 by A4,ZFMISC_1:33;
      y`1 in [:X,Y :] by A1,A3,MCART_1:10;
      then
A9:   y`1 = [y`1`1,y`1`2] by MCART_1:23;
A10:  [x`1`2,x`2] = [y`1`2,y`2] by A7,A4,ZFMISC_1:33;
      then x`1`2 = y`1`2 by ZFMISC_1:33;
      hence thesis by A5,A8,A10,A6,A9,ZFMISC_1:33;
    end;
    thus dom f = [:[:X,Y:],Z:] by A1;
    thus rng f c= [:X,[:Y,Z:]:]
    proof
      let x;
      assume x in rng f;
      then consider y such that
A11:  y in dom f and
A12:  x = f.y by FUNCT_1:def 5;
A13:  y`1 in [:X,Y:] by A1,A11,MCART_1:10;
      then
A14:  y`1`2 in Y by MCART_1:10;
      y`2 in Z by A1,A11,MCART_1:10;
      then
A15:  [y`1`2,y`2] in [:Y,Z:] by A14,ZFMISC_1:106;
A16:  y`1`1 in X by A13,MCART_1:10;
      x = [y`1`1,[y`1`2,y`2]] by A1,A11,A12;
      hence thesis by A16,A15,ZFMISC_1:106;
    end;
    let x;
A17: [x`1,x`2`1]`1 = x`1 & [x`1,x`2`1]`2 = x`2 `1 by MCART_1:7;
A18: [[x`1,x`2`1],x`2`2]`1 = [x`1,x`2`1] & [[x`1,x`2`1],x`2`2]`2 = x`2`2
    by MCART_1:7;
    assume
A19: x in [:X,[:Y,Z:]:];
    then
A20: x`2 in [:Y,Z:] by MCART_1:10;
    then
A21: x`2`1 in Y by MCART_1:10;
A22: x`2`2 in Z by A20,MCART_1:10;
    x`1 in X by A19,MCART_1:10;
    then [x`1,x`2`1] in [:X,Y:] by A21,ZFMISC_1:106;
    then
A23: [[x`1,x`2`1],x`2`2] in [:[:X,Y:],Z:] by A22,ZFMISC_1:106;
A24: x`2 = [x`2`1,x`2`2] by A20,MCART_1:23;
    x = [x`1,x`2] by A19,MCART_1:23;
    then x = f.[[x`1,x`2`1],x`2`2] by A1,A24,A17,A23,A18;
    hence thesis by A1,A23,FUNCT_1:def 5;
  end;
  hence thesis by CARD_1:21;
end;

theorem Th13:
  X,[:X,{x}:] are_equipotent & card X = card [:X,{x}:]
proof
  deffunc f(set) = [$1,x];
  consider f such that
A1: dom f = X & for y st y in X holds f.y = f(y) from FUNCT_1:sch 3;
  thus X,[:X,{x}:] are_equipotent
  proof
    take f;
    thus f is one-to-one
    proof
      let y,z;
      assume that
A2:   y in dom f & z in dom f and
A3:   f.y = f.z;
A4:   [y,x]`1 = y by MCART_1:7;
      f.y = [y,x] & f.z = [z,x] by A1,A2;
      hence thesis by A3,A4,MCART_1:7;
    end;
    thus dom f = X by A1;
    thus rng f c= [:X,{x}:]
    proof
      let y;
A5:   x in {x} by TARSKI:def 1;
      assume y in rng f;
      then consider z such that
A6:   z in dom f and
A7:   y = f.z by FUNCT_1:def 5;
      y = [z,x] by A1,A6,A7;
      hence thesis by A1,A6,A5,ZFMISC_1:106;
    end;
    let y;
    assume y in [:X,{x}:];
    then consider y1,y2 being set such that
A8: y1 in X and
A9: y2 in {x} and
A10: y = [y1,y2] by ZFMISC_1:103;
    y2 = x by A9,TARSKI:def 1;
    then y = f.y1 by A1,A8,A10;
    hence thesis by A1,A8,FUNCT_1:def 5;
  end;
  hence thesis by CARD_1:21;
end;

Lm3: [:X,Y:],[:card X,Y:] are_equipotent
proof
  X,card X are_equipotent by CARD_1:def 5;
  then consider f such that
A1: f is one-to-one and
A2: dom f = X and
A3: rng f = card X by WELLORD2:def 4;
  deffunc g(set) = [f.$1`1,$1`2];
  consider g such that
A4: dom g = [:X,Y:] & for x st x in [:X,Y:] holds g.x = g(x) from
  FUNCT_1:sch 3;
  take g;
  thus g is one-to-one
  proof
    let x,y;
    assume
A5: x in dom g & y in dom g;
    then
A6: x`1 in X & y`1 in X by A4,MCART_1:10;
    assume
A7: g.x = g.y;
    g.x = [f.x`1,x`2] & g.y = [f.y`1,y `2] by A4,A5;
    then
A8: f.x`1 = f.y`1 & x`2 = y`2 by A7,ZFMISC_1:33;
    x = [x`1,x`2] & y = [y`1,y`2] by A4,A5,MCART_1:23;
    hence thesis by A1,A2,A6,A8,FUNCT_1:def 8;
  end;
  thus dom g = [:X,Y:] by A4;
  thus rng g c= [:card X,Y:]
  proof
    let y;
    assume y in rng g;
    then consider x such that
A9: x in dom g and
A10: y = g.x by FUNCT_1:def 5;
    x`1 in X by A4,A9,MCART_1:10;
    then
A11: f.x`1 in card X by A2,A3,FUNCT_1:def 5;
    y = [f.x`1,x`2] & x`2 in Y by A4,A9,A10,MCART_1:10;
    hence thesis by A11,ZFMISC_1:106;
  end;
  let y;
  assume
A12: y in [:card X,Y:];
  then y`1 in card X by MCART_1:10;
  then consider x such that
A13: x in X and
A14: y`1 = f.x by A2,A3,FUNCT_1:def 5;
A15: y = [y`1,y`2] by A12,MCART_1:23;
  y`2 in Y by A12,MCART_1:10;
  then
A16: [x,y`2] in [:X,Y:] by A13,ZFMISC_1:106;
  [x,y`2]`1 = x & [x,y`2]`2 = y`2 by MCART_1:7;
  then g.[x,y`2] = y by A4,A15,A14,A16;
  hence thesis by A4,A16,FUNCT_1:def 5;
end;

theorem Th14:
  [:X,Y:],[:card X,Y:] are_equipotent & [:X,Y:],[:X,card Y:]
are_equipotent & [:X,Y:],[:card X,card Y:] are_equipotent & card [:X,Y:] = card
[:card X,Y:] & card [:X,Y:] = card [:X,card Y:] & card [:X,Y:] = card [:card X,
  card Y:]
proof
  [:Y,X:],[:card Y,X:] are_equipotent & [:X,Y:],[:Y,X:] are_equipotent by
  Lm2,Lm3;
  then
A1: [:X,Y:],[:card Y,X:] are_equipotent by WELLORD2:22;
  [:card Y,X:],[:X,card Y:] are_equipotent by Lm2;
  hence
A2: [:X,Y:],[:card X,Y:] are_equipotent & [:X,Y:],[:X,card Y:]
  are_equipotent by A1,Lm3,WELLORD2:22;
  [:X,card Y:],[:card X,card Y:] are_equipotent by Lm3;
  hence [:X,Y:],[:card X,card Y:] are_equipotent by A2,WELLORD2:22;
  hence thesis by A2,CARD_1:21;
end;

theorem Th15:
  X1,Y1 are_equipotent & X2,Y2 are_equipotent implies [:X1,X2:],[:
  Y1,Y2:] are_equipotent & card [:X1,X2:] = card [:Y1,Y2:]
proof
  assume X1,Y1 are_equipotent & X2,Y2 are_equipotent;
  then
A1: card X1 = card Y1 & card X2 = card Y2 by CARD_1:21;
  [:X1,X2:],[:card X1,card X2:] are_equipotent & [:Y1,Y2:],[:card Y1,card
  Y2:] are_equipotent by Th14;
  hence [:X1,X2:],[:Y1,Y2:] are_equipotent by A1,WELLORD2:22;
  hence thesis by CARD_1:21;
end;

theorem
  x1 <> x2 implies A+^B,[:A,{x1}:] \/ [:B,{x2}:] are_equipotent & card(A
  +^B) = card([:A,{x1}:] \/ [:B,{x2}:]) by Lm1;

theorem Th17:
  x1 <> x2 implies K+`M,[:K,{x1}:] \/ [:M,{x2}:] are_equipotent &
  K+`M = card([:K,{x1}:] \/ [:M,{x2}:])
proof
  assume x1 <> x2;
  then card([:K,{x1}:] \/ [:M,{x2}:]) = K+`M by Lm1;
  hence thesis by CARD_1:def 5;
end;

theorem Th18:
  A*^B,[:A,B:] are_equipotent & card(A*^B) = card [:A,B:]
proof
  defpred P[set,set] means ex O1, O2 being Ordinal st O1 = $1`1 & O2 = $1`2 &
  $2 = O1*^B+^O2;
A1: for x st x in [:A,B:] ex y st P[x,y]
  proof
    let x;
    assume x in [:A,B:];
    then x`1 in A & x`2 in B by MCART_1:10;
    then reconsider x1 = x`1, x2 = x`2 as Ordinal;
    take x1*^B+^x2;
    take x1, x2;
    thus thesis;
  end;
  consider f such that
A2: dom f = [:A,B:] & for x st x in [:A,B:] holds P[x,f.x] from CLASSES1
  :sch 1(A1);
A3: [:A,B:],A*^B are_equipotent
  proof
    take f;
    thus f is one-to-one
    proof
      let x,y;
      assume
A4:   x in dom f & y in dom f;
      then
A5:   x`2 in B & y`2 in B by A2,MCART_1:10;
      x`1 in A & y`1 in A by A2,A4,MCART_1:10;
      then reconsider x1 = x`1, y1 = y`1 as Ordinal by A5;
      assume
A6:   f.x = f.y;
A7:   x = [x`1,x`2] & y = [y`1,y`2] by A2,A4,MCART_1:23;
A8:   (ex O1, O2 being Ordinal st O1 = x`1 & O2 = x`2 & f.x = O1*^B+^O2 )
& ex O3, O4 being Ordinal st O3 = y`1 & O4 = y`2 & f.y = O3*^B+^O4 by A2,A4;
      then x1 = y1 by A5,A6,ORDINAL3:56;
      hence thesis by A7,A5,A8,A6,ORDINAL3:56;
    end;
    thus dom f = [:A,B:] by A2;
    thus rng f c= A*^B
    proof
      let y;
A9:   1*^B = B by ORDINAL2:56;
      assume y in rng f;
      then consider x such that
A10:  x in dom f and
A11:  y = f.x by FUNCT_1:def 5;
      consider x1, x2 being Ordinal such that
A12:  x1 = x`1 and
A13:  x2 = x`2 & f.x = x1*^B+^x2 by A2,A10;
      x1+^1 = succ x1 by ORDINAL2:48;
      then
A14:  x1*^B+^1*^B = (succ x1)*^B by ORDINAL3:54;
      x`1 in A by A2,A10,MCART_1:10;
      then succ x1 c= A by A12,ORDINAL1:33;
      then
A15:  x1*^B+^1*^B c= A*^B by A14,ORDINAL2:58;
      x`2 in B by A2,A10,MCART_1:10;
      then y in x1*^B+^1*^B by A11,A13,A9,ORDINAL2:49;
      hence thesis by A15;
    end;
    let y;
    assume
A16: y in A*^B;
    then reconsider C = y as Ordinal;
A17: C = (C div^ B)*^B+^(C mod^ B) & [C div^ B,C mod^ B]`1 = C div^ B by
    MCART_1:7,ORDINAL3:78;
    C div^ B in A & C mod^ B in B by A16,ORDINAL3:80;
    then
A18: [C div^ B,C mod^ B] in [:A,B:] by ZFMISC_1:106;
    then
    [C div^ B,C mod^ B]`2 = C mod^ B & ex O1, O2 being Ordinal st O1 = [C
div^ B,C mod^ B]`1 & O2 = [C div^ B,C mod^ B]`2 & f.[C div^ B,C mod^ B] = O1*^B
    +^ O2 by A2,MCART_1:7;
    hence thesis by A2,A18,A17,FUNCT_1:def 5;
  end;
  hence A*^B,[:A,B:] are_equipotent;
  thus thesis by A3,CARD_1:21;
end;

deffunc plus(set,set) = [:$1,{0}:] \/ [:$2,{1}:];
deffunc plus(set,set,set,set) = [:$1,{$3}:] \/ [:$2,{$4}:];

canceled 4;

theorem Th23:
  X1,Y1 are_equipotent & X2,Y2 are_equipotent & x1 <> x2 & y1 <>
y2 implies [:X1,{x1}:] \/ [:X2,{x2}:],[:Y1,{y1}:] \/ [:Y2,{y2}:] are_equipotent
  & card ([:X1,{x1}:] \/ [:X2,{x2}:]) = card ([:Y1,{y1}:] \/ [:Y2,{y2}:])
proof
  assume that
A1: X1,Y1 are_equipotent and
A2: X2,Y2 are_equipotent and
A3: x1 <> x2 and
A4: y1 <> y2;
  {x2},{y2} are_equipotent by CARD_1:48;
  then
A5: [:X2,{x2}:],[:Y2,{y2}:] are_equipotent by A2,Th15;
A6: now
    assume [:Y1,{y1}:] meets [:Y2,{y2}:];
    then consider y being set such that
A7: y in [:Y1,{y1}:] and
A8: y in [:Y2,{y2}:] by XBOOLE_0:3;
    y`2 in {y1} by A7,MCART_1:10;
    then
A9: y`2 = y1 by TARSKI:def 1;
    y`2 in {y2} by A8,MCART_1:10;
    hence contradiction by A4,A9,TARSKI:def 1;
  end;
A10: now
    assume [:X1,{x1}:] meets [:X2,{x2}:];
    then consider x being set such that
A11: x in [:X1,{x1}:] and
A12: x in [:X2,{x2}:] by XBOOLE_0:3;
    x`2 in {x1} by A11,MCART_1:10;
    then
A13: x`2 = x1 by TARSKI:def 1;
    x`2 in {x2} by A12,MCART_1:10;
    hence contradiction by A3,A13,TARSKI:def 1;
  end;
  {x1},{y1} are_equipotent by CARD_1:48;
  then [:X1,{x1}:],[:Y1,{y1}:] are_equipotent by A1,Th15;
  hence [:X1,{x1}:] \/ [:X2,{x2}:],[:Y1,{y1}:] \/ [:Y2,{y2}:]
  are_equipotent by A5,A10,A6,CARD_1:58;
  hence thesis by CARD_1:21;
end;

theorem Th24:
  card(A+^B) = card A +` card B
proof
A1: A+^B,plus(A,B) are_equipotent by Lm1;
  A,card A are_equipotent & B,card B are_equipotent by CARD_1:def 5;
  then
A2: plus(A,B),plus( card A, card B) are_equipotent by Th23;
  plus( card A, card B),card A +^ card B are_equipotent by Lm1;
  then plus(A,B),card A +^ card B are_equipotent by A2,WELLORD2:22;
  then A+^B,card A +^ card B are_equipotent by A1,WELLORD2:22;
  hence thesis by CARD_1:21;
end;

theorem Th25:
  card(A*^B) = card A *` card B
proof
  thus card (A*^B) = card [:A,B:] by Th18
    .= card A *` card B by Th14;
end;

theorem
  [:X,{0}:] \/ [:Y,{1}:],[:Y,{0}:] \/ [:X,{1}:] are_equipotent & card([:
  X,{0}:] \/ [:Y,{1}:]) = card([:Y,{0}:] \/ [:X,{1}:]) by Th23;

theorem Th27:
  [:X1,X2:] \/ [:Y1,Y2:],[:X2,X1:] \/ [:Y2,Y1:] are_equipotent &
  card ([:X1,X2:] \/ [:Y1,Y2:]) = card ([:X2,X1:] \/ [:Y2,Y1:])
proof
  deffunc f(set) = [$1`2,$1`1];
  consider f such that
A1: dom f = [:X1,X2:] \/ [:Y1,Y2:] & for x st x in [:X1,X2:] \/ [:Y1,Y2
  :] holds f.x = f(x) from FUNCT_1:sch 3;
  thus [:X1,X2:] \/ [:Y1,Y2:],[:X2,X1:] \/ [:Y2,Y1:] are_equipotent
  proof
    take f;
    thus f is one-to-one
    proof
      let x1,x2;
      assume that
A2:   x1 in dom f and
A3:   x2 in dom f and
A4:   f.x1 = f.x2;
      x1 in [:X1,X2:] or x1 in [:Y1,Y2:] by A1,A2,XBOOLE_0:def 3;
      then
A5:   x1 = [x1`1,x1`2] by MCART_1:23;
      x2 in [:X1,X2:] or x2 in [:Y1,Y2:] by A1,A3,XBOOLE_0:def 3;
      then
A6:   x2 = [x2`1,x2`2] by MCART_1:23;
A7:   f.x1 = [x1`2,x1`1] & f.x2 = [x2`2,x2`1] by A1,A2,A3;
      then x1`1 = x2`1 by A4,ZFMISC_1:33;
      hence thesis by A4,A7,A5,A6,ZFMISC_1:33;
    end;
    thus dom f = [:X1,X2:] \/ [:Y1,Y2:] by A1;
    thus rng f c= [:X2,X1:] \/ [:Y2,Y1:]
    proof
      let x;
      assume x in rng f;
      then consider y such that
A8:   y in dom f and
A9:   x = f.y by FUNCT_1:def 5;
      y in [:X1,X2:] or y in [:Y1,Y2:] by A1,A8,XBOOLE_0:def 3;
      then
A10:  y`1 in X1 & y`2 in X2 or y`1 in Y1 & y`2 in Y2 by MCART_1:10;
      x = [y`2,y`1] by A1,A8,A9;
      then x in [:X2,X1:] or x in [:Y2,Y1:] by A10,ZFMISC_1:106;
      hence thesis by XBOOLE_0:def 3;
    end;
    let x;
A11: [x`2,x`1]`1 = x`2 & [x`2,x`1]`2 = x`1 by MCART_1:7;
    assume x in [:X2,X1:] \/ [:Y2,Y1:];
    then
A12: x in [:X2,X1:] or x in [:Y2,Y1:] by XBOOLE_0:def 3;
    then x`1 in X2 & x`2 in X1 or x`1 in Y2 & x`2 in Y1 by MCART_1:10;
    then [x`2,x`1] in [:X1,X2:] or [x`2,x`1] in [:Y1,Y2:] by
    ZFMISC_1:106;
    then
A13: [x`2,x`1] in [:X1,X2:] \/ [:Y1,Y2:] by XBOOLE_0:def 3;
    x = [x`1,x`2] by A12,MCART_1:23;
    then f.[x`2,x`1] = x by A1,A13,A11;
    hence thesis by A1,A13,FUNCT_1:def 5;
  end;
  hence thesis by CARD_1:21;
end;

theorem Th28:
  x <> y implies card X +` card Y = card([:X,{x}:] \/ [:Y,{y}:])
proof
  assume
A1: x <> y;
  X,card X are_equipotent & Y,card Y are_equipotent by CARD_1:def 5;
  then card plus(X,Y,x,y) = card plus(card X,card Y,x,y) by A1,Th23;
  hence thesis by A1,Lm1;
end;

theorem
  M+`0 = M
proof
  thus M+`0 = card plus(M,{}) by Lm1
    .= card M by Th13
    .= M by CARD_1:def 5;
end;

Lm4: x <> y implies [:X,{x}:] misses [:Y,{y}:]
proof
  assume
A1: x <> y;
  assume not thesis;
  then consider z being set such that
A2: z in [:X,{x}:] and
A3: z in [:Y,{y}:] by XBOOLE_0:3;
  z`2 = x by A2,MCART_1:13;
  hence contradiction by A1,A3,MCART_1:13;
end;

canceled;

theorem Th31:
  (K+`M)+`N = K+`(M+`N)
proof
A1: card(K+`M+`N) = K+`M+`N;
A2: K+`M+`N,[:K+`M,{0}:] \/ [:N,{2}:] are_equipotent by Th17;
  [:M,{1}:] misses [:N,{2}:] by Lm4;
  then
A3: [:M,{1}:] /\ [:N,{2}:] = {} by XBOOLE_0:def 7;
  [:K,{0}:] misses [:N,{2}:] by Lm4;
  then [:K,{0}:] /\ [:N,{2}:] = {} by XBOOLE_0:def 7;
  then ([:K,{0}:] \/ [:M,{1}:]) /\ [:N,{2}:] = {} \/ {} by
  A3,XBOOLE_1:23
    .= {};
  then
A4: ([:K,{0}:] \/ [:M,{1}:]) misses [:N,{2}:] by XBOOLE_0:def 7;
  K+`M,[:K,{0}:] \/ [:M,{1}:] are_equipotent & K+`M,[:K+`M,{0}:]
  are_equipotent by Th13,Th17;
  then
A5: [:K+`M,{0}:],[:K,{0}:] \/ [:M,{1}:] are_equipotent by WELLORD2:22;
  [:K,{0}:] misses [:N,{2}:] by Lm4;
  then
A6: [:K,{0}:] /\ [:N,{2}:] = {} by XBOOLE_0:def 7;
  [:K,{0}:] misses [:M,{1}:] by Lm4;
  then [:K,{0}:] /\ [:M,{1}:] = {} by XBOOLE_0:def 7;
  then [:K,{0}:] /\ ([:M,{1}:] \/ [:N,{2}:]) = {} \/ {} by
  A6,XBOOLE_1:23
    .= {};
  then
A7: [:K,{0}:] misses ([:M,{1}:] \/ [:N,{2}:]) by XBOOLE_0:def 7;
  M+`N,[:M,{1}:] \/ [:N,{2}:] are_equipotent & M+`N,[:M+`N,{2}:]
  are_equipotent by Th13,Th17;
  then
A8: [:M+`N,{2}:],[:M,{1}:] \/ [:N,{2}:] are_equipotent by WELLORD2:22;
  [:K,{0}:] misses [:M+`N,{2}:] by Lm4;
  then
A9: [:K,{0}:] \/ ([:M,{1}:] \/ [:N,{2}:]),[:K,{0}:] \/ [:M+`N,{2}:]
  are_equipotent by A7,A8,CARD_1:58;
  [:K+`M,{0}:] misses [:N,{2}:] by Lm4;
  then
A10: [:K+`M,{0}:] \/ [:N,{2}:], [:K,{0}:] \/ [:M,{1}:] \/ [:N,{2}:]
  are_equipotent by A4,A5,CARD_1:58;
  [:K,{0}:] \/ ([:M,{1}:] \/ [:N,{2}:]) = [:K,{0}:] \/ [:M,{1}:] \/ [:N,{
  2}:] by XBOOLE_1:4;
  then [:K+`M,{0}:] \/ [:N,{2}:],[:K,{0}:] \/ [:M+`N,{2}:] are_equipotent by
  A10,A9,WELLORD2:22;
  then
A11: K+`M+`N,[:K,{0}:] \/ [:M+`N,{2}:] are_equipotent by A2,
  WELLORD2:22;
  [:K,{0}:] \/ [:M+`N,{2}:],K+`(M+`N) are_equipotent by Th17;
  then K+`M+`N,K+`(M+`N) are_equipotent by A11,WELLORD2:22;
  hence thesis by A1,CARD_1:def 5;
end;

theorem
  K*`0 = 0;

theorem
  K*`1 = K
proof
  K = card K by CARD_1:def 5;
  hence thesis by Th13,ORDINAL3:18;
end;

canceled;

theorem Th35:
  (K*`M)*`N = K*`(M*`N)
proof
  thus (K*`M)*`N = card [:[:K,M:],N:] by Th14
    .= card [:K,[:M,N:]:] by Th12
    .= K*`(M*`N) by Th14;
end;

theorem Th36:
  2*`K = K+`K
proof
  thus 2*`K = card([:{{}},K:] \/ [:{1},K:]) by CARD_1:88,ZFMISC_1:132
    .= card([:K,{{}}:] \/ [:K,{1}:]) by Th27
    .= card K +` card K by Th28
    .= K +` card K by CARD_1:def 5
    .= K +` K by CARD_1:def 5;
end;

theorem Th37:
  K*`(M+`N) = K*`M +` K*`N
proof
A1: [:card [:K,M:],{0}:],[:[:K,M:],{0}:] are_equipotent by Th14;
  M,[:M,{0}:] are_equipotent by Th13;
  then
A2: [:K,M:],[:K,[:M,{0}:]:] are_equipotent by Th15;
  [:[:K,M:],{0}:],[:K,M:] are_equipotent by Th13;
  then [:[:K,M:],{0}:],[:K,[:M,{0}:]:] are_equipotent by A2,WELLORD2:22;
  then
A3: [:card [:K,M:],{0}:],[:K,[:M,{0}:]:] are_equipotent by
  A1, WELLORD2
  :22;
A4: [:card [:K,N:],{1}:],[:[:K,N:],{1}:] are_equipotent by Th14;
  [:M,{0}:] misses [:N,{1}:] by Lm4;
  then
A5: [:K,[:M,{0}:]:] misses [:K,[:N,{1}:]:] by ZFMISC_1:127;
  N,[:N,{1}:] are_equipotent by Th13;
  then
A6: [:K,N:],[:K,[:N,{1}:]:] are_equipotent by Th15;
A7: K*`(M+`N) = card [:K,card plus(M,N):] by Th17
    .= card [:K,plus(M,N):] by Th14
    .= card ([:K,[:M,{0}:]:] \/ [:K,[:N,{1}:]:]) by ZFMISC_1:120;
  [:[:K,N:],{1}:],[:K,N:] are_equipotent by Th13;
  then [:[:K,N:],{1}:],[:K,[:N,{1}:]:] are_equipotent by A6,WELLORD2:22;
  then
A8: [:card [:K,N:],{1}:],[:K,[:N,{1}:]:] are_equipotent by
  A4, WELLORD2
  :22;
  [:card [:K,M:],{0}:] misses [:card [:K,N:],{1}:] by Lm4;
  then
  [:card [:K,M:],{0}:] \/ [:card [:K,N:],{1}:], [:K,[:M,{0}:]:] \/ [:K,[:
  N,{1}:]:] are_equipotent by A3,A8,A5, CARD_1:58;
  hence K*`(M+`N) = card([:card [:K,M:],{0}:] \/ [:card [:K,N:],{1}:]) by A7,
  CARD_1:21
    .= K*`M +` K*`N by Th17;
end;

theorem
  exp(K,0) = 1
proof
  thus exp(K,0) = card {{}} by FUNCT_5:64
    .= 1 by CARD_1:50;
end;

theorem
  K <> 0 implies exp(0,K) = 0;

theorem
  exp(K,1) = K & exp(1,K) = 1
proof
  thus exp(K,1) = card K by FUNCT_5:65,ORDINAL3:18
    .= K by CARD_1:def 5;
  thus exp(1,K) = card {K --> {}} by FUNCT_5:66,ORDINAL3:18
    .= 1 by CARD_1:50;
end;

theorem
  exp(K,M+`N) = exp(K,M)*`exp(K,N)
proof
A1: [:M,{0}:] misses [:N,{1}:] by ZFMISC_1:131;
  [:M,{0}:],M are_equipotent by Th13;
  then
A2: Funcs([:M,{0}:],K),Funcs(M,K) are_equipotent by FUNCT_5:67;
  [:N,{1}:],N are_equipotent by Th13;
  then
A3: Funcs([:N,{1}:],K),Funcs(N,K) are_equipotent by FUNCT_5:67;
  M+`N,[:M,{0}:] \/ [:N,{1}:] are_equipotent by Th17;
  hence exp(K,M+`N) = card Funcs([:M,{0}:] \/ [:N,{1}:],K) by FUNCT_5:67
    .= card [:Funcs([:M,{0}:],K),Funcs([:N,{1}:],K):] by A1,FUNCT_5:69
    .= card [:Funcs(M,K),Funcs(N,K):] by A2,A3,Th15
    .= exp(K,M)*`exp(K,N) by Th14;
end;

theorem
  exp(K*`M,N) = exp(K,N)*`exp(M,N)
proof
  card(K*`M) = K*`M & card N = card N;
  hence exp(K*`M,N) = card Funcs(N,[:K,M:]) by FUNCT_5:68
    .= card [:Funcs(N,K),Funcs(N,M):] by FUNCT_5:71
    .= exp(K,N)*`exp(M,N) by Th14;
end;

theorem
  exp(K,M*`N) = exp(exp(K,M),N)
proof
A1: Funcs(M,K),card Funcs(M,K) are_equipotent by CARD_1:def 5;
  [:M,N:],M*`N are_equipotent & [:N,M:],[:M,N:] are_equipotent by Lm2,
  CARD_1:def 5;
  then [:N,M:],M*`N are_equipotent by WELLORD2:22;
  hence exp(K,M*`N) = card Funcs([:N,M:],K) by FUNCT_5:67
    .= card Funcs(N,Funcs(M,K)) by FUNCT_5:70
    .= exp(exp(K,M),N) by A1,FUNCT_5:67;
end;

theorem
  exp(2,card X) = card bool X
proof
  card card X = card X & card 2 = card {{},1} by CARD_1:88;
  hence exp(2,card X) = card Funcs(X,{{},1}) by FUNCT_5:68
    .= card bool X by FUNCT_5:72;
end;

theorem
  exp(K,2) = K*`K by CARD_1:88,FUNCT_5:73;

theorem
  exp(K+`M,2) = K*`K +` 2*`K*`M +` M*`M
proof
  thus exp(K+`M,2) = (K+`M)*`(K+`M) by CARD_1:88,FUNCT_5:73
    .= K*`(K+`M) +` M*`(K+`M) by Th37
    .= K*`K +` K*`M +` M*`(K+`M) by Th37
    .= K*`K +` K*`M +` (M*`K +` M*`M) by Th37
    .= K*`K +` K*`M +` K*`M +` M*`M by Th31
    .= K*`K +` (K*`M +` K*`M) +` M*`M by Th31
    .= K*`K +` 2*`(K*`M) +` M*`M by Th36
    .= K*`K +` 2*`K*`M +` M*`M by Th35;
end;

theorem Th47:
  card(X \/ Y) c= card X +` card Y
proof
  consider f such that
A1: dom f = plus(X,Y) & for x st x in plus(X,Y) holds f.x = g(x) from
  FUNCT_1:sch 3;
  X \/ Y c= rng f
  proof
    let x;
    assume x in X \/ Y;
    then
A2: x in X or x in Y by XBOOLE_0:def 3;
    per cases;
    suppose
      x in X;
      then [x,0] in [:X,{0}:] by ZFMISC_1:129;
      then
A3:   [x,0] in plus(X,Y) by XBOOLE_0:def 3;
      [x,0]`1 = x by MCART_1:7;
      then x = f.[x,0] by A1,A3;
      hence thesis by A1,A3,FUNCT_1:def 5;
    end;
    suppose
      not x in X;
      then [x,1] in [:Y,{1}:] by A2,ZFMISC_1:129;
      then
A4:   [x,1] in plus(X,Y) by XBOOLE_0:def 3;
      [x,1]`1 = x by MCART_1:7;
      then x = f.[x,1] by A1,A4;
      hence thesis by A1,A4,FUNCT_1:def 5;
    end;
  end;
  then card(X \/ Y) c= card plus(X,Y) by A1,CARD_1:28;
  hence thesis by Th28;
end;

theorem Th48:
  X misses Y implies card (X \/ Y) = card X +` card Y
proof
  assume
A1: X misses Y;
  X,[:X,{0}:] are_equipotent & [:X,{0}:],[:card X,{0}:] are_equipotent by
  Th13,Th14;
  then
A2: X,[:card X,{0}:] are_equipotent by WELLORD2:22;
  Y,[:Y,{1}:] are_equipotent & [:Y,{1}:],[:card Y,{1}:] are_equipotent by
  Th13,Th14;
  then
A3: Y,[:card Y,{1}:] are_equipotent by WELLORD2:22;
  [:card X,{0}:] misses [:card Y,{1}:] by Lm4;
  then X \/ Y,[:card X,{0}:] \/ [:card Y,{1}:] are_equipotent by A1,A2,A3,
  CARD_1:58;
  hence card (X \/ Y) = card ([:card X,{0}:] \/ [:card Y,{1}:]) by CARD_1:21
    .= card X +` card Y by Th17;
end;

reserve m,n for Element of NAT;

theorem Th49:
  n+m = n +^ m
proof
  defpred P[Element of NAT] means n+$1 = n +^ $1;
A1: for m st P[m] holds P[m+1]
  proof
    let m such that
A2: P[m];
    thus n+(m+1) = n+m+1 .= succ( n +^ m) by A2,NAT_1:39
      .= n +^ succ m by ORDINAL2:45
      .= n +^ (m+1) by NAT_1:39;
  end;
A3: P[0] by ORDINAL2:44;
  for m holds P[m] from NAT_1:sch 1(A3,A1);
  hence thesis;
end;

theorem Th50:
  n*m = n *^ m
proof
  defpred P[Element of NAT] means $1*m = $1 *^ m;
A1: for n st P[n] holds P[n+1]
  proof
    let n such that
A2: P[n];
    thus (n+1)*m = n*m+1*m .= n *^ m +^ m by A2,Th49
      .= n *^ m +^ 1 *^ m by ORDINAL2:56
      .= ( n +^ 1) *^ m by ORDINAL3:54
      .= (succ n) *^ m by ORDINAL2:48
      .= (n+1) *^ m by NAT_1:39;
  end;
A3: P[0] by ORDINAL2:52;
  for n holds P[n] from NAT_1:sch 1(A3,A1);
  hence thesis;
end;

theorem Th51:
  card(n+m) = card n +` card m
proof
  thus card(n+m) = card( n +^ m) by Th49
    .= card n +` card m by Th24;
end;

theorem Th52:
  card(n*m) = card n *` card m
proof
  thus card(n*m) = card( n *^ m) by Th50
    .= card n *` card m by Th25;
end;

theorem Th53:
  for X,Y being finite set st X misses Y holds card (X \/ Y) = card X + card Y
proof
  let X,Y be finite set;
  assume X misses Y;
  then card card (X \/ Y) = card card X +` card card Y by Th48
    .= card (card X + card Y) by Th51;
  hence thesis by CARD_1:71;
end;

theorem Th54:
  for X being finite set st not x in X holds card (X \/ {x}) = card X + 1
proof
  let X be finite set;
A1: card {x} = 1 by CARD_1:50;
  assume not x in X;
  hence thesis by A1,Th53,ZFMISC_1:56;
end;

canceled 5;

theorem Th60:
  for X being set holds card X = 1 iff ex x st X = {x}
proof
  let X be set;
  card {0} = 1 by CARD_1:50;
  hence card X = 1 implies ex x st X = {x} by CARD_1:49;
  given x such that
A1: X = {x};
  thus thesis by A1,CARD_1:50;
end;

canceled;

theorem Th62:
  for X,Y being finite set holds card(X \/ Y) <= card X + card Y
proof
  let X,Y be finite set;
  card X = card card X & card Y = card card Y;
  then card X +` card Y = card(card X + card Y) by Th51;
  then
  card card(X \/ Y) = card(X \/ Y) & card (X \/ Y) c= card(card X + card Y
  ) by Th47;
  hence thesis by NAT_1:41;
end;

theorem Th63:
  for X,Y being finite set st Y c= X holds card (X \ Y) = card X - card Y
proof
  let X,Y be finite set;
  defpred P[set] means ex S being finite set st S = $1 & card (X \ S) = card X
  - card S;
  card X - 0 = card X & X \ {} = X;
  then
A1: P[{}] by CARD_1:47;
  assume
A2: Y c= X;
A3: for X1,Z being set st X1 in Y & Z c= Y & P[Z] holds P[Z \/ {X1}]
  proof
    let X1,Z be set such that
A4: X1 in Y and
A5: Z c= Y and
A6: P[Z] and
A7: not P[Z \/ {X1}];
A8: card {X1} = 1 by CARD_1:50;
A9: now
      assume X1 in Z;
      then {X1} c= Z by ZFMISC_1:37;
      then Z = Z \/ {X1} by XBOOLE_1:12;
      hence P[Z \/ {X1}] by A6;
    end;
    then
A10: X1 in X \ Z by A2,A4,A7,XBOOLE_0:def 5;
    then consider m being Nat such that
A11: card (X \ Z) = m+1 by NAT_1:6;
    reconsider Z1 = Z as finite set by A5;
A12: X \ Z, card (X \ Z) are_equipotent & X \ (Z \/ {X1}) = X \ Z \ {X1}
    by CARD_1:def 5,XBOOLE_1:41;
    card { X1} = 1 by CARD_1:50;
    then
A13: card Z1 + card {X1} = card (Z1 \/ {X1}) by A7,A9,Th54;
    m+1 = succ m by NAT_1:39;
    then m in m+1 & m = m+1 \ {m} by ORDINAL1:10,52;
    then X \ (Z \/ {X1}), m are_equipotent by A10,A11,A12,CARD_1:61;
    then card (X \ (Z \/ {X1})) = card X - card (Z1 \/ {X1}) by A6,A13,A11,
    A8,CARD_1:def 5;
    hence contradiction by A7;
  end;
A14: Y is finite;
  P[Y] from FINSET_1:sch 2(A14,A1,A3);
  hence thesis;
end;

theorem
  for X,Y being finite set holds card (X \/ Y) = card X + card Y - card
  (X /\ Y)
proof
  let X,Y be finite set;
  Y \ X = Y \ X /\ Y by XBOOLE_1:47;
  then
A1: card (Y \ X) = card Y - card (X /\ Y) by Th63,XBOOLE_1:17;
  card (X \/ (Y \ X)) = card X + card (Y \ X) by Th53,XBOOLE_1:79;
  hence thesis by A1,XBOOLE_1:39;
end;

theorem
  for X,Y being finite set holds card [:X,Y:] = card X * card Y
proof
  let X,Y be finite set;
  card card [:X,Y:] = card card X *` card card Y by Th14
    .= card (card X * card Y) by Th52;
  hence thesis by CARD_1:71;
end;

theorem :: GRAPH_5:1, AK, 21.02.2006
  for f being finite Function holds card rng f <= card dom f
proof
  let f be finite Function;
  reconsider X=rng f, Y=dom f as set;
  card X c= card Y by CARD_1:28;
  hence thesis by NAT_1:40;
end;

theorem
  for X,Y being finite set st X c< Y holds card X < card Y & card X in card Y
proof
  let X,Y be finite set;
  assume
A1: X c< Y;
  then X c= Y by XBOOLE_0:def 8;
  then
A2: Y = X \/ (Y\X) by XBOOLE_1:45;
  then
A3: card Y = card X + card (Y\X) by Th53,XBOOLE_1:79;
  then
A4: card X <= card Y by NAT_1:11;
  now
    assume card (Y\X) = 0;
    then Y \ X = {};
    hence contradiction by A1,A2;
  end;
  then card X <> card Y by A3;
  hence card X < card Y by A4,XXREAL_0:1;
  hence thesis by NAT_1:45;
end;

theorem
  (card X c= card Y or card X in card Y) & Y is finite implies X is finite
proof
  assume that
A1: card X c= card Y or card X in card Y and
A2: Y is finite;
  card X c= card Y by A1,ORDINAL1:def 2;
  hence thesis by A2;
end;

reserve x1,x2,x3,x4,x5,x6,x7,x8 for set;

theorem Th69:
  card {x1,x2} <= 2
proof
A1: {x1,x2} = {x1} \/ {x2} & 1+1 = 2 by ENUMSET1:41;
  card {x1} = 1 & card {x2} = 1 by CARD_1:50;
  hence thesis by A1,Th62;
end;

theorem Th70:
  card {x1,x2,x3} <= 3
proof
  card {x2,x3} <= 2 by Th69;
  then
A1: 1 + card {x2,x3} <= 1+2 by XREAL_1:9;
  card {x1} = 1 & {x1,x2,x3} = {x1} \/ {x2,x3} by CARD_1:50,ENUMSET1:42;
  then card {x1,x2,x3} <= 1 + card {x2,x3} by Th62;
  hence thesis by A1,XXREAL_0:2;
end;

theorem Th71:
  card {x1,x2,x3,x4} <= 4
proof
  card {x2,x3,x4} <= 3 by Th70;
  then
A1: 1 + card {x2,x3,x4} <= 1+3 by XREAL_1:9;
  card {x1} = 1 & {x1,x2,x3,x4} = {x1} \/ {x2,x3,x4} by CARD_1:50,ENUMSET1
  :44;
  then card {x1,x2,x3,x4} <= 1 + card {x2,x3,x4 } by Th62;
  hence thesis by A1,XXREAL_0:2;
end;

theorem Th72:
  card {x1,x2,x3,x4,x5} <= 5
proof
  card {x2,x3,x4,x5} <= 4 by Th71;
  then
A1: 1 + card {x2,x3,x4,x5} <= 1+4 by XREAL_1:9;
  card {x1} = 1 & {x1,x2,x3,x4,x5} = {x1} \/ {x2,x3,x4,x5} by CARD_1:50 ,
  ENUMSET1:47;
  then card {x1,x2,x3,x4,x5} <= 1 + card {x2,x3,x4,x5} by Th62;
  hence thesis by A1,XXREAL_0:2;
end;

theorem Th73:
  card {x1,x2,x3,x4,x5,x6} <= 6
proof
  card {x2,x3,x4,x5,x6} <= 5 by Th72;
  then
A1: 1 + card {x2,x3,x4,x5,x6} <= 1+5 by XREAL_1:9;
  card {x1} = 1 & {x1,x2,x3,x4,x5,x6} = {x1} \/ {x2,x3,x4,x5,x6} by CARD_1
  :50 ,ENUMSET1:51;
  then card {x1,x2,x3,x4,x5,x6} <= 1 + card {x2,x3,x4,x5,x6} by Th62;
  hence thesis by A1,XXREAL_0:2;
end;

theorem Th74:
  card {x1,x2,x3,x4,x5,x6,x7} <= 7
proof
  card {x2,x3,x4,x5,x6,x7} <= 6 by Th73;
  then
A1: 1 + card {x2,x3,x4,x5,x6,x7} <= 1+6 by XREAL_1:9;
  card {x1} = 1 & {x1,x2,x3,x4,x5,x6,x7} = {x1} \/ {x2,x3,x4,x5,x6,x7} by
  CARD_1:50,ENUMSET1:56;
  then card {x1,x2,x3,x4,x5,x6,x7} <= 1 + card {x2,x3,x4,x5,x6,x7} by
  Th62;
  hence thesis by A1,XXREAL_0:2;
end;

theorem
  card {x1,x2,x3,x4,x5,x6,x7,x8} <= 8
proof
  card {x2,x3,x4,x5,x6,x7,x8} <= 7 by Th74;
  then
A1: 1 + card {x2,x3,x4,x5,x6,x7,x8} <= 1+7 by XREAL_1:9;
  {x1,x2,x3,x4,x5,x6,x7,x8} = {x1} \/ {x2,x3,x4,x5,x6,x7,x8} & card {x1} =
  1 by CARD_1:50,ENUMSET1:62;
  then card {x1,x2,x3,x4,x5,x6,x7,x8} <= 1 + card {x2,x3,x4,x5,x6,x7,x8} by
  Th62;
  hence thesis by A1,XXREAL_0:2;
end;

theorem Th76:
  x1 <> x2 implies card {x1,x2} = 2
proof
A1: card {x1} = 1 & card {x2} = 1 by CARD_1:50;
A2: {x1,x2} = {x1} \/ {x2} & 1+1 = 2 by ENUMSET1:41;
  assume x1 <> x2;
  hence thesis by A1,A2,Th53,ZFMISC_1:17;
end;

theorem Th77:
  x1 <> x2 & x1 <> x3 & x2 <> x3 implies card {x1,x2,x3} = 3
proof
  assume x1 <> x2 & x1 <> x3 & x2 <> x3;
  then
A1: card {x1,x2} = 2 & not x3 in {x1,x2} by Th76,TARSKI:def 2;
  {x1,x2,x3} = {x1,x2} \/ {x3} by ENUMSET1:43;
  hence card {x1,x2,x3} = 2+1 by A1,Th54
    .= 3;
end;

theorem Th78:
  x1 <> x2 & x1 <> x3 & x1 <> x4 & x2 <> x3 & x2 <> x4 & x3 <> x4
  implies card {x1,x2,x3,x4} = 4
proof
  assume x1 <> x2 & x1 <> x3 & x1 <> x4 & x2 <> x3 & x2 <> x4 & x3 <> x4;
  then
A1: card {x1,x2,x3} = 3 & not x4 in {x1,x2,x3} by Th77,ENUMSET1:def 1;
  {x1,x2,x3,x4} = {x1,x2,x3} \/ {x4} by ENUMSET1:46;
  hence card {x1,x2,x3,x4} = 3+1 by A1,Th54
    .= 4;
end;

begin :: Addenda
:: from GROUP_3

theorem
  for X being finite set st card X = 2 ex x,y st x <> y & X = {x,y}
proof
  let X be finite set;
  assume
A1: card X = 2;
  then consider x being set such that
A2: x in X by CARD_1:47,XBOOLE_0:def 1;
  {x} c= X by A2,ZFMISC_1:37;
  then card(X \ {x}) = card X - card{x} by Th63
    .= 2 - 1 by A1,CARD_1:50;
  then consider y such that
A3: X \ {x} = {y} by Th60;
  take x,y;
  x in {x} by TARSKI:def 1;
  hence x <> y by A3,XBOOLE_0:def 5;
  thus X c= {x,y}
  proof
    let z be set;
    assume
A4: z in X;
    per cases;
    suppose
      z = x;
      hence thesis by TARSKI:def 2;
    end;
    suppose
      z <> x;
      then not z in {x} by TARSKI:def 1;
      then z in {y} by A3,A4,XBOOLE_0:def 5;
      then z = y by TARSKI:def 1;
      hence thesis by TARSKI:def 2;
    end;
  end;
  let z be set;
  assume z in {x,y};
  then
A5: z = x or z = y by TARSKI:def 2;
  y in X \ {x} by A3,TARSKI:def 1;
  hence thesis by A2,A5;
end;
 
:: from YELLOW_6, 2004.07.25

theorem
  for f being Function holds card rng f c= card dom f
proof
  let f be Function;
  rng f = f.:dom f by RELAT_1:146;
  hence thesis by Th2;
end;
 
:: from RLVECT_3

Lm5: now
  let n;
  assume
A1: for Z being finite set holds card Z = n & Z <> {} & (for X,Y st X in
  Z & Y in Z holds X c= Y or Y c= X) implies union Z in Z;
  let Z be finite set;
  assume that
A2: card Z = n + 1 and
A3: Z <> {} and
A4: for X,Y st X in Z & Y in Z holds X c= Y or Y c= X;
  consider y being Element of Z;
  per cases;
  suppose
    n = 0;
    then consider x such that
A5: Z = {x} by A2,Th60;
    union Z = x by A5,ZFMISC_1:31;
    hence union Z in Z by A5,TARSKI:def 1;
  end;
  suppose
A6: n <> 0;
    set Y = Z \ {y};
    reconsider Y as finite set;
    {y} c= Z by A3,ZFMISC_1:37;
    then
A7: card Y = (n + 1) - card{y} by A2,Th63
      .= n + 1 - 1 by CARD_1:50
      .= n;
    for a,b being set st a in Y & b in Y holds a c= b or b c= a by A4;
    then
A8: union Y in Y by A1,A6,A7,CARD_1:47;
    then
A9: union Y in Z;
    Z = (Z \ {y}) \/ {y}
    proof
      thus Z c= (Z \ {y}) \/ {y}
      proof
        let x;
        assume x in Z;
        then x in Z \ {y} or x in {y} by XBOOLE_0:def 5;
        hence thesis by XBOOLE_0:def 3;
      end;
      let x;
      assume x in (Z \ {y}) \/ {y};
      then
A10:  x in (Z \ {y}) or x in {y} by XBOOLE_0:def 3;
      {y} c= Z by A3,ZFMISC_1:37;
      hence thesis by A10;
    end;
    then
A11: union Z = union Y \/ union {y} by ZFMISC_1:96
      .= union Y \/ y by ZFMISC_1:31;
A12: y in Z by A3;
    y c= union Y or union Y c= y by A4,A8;
    hence union Z in Z by A9,A12,A11,XBOOLE_1:12;
  end;
end;

Lm6: for Z being finite set holds Z <> {} & (for X,Y st X in Z & Y in Z holds
X c= Y or Y c= X) implies union Z in Z
proof
  defpred P[Element of NAT] means for Z being finite set st card Z = $1 & Z <>
  {} & (for X,Y st X in Z & Y in Z holds X c= Y or Y c= X) holds union Z in Z;
  let Z be finite set;
A1: card Z = card Z;
A2: P[0];
A3: for k be Element of NAT st P[k] holds P[k+1] by Lm5;
  for n holds P[n] from NAT_1:sch 1(A2,A3);
  hence thesis by A1;
end;

theorem
  Z <> {} & Z is finite & (for X,Y st X in Z & Y in Z holds X c= Y or Y
  c= X) implies union Z in Z by Lm6;

theorem
  x1,x2,x3,x4,x5 are_mutually_different implies card {x1,x2,x3,x4,x5} = 5
proof
A1: {x1,x2,x3,x4,x5} = {x1,x2,x3,x4} \/ {x5} by ENUMSET1:50;
  assume
A2: x1,x2,x3,x4,x5 are_mutually_different;
  then
A3: x3 <> x5 & x4 <> x5 by ZFMISC_1:def 7;
A4: x2 <> x4 & x3 <> x4 by A2,ZFMISC_1:def 7;
A5: x1 <> x4 & x2 <> x3 by A2,ZFMISC_1:def 7;
  x1 <> x5 & x2 <> x5 by A2,ZFMISC_1:def 7;
  then
A6: not x5 in {x1,x2,x3,x4} by A3,ENUMSET1:def 2;
  x1 <> x2 & x1 <> x3 by A2,ZFMISC_1:def 7;
  then card {x1,x2,x3,x4} = 4 by A5,A4,Th78;
  hence card {x1,x2,x3,x4,x5} = 4+1 by A6,A1,Th54
    .= 5;
end;
 
:: from MATRIX_1, 2007.07.22, A.T, generalized

theorem
  for M1, M2 being set st card M1 = 0 & card M2 = 0 holds M1 = M2
proof
  let M1, M2 be set;
  assume that
A1: card M1 = {} and
A2: card M2 = {};
  M1 = {} by A1;
  hence thesis by A2;
end;
 
:: missing, 2007.06.14, A.T.

registration
  let x,y;
  cluster [x,y] -> non natural;
  coherence
  proof
    assume [x,y] is natural;
    then reconsider n = [x,y] as natural number;
    card n <= 2 by Th69;
    then
A1: n <= 2 by CARD_1:def 5;
    per cases by A1,NAT_1:27;
    suppose
      n = 0;
      hence contradiction;
    end;
    suppose
      n = 1;
      hence contradiction by CARD_1:87,ZFMISC_1:8;
    end;
    suppose
      n = 2;
      hence contradiction by CARD_1:88,ZFMISC_1:10;
    end;
  end;
end;
