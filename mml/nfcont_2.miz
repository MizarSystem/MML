:: The Uniform Continuity of Functions on Normed Linear Spaces
::  by Takaya Nishiyama , Artur Korni{\l}owicz and Yasunari Shidama
::
:: Received April 6, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies PRE_TOPC, FCONT_1, FCONT_2, ALI2, RLVECT_1, FUNCT_1, FINSEQ_4,
      FUNCT_4, ARYTM_1, ARYTM_3, RELAT_1, ABSVALUE, ORDINAL2, SEQ_1, FUNCOP_1,
      SEQ_2, SEQM_3, SQUARE_1, COMPTS_1, BOOLE, SEQ_4, PARTFUN1, PARTFUN2,
      NORMSP_1, POWER, LOPBAN_1, RSSPACE3, ARYTM, FRECHET2;
 notations TARSKI, XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, RELSET_1, PARTFUN1,
      FUNCT_2, PARTFUN2, FUNCOP_1, FINSEQ_4, STRUCT_0, PRE_TOPC, ORDINAL1,
      COMPLEX1, XCMPLX_0, XREAL_0, NUMBERS, REAL_1, NAT_1, ALG_1, RLVECT_1,
      SQUARE_1, SEQ_1, SEQ_2, SEQM_3, SEQ_4, ABIAN, POWER, VFUNCT_1, NORMSP_1,
      BHSP_3, LOPBAN_1, LOPBAN_3, RSSPACE3, NFCONT_1, XXREAL_0;
 constructors REAL_1, SQUARE_1, SEQ_2, SERIES_1, SEQ_4, PREPOWER, PARTFUN2,
      FINSEQ_4, VFUNCT_1, ABIAN, RSSPACE3, LOPBAN_3, NFCONT_1, NAT_1, BHSP_3,
      XXREAL_0;
 registrations RELSET_1, STRUCT_0, ORDINAL2, XREAL_0, MEMBERED, FUNCT_1,
      SEQM_3;
 definitions RLVECT_1;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;
 theorems TARSKI, ABSVALUE, XBOOLE_1, REAL_1, REAL_2, RLVECT_1, XCMPLX_1,
      FUNCOP_1, SEQ_1, SEQ_2, SERIES_1, NAT_1, POWER, SQUARE_1, RELAT_1,
      RELSET_1, FUNCT_1, FINSEQ_4, VFUNCT_1, LOPBAN_1, NORMSP_1, SEQ_4,
      FUNCT_2, RSSPACE3, FUNCT_7, LOPBAN_3, PARTFUN2, NFCONT_1, BHSP_3,
      XREAL_1, COMPLEX1;
 schemes SEQ_1, RECDEF_1, NAT_1, FUNCT_2;

begin :: Uniform Continuity of Functions on Normed Linear Spaces

 reserve n,m,k for Nat,
         x,X,X1,Z,Z1 for set,
         s,g,r,p for Real,
         S,T for RealNormSpace,
         f,f1,f2 for PartFunc of S, T,
         s1,s2,q1 for sequence of S,
         x0,x1,x2 for Point of S,
         Y for Subset of S;

definition let X,S,T;
           let f;
  pred f is_uniformly_continuous_on X means :Def1:
  X c= dom f & for r st 0<r ex s st 0<s & for x1,x2 st x1 in X & x2 in X &
  ||.x1 - x2.|| < s holds ||. f/.x1 - f/.x2 .|| < r;
end;

definition let X,S;
           let f be PartFunc of the carrier of S,REAL;
  pred f is_uniformly_continuous_on X means :Def2:
  X c= dom f & for r st 0<r ex s st 0<s & for x1,x2 st x1 in X & x2 in X &
  ||.x1 - x2.|| < s holds abs(f/.x1 - f/.x2) < r;
end;

theorem Th1:
f is_uniformly_continuous_on X & X1 c= X implies
f is_uniformly_continuous_on X1
proof assume A1: f is_uniformly_continuous_on X & X1 c= X;
  then X c= dom f by Def1; hence X1 c= dom f by A1,XBOOLE_1:1;
 let r; assume 0<r; then consider s such that A2: 0<s &
  for x1,x2 st x1 in X & x2 in X & ||.x1-x2.||<s holds ||.f/.x1-f/.x2.||<r
  by A1,Def1;
 take s; thus 0<s by A2;
 let x1,x2; assume x1 in X1 & x2 in X1
 & ||.x1-x2.||<s; hence thesis by A1,A2;
end;

theorem
  f1 is_uniformly_continuous_on X & f2 is_uniformly_continuous_on X1 implies
f1+f2 is_uniformly_continuous_on X/\X1
proof assume A1: f1 is_uniformly_continuous_on X &
                 f2 is_uniformly_continuous_on X1;
 A2: X /\ X1 c= X & X /\ X1 c= X1 by XBOOLE_1:17;
 then A3: f1 is_uniformly_continuous_on X /\ X1 by A1,Th1;
 then A4: X /\ X1 c= dom f1 by Def1;
 A5: f2 is_uniformly_continuous_on X /\ X1 by A1,A2,Th1;
 then X /\ X1 c= dom f2 by Def1;
 then X /\ X1 c= dom f1 /\ dom f2 by A4,XBOOLE_1:19;
 hence A6: X /\ X1 c= dom (f1+f2) by VFUNCT_1:def 1;
 let r; assume 0<r; then A7: 0<r/2 by SEQ_2:3;
 then consider s such that
 A8: 0<s & for x1,x2 st x1 in X /\ X1 & x2 in X /\X1 &
  ||.x1-x2.||<s holds ||.f1/.x1-f1/.x2.||<r/2 by A3,Def1;
 consider g such that A9: 0<g
     & for x1,x2 st x1 in X /\ X1 & x2 in X /\ X1 &
  ||.x1-x2.||<g holds ||.f2/.x1-f2/.x2.||<r/2 by A5,A7,Def1;
 take p=min(s,g);
 thus 0<p by A8,A9,SQUARE_1:38;
 A10: p <= s & p <= g by SQUARE_1:35;
 let x1,x2; assume A11: x1 in X/\X1 & x2 in X/\X1 & ||.x1-x2.||<p;
 then x1 in X/\X1 & x2 in X/\X1 & ||.x1-x2.||<s by A10,XREAL_1:2;
 then A12: ||.f1/.x1-f1/.x2.||<r/2 by A8;
   x1 in X/\X1 & x2 in X/\X1 & ||.x1-x2.||<g by A10,A11,XREAL_1:2;
 then ||.f2/.x1-f2/.x2.||<r/2 by A9;
 then A13: ||.f1/.x1-f1/.x2.||+||.f2/.x1-f2/.x2.||<r/2+r/2 by A12,XREAL_1:10;
 A14:  ||.(f1+f2)/.x1-(f1+f2)/.x2.||
 = ||.f1/.x1 + f2/.x1-(f1+f2)/.x2.|| by A6,A11,VFUNCT_1:def 1
  .= ||.f1/.x1 + f2/.x1 - (f1/.x2+f2/.x2).|| by A6,A11,VFUNCT_1:def 1
  .= ||.f1/.x1 + (f2/.x1 - (f1/.x2+f2/.x2)).|| by RLVECT_1:def 6
  .= ||.f1/.x1 + (f2/.x1 - f1/.x2-f2/.x2).|| by RLVECT_1:41
  .= ||.f1/.x1 + (-f1/.x2 + f2/.x1-f2/.x2).||
  .= ||.f1/.x1 + (-f1/.x2 + (f2/.x1-f2/.x2)).|| by RLVECT_1:def 6
  .= ||.f1/.x1 +- f1/.x2 + (f2/.x1-f2/.x2).|| by RLVECT_1:def 6
  .= ||.f1/.x1 - f1/.x2 + (f2/.x1-f2/.x2).||;
     ||.f1/.x1 - f1/.x2 + (f2/.x1-f2/.x2).||
         <= ||.f1/.x1-f1/.x2.|| + ||.f2/.x1-f2/.x2.||
   by NORMSP_1:def 2;
 hence thesis by A13,A14,XREAL_1:2;
end;

theorem
  f1 is_uniformly_continuous_on X & f2 is_uniformly_continuous_on X1 implies
f1-f2 is_uniformly_continuous_on X/\X1
proof assume A1: f1 is_uniformly_continuous_on X &
                 f2 is_uniformly_continuous_on X1;
 A2: X /\ X1 c= X & X /\ X1 c= X1 by XBOOLE_1:17;
 then A3: f1 is_uniformly_continuous_on X /\ X1 by A1,Th1;
 then A4: X /\ X1 c= dom f1 by Def1;
 A5: f2 is_uniformly_continuous_on X /\ X1 by A1,A2,Th1;
 then X /\ X1 c= dom f2 by Def1;
 then X /\ X1 c= dom f1 /\ dom f2 by A4,XBOOLE_1:19;
 hence A6: X /\ X1 c= dom (f1-f2) by VFUNCT_1:def 2;
 let r; assume 0<r; then A7: 0<r/2 by SEQ_2:3;
 then consider s such that
 A8: 0<s & for x1,x2 st x1 in X /\ X1 & x2 in X /\X1 &
  ||.x1-x2.||<s holds ||.f1/.x1-f1/.x2.||<r/2 by A3,Def1;
 consider g such that A9: 0<g & for x1,x2 st x1 in X /\ X1 & x2 in X /\ X1 &
  ||.x1-x2.||<g holds ||.f2/.x1-f2/.x2.||<r/2 by A5,A7,Def1;
 take p=min(s,g);
 thus 0<p by A8,A9,SQUARE_1:38;
 A10: p <= s & p <= g by SQUARE_1:35;
 let x1,x2; assume A11: x1 in X/\X1 & x2 in X/\X1 & ||.x1-x2.||<p;
 then x1 in X/\X1 & x2 in X/\X1 & ||.x1-x2.||<s by A10,XREAL_1:2;
 then A12: ||.f1/.x1-f1/.x2.||<r/2 by A8;
   x1 in X/\X1 & x2 in X/\X1 & ||.x1-x2.||<g by A10,A11,XREAL_1:2;
 then ||.f2/.x1-f2/.x2.||<r/2 by A9;
 then A13: ||.f1/.x1-f1/.x2.||+||.f2/.x1-f2/.x2.||<r/2+r/2 by A12,XREAL_1:10;
 A14: ||.(f1-f2)/.x1-(f1-f2)/.x2.||
 = ||.f1/.x1 - f2/.x1-(f1-f2)/.x2.|| by A6,A11,VFUNCT_1:def 2
  .= ||.f1/.x1 - f2/.x1 - (f1/.x2-f2/.x2).|| by A6,A11,VFUNCT_1:def 2
  .= ||.f1/.x1 - (f2/.x1 + (f1/.x2-f2/.x2)).|| by RLVECT_1:41
  .= ||.f1/.x1 - (f1/.x2 + f2/.x1-f2/.x2).|| by RLVECT_1:def 6
  .= ||.f1/.x1 - (f1/.x2 + (f2/.x1-f2/.x2)).|| by RLVECT_1:def 6
  .= ||.f1/.x1 - f1/.x2 - (f2/.x1-f2/.x2).|| by RLVECT_1:41;
   ||.f1/.x1 - f1/.x2 - (f2/.x1-f2/.x2).||
   <= ||.f1/.x1-f1/.x2.|| + ||.f2/.x1-f2/.x2.|| by NORMSP_1:7;
 hence thesis by A13,A14,XREAL_1:2;
end;

theorem Th4:
f is_uniformly_continuous_on X implies p(#)f is_uniformly_continuous_on X
proof assume A1: f is_uniformly_continuous_on X;
 then X c= dom f by Def1;
 hence A2: X c= dom (p(#)f) by VFUNCT_1:def 4;
   now per cases;
 suppose A3: p=0;
  let r; assume A4: 0<r;
  then consider s such that A5: 0<s & for x1,x2 st x1 in X & x2 in X &
   ||.x1-x2.||<s holds ||.f/.x1-f/.x2.||<r by A1,Def1;
  take s; thus 0<s by A5;
  let x1,x2; assume A6: x1 in X & x2 in X & ||.x1-x2.||<s;
  then ||.(p(#)f)/.x1-(p(#)f)/.x2.||
  = ||.p*(f/.x1)-(p(#)f)/.x2.|| by A2,VFUNCT_1:def 4
  .= ||.0.T -(p(#)f)/.x2.|| by A3,RLVECT_1:23
  .= ||.0.T - p*(f/.x2).|| by A2,A6,VFUNCT_1:def 4
  .= ||.0.T - 0.T.|| by A3,RLVECT_1:23
  .= ||.0.T .|| by RLVECT_1:26
  .= 0 by NORMSP_1:def 2;
   hence ||.(p(#)f)/.x1-(p(#)f)/.x2.|| <r by A4;
  end;
  suppose
 A7: p<>0; then A8: 0<abs(p) by COMPLEX1:133;
 A9: 0 <> abs(p) by A7,COMPLEX1:133;
  let r; assume 0<r; then 0 < r/abs(p) by A8,XREAL_1:141;
  then consider s such that A10: 0<s & for x1,x2 st x1 in X & x2 in X &
   ||.x1-x2.||<s holds ||.f/.x1-f/.x2.||<r/abs(p) by A1,Def1;
  take s; thus 0<s by A10;
  let x1,x2; assume A11: x1 in X & x2 in X & ||.x1-x2.||<s;
  then A12: ||.(p(#)f)/.x1-(p(#)f)/.x2.||
  = ||.p*(f/.x1)-(p(#)f)/.x2.|| by A2,VFUNCT_1:def 4
   .= ||.p*(f/.x1) - p*(f/.x2).|| by A2,A11,VFUNCT_1:def 4
   .= ||.p*(f/.x1 - f/.x2).|| by RLVECT_1:48
   .= abs(p)*||.f/.x1 - f/.x2.|| by NORMSP_1:def 2;
    ||.f/.x1-f/.x2.||<r/abs(p) by A10,A11;
  then abs(p)*||.f/.x1-f/.x2.||<r/abs(p)*abs(p) by A8,XREAL_1:70;
  hence ||.(p(#)f)/.x1-(p(#)f)/.x2.|| <r by A9,A12,XCMPLX_1:88;
 end;
 end;
 hence thesis;
end;

theorem
  f is_uniformly_continuous_on X implies -f is_uniformly_continuous_on X
proof assume A1: f is_uniformly_continuous_on X;
   -f = (-1)(#)f by VFUNCT_1:29;
 hence thesis by A1,Th4;
end;

theorem
  f is_uniformly_continuous_on X implies ||.f .|| is_uniformly_continuous_on X
proof assume A1: f is_uniformly_continuous_on X;
 then X c= dom f by Def1;
 hence A2: X c= dom (||.f.||) by NFCONT_1:def 2;
  let r; assume 0<r;
  then consider s such that A3: 0<s & for x1,x2 st x1 in X & x2 in X &
   ||.x1-x2.||<s holds ||.f/.x1-f/.x2.||<r by A1,Def1;
  take s; thus 0<s by A3;
  let x1,x2; assume A4: x1 in X & x2 in X & ||.x1-x2.||<s;
  then
  abs((||.f.||)/.x1-(||.f.||)/.x2)
   =abs((||.f.||).x1-(||.f.||)/.x2)  by A2,FINSEQ_4:def 4
  .=abs((||.f.||).x1-(||.f.||).x2)  by A2,A4,FINSEQ_4:def 4
  .= abs(||.f/.x1.||-(||.f.||).x2)  by A2,A4,NFCONT_1:def 2
   .= abs(||.f/.x1.|| - ||.f/.x2.||)
   by A2,A4,NFCONT_1:def 2;
  then A5: abs((||.f.||)/.x1-(||.f.||)/.x2)
  <= ||.f/.x1-f/.x2.|| by NORMSP_1:13;
    ||.f/.x1-f/.x2.||<r by A3,A4;
    hence thesis by A5,XREAL_1:2;
end;

theorem Th7:
f is_uniformly_continuous_on X implies f is_continuous_on X
proof assume A1: f is_uniformly_continuous_on X;
then A2: X c= dom f & for r st 0<r ex s st 0<s & for x1,x2 st x1 in X & x2 in X
& ||.x1 - x2.|| < s holds ||.f/.x1 - f/.x2.||< r by Def1;
   now let x0;
       let r be Real;
  assume A3: x0 in X & 0<r;
  then consider s be Real such that
A4: 0<s & for x1,x2 st x1 in X & x2 in X &
    ||.x1 -x2.|| < s holds ||.f/.x1 - f/.x2.||< r by A1,Def1;
  take s; thus 0<s by A4;
  let x1;
  assume x1 in X & ||.x1-x0.|| < s;
  hence ||.f/.x1 - f/.x0.|| < r by A3,A4;
 end;
 hence f is_continuous_on X by A2,NFCONT_1:26;
end;

theorem Th8:
for f be PartFunc of the carrier of S, REAL
st  f is_uniformly_continuous_on X holds
 f is_continuous_on X
proof
let f be PartFunc of the carrier of S, REAL;
assume A1: f is_uniformly_continuous_on X;
then A2: X c= dom f & for r st 0<r ex s st 0<s & for x1,x2 st x1 in X & x2 in X
& ||.x1 - x2.|| < s holds abs(f/.x1 - f/.x2)< r by Def2;
   now let x0;
       let r be Real;
  assume A3: x0 in X & 0<r;
  then consider s be Real such that
A4: 0<s & for x1,x2 st x1 in X & x2 in X &
    ||.x1 -x2.|| < s holds abs(f/.x1 - f/.x2)< r by A1,Def2;
  take s; thus 0<s by A4;
  let x1;
  assume x1 in X & ||.x1-x0.|| < s;
  hence abs(f/.x1 - f/.x0) < r by A3,A4;
 end;
 hence f is_continuous_on X by A2,NFCONT_1:27;
end;

theorem Th9:
f is_Lipschitzian_on X implies f is_uniformly_continuous_on X
proof assume A1: f is_Lipschitzian_on X;
 hence X c= dom f by NFCONT_1:def 13;
 consider r be Real such that A2: 0<r &
 for x1,x2 st x1 in X & x2 in X holds
    ||.f/.x1-f/.x2.||<=r*||.x1-x2.|| by A1,NFCONT_1:def 13;
 let p be Real such that A3: 0<p;
 take s=p/r; thus 0<s by A2,A3,XREAL_1:141;
 let x1,x2; assume A4:  x1 in X & x2 in X & ||.x1 -x2.|| < s;
 then A5: r*||.x1 -x2.|| < s*r by A2,XREAL_1:70;
   ||.f/.x1-f/.x2.||<=r*||.x1-x2.|| by A2,A4;
 then ||.f/.x1-f/.x2.||<p/r*r by A5,XREAL_1:2;
 hence thesis by A2,XCMPLX_1:88;
end;

theorem
for f, Y st Y is compact & f is_continuous_on Y holds
f is_uniformly_continuous_on Y
proof let f, Y such that A1: Y is compact and
 A2: f is_continuous_on Y;
 A3: Y c= dom f by A2,NFCONT_1:def 11;
 assume not thesis;
 then consider r such that A4: 0<r and
 A5: for s st 0<s ex x1,x2 st x1 in Y & x2 in Y & ||.x1 -x2.|| < s &
     not ||.f/.x1 - f/.x2.||< r by A3,Def1;
 A6: now let n;
        0 <> n+1 by NAT_1:21; then
        0 < n+1 by NAT_1:19;
      hence 0 < 1/(n+1) by XREAL_1:141;
     end;
 defpred P[Nat,Point of S] means
 $2 in Y & ex x2 st x2 in Y
 & ||.$2-x2.||<1/($1+1) & not ||.f/.($2)-f/.x2.|| < r;
 A7: now let n;
    0 < 1/(n+1) by A6;
  then consider x1 such that
  A8: ex x2 st x1 in Y & x2 in Y & ||.x1 -x2.|| < 1/(n+1) &
  not ||.f/.x1 - f/.x2.||< r by A5;
  take x1;
  thus P[n,x1] by A8;
 end;
 consider s1 such that A9: for n holds P[n,s1.n]
 from FUNCT_2:sch 3(A7);
 defpred P[Nat, Point of S] means
   $2 in Y & ||.s1.$1-$2.|| < 1/($1+1)
   & not ||.f/.(s1.$1)-f/.$2.|| < r;
A10: for n ex x2 st P[n,x2] by A9;
 consider s2 such that A11:
 for n holds P[n,s2.n] from FUNCT_2:sch 3(A10);
  A12: now let p be Real such that A13: 0<p;
   consider k be Nat such that A14: p" < k by SEQ_4:10;
   A15: 0 < p" by A13,XREAL_1:124;
    A16: k < k+1 by NAT_1:38;
   then p" < k+1 by A14,XREAL_1:2;
   then 1/(k+1) < 1/p" by A15,XREAL_1:78;
   then A17: 1/(k+1) < p by XCMPLX_1:218;
   take k; let m such that A18: k <= m;
   A19: 0 < k+1 by A14,A15,A16; k+1 <= m+1 by A18,XREAL_1:8;
   then A20: 1/(m+1) <= 1/(k+1) by A19,XREAL_1:120;
   A21: ||.(s1-s2).m - 0.S.|| = ||.s1.m - s2.m - 0.S.|| by NORMSP_1:def 6
                                     .= ||.s1.m - s2.m .|| by RLVECT_1:26;
     ||.s1.m - s2.m.|| < 1/(m+1) by A11;
   then ||.s1.m - s2.m.|| < 1/(k+1) by A20,XREAL_1:2;
   hence ||.(s1-s2).m - 0.S.|| < p by A17,A21,XREAL_1:2;
  end;
 then A22: s1-s2 is convergent by NORMSP_1:def 9;
 then A23: lim (s1-s2) = 0.S by A12,NORMSP_1:def 11;
   now let x; assume x in rng s1;
   then ex n st x=s1.n by NFCONT_1:6;
   hence x in Y by A9;
 end;
 then A24: rng s1 c= Y by TARSKI:def 3;
 then consider q1 such that A25: q1 is subsequence of s1 & q1 is convergent &
 (lim q1) in Y by A1,NFCONT_1:def 4;
 A26: f|Y is_continuous_in (lim q1) by A2,A25,NFCONT_1:def 11;
 consider Ns1 be increasing Seq_of_Nat such that
 A27: q1=s1*Ns1 by A25,BHSP_3:def 4;
 A28: (s1-s2)*Ns1 is subsequence of s1-s2 by BHSP_3:def 4;
 then A29: (s1-s2)*Ns1 is convergent by A22,LOPBAN_3:11;
 A30: lim ((s1-s2)*Ns1) = 0.S  by A22,A23,A28,LOPBAN_3:12;
 set q2 = q1 - (s1-s2)*Ns1;
 A31: q2 is convergent by A25,A29,NORMSP_1:35;
 A32: lim q2 = lim q1 - 0.S by A25,A29,A30,NORMSP_1:43
           .= lim q1 by RLVECT_1:26;
   now let n;
  thus q2.n = (s1*Ns1).n - ((s1-s2)*Ns1).n
  by A27,NORMSP_1:def 6
   .= s1.(Ns1.n) - ((s1-s2)*Ns1).n by LOPBAN_3:7
   .= s1.(Ns1.n) - (s1-s2).(Ns1.n) by LOPBAN_3:7
   .= s1.(Ns1.n) - (s1.(Ns1.n)-s2.(Ns1.n)) by NORMSP_1:def 6
   .= (s1.(Ns1.n) - s1.(Ns1.n)) + s2.(Ns1.n) by RLVECT_1:43
   .= 0.S  + s2.(Ns1.n) by RLVECT_1:28
   .= s2.(Ns1.n) by RLVECT_1:10
   .= (s2*Ns1).n by LOPBAN_3:7;
 end;
then A33: q2 = s2*Ns1 by FUNCT_2:113;
then A34: q2 is subsequence of s2 by BHSP_3:def 4;
  now let x; assume x in rng s2; then ex n st
 x=s2.n by NFCONT_1:6; hence x in Y by A11;
 end;
 then A35: rng s2 c= Y by TARSKI:def 3;
   rng q1 c= rng s1 by A25,NFCONT_1:7;
 then A36: rng q1 c= Y by A24,XBOOLE_1:1;
 then rng q1 c= dom f by A3,XBOOLE_1:1;
 then rng q1 c= dom f /\ Y by A36,XBOOLE_1:19;
 then A37: rng q1 c= dom (f|Y) by RELAT_1:90;
 then A38: (f|Y)*q1 is convergent
 & (f|Y)/.(lim q1)=lim((f|Y)*q1) by A25,A26,NFCONT_1:def 9;
   rng q2 c= rng s2 by A34,NFCONT_1:7;
 then A39: rng q2 c= Y by A35,XBOOLE_1:1;
 then rng q2 c= dom f by A3,XBOOLE_1:1;
 then rng q2 c= dom f /\ Y by A39,XBOOLE_1:19;
 then A40: rng q2 c= dom (f|Y) by RELAT_1:90;
 then A41: (f|Y)*q2 is convergent & (f|Y)/.(lim q1) = lim ((f|Y)*q2)
      by A26,A31,A32,NFCONT_1:def 9;
 then A42: (f|Y)*q1 - (f|Y)*q2 is convergent by A38,NORMSP_1:35;
 A43: lim ((f|Y)*q1 - (f|Y)*q2) = (f|Y)/.(lim q1) - (f|Y)/.(lim q1)
      by A38,A41,NORMSP_1:43
     .= 0.T by RLVECT_1:28;
   now let n;
  consider k be Nat such that
A44: for m st k<=m holds
   ||.((f|Y)*q1 - (f|Y)*q2).m-0.T .||<r
  by A4,A42,A43,NORMSP_1:def 11;
  A45: ||.((f|Y)*q1 - (f|Y)*q2).k - 0.T.||<r by A44;
  A46: q1.k in rng q1 by NFCONT_1:6;
  A47: q2.k in rng q2 by NFCONT_1:6;
    ||.((f|Y)*q1 - (f|Y)*q2).k - 0.T .||
   = ||.((f|Y)*q1 - (f|Y)*q2).k  .|| by RLVECT_1:26
  .= ||.((f|Y)*q1).k - ((f|Y)*q2).k.|| by NORMSP_1:def 6
  .= ||.(f|Y)/.(q1.k) - ((f|Y)*q2).k.|| by A37,NFCONT_1:8
  .= ||.(f|Y)/.(q1.k) - (f|Y)/.(q2.k) .|| by A40,NFCONT_1:8
  .= ||.f/.(q1.k) - (f|Y)/.(q2.k).|| by A37,A46,PARTFUN2:32
  .= ||.f/.(q1.k) - f/.(q2.k).|| by A40,A47,PARTFUN2:32
  .= ||.f/.(s1.(Ns1.k)) - f/.((s2*Ns1).k).|| by A27,A33,LOPBAN_3:7
  .= ||.f/.(s1.(Ns1.k)) - f/.(s2.(Ns1.k)).|| by LOPBAN_3:7;
  hence contradiction by A11,A45;
 end; hence contradiction;
end;

theorem
  Y c= dom f & Y is compact & f is_uniformly_continuous_on Y implies
 f.:Y is compact
proof assume A1: Y c= dom f & Y is compact & f is_uniformly_continuous_on Y;
 then f is_continuous_on Y by Th7;
 hence thesis by A1,NFCONT_1:39;
end;

theorem
  for f be PartFunc of the carrier of S, REAL
  for Y st Y <> {} & Y c= dom f & Y is compact &
 f is_uniformly_continuous_on Y ex x1,x2 st x1 in Y & x2 in Y &
  f/.x1 = upper_bound (f.:Y) & f/.x2 = lower_bound (f.:Y)
proof
let f be PartFunc of the carrier of S, REAL;
let Y;
assume A1: Y <> {} & Y c= dom f & Y is compact &
                         f is_uniformly_continuous_on Y;
 then f is_continuous_on Y by Th8;
 hence thesis by A1,NFCONT_1:44;
end;

theorem
  X c= dom f & f is_constant_on X implies f is_uniformly_continuous_on X
proof assume X c= dom f & f is_constant_on X;
 then f is_Lipschitzian_on X by NFCONT_1:50;
 hence thesis by Th9;
end;

begin :: Contraction Mapping Principle on Normed Linear Spaces

definition let M be RealBanachSpace;
  mode contraction of M -> Function of the carrier of M, the carrier of M
    means :Def3:
  ex L being Real st 0 < L & L < 1 &
    for x,y being Point of M holds
      ||.it.x - it.y.|| <=L*||.x - y.||;
existence
proof
 consider x being Point of M;
   (the carrier of M) --> x is Function of the carrier of M, the carrier of M
 proof
A1: dom ((the carrier of M) --> x) = the carrier of M by FUNCOP_1:19;
 rng ((the carrier of M) --> x) c= {x} by FUNCOP_1:19;
 then rng ((the carrier of M) --> x) c= the carrier of M by XBOOLE_1:1;
  hence thesis by A1,FUNCT_2:def 1,RELSET_1:11;
 end;
 then reconsider f = (the carrier of M) --> x as Function of the carrier of M,
   the carrier of M;
 take f, 1/2;
 thus 0<1/2 & 1/2<1;
 let z,y be Point of M;
   f.z=x & f.y=x by FUNCOP_1:13;
 then A2: ||.f.z-f.y.|| = 0 by NORMSP_1:10;
   ||.z-y.||>=0 by NORMSP_1:8;
 hence ||.f.z-f.y.||<=(1/2)*||.z-y.|| by A2,REAL_2:121;
end;
end;

Lm1:
  (for X be RealNormSpace
   for x, y be Point of X holds ||.x-y.|| = 0 iff x = y) &
  (for X be RealNormSpace
   for x, y be Point of X holds ||.x-y.|| <> 0 iff x <> y) &
  (for X be RealNormSpace
   for x, y be Point of X holds ||.x-y.|| > 0 iff x <> y) &
  (for X be RealNormSpace
   for x, y be Point of X holds ||.x-y.|| = ||.y-x.||) &
  (for X be RealNormSpace
   for x, y, z be Point of X
   for e be Real st e > 0 holds
   ((||.x-z.|| <e/2 & ||.z-y.|| <e/2) implies ||.x-y.|| <e)) &
  (for X be RealNormSpace
   for x,y,z be Point of X
   for e be Real st e>0 holds
   ((||.x-z.|| <e/2 & ||.y-z.|| <e/2) implies ||.x-y.|| <e)) &
  (for X be RealNormSpace
   for x be Point of X
     st (for e be Real st e>0 holds ||.x.|| <e)
   holds x=0.X  )  &
  (for X be RealNormSpace
   for x,y be Point of X
     st (for e be Real st e>0 holds ||.x-y.|| <e) holds x=y)
proof
  A1: for X be RealNormSpace
   for x, y be Point of X holds ||.x-y.|| > 0 iff x <> y
  proof
    let X be RealNormSpace;
    let x, y be Point of X;
    0 < ||.x-y.|| implies x-y <> 0.X by NORMSP_1:def 2;
    hence 0 < ||.x-y.|| implies x <> y by RLVECT_1:28;
    now assume x <> y;
      then 0 <> ||.x-y.|| by NORMSP_1:15;
      hence 0 < ||.x-y.|| by NORMSP_1:8;
    end;
    hence x <> y implies 0 < ||.x-y.||;
  end;
  A2: for X be RealNormSpace
   for x, y be Point of X holds ||.x-y.|| = ||.y-x.||
  proof
    let X be RealNormSpace;
    let x, y be Point of X;
    thus ||.x-y.|| = ||.-(x-y).|| by NORMSP_1:6
     .= ||.y+(-x).|| by RLVECT_1:47
     .= ||.y-x.||;
  end;
  A3: for X be RealNormSpace
   for x, y, z be Point of X
   for e be Real st e > 0 holds
   ((||.x-z.|| < e/2 & ||.z-y.|| < e/2)  implies ||.x-y.|| <e)
  proof
    let X be RealNormSpace;
    let x, y, z be Point of X;
    let e be Real such that e > 0;
    assume ||.x-z.|| < e/2 & ||.z-y.|| < e/2;
      then A4: ||.x-z.|| + ||.z-y.|| < e/2 + e/2 by XREAL_1:10;
    ||.x-y.|| <= ||.x-z.|| + ||.z-y.|| by NORMSP_1:14;
    then ||.x-y.|| + (||.x-z.|| + ||.z-y.||)
      < (||.x-z.|| + ||.z-y.||) + e by A4,XREAL_1:10;
    then ||.x-y.|| + (||.x-z.|| + ||.z-y.||) +- (||.x-z.|| + ||.z-y.||)
      < e + (||.x-z.|| + ||.z-y.||) +- (||.x-z.|| + ||.z-y.||)
      by XREAL_1:10;
    hence thesis;
  end;
  A5: for X be RealNormSpace
   for x, y, z be Point of X
   for e be Real st e > 0 holds
   ((||.x-z.|| < e/2 & ||.y-z.|| < e/2) implies ||.x-y.|| < e)
  proof
    let X be RealNormSpace;
    let x, y, z be Point of X;
    let e be Real such that A6: e > 0;
    assume A7: ||.x-z.|| < e/2 & ||.y-z.|| < e/2;
    then ||.-(y-z).|| < e/2 by NORMSP_1:6;
    then ||.z+-y.|| < e/2 by RLVECT_1:47;
    then ||.z-y.|| < e/2;
    hence thesis by A3,A6,A7;
  end;
  A8: for X be RealNormSpace
   for x be Point of X
     st (for e be Real st e>0 holds ||.x.|| <e)
   holds x=0.X
  proof
    let X be RealNormSpace;
    let x be Point of X such that
    A9: for e be Real st e > 0 holds ||.x.|| <e;
    now assume
      A10: x<>0.X;
       0<>  ||.x.|| by A10,NORMSP_1:def 2;
     then 0< ||.x.|| by NORMSP_1:8;
     hence contradiction by A9;
    end;
    hence thesis;
  end;
  for X be RealNormSpace for x,y be Point of X
     st (for e be Real st e>0 holds ||.x-y.|| <e) holds x=y
  proof
    let X be RealNormSpace;
    let x,y be Point of X such that
A11:   for e be Real st e > 0 holds ||.x-y.|| <e;
    x-y = 0.X by A8,A11;
    hence thesis by RLVECT_1:35;
  end;
  hence thesis by A1,A2,A3,A5,A8,NORMSP_1:10;
end;

Lm2:
for K, L, e be real number st 0 < K & K < 1 & 0 < e
   ex n be Nat st abs (L * (K to_power n )) < e
proof
let K, L, e be real number such that
A1: 0 < K and
A2: K < 1 and
A3: 0 < e;
deffunc P(Nat)=K to_power ($1+1);
consider rseq be Real_Sequence such that
A4: for n be Nat holds rseq.n=P(n) from SEQ_1:sch 1;
A5: rseq is convergent & lim rseq = 0 by A1,A2,A4,SERIES_1:1;
A6: L(#)rseq is convergent by A5,SEQ_2:21;
A7: lim ( L(#)rseq ) = L* lim rseq by A5,SEQ_2:22
                    .=0 by A5;
consider n be Nat such that
A8:for m be Nat st n<=m holds
     abs( (L(#)rseq).m-0  ) < e by A3,A6,A7,SEQ_2:def 7;
 abs( (L(#)rseq).n-0 ) < e by A8;
then abs( L*rseq.n ) < e by SEQ_1:13;
then abs( L*(K to_power (n+1)) ) < e by A4;
hence thesis;
end;

theorem Th14:
  for X be RealBanachSpace
   for f be Function of X, X st f is contraction of X
      ex xp be Point of X st
         f.xp=xp &
         for x be Point of X st f.x=x holds xp=x
proof
  let X be RealBanachSpace;
  let f be Function of X,X such that
  A1: f is contraction of X;
consider x0 be Element of X;
deffunc G(set,set)=f.$2;
consider g be Function such that
 A2:   dom g = NAT & g.0 = x0
 & for n being Element of NAT holds g.(n+1) = G(n,g.n) from RECDEF_1:sch 3;
consider K be Real such that
 A3:  0 < K and
 A4:  K < 1 and
 A5:  for x,y be Point of X holds
       ||.f.x - f.y.|| <=K*||.x - y.||  by A1,Def3;
defpred P[Nat] means g.$1 in the carrier of X;
 A6: P[0] by A2;
 A7:  for k be Nat st P[k] holds P[k+1]
 proof
 let k be Nat such that
   A8: P[k];
   g.(k+1)=f.(g.k) by A2;
   hence thesis by A8,FUNCT_2:7;
 end;
 for n be Nat holds P[n] from NAT_1:sch 1(A6,A7); then
A9: for n be set st n in NAT holds g.n in the carrier of X;
reconsider g as sequence of X by A2,A9,FUNCT_2:5;
A10: for n be Nat holds ||.g.(n+1) - g.n .||
      <= ||. g.1-g.0 .||* (K to_power n )
proof
  defpred P[Nat] means
     ||.g.($1+1) - g.($1) .|| <= ||. g.1-g.0 .||* (K to_power $1 );
 A11:   ||.g.(0+1) - g.(0) .|| = ||. g.1-g.0 .|| * 1
            .= ||. g.1-g.0 .|| * (K to_power 0 ) by POWER:29;
 A12:  P[0] by A11;
 A13:  for k be Nat st P[k] holds P[k+1]
 proof
 let k be Nat such that
   A14: P[k];
   A15: g.(k+1) =f.(g.k) by A2;
   A16: ||.g.((k+1)+1) - g.(k+1) .||
          = ||.f.(g.(k+1)) - f.(g.k) .|| by A2,A15;
   A17:  ||.f.(g.(k+1)) - f.(g.k) .||
         <= K* ||. g.(k+1) - g.k .|| by A5;
  K* ||. g.(k+1) - g.k .||
   <=  K*( ||. g.1-g.0 .||* (K to_power k ) ) by A3,A14,XREAL_1:66;
   then ||.f.(g.(k+1)) - f.(g.k)  .||
             <=||. g.1-g.0 .||*(K* (K to_power k ))
               by A17,XREAL_1:2; then
        ||.f.(g.(k+1)) - f.(g.k)  .||
             <=||. g.1-g.0 .||*((K to_power 1)* (K to_power k )) by POWER:30;
   hence thesis by A3,A16,POWER:32;
 end;
 for n be Nat holds P[n] from NAT_1:sch 1(A12,A13);
hence thesis;
end;

A18:
for k, n be Nat holds ||.g.(n+k) - g.n .||
      <=  ||. g.1-g.0 .||* ((K to_power n - K to_power (n+k)) /(1-K))
proof
  defpred P[Nat] means
    for n be Nat holds ||.g.(n+$1) - g.n .||
         <= ||. g.1-g.0 .||*( (K to_power n-K to_power (n+$1)) /(1-K));
     now let n be Nat;
       A19: ||.g.(n+0) - g.n .||= ||.0.X.|| by RLVECT_1:28
                               .=0 by NORMSP_1:5;
       thus ||.g.(n+0) - g.n .||
         <= ||. g.1-g.0 .||* ((K to_power n-K to_power (n+0))/(1-K)) by A19;
    end;
  then
  A20: P[0];
  A21: now let k be Nat such that
    A22:  P[k];
      now let n be Nat;
      A23: ||.g.(n+k) - g.n .||
            <= ||. g.1-g.0 .||* ((K to_power n - K to_power (n+k)) /(1-K))
            by A22;
      A24: ||.g.(n+(k+1)) - g.n .||
           <= ||.g.(n+(k+1)) - g.(n+k) .|| +  ||.g.(n+k) - g.n .||
           by NORMSP_1:14;
      ||.g.((n+k)+1) - g.(n+k) .||
           <= ||. g.1-g.0 .||* (K to_power (n+k)) by A10; then
      ||.g.(n+(k+1)) - g.(n+k) .|| +  ||.g.(n+k) - g.n .||
       <= ||. g.1-g.0 .||* (K to_power (n+k))
         + ||. g.1-g.0 .|| * ((K to_power n - K to_power (n+k)) /(1-K))
       by A23,XREAL_1:9; then
      A25:  ||.g.(n+(k+1)) - g.n .|| <= ||. g.1-g.0 .||
                * ((K to_power n - K to_power (n+k)) /(1-K))
               + ||. g.1-g.0 .||* (K to_power (n+k)) by A24,XREAL_1:2;
      A26: 1- K <> 0 by A4;
      ||. g.1-g.0 .||*((K to_power n - K to_power (n+k)) /(1-K))
   + ||. g.1-g.0 .||*(K to_power (n+k))
     = ||. g.1-g.0 .||*((K to_power n - K to_power (n+k))/(1-K))
         + ||. g.1-g.0 .||*(K to_power (n+k))*(1-K)/(1-K)
           by A26,XCMPLX_1:90
     .= ||. g.1-g.0 .||*((K to_power n - K to_power (n+k))/(1-K))
         + ||. g.1-g.0 .||*((K to_power (n+k))*(1-K))/(1-K)
     .= ||. g.1-g.0 .||*((K to_power n - K to_power (n+k))/(1-K))
         + ||. g.1-g.0 .||*((K to_power (n+k)*(1-K))/(1-K))
            by XCMPLX_1:75
     .= ||. g.1-g.0 .||*(((K to_power n - K to_power (n+k))/(1-K))
         + ((K to_power (n+k)*(1-K))/(1-K)))
    .= ||. g.1-g.0 .||*(  ((K to_power n - K to_power (n+k))
      +(K to_power (n+k)*(1-K)))/(1-K) ) by XCMPLX_1:63
    .= ||. g.1-g.0 .||*((K to_power n- K*K to_power (n+k))   /(1-K))
  .= ||. g.1-g.0 .||*((K to_power n- (K to_power 1)
  *K to_power (n+k)) /(1-K)) by POWER:30
  .= ||. g.1-g.0 .||*((K to_power n - K to_power (n+k+1))/(1-K))
      by A3,POWER:32;
 hence ||.g.(n+(k+1)) - g.n .||
      <= ||. g.1-g.0 .||*((K to_power n  - K to_power (n+(k+1)))/(1-K))
        by A25;
     end;
   hence P[k+1];
   end;
 for k be Nat holds P[k] from NAT_1: sch 1(A20,A21);
 hence thesis;
  end;
A27: for k,n be Nat holds ||.g.(n+k) - g.n .||
      <= ||. g.1-g.0 .||* (K to_power n  /(1-K))
 proof
 let k be Nat;
   now let n be Nat;
   A28: ||.g.(n+k) - g.n .||
      <= ||. g.1-g.0 .||*( (K to_power n - K to_power (n+k)) /(1-K)) by A18;
   A29: (K to_power n - K to_power (n+k)) /(1-K)
      <= (( K to_power n ) /(1-K))
      proof
      A30: K to_power (n+k) > 0 by A3,POWER:39;
A31: 1-K > 1-1 by A4,REAL_1:92;
      K to_power n - K to_power (n+k) <= K to_power n - 0 by A30,REAL_1:92;
     hence thesis by A31,REAL_1:73;
      end;
     0 <= ||. g.1-g.0 .|| by NORMSP_1:8; then
     ||. g.1-g.0 .||*( (K to_power n - K to_power (n+k)) /(1-K) )
      <= ||. g.1-g.0 .||*( ( K to_power n ) /(1-K) ) by A29,XREAL_1:66;
   hence ||.g.(n+k) - g.n .|| <=  ||. g.1-g.0 .||* ((K to_power n)  /(1-K))
         by A28,XREAL_1:2;
   end;
 hence thesis;
 end;
g is Cauchy_sequence_by_Norm
proof
now let e be Real such that
  A32: e >0;
  A33: e/2 > 0 by A32,SEQ_2:3;
  consider n be Nat such that
 A34: abs( ||. g.1-g.0 .||/(1-K)* (K to_power n) ) < e/2
 by A3,A4,A33,Lm2;
  ||. g.1-g.0 .||/(1-K)* (K to_power n)
 <= abs( ||. g.1-g.0 .||/(1-K)* (K to_power n) ) by ABSVALUE:11; then
   ||. g.1-g.0 .||/(1-K)* (K to_power n) < e/2 by A34,XREAL_1:2; then
A35:   ||. g.1-g.0 .|| *( (K to_power n)/(1-K)) < e/2 by XCMPLX_1:76;
  take nn=n+1;
   now let m,l be Nat such that
        A36: nn <= m and
        A37: nn <= l;
        n < nn by NAT_1:38; then
        A38: n < m by A36,XREAL_1:2;
        A39: n < l by A37,NAT_1:38;
        consider k1 be Nat such that
          A40: n+k1 =m by A38,NAT_1:28;
        consider k2 be Nat such that
          A41: n+k2 =l by A39,NAT_1:28;
         ||.g.(n+k1) - g.n .||
         <= ||. g.1-g.0 .||*( (K to_power n) /(1-K)) by A27; then
       A42: ||.g.m - g.n .|| < e/2 by A35,A40,XREAL_1:2;
         ||.g.(n+k2) - g.n .||
         <= ||. g.1-g.0 .||*( (K to_power n) /(1-K) ) by A27; then
       A43: ||.g.l - g.n .|| < e/2 by A35,A41,XREAL_1:2;
        thus ||.g.l - g.m .|| < e by A32,A42,A43,Lm1;
       end;
  hence for n, m be Nat st
    n >= nn & m >= nn holds ||.g.n - g.m.|| < e;
 end;
 hence thesis by RSSPACE3:10;
end;
then
A44: g is convergent by LOPBAN_1:def 16;
A45: lim g = f. (lim g)
proof
A46: g^\1 is convergent & lim (g^\1) = lim g by A44,LOPBAN_3:14;
A47:now let n be Nat;
  A48: ||. (g^\1).n  - f. (lim g) .||
     = ||. g.(n+1) - f. (lim g) .|| by LOPBAN_3:def 6
     .= ||. f.(g.n)- f. (lim g) .|| by A2;
  thus ||. (g^\1).n - f. (lim g) .|| <= K* ||. g.n - lim g .|| by A5,A48;
 end;
 A49: ||. g - lim g .|| is convergent by A44,NORMSP_1:41;
 A50: K(#)||. g - lim g .|| is convergent by A49,SEQ_2:21;
 A51: lim (K(#)(||.g - lim g.||)) =K*lim ||.(g - lim g).|| by A49,SEQ_2:22
                                    .=K*0 by A44,NORMSP_1:41
                                    .=0;
 for e be Real st e >0 ex n be Nat st
         for m be Nat st n<=m holds ||. (g^\1).m - f. (lim g) .|| <e
proof
  let e be Real such that
   A52: e >0;
   consider n be Nat such that
   A53:  for m be Nat st n<=m holds abs( (K(#)||. g - lim g .||).m-0)
      <e by A50,A51,A52,SEQ_2:def 7;
  take n;
 now let m be Nat such that
     A54: n<=m;
     abs( (K(#)||. g - lim g .||).m-0)<e by A53,A54; then
     abs( K*||. g - lim g .||.m) < e by SEQ_1:13; then
      abs( K*||. (g - lim g).m .||) < e by NORMSP_1:def 10; then
     A55: abs( K*||. g.m - lim g .||) < e by NORMSP_1:def 7;
     A56:K*||. g.m - lim g .|| <=abs( K*||. g.m - lim g .||) by ABSVALUE:11;
     A57: K*||. g.m - lim g .|| < e by A55,A56,XREAL_1:2;
      ||. (g^\1).m  - f. (lim g) .||  <= K* ||. g.m - lim g .||  by A47;
     hence ||. (g^\1).m  - f. (lim g) .||  < e by A57,XREAL_1:2;
   end;
  hence thesis;
end;
hence thesis by A46,NORMSP_1:def 11;
end;
take xp=lim g;
A58: xp=f.xp by A45;
A59:
 now let x be Point of X such that
   A60: f.x=x;
   A61: for k be Nat holds
      ||.x-xp.|| <= ||.x-xp.||*(K to_power k)
    proof
      defpred P[Nat] means
      ||.x-xp .|| <= ||.x-xp.||*(K to_power $1);
A62:   ||.x-xp .|| = ||. x-xp .|| * 1
                 .= ||. x-xp .|| * (K to_power 0 ) by POWER:29;
 A63:  P[0] by A62;
 A64:  for k be Nat st P[k] holds P[k+1]
 proof
 let k be Nat such that
   A65: P[k];
   A66: ||.x-xp.|| <= ||.x-xp.||*(K to_power k) by A65;
   A67: x=f.x by A60;
   A68: xp=f.xp by A45;
   A69: ||.x - xp .|| = ||.f.x - f.xp .|| by A67,A68;
   A70: ||.f.x - f.xp .|| <= K* ||. x-xp .|| by A5;
   K* ||. x-xp .||
   <= K*( ||.x-xp.||*(K to_power k) ) by A3,A66,XREAL_1:66;
   then ||.f.x - f.xp .|| <= ||. x-xp .||*(K* (K to_power k ))
     by A70,XREAL_1:2;
   then ||.f.x - f.xp .||
     <= ||. x-xp .||*((K to_power 1)* (K to_power k )) by POWER:30;
   hence thesis by A3,A69,POWER:32;
 end;
 for n be Nat holds P[n] from NAT_1:sch 1(A63,A64);
hence thesis;
 end;
A71: for e be Real st 0 <e holds ||.x-xp.|| < e
      proof
      let e be Real such that
A72: 0 < e;
      consider n be Nat such that
A73: abs(||.x-xp.||*(K to_power n) ) < e by A3,A4,A72,Lm2;
A74:   ||.x-xp.||*(K to_power n) <=abs(||.x-xp.||*(K to_power n))
        by ABSVALUE:11;
A75:   ||.x-xp.||*(K to_power n) <e by A73,A74,XREAL_1:2;
      ||.x-xp.|| <= ||.x-xp.||*(K to_power n) by A61;
      hence ||.x-xp.|| < e by A75,XREAL_1:2;
      end;
      thus x=xp by A71,Lm1;
 end;
thus thesis by A58,A59;
end;

theorem
   for X be RealBanachSpace
    for f be Function of X,X st
      ex n0 be Nat st iter(f,n0) is contraction of X holds
        ex xp be Point of X st
          f.xp=xp &
          (for x be Point of X st f.x=x holds xp=x)
 proof
   let X be RealBanachSpace;
   let f be Function of X, X;
   given n0 be Nat such that
A1:iter(f,n0) is contraction of X;
   consider xp be Point of X such that
A2:(iter(f,n0)).xp=xp and
A3: for x be Point of X st iter(f,n0).x=x holds xp=x by A1,Th14;
A4: (iter(f,n0)).(f.xp)
            =(iter(f,n0)*f).xp by FUNCT_2:21
           .=(iter(f,n0+1)).xp by FUNCT_7:71
           .=(f*iter(f,n0)).xp by FUNCT_7:73
            .=f.xp by A2,FUNCT_2:21;
A5: f.xp=xp by A3,A4;
     now let x be Point of X such that
A6:   f.x=x;
       for n be Nat holds (iter(f,n)).x=x
       proof
         defpred P[Nat] means (iter(f,$1)).x=x;
         (iter(f,0)).x=(id the carrier of X).x by FUNCT_7:86
                     .=x by FUNCT_1:34; then
     A7: P[0];
     A8: now let n be Nat such that
      A9: P[n];
           (iter(f,n+1)).x =(f*iter(f,n)).x by FUNCT_7:73
                       .=x by A6,A9,FUNCT_2:21;
           hence P[n+1];
         end;
         for n be Nat holds P[n] from NAT_1:sch 1(A7,A8);
         hence thesis;
       end;
       then (iter(f,n0)).x=x;
       hence xp=x by A3;
    end;
    hence thesis by A5;
end;

theorem
  for K, L, e be real number st 0 < K & K < 1 & 0 < e
   ex n be Nat st abs (L * (K to_power n)) < e by Lm2;
