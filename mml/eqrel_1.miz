:: Equivalence Relations and Classes of Abstraction
::  by Konrad Raczkowski and Pawe{\l} Sadowski
::
:: Received November 16, 1989
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies RELAT_1, SETFAM_1, ARYTM_1, BOOLE, RELAT_2, LATTICES, FINSEQ_1,
      FUNCT_1, TARSKI, EQREL_1, PARTFUN1, TOLER_1, AMI_1, T_1TOPSP, PUA2MSS1,
      PROB_1, NAT_1, COMPLEX1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, XCMPLX_0, XXREAL_0, RELAT_1,
      RELAT_2, RELSET_1, SETFAM_1, FINSEQ_1, FUNCT_1, NUMBERS, NAT_1, PARTFUN1;
 constructors SETFAM_1, RELAT_2, PARTFUN1, XXREAL_0, XREAL_0, NAT_1, FINSEQ_1;
 registrations XBOOLE_0, SUBSET_1, RELSET_1, PARTFUN1, ARYTM_3, XXREAL_0,
      XREAL_0, FINSEQ_1, RELAT_1;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, RELAT_1, RELAT_2, XBOOLE_0, PARTFUN1, SETFAM_1;
 theorems RELAT_1, RELAT_2, RELSET_1, SETFAM_1, ZFMISC_1, TARSKI, FINSEQ_1,
      NAT_1, REAL_1, XBOOLE_0, XBOOLE_1, ORDERS_1, PARTFUN1, XREAL_1, XXREAL_0;
 schemes SUBSET_1, RELSET_1, FINSEQ_1, NAT_1;

begin

 reserve X,Y,Z,x,y,y1,y2,z for set;
 reserve i,j,k for Element of NAT;
 reserve A,B,C for Subset of X;
 reserve R,R1,R2 for Relation of X;
 reserve AX for Subset of [:X,X:];
 reserve SFXX for Subset-Family of [:X,X:];

:: Equivalence Relation and its properties

definition let X;
  func nabla X -> Relation of X equals
    [:X,X:];
  coherence by RELSET_1:def 1;
end;

registration let X;
  cluster nabla X -> total reflexive;
 coherence
  proof
   thus dom nabla X c= X;
   thus X c= dom nabla X
    proof let x be set;
     assume x in X;
      then [x,x] in [:X,X:] by ZFMISC_1:106;
     hence x in dom nabla X by RELAT_1:def 4;
    end;
   let x;
   assume x in field nabla X;
    then x in dom nabla X \/ rng nabla X;
   hence [x,x] in nabla X by ZFMISC_1:106;
 end;
end;

definition let X; let R1,R2;
  redefine func R1 /\ R2 -> Relation of X;
coherence
 proof
  R1 /\ R2 c= [:X,X:];
  hence thesis;
 end;
  func R1 \/ R2 -> Relation of X;
coherence
 proof
  R1 \/ R2 c= [:X,X:];
  hence thesis;
 end;
end;

theorem
  nabla X \/ R1 = nabla X by XBOOLE_1:12;

canceled 2;

theorem
 id X is_reflexive_in X & id X is_symmetric_in X &
id X is_transitive_in X
  proof
    thus for x st x in X holds [x,x] in id X by RELAT_1:def 10;
    thus for x,y st x in X & y in X & [x,y] in id X holds
    [y,x] in id X by RELAT_1:def 10;
   thus for x,y,z st x in X & y in X & z in X & [x,y] in id X &
   [y,z] in id X holds [x,z] in id X by RELAT_1:def 10;
  end;

definition let X;
  mode Tolerance of X is total reflexive symmetric Relation of X;
  mode Equivalence_Relation of X is total symmetric transitive Relation of X;
end;

canceled;

theorem
  id X is Equivalence_Relation of X;

theorem Th7:
  nabla X is Equivalence_Relation of X
 proof
A1: nabla X is_symmetric_in X
   proof
    for x,y holds x in X & y in X & [x,y] in nabla X implies
      [y,x] in nabla X by ZFMISC_1:107;
    hence thesis by RELAT_2:def 3;
   end;
A2: nabla X is_transitive_in X
   proof
       for x,y,z st x in X & y in X & z in X & [x,y] in nabla X &
    [y,z] in nabla X holds [x,z] in nabla X by ZFMISC_1:106;
    hence thesis by RELAT_2:def 8;
   end;
     field nabla X = X by ORDERS_1:97;
   hence thesis by A1,A2,RELAT_2:def 11,def 16;
 end;

registration let X;
  cluster nabla X -> total symmetric transitive;
  coherence by Th7;
end;

 reserve EqR,EqR1,EqR2,EqR3 for Equivalence_Relation of X;

Lm1: [x,y] in R implies x in X & y in X
  proof
   assume [x,y] in R;
    then x in dom R & y in rng R by RELAT_1:20;
   hence thesis;
  end;

canceled 3;

theorem Th11:
  for R being total reflexive Relation of X holds
    x in X implies [x,x] in R
  proof
    let R be total reflexive Relation of X;
    field R = X by ORDERS_1:97;
    then R is_reflexive_in X by RELAT_2:def 9;
    hence thesis by RELAT_2:def 1;
  end;

theorem Th12:
  for R being total symmetric Relation of X holds
    [x,y] in R implies [y,x] in R
  proof
    let R be total symmetric Relation of X;
A1: field R = X by ORDERS_1:97;
   assume [x,y] in R;
   then A2: x in X & y in X & [x,y] in R by Lm1;
    R is_symmetric_in X by A1,RELAT_2:def 11;
   hence thesis by A2,RELAT_2:def 3;
  end;

theorem Th13:
  for R being total transitive Relation of X holds
    [x,y] in R & [y,z] in R implies [x,z] in R
  proof
    let R be total transitive Relation of X;
A1: field R = X by ORDERS_1:97;
   assume [x,y] in R & [y,z] in R;
   then A2: x in X & y in X & z in X & [x,y] in R & [y,z] in R by Lm1;
    R is_transitive_in X by A1,RELAT_2:def 16;
   hence thesis by A2,RELAT_2:def 8;
  end;

theorem
  for R being total reflexive Relation of X holds
    (ex x being set st x in X) implies R <> {} by Th11;

canceled;

theorem Th16:
  R is Equivalence_Relation of X iff
    R is reflexive symmetric transitive & field R = X
proof
 thus R is Equivalence_Relation of X implies
 R is reflexive symmetric transitive & field R = X by ORDERS_1:97;
 assume
A1:  R is reflexive & R is symmetric & R is transitive & field R = X;
 then R is_reflexive_in X by RELAT_2:def 9;
 then dom R = X by ORDERS_1:98;
 hence thesis by A1,PARTFUN1:def 4;
end;

definition let X; let EqR1,EqR2;
  redefine func EqR1 /\ EqR2 -> Equivalence_Relation of X;
coherence
 proof
A1: field (EqR1 /\ EqR2) = X
  proof A2: for x st x in X holds [x,x] in EqR1 by Th11;
   for x st x in X holds [x,x] in EqR2 by Th11;
   then id X c= EqR1 & id X c= EqR2 by A2,RELAT_1:73;
   then id X c= EqR1 /\ EqR2 by XBOOLE_1:19;
   then dom(EqR1 /\ EqR2) = X & rng(EqR1 /\ EqR2) = X by RELSET_1:31,
32;
   hence thesis;
  end;
A3: EqR1 /\ EqR2 is reflexive by RELAT_2:31;
A4: EqR1 /\ EqR2 is symmetric by RELAT_2:35;
  EqR1 /\ EqR2 is transitive by RELAT_2:43;
  hence thesis by A1,A3,A4,Th16;
 end;
end;

theorem
  id X /\ EqR = id X
proof
  now let x,y; assume [x,y] in id X;
  then x in X & x = y by RELAT_1:def 10;
  hence [x,y] in EqR by Th11;
 end;
 then id X c= EqR by RELAT_1:def 3;
 hence thesis by XBOOLE_1:28;
end;

canceled;

theorem Th19:
  for SFXX st (SFXX <> {} & for Y st Y in SFXX
   holds Y is Equivalence_Relation of X)
    holds meet SFXX is Equivalence_Relation of X
proof
 let SFXX such that A1: SFXX <> {} and
A2: for Y st Y in SFXX holds Y is Equivalence_Relation of X;
 reconsider XX = meet SFXX as Relation of X by RELSET_1:def 1;
A3: XX is_reflexive_in X
    proof let x such that A4: x in X;
     for Y st Y in SFXX holds [x,x] in Y
     proof let Y; assume Y in SFXX;
       then Y is Equivalence_Relation of X by A2;
       hence thesis by A4,Th11;
     end;
     hence thesis by A1,SETFAM_1:def 1;
    end;
 A5: XX is_symmetric_in X
    proof let x,y;
     assume A6: x in X & y in X & [x,y] in XX;
     now let Y; assume A7: Y in SFXX;
       then A8: Y is Equivalence_Relation of X by A2;
       [x,y] in Y by A6,A7,SETFAM_1:def 1;
       hence [y,x] in Y by A8,Th12;
     end;
     hence thesis by A1,SETFAM_1:def 1;
    end;
A9:   XX is_transitive_in X
    proof let x,y,z;
     assume A10: x in X & y in X & z in X & [x,y] in XX & [y,z] in XX;
     now let Y; assume A11: Y in SFXX;
       then A12: Y is Equivalence_Relation of X by A2;
       [x,y] in Y & [y,z] in Y by A10,A11,SETFAM_1:def 1;
       hence [x,z] in Y by A12,Th13;
     end;
     hence thesis by A1,SETFAM_1:def 1;
    end;
A13:     field XX = X by A3,ORDERS_1:98;
   dom XX = X by A3,ORDERS_1:98;
 hence thesis by A5,A9,A13,PARTFUN1:def 4,RELAT_2:def 11,def 16;
end;

theorem Th20:
  for R holds ex EqR st R c= EqR & for EqR2 st R c= EqR2 holds EqR c= EqR2
 proof let R;
  defpred P[set] means $1 is Equivalence_Relation of X & R c= $1;
  consider F being Subset-Family of [:X,X:] such that
A1: for AX holds AX in F iff P[AX] from SUBSET_1:sch 3;
  R c= nabla X;
  then A2: F <> {} by A1;
  for Y st Y in F holds Y is Equivalence_Relation of X by A1;
  then reconsider EqR = meet F as Equivalence_Relation of X by A2,Th19;
  take EqR;
A3:  for Y st Y in F holds R c= Y by A1;
     now let EqR2; assume R c= EqR2;
   then EqR2 in F by A1;
   hence EqR c= EqR2 by SETFAM_1:4;
  end;
  hence thesis by A2,A3,SETFAM_1:6;
 end;

definition let X; let EqR1,EqR2;
 canceled;

  func EqR1 "\/" EqR2 -> Equivalence_Relation of X means  :Def3:
  EqR1 \/ EqR2 c= it & for EqR st EqR1 \/ EqR2 c= EqR holds it c= EqR;
existence by Th20;
uniqueness
proof let R1,R2 be Equivalence_Relation of X such that
 A1: EqR1 \/ EqR2 c= R1 & for EqR st EqR1 \/ EqR2 c= EqR holds R1 c= EqR and
 A2: EqR1 \/ EqR2 c= R2 & for EqR st EqR1 \/ EqR2 c= EqR holds R2 c= EqR;
 A3: R1 c= R2 by A1,A2;
    R2 c= R1 by A1,A2;
 hence thesis by A3,XBOOLE_0:def 10;
end;
 commutativity;
 idempotence;
end;

theorem
   (EqR1 "\/" EqR2) "\/" EqR3 = EqR1 "\/" (EqR2 "\/" EqR3)
  proof
    for EqR4 be Equivalence_Relation of X
    holds EqR4 = (EqR1 "\/" EqR2) "\/" EqR3 implies
    EqR1 "\/" (EqR2 "\/" EqR3) c= EqR4
   proof
    let EqR4 be Equivalence_Relation of X;
    A1: EqR1 "\/" EqR2 c= (EqR1 "\/" EqR2) \/ EqR3 by XBOOLE_1:7;
    A2: EqR3 c= (EqR1 "\/" EqR2) \/ EqR3 by XBOOLE_1:7;
    A3: EqR1 \/ EqR2 c= EqR1 "\/" EqR2 by Def3;
    A4: EqR1 c= EqR1 \/ EqR2 & EqR2 c= EqR1 \/ EqR2 by XBOOLE_1:7;
    assume EqR4 = (EqR1 "\/" EqR2) "\/" EqR3;
    then A5: (EqR1 "\/" EqR2) \/ EqR3 c= EqR4 by Def3;
    then EqR1 "\/" EqR2 c= EqR4 by A1,XBOOLE_1:1;
    then EqR1 \/ EqR2 c= EqR4 by A3,XBOOLE_1:1;
    then A6: EqR1 c= EqR4 & EqR2 c= EqR4 & EqR3 c= EqR4 by A2,A4,A5,XBOOLE_1:1;
    then EqR2 \/ EqR3 c= EqR4 by XBOOLE_1:8;
    then EqR2 "\/" EqR3 c= EqR4 by Def3;
    then EqR1 \/ (EqR2 "\/" EqR3) c= EqR4 by A6,XBOOLE_1:8;
    hence EqR1 "\/" (EqR2 "\/" EqR3) c= EqR4 by Def3;
   end;
   then A7: EqR1 "\/" (EqR2 "\/" EqR3) c= (EqR1 "\/" EqR2) "\/" EqR3;
      for EqR4 be Equivalence_Relation of X
      holds EqR4 = EqR1 "\/" (EqR2 "\/" EqR3) implies
    (EqR1 "\/" EqR2) "\/" EqR3 c= EqR4
   proof
    let EqR4 be Equivalence_Relation of X;
    A8: EqR2 "\/" EqR3 c= EqR1 \/ (EqR2 "\/" EqR3) by XBOOLE_1:7;
    A9: EqR1 c= EqR1 \/ (EqR2 "\/" EqR3) by XBOOLE_1:7;
    A10: EqR2 \/ EqR3 c= EqR2 "\/" EqR3 by Def3;
    A11: EqR2 c= EqR2 \/ EqR3 & EqR3 c= EqR2 \/ EqR3 by XBOOLE_1:7;
    assume EqR4 = EqR1 "\/" (EqR2 "\/" EqR3);
    then A12: EqR1 \/ (EqR2 "\/" EqR3) c= EqR4 by Def3;
    then EqR2 "\/" EqR3 c= EqR4 by A8,XBOOLE_1:1;
    then EqR2 \/ EqR3 c= EqR4 by A10,XBOOLE_1:1;
    then A13: EqR1 c= EqR4 & EqR2 c= EqR4 & EqR3 c= EqR4 by A9,A11,A12,
XBOOLE_1:1;
    then EqR1 \/ EqR2 c= EqR4 by XBOOLE_1:8;
    then EqR1 "\/" EqR2 c= EqR4 by Def3;
    then (EqR1 "\/" EqR2) \/ EqR3 c= EqR4 by A13,XBOOLE_1:8;
    hence (EqR1 "\/" EqR2) "\/" EqR3 c= EqR4 by Def3;
   end;
   then (EqR1 "\/" EqR2) "\/" EqR3 c= EqR1 "\/" (EqR2 "\/" EqR3);
   hence thesis by A7,XBOOLE_0:def 10;
  end;

theorem
  EqR "\/" EqR = EqR;

theorem
 EqR1 "\/" EqR2 = EqR2 "\/" EqR1;

theorem
  EqR1 /\ (EqR1 "\/" EqR2) = EqR1
proof
 thus EqR1 /\ (EqR1 "\/" EqR2) c= EqR1 by XBOOLE_1:17;
 A1: EqR1 c= EqR1 \/ EqR2 by XBOOLE_1:7;
    EqR1 \/ EqR2 c= EqR1 "\/" EqR2 by Def3;
 then EqR1 c= EqR1 "\/" EqR2 by A1,XBOOLE_1:1;
 hence thesis by XBOOLE_1:19;
end;

theorem
  EqR1 "\/" (EqR1 /\ EqR2) = EqR1
proof
A1: EqR1 = EqR1 \/ (EqR1 /\ EqR2) by XBOOLE_1:22;
    for EqR st EqR1 \/ (EqR1 /\ EqR2) c= EqR holds EqR1 c= EqR by XBOOLE_1:22;
 hence thesis by A1,Def3;
end;

scheme ExEqRel {X() -> set,P[set,set]}:
  ex EqR being Equivalence_Relation of X() st
  for x,y holds [x,y] in EqR iff x in X() & y in X() & P[x,y]
  provided
A1: for x st x in X() holds P[x,x] and
A2: for x,y st P[x,y] holds P[y,x] and
A3: for x,y,z st P[x,y] & P[y,z] holds P[x,z]
proof
 consider Y being Relation of X(),X() such that A4:
 for x,y holds [x,y] in Y iff x in X() & y in X() & P[x,y] from RELSET_1:sch 1;
 A5: Y is_reflexive_in X()
 proof let x; assume A6: x in X();
  then P[x,x] by A1;
  hence thesis by A4,A6;
 end;
 A7: Y is_symmetric_in X()
 proof let x,y;
  assume A8: x in X() & y in X() & [x,y] in Y;
  then P[x,y] by A4;
  then y in X() & x in X() & P[y,x] by A2,A8;
  hence thesis by A4;
 end;
A9:   Y is_transitive_in X()
 proof let x,y,z;
  assume A10: x in X() & y in X() & z in X() & [x,y] in Y & [y,z] in Y;
  then A11: P[x,y] by A4;
     P[y,z] by A4,A10;
  then P[x,z] by A3,A11;
  hence thesis by A4,A10;
 end;
A12: field Y = X() by A5,ORDERS_1:98;
   dom Y = X() by A5,ORDERS_1:98;
 then reconsider R1 = Y as Equivalence_Relation of X()
                        by A7,A9,A12,PARTFUN1:def 4,RELAT_2:def 11,
def 16;
 take R1;
 thus thesis by A4;
end;

:: Classes of abstraction

notation let R be Relation,
               x be set;
 synonym Class(R,x) for Im(R,x);
end;

definition let X, Y be set,
               R be Relation of X, Y,
               x be set;
 canceled;
  redefine func Class(R,x) -> Subset of Y;
  coherence
  proof
    R.:{x} c= Y;
    hence thesis;
  end;
end;

theorem
  for R being Relation holds
    y in Class (R,x) iff [x,y] in R
 proof
  let R be Relation;
  thus y in Class(R,x) implies [x,y] in R
  proof
    assume y in Class(R,x);
     then ex z being set st [z,y] in R & z in {x} by RELAT_1:def 13;
    hence thesis by TARSKI:def 1;
  end;
  assume A1: [x,y] in R;
   x in {x} by TARSKI:def 1;
  hence thesis by A1,RELAT_1:def 13;
 end;

theorem Th27:
  for R being total symmetric Relation of X holds
    y in Class (R,x) iff [y,x] in R
 proof
  let R be total symmetric Relation of X;
  thus y in Class(R,x) implies [y,x] in R
  proof
    assume y in Class(R,x);
     then ex z being set st [z,y] in R & z in {x} by RELAT_1:def 13;
    then [x,y] in R by TARSKI:def 1;
    hence [y,x] in R by Th12;
  end;
  assume [y,x] in R;
  then A1: [x,y] in R by Th12;
   x in {x} by TARSKI:def 1;
  hence thesis by A1,RELAT_1:def 13;
 end;

theorem Th28:
  for R being Tolerance of X holds
    for x st x in X holds x in Class (R,x)
  proof
   let R be Tolerance of X;
   let x;
   assume x in X;
   then [x,x] in R by Th11;
   hence thesis by Th27;
  end;

theorem
   for R being Tolerance of X holds
    for x st x in X holds ex y st x in Class(R,y)
  proof
   let R be Tolerance of X;
   let x;
   assume x in X;
   then x in Class(R,x) by Th28;
   hence thesis;
  end;

theorem
   for R being transitive Tolerance of X holds
    y in Class(R,x) & z in Class(R,x) implies [y,z] in R
  proof
    let R be transitive Tolerance of X;
    assume y in Class(R,x) & z in Class(R,x);
    then [y,x] in R & [z,x] in R by Th27;
    then [y,x] in R & [x,z] in R by Th12;
    hence thesis by Th13;
  end;

Lm2:
  for x st x in X holds [x,y] in EqR iff Class(EqR,x) = Class(EqR,y)
proof let x such that A1: x in X;
 thus [x,y] in EqR implies Class(EqR,x) = Class(EqR,y)
 proof
  assume A2: [x,y] in EqR;
     now let z;
    assume z in Class(EqR,x);
    then [z,x] in EqR by Th27;
    then [z,y] in EqR by A2,Th13;
    hence z in Class(EqR,y) by Th27;
  end;
  then A3: Class(EqR,x) c= Class(EqR,y) by TARSKI:def 3;
     now let z;
    assume z in Class(EqR,y);
    then A4: [z,y] in EqR by Th27;
       [y,x] in EqR by A2,Th12;
    then [z,x] in EqR by A4,Th13;
    hence z in Class(EqR,x) by Th27;
  end;
  then Class(EqR,y) c= Class(EqR,x) by TARSKI:def 3;
  hence thesis by A3,XBOOLE_0:def 10;
 end;
 assume Class(EqR,x) = Class(EqR,y);
 then x in Class(EqR,y) by A1,Th28;
 hence thesis by Th27;
end;

theorem Th31:
  for x st x in X holds y in Class(EqR,x) iff Class(EqR,x) = Class(EqR,y)
proof let x such that A1: x in X;
 thus y in Class(EqR,x) implies Class(EqR,x) = Class(EqR,y)
 proof
  assume y in Class(EqR,x);
   then [y,x] in EqR by Th27;
  then [x,y] in EqR by Th12;
  hence thesis by A1,Lm2;
 end;
 assume Class(EqR,x) = Class(EqR,y);
 then [x,y] in EqR by A1,Lm2;
 then [y,x] in EqR by Th12;
 hence thesis by Th27;
end;

theorem Th32:
  for x,y st x in X & y in X holds Class(EqR,x) = Class(EqR,y) or
    Class(EqR,x) misses Class(EqR,y)
 proof let x,y;
  assume A1: x in X & y in X;
  A2: [x,y] in EqR implies Class(EqR,x) = Class(EqR,y)
      proof
       assume [x,y] in EqR;
       then x in Class(EqR,y) by Th27;
       hence thesis by A1,Th31;
      end;
     not [x,y] in EqR implies Class(EqR,x) misses Class(EqR,y)
   proof
    assume A3: not [x,y] in EqR;
    assume Class(EqR,x) meets Class(EqR,y);
    then consider z such that
A4: z in Class(EqR,x) & z in Class(EqR,y) by XBOOLE_0:3;
       [z,x] in EqR & [z,y] in EqR by A4,Th27;
    then [x,z] in EqR & [z,y] in EqR by Th12;
    hence contradiction by A3,Th13;
   end;
  hence thesis by A2;
 end;

theorem Th33:
   for x st x in X holds Class(id X,x) = {x}
 proof let x;
  assume A1: x in X;
      now let y;
    assume y in Class(id X,x);
    then [y,x] in id X by Th27;
    hence y = x by RELAT_1:def 10;
  end;
  then for y holds y in Class(id X,x) iff y = x by A1,Th28;
  hence thesis by TARSKI:def 1;
 end;

theorem Th34:
for x st x in X holds Class(nabla X,x) = X
proof let x such that A1: x in X;
    now let y; assume y in X;
  then [y,x] in nabla X by A1,ZFMISC_1:106;
  hence y in Class(nabla X,x) by Th27;
 end;
 then for y holds y in X iff y in Class(nabla X,x);
 hence thesis by TARSKI:2;
end;

theorem Th35:
(ex x st Class(EqR,x) = X) implies EqR = nabla X
proof
 given x such that A1: Class(EqR,x) = X;
  [:X,X:] c= EqR
   proof let y,z; assume [y,z] in [:X,X:];
     then y in Class(EqR,x) & z in Class(EqR,x) by A1,ZFMISC_1:106;
     then [y,x] in EqR & [z,x] in EqR by Th27;
     then [y,x] in EqR & [x,z] in EqR by Th12;
    hence [y,z] in EqR by Th13;
   end;
 hence thesis by XBOOLE_0:def 10;
end;

theorem
   x in X implies ([x,y] in EqR1 "\/" EqR2 iff
ex f being FinSequence st 1 <= len f & x = f.1 & y = f.(len f) &
for i st 1 <= i & i < len f holds [f.i,f.(i+1)] in EqR1 \/ EqR2)
proof assume A1: x in X;
 thus [x,y] in EqR1 "\/" EqR2 implies
 ex f being FinSequence st 1 <= len f & x = f.1 & y = f.(len f) &
 for i st 1 <= i & i < len f holds [f.i,f.(i+1)] in EqR1 \/ EqR2
 proof assume A2: [x,y] in EqR1 "\/" EqR2;
  defpred P[set,set] means ex f being FinSequence st (1 <= len f &
  $1 = f.1 & $2 = f.(len f) & for i st 1 <= i & i < len f holds
  [f.i,f.(i+1)] in EqR1 \/ EqR2);
  consider Y being Relation of X,X such that A3: for x,y holds
  [x,y] in Y iff x in X & y in X & P[x,y] from RELSET_1:sch 1;
  A4: Y is_reflexive_in X
  proof let x such that A5: x in X;
   set g = <*x*>;
   A6: len g = 1 & g.1 = x by FINSEQ_1:57;
      for i st 1 <= i & i < len g holds [g.i,g.(i+1)] in EqR1 \/ EqR2 by
FINSEQ_1:57;
   hence thesis by A3,A5,A6;
  end;
  A7: Y is_symmetric_in X
  proof let x,y;
   assume A8: x in X & y in X & [x,y] in Y;
   then consider f being FinSequence such that A9:
   (1 <= len f & x = f.1 & y = f.(len f) &
   for i st 1 <= i & i < len f holds [f.i,f.(i+1)] in EqR1 \/ EqR2) by A3;
   defpred P[Element of NAT,set] means $2 = f.(1 + (len f) - $1);
   A10: for k,y1,y2 st k in Seg(len f) & P[k,y1] & P[k,y2] holds y1 =y2;
   A11: for k st k in Seg(len f) ex z st P[k,z];
   consider g being FinSequence such that A12: dom g = Seg(len f) &
   for k st k in Seg(len f) holds P[k,g.k] from FINSEQ_1:sch 1(A10,A11);
   A13: len f = len g by A12,FINSEQ_1:def 3;
   A14: 1 <= len g by A9,A12,FINSEQ_1:def 3;
   A15: now
            1 in Seg(len f) by A9,FINSEQ_1:3;
        then g.1 = f.((len f) + 1 - 1) by A12
           .= f.(len f);
        hence y = g.1 by A9;
       end;
   A16: now
            len f in Seg(len f) by A9,FINSEQ_1:3;
        then g.(len f) = f.(1 + (len f) - len f) by A12
                 .= f.(1 + 0);
        hence x = g.(len g) by A9,A12,FINSEQ_1:def 3;
       end;
      for j st 1 <= j & j < len g holds [g.j,g.(j+1)] in EqR1 \/ EqR2
   proof let j;
    assume A17: 1 <= j & j < len g;
    then j in Seg(len f) by A13,FINSEQ_1:3;
    then A18: g.j = f.(1 + (len f) - j) by A12
           .= f.(((len f) - j) + 1);
    A19: 1 <= (j + 1) by NAT_1:12;
       (j + 1) <= len f by A13,A17,NAT_1:13;
     then (j + 1) in Seg(len f) by A19,FINSEQ_1:3;
    then A20: g.(j + 1) = f.((len f) + 1 - (1 + j)) by A12
                 .= f.((len f) - j);
    reconsider k = (len f) - j as Element of NAT by A13,A17,NAT_1:21;
       j - (len f) < (len f) - (len f) by A13,A17,XREAL_1:11;
    then - ((len f) - j) < - 0;
    then 0 < k by XREAL_1:26;
then A21:    0 + 1 <= k by NAT_1:13;
    - j < -0 by XREAL_1:26,A17;
    then (len f) + -j < 0 + (len f) by XREAL_1:8;
    then A22: [f.k,f.(k + 1)] in EqR1 \/ EqR2 by A9,A21;
       now per cases by A22,XBOOLE_0:def 2;
     suppose [f.k,f.(k + 1)] in EqR1;
      then [f.(k + 1),f.k] in EqR1 by Th12;
      hence [f.(k + 1),f.k] in EqR1 \/ EqR2 by XBOOLE_0:def 2;
     end;
     suppose [f.k,f.(k + 1)] in EqR2;
      then [f.(k + 1),f.k] in EqR2 by Th12;
      hence [f.(k + 1),f.k] in EqR1 \/ EqR2 by XBOOLE_0:def 2;
    end;
    end;
    hence thesis by A18,A20;
   end;
   hence thesis by A3,A8,A14,A15,A16;
  end;
A23:    Y is_transitive_in X
  proof let x,y,z;
   assume A24: x in X & y in X & z in X & [x,y] in Y & [y,z] in Y;
   then consider f being FinSequence such that A25:
   (1 <= len f & x = f.1 & y = f.(len f) &
   for i st 1 <= i & i < len f holds [f.i,f.(i+1)] in EqR1 \/ EqR2) by A3;
   consider g being FinSequence such that A26:
   (1 <= len g & y = g.1 & z = g.(len g) &
   for i st 1 <= i & i < len g holds [g.i,g.(i+1)] in EqR1 \/ EqR2) by A3,A24;
   set h = f^g;
   A27:len h = len f + len g by FINSEQ_1:35;
   then A28: 1 <= len h by A25,NAT_1:12;
      1 in Seg(len f) by A25,FINSEQ_1:3;
   then 1 in dom f by FINSEQ_1:def 3;
   then A29: x = h.1 by A25,FINSEQ_1:def 7;
   A30: len f + 1 <= len f + len g by A26,XREAL_1:9;
   then A31:   h.(len h) = g.(len g + len f - len f) by A27,FINSEQ_1:36
            .= g.(len g);
      for i st 1 <= i & i < len h holds [h.i,h.(i+1)] in EqR1 \/ EqR2
   proof let i;
    assume A32: 1 <= i & i < len h;
    then A33: (1 <= i & i < len f) or i = len f or len f < i by REAL_1:def 5;
       now per cases by A32,A33,NAT_1:13;
     suppose A34: (1 <= i & i < len f);
      then i in Seg(len f) by FINSEQ_1:3;
      then i in dom f by FINSEQ_1:def 3;
      then A35: h.i = f.i by FINSEQ_1:def 7;
       1 <= i + 1 & i + 1 <= len f by A34,NAT_1:13;
      then i + 1 in Seg len f by FINSEQ_1:3;
      then i + 1 in dom f by FINSEQ_1:def 3;
       then h.(i + 1) = f.(i + 1) by FINSEQ_1:def 7;
      hence [h.i,h.(i+1)] in EqR1 \/ EqR2 by A25,A34,A35;
     end;
     suppose A36: i = len f;
         len f in Seg len f by A25,FINSEQ_1:3;
      then len f in dom f by FINSEQ_1:def 3;
      then A37: h.i = y by A25,A36,FINSEQ_1:def 7;
      A38: h.(i + 1) = g.(1 + len f - len f) by A30,A36,FINSEQ_1:36
                   .= y by A26;
         [y,y] in EqR1 by A24,Th11;
      hence [h.i,h.(i+1)] in EqR1 \/ EqR2 by A37,A38,XBOOLE_0:def 2;
     end;
     suppose A39: (len f + 1 <= i & i < len h);
      then (len f + 1 <= i + 1 & i + 1 <= len f + len g) by A27,NAT_1:13;
      then A40: h.(i + 1) = g.(1 + i - len f) by FINSEQ_1:36
                   .= g.((i - len f) + 1);
        1 <= len f & len f < i by A25,A39,NAT_1:13;
     then reconsider k = i - len f as Element of NAT by NAT_1:21;
   (1 + len f - len f <= i - len f & i < len f + len g) by A39,FINSEQ_1:35
,XREAL_1:11;
     then (1 <= i - len f & i - len f < len g) by XREAL_1:21;
     then [g.k,g.(k + 1)] in EqR1 \/ EqR2 by A26;
     hence [h.i,h.(i+1)] in EqR1 \/ EqR2 by A27,A39,A40,FINSEQ_1:36;
    end;
    end;
    hence thesis;
   end;
   hence thesis by A3,A24,A26,A28,A29,A31;
  end;
  field Y = X & dom Y = X by A4,ORDERS_1:98;
  then reconsider R1 = Y as Equivalence_Relation of X
                        by A7,A23,PARTFUN1:def 4,RELAT_2:def 11,def 16;
     for x,y holds [x,y] in EqR1 \/ EqR2 implies [x,y] in R1
  proof let x,y;
   assume A41: [x,y] in EqR1 \/ EqR2;
   then A42: x in X & y in X by Lm1;
   set g = <*x,y*>;
   A43: len g = 2 by FINSEQ_1:61;
   A44:len g = 1 + 1 by FINSEQ_1:61;
   A45: g.1 = x & g.2 = y by FINSEQ_1:61;
   A46: g.1 = x & g.(len g) = y by A43,FINSEQ_1:61;
      for i st 1 <= i & i < len g holds [g.i,g.(i+1)] in EqR1 \/ EqR2
   proof let i;
    assume 1 <= i & i < len g;
    then 1 <= i & i <= 1 by A44,NAT_1:13;
    then 1 = i by XXREAL_0:1;
    hence thesis by A41,A45;
   end;
   hence thesis by A3,A42,A44,A46;
  end;
  then EqR1 \/ EqR2 c= R1 by RELAT_1:def 3;
  then EqR1 "\/"
 EqR2 c= R1 by Def3; then consider f being FinSequence such that A47:
  1 <= len f & x = f.1 & y = f.(len f) &
  for i st 1 <= i & i < len f holds [f.i,f.(i+1)] in EqR1 \/ EqR2 by A2,A3;
  take f;
  thus thesis by A47;
 end;
given f being FinSequence such that A48: 1 <= len f &
                x = f.1 & y = f.(len f) &
        for i st 1 <= i & i < len f holds [f.i,f.(i+1)] in EqR1 \/ EqR2;
  defpred P[Element of NAT] means
    1 <= $1 & $1 <= len f implies [f.1,f.$1] in EqR1 "\/" EqR2;
   for i holds P[i]
  proof
   A49:P[0];
   A50:now let i;
        assume A51: P[i];
      thus P[i+1]
      proof
      assume A52: 1 <= i + 1 & i + 1 <= len f;
      then A53: i < len f by NAT_1:13;
A54:     1 <= i or 1 = i + 1 by A52,NAT_1:8;
      A55: EqR1 \/ EqR2 c= EqR1 "\/" EqR2 by Def3;
          now per cases by A54;
        suppose A56: 1 <= i;
         then [f.i,f.(i+1)] in EqR1 \/ EqR2 by A48,A53;
hence [f.1,f.(i+1)] in EqR1 "\/" EqR2 by A51,A52,A55,A56,Th13,NAT_1:13;
        end;
        suppose A57: 0 = i;
           [f.1,f.1] in EqR1 by A1,A48,Th11;
        then [f.1,f.1] in EqR1 \/ EqR2 by XBOOLE_0:def 2;
        hence [f.1,f.(i+1)] in EqR1 "\/" EqR2 by A55,A57;
       end;
       end;
     hence [f.1,f.(i + 1)] in EqR1 "\/" EqR2;
     end;
    end;
   thus thesis from NAT_1:sch 1(A49,A50);
  end;
 hence thesis by A48;
end;

theorem Th37:
for E being Equivalence_Relation of X st E = EqR1 \/ EqR2 holds
for x st x in X holds
Class(E,x) = Class(EqR1,x) or Class(E,x) = Class(EqR2,x)
proof let E be Equivalence_Relation of X such that
 A1: E = EqR1 \/ EqR2;
    for x st x in X holds
 Class(E,x) = Class(EqR1,x) or Class(E,x) = Class(EqR2,x)
 proof let x such that x in X;
  assume that A2: not Class(E,x) = Class(EqR1,x) and
       A3: not Class(E,x) = Class(EqR2,x);
  consider y such that A4: y in Class(E,x) & not y in Class(EqR1,x) or
       y in Class(EqR1,x) & not y in Class(E,x) by A2,TARSKI:2;
A5:  now assume that A6: y in Class(EqR1,x) and A7: not y in Class(E,x);
      [y,x] in EqR1 by A6,Th27;
   then [y,x] in E by A1,XBOOLE_0:def 2;
   hence contradiction by A7,Th27;
  end;
  consider z such that A8: z in Class(E,x) & not z in Class(EqR2,x) or
       z in Class(EqR2,x) & not z in Class(E,x) by A3,TARSKI:2;
A9:  now assume that A10: z in Class(EqR2,x) and A11: not z in Class(E,x);
      [z,x] in EqR2 by A10,Th27;
   then [z,x] in E by A1,XBOOLE_0:def 2;
   hence contradiction by A11,Th27;
  end;
  A12:[y,x] in E by A4,A5,Th27;
     not [y,x] in EqR1 by A4,A5,Th27;
  then A13:[y,x] in EqR2 by A1,A12,XBOOLE_0:def 2;
  A14:[z,x] in E by A8,A9,Th27;
  then [x,z] in E by Th12;
then A15:  [y,z] in E by A12,Th13;
     not [z,x] in EqR2 by A8,A9,Th27;
  then A16: [z,x] in EqR1 by A1,A14,XBOOLE_0:def 2;
  A17:now assume A18:[y,z] in EqR2;
      [x,y] in EqR2 by A13,Th12;
   then [x,z] in EqR2 by A18,Th13;
   then [z,x] in EqR2 by Th12;
   hence contradiction by A8,A9,Th27;
  end;
     now assume [y,z] in EqR1;
   then A19:[z,y] in EqR1 by Th12;
      [x,z] in EqR1 by A16,Th12;
   then [x,y] in EqR1 by A19,Th13;
   then [y,x] in EqR1 by Th12;
   hence contradiction by A4,A5,Th27;
  end;
 hence contradiction by A1,A15,A17,XBOOLE_0:def 2;
 end;
 hence thesis;
end;

theorem
  EqR1 \/ EqR2 = nabla X implies EqR1 = nabla X or EqR2 = nabla X
proof
 assume A1: EqR1 \/ EqR2 = nabla X;
 A2: now assume X = {};
      then nabla X = {} by ZFMISC_1:113;
     hence EqR1 = nabla X or EqR2 = nabla X by A1,XBOOLE_1:15;
    end;
    (not X = {}) implies EqR1 = nabla X or EqR2 = nabla X
 proof
  assume A3: X <> {};
  consider y being Element of X;
     now let x; assume A4: x in X;
   then Class(nabla X,x) = Class(EqR1,x) or
   Class(nabla X,x) = Class(EqR2,x) by A1,Th37;
   hence Class(EqR1,x) = X or Class(EqR2,x) = X by A4,Th34;
  end;
  then Class(EqR1,y) = X or Class(EqR2,y) = X by A3;
  hence thesis by Th35;
 end;
 hence thesis by A2;
end;

definition let X; let EqR;
  func Class EqR -> Subset-Family of X means  :Def5:
  A in it iff ex x st x in X & A = Class(EqR,x);
 existence
 proof
   defpred P[set] means ex x st x in X & $1 = Class(EqR,x);
   consider F being Subset-Family of X such that
A1:for A holds A in F iff P[A] from SUBSET_1:sch 3;
   take F;
   thus thesis by A1;
 end;
 uniqueness
 proof
  let F1,F2 be Subset-Family of X;
  assume that
  A2: for A holds A in F1 iff ex x st x in X & A = Class(EqR,x) and
  A3: for A holds A in F2 iff ex x st x in X & A = Class(EqR,x);
       now let A;
        A in F1 iff ex x st x in X & A = Class(EqR,x) by A2;
     hence A in F1 iff A in F2 by A3;
    end;
  hence F1 = F2 by SETFAM_1:44;
 end;
 end;

canceled;

theorem Th40:
  X = {} implies Class EqR = {}
proof
 assume that A1: X = {} and A2: Class EqR <> {};
 consider z being Element of Class EqR;
    z is Subset of X by A2,TARSKI:def 3;
 then ex x st x in X & z = Class(EqR,x) by A2,Def5;
 hence contradiction by A1;
end;

definition let X;
  mode a_partition of X -> Subset-Family of X means :Def6:
  union it = X & for A st A in it holds A<>{} &
  for B st B in it holds A = B or A misses B if X <> {} otherwise it = {};
existence
proof
 thus X <> {} implies
 ex F being Subset-Family of X st union F = X &
 for A st A in F holds A<>{} & for B st B in F holds A = B or A misses B
 proof assume X <> {};
  defpred P[set] means ex x st x in X & $1 = {x};
  consider F being Subset-Family of X such that
  A1: for A being Subset of X holds A in F iff P[A] from SUBSET_1:sch 3;
A2:  now let y;
      now assume A3: y in X;
    set Z = {y};
    A4: y in Z by TARSKI:def 1;
       Z is Subset of X by A3,ZFMISC_1:37;
    then Z in F by A1,A3;
    hence ex Z st y in Z & Z in F by A4;
   end;
   hence y in X iff ex Z st y in Z & Z in F;
  end;
  A5: for A st A in F holds A<>{} & for B st B in F holds
      A = B or A misses B
  proof let A; assume A in F;
   then consider x such that A6: x in X & A = {x} by A1;
   thus A <> {} by A6;
   let B; assume B in F;
   then consider y such that A7: y in X & B = {y} by A1;
      now assume A8: (not x = y);
       for z st z in A holds not z in B
    proof let z; assume z in A;
     then not z = y by A6,A8,TARSKI:def 1;
     hence thesis by A7,TARSKI:def 1;
    end;
    hence A misses B by XBOOLE_0:3;
   end;
   hence thesis by A6,A7;
  end;
  take F;
  thus thesis by A2,A5,TARSKI:def 4;
 end;
 assume X = {};
    {} c= bool X by XBOOLE_1:2;
 hence thesis;
end;
consistency;
end;

canceled;

theorem Th42:
  Class EqR is a_partition of X
proof
 A1: X <> {} implies Class EqR is a_partition of X
 proof assume A2: X <> {};
     now let x;
      now assume A3: x in X;
    consider Z such that A4: Z = Class(EqR,x);
    A5: x in Z by A3,A4,Th28;
       Z in Class EqR by A3,A4,Def5;
    hence ex Z st x in Z & Z in Class EqR by A5;
   end;
   hence x in X iff ex Z st x in Z & Z in Class EqR;
  end;
  then A6: union(Class EqR) = X by TARSKI:def 4;
     for A st A in Class EqR holds
  A<>{} & for B st B in Class EqR holds A = B or A misses B
  proof let A; assume A in Class EqR;
   then consider x such that A7: x in X & A = Class(EqR,x) by Def5;
   thus A <> {} by A7,Th28;
   let B; assume B in Class EqR;
   then ex y st y in X & B = Class(EqR,y) by Def5;
   hence thesis by A7,Th32;
  end;
  hence thesis by A2,A6,Def6;
 end;
    X = {} implies Class EqR is a_partition of X
 proof assume A8: X = {};
  then Class EqR = {} by Th40;
  hence thesis by A8,Def6;
 end;
 hence thesis by A1;
end;

theorem Th43:
  for P being a_partition of X holds ex EqR st P = Class EqR
proof let P be a_partition of X;
 A1: X <> {} implies ex EqR st P = Class EqR
 proof assume A2: X <> {}; then A3: union P = X by Def6;
  defpred P[set,set] means ex A st A in P & $1 in A & $2 in A;
  A4: for x st x in X holds P[x,x]
  proof let x; assume x in X;
   then consider Z such that A5: x in Z & Z in P by A3,TARSKI:def 4;
   reconsider A = Z as Subset of X by A5;
   take A;
   thus thesis by A5;
  end;
  A6: for x,y st P[x,y] holds P[y,x];
  A7: for x,y,z st P[x,y] & P[y,z] holds P[x,z]
  proof let x,y,z;
   given A such that A8: A in P & x in A & y in A;
   given B such that A9: B in P & y in B & z in B;
       A = B or A misses B by A8,A9,Def6;
   hence thesis by A8,A9,XBOOLE_0:3;
  end;
  consider R being Equivalence_Relation of X such that
  A10: for x,y holds [x,y] in R iff x in X & y in X &
      P[x,y] from ExEqRel(A4,A6,A7);
  take R;
     P = Class R
  proof
      now let A;
    A11: now assume A12: A in P;
    then A13: A <> {} by A2,Def6;
     consider x being Element of A;
     A14: x in X by A13,TARSKI:def 3;
        now let y;
      A15: now assume y in A;
       then [y,x] in R by A10,A12,A14;
       hence y in Class(R,x) by Th27;
      end;
         now assume y in Class(R,x);
       then [y,x] in R by Th27;
       then consider B such that A16: B in P & y in B & x in B by A10;
       A meets B by A13,A16,XBOOLE_0:3;
       hence y in A by A12,A16,Def6;
      end;
      hence y in A iff y in Class(R,x) by A15;
     end;
     then A = Class(R,x) by TARSKI:2;
     hence A in Class R by A14,Def5;
    end;
       now assume A in Class R;
     then consider x such that A17: x in X & A = Class(R,x) by Def5;
        x in Class(R,x) by A17,Th28;
     then [x,x] in R by Th27;
     then consider B such that A18: B in P & x in B & x in B by A10;
     A = B
     proof
         now let y;
       A19: now assume y in A;
        then [y,x] in R by A17,Th27;
        then consider C such that A20: C in P & y in C & x in C by A10;
        B meets C by A18,A20,XBOOLE_0:3;
        hence y in B by A18,A20,Def6;
       end;
          now assume y in B;
        then [y,x] in R by A10,A18;
        hence y in A by A17,Th27;
       end;
       hence y in A iff y in B by A19;
      end;
      hence thesis by TARSKI:2;
     end;
     hence A in P by A18;
    end;
    hence A in P iff A in Class R by A11;
   end;
   hence thesis by SETFAM_1:44;
  end;
  hence thesis;
 end;
 X = {} implies ex EqR st P = Class EqR
 proof
  assume A21: X = {};
  then A22: P = {} by Def6;
  consider EqR1;
  take EqR1;
  thus P = Class EqR1 by A21,A22,Th40;
 end;
 hence thesis by A1;
end;

theorem
  for x st x in X holds [x,y] in EqR iff Class(EqR,x) = Class(EqR,y) by Lm2;

theorem
  x in Class EqR implies ex y being Element of X st x = Class(EqR,y)
proof
 assume A1: x in Class EqR;
 then reconsider x as Subset of X;
 consider y such that A2: y in X & x = Class(EqR,y) by A1,Def5;
 reconsider y as Element of X by A2;
 take y;
 thus thesis by A2;
end;

begin :: Addenda

:: from FSM_1, PARTIT1, 2005.02.06, A.T.

registration let X be non empty set;
 cluster -> non empty a_partition of X;
 coherence
  proof let P be a_partition of X;
    consider x being Element of X;
    union P = X by Def6;
    then ex A being set st x in A & A in P by TARSKI:def 4;
   hence thesis;
  end;
end;

:: from PUA2MSS1

registration let X be set;
  cluster -> with_non-empty_elements a_partition of X;
  coherence
  proof let P be a_partition of X; assume
A1:  {} in P;
    then X <> {} by Def6;
   hence contradiction by A1,Def6;
  end;
end;

definition let X be set, R be Equivalence_Relation of X;
  redefine func Class R -> a_partition of X;
  coherence by Th42;
end;

:: from PRALG_3

registration let I be non empty set, R be Equivalence_Relation of I;
  cluster Class R -> non empty;
  coherence;
end;

registration let I be non empty set, R be Equivalence_Relation of I;
  cluster Class R -> with_non-empty_elements;
  coherence;
end;

notation let I be non empty set, R be Equivalence_Relation of I;
  let x be Element of I;
  synonym EqClass(R,x) for Class(R,x);
end;

definition let I be non empty set, R be Equivalence_Relation of I;
  let x be Element of I;
  redefine func EqClass(R,x) -> Element of Class R;
  coherence
   proof
    thus Class(R,x) is Element of Class R by Def5;
   end;
end;

definition let X be set;
  func SmallestPartition X -> a_partition of X equals
    Class id X;
  coherence;
end;

theorem
 for X being non empty set holds
   SmallestPartition X = {{x} where x is Element of X: not contradiction}
  proof let X be non empty set;
   set Y = {{x} where x is Element of X: not contradiction};
   hereby let x be set; assume x in SmallestPartition X;
    then consider y being set such that
A1: y in X & x = Class(id X, y) by Def5;
    x = {y} by A1,Th33;
    hence x in Y by A1;
   end;
   let x be set; assume x in Y;
   then consider y being Element of X such that
A2:x = {y};
   Class(id X, y) = x by A2,Th33;
   hence thesis by Def5;
  end;

:: from T_1TOPSP, 30.12.2006, AK

:: Classes of partitions of a set

reserve X for non empty set, x for Element of X, y,v,w for set;

definition
 let X be non empty set,x be Element of X,S1 be a_partition of X;
 func EqClass(x,S1) -> Subset of X means :Def8:
 x in it & it in S1;
 existence
  proof
   consider EQR being Equivalence_Relation of X such that
A1: S1 = Class EQR by Th43;
   take Class(EQR,x);
   thus x in Class(EQR,x) by Th28;
   thus Class(EQR,x) in S1 by A1,Def5;
  end;
 uniqueness
  proof
   let A,B be Subset of X; assume
A2: x in A & A in S1 & x in B & B in S1;
   then A meets B by XBOOLE_0:3;
   hence A = B by A2,Def6;
  end;
end;

theorem Th47:
 for S1,S2 being a_partition of X st
 (for x being Element of X holds EqClass(x,S1) = EqClass(x,S2)) holds S1=S2
  proof
   let S1,S2 be a_partition of X;
   assume
A1: for x being Element of X holds EqClass(x,S1) = EqClass(x,S2);
       now let P be Subset of X;
    thus P in S1 implies P in S2
     proof
      assume
A2:    P in S1;
      then A3:    P is non empty by Def6;
      consider x being Element of P;
          x in P by A3;
      then reconsider x as Element of X;
          P = EqClass(x,S1) by A2,A3,Def8;
      then P = EqClass(x,S2) by A1;
      hence P in S2 by Def8;
     end;
    thus P in S2 implies P in S1
     proof
      assume
A4:   P in S2;
then A5:   P <> {} by Def6;
      consider x being Element of P;
          x in P by A5;
      then reconsider x as Element of X;
          P = EqClass(x,S2) by A4,A5,Def8;
      then P = EqClass(x,S1) by A1;
      hence P in S1 by Def8;
     end;
   end;
   hence thesis by SETFAM_1:44;
  end;

theorem Th48:
 for X being non empty set holds {X} is a_partition of X
  proof
   let X be non empty set;
   reconsider A1 = {X} as Subset-Family of X by ZFMISC_1:80;
A1: union A1 = X by ZFMISC_1:31;
    for A being Subset of X st A in A1 holds A <> {} & for B being
             Subset of X st B in A1 holds A = B or A misses B
    proof
     let A be Subset of X;
     assume
A2:  A in A1;
     hence A <> {} by TARSKI:def 1;
     let B be Subset of X;
     assume B in A1;
     then B = X by TARSKI:def 1;
     hence A = B or A misses B by A2,TARSKI:def 1;
    end;
   hence {X} is a_partition of X by A1,Def6;
  end;

definition let X be set;
  mode Family-Class of X means :Def9:
   it c= bool bool X;
  existence;
end;

Lm3: for X being set holds {} is Family-Class of X
  proof
    let X be set;
    {} c= bool bool X by XBOOLE_1:2;
    hence thesis by Def9;
  end;

definition let X be set;
 let F be Family-Class of X;
 attr F is partition-membered means :Def10:
   for S being set st S in F holds S is a_partition of X;
end;

registration let X be set;
 cluster partition-membered Family-Class of X;
 existence
  proof
   reconsider E = {} as Family-Class of X by Lm3;
   take E;
   let S be set; assume S in E;
   hence thesis;
  end;
end;

definition let X be set;
  mode Part-Family of X is partition-membered Family-Class of X;
end;

reserve F for Part-Family of X;

registration
 let X be non empty set;
 cluster non empty a_partition of X;
 existence
  proof
   take {X};
   thus thesis by Th48;
 end;
end;

theorem Th49:
 for X being set, p being a_partition of X holds
  {p} is Part-Family of X
   proof
    let X be set;
    let p be a_partition of X;
A1: {p} is Family-Class of X
    proof
      let x be set;
      assume
A2:   x in {p};
      p in bool bool X;
      hence x in bool bool X by A2,TARSKI:def 1;
    end;
   for x be set st x in {p} holds x is a_partition of X by TARSKI:def 1;
    hence thesis by A1,Def10;
end;

registration let X be set;
 cluster non empty Part-Family of X;
 existence
  proof
   consider p being a_partition of X;
    reconsider P = {p} as Part-Family of X by Th49;
   take P;
   thus thesis;
  end;
end;

theorem Th50:
 for S1 being a_partition of X,
     x,y being Element of X holds
    EqClass(x,S1) meets EqClass(y,S1) implies EqClass(x,S1) = EqClass(y,S1)
     proof
      let S1 be a_partition of X;
      let x,y be Element of X;
      assume
A1:    EqClass(x,S1) meets EqClass(y,S1);
      consider EQR being Equivalence_Relation of X such that
A2:    S1 = Class EQR by Th43;
A3:    Class(EQR,x) = EqClass(x,S1)
       proof
A4:     x in Class(EQR,x) by Th28;
           Class(EQR,x) in S1 by A2,Def5;
       hence thesis by A4,Def8;
      end;
      Class(EQR,y) = EqClass(y,S1)
       proof
A5:     y in Class(EQR,y) by Th28;
           Class(EQR,y) in S1 by A2,Def5;
       hence thesis by A5,Def8;
      end;
      hence thesis by A1,A3,Th32;
     end;

Lm4:
 for A being set holds
  A in {EqClass(x,S) where S is a_partition of X : S in F} implies
  ex T being a_partition of X st T in F & A = EqClass(x,T)
   proof
    let A be set;
    assume A in {EqClass(x,S) where S is a_partition of X : S in F};
    then consider S being a_partition of X such that
A1:  A = EqClass(x,S) & S in F;
    take S;
    thus thesis by A1;
   end;

theorem
 for A being set,X being non empty set,S being a_partition of X holds
 A in S implies ex x being Element of X st A = EqClass(x,S)
  proof
   let A be set,X be non empty set,S be a_partition of X;
   assume
A1: A in S;
   then A is non empty by Def6;
   then consider x being set such that
A2: x in A by XBOOLE_0:def 1;
   take x;
   thus thesis by A1,A2,Def8;
  end;

definition
 let X be non empty set,F be non empty Part-Family of X;
 func Intersection F -> non empty a_partition of X means
 for x being Element of X holds EqClass(x,it)
  = meet{EqClass(x,S) where S is a_partition of X : S in F};
 uniqueness
  proof
   given S1,S2 being a_partition of X such that
A1: for x being Element of X holds
     EqClass(x,S1) = meet{EqClass(x,S) where S is a_partition of X : S in F}
     and
A2: for x being Element of X holds
     EqClass(x,S2) = meet{EqClass(x,S) where S is a_partition of X : S in F}
     and
A3: not S1 = S2;
   now let x be Element of X;
    EqClass(x,S1) =
      meet{EqClass(x,S) where S is a_partition of X : S in F} by A1;
    hence EqClass(x,S1) = EqClass(x,S2) by A2;
   end;
   hence contradiction by A3,Th47;
  end;
 existence
  proof
   thus ex G being non empty a_partition of X st
         for x being Element of X holds EqClass(x,G)
              = meet{EqClass(x,S) where S is a_partition of X : S in F}
    proof
     set G = {meet{EqClass(x,S) where S is a_partition of X : S in F}
                   where x is Element of X : not contradiction };
     G c= bool X
      proof
       let y be set;
       assume y in G;
       then consider x being Element of X such that
A4:    y = meet{EqClass(x,S) where S is a_partition of X : S in F};
       y c= X
        proof
         let e be set;
         assume
A5:      e in y;
         consider T being set such that
A6:      T in F by XBOOLE_0:def 1;
         reconsider T as a_partition of X by A6,Def10;
         EqClass(x,T) in {EqClass(x,S) where S is a_partition of X :
           S in F } by A6;
         then consider f being set such that
A7:      f in {EqClass(x,S) where S is a_partition of X : S in F};
         consider S being a_partition of X such that
A8:      f = EqClass(x,S) & S in F by A7;
             e in EqClass(x,S) by A4,A5,A7,A8,SETFAM_1:def 1;
         hence e in X;
        end;
       hence y in bool X;
      end;
     then reconsider G as Subset-Family of X;
     G is a_partition of X
      proof
       per cases;
       case X <> {};
       thus union G = X
        proof
         thus union G c= X;
         thus X c= union G
          proof
           let a be set;
           assume a in X;
           then reconsider a1=a as Element of X;
           consider T being set such that
A9:        T in F by XBOOLE_0:def 1;
           reconsider T as a_partition of X by A9,Def10;
A10:        EqClass(a1,T) in
           {EqClass(a1,S) where S is a_partition of X : S in F} by A9;
           for T being set
           st T in {EqClass(a1,S) where S is a_partition of X : S in F} holds
           a1 in T
            proof
             let T be set;
             assume
                 T in {EqClass(a1,S) where S is a_partition of X : S in F};
             then consider A being a_partition of X such that
A11:          T = EqClass(a1,A)
             and A in F;
             thus thesis by A11,Def8;
            end;
           then A12:        a in meet{EqClass(a1,S) where S is a_partition of X
: S in F}
                                       by A10,SETFAM_1:def 1;
               meet{EqClass(a1,S) where S is a_partition of X : S in F} in G;
           hence a in union G by A12,TARSKI:def 4;
          end;
        end;
       let A be Subset of X;
       assume
A13:   A in G;
       then consider x being Element of X such that
A14:    A = meet{EqClass(x,S) where S is a_partition of X : S in F};
       consider T being set such that
A15:    T in F by XBOOLE_0:def 1;
       reconsider T as a_partition of X by A15,Def10;
A16:    EqClass(x,T) in {EqClass(x,S) where S is a_partition of X : S in F}
                     by A15;
           for Y being set
       st Y in {EqClass(x,S) where S is a_partition of X : S in F} holds
       x in Y
        proof
         let Y be set;
         assume Y in {EqClass(x,S) where S is a_partition of X : S in F};
         then ex T being a_partition of X st Y = EqClass(x,T) & T in F;
         hence x in Y by Def8;
        end;
       hence A<>{} by A14,A16,SETFAM_1:def 1;
       consider x being Element of X such that
A17:    A = meet{EqClass(x,S) where S is a_partition of X : S in F} by A13;
       let B be Subset of X;
       assume B in G;
       then consider y being Element of X such that
A18:    B = meet{EqClass(y,S) where S is a_partition of X : S in F};
       thus A = B or A misses B
        proof
A19:      {EqClass(x,S) where S is a_partition of X : S in F} is non empty
           proof
            consider T being set such that
A20:         T in F by XBOOLE_0:def 1;
            reconsider T as a_partition of X by A20,Def10;
            EqClass(x,T) in
            {EqClass(x,S) where S is a_partition of X : S in F} by A20;
            hence thesis;
           end;
A21:      {EqClass(y,S) where S is a_partition of X : S in F} is non empty
           proof
            consider T being set such that
A22:         T in F by XBOOLE_0:def 1;
            reconsider T as a_partition of X by A22,Def10;
                EqClass(y,T) in
            {EqClass(y,S) where S is a_partition of X : S in F} by A22;
            hence thesis;
           end;
             now
         assume A meets B;
         then consider c being set such that
A23:      c in A & c in B by XBOOLE_0:3;
              c in meet{EqClass(x,S) where S is a_partition of X : S in F} /\
              meet{EqClass(y,S) where S is a_partition of X : S in F}
          by A17,A18,A23,XBOOLE_0:def 3;
         then A24: c in meet({EqClass(x,S) where S is a_partition of X : S in F
}
             \/ {EqClass(y,S) where S is a_partition of X : S in F})
                                                      by A19,A21,SETFAM_1:10;

A25:     now let T be a_partition of X;
          assume
              T in F;
          then EqClass(x,T) in {EqClass(x,S) where S is a_partition of X : S
in F};
          then EqClass(x,T) in {EqClass(x,S) where S is a_partition of X : S
in F}
        \/ {EqClass(y,S) where S is a_partition of X : S in F} by
XBOOLE_0:def 2;
          hence c in EqClass(x,T) by A24,SETFAM_1:def 1;
         end;
         A26: now
          let T be a_partition of X;
          assume T in F;
          then EqClass(y,T) in {EqClass(y,S) where S is a_partition of X : S
in F};
          then EqClass(y,T) in {EqClass(x,S) where S is a_partition of X : S
in F}
        \/ {EqClass(y,S) where S is a_partition of X : S in F} by
XBOOLE_0:def 2;
          hence c in EqClass(y,T) by A24,SETFAM_1:def 1;
         end;
A27:      for T being a_partition of X st T in F holds
         ex A being set st A in EqClass(x,T) & A in EqClass(y,T)
          proof
           let T be a_partition of X;
           assume
A28:        T in F;
           take c;
           thus thesis by A25,A26,A28;
          end;
A29:      for T being a_partition of X st T in F holds
         (EqClass(x,T)) meets (EqClass(y,T))
          proof
           let T be a_partition of X; assume T in F;
           then consider A being set such that
A30:        A in EqClass(x,T) & A in EqClass(y,T) by A27;
           thus thesis by A30,XBOOLE_0:3;
          end;
A31:       for T being a_partition of X st T in F holds
                             EqClass(x,T) = EqClass(y,T)
          proof
           let T be a_partition of X;
           assume T in F;
           then EqClass(x,T) meets EqClass(y,T) by A29;
           hence thesis by Th50;
          end;
          {EqClass(x,S) where S is a_partition of X : S in F} =
          {EqClass(y,S) where S is a_partition of X : S in F}
           proof
            thus {EqClass(x,S) where S is a_partition of X : S in F}
             c= {EqClass(y,S) where S is a_partition of X : S in F}
             proof
              let A be set;
              assume
              A in {EqClass(x,S) where S is a_partition of X : S in F};
              then consider T being a_partition of X such that
A32:           T in F &
              A = EqClass(x,T) by Lm4;
              A = EqClass(y,T) by A31,A32;
              hence thesis by A32;
             end;
            thus {EqClass(y,S) where S is a_partition of X : S in F}
             c= {EqClass(x,S) where S is a_partition of X : S in F}
             proof
              let A be set;
              assume A in {EqClass(y,S) where S is a_partition of X : S in F};
              then consider T being a_partition of X such that
A33:           T in F & A = EqClass(y,T) by Lm4;
                  A = EqClass(x,T) by A31,A33;
              hence thesis by A33;
             end;
           end;
          hence thesis by A17,A18;
         end;
         hence thesis;
        end;
       end;
       case X = {};
       hence G = {};
      end;
      end;
     then reconsider G as non empty a_partition of X;
     take G;
         for x being Element of X holds EqClass(x,G)
         = meet{EqClass(x,S) where S is a_partition of X : S in F}
      proof
       let x be Element of X;
A34:     meet{EqClass(x,S) where S is a_partition of X : S in F} in G;
        x in meet{EqClass(x,S) where S is a_partition of X : S in F}
        proof
A35:       {EqClass(x,S) where S is a_partition of X : S in F} is non empty
           proof
            consider T being set such that
A36:          T in F by XBOOLE_0:def 1;
            reconsider T as a_partition of X by A36,Def10;
                EqClass(x,T) in
            {EqClass(x,S) where S is a_partition of X : S in F} by A36;
            hence thesis;
           end;
             now
          let Y be set;
          assume Y in {EqClass(x,S) where S is a_partition of X : S in F};
          then ex T being a_partition of X st
          Y = EqClass(x,T) & T in F;
          hence x in Y by Def8;
         end;
         hence thesis by A35,SETFAM_1:def 1;
        end;
       hence thesis by A34,Def8;
      end;
     hence thesis;
  end;
 end;
end;

theorem Th52:
 for X being non empty set,
     S being a_partition of X,
     A being Subset of S holds (union S) \ (union A) = union (S \ A)
      proof
       let X be non empty set;
       let S be a_partition of X;
       let A be Subset of S;
       thus (union S) \ (union A) c= union (S \ A)
        proof
         let y be set;
         assume y in (union S) \ (union A);
         then A1:       y in (union S) & not y in (union A) by XBOOLE_0:def 4;
         then consider z being set such that
A2:       y in z & z in S by TARSKI:def 4;
             not z in A by A1,A2,TARSKI:def 4;
         then y in z & z in S \ A by A2,XBOOLE_0:def 4;
         hence thesis by TARSKI:def 4;
        end;
       thus union (S \ A) c= (union S) \ (union A)
        proof
         let y be set;
         assume y in union(S \ A);
         then consider z being set such that
A3:       y in z & z in S \ A by TARSKI:def 4;
A4:      y in z & z in S & not z in A by A3,XBOOLE_0:def 4;
         then A5:      y in union S by TARSKI:def 4;
         A6: now
          let w be set;
          assume
A7:        w in A;
          then w in S;
          then z misses w by A4,A7,Def6;
          hence not y in w by A3,XBOOLE_0:3;
         end;
             now
          assume y in union A;
          then consider v being set such that
A8:        y in v & v in A by TARSKI:def 4;
          thus contradiction by A6,A8;
         end;
         hence thesis by A5,XBOOLE_0:def 4;
        end;
      end;

theorem
 for X being non empty set,A being Subset of X,
     S being a_partition of X holds A in S implies union(S \ {A}) = X \ A
      proof
       let X be non empty set;
       let A be Subset of X;
       let S be a_partition of X;
       assume
A1:     A in S;
       {A} c= S
       proof
        let y be set;
        assume y in {A};
        hence thesis by A1,TARSKI:def 1;
       end;
       then reconsider AA = {A} as Subset of S;
       thus union (S \ {A}) = (union S) \ (union AA) by Th52
                           .= X \ (union {A}) by Def6
                           .= X \ A by ZFMISC_1:31;
      end;

