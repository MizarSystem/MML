:: Real Function Differentiability - Part II
::  by Jaros{\l}aw Kotowicz and Konrad Raczkowski
::
:: Received January 10, 1991
:: Copyright (c) 1991 Association of Mizar Users
 
environ

 vocabularies NUMBERS, REAL_1, SUBSET_1, SEQ_1, FDIFF_1, FUNCT_1, RCOMP_1,
      PARTFUN1, ARYTM_1, VALUED_0, SEQ_2, ORDINAL2, CARD_1, RELAT_1, ARYTM_3,
      XREAL_0, ORDINAL1, XXREAL_0, COMPLEX1, NAT_1, TARSKI, VALUED_1, FUNCT_2,
      FUNCOP_1, XXREAL_1, FCONT_1, SQUARE_1, XBOOLE_0, ORDINAL4, LIMFUNC1;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XXREAL_0,
      XREAL_0, COMPLEX1, REAL_1, NAT_1, FUNCT_1, FUNCT_2, VALUED_0, VALUED_1,
      SEQ_1, RELSET_1, SEQ_2, PARTFUN1, SQUARE_1, PARTFUN2, RFUNCT_1, RCOMP_1,
      FCONT_1, FDIFF_1, LIMFUNC1, RECDEF_1;
 constructors PARTFUN1, REAL_1, SQUARE_1, NAT_1, COMPLEX1, SEQ_2, SEQM_3,
      PROB_1, RCOMP_1, PARTFUN2, RFUNCT_1, RFUNCT_2, FCONT_1, LIMFUNC1,
      FDIFF_1, SEQ_1, VALUED_1, RECDEF_1, RELSET_1, BINOP_2, RVSUM_1;
 registrations ORDINAL1, RELSET_1, NUMBERS, XCMPLX_0, XXREAL_0, XREAL_0, NAT_1,
      MEMBERED, SEQM_3, RCOMP_1, RFUNCT_2, FDIFF_1, FCONT_3, VALUED_0,
      VALUED_1, FUNCT_2, FUNCOP_1, SEQ_4;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, XBOOLE_0, FDIFF_1, SQUARE_1, SUBSET_1, LIMFUNC1, PROB_1,
      VALUED_0, VALUED_1, SEQM_3;
 theorems TARSKI, FUNCT_1, FUNCT_2, NAT_1, SEQ_1, SEQ_2, SEQM_3, SEQ_4,
      ABSVALUE, PARTFUN1, PARTFUN2, RCOMP_1, RFUNCT_1, RFUNCT_2, FCONT_1,
      FDIFF_1, SCHEME1, ROLLE, FCONT_3, FUNCT_3, RELAT_1, RELSET_1, XBOOLE_0,
      XBOOLE_1, XCMPLX_0, XCMPLX_1, COMPLEX1, FUNCOP_1, XREAL_1, XXREAL_0,
      ORDINAL1, VALUED_1, XXREAL_1, VALUED_0;
 schemes NAT_1, RECDEF_1, SEQ_1, SCHEME1, FUNCT_2;

begin

reserve x for set;
reserve x0,r,r1,r2,g,g1,g2,p for Real;
reserve n,m,k,l for Element of NAT;
reserve a,b,d for Real_Sequence;
reserve h,h1,h2 for convergent_to_0 Real_Sequence;
reserve c,c1 for constant Real_Sequence;
reserve A for open Subset of REAL;
reserve f,f1,f2 for PartFunc of REAL,REAL;
reserve L for LINEAR;
reserve R for REST;

definition
  let h;
  redefine func -h -> convergent_to_0 Real_Sequence;
  coherence
  proof
    h is non-zero by FDIFF_1:def 1;
    then
A1: -h is non-zero by SEQ_1:53;
A2: h is convergent by FDIFF_1:def 1;
    lim h = 0 by FDIFF_1:def 1;
    then
A3: lim (-h) = -0 by A2,SEQ_2:24;
    -h is convergent by A2,SEQ_2:23;
    hence thesis by A1,A3,FDIFF_1:def 1;
  end;
end;

theorem Th1:
  a is convergent & b is convergent & lim a = lim b & (for n holds
  d.(2*n) = a.n & d.(2*n + 1) = b.n) implies d is convergent & lim d = lim a
proof
  assume that
A1: a is convergent and
A2: b is convergent and
A3: lim a = lim b and
A4: for n holds d.(2*n) = a.n & d.(2*n+1) = b.n;
A5: now
    let r be real number;
    assume
A6: 0<r;
    then consider k1 be Element of NAT such that
A7: for m st k1 <= m holds abs(a.m - lim a) < r by A1,SEQ_2:def 7;
    consider k2 be Element of NAT such that
A8: for m st k2 <= m holds abs(b.m - lim b) < r by A2,A6,SEQ_2:def 7;
    take n = max(2*k1,2*k2+1);
    let m;
    assume
A9: n<=m;
    then
A10: 2*k2 + 1 <= m by XXREAL_0:30;
    consider n such that
A11: m = 2*n or m = 2*n + 1 by SCHEME1:1;
A12: 2*k1 <= m by A9,XXREAL_0:30;
    now
      per cases by A11;
      suppose
A13:    m = 2*n;
        then
A14:    n >= k1 by A12,XREAL_1:70;
        abs(d.m - lim a) = abs(a.n - lim a) by A4,A13;
        hence abs(d.m - lim a) < r by A7,A14;
      end;
      suppose
A15:    m = 2*n + 1;
A16:    now
          assume n < k2;
          then 2*n < 2*k2 by XREAL_1:70;
          hence contradiction by A10,A15,XREAL_1:8;
        end;
        abs(d.m - lim a) = abs(b.n - lim a) by A4,A15;
        hence abs(d.m - lim a) < r by A3,A8,A16;
      end;
    end;
    hence abs(d.m - lim a) < r;
  end;
  hence d is convergent by SEQ_2:def 6;
  hence thesis by A5,SEQ_2:def 7;
end;

theorem Th2:
  (for n holds a.n = 2*n) implies a is increasing sequence of NAT
proof
  assume
A1: for n holds a.n = 2*n;
A2: a is increasing
  proof
    let n;
A3: 2*n + 0 < 2*n + 2 by XREAL_1:10;
    2*n + 2 = 2*(n + 1) .= a.(n + 1) by A1;
    hence a.n < a.(n+1) by A1,A3;
  end;
A4: now
    let x;
    assume x in NAT;
    then reconsider n = x as Element of NAT;
    a.n = 2*n by A1;
    hence a.x in NAT;
  end;
  dom a = NAT by FUNCT_2:def 1;
  hence thesis by A2,A4,FUNCT_2:5;
end;

theorem Th3:
  (for n holds a.n = 2*n + 1) implies a is increasing sequence of NAT
proof
  assume
A1: for n holds a.n = 2*n + 1;
A2: a is increasing
  proof
    let n;
A3: 2*n + 1+0 < 2*n + 1+2 by XREAL_1:10;
    2*n + 1+2 = 2*(n + 1) + 1 .= a.(n + 1) by A1;
    hence a.n < a.(n+1) by A1,A3;
  end;
A4: now
    let x;
    assume x in dom a;
    then reconsider n = x as Element of NAT;
    a.n = 2*n+1 by A1;
    hence a.x in NAT;
  end;
  dom a = NAT by FUNCT_2:def 1;
  hence thesis by A2,A4,FUNCT_2:5;
end;

theorem Th4:
  rng c = {x0} implies c is convergent & lim c = x0 & h + c is
  convergent & lim(h + c) = x0
proof
  assume
A1: rng c = {x0};
  thus c is convergent;
  x0 in rng c by A1,TARSKI:def 1;
  hence
A2: lim c = x0 by SEQ_4:40;
A3: h is convergent by FDIFF_1:def 1;
  hence h + c is convergent by SEQ_2:19;
  lim h = 0 by FDIFF_1:def 1;
  hence lim (h + c) = 0 + x0 by A2,A3,SEQ_2:20
    .= x0;
end;

theorem Th5:
  rng a = {r} & rng b = {r} implies a = b
proof
  assume that
A1: rng a = {r} and
A2: rng b = {r};
  now
    let n;
    a.n in rng a by VALUED_0:28;
    then
A3: a.n = r by A1,TARSKI:def 1;
    b.n in rng b by VALUED_0:28;
    hence a.n = b.n by A2,A3,TARSKI:def 1;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th6:
  a is subsequence of h implies a is convergent_to_0 Real_Sequence
proof
  assume
A1: a is subsequence of h;
  then consider I be increasing sequence of NAT such that
A2: a = h*I by VALUED_0:def 17;
A3: h is non-zero by FDIFF_1:def 1;
  now
    given n such that
A4: a.n = 0;
    h.(I.n) <> 0 by A3,SEQ_1:7;
    hence contradiction by A2,A4,FUNCT_2:21;
  end;
  then
A5: a is non-zero by SEQ_1:7;
A6: h is convergent by FDIFF_1:def 1;
  then
A7: a is convergent by A1,SEQ_4:29;
  lim h = 0 by FDIFF_1:def 1;
  then lim a = 0 by A1,A6,SEQ_4:30;
  hence thesis by A7,A5,FDIFF_1:def 1;
end;

theorem Th7:
  (for h,c st rng c = {g} & rng (h + c) c= dom f & {g} c= dom f
holds h"(#)(f/*(h+c) - f/*c) is convergent) implies for h1,h2,c st rng c = {g}
  & rng (h1 + c) c= dom f & rng (h2 + c) c= dom f & {g} c= dom f holds lim (h1"
  (#)(f/*(h1+c) - f/*c)) = lim(h2"(#)(f/*(h2+c) - f/*c))
proof
  assume
A1: for h,c st rng c = {g} & rng (h+c) c= dom f & {g} c= dom f holds h"
  (#)(f/*(h+c) - f/*c) is convergent;
  let h1,h2,c such that
A2: rng c = {g} and
A3: rng (h1+c) c= dom f and
A4: rng (h2+c) c= dom f and
A5: {g} c= dom f;
  deffunc G(Element of NAT) = h2.$1;
  deffunc F(Element of NAT) = h1.$1;
  consider a such that
A6: for n holds a.(2*n) = F(n) & a.(2*n + 1) = G(n) from SCHEME1:sch 2;
A7: h2 is non-zero by FDIFF_1:def 1;
A8: h1 is non-zero by FDIFF_1:def 1;
  now
    let n;
    consider m such that
A9: n = 2*m or n = 2*m + 1 by SCHEME1:1;
    now
      per cases by A9;
      suppose
        n = 2*m;
        then a.n = h1.m by A6;
        hence a.n <> 0 by A8,SEQ_1:7;
      end;
      suppose
        n = 2*m + 1;
        then a.n = h2.m by A6;
        hence a.n <> 0 by A7,SEQ_1:7;
      end;
    end;
    hence a.n <> 0;
  end;
  then
A10: a is non-zero by SEQ_1:7;
A11: lim h1 = 0 by FDIFF_1:def 1;
A12: lim h2 = 0 by FDIFF_1:def 1;
A13: h2 is convergent by FDIFF_1:def 1;
A14: h1 is convergent by FDIFF_1:def 1;
  then
A15: a is convergent by A6,A11,A13,A12,Th1;
  lim a = 0 by A6,A14,A11,A13,A12,Th1;
  then reconsider a as convergent_to_0 Real_Sequence by A15,A10,FDIFF_1:def 1;
A16: rng (a + c) c= dom f
  proof
    let x;
    assume x in rng (a + c);
    then consider n such that
A17: x = (a + c).n by FUNCT_2:190;
    consider m such that
A18: n = 2*m or n = 2*m + 1 by SCHEME1:1;
    now
      per cases by A18;
      suppose
A19:    n = 2*m;
A20:    (h1 + c).m in rng (h1 + c) by VALUED_0:28;
        (a + c).n = a.n + c.n by SEQ_1:11
          .= h1.m + c.n by A6,A19
          .= h1.m + c.m by VALUED_0:23
          .= (h1 + c).m by SEQ_1:11;
        hence (a + c).n in dom f by A3,A20;
      end;
      suppose
A21:    n = 2*m + 1;
A22:    (h2 + c).m in rng (h2 + c) by VALUED_0:28;
        (a + c).n = a.n + c.n by SEQ_1:11
          .= h2.m + c.n by A6,A21
          .= h2.m + c.m by VALUED_0:23
          .= (h2 + c).m by SEQ_1:11;
        hence (a + c).n in dom f by A4,A22;
      end;
    end;
    hence thesis by A17;
  end;
  then
A23: a"(#)(f/*(a+c) - f/*c) is convergent by A1,A2,A5;
  deffunc G(Element of NAT) = 2*$1+1;
  consider d such that
A24: for n holds d.n = G(n) from SEQ_1:sch 1;
  reconsider I2 = d as increasing sequence of NAT by A24,Th3;
  now
    let n;
    thus ((a"(#)(f/*(a+c) - f/*c))*I2).n = (a"(#)(f/*(a+c) - f/*c)).(I2.n) by
    FUNCT_2:21
      .= (a"(#)(f/*(a+c) - f/*c)).(2*n + 1) by A24
      .= (a").(2*n + 1) * (f/*(a+c) - f/*c).(2*n + 1) by SEQ_1:12
      .= (a").(2*n + 1) * ((f/*(a+c)).(2*n + 1) - (f/*c).(2*n + 1)) by
    RFUNCT_2:6
      .= (a").(2*n + 1) * (f.((a+c).(2*n+1)) - (f/*c).(2*n+1)) by A16,
    FUNCT_2:185
      .= (a").(2*n + 1) * (f.(a.(2*n+1) + c.(2*n+1)) - (f/*c).(2*n+1)) by
    SEQ_1:11
      .= (a").(2*n + 1) * (f.(h2.n + c.(2*n+1)) - (f/*c).(2*n+1)) by A6
      .= (a").(2*n + 1) * (f.(h2.n + c.n) - (f/*c).(2*n+1)) by VALUED_0:23
      .= (a").(2*n + 1) * (f.((h2 + c).n) - (f/*c).(2*n+1)) by SEQ_1:11
      .= (a").(2*n + 1) * ((f/*(h2+c)).n - (f/*c).(2*n+1)) by A4,FUNCT_2:185
      .= (a.(2*n + 1))" * ((f/*(h2+c)).n - (f/*c).(2*n+1)) by VALUED_1:10
      .= (h2.n)" * ((f/*(h2+c)).n - (f/*c).(2*n+1)) by A6
      .= (h2").n * ((f/*(h2+c)).n - (f/*c).(2*n+1)) by VALUED_1:10
      .= (h2").n * ((f/*(h2+c)).n - f.(c.(2*n+1))) by A2,A5,FUNCT_2:185
      .= (h2").n * ((f/*(h2+c)).n - f.(c.n)) by VALUED_0:23
      .= (h2").n * ((f/*(h2+c)).n - (f/*c).n) by A2,A5,FUNCT_2:185
      .= (h2").n * (f/*(h2+c) - (f/*c)).n by RFUNCT_2:6
      .= ((h2")(#)(f/*(h2+c) - (f/*c))).n by SEQ_1:12;
  end;
  then
A25: (a"(#)(f/*(a+c) - f/*c))*I2 = h2"(#)(f/*(h2+c) - f/*c) by FUNCT_2:
  113;
  (a"(#)(f/*(a+c) - f/*c))*I2 is subsequence of a"(#)(f/*(a+c) - f/*c) by
  VALUED_0:def 17;
  then
A26: lim ((a"(#)(f/*(a+c) - f/*c))*I2) = lim (a"(#) (f/*(a+c) - f/*c)) by
  A23,SEQ_4:30;
  deffunc F(Element of NAT) = 2*$1;
  consider b such that
A27: for n holds b.n = F(n) from SEQ_1:sch 1;
  reconsider I1 = b as increasing sequence of NAT by A27,Th2;
  now
    let n;
    thus ((a"(#)(f/*(a+c) - f/*c))*I1).n = (a"(#)(f/*(a+c) - f/*c)).(I1.n) by
    FUNCT_2:21
      .= (a"(#)(f/*(a+c) - f/*c)).(2*n) by A27
      .= (a").(2*n) * (f/*(a+c) - f/*c).(2*n) by SEQ_1:12
      .= (a").(2*n) * ((f/*(a+c)).(2*n) - (f/*c).(2*n)) by RFUNCT_2:6
      .= (a").(2*n) * (f.((a+c).(2*n)) - (f/*c).(2*n)) by A16,FUNCT_2:185
      .= (a").(2*n) * (f.(a.(2*n) + c.(2*n)) - (f/*c).(2*n)) by SEQ_1:11
      .= (a").(2*n) * (f.(h1.n + c.(2*n)) - (f/*c).(2*n)) by A6
      .= (a").(2*n) * (f.(h1.n + c.n) - (f/*c).(2*n)) by VALUED_0:23
      .= (a").(2*n) * (f.((h1 + c).n) - (f/*c).(2*n)) by SEQ_1:11
      .= (a").(2*n) * ((f/*(h1+c)).n - (f/*c).(2*n)) by A3,FUNCT_2:185
      .= (a.(2*n))" * ((f/*(h1+c)).n - (f/*c).(2*n)) by VALUED_1:10
      .= (h1.n)" * ((f/*(h1+c)).n - (f/*c).(2*n)) by A6
      .= (h1").n * ((f/*(h1+c)).n - (f/*c).(2*n)) by VALUED_1:10
      .= (h1").n * ((f/*(h1+c)).n - f.(c.(2*n))) by A2,A5,FUNCT_2:185
      .= (h1").n * ((f/*(h1+c)).n - f.(c.n)) by VALUED_0:23
      .= (h1").n * ((f/*(h1+c)).n - (f/*c).n) by A2,A5,FUNCT_2:185
      .= (h1").n * (f/*(h1+c) - (f/*c)).n by RFUNCT_2:6
      .= ((h1")(#)(f/*(h1+c) - (f/*c))).n by SEQ_1:12;
  end;
  then
A28: (a"(#)(f/*(a+c) - f/*c))*I1 = h1"(#)(f/*(h1+c) - f/*c) by FUNCT_2:
  113;
  (a"(#)(f/*(a+c) - f/*c))*I1 qua Real_Sequence is subsequence of a"(#)(f
  /*(a+c) - f/*c) by VALUED_0:def 17;
  hence thesis by A23,A26,A28,A25,SEQ_4:30;
end;

theorem Th8:
  (ex N be Neighbourhood of r st N c= dom f) implies ex h,c st rng
  c = {r} & rng (h+c) c= dom f & {r} c= dom f
proof
  given N be Neighbourhood of r such that
A1: N c= dom f;
  reconsider a = NAT --> r as Real_Sequence by FUNCOP_1:57;
  consider g be real number such that
A2: 0 < g and
A3: N = ].r - g, r + g.[ by RCOMP_1:def 7;
  reconsider a as constant Real_Sequence;
  deffunc G(Element of NAT) = g/($1+2);
  consider b such that
A4: for n holds b.n = G(n) from SEQ_1:sch 1;
A5: lim b = 0 by A4,SEQ_4:46;
A6: b is convergent by A4,SEQ_4:46;
  now
    let n;
    0 < g/(n+2) by A2;
    hence 0 <> b.n by A4;
  end;
  then b is non-zero by SEQ_1:7;
  then reconsider b as convergent_to_0 Real_Sequence by A6,A5,FDIFF_1:def 1;
  take b;
  take a;
  thus rng a = {r}
  proof
    thus rng a c= {r}
    proof
      let x;
      assume x in rng a;
      then ex n st x = a.n by FUNCT_2:190;
      then x = r by FUNCOP_1:13;
      hence thesis by TARSKI:def 1;
    end;
    let x;
    assume x in {r};
    then x = r by TARSKI:def 1
      .= a.0 by FUNCOP_1:13;
    hence thesis by VALUED_0:28;
  end;
  thus rng (b+a) c= dom f
  proof
    let x;
    assume x in rng (b+a);
    then consider n such that
A7: x = (b+a).n by FUNCT_2:190;
    0+1 < n + 2 by XREAL_1:10;
    then g*1 < g*(n+2) by A2,XREAL_1:99;
    then g * (n+2)" < g*(n + 2)*((n + 2)") by XREAL_1:70;
    then g * ((n+2)") < g*((n + 2)*(n + 2)");
    then g * (n+2)" < g * 1 by XCMPLX_0:def 7;
    then g/(n+2) < g by XCMPLX_0:def 9;
    then
A8: r + g/(n+2) < r + g by XREAL_1:8;
A9: r - g < r - 0 by A2,XREAL_1:17;
    r + 0 < r + g/(n+2) by A2,XREAL_1:10;
    then r - g < r + g/(n+2) by A9,XXREAL_0:2;
    then
A10: r + g/(n+2) in {g1: r - g < g1 & g1 < r + g} by A8;
    x = b.n +a.n by A7,SEQ_1:11
      .= g/(n+2) + a.n by A4
      .= g/(n+2) + r by FUNCOP_1:13;
    then x in N by A3,A10,RCOMP_1:def 2;
    hence thesis by A1;
  end;
  let x;
  assume x in {r};
  then x = r by TARSKI:def 1;
  then x in N by RCOMP_1:37;
  hence thesis by A1;
end;

theorem Th9:
  rng a c= dom (f2*f1) implies rng a c= dom f1 & rng (f1/*a) c= dom f2
proof
  assume
A1: rng a c= dom (f2*f1);
  then
A2: f1.:(rng a) c= dom f2 by FUNCT_1:171;
  rng a c= dom f1 by A1,FUNCT_1:171;
  hence thesis by A2,VALUED_0:30;
end;

scheme
  ExIncSeqofNat{ s()->Real_Sequence,P[set] }: ex q being increasing sequence
of NAT st (for n holds P[(s()*q).n]) & for n st (for r st r = s().n holds P[r])
  ex m st n = q.m
provided
A1: for n ex m st n <= m & P[s().m]
proof
  defpred P[Nat,set,set] means for n,m st $2 = n & $3 = m holds n < m & P[s().
  m] & for k st n < k & P[s().k] holds m <= k;
  defpred R[Nat] means P[s().$1];
  ex m1 be Element of NAT st 0 <= m1 & P[s().m1] by A1;
  then
A2: ex m be Nat st R[m];
  consider M be Nat such that
A3: R[M] & for n be Nat st R[n] holds M <= n from NAT_1:sch 5(A2);
  reconsider M9 = M as Element of NAT by ORDINAL1:def 13;
A4: now
    let n;
    consider m such that
A5: n + 1 <= m and
A6: P[s().m] by A1;
    take m;
    thus n < m & P[s().m] by A5,A6,NAT_1:13;
  end;
A7: for n for x be Element of NAT ex y be Element of NAT st P[n,x,y]
  proof
    let n;
    let x be Element of NAT;
    defpred R[Nat] means x < $1 & P[s().$1];
    ex m st R[m] by A4;
    then
A8: ex m be Nat st R[m];
    consider l be Nat such that
A9: R[l] & for k be Nat st R[k] holds l <= k from NAT_1:sch 5(A8);
    take l;
    l in NAT by ORDINAL1:def 13;
    hence thesis by A9;
  end;
  consider F be Function of NAT,NAT such that
A10: F.0 = M9 & for n be Element of NAT holds P[n,F.n,F.(n+1)] from
  RECDEF_1:sch 2(A7);
A11: rng F c= REAL by XBOOLE_1:1;
A12: rng F c= NAT;
A13: dom F = NAT by FUNCT_2:def 1;
  then reconsider F as Real_Sequence by A11,RELSET_1:11;
A14: now
    let n;
    F.n in rng F by A13,FUNCT_1:def 5;
    hence F.n is Element of NAT by A12;
  end;
  now
    let n;
A15: F.(n+1) is Element of NAT by A14;
    F.n is Element of NAT by A14;
    hence F.n < F.(n+1) by A10,A15;
  end;
  then reconsider F as increasing sequence of NAT by SEQM_3:def 11;
A16: for n st P[s().n] ex m st F.m = n
  proof
    defpred R[Nat] means P[s().$1] & for m holds F.m <> $1;
    assume ex n st R[n];
    then
A17: ex n be Nat st R[n];
    consider M1 be Nat such that
A18: R[M1] & for n be Nat st R[n] holds M1 <= n from NAT_1:sch 5(A17);
    defpred H[Nat] means $1 < M1 & P[s().$1] & ex m st F.m = $1;
A19: ex n being Nat st H[n]
    proof
      take M;
A20:  M <> M1 by A10,A18;
      M <= M1 by A3,A18;
      hence M < M1 by A20,XXREAL_0:1;
      thus P[s().M] by A3;
      take 0;
      thus thesis by A10;
    end;
A21: for n being Nat st H[n] holds n <= M1;
    consider X be Nat such that
A22: H[X] & for n being Nat st H[n] holds n <= X from NAT_1:sch 6(A21,
    A19);
A23: for k st X < k & k < M1 holds not P[s().k]
    proof
      given k such that
A24:  X < k and
A25:  k < M1 and
A26:  P[s().k];
      now
        per cases;
        suppose
          ex m st F.m = k;
          hence contradiction by A22,A24,A25,A26;
        end;
        suppose
          for m holds F.m <> k;
          hence contradiction by A18,A25,A26;
        end;
      end;
      hence contradiction;
    end;
    consider m such that
A27: F.m = X by A22;
    M1 in NAT by ORDINAL1:def 13;
    then
A28: F.(m+1) <= M1 by A10,A18,A22,A27;
A29: P[s().(F.(m+1))] by A10,A27;
A30: X < F.(m+1) by A10,A27;
    now
      assume F.(m+1) <> M1;
      then F.(m+1) < M1 by A28,XXREAL_0:1;
      hence contradiction by A23,A30,A29;
    end;
    hence contradiction by A18;
  end;
  take F;
  set q = s()*F;
  defpred S[Nat] means P[q.$1];
A31: for k st S[k] holds S[k+1]
  proof
    let k;
    assume P[q.k];
    P[k,F.k,F.(k+1)] by A10;
    then P[s().(F.(k+1))];
    hence thesis by FUNCT_2:21;
  end;
A32: S[0] by A3,A10,FUNCT_2:21;
  thus for n holds S[n] from NAT_1:sch 1(A32,A31);
  let n;
  assume for r st r = s().n holds P[r];
  then consider m such that
A33: F.m = n by A16;
  take m;
  thus thesis by A33;
end;

theorem
  f.x0 <> r & f is_differentiable_in x0 implies ex N be Neighbourhood of
  x0 st N c= dom f & for g st g in N holds f.g <> r
proof
  assume that
A1: f.x0 <> r and
A2: f is_differentiable_in x0;
  ex N be Neighbourhood of x0 st N c= dom f & ex L,R st for r st r in N
  holds f.r - f.x0 = L.(r-x0) + R.(r-x0) by A2,FDIFF_1:def 5;
  hence thesis by A1,A2,FCONT_3:15,FDIFF_1:32;
end;

Lm1: (ex N be Neighbourhood of x0 st N c= dom f) & (for h,c st rng c = {x0} &
rng (h+c) c= dom f holds h"(#)(f/*(h+c) - f/*c) is convergent) implies f
is_differentiable_in x0 & for h,c st rng c = {x0} & rng (h+c) c= dom f holds
diff(f,x0) = lim (h"(#)(f/*(h+c) - f/*c))
proof
  deffunc G(Real) = 0;
  defpred P[set] means $1 in REAL;
  given N be Neighbourhood of x0 such that
A1: N c= dom f;
  assume
A2: for h,c st rng c = {x0} & rng (h+c) c= dom f holds h"(#)(f/*(h+c) -
  f/*c) is convergent;
  then
A3: for h,c holds rng c = {x0} & rng (h+c) c= dom f & {x0} c= dom f implies
  h"(#)(f/*(h+c) - f/*c) is convergent;
  consider r be real number such that
A4: 0 < r and
A5: N = ].x0 - r,x0 + r.[ by RCOMP_1:def 7;
  consider h,c such that
A6: rng c = {x0} and
A7: rng (h+c) c= dom f and
A8: {x0} c= dom f by A1,Th8;
  set l = lim (h"(#)(f/*(h+c) - f/*c));
  deffunc F(Real) = l * $1;
  consider L be PartFunc of REAL,REAL such that
A9: for g holds g in dom L iff P[g] and
A10: for g st g in dom L holds L.g = F(g) from SEQ_1:sch 3;
A11: dom L = REAL by A9,FDIFF_1:1;
  then
A12: for g holds L.g = l * g by A10;
A13: L is total by A11,PARTFUN1:def 4;
  deffunc F(Real) = $1 + x0;
  consider T be PartFunc of REAL,REAL such that
A14: for g holds g in dom T iff P[g] and
A15: for g st g in dom T holds T.g = F(g) from SEQ_1:sch 3;
A16: dom T = REAL by A14,FDIFF_1:1;
  deffunc F(real number) = (f*T).$1 - f.x0;
  consider T1 be PartFunc of REAL,REAL such that
A17: for g holds g in dom T1 iff P[g] and
A18: for g st g in dom T1 holds T1.g = F(g) from SEQ_1:sch 3;
  deffunc F(Real) = (T1 - L).$1;
A19: dom T1 = REAL by A17,FDIFF_1:1;
  then
A20: T1 is total by PARTFUN1:def 4;
  reconsider L as LINEAR by A13,A12,FDIFF_1:def 4;
  defpred P[set] means $1 in ].-r,r.[;
  consider R be PartFunc of REAL,REAL such that
A21: R is total and
A22: for g st g in dom R holds (P[g] implies R.g = F(g)) & (not P[g]
  implies R.g = G(g)) from SCHEME1:sch 8;
A23: dom R = REAL by A21,PARTFUN1:def 4;
A24: now
    let n;
    c.n in {x0} by A6,VALUED_0:28;
    hence c.n = x0 by TARSKI:def 1;
  end;
  now
    let h1;
A25: lim h1 = 0 by FDIFF_1:def 1;
    h1 is convergent by FDIFF_1:def 1;
    then consider k such that
A26: for n st k <= n holds abs(h1.n - 0) < r by A4,A25,SEQ_2:def
    7;
    set h2 = h1^\k;
A27: now
      let n;
      abs(h1.(n+k) - 0) < r by A26,NAT_1:12;
      then h1.(n+k) in ].0 - r,0 + r.[ by RCOMP_1:8;
      hence h2.n in ].-r,r.[ by NAT_1:def 3;
    end;
A28: rng (h2 + c) c= dom f
    proof
      let x;
      assume x in rng (h2 + c);
      then consider n such that
A29:  x = (h2 + c).n by FUNCT_2:190;
      h2.n in ].-r,r.[ by A27;
      then h2.n in {g: -r < g & g < r} by RCOMP_1:def 2;
      then
A30:  ex g st g = h2.n & -r < g & g < r;
      then
A31:  h2.n + x0 < x0 + r by XREAL_1:8;
      x0 + -r < h2.n + x0 by A30,XREAL_1:8;
      then
A32:  h2.n + x0 in {g: x0 - r < g & g < x0 + r} by A31;
      x = h2.n + c.n by A29,SEQ_1:11
        .= h2.n + x0 by A24;
      then x in ].x0 - r,x0 + r.[ by A32,RCOMP_1:def 2;
      hence thesis by A1,A5;
    end;
    set b = h2"(#)(L/*h2);
    set a = h2"(#)(T1/*h2);
A33: (h1"(#)(R/*h1))^\k = ((h1")^\k) (#) ((R/*h1)^\k) by SEQM_3:42
      .= h2" (#) ((R/*h1)^\k) by SEQM_3:41
      .= h2" (#) (R/*h2) by A21,VALUED_0:29;
A34: a - b = h2"(#)(T1/*h2 - L/*h2) by SEQ_1:29
      .= h2"(#)((T1-L)/*h2) by A13,A20,RFUNCT_2:32;
A35: now
      let n;
A36:  h2.n in ].-r,r.[ by A27;
      thus (a - b).n = (h2").n * ((T1-L)/*h2).n by A34,SEQ_1:12
        .= (h2").n * (T1 - L).(h2.n) by A13,A20,FUNCT_2:192
        .= (h2").n * R.(h2.n) by A22,A23,A36
        .= (h2").n * (R/*h2).n by A21,FUNCT_2:192
        .= (h2" (#) (R/*h2)).n by SEQ_1:12;
    end;
A37: h2 is non-zero by FDIFF_1:def 1;
A38: now
      let n be Nat;
A39:  n in NAT by ORDINAL1:def 13;
      then
A40:  h2.n <> 0 by A37,SEQ_1:7;
      thus b.n = (h2").n * (L/*h2).n by A39,SEQ_1:12
        .= (h2").n * L.(h2.n) by A13,A39,FUNCT_2:192
        .= (h2").n * ((h2.n) * l) by A10,A11
        .= (h2").n * (h2.n) * l
        .= (h2.n)" * (h2.n) * l by VALUED_1:10
        .= 1 * l by A40,XCMPLX_0:def 7
        .= l;
    end;
    then
A41: b is constant by VALUED_0:def 18;
    now
      let n;
A42:  c.n = x0 by A24;
      thus a.n = (h2").n * (T1/*h2).n by SEQ_1:12
        .= (h2").n * T1.(h2.n) by A20,FUNCT_2:192
        .= (h2").n * ((f*T).(h2.n) - f.x0) by A18,A19
        .= (h2").n * (f.(T.(h2.n)) - f.x0) by A16,FUNCT_1:23
        .= (h2").n * (f.(h2.n + x0) - f.x0) by A15,A16
        .= (h2").n * (f.((h2 + c).n) - f.(c.n)) by A42,SEQ_1:11
        .= (h2").n * (f.((h2 + c).n) - (f/*c).n) by A6,A8,FUNCT_2:185
        .= (h2").n * ((f/*(h2+c)).n - (f/*c).n) by A28,FUNCT_2:185
        .= (h2").n * ((f/*(h2+c)) - (f/*c)).n by RFUNCT_2:6
        .= (h2" (#) ((f/*(h2+c)) - (f/*c))).n by SEQ_1:12;
    end;
    then
A43: a = h2" (#) ((f/*(h2+c)) - (f/*c)) by FUNCT_2:113;
    then
A44: a is convergent by A2,A6,A28;
    then a - b is convergent by A41,SEQ_2:25;
    then
A45: h2"(#)(R/*h2) is convergent by A35,FUNCT_2:113;
    hence h1"(#)(R/*h1) is convergent by A33,SEQ_4:35;
A46: l = lim a by A3,A6,A7,A8,A28,A43,Th7;
    lim b = b.0 by A41,SEQ_4:40
      .= l by A38;
    then lim (a - b) = l - l by A44,A46,A41,SEQ_2:26
      .= 0;
    then lim (h2"(#) (R/*h2)) = 0 by A35,FUNCT_2:113;
    hence lim (h1"(#)(R/*h1)) = 0 by A45,A33,SEQ_4:36;
  end;
  then reconsider R as REST by A21,FDIFF_1:def 3;
A47: now
    take N;
    thus N c= dom f by A1;
    take L;
    take R;
    thus L.1 = l * 1 by A10,A11
      .= l;
    let g1;
    assume g1 in N;
    then g1 - x0 in ].-r,r.[ by A5,FCONT_3:10;
    hence L.(g1 - x0)+R.(g1 - x0) = L.(g1-x0)+(T1-L).(g1-x0) by A22,A23
      .= L.(g1 - x0) + (T1.(g1 - x0) - L.(g1 - x0)) by A13,A20,RFUNCT_1:72
      .= (f*T).(g1 - x0) - f.x0 by A18,A19
      .= f.(T.(g1 - x0)) - f.x0 by A16,FUNCT_1:23
      .= f.(g1 - x0 + x0) - f.x0 by A15,A16
      .= f.g1 - f.x0;
  end;
  thus f is_differentiable_in x0
  proof
    consider N1 be Neighbourhood of x0 such that
A48: N1 c= dom f and
A49: ex L be LINEAR, R be REST st L.1 = l & for g st g in N1 holds f.g
    - f.x0 = L.(g - x0) + R.(g - x0) by A47;
    take N1;
    thus N1 c= dom f by A48;
    consider L1 be LINEAR, R1 be REST such that
    L1.1 = l and
A50: for g st g in N1 holds f.g - f.x0 = L1.(g - x0) + R1.(g - x0) by
    A49;
    take L1;
    take R1;
    thus thesis by A50;
  end;
  then
A51: diff(f,x0) = l by A47,FDIFF_1:def 6;
  let h1,c1;
  assume that
A52: rng c1 = {x0} and
A53: rng (h1 + c1) c= dom f;
  c1 = c by A6,A52,Th5;
  hence thesis by A3,A6,A7,A8,A51,A53,Th7;
end;

theorem Th11:
  f is_differentiable_in x0 iff (ex N be Neighbourhood of x0 st N
c= dom f) & for h,c st rng c = {x0} & rng (h + c) c= dom f holds h"(#)(f/*(h+c)
  - f/*c) is convergent
proof
  thus f is_differentiable_in x0 implies (ex N be Neighbourhood of x0 st N c=
dom f) & for h,c st rng c = {x0} & rng (h + c) c= dom f holds h"(#)(f/*(h+c) -
  f/*c) is convergent
  proof
    assume
A1: f is_differentiable_in x0;
    then consider N be Neighbourhood of x0 such that
A2: N c= dom f and
    ex L be LINEAR, R be REST st for g st g in N holds f.g - f.x0 = L.(g -
    x0) + R.(g - x0) by FDIFF_1:def 5;
    thus ex N be Neighbourhood of x0 st N c= dom f by A2;
    let h,c such that
A3: rng c = {x0} and
A4: rng (h + c) c= dom f;
A5: lim h = 0 by FDIFF_1:def 1;
    consider r be real number such that
A6: 0 < r and
A7: N = ].x0 - r,x0 + r.[ by RCOMP_1:def 7;
    h is convergent by FDIFF_1:def 1;
    then consider k such that
A8: for n st k <= n holds abs(h.n - 0) < r by A5,A6,SEQ_2:def 7;
    set h1 = h^\k;
    rng (h1 + c) c= N
    proof
      let x;
      assume x in rng (h1 + c);
      then consider n such that
A9:   x = (h1 + c).n by FUNCT_2:190;
      c.n in rng c by VALUED_0:28;
      then c.n = x0 by A3,TARSKI:def 1;
      then
A10:  x = h1.n + x0 by A9,SEQ_1:11
        .= h.(n+k) + x0 by NAT_1:def 3;
      abs(h.(n+k) - 0) < r by A8,NAT_1:12;
      then h.(n+k) in ].0 - r, 0 + r.[ by RCOMP_1:8;
      then h.(n+k) in {g: -r < g & g < r} by RCOMP_1:def 2;
      then
A11:  ex g st g = h.(n+k) & -r < g & g < r;
      then
A12:  h.(n+k) + x0 < x0 + r by XREAL_1:8;
      x0 + -r < h.(n+k) + x0 by A11,XREAL_1:8;
      then h.(n+k) + x0 in {g: x0 - r < g & g < x0 + r} by A12;
      hence thesis by A7,A10,RCOMP_1:def 2;
    end;
    then
A13: h1"(#)(f/*(h1+c) - f/*c) is convergent by A1,A2,A3,FDIFF_1:20;
A14: {x0} c= dom f
    proof
      let x;
      assume x in {x0};
      then
A15:  x = x0 by TARSKI:def 1;
      x0 in N by RCOMP_1:37;
      hence thesis by A2,A15;
    end;
    c^\k = c by VALUED_0:26;
    then h1"(#)(f/*(h1+c) - f/*c) = h1"(#)(f/*((h+c)^\k) - f/*(c^\k)) by
    SEQM_3:37
      .= h1"(#)(((f/*(h+c))^\k) - f/*(c^\k)) by A4,VALUED_0:27
      .= h1"(#)(((f/*(h+c))^\k) - ((f/*c)^\k)) by A3,A14,VALUED_0:27
      .= h1"(#)((f/*(h+c) - (f/*c))^\k) by SEQM_3:39
      .= ((h")^\k)(#)((f/*(h+c) - (f/*c))^\k) by SEQM_3:41
      .= ((h")(#)(f/*(h+c) - (f/*c)))^\k by SEQM_3:42;
    hence thesis by A13,SEQ_4:35;
  end;
  assume that
A16: ex N be Neighbourhood of x0 st N c= dom f and
A17: for h,c st rng c = {x0} & rng (h + c) c= dom f holds h"(#)(f/*(h+c)
  - f/*c) is convergent;
  thus thesis by A16,A17,Lm1;
end;

theorem Th12:
  f is_differentiable_in x0 & diff(f,x0) = g iff (ex N be
  Neighbourhood of x0 st N c= dom f) & for h,c st rng c = {x0} & rng (h + c) c=
dom f holds h"(#)(f/*(h+c) - f/*c) is convergent & lim (h"(#)(f/*(h+c) - f/*c))
  = g
proof
  thus f is_differentiable_in x0 & diff(f,x0) = g implies (ex N be
  Neighbourhood of x0 st N c= dom f) & for h,c st rng c = {x0} & rng (h + c) c=
dom f holds h"(#)(f/*(h+c) - f/*c) is convergent & lim (h"(#)(f/*(h+c) - f/*c))
  = g
  proof
    assume that
A1: f is_differentiable_in x0 and
A2: diff(f,x0) = g;
    thus ex N be Neighbourhood of x0 st N c= dom f by A1,Th11;
A3: for h,c st rng c = {x0} & rng (h + c) c= dom f holds h"(#)(f/*(h+c) -
    f /*c) is convergent by A1,Th11;
    ex N be Neighbourhood of x0 st N c= dom f by A1,Th11;
    hence thesis by A2,A3,Lm1;
  end;
  assume that
A4: ex N be Neighbourhood of x0 st N c= dom f and
A5: for h,c st rng c = {x0} & rng (h + c) c= dom f holds h"(#)(f/*(h+c)
  - f/*c) is convergent & lim (h"(#)(f/*(h+c) - f/*c)) = g;
A6: for h,c holds rng c = {x0} & rng (h + c) c= dom f implies h"(#)(f/*(h+c)
  - f/*c) is convergent by A5;
  hence f is_differentiable_in x0 by A4,Lm1;
  consider h,c such that
A7: rng c = {x0} and
A8: rng (h+c) c= dom f and
  {x0} c= dom f by A4,Th8;
  lim (h"(#)(f/*(h+c) - f/*c)) = g by A5,A7,A8;
  hence thesis by A4,A6,A7,A8,Lm1;
end;

Lm2: (ex N be Neighbourhood of x0 st N c= dom (f2*f1)) & f1
is_differentiable_in x0 & f2 is_differentiable_in f1.x0 implies f2*f1
is_differentiable_in x0 & diff(f2*f1,x0) = diff(f2,f1.x0)*diff(f1,x0)
proof
  given N be Neighbourhood of x0 such that
A1: N c= dom (f2*f1);
  assume that
A2: f1 is_differentiable_in x0 and
A3: f2 is_differentiable_in f1.x0;
  for h,c st rng c = {x0} & rng (h + c) c= dom (f2*f1) holds h"(#)((f2*f1)
/*(h+c) - (f2*f1)/*c) is convergent & lim (h"(#)((f2*f1)/*(h+c) - (f2*f1)/*c))
  = diff(f2,f1.x0)*diff(f1,x0)
  proof
    let h,c;
    assume that
A4: rng c = {x0} and
A5: rng (h + c) c= dom (f2*f1);
A6: rng (h + c) c= dom f1 by A5,Th9;
    set a = f1/*c;
A7: now
      let n;
      c.n in rng c by VALUED_0:28;
      hence c.n = x0 by A4,TARSKI:def 1;
    end;
A8: rng c c= dom (f2*f1)
    proof
      let x;
      assume x in rng c;
      then
A9:   ex n st c.n = x by FUNCT_2:190;
      x0 in N by RCOMP_1:37;
      then x0 in dom (f2*f1) by A1;
      hence thesis by A7,A9;
    end;
    set d = f1/*(h+c) - f1/*c;
A10: lim h = 0 by FDIFF_1:def 1;
A11: h is convergent by FDIFF_1:def 1;
    then
A12: h + c is convergent by SEQ_2:19;
    lim c = c.0 by SEQ_4:40;
    then lim c = x0 by A7;
    then
A13: lim (h + c) = 0 + x0 by A11,A10,SEQ_2:20
      .= x0;
A14: f1 is_continuous_in x0 by A2,FDIFF_1:32;
    then
A15: f1/*(h+c) is convergent by A6,A12,A13,FCONT_1:def 1;
A16: f1.x0 = lim (f1/*(h+c)) by A6,A12,A13,A14,FCONT_1:def 1;
A17: rng (f1/*(h+c)) c= dom f2 by A5,Th9;
A18: rng c c= dom f1
    proof
      let x;
      assume x in rng c;
      then
A19:  x = x0 by A4,TARSKI:def 1;
      x0 in N by RCOMP_1:37;
      hence thesis by A1,A19,FUNCT_1:21;
    end;
A20: rng a = { f1.x0 }
    proof
      thus rng a c= { f1.x0 }
      proof
        let x;
        assume x in rng a;
        then consider n such that
A21:    a.n = x by FUNCT_2:190;
        c.n = x0 by A7;
        then x = f1.x0 by A18,A21,FUNCT_2:185;
        hence thesis by TARSKI:def 1;
      end;
      let x;
A22:  a.0 in rng a by VALUED_0:28;
      assume x in {f1.x0};
      then
A23:  x = f1.x0 by TARSKI:def 1;
      c.0 = x0 by A7;
      hence thesis by A18,A23,A22,FUNCT_2:185;
    end;
A24: rng a c= dom f2
    proof
      let x;
      assume x in rng a;
      then
A25:  x = f1.x0 by A20,TARSKI:def 1;
      x0 in N by RCOMP_1:37;
      hence thesis by A1,A25,FUNCT_1:21;
    end;
A26: now
      let n be Nat;
      a.n in rng a by VALUED_0:28;
      then
A27:  a.n = f1.x0 by A20,TARSKI:def 1;
      a.(n+1) in rng a by VALUED_0:28;
      hence a.n = a.(n+1) by A20,A27,TARSKI:def 1;
    end;
    then a is constant by VALUED_0:25;
    then
A28: d is convergent by A15,SEQ_2:25;
    reconsider a as constant Real_Sequence by A26,VALUED_0:25;
    a.0 in rng a by VALUED_0:28;
    then a.0 = f1.x0 by A20,TARSKI:def 1;
    then lim a = f1.x0 by SEQ_4:40;
    then
A29: lim d = f1.x0 - f1.x0 by A15,A16,SEQ_2:26
      .= 0;
    h"(#)((f2*f1)/*(h+c) - (f2*f1)/*c) is convergent & lim (h"(#)((f2*f1)
    /*(h+c) - (f2*f1)/*c)) = diff(f2,f1.x0)*diff(f1,x0)
    proof
      per cases;
      suppose
        ex k st for n st k <= n holds (f1/*(h+c)).n <> f1.x0;
        then consider k such that
A30:    for n st k <= n holds (f1/*(h+c)).n <> f1.x0;
        now
          given n such that
A31:      (d^\k).n = 0;
          0 = d.(n + k) by A31,NAT_1:def 3
            .= (f1/*(h+c)).(n+k) - (f1/*c).(n+k) by RFUNCT_2:6
            .= (f1/*(h+c)).(n+k) - f1.(c.(n+k)) by A18,FUNCT_2:185
            .= (f1/*(h+c)).(n+k) - f1.x0 by A7;
          hence contradiction by A30,NAT_1:12;
        end;
        then
A32:    d^\k is non-zero by SEQ_1:7;
        set c1 = c^\k;
        set h1 = h^\k;
        set a1 = a^\k;
        set s = h1"(#)(f1/*(h1 + c1) - f1/*c1);
A33:    now
          let n;
          thus (d + a).n = d.n + a.n by SEQ_1:11
            .= (f1/*(h+c)).n - a.n + a.n by RFUNCT_2:6
            .= (f1/*(h+c)).n;
        end;
        lim (d^\k) = 0 by A28,A29,SEQ_4:33;
        then reconsider d1 = d^\k as convergent_to_0 Real_Sequence by A28,A32,
        FDIFF_1:def 1;
        set t = d1"(#)(f2/*(d1 + a1) - f2/*a1);
        d1 + a1 = (d + a)^\k by SEQM_3:37;
        then
A34:    d1 + a1 = (f1/*(h+c))^\k by A33,FUNCT_2:113;
        rng ((f1/*(h+c))^\k) c= rng (f1/*(h+c)) by VALUED_0:21;
        then
A35:    rng (d1 + a1) c= dom f2 by A17,A34,XBOOLE_1:1;
A36:    rng a1 = {f1.x0} by A20,VALUED_0:26;
        then
A37:    lim t = diff(f2,f1.x0) by A3,A35,Th12;
        diff(f2,f1.x0) = diff(f2,f1.x0);
        then
A38:    t is convergent by A3,A36,A35,Th12;
        rng ((h + c)^\k) c= rng (h + c) by VALUED_0:21;
        then rng ((h + c)^\k) c= dom f1 by A6,XBOOLE_1:1;
        then
A39:    rng (h1 + c1) c= dom f1 by SEQM_3:37;
A40:    rng c1 = {x0} by A4,VALUED_0:26;
        diff(f1,x0) = diff(f1,x0);
        then
A41:    s is convergent by A2,A40,A39,Th12;
A42:    t(#)s = d1"(#)(f2/*(d1+a1) - f2/*a1) (#) ((f1/*((h+c)^\k) - f1/*
        c1) (#)h1") by SEQM_3:37
          .= d1"(#)(f2/*(d1+a1) - f2/*a1) (#) (((f1/*(h+c))^\k - f1/*c1)(#)
        h1") by A6,VALUED_0:27
          .= d1"(#)(f2/*(d1+a1) - f2/*a1) (#) (((f1/*(h+c))^\k - (f1/*c)^\k)
        (#)h1") by A18,VALUED_0:27
          .= ((f2/*(d1+a1) - f2/*a1)/"d1) (#) (d1(#)h1") by SEQM_3:39
          .= ((f2/*(d1+a1) - f2/*a1)/"d1) (#) d1(#)h1" by SEQ_1:22
          .= (f2/*(d1+a1) - f2/*a1) (#) h1" by A32,SEQ_1:45
          .= ((f2/*(f1/*(h+c)))^\k - f2/*a1) (#) h1" by A17,A34,VALUED_0:27
          .= (((f2*f1)/*(h+c))^\k - f2/*a1) (#) h1" by A5,VALUED_0:31
          .= (((f2*f1)/*(h+c))^\k - ((f2/*a)^\k)) (#) h1" by A24,VALUED_0:27
          .= (((f2*f1)/*(h+c))^\k - (((f2*f1)/*c)^\k)) (#) h1" by A8,
        VALUED_0:31
          .= (((f2*f1)/*(h+c) - (f2*f1)/*c)^\k) (#) h1" by SEQM_3:39
          .= (((f2*f1)/*(h+c) - (f2*f1)/*c)^\k) (#) ((h")^\k) by SEQM_3:41
          .= (h"(#)((f2*f1)/*(h+c) - (f2*f1)/*c))^\k by SEQM_3:42;
        then
A43:    (h"(#) ((f2*f1)/*(h+c) - (f2*f1)/*c))^\k is convergent by
        A41
        ,A38,SEQ_2:28;
        hence h"(#)((f2*f1)/*(h+c) - (f2*f1)/*c) is convergent by SEQ_4:35;
        lim s = diff(f1,x0) by A2,A40,A39,Th12;
        then lim ((h"(#)((f2*f1)/*(h+c) - (f2*f1)/*c))^\k) = diff(f2,f1.x0) *
        diff(f1,x0 ) by A42,A41,A38,A37,SEQ_2:29;
        hence thesis by A43,SEQ_4:36;
      end;
      suppose
A44:    for k ex n st k <= n & (f1/*(h+c)).n = f1.x0;
        defpred P[set] means $1 = f1.x0;
A45:    for k ex n st k <= n & P[(f1/*(h+c)).n] by A44;
        consider I1 be increasing sequence of NAT such that
A46:    for n holds P[(f1/*(h+c)*I1).n] and
        for n st for r st r = (f1/*(h+c)).n holds P[r] ex m st n = I1.m
        from ExIncSeqofNat(A45);
        (h*I1) is subsequence of h by VALUED_0:def 17;
        then reconsider h1 = h*I1 as convergent_to_0 Real_Sequence by Th6;
        set c1 = c*I1;
A47:    c1 is subsequence of c by VALUED_0:def 17;
        then
A48:    c1 = c by VALUED_0:26;
A49:    rng c1 = {x0} by A4,A47,VALUED_0:26;
        reconsider c1 as constant Real_Sequence;
A50:    now
          let g be real number such that
A51:      0 < g;
          take n = 0;
          let m such that
          n <= m;
          abs((h1"(#)(f1/*(h1+c1) - f1/*c1)).m - 0) = abs((h1").m *(f1/*(
          h1+c1) - f1/*c1).m) by SEQ_1:12
            .= abs((h1").m *((f1/*(h1+c1)).m - (f1/*c1).m)) by RFUNCT_2:6
            .= abs((h1").m *((f1/*(h1+c1)).m - f1.(c.m))) by A18,A48,
          FUNCT_2:185
            .= abs((h1").m *((f1/*(h1+c1)).m - f1.x0)) by A7
            .= abs((h1").m *((f1/*((h+c)*I1)).m - f1.x0)) by RFUNCT_2:13
            .= abs((h1").m *((f1/*(h+c)*I1).m - f1.x0)) by A6,FUNCT_2:187
            .= abs((h1").m *(f1.x0 - f1.x0)) by A46
            .= 0 by ABSVALUE:7;
          hence abs((h1"(#)(f1/*(h1+c1) - f1/*c1)).m - 0) < g by A51;
        end;
        (h + c)*I1 is subsequence of (h + c) by VALUED_0:def 17;
        then rng ((h + c)*I1) c= rng (h + c) by VALUED_0:21;
        then rng ((h + c)*I1) c= dom f1 by A6,XBOOLE_1:1;
        then
A52:    rng (h1 + c1) c= dom f1 by RFUNCT_2:13;
        then
A53:    lim (h1"(#)(f1/*(h1+c1) - f1/*c1)) = diff(f1,x0) by A2,A49,
        Th12;
        diff(f1,x0) = diff(f1,x0);
        then h1"(#)(f1/*(h1+c1) - f1/*c1) is convergent by A2,A49,
        A52,Th12;
        then
A54:    diff(f1,x0) = 0 by A53,A50,SEQ_2:def 7;
        now
          per cases;
          suppose
            ex k st for n st k <= n holds (f1/*(h+c)).n = f1.x0;
            then consider k such that
A55:        for n st k <= n holds (f1/*(h+c)).n = f1.x0;
A56:        now
              let g be real number such that
A57:          0 < g;
              take n = k;
              let m;
              assume n <= m;
              then
A58:          (f1/*(h+c)).m = f1.x0 by A55;
              abs((h"(#)((f2*f1)/*(h+c) - (f2*f1)/*c)).m - diff(f2,f1.x0
)*diff(f1, x0 ) ) = abs((h").m * ((f2*f1)/*(h+c) - (f2*f1)/*c).m) by A54,SEQ_1:
              12
                .= abs((h").m * (((f2*f1)/*(h+c)).m - ((f2*f1)/*c).m)) by
              RFUNCT_2:6
                .= abs((h").m * ((f2/*(f1/*(h+c))).m - ((f2*f1)/*c).m)) by
              A5,VALUED_0:31
                .= abs((h").m * (f2.((f1/*(h+c)).m) - ((f2*f1)/*c).m)) by
              A17
              ,FUNCT_2:185
                .= abs((h").m * (f2.(f1.x0) - (f2/*(f1/*c)).m)) by A8,A58,
              VALUED_0:31
                .= abs((h").m * (f2.(f1.x0) - f2.((f1/*c).m))) by A24,
              FUNCT_2:185
                .= abs((h").m * (f2.(f1.x0) - f2.(f1.(c.m)))) by A18,
              FUNCT_2:185
                .= abs((h").m * (f2.(f1.x0) - f2.(f1.x0))) by A7
                .= 0 by ABSVALUE:7;
              hence
              abs((h"(#) ((f2*f1)/*(h+c) - (f2*f1)/*c)).m - diff(f2,f1.x0
              )*diff(f1,x0))<g by A57;
            end;
            hence h"(#)((f2*f1)/*(h+c) - (f2*f1)/*c) is convergent by
            SEQ_2:def 6;
            hence lim (h"(#)((f2*f1)/*(h+c) - (f2*f1)/*c)) = diff(f2,f1.x0)*
            diff(f1,x0) by A56,SEQ_2:def 7;
          end;
          suppose
A59:        for k ex n st k <= n & (f1/*(h+c)).n <> f1.x0;
            defpred P[set] means $1 <> f1.x0;
A60:        for k ex n st k <= n & P[(f1/*(h+c)).n] by A59;
            consider I2 be increasing sequence of NAT such that
A61:        for n holds P[(f1/*(h+c)*I2).n] and
A62:        for n st for r st r = (f1/*(h+c)).n holds P[r] ex m st n
            = I2.m from ExIncSeqofNat(A60);
            now
              given n such that
A63:          (d*I2).n = 0;
              0 = d.(I2.n) by A63,FUNCT_2:21
                .= (f1/*(h+c)).(I2.n) - (f1/*c).(I2.n) by RFUNCT_2:6
                .= (f1/*(h+c)).(I2.n) - f1.(c.(I2.n)) by A18,FUNCT_2:185
                .= (f1/*(h+c)).(I2.n) - f1.x0 by A7
                .= (f1/*(h+c)*I2).n - f1.x0 by FUNCT_2:21;
              hence contradiction by A61;
            end;
            then
A64:        (d*I2) is non-zero by SEQ_1:7;
            (h*I2) is subsequence of h by VALUED_0:def 17;
            then reconsider h2 = h*I2 as convergent_to_0 Real_Sequence by Th6;
            set a1 = a*I2;
            set c2 = c*I2;
            reconsider c2 as constant Real_Sequence;
            set s = h2"(#)(f1/*(h2+c2) - f1/*c2);
A65:        d*I2 is subsequence of d by VALUED_0:def 17;
            then
A66:        d*I2 is convergent by A28,SEQ_4:29;
            lim (d*I2) = 0 by A28,A29,A65,SEQ_4:30;
            then reconsider
d1 = d*I2 as convergent_to_0 Real_Sequence by A66,A64,FDIFF_1:def 1;
            set t = d1"(#)(f2/*(d1+a1) - f2/*a1);
            a1 is subsequence of a by VALUED_0:def 17;
            then
A67:        rng a1 = {f1.x0} by A20,VALUED_0:26;
A68:        now
              let n;
              thus (d + a).n = d.n + a.n by SEQ_1:11
                .= (f1/*(h+c)).n - a.n + a.n by RFUNCT_2:6
                .= (f1/*(h+c)).n;
            end;
            d1 + a1 = (d + a)*I2 by RFUNCT_2:13;
            then
A69:        d1 + a1 = f1/*(h+c)*I2 by A68,FUNCT_2:113;
A70:        t(#)s = d1"(#)(f2/*(d1+a1) - f2/*a1) (#) ((f1/*((h + c)*I2)
            - f1/*c2)(#)h2") by RFUNCT_2:13
              .= d1"(#)(f2/*(d1+a1) - f2/*a1) (#) ((f1/*(h+c)*I2 - f1/*c2)
            (#)h2") by A6,FUNCT_2:187
              .= d1"(#)(f2/*(d1+a1) - f2/*a1) (#) ((f1/*(h+c)*I2 - f1/*c*I2)
            (#)h2") by A18,FUNCT_2:187
              .= ((f2/*(d1+a1) - f2/*a1)/"d1) (#) (d1(#)h2") by RFUNCT_2:13
              .= ((f2/*(d1+a1) - f2/*a1)/"d1) (#) d1(#)h2" by SEQ_1:22
              .= (f2/*(d1+a1) - f2/*a1) (#) h2" by A64,SEQ_1:45
              .= (f2/*(f1/*(h+c))*I2 - f2/*a1) (#) h2" by A17,A69,FUNCT_2:
            187
              .= ((f2*f1)/*(h+c)*I2 - f2/*a1) (#) h2" by A5,VALUED_0:31
              .= (((f2*f1)/*(h+c))*I2 - (f2/*a*I2)) (#) h2" by A24,FUNCT_2:
            187
              .= (((f2*f1)/*(h+c))*I2 - (((f2*f1)/*c)*I2)) (#) h2" by A8,
            VALUED_0:31
              .= (((f2*f1)/*(h+c) - (f2*f1)/*c)*I2) (#) h2" by RFUNCT_2:13
              .= (((f2*f1)/*(h+c) - (f2*f1)/*c)*I2) (#) ((h")*I2) by
            RFUNCT_2:16
              .= (h"(#)((f2*f1)/*(h+c) - (f2*f1)/*c))*I2 by RFUNCT_2:13;
            reconsider a1 as constant Real_Sequence;
            f1/*(h+c)*I2 is subsequence of f1/*(h+c) by VALUED_0:def 17;
            then rng (f1/*(h+c)*I2) c= rng (f1/*(h+c)) by VALUED_0:21;
            then
A71:        rng (d1 + a1) c= dom f2 by A17,A69,XBOOLE_1:1;
            (h + c)*I2 is subsequence of (h + c) by VALUED_0:def 17;
            then rng ((h + c)*I2) c= rng (h + c) by VALUED_0:21;
            then rng ((h + c)*I2) c= dom f1 by A6,XBOOLE_1:1;
            then
A72:        rng (h2 + c2) c= dom f1 by RFUNCT_2:13;
            c2 is subsequence of c by VALUED_0:def 17;
            then
A73:        rng c2 = {x0} by A4,VALUED_0:26;
            then
A74:        lim s = diff(f1,x0) by A2,A72,Th12;
            diff(f1,x0) = diff(f1,x0);
            then
A75:        s is convergent by A2,A73,A72,Th12;
            diff(f2,f1.x0) = diff(f2,f1.x0);
            then
A76:        t is convergent by A3,A67,A71,Th12;
            then
A77:        (h"(#) ((f2*f1)/*(h+c) - (f2*f1)/*c))*I2 is convergent by
            A70,A75,SEQ_2:28;
            lim t = diff(f2,f1.x0) by A3,A67,A71,Th12;
            then
A78:        lim ((h"(#)((f2*f1)/*(h+c) - (f2*f1)/*c))*I2) = diff(f2,f1.
            x0) * diff(f1,x0) by A70,A75,A74,A76,SEQ_2:29;
A79:        now
              set DF = diff(f2,f1.x0) * diff(f1,x0);
              let g be real number such that
A80:          0 < g;
              consider k such that
A81:          for m st k <= m holds abs(((h"(#)((f2*f1)/*(h+c)-(f2*
              f1)/*c))*I2).m - DF)<g by A77,A78,A80,SEQ_2:def 7;
              take n = I2.k;
              let m such that
A82:          n <= m;
              now
                per cases;
                suppose
A83:              (f1/*(h+c)).m = f1.x0;
                  abs((h"(#)((f2*f1)/*(h+c) - (f2*f1)/*c)).m - DF) = abs
                  ((h").m * (((f2*f1)/*(h+c) - (f2*f1)/*c).m)) by A54,SEQ_1:12
                    .= abs((h").m * (((f2*f1)/*(h+c)).m - ((f2*f1)/*c).m))
                  by RFUNCT_2:6
                    .= abs((h").m*(((f2*f1)/*(h+c)).m -(f2/*(f1/*c)).m)) by
                  A8,VALUED_0:31
                    .= abs((h").m*(((f2*f1)/*(h+c)).m -f2.((f1/*c).m))) by
                  A24,FUNCT_2:185
                    .= abs((h").m*(((f2*f1)/*(h+c)).m -f2.(f1.(c.m)))) by
                  A18,FUNCT_2:185
                    .= abs((h").m * (((f2*f1)/*(h+c)).m - f2.(f1.x0))) by A7
                    .= abs((h").m *((f2/*(f1/*(h+c))).m - f2.(f1.x0))) by A5
                  ,VALUED_0:31
                    .= abs((h").m*(f2.(f1.x0) - f2.(f1.x0))) by A17,A83,
                  FUNCT_2:185
                    .= 0 by ABSVALUE:7;
                  hence
                  abs((h"(#)((f2*f1)/*(h+c) - (f2*f1)/*c)).m - diff(f2,f1
                  .x0)*diff(f1,x0))<g by A80;
                end;
                suppose
                  (f1/*(h+c)).m <> f1.x0;
                  then for r1 holds (f1/*(h+c)).m=r1 implies r1<>f1.x0;
                  then consider l such that
A84:              m = I2.l by A62;
                  dom I2 = NAT by FUNCT_2:def 1;
                  then l >= k by A82,A84,VALUED_0:def 13;
                  then abs(((h"(#)((f2*f1)/*(h+c)-(f2*f1)/*c))*I2).l - DF) <
                  g by A81;
                  hence abs((h"(#)((f2*f1)/*(h+c)-(f2*f1)/*c)).m - DF) < g by
                  A84,FUNCT_2:21;
                end;
              end;
              hence
              abs((h"(#)((f2*f1)/*(h+c) - (f2*f1)/*c)).m - diff(f2,f1.x0)
              *diff(f1,x0))<g;
            end;
            hence h"(#)((f2*f1)/*(h+c) - (f2*f1)/*c) is convergent by
            SEQ_2:def 6;
            hence lim (h"(#) ((f2*f1)/*(h+c) - (f2*f1)/*c)) = diff(f2,f1.x0)*
            diff(f1,x0) by A79,SEQ_2:def 7;
          end;
        end;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
  hence thesis by A1,Th12;
end;

theorem Th13:
  f1 is_differentiable_in x0 & f2 is_differentiable_in f1.x0
implies f2*f1 is_differentiable_in x0 & diff(f2*f1,x0) = diff(f2,f1.x0)*diff(f1
  ,x0)
proof
  assume that
A1: f1 is_differentiable_in x0 and
A2: f2 is_differentiable_in f1.x0;
  consider N2 be Neighbourhood of f1.x0 such that
A3: N2 c= dom f2 by A2,Th11;
  f1 is_continuous_in x0 by A1,FDIFF_1:32;
  then consider N3 be Neighbourhood of x0 such that
A4: f1.:N3 c= N2 by FCONT_1:5;
  consider N1 be Neighbourhood of x0 such that
A5: N1 c= dom f1 by A1,Th11;
  consider N be Neighbourhood of x0 such that
A6: N c= N1 and
A7: N c= N3 by RCOMP_1:38;
  N c= dom (f2*f1)
  proof
    let x;
    assume
A8: x in N;
    then reconsider x9 = x as Real;
A9: x in N1 by A6,A8;
    then f1.x9 in f1.:N3 by A5,A7,A8,FUNCT_1:def 12;
    then f1.x9 in N2 by A4;
    hence thesis by A5,A3,A9,FUNCT_1:21;
  end;
  hence thesis by A1,A2,Lm2;
end;

theorem Th14:
  f2.x0 <> 0 & f1 is_differentiable_in x0 & f2
is_differentiable_in x0 implies f1/f2 is_differentiable_in x0 & diff(f1/f2,x0)
  = (diff(f1,x0) * f2.x0 - diff(f2,x0) * f1.x0)/(f2.x0)^2
proof
  assume that
A1: f2.x0 <> 0 and
A2: f1 is_differentiable_in x0 and
A3: f2 is_differentiable_in x0;
  consider N1 be Neighbourhood of x0 such that
A4: N1 c= dom f1 by A2,Th11;
  ex N2 be Neighbourhood of x0 st N2 c= dom f2 by A3,Th11;
  then consider N3 be Neighbourhood of x0 such that
A5: N3 c= dom f2 and
A6: for g st g in N3 holds f2.g <> 0 by A1,A3,FCONT_3:15,FDIFF_1:32;
  consider N be Neighbourhood of x0 such that
A7: N c= N1 and
A8: N c= N3 by RCOMP_1:38;
A9: N c= dom f2 \ f2"{0}
  proof
    let x;
    assume
A10: x in N;
    then reconsider x9 = x as Real;
    f2.x9 <> 0 by A6,A8,A10;
    then not f2.x9 in {0} by TARSKI:def 1;
    then
A11: not x9 in f2"{0} by FUNCT_1:def 13;
    x9 in N3 by A8,A10;
    hence thesis by A5,A11,XBOOLE_0:def 5;
  end;
A12: f2 is_continuous_in x0 by A3,FDIFF_1:32;
  N c= dom f1 by A4,A7,XBOOLE_1:1;
  then
A13: N c= dom f1 /\ (dom f2 \ f2"{0}) by A9,XBOOLE_1:19;
A14: for h,c st rng c = {x0} & rng (h + c) c= dom (f1/f2) holds h"(#)((f1/f2
)/*(h+c) - (f1/f2)/*c) is convergent & lim (h"(#)((f1/f2)/*(h+c) - (f1/f2)/*c))
  = (diff(f1,x0) * f2.x0 - diff(f2,x0) * f1.x0)/((f2.x0)^2)
  proof
    dom (f2^) = dom f2 \ f2"{0} by RFUNCT_1:def 8;
    then
A15: dom (f2^) c= dom f2 by XBOOLE_1:36;
    let h,c;
    assume that
A16: rng c = {x0} and
A17: rng (h + c) c= dom (f1/f2);
A18: rng (h + c) c= dom f1 /\ (dom f2 \ f2"{0}) by A17,RFUNCT_1:def 4;
    dom f1 /\ (dom f2 \ f2"{0}) c= dom f2 \ f2"{0} by XBOOLE_1:17;
    then
A19: rng (h + c) c= dom f2 \ f2"{0} by A18,XBOOLE_1:1;
    then
A20: rng (h + c) c= dom (f2^) by RFUNCT_1:def 8;
    then
A21: f2/*(h+c) is non-zero by RFUNCT_2:26;
A22: lim c = x0 by A16,Th4;
    set u2 = f2/*c;
    set u1 = f1/*c;
    set v2 = f2/*(h+c);
    set h2 = h" (#)(v2 - u2);
    set v1 = f1/*(h+c);
    set h1 = h" (#)(v1 - u1);
A23: f1 is_continuous_in x0 by A2,FDIFF_1:32;
A24: rng c c= dom f1 /\ dom (f2^)
    proof
      let x;
      assume x in rng c;
      then
A25:  x = x0 by A16,TARSKI:def 1;
      x0 in N by RCOMP_1:37;
      then x in dom f1 /\ (dom f2 \ f2"{0}) by A13,A25;
      hence thesis by RFUNCT_1:def 8;
    end;
A26: dom f1 /\ dom (f2^) c= dom (f2^) by XBOOLE_1:17;
    then
A27: f2/*c is non-zero by A24,RFUNCT_2:26,XBOOLE_1:1;
    dom f1 /\ (dom f2 \ f2"{0}) c= dom f1 by XBOOLE_1:17;
    then
A28: rng (h + c) c= dom f1 by A18,XBOOLE_1:1;
    then
A29: rng (h + c) c= dom f1 /\ dom (f2^) by A20,XBOOLE_1:19;
A30: h"(#) ((f1/f2)/*(h+c) - (f1/f2)/*c)=h"(#)((f1(#) (f2^))/*(h + c) - (
    f1/f2)/*c) by RFUNCT_1:47
      .= h"(#)((f1(#)(f2^))/*(h + c) - (f1(#)(f2^))/*c) by RFUNCT_1:47
      .= h"(#)((f1/*(h+c))(#)((f2^)/*(h + c)) - (f1(#) (f2^))/*c) by A29,
    RFUNCT_2:23
      .= h"(#)((f1/*(h+c))/"(f2/*(h + c)) - (f1(#)(f2^))/*c) by A20,RFUNCT_2
    :27
      .= h"(#)((f1/*(h+c))/"(f2/*(h + c)) - (f1/*c)(#) ((f2^)/*c)) by A24,
    RFUNCT_2:23
      .= h" (#) ((f1/*(h+c))/"(f2/*(h + c)) - (f1/*c)/"(f2/*c)) by A24,A26,
    RFUNCT_2:27,XBOOLE_1:1
      .= h" (#) (((f1/*(h+c))(#)(f2/*c) - (f1/*c) (#) (f2/*(h + c)))/"((f2/*
    (h+c))(#) (f2/*c))) by A21,A27,SEQ_1:58
      .= (h" (#) ((f1/*(h+c))(#)(f2/*c) - (f1/*c)(#)(f2/*(h + c))))/"((f2/*(
    h+c))(#) (f2/*c)) by SEQ_1:49;
    rng c c= dom (f2^) by A24,A26,XBOOLE_1:1;
    then
A31: rng c c= dom f2 by A15,XBOOLE_1:1;
    then
A32: f2/*c is convergent by A12,A22,FCONT_1:def 1;
    dom f1 /\ dom (f2^) c= dom f1 by XBOOLE_1:17;
    then
A33: rng c c= dom f1 by A24,XBOOLE_1:1;
    then
A34: lim(f1/*c) = f1.x0 by A22,A23,FCONT_1:def 1;
    dom f2 \ f2"{0} c= dom f2 by XBOOLE_1:36;
    then
A35: rng (h + c) c= dom f2 by A19,XBOOLE_1:1;
    diff(f2,x0) = diff(f2,x0);
    then
A36: h2 is convergent by A3,A16,A35,Th12;
A37: f1/*c is convergent by A33,A22,A23,FCONT_1:def 1;
    then
A38: u1(#)h2 is convergent by A36,SEQ_2:28;
    lim h2 = diff(f2,x0) by A3,A16,A35,Th12;
    then
A39: lim (u1(#)h2) = diff(f2,x0) * f1.x0 by A36,A37,A34,SEQ_2:29;
    set w1 = (f2/*(h+c)) (#) (f2/*c);
A40: lim(h + c) = x0 by A16,Th4;
A41: h + c is convergent by A16,Th4;
    then
A42: f2/*(h+c) is convergent by A12,A35,A40,FCONT_1:def 1;
    then
A43: w1 is convergent by A32,SEQ_2:28;
    f2/*(h+c) is non-zero by A20,RFUNCT_2:26;
    then
A44: w1 is non-zero by A27,SEQ_1:43;
A45: lim(f2/*c) = f2.x0 by A12,A31,A22,FCONT_1:def 1;
    diff(f1,x0) = diff(f1,x0);
    then
A46: h1 is convergent by A2,A16,A28,Th12;
    then
A47: h1(#)u2 is convergent by A32,SEQ_2:28;
    lim (f2/*(h+c)) = f2.x0 by A12,A35,A41,A40,FCONT_1:def 1;
    then
A48: lim w1 = (f2.x0)^2 by A42,A32,A45,SEQ_2:29;
    now
      let n;
      thus (h" (#) (v1(#)u2 - u1(#)v2)).n = (h").n * (v1(#)u2 - u1(#)v2).n by
      SEQ_1:12
        .= (h").n * ((v1(#)u2).n - (u1(#)v2).n) by RFUNCT_2:6
        .= (h").n * (v1.n *u2.n - (u1(#)v2).n) by SEQ_1:12
        .= (h").n * ((v1.n - u1.n) *u2.n + (u1.n * u2.n) - u1.n * v2.n) by
      SEQ_1:12
        .= (h").n * (v1.n - u1.n) *u2.n - u1.n* ((h").n * (v2.n - u2.n))
        .= (h").n * (v1 - u1).n * u2.n - u1.n* ((h").n * (v2.n - u2.n)) by
      RFUNCT_2:6
        .= (h").n * (v1 - u1).n * u2.n - u1.n* ((h").n * (v2 - u2).n) by
      RFUNCT_2:6
        .= (h" (#) (v1 - u1)).n * u2.n - u1.n* ((h").n * (v2 - u2).n) by
      SEQ_1:12
        .= (h" (#)(v1 - u1)).n * u2.n - u1.n* (h" (#) (v2 - u2)).n by SEQ_1:
      12
        .= (h" (#)(v1 - u1) (#) u2).n - u1.n* (h" (#) (v2 - u2)).n by SEQ_1:
      12
        .= (h" (#)(v1 - u1) (#) u2).n - (u1 (#) (h" (#) (v2 - u2))).n by
      SEQ_1:12
        .= (h" (#)(v1 - u1) (#) u2 - u1 (#) (h" (#) (v2 - u2))).n by
      RFUNCT_2:6;
    end;
    then
A49: h" (#) (v1(#)u2 - u1(#)v2) = h"(#)(v1 - u1)(#)u2 - u1(#)(h"(#) (v2 -
    u2)) by FUNCT_2:113;
    then
A50: h" (#) (v1(#)u2 - u1(#)v2) is convergent by A47,A38,SEQ_2:25;
    hence h"(#)((f1/f2)/*(h+c) - (f1/f2)/*c) is convergent by A1,A30,A43,A48,
    A44,SEQ_2:37;
    lim h1 = diff(f1,x0) by A2,A16,A28,Th12;
    then lim (h1(#)u2) = diff(f1,x0) * f2.x0 by A32,A45,A46,SEQ_2:29;
    then
    lim (h" (#) (v1(#)u2 - u1(#) v2)) = diff(f1,x0) * f2.x0 - diff(f2,x0)
    * f1.x0 by A49,A47,A38,A39,SEQ_2:26;
    hence thesis by A1,A30,A43,A48,A50,A44,SEQ_2:38;
  end;
  N c= dom (f1/f2) by A13,RFUNCT_1:def 4;
  hence thesis by A14,Th12;
end;

theorem Th15:
  f.x0 <> 0 & f is_differentiable_in x0 implies f^
  is_differentiable_in x0 & diff(f^,x0) = - diff(f,x0)/(f.x0)^2
proof
  reconsider f1 = dom f --> 1 as PartFunc of dom f, REAL by FUNCOP_1:57;
  dom f1 c= REAL by RELAT_1:def 18;
  then reconsider f1 as PartFunc of REAL, REAL by RELSET_1:13;
  assume that
A1: f.x0 <> 0 and
A2: f is_differentiable_in x0;
  consider N be Neighbourhood of x0 such that
A3: N c= dom f by A2,Th11;
A4: x0 in N by RCOMP_1:37;
A5: dom f1 = dom f by FUNCOP_1:19;
A6: rng f1 = {1}
  proof
    thus rng f1 c= {1} by FUNCOP_1:19;
    let x;
A7: x0 in N by RCOMP_1:37;
    assume x in {1};
    then x = 1 by TARSKI:def 1;
    then f1.x0 = x by A3,A7,FUNCOP_1:13;
    hence thesis by A5,A3,A7,FUNCT_1:def 5;
  end;
  then
A8: f1 is_differentiable_on N by A5,A3,FDIFF_1:19;
  then
A9: f1 is_differentiable_in x0 by A4,FDIFF_1:16;
  0 = (f1`|N).x0 by A5,A3,A6,FDIFF_1:19,RCOMP_1:37
    .= diff(f1,x0) by A8,A4,FDIFF_1:def 8;
  then
  diff(f1/f,x0) = (0 * f.x0 - diff(f,x0) * f1.x0)/((f.x0)^2) by A1,A2,A9
  ,Th14;
  then
A10: diff(f1/f,x0) = (- diff(f,x0) * f1.x0)/((f.x0)^2)
    .= (- diff(f,x0) * 1)/((f.x0)^2) by A3,A4,FUNCOP_1:13
    .= - diff(f,x0)/((f.x0)^2) by XCMPLX_1:188;
A11: dom (f1/f) = dom f1 /\ (dom f \ f"{0}) by RFUNCT_1:def 4
    .= dom f \ f"{0} by A5,XBOOLE_1:28,36
    .= dom (f^) by RFUNCT_1:def 8;
A12: dom f \ f"{0} c= dom f1 by A5,XBOOLE_1:36;
A13: now
A14: dom f \ f"{0} = dom (f^) by RFUNCT_1:def 8;
    let r such that
A15: r in dom (f1/f);
    thus (f1/f).r = f1.r * (f.r)" by A15,RFUNCT_1:def 4
      .= 1 * (f.r)" by A5,A12,A11,A15,A14,FUNCOP_1:13
      .= (f^).r by A11,A15,RFUNCT_1:def 8;
  end;
  f1/f is_differentiable_in x0 by A1,A2,A9,Th14;
  hence thesis by A10,A11,A13,PARTFUN1:34;
end;

theorem
  f is_differentiable_on A implies f|A is_differentiable_on A & f`|A = (
  f|A) `| A
proof
  assume
A1: f is_differentiable_on A;
  then A c= dom f by FDIFF_1:def 7;
  then A c= dom f /\ A by XBOOLE_1:19;
  then
A2: A c= dom (f|A) by RELAT_1:90;
  now
    let x0;
    assume x0 in A;
    then f|A is_differentiable_in x0 by A1,FDIFF_1:def 7;
    hence (f|A)|A is_differentiable_in x0 by RELAT_1:101;
  end;
  hence
A3: f|A is_differentiable_on A by A2,FDIFF_1:def 7;
  then
A4: dom((f|A)`|A) = A by FDIFF_1:def 8;
A5: now
    let x0;
    assume
A6: x0 in A;
    then consider N2 being Neighbourhood of x0 such that
A7: N2 c= A by RCOMP_1:39;
A8: f|A is_differentiable_in x0 by A1,A6,FDIFF_1:def 7;
A9: f is_differentiable_in x0 by A1,A6,FDIFF_1:16;
    then consider N1 being Neighbourhood of x0 such that
A10: N1 c= dom f and
A11: ex L,R st for r st r in N1 holds f.r - f.x0 = L.(r - x0) + R.(r -
    x0) by FDIFF_1:def 5;
    consider L,R such that
A12: for r st r in N1 holds f.r - f.x0 = L.(r - x0) + R.(r - x0) by
    A11;
    consider N being Neighbourhood of x0 such that
A13: N c= N1 and
A14: N c= N2 by RCOMP_1:38;
A15: N c= A by A7,A14,XBOOLE_1:1;
    then
A16: N c= dom (f|A) by A2,XBOOLE_1:1;
A17: now
      let r;
      assume
A18:  r in N;
      then
A19:  r in A by A15;
      thus (f|A).r - (f|A).x0 = (f|A).r - f.x0 by A2,A6,FUNCT_1:70
        .= f.r - f.x0 by A2,A19,FUNCT_1:70
        .= L.(r-x0) + R.(r-x0) by A12,A13,A18;
    end;
    thus (f`|A).x0 = diff(f,x0) by A1,A6,FDIFF_1:def 8
      .= L.1 by A9,A10,A12,FDIFF_1:def 6
      .= diff(f|A,x0) by A8,A16,A17,FDIFF_1:def 6
      .= ((f|A)`|A).x0 by A3,A6,FDIFF_1:def 8;
  end;
  dom (f`|A) = A by A1,FDIFF_1:def 8;
  hence thesis by A4,A5,PARTFUN1:34;
end;

theorem
  f1 is_differentiable_on A & f2 is_differentiable_on A implies f1 + f2
  is_differentiable_on A & (f1 + f2)`|A = f1`|A + f2`|A
proof
  assume that
A1: f1 is_differentiable_on A and
A2: f2 is_differentiable_on A;
A3: A c= dom f2 by A2,FDIFF_1:def 7;
  A c= dom f1 by A1,FDIFF_1:def 7;
  then A c= dom f1 /\ dom f2 by A3,XBOOLE_1:19;
  then
A4: A c= dom (f1 + f2) by VALUED_1:def 1;
  then f1 + f2 is_differentiable_on A by A1,A2,FDIFF_1:26;
  then
A5: dom ((f1 + f2)`|A) = A by FDIFF_1:def 8;
A6: dom (f2`|A) = A by A2,FDIFF_1:def 8;
  dom (f1`|A) = A by A1,FDIFF_1:def 8;
  then dom (f1`|A) /\ dom (f2`|A) = A by A6;
  then
A7: dom ((f1`|A) + (f2`|A)) = A by VALUED_1:def 1;
  now
    let x0;
    assume
A8: x0 in A;
    hence ((f1 + f2)`|A).x0 = diff(f1,x0) + diff(f2,x0) by A1,A2,A4,
    FDIFF_1:26
      .= (f1`|A).x0 + diff(f2,x0) by A1,A8,FDIFF_1:def 8
      .= (f1`|A).x0 + (f2`|A).x0 by A2,A8,FDIFF_1:def 8
      .= ((f1`|A) + (f2`|A)).x0 by A7,A8,VALUED_1:def 1;
  end;
  hence thesis by A1,A2,A4,A5,A7,FDIFF_1:26,PARTFUN1:34;
end;

theorem
  f1 is_differentiable_on A & f2 is_differentiable_on A implies f1 - f2
  is_differentiable_on A & (f1 - f2)`|A = f1`|A - f2`|A
proof
  assume that
A1: f1 is_differentiable_on A and
A2: f2 is_differentiable_on A;
A3: A c= dom f2 by A2,FDIFF_1:def 7;
  A c= dom f1 by A1,FDIFF_1:def 7;
  then A c= dom f1 /\ dom f2 by A3,XBOOLE_1:19;
  then
A4: A c= dom (f1 - f2) by VALUED_1:12;
  then f1 - f2 is_differentiable_on A by A1,A2,FDIFF_1:27;
  then
A5: dom ((f1 - f2)`|A) = A by FDIFF_1:def 8;
A6: dom (f2`|A) = A by A2,FDIFF_1:def 8;
  dom (f1`|A) = A by A1,FDIFF_1:def 8;
  then dom (f1`|A) /\ dom (f2`|A) = A by A6;
  then
A7: dom ((f1`|A) - (f2`|A)) = A by VALUED_1:12;
  now
    let x0;
    assume
A8: x0 in A;
    hence ((f1 - f2)`|A).x0 = diff(f1,x0) - diff(f2,x0) by A1,A2,A4,
    FDIFF_1:27
      .= (f1`|A).x0 - diff(f2,x0) by A1,A8,FDIFF_1:def 8
      .= (f1`|A).x0 - (f2`|A).x0 by A2,A8,FDIFF_1:def 8
      .= ((f1`|A) - (f2`|A)).x0 by A7,A8,VALUED_1:13;
  end;
  hence thesis by A1,A2,A4,A5,A7,FDIFF_1:27,PARTFUN1:34;
end;

theorem
  f is_differentiable_on A implies r(#)f is_differentiable_on A & (r(#)f
  )`|A = r(#)(f`|A)
proof
  assume
A1: f is_differentiable_on A;
  then A c= dom f by FDIFF_1:def 7;
  then
A2: A c= dom (r(#)f) by VALUED_1:def 5;
  then r(#)f is_differentiable_on A by A1,FDIFF_1:28;
  then
A3: dom ((r(#)f)`|A) = A by FDIFF_1:def 8;
  dom (f`|A) = A by A1,FDIFF_1:def 8;
  then
A4: dom (r(#)(f`|A)) = A by VALUED_1:def 5;
  now
    let x0;
    assume
A5: x0 in A;
    hence ((r(#)f)`|A).x0 = r * diff(f,x0) by A1,A2,FDIFF_1:28
      .= r * ((f`|A).x0) by A1,A5,FDIFF_1:def 8
      .= (r(#)(f`|A)).x0 by A4,A5,VALUED_1:def 5;
  end;
  hence thesis by A1,A2,A3,A4,FDIFF_1:28,PARTFUN1:34;
end;

theorem
  f1 is_differentiable_on A & f2 is_differentiable_on A implies f1 (#)
  f2 is_differentiable_on A & (f1(#)f2)`|A = (f1`|A)(#)f2 + f1(#)(f2`|A)
proof
  assume that
A1: f1 is_differentiable_on A and
A2: f2 is_differentiable_on A;
A3: A c= dom f1 by A1,FDIFF_1:def 7;
A4: A c= dom f2 by A2,FDIFF_1:def 7;
  then A c= dom f1 /\ dom f2 by A3,XBOOLE_1:19;
  then
A5: A c= dom (f1(#)f2) by VALUED_1:def 4;
  then f1(#)f2 is_differentiable_on A by A1,A2,FDIFF_1:29;
  then
A6: dom ((f1(#)f2)`|A) = A by FDIFF_1:def 8;
  dom (f2`|A) = A by A2,FDIFF_1:def 8;
  then dom f1 /\ dom (f2`|A) = A by A3,XBOOLE_1:28;
  then
A7: dom (f1(#)(f2`|A)) = A by VALUED_1:def 4;
  dom (f1`|A) = A by A1,FDIFF_1:def 8;
  then dom (f1`|A) /\ dom f2 = A by A4,XBOOLE_1:28;
  then dom ((f1`|A)(#)f2) = A by VALUED_1:def 4;
  then dom ((f1`|A)(#)f2) /\ dom (f1(#)(f2`|A)) = A by A7;
  then
A8: dom (((f1`|A)(#)f2) + (f1(#)(f2`|A))) = A by VALUED_1:def 1;
  now
    let x0;
    assume
A9: x0 in A;
    hence
    ((f1(#) f2)`|A).x0 = diff(f1,x0)*(f2.x0) + (f1.x0)*diff(f2,x0) by A1,
    A2,A5, FDIFF_1:29
      .= (f1`|A).x0*f2.x0 + (f1.x0)*diff(f2,x0) by A1,A9,FDIFF_1:def 8
      .= (f1`|A).x0*f2.x0 + (f1.x0)*(f2`|A).x0 by A2,A9,FDIFF_1:def 8
      .= ((f1`|A)(#)f2).x0 + (f1.x0)*(f2`|A).x0 by VALUED_1:5
      .= ((f1`|A)(#)f2).x0 + (f1(#)(f2`|A)).x0 by VALUED_1:5
      .= (((f1`|A)(#)f2) + (f1(#)(f2`|A))).x0 by A8,A9,VALUED_1:def 1;
  end;
  hence thesis by A1,A2,A5,A6,A8,FDIFF_1:29,PARTFUN1:34;
end;

Lm3: (f(#)f)"{0} = f"{0}
proof
  thus (f(#)f)"{0} c= f"{0}
  proof
    let x;
    assume
A1: x in (f(#)f)"{0};
    then reconsider x9 = x as Real;
    (f (#) f).x9 in {0} by A1,FUNCT_1:def 13;
    then 0 = (f (#) f).x9 by TARSKI:def 1
      .= f.x9 * f.x9 by VALUED_1:5;
    then f.x9 = 0;
    then
A2: f.x9 in {0} by TARSKI:def 1;
    x9 in dom (f (#) f) by A1,FUNCT_1:def 13;
    then x9 in dom f /\ dom f by VALUED_1:def 4;
    hence thesis by A2,FUNCT_1:def 13;
  end;
  let x;
  assume
A3: x in f"{0};
  then reconsider x9 = x as Real;
  f.x9 in {0} by A3,FUNCT_1:def 13;
  then 0 = f.x9 by TARSKI:def 1;
  then f.x9 * f.x9 = 0;
  then (f (#) f).x9 = 0 by VALUED_1:5;
  then
A4: (f (#) f).x9 in {0} by TARSKI:def 1;
  x9 in dom f /\ dom f by A3,FUNCT_1:def 13;
  then x9 in dom (f (#) f) by VALUED_1:def 4;
  hence thesis by A4,FUNCT_1:def 13;
end;

theorem
  f1 is_differentiable_on A & f2 is_differentiable_on A & (for x0 st x0
in A holds f2.x0 <> 0) implies f1/f2 is_differentiable_on A & (f1/f2)`|A = (f1
  `|A (#) f2 - f2`|A (#) f1)/(f2 (#) f2)
proof
  assume that
A1: f1 is_differentiable_on A and
A2: f2 is_differentiable_on A and
A3: for x0 st x0 in A holds f2.x0 <> 0;
A4: A c= dom f1 by A1,FDIFF_1:16;
A5: A c= dom f2 by A2,FDIFF_1:16;
A6: A c= dom f2 \ f2"{0}
  proof
    let x;
    assume
A7: x in A;
    then reconsider x9 = x as Real;
    assume not x in dom f2 \ f2"{0};
    then not x in dom f2 or x in f2"{0} by XBOOLE_0:def 5;
    then f2.x9 in {0} by A5,A7,FUNCT_1:def 13;
    then f2.x9 = 0 by TARSKI:def 1;
    hence contradiction by A3,A7;
  end;
  then A c= dom f1 /\ (dom f2 \ f2"{0}) by A4,XBOOLE_1:19;
  then
A8: A c= dom (f1/f2) by RFUNCT_1:def 4;
A9: now
    let x0;
    assume
A10: x0 in A;
    hence
A11: f2. x0 <> 0 by A3;
    thus
A12: f1 is_differentiable_in x0 by A1,A10,FDIFF_1:16;
    thus f2 is_differentiable_in x0 by A2,A10,FDIFF_1:16;
    hence f1/f2 is_differentiable_in x0 by A11,A12,Th14;
  end;
  then for x0 holds x0 in A implies f1/f2 is_differentiable_in x0;
  hence
A13: f1/f2 is_differentiable_on A by A8,FDIFF_1:16;
  then
A14: A = dom ((f1/f2)`|A) by FDIFF_1:def 8;
A15: now
    let x0;
    assume
A16: x0 in dom ((f1/f2)`|A);
    then
A17: f1 is_differentiable_in x0 by A9,A14;
    dom (f2`|A) = A by A2,FDIFF_1:def 8;
    then x0 in dom (f2`|A) /\ dom f1 by A4,A14,A16,XBOOLE_0:def 4;
    then
A18: x0 in dom ((f2`|A)(#)f1) by VALUED_1:def 4;
    dom (f1`|A) = A by A1,FDIFF_1:def 8;
    then x0 in dom (f1`|A) /\ dom f2 by A5,A14,A16,XBOOLE_0:def 4;
    then x0 in dom ((f1`|A)(#)f2) by VALUED_1:def 4;
    then x0 in dom ((f1`|A)(#)f2) /\ dom ((f2`|A)(#)f1) by A18, XBOOLE_0:
    def 4;
    then
A19: x0 in dom ((f1`|A)(#)f2 - (f2`|A)(#)f1) by VALUED_1:12;
A20: f2.x0 <> 0 by A9,A14,A16;
    then f2.x0 * f2.x0 <> 0;
    then (f2 (#) f2).x0 <> 0 by VALUED_1:5;
    then not (f2 (#) f2).x0 in {0} by TARSKI:def 1;
    then
A21: not x0 in (f2 (#) f2)"{0} by FUNCT_1:def 13;
    x0 in dom f2 /\ dom f2 by A5,A14,A16;
    then x0 in dom (f2 (#) f2) by VALUED_1:def 4;
    then x0 in dom (f2 (#) f2) \ (f2 (#) f2)"{0} by A21,XBOOLE_0:def 5;
    then
    x0 in dom ((f1`|A)(#)f2 - (f2`|A)(#)f1) /\ (dom (f2 (#) f2) \ (f2 (#)
    f2)"{0}) by A19,XBOOLE_0:def 4;
    then
A22: x0 in dom (((f1`|A)(#)f2 - (f2`|A)(#)f1)/(f2 (#) f2)) by
    RFUNCT_1:def 4;
A23: f2 is_differentiable_in x0 by A9,A14,A16;
    thus ((f1/f2)`|A).x0 = diff(f1/f2,x0) by A13,A14,A16,FDIFF_1:def 8
      .= (diff(f1,x0) * f2.x0 - diff(f2,x0) * f1.x0)/(f2.x0)^2 by A20,A17,
    A23, Th14
      .= ((f1`|A).x0 * f2.x0 - diff(f2,x0) * f1.x0)/(f2.x0)^2 by A1,A14,A16,
    FDIFF_1:def 8
      .= ((f1`|A).x0 * f2.x0 - (f2`|A).x0 * f1.x0)/(f2.x0)^2 by A2,A14,A16,
    FDIFF_1:def 8
      .= (((f1`|A) (#) f2).x0 - (f2`|A).x0 * f1.x0)/(f2.x0)^2 by VALUED_1:5
      .= (((f1`|A) (#) f2).x0 - ((f2`|A) (#) f1).x0)/(f2.x0)^2 by VALUED_1:5
      .= ((f1`|A) (#) f2 - (f2`|A) (#) f1).x0/(f2.x0 * f2.x0) by A19,
    VALUED_1:13
      .= ((f1`|A) (#) f2 - (f2`|A) (#) f1).x0/(f2 (#) f2).x0 by VALUED_1:5
      .= ((f1`|A) (#) f2 - (f2`|A) (#) f1).x0 * ((f2 (#) f2).x0)" by
    XCMPLX_0:def 9
      .= (((f1`|A) (#) f2 - (f2`|A) (#) f1)/(f2 (#) f2)).x0 by A22,RFUNCT_1:
    def 4;
  end;
  dom ((f1`|A (#) f2 - f2`|A (#) f1)/(f2 (#) f2)) = dom (f1`|A (#) f2 -
  f2`|A (#) f1) /\ (dom (f2 (#) f2) \ (f2 (#) f2)"{0}) by RFUNCT_1:def 4
    .= dom (f1`|A (#) f2) /\ dom (f2`|A (#) f1) /\ (dom (f2 (#) f2) \ (f2
  (#) f2)"{0}) by VALUED_1:12
    .= dom (f1`|A) /\ dom f2 /\ dom (f2`|A (#) f1) /\ (dom (f2 (#) f2) \ (f2
  (#) f2)"{0}) by VALUED_1:def 4
    .= A /\ dom f2 /\ dom (f2`|A (#) f1) /\ (dom (f2 (#) f2) \ (f2 (#) f2)"{
  0}) by A1,FDIFF_1:def 8
    .= A /\ dom (f2`|A (#) f1) /\ (dom (f2 (#) f2) \ (f2 (#) f2)"{0}) by A5,
  XBOOLE_1:28
    .= A /\ (dom (f2`|A) /\ dom f1) /\ (dom (f2 (#) f2) \ (f2 (#) f2)"{0})
  by VALUED_1:def 4
    .= A /\ (A /\ dom f1) /\ (dom (f2 (#) f2) \ (f2 (#) f2)"{0}) by A2,
  FDIFF_1:def 8
    .= A /\ A /\ (dom (f2 (#) f2) \ (f2 (#) f2)"{0}) by A4,XBOOLE_1:28
    .= A /\ (dom f2 /\ dom f2 \ (f2 (#) f2)"{0}) by VALUED_1:def 4
    .= A /\ (dom f2 \ f2"{0}) by Lm3
    .= dom ((f1/f2)`|A) by A6,A14,XBOOLE_1:28;
  hence thesis by A15,PARTFUN1:34;
end;

theorem
  f is_differentiable_on A & (for x0 st x0 in A holds f.x0 <> 0) implies
  f^ is_differentiable_on A & (f^)`|A = - (f`|A)/ (f (#) f)
proof
  assume that
A1: f is_differentiable_on A and
A2: for x0 st x0 in A holds f.x0 <> 0;
A3: now
    let x0;
    assume
A4: x0 in A;
    hence
A5: f. x0 <> 0 by A2;
    thus f is_differentiable_in x0 by A1,A4,FDIFF_1:16;
    hence f^ is_differentiable_in x0 by A5,Th15;
  end;
  then
A6: for x0 holds x0 in A implies f^ is_differentiable_in x0;
A7: A c= dom f by A1,FDIFF_1:16;
A8: A c= dom f \ f"{0}
  proof
    let x;
    assume
A9: x in A;
    then reconsider x9 = x as Real;
    assume not x in dom f \ f"{0};
    then not x in dom f or x in f"{0} by XBOOLE_0:def 5;
    then f.x9 in {0} by A7,A9,FUNCT_1:def 13;
    then f.x9 = 0 by TARSKI:def 1;
    hence contradiction by A2,A9;
  end;
  then A c= dom (f^) by RFUNCT_1:def 8;
  hence
A10: f^ is_differentiable_on A by A6,FDIFF_1:16;
  then
A11: A = dom ((f^)`|A) by FDIFF_1:def 8;
A12: now
    let x0;
A13: dom (f`|A) = A by A1,FDIFF_1:def 8;
    assume
A14: x0 in dom ((f^)`|A);
    then
A15: f is_differentiable_in x0 by A3,A11;
A16: f.x0 <> 0 by A3,A11,A14;
    then f.x0 * f.x0 <> 0;
    then (f (#) f).x0 <> 0 by VALUED_1:5;
    then not (f (#) f).x0 in {0} by TARSKI:def 1;
    then
A17: not x0 in (f (#) f)"{0} by FUNCT_1:def 13;
    x0 in dom f /\ dom f by A7,A11,A14;
    then x0 in dom (f (#) f) by VALUED_1:def 4;
    then x0 in dom (f (#) f) \ (f (#) f)"{0} by A17,XBOOLE_0:def 5;
    then x0 in dom (f`|A) /\ (dom (f (#) f) \ (f (#) f)"{0}) by A11,A14,A13,
    XBOOLE_0:def 4;
    then
A18: x0 in dom ((f`|A)/(f (#) f)) by RFUNCT_1:def 4;
    thus ((f^)`|A).x0 = diff(f^,x0) by A10,A11,A14,FDIFF_1:def 8
      .= - diff(f,x0)/(f.x0)^2 by A16,A15,Th15
      .= - (f`|A).x0/(f.x0 * f.x0) by A1,A11,A14,FDIFF_1:def 8
      .= - (f`|A).x0/(f (#) f).x0 by VALUED_1:5
      .= - (f`|A).x0 * ((f (#) f).x0)" by XCMPLX_0:def 9
      .= - ((f`|A)/(f (#) f)).x0 by A18,RFUNCT_1:def 4
      .= (- (f`|A)/(f (#) f)).x0 by VALUED_1:8;
  end;
  dom (- (f`|A)/(f (#) f)) = dom ((f`|A)/(f (#) f)) by VALUED_1:8
    .= dom (f`|A) /\ (dom (f (#) f) \ (f (#) f)"{0}) by RFUNCT_1:def 4
    .= A /\ (dom (f (#) f) \ (f (#) f)"{0}) by A1,FDIFF_1:def 8
    .= A /\ (dom f /\ dom f \ (f (#) f)"{0}) by VALUED_1:def 4
    .= A /\ (dom f \ f"{0}) by Lm3
    .= dom ((f^)`|A) by A8,A11,XBOOLE_1:28;
  hence thesis by A12,PARTFUN1:34;
end;

theorem
  f1 is_differentiable_on A & (f1.:A) is open Subset of REAL & f2
is_differentiable_on (f1.:A) implies f2*f1 is_differentiable_on A & (f2*f1)`|A
  = ((f2`|(f1.:A))*f1) (#) (f1`|A)
proof
  assume that
A1: f1 is_differentiable_on A and
A2: (f1.:A) is open Subset of REAL and
A3: f2 is_differentiable_on (f1.:A);
A4: A c= dom f1 by A1,FDIFF_1:16;
A5: now
    let x0;
    assume
A6: x0 in A;
    hence
A7: f1 is_differentiable_in x0 by A1,FDIFF_1:16;
    thus x0 in dom f1 by A4,A6;
    thus f1.x0 in f1.:A by A4,A6,FUNCT_1:def 12;
    hence f2 is_differentiable_in f1.x0 by A2,A3,FDIFF_1:16;
    hence f2*f1 is_differentiable_in x0 by A7,Th13;
  end;
  f1.:A c= dom f2 by A2,A3,FDIFF_1:16;
  then
A8: A c= dom (f2*f1) by A4,FUNCT_3:3;
  for x0 holds x0 in A implies f2*f1 is_differentiable_in x0 by A5;
  hence
A9: f2*f1 is_differentiable_on A by A8,FDIFF_1:16;
  then
A10: dom ((f2*f1)`|A) = A by FDIFF_1:def 8;
A11: now
    let x0;
    assume
A12: x0 in dom ((f2*f1)`|A);
    then
A13: f1 is_differentiable_in x0 by A5,A10;
A14: x0 in dom f1 by A5,A10,A12;
A15: f1.x0 in f1.:A by A5,A10,A12;
A16: f2 is_differentiable_in f1.x0 by A5,A10,A12;
    thus ((f2*f1)`|A).x0 = diff(f2*f1,x0) by A9,A10,A12,FDIFF_1:def 8
      .= diff(f2,f1.x0) * diff(f1,x0) by A13,A16,Th13
      .= diff(f2,f1.x0) * (f1`|A).x0 by A1,A10,A12,FDIFF_1:def 8
      .= (f2`|(f1.:A)).(f1.x0) * (f1`|A).x0 by A3,A15,FDIFF_1:def 8
      .= ((f2`|(f1.:A))*f1).x0 * (f1`|A).x0 by A14,FUNCT_1:23
      .= (((f2`|(f1.:A))*f1) (#) (f1`|A)).x0 by VALUED_1:5;
  end;
  dom (f2`|(f1.:A)) = f1.:A by A3,FDIFF_1:def 8;
  then A c= dom ((f2`|(f1.:A))*f1) by A4,FUNCT_1:171;
  then dom ((f2*f1)`|A) = dom ((f2`|(f1.:A))*f1) /\ A by A10,XBOOLE_1:28
    .= dom ((f2`|(f1.:A))*f1) /\ dom (f1`|A) by A1,FDIFF_1:def 8
    .= dom (((f2`|(f1.:A))*f1) (#) (f1`|A)) by VALUED_1:def 4;
  hence thesis by A11,PARTFUN1:34;
end;

theorem Th24:
  A c= dom f & (for r,p st r in A & p in A holds abs(f.r - f.p) <=
(r - p)^2) implies f is_differentiable_on A & for x0 st x0 in A holds diff(f,x0
  ) = 0
proof
  assume that
A1: A c= dom f and
A2: for r,p st r in A & p in A holds abs(f.r - f.p) <= (r - p)^2;
A3: now
    let x0;
    assume x0 in A;
    then consider N be Neighbourhood of x0 such that
A4: N c= A by RCOMP_1:39;
A5: N c= dom f by A1,A4,XBOOLE_1:1;
    for h,c st rng c = {x0} & rng (h + c) c= dom f holds h"(#)(f/*(h+c) -
    f/*c) is convergent & lim (h"(#) (f/*(h+c) - f/*c)) = 0
    proof
      reconsider a = NAT --> 0 as Real_Sequence by FUNCOP_1:57;
      let h,c;
      assume that
A6:   rng c = {x0} and
A7:   rng (h + c) c= dom f;
A8:   lim (h + c) = x0 by A6,Th4;
      consider r be real number such that
A9:   0 < r and
A10:  N = ].x0 - r, x0 + r.[ by RCOMP_1:def 7;
      h + c is convergent by A6,Th4;
      then consider n such that
A11:  for m st n <= m holds abs((h + c).m - x0) < r by A8,A9,
      SEQ_2:def 7;
      set hc = (h + c)^\n;
A12:  rng hc c= A
      proof
        let x;
        assume x in rng hc;
        then consider m such that
A13:    x = hc.m by FUNCT_2:190;
        x = (h + c).(m+n) by A13,NAT_1:def 3;
        then abs(hc.m - x0) < r by A11,A13,NAT_1:12;
        then x in N by A10,A13,RCOMP_1:8;
        hence thesis by A4;
      end;
A14:  rng c c= dom f
      proof
        let x;
        assume x in rng c;
        then
A15:    x = x0 by A6,TARSKI:def 1;
        x0 in N by RCOMP_1:37;
        hence thesis by A5,A15;
      end;
      set fn = (h"(#)(f/*(h+c) - f/*c))^\n;
      set h1 = h^\n;
A16:  h1 is convergent by FDIFF_1:def 1;
      then
A17:  abs(h1) is convergent by SEQ_4:26;
      set c1 = c^\n;
A18:  rng c1 c= A
      proof
A19:    rng c1 c= rng c by VALUED_0:21;
        let x;
        assume x in rng c1;
        then
A20:    x = x0 by A6,A19,TARSKI:def 1;
        x0 in N by RCOMP_1:37;
        hence thesis by A4,A20;
      end;
      h1 is non-zero by FDIFF_1:def 1;
      then
A21:  abs(h1) is non-zero by SEQ_1:61;
A22:  for m holds a.m <= abs(fn).m & abs(fn).m <= abs(h1).m
      proof
        let m;
A23:    c1.m in rng c1 by VALUED_0:28;
        hc.m in rng hc by VALUED_0:28;
        then abs(f.(hc.m) - f.(c1.m)) <= (hc.m - c1.m)^2 by A2,A12,A18,
        A23;
        then
A24:    abs(f.(hc.m) - f.(c1.m)) <= (abs(hc.m - c1.m))^2 by
        COMPLEX1
        :161;
A25:    (abs(h1).m)" * abs((f/*(h+c) - (f/*c))^\n).m = (abs(h1)").m * abs
        ((f/*(h+c) - f/*c)^\n).m by VALUED_1:10
          .= (abs(h1)" (#) abs((f/*(h+c) - f/*c)^\n)).m by SEQ_1:12
          .= (abs(h1") (#) abs((f/*(h+c) - f/*c)^\n)).m by SEQ_1:62
          .= abs(h1" (#) ((f/*(h+c) - f/*c)^\n)).m by SEQ_1:60
          .= abs((h")^\n (#) ((f/*(h+c) - f/*c)^\n)).m by SEQM_3:41
          .= abs((h"(#)(f/*(h+c) - f/*c))^\n).m by SEQM_3:42;
        0 <= abs( fn.m ) by COMPLEX1:132;
        then a.m <= abs( fn.m ) by FUNCOP_1:13;
        hence a.m <= abs(fn).m by SEQ_1:16;
A26:    (abs(hc.m - c1.m))^2 = (abs((h1 + c1).m - c1.m))^2 by SEQM_3:37
          .= (abs(h1.m + c1.m - c1.m))^2 by SEQ_1:11
          .= (abs(h1).m)^2 by SEQ_1:16
          .= abs(h1).m * abs(h1).m;
        0 <= abs(h1.m) by COMPLEX1:132;
        then
A27:    0 <= abs(h1).m by SEQ_1:16;
A28:    abs(h1).m <> 0 by A21,SEQ_1:7;
A29:    abs(h1).m * abs(h1).m * (abs(h1).m)" = abs(h1).m * (abs(h1).m * (
        abs(h1).m)")
          .= abs(h1).m * 1 by A28,XCMPLX_0:def 7
          .= abs(h1).m;
        abs(f.(hc.m) - f.(c1.m)) = abs((f/*hc).m - f.(c1.m)) by A1,A12,
        FUNCT_2:185,XBOOLE_1:1
          .= abs((f/*hc).m - (f/*c1).m) by A1,A18,FUNCT_2:185,XBOOLE_1:1
          .= abs(((f/*hc) - (f/*c1)).m) by RFUNCT_2:6
          .= abs((f/*hc) - (f/*c1)).m by SEQ_1:16
          .= abs((f/*(h+c))^\n - (f/*c1)).m by A7,VALUED_0:27
          .= abs((f/*(h+c))^\n - ((f/*c)^\n)).m by A14,VALUED_0:27
          .= abs((f/*(h+c) - (f/*c))^\n).m by SEQM_3:39;
        hence thesis by A24,A26,A27,A29,A25,XREAL_1:66;
      end;
      lim h1 = 0 by FDIFF_1:def 1;
      then
A30:  lim abs(h1) = abs(0) by A16,SEQ_4:27
        .= 0 by ABSVALUE:7;
A31:  lim a = a.0 by SEQ_4:41
        .= 0 by FUNCOP_1:13;
      then
A32:  lim abs(fn) = 0 by A17,A30,A22,SEQ_2:34;
A33:  abs(fn) is convergent by A31,A17,A30,A22,SEQ_2:33;
      then
A34:  fn is convergent by A32,SEQ_4:28;
      hence h"(#)(f/*(h+c) - f/*c) is convergent by SEQ_4:35;
      lim fn = 0 by A33,A32,SEQ_4:28;
      hence thesis by A34,SEQ_4:36;
    end;
    hence f is_differentiable_in x0 & diff(f,x0) = 0 by A5,Th12;
  end;
  then for x0 holds x0 in A implies f is_differentiable_in x0;
  hence f is_differentiable_on A by A1,FDIFF_1:16;
  let x0;
  assume x0 in A;
  hence thesis by A3;
end;

theorem Th25:
  (for r1,r2 st r1 in ].p,g.[ & r2 in ].p,g.[ holds abs(f.r1 - f.
r2) <= (r1 - r2)^2) & ].p,g.[ c= dom f implies f is_differentiable_on ].p,g.[ &
  f|].p,g.[ is constant
proof
  assume that
A1: for r1,r2 st r1 in ].p,g.[ & r2 in ].p,g.[ holds abs(f.r1-f.r2)<=(r1
  -r2)^2 and
A2: ].p,g.[ c= dom f;
  thus
A3: f is_differentiable_on ].p,g.[ by A1,A2,Th24;
  for x0 st x0 in ].p,g.[ holds diff(f,x0)=0 by A1,A2,Th24;
  hence thesis by A2,A3,ROLLE:7;
end;

theorem
  left_open_halfline(r) c= dom f & (for r1,r2 st r1 in
left_open_halfline(r) & r2 in left_open_halfline(r) holds abs(f.r1 - f.r2) <= (
  r1 - r2)^2) implies f is_differentiable_on left_open_halfline(r) & f|
  left_open_halfline r is constant
proof
  assume that
A1: left_open_halfline(r) c= dom f and
A2: for r1,r2 st r1 in left_open_halfline(r) & r2 in left_open_halfline(
  r) holds abs(f.r1 - f.r2) <= (r1 - r2)^2;
  now
    let r1,r2;
    assume that
A3: r1 in left_open_halfline(r) /\ dom f and
A4: r2 in left_open_halfline(r) /\ dom f;
    set rr = min(r1,r2);
A5: ].rr - 1, r.[ c= left_open_halfline(r) by XXREAL_1:263;
    then
A6: for g1,g2 st g1 in ].rr - 1, r.[ & g2 in ].rr - 1, r.[ holds abs(f.g1
    - f.g2) <= (g1 - g2)^2 by A2;
    r2 in left_open_halfline(r) by A4,XBOOLE_0:def 4;
    then r2 in {p: p < r} by XXREAL_1:229;
    then
A7: ex g2 st g2 = r2 & g2 < r;
    rr - 1 < r2 - 0 by XREAL_1:17,XXREAL_0:17;
    then r2 in {g2: rr - 1 < g2 & g2 < r} by A7;
    then
A8: r2 in ].rr - 1, r.[ by RCOMP_1:def 2;
    r2 in dom f by A4,XBOOLE_0:def 4;
    then
A9: r2 in ].rr - 1, r.[ /\ dom f by A8,XBOOLE_0:def 4;
    r1 in left_open_halfline(r) by A3,XBOOLE_0:def 4;
    then r1 in {g: g < r} by XXREAL_1:229;
    then
A10: ex g1 st g1 = r1 & g1 < r;
    rr - 1 < r1 - 0 by XREAL_1:17,XXREAL_0:17;
    then r1 in {g1: rr - 1 < g1 & g1 < r} by A10;
    then
A11: r1 in ].rr - 1, r.[ by RCOMP_1:def 2;
    r1 in dom f by A3,XBOOLE_0:def 4;
    then
A12: r1 in ].rr - 1, r.[ /\ dom f by A11,XBOOLE_0:def 4;
    ].rr - 1, r.[ c= dom f by A1,A5,XBOOLE_1:1;
    then f|].rr-1,r.[ is constant by A6,Th25;
    hence f.r1 = f.r2 by A12,A9,PARTFUN2:77;
  end;
  hence thesis by A1,A2,Th24,PARTFUN2:77;
end;

theorem
  right_open_halfline(r) c= dom f & (for r1,r2 st r1 in
right_open_halfline(r) & r2 in right_open_halfline(r) holds abs(f.r1 - f.r2) <=
  (r1 - r2)^2) implies f is_differentiable_on right_open_halfline(r) & f|
  right_open_halfline r is constant
proof
  assume that
A1: right_open_halfline(r) c= dom f and
A2: for r1,r2 st r1 in right_open_halfline(r) & r2 in
  right_open_halfline(r) holds abs(f.r1 - f.r2) <= (r1 - r2)^2;
  now
    let r1,r2;
    assume that
A3: r1 in right_open_halfline(r) /\ dom f and
A4: r2 in right_open_halfline(r) /\ dom f;
    set rr = max(r1,r2);
A5: ].r, rr + 1.[ c= right_open_halfline(r) by XXREAL_1:247;
    then
A6: for g1,g2 st g1 in ].r, rr + 1 .[ & g2 in ].r, rr + 1.[ holds abs(f.
    g1 - f.g2) <= (g1 - g2)^2 by A2;
    r2 in right_open_halfline(r) by A4,XBOOLE_0:def 4;
    then r2 in {p: r < p} by XXREAL_1:230;
    then
A7: ex g2 st g2 = r2 & r < g2;
    r2 + 0 < rr + 1 by XREAL_1:10,XXREAL_0:25;
    then r2 in {g2: r < g2 & g2 < rr + 1} by A7;
    then
A8: r2 in ].r, rr + 1.[ by RCOMP_1:def 2;
    r2 in dom f by A4,XBOOLE_0:def 4;
    then
A9: r2 in ].r, rr + 1.[ /\ dom f by A8,XBOOLE_0:def 4;
    r1 in right_open_halfline(r) by A3,XBOOLE_0:def 4;
    then r1 in {g: r < g} by XXREAL_1:230;
    then
A10: ex g1 st g1 = r1 & r < g1;
    r1 + 0 < rr + 1 by XREAL_1:10,XXREAL_0:25;
    then r1 in {g1: r < g1 & g1 < rr + 1} by A10;
    then
A11: r1 in ].r, rr + 1.[ by RCOMP_1:def 2;
    r1 in dom f by A3,XBOOLE_0:def 4;
    then
A12: r1 in ].r, rr + 1.[ /\ dom f by A11,XBOOLE_0:def 4;
    ].r, rr + 1 .[ c= dom f by A1,A5,XBOOLE_1:1;
    then f|].r,rr+1.[ is constant by A6,Th25;
    hence f.r1 = f.r2 by A12,A9,PARTFUN2:77;
  end;
  hence thesis by A1,A2,Th24,PARTFUN2:77;
end;

theorem
  f is total & (for r1,r2 holds abs(f.r1 - f.r2) <= (r1 - r2)^2) implies
  f is_differentiable_on [#](REAL) & f|[#]REAL is constant
proof
  assume that
A1: f is total and
A2: for r1,r2 holds abs(f.r1 - f.r2) <= (r1 - r2)^2;
A3: dom f = [#] REAL by A1,PARTFUN1:def 4;
A4: now
    let r1,r2;
    assume that
A5: r1 in [#](REAL) /\ dom f and
A6: r2 in [#] (REAL) /\ dom f;
    set rx = max(r1,r2);
    set rn = min(r1,r2);
A7: r1 + 0 < rx + 1 by XREAL_1:10,XXREAL_0:25;
A8: r2 + 0 < rx + 1 by XREAL_1:10,XXREAL_0:25;
    rn - 1 < r2 - 0 by XREAL_1:17,XXREAL_0:17;
    then r2 in {g2: rn - 1 < g2 & g2 < rx + 1} by A8;
    then
A9: r2 in ].rn - 1, rx + 1.[ by RCOMP_1:def 2;
    r2 in dom f by A6,XBOOLE_0:def 4;
    then
A10: r2 in ].rn - 1, rx + 1.[ /\ dom f by A9,XBOOLE_0:def 4;
    rn - 1 < r1 - 0 by XREAL_1:17,XXREAL_0:17;
    then r1 in {g1: rn - 1 < g1 & g1 < rx + 1} by A7;
    then
A11: r1 in ].rn - 1, rx + 1.[ by RCOMP_1:def 2;
    r1 in dom f by A5,XBOOLE_0:def 4;
    then
A12: r1 in ].rn - 1, rx + 1.[ /\ dom f by A11,XBOOLE_0:def 4;
    for g1,g2 holds g1 in ].rn - 1, rx + 1 .[ & g2 in ].rn - 1, rx + 1.[
    implies abs(f.g1 - f.g2) <= (g1 - g2)^2 by A2;
    then f|].rn-1,rx+1.[ is constant by A3,Th25;
    hence f.r1 = f.r2 by A12,A10,PARTFUN2:77;
  end;
  for r1,r2 holds r1 in [#] REAL & r2 in [#] REAL implies abs(f.r1 - f.r2)
  <= (r1 - r2)^2 by A2;
  hence thesis by A3,A4,Th24,PARTFUN2:77;
end;

theorem Th29:
  left_open_halfline(r) c= dom f & f is_differentiable_on
left_open_halfline(r) & (for x0 st x0 in left_open_halfline(r) holds 0 < diff(f
,x0)) implies f|left_open_halfline r is increasing & f|left_open_halfline(r) is
  one-to-one
proof
  assume
A1: left_open_halfline(r) c= dom f;
  assume that
A2: f is_differentiable_on left_open_halfline(r) and
A3: for x0 st x0 in left_open_halfline(r) holds 0 < diff(f,x0);
  now
    let r1,r2;
    assume that
A4: r1 in left_open_halfline(r) /\ dom f and
A5: r2 in left_open_halfline(r) /\ dom f and
A6: r1 < r2;
    set rr = min(r1,r2);
A7: rr - 1 < r2 - 0 by XREAL_1:17,XXREAL_0:17;
    r2 in left_open_halfline(r) by A5,XBOOLE_0:def 4;
    then r2 in {p: p < r} by XXREAL_1:229;
    then ex g2 st g2 = r2 & g2 < r;
    then r2 in {g2: rr - 1 < g2 & g2 < r} by A7;
    then
A8: r2 in ].rr - 1, r.[ by RCOMP_1:def 2;
    r2 in dom f by A5,XBOOLE_0:def 4;
    then
A9: r2 in ].rr - 1, r.[ /\ dom f by A8,XBOOLE_0:def 4;
A10: f is_differentiable_on ].rr - 1, r.[ by A2,FDIFF_1:34,XXREAL_1:263;
A11: ].rr - 1, r.[ c= left_open_halfline(r) by XXREAL_1:263;
    then for g1 st g1 in ].rr - 1, r.[ holds 0 < diff(f,g1) by A3;
    then
A12: f|].rr-1,r.[ is increasing by A1,A11,A10,ROLLE:9,XBOOLE_1:1;
A13: rr - 1 < r1 - 0 by XREAL_1:17,XXREAL_0:17;
    r1 in left_open_halfline(r) by A4,XBOOLE_0:def 4;
    then r1 in {g: g < r} by XXREAL_1:229;
    then ex g1 st g1 = r1 & g1 < r;
    then r1 in {g1: rr - 1 < g1 & g1 < r} by A13;
    then
A14: r1 in ].rr - 1, r.[ by RCOMP_1:def 2;
    r1 in dom f by A4,XBOOLE_0:def 4;
    then r1 in ].rr - 1, r.[ /\ dom f by A14,XBOOLE_0:def 4;
    hence f.r1 < f.r2 by A6,A12,A9,RFUNCT_2:43;
  end;
  hence f|left_open_halfline r is increasing by RFUNCT_2:43;
  hence thesis by FCONT_3:16;
end;

theorem Th30:
  left_open_halfline(r) c= dom f & f is_differentiable_on
left_open_halfline(r) & (for x0 st x0 in left_open_halfline(r) holds diff(f,x0)
< 0) implies f|left_open_halfline r is decreasing & f|left_open_halfline(r) is
  one-to-one
proof
  assume
A1: left_open_halfline(r) c= dom f;
  assume that
A2: f is_differentiable_on left_open_halfline(r) and
A3: for x0 st x0 in left_open_halfline(r) holds diff(f,x0) < 0;
  now
    let r1,r2;
    assume that
A4: r1 in left_open_halfline(r) /\ dom f and
A5: r2 in left_open_halfline(r) /\ dom f and
A6: r1 < r2;
    set rr = min(r1,r2);
A7: rr - 1 < r2 - 0 by XREAL_1:17,XXREAL_0:17;
    r2 in left_open_halfline(r) by A5,XBOOLE_0:def 4;
    then r2 in {p: p < r} by XXREAL_1:229;
    then ex g2 st g2 = r2 & g2 < r;
    then r2 in {g2: rr - 1 < g2 & g2 < r} by A7;
    then
A8: r2 in ].rr - 1, r.[ by RCOMP_1:def 2;
    r2 in dom f by A5,XBOOLE_0:def 4;
    then
A9: r2 in ].rr - 1, r.[ /\ dom f by A8,XBOOLE_0:def 4;
A10: f is_differentiable_on ].rr - 1, r.[ by A2,FDIFF_1:34,XXREAL_1:263;
A11: ].rr - 1, r.[ c= left_open_halfline(r) by XXREAL_1:263;
    then for g1 st g1 in ].rr - 1, r.[ holds diff(f,g1) < 0 by A3;
    then
A12: f|].rr-1,r.[ is decreasing by A1,A11,A10,ROLLE:10,XBOOLE_1:1;
A13: rr - 1 < r1 - 0 by XREAL_1:17,XXREAL_0:17;
    r1 in left_open_halfline(r) by A4,XBOOLE_0:def 4;
    then r1 in {g: g < r} by XXREAL_1:229;
    then ex g1 st g1 = r1 & g1 < r;
    then r1 in {g1: rr - 1 < g1 & g1 < r} by A13;
    then
A14: r1 in ].rr - 1, r.[ by RCOMP_1:def 2;
    r1 in dom f by A4,XBOOLE_0:def 4;
    then r1 in ].rr - 1, r.[ /\ dom f by A14,XBOOLE_0:def 4;
    hence f.r2 < f.r1 by A6,A12,A9,RFUNCT_2:44;
  end;
  hence f|left_open_halfline r is decreasing by RFUNCT_2:44;
  hence thesis by FCONT_3:16;
end;

theorem
  left_open_halfline(r) c= dom f & f is_differentiable_on
left_open_halfline(r) & (for x0 st x0 in left_open_halfline(r) holds 0 <= diff(
  f,x0)) implies f|left_open_halfline r is non-decreasing
proof
  assume
A1: left_open_halfline(r) c= dom f;
  assume that
A2: f is_differentiable_on left_open_halfline(r) and
A3: for x0 st x0 in left_open_halfline(r) holds 0 <= diff(f,x0);
  now
    let r1,r2;
    assume that
A4: r1 in left_open_halfline(r) /\ dom f and
A5: r2 in left_open_halfline(r) /\ dom f and
A6: r1 < r2;
    set rr = min(r1,r2);
A7: rr - 1 < r2 - 0 by XREAL_1:17,XXREAL_0:17;
    r2 in left_open_halfline(r) by A5,XBOOLE_0:def 4;
    then r2 in {p: p < r} by XXREAL_1:229;
    then ex g2 st g2 = r2 & g2 < r;
    then r2 in {g2: rr - 1 < g2 & g2 < r} by A7;
    then
A8: r2 in ].rr - 1, r.[ by RCOMP_1:def 2;
    r2 in dom f by A5,XBOOLE_0:def 4;
    then
A9: r2 in ].rr - 1, r.[ /\ dom f by A8,XBOOLE_0:def 4;
A10: f is_differentiable_on ].rr - 1, r.[ by A2,FDIFF_1:34,XXREAL_1:263;
A11: ].rr - 1, r.[ c= left_open_halfline(r) by XXREAL_1:263;
    then for g1 st g1 in ].rr - 1, r.[ holds 0 <= diff(f,g1) by A3;
    then
A12: f|].rr-1,r.[ is non-decreasing by A1,A11,A10,
    ROLLE:11,XBOOLE_1:1;
A13: rr - 1 < r1 - 0 by XREAL_1:17,XXREAL_0:17;
    r1 in left_open_halfline(r) by A4,XBOOLE_0:def 4;
    then r1 in {g: g < r} by XXREAL_1:229;
    then ex g1 st g1 = r1 & g1 < r;
    then r1 in {g1: rr - 1 < g1 & g1 < r} by A13;
    then
A14: r1 in ].rr - 1, r.[ by RCOMP_1:def 2;
    r1 in dom f by A4,XBOOLE_0:def 4;
    then r1 in ].rr - 1, r.[ /\ dom f by A14,XBOOLE_0:def 4;
    hence f.r1 <= f.r2 by A6,A12,A9,RFUNCT_2:45;
  end;
  hence thesis by RFUNCT_2:45;
end;

theorem
  left_open_halfline(r) c= dom f & f is_differentiable_on
left_open_halfline(r) & (for x0 st x0 in left_open_halfline(r) holds diff(f,x0)
  <= 0) implies f|left_open_halfline r is non-increasing
proof
  assume
A1: left_open_halfline(r) c= dom f;
  assume that
A2: f is_differentiable_on left_open_halfline(r) and
A3: for x0 st x0 in left_open_halfline(r) holds diff(f,x0) <= 0;
  now
    let r1,r2;
    assume that
A4: r1 in left_open_halfline(r) /\ dom f and
A5: r2 in left_open_halfline(r) /\ dom f and
A6: r1 < r2;
    set rr = min(r1,r2);
A7: rr - 1 < r2 - 0 by XREAL_1:17,XXREAL_0:17;
    r2 in left_open_halfline(r) by A5,XBOOLE_0:def 4;
    then r2 in {p: p < r} by XXREAL_1:229;
    then ex g2 st g2 = r2 & g2 < r;
    then r2 in {g2: rr - 1 < g2 & g2 < r} by A7;
    then
A8: r2 in ].rr - 1, r.[ by RCOMP_1:def 2;
    r2 in dom f by A5,XBOOLE_0:def 4;
    then
A9: r2 in ].rr - 1, r.[ /\ dom f by A8,XBOOLE_0:def 4;
A10: f is_differentiable_on ].rr - 1, r.[ by A2,FDIFF_1:34,XXREAL_1:263;
A11: ].rr - 1, r.[ c= left_open_halfline(r) by XXREAL_1:263;
    then for g1 st g1 in ].rr - 1, r.[ holds diff(f,g1) <= 0 by A3;
    then
A12: f|].rr-1,r.[ is non-increasing by A1,A11,A10,
    ROLLE:12,XBOOLE_1:1;
A13: rr - 1 < r1 - 0 by XREAL_1:17,XXREAL_0:17;
    r1 in left_open_halfline(r) by A4,XBOOLE_0:def 4;
    then r1 in {g: g < r} by XXREAL_1:229;
    then ex g1 st g1 = r1 & g1 < r;
    then r1 in {g1: rr - 1 < g1 & g1 < r} by A13;
    then
A14: r1 in ].rr - 1, r.[ by RCOMP_1:def 2;
    r1 in dom f by A4,XBOOLE_0:def 4;
    then r1 in ].rr - 1, r.[ /\ dom f by A14,XBOOLE_0:def 4;
    hence f.r2 <= f.r1 by A6,A12,A9,RFUNCT_2:46;
  end;
  hence thesis by RFUNCT_2:46;
end;

theorem Th33:
  right_open_halfline(r) c= dom f & f is_differentiable_on
right_open_halfline(r) & (for x0 st x0 in right_open_halfline(r) holds 0 < diff
(f,x0)) implies f|right_open_halfline r is increasing & f|right_open_halfline(r
  ) is one-to-one
proof
  assume
A1: right_open_halfline(r) c= dom f;
  assume that
A2: f is_differentiable_on right_open_halfline(r) and
A3: for x0 st x0 in right_open_halfline(r) holds 0 < diff(f,x0);
  now
    let r1,r2;
    assume that
A4: r1 in right_open_halfline(r) /\ dom f and
A5: r2 in right_open_halfline(r) /\ dom f and
A6: r1 < r2;
    set rr = max(r1,r2);
A7: r2 + 0 < rr + 1 by XREAL_1:10,XXREAL_0:25;
    r2 in right_open_halfline(r) by A5,XBOOLE_0:def 4;
    then r2 in {p: r < p} by XXREAL_1:230;
    then ex g2 st g2 = r2 & r < g2;
    then r2 in {g2: r < g2 & g2 < rr + 1} by A7;
    then
A8: r2 in ].r, rr + 1.[ by RCOMP_1:def 2;
    r2 in dom f by A5,XBOOLE_0:def 4;
    then
A9: r2 in ].r, rr + 1.[ /\ dom f by A8,XBOOLE_0:def 4;
A10: f is_differentiable_on ].r, rr + 1.[ by A2,FDIFF_1:34,XXREAL_1:247;
A11: ].r, rr + 1.[ c= right_open_halfline(r) by XXREAL_1:247;
    then for g1 st g1 in ].r, rr + 1.[ holds 0 < diff(f,g1) by A3;
    then
A12: f|].r,rr+1.[ is increasing by A1,A11,A10,ROLLE:9,XBOOLE_1:1;
A13: r1 + 0 < rr + 1 by XREAL_1:10,XXREAL_0:25;
    r1 in right_open_halfline(r) by A4,XBOOLE_0:def 4;
    then r1 in {g: r < g} by XXREAL_1:230;
    then ex g1 st g1 = r1 & r < g1;
    then r1 in {g1: r < g1 & g1 < rr + 1} by A13;
    then
A14: r1 in ].r, rr + 1.[ by RCOMP_1:def 2;
    r1 in dom f by A4,XBOOLE_0:def 4;
    then r1 in ].r, rr + 1.[ /\ dom f by A14,XBOOLE_0:def 4;
    hence f.r1 < f.r2 by A6,A12,A9,RFUNCT_2:43;
  end;
  hence f|right_open_halfline r is increasing by RFUNCT_2:43;
  hence thesis by FCONT_3:16;
end;

theorem Th34:
  right_open_halfline(r) c= dom f & f is_differentiable_on
right_open_halfline(r) & (for x0 st x0 in right_open_halfline(r) holds diff(f,
x0) < 0) implies f|right_open_halfline r is decreasing & f|right_open_halfline(
  r) is one-to-one
proof
  assume
A1: right_open_halfline(r) c= dom f;
  assume that
A2: f is_differentiable_on right_open_halfline(r) and
A3: for x0 st x0 in right_open_halfline(r) holds diff(f,x0) < 0;
  now
    let r1,r2;
    assume that
A4: r1 in right_open_halfline(r) /\ dom f and
A5: r2 in right_open_halfline(r) /\ dom f and
A6: r1 < r2;
    set rr = max(r1,r2);
A7: r2 + 0 < rr + 1 by XREAL_1:10,XXREAL_0:25;
    r2 in right_open_halfline(r) by A5,XBOOLE_0:def 4;
    then r2 in {p: r < p} by XXREAL_1:230;
    then ex g2 st g2 = r2 & r < g2;
    then r2 in {g2: r < g2 & g2 < rr + 1} by A7;
    then
A8: r2 in ].r, rr + 1.[ by RCOMP_1:def 2;
    r2 in dom f by A5,XBOOLE_0:def 4;
    then
A9: r2 in ].r, rr + 1.[ /\ dom f by A8,XBOOLE_0:def 4;
A10: f is_differentiable_on ].r, rr + 1.[ by A2,FDIFF_1:34,XXREAL_1:247;
A11: ].r, rr + 1.[ c= right_open_halfline(r) by XXREAL_1:247;
    then for g1 st g1 in ].r, rr + 1.[ holds diff(f,g1) < 0 by A3;
    then
A12: f|].r,rr+1.[ is decreasing by A1,A11,A10,ROLLE:10,XBOOLE_1:1;
A13: r1 + 0 < rr + 1 by XREAL_1:10,XXREAL_0:25;
    r1 in right_open_halfline(r) by A4,XBOOLE_0:def 4;
    then r1 in {g: r < g} by XXREAL_1:230;
    then ex g1 st g1 = r1 & r < g1;
    then r1 in {g1: r < g1 & g1 < rr + 1} by A13;
    then
A14: r1 in ].r, rr + 1.[ by RCOMP_1:def 2;
    r1 in dom f by A4,XBOOLE_0:def 4;
    then r1 in ].r, rr + 1.[ /\ dom f by A14,XBOOLE_0:def 4;
    hence f.r2 < f.r1 by A6,A12,A9,RFUNCT_2:44;
  end;
  hence f|right_open_halfline r is decreasing by RFUNCT_2:44;
  hence thesis by FCONT_3:16;
end;

theorem
  right_open_halfline(r) c= dom f & f is_differentiable_on
  right_open_halfline(r) & (for x0 st x0 in right_open_halfline(r) holds 0 <=
  diff(f,x0)) implies f|right_open_halfline r is non-decreasing
proof
  assume
A1: right_open_halfline(r) c= dom f;
  assume that
A2: f is_differentiable_on right_open_halfline(r) and
A3: for x0 st x0 in right_open_halfline(r) holds 0 <= diff(f,x0);
  now
    let r1,r2;
    assume that
A4: r1 in right_open_halfline(r) /\ dom f and
A5: r2 in right_open_halfline(r) /\ dom f and
A6: r1 < r2;
    set rr = max(r1,r2);
A7: r2 + 0 < rr + 1 by XREAL_1:10,XXREAL_0:25;
    r2 in right_open_halfline(r) by A5,XBOOLE_0:def 4;
    then r2 in {p: r < p} by XXREAL_1:230;
    then ex g2 st g2 = r2 & r < g2;
    then r2 in {g2: r < g2 & g2 < rr + 1} by A7;
    then
A8: r2 in ].r, rr + 1.[ by RCOMP_1:def 2;
    r2 in dom f by A5,XBOOLE_0:def 4;
    then
A9: r2 in ].r, rr + 1.[ /\ dom f by A8,XBOOLE_0:def 4;
A10: f is_differentiable_on ].r, rr + 1.[ by A2,FDIFF_1:34,XXREAL_1:247;
A11: ].r, rr + 1.[ c= right_open_halfline(r) by XXREAL_1:247;
    then for g1 st g1 in ].r, rr + 1.[ holds 0 <= diff(f,g1) by A3;
    then
A12: f|].r,rr+1.[ is non-decreasing by A1,A11,A10,
    ROLLE:11,XBOOLE_1:1;
A13: r1 + 0 < rr + 1 by XREAL_1:10,XXREAL_0:25;
    r1 in right_open_halfline(r) by A4,XBOOLE_0:def 4;
    then r1 in {g: r < g} by XXREAL_1:230;
    then ex g1 st g1 = r1 & r < g1;
    then r1 in {g1: r < g1 & g1 < rr + 1} by A13;
    then
A14: r1 in ].r, rr + 1.[ by RCOMP_1:def 2;
    r1 in dom f by A4,XBOOLE_0:def 4;
    then r1 in ].r, rr + 1.[ /\ dom f by A14,XBOOLE_0:def 4;
    hence f.r1 <= f.r2 by A6,A12,A9,RFUNCT_2:45;
  end;
  hence thesis by RFUNCT_2:45;
end;

theorem
  right_open_halfline(r) c= dom f & f is_differentiable_on
right_open_halfline(r) & (for x0 st x0 in right_open_halfline(r) holds diff(f,
  x0) <= 0) implies f|right_open_halfline r is non-increasing
proof
  assume
A1: right_open_halfline(r) c= dom f;
  assume that
A2: f is_differentiable_on right_open_halfline(r) and
A3: for x0 st x0 in right_open_halfline(r) holds diff(f,x0) <= 0;
  now
    let r1,r2;
    assume that
A4: r1 in right_open_halfline(r) /\ dom f and
A5: r2 in right_open_halfline(r) /\ dom f and
A6: r1 < r2;
    set rr = max(r1,r2);
A7: r2 + 0 < rr + 1 by XREAL_1:10,XXREAL_0:25;
    r2 in right_open_halfline(r) by A5,XBOOLE_0:def 4;
    then r2 in {p: r < p} by XXREAL_1:230;
    then ex g2 st g2 = r2 & r < g2;
    then r2 in {g2: r < g2 & g2 < rr + 1} by A7;
    then
A8: r2 in ].r, rr + 1.[ by RCOMP_1:def 2;
    r2 in dom f by A5,XBOOLE_0:def 4;
    then
A9: r2 in ].r, rr + 1.[ /\ dom f by A8,XBOOLE_0:def 4;
A10: f is_differentiable_on ].r, rr + 1.[ by A2,FDIFF_1:34,XXREAL_1:247;
A11: ].r, rr + 1.[ c= right_open_halfline(r) by XXREAL_1:247;
    then for g1 st g1 in ].r, rr + 1.[ holds diff(f,g1) <= 0 by A3;
    then
A12: f|].r,rr+1.[ is non-increasing by A1,A11,A10,
    ROLLE:12,XBOOLE_1:1;
A13: r1 + 0 < rr + 1 by XREAL_1:10,XXREAL_0:25;
    r1 in right_open_halfline(r) by A4,XBOOLE_0:def 4;
    then r1 in {g: r < g} by XXREAL_1:230;
    then ex g1 st g1 = r1 & r < g1;
    then r1 in {g1: r < g1 & g1 < rr + 1} by A13;
    then
A14: r1 in ].r, rr + 1.[ by RCOMP_1:def 2;
    r1 in dom f by A4,XBOOLE_0:def 4;
    then r1 in ].r, rr + 1.[ /\ dom f by A14,XBOOLE_0:def 4;
    hence f.r2 <= f.r1 by A6,A12,A9,RFUNCT_2:46;
  end;
  hence thesis by RFUNCT_2:46;
end;

theorem Th37:
  [#](REAL) c= dom f & f is_differentiable_on [#](REAL) & (for x0
  holds 0 < diff(f,x0)) implies f|[#]REAL is increasing & f is one-to-one
proof
  assume
A1: [#](REAL) c= dom f;
  assume that
A2: f is_differentiable_on [#](REAL) and
A3: for x0 holds 0 < diff(f,x0);
A4: now
    let r1,r2;
    assume that
A5: r1 in [#](REAL) /\ dom f and
A6: r2 in [#](REAL) /\ dom f and
A7: r1 < r2;
    set rx = max(r1,r2);
    set rn = min(r1,r2);
A8: r2 + 0 < rx + 1 by XREAL_1:10,XXREAL_0:25;
    rn - 1 < r2 - 0 by XREAL_1:17,XXREAL_0:17;
    then r2 in {g2: rn - 1 < g2 & g2 < rx + 1} by A8;
    then
A9: r2 in ].rn - 1, rx + 1.[ by RCOMP_1:def 2;
    r2 in dom f by A6,XBOOLE_0:def 4;
    then
A10: r2 in ].rn - 1, rx + 1.[ /\ dom f by A9,XBOOLE_0:def 4;
A11: for g1 holds g1 in ].rn - 1, rx + 1 .[ implies 0 < diff(f,g1) by A3;
    f is_differentiable_on ].rn - 1, rx + 1.[ by A2,FDIFF_1:34;
    then
A12: f|].rn-1,rx+1.[ is increasing by A1,A11,ROLLE:9,XBOOLE_1:1;
A13: r1 + 0 < rx + 1 by XREAL_1:10,XXREAL_0:25;
    rn - 1 < r1 - 0 by XREAL_1:17,XXREAL_0:17;
    then r1 in {g1: rn - 1 < g1 & g1 < rx + 1} by A13;
    then
A14: r1 in ].rn - 1, rx + 1.[ by RCOMP_1:def 2;
    r1 in dom f by A5,XBOOLE_0:def 4;
    then r1 in ].rn - 1, rx + 1.[ /\ dom f by A14,XBOOLE_0:def 4;
    hence f.r1 < f.r2 by A7,A12,A10,RFUNCT_2:43;
  end;
  hence f|[#]REAL is increasing by RFUNCT_2:43;
  now
    given r1,r2 such that
A15: r1 in dom f and
A16: r2 in dom f and
A17: f.r1 = f.r2 and
A18: r1 <> r2;
A19: r2 in [#](REAL) /\ dom f by A16,XBOOLE_0:def 4;
A20: r1 in [#](REAL) /\ dom f by A15,XBOOLE_0:def 4;
    now
      per cases by A18,XXREAL_0:1;
      suppose
        r1 < r2;
        hence contradiction by A4,A17,A20,A19;
      end;
      suppose
        r2 < r1;
        hence contradiction by A4,A17,A20,A19;
      end;
    end;
    hence contradiction;
  end;
  hence thesis by PARTFUN1:38;
end;

theorem Th38:
  [#](REAL) c= dom f & f is_differentiable_on [#](REAL) & (for x0
  holds diff(f,x0) < 0) implies f|[#]REAL is decreasing & f is one-to-one
proof
  assume
A1: [#](REAL) c= dom f;
  assume that
A2: f is_differentiable_on [#](REAL) and
A3: for x0 holds diff(f,x0) < 0;
A4: now
    let r1,r2;
    assume that
A5: r1 in [#](REAL) /\ dom f and
A6: r2 in [#](REAL) /\ dom f and
A7: r1 < r2;
    set rx = max(r1,r2);
    set rn = min(r1,r2);
A8: r2 + 0 < rx + 1 by XREAL_1:10,XXREAL_0:25;
    rn - 1 < r2 - 0 by XREAL_1:17,XXREAL_0:17;
    then r2 in {g2: rn - 1 < g2 & g2 < rx + 1} by A8;
    then
A9: r2 in ].rn - 1, rx + 1.[ by RCOMP_1:def 2;
    r2 in dom f by A6,XBOOLE_0:def 4;
    then
A10: r2 in ].rn - 1, rx + 1.[ /\ dom f by A9,XBOOLE_0:def 4;
A11: for g1 holds g1 in ].rn - 1, rx + 1 .[ implies diff(f,g1) < 0 by A3;
    f is_differentiable_on ].rn - 1, rx + 1.[ by A2,FDIFF_1:34;
    then
A12: f|].rn-1,rx+1.[ is decreasing by A1,A11,ROLLE:10,XBOOLE_1:1;
A13: r1 + 0 < rx + 1 by XREAL_1:10,XXREAL_0:25;
    rn - 1 < r1 - 0 by XREAL_1:17,XXREAL_0:17;
    then r1 in {g1: rn - 1 < g1 & g1 < rx + 1} by A13;
    then
A14: r1 in ].rn - 1, rx + 1.[ by RCOMP_1:def 2;
    r1 in dom f by A5,XBOOLE_0:def 4;
    then r1 in ].rn - 1, rx + 1.[ /\ dom f by A14,XBOOLE_0:def 4;
    hence f.r2 < f.r1 by A7,A12,A10,RFUNCT_2:44;
  end;
  hence f|[#]REAL is decreasing by RFUNCT_2:44;
  now
    given r1,r2 such that
A15: r1 in dom f and
A16: r2 in dom f and
A17: f.r1 = f.r2 and
A18: r1 <> r2;
A19: r2 in [#](REAL) /\ dom f by A16,XBOOLE_0:def 4;
A20: r1 in [#](REAL) /\ dom f by A15,XBOOLE_0:def 4;
    now
      per cases by A18,XXREAL_0:1;
      suppose
        r1 < r2;
        hence contradiction by A4,A17,A20,A19;
      end;
      suppose
        r2 < r1;
        hence contradiction by A4,A17,A20,A19;
      end;
    end;
    hence contradiction;
  end;
  hence thesis by PARTFUN1:38;
end;

theorem
  [#](REAL) c= dom f & f is_differentiable_on [#](REAL) & (for x0 holds
  0 <= diff(f,x0)) implies f|[#]REAL is non-decreasing
proof
  assume
A1: [#](REAL) c= dom f;
  assume that
A2: f is_differentiable_on [#](REAL) and
A3: for x0 holds 0 <= diff(f,x0);
  now
    let r1,r2;
    assume that
A4: r1 in [#](REAL) /\ dom f and
A5: r2 in [#](REAL) /\ dom f and
A6: r1 < r2;
    set rx = max(r1,r2);
    set rn = min(r1,r2);
A7: r2 + 0 < rx + 1 by XREAL_1:10,XXREAL_0:25;
    rn - 1 < r2 - 0 by XREAL_1:17,XXREAL_0:17;
    then r2 in {g2: rn - 1 < g2 & g2 < rx + 1} by A7;
    then
A8: r2 in ].rn - 1, rx + 1.[ by RCOMP_1:def 2;
    r2 in dom f by A5,XBOOLE_0:def 4;
    then
A9: r2 in ].rn - 1, rx + 1.[ /\ dom f by A8,XBOOLE_0:def 4;
A10: for g1 holds g1 in ].rn - 1, rx + 1 .[ implies 0 <= diff(f,g1) by A3;
    f is_differentiable_on ].rn - 1, rx + 1.[ by A2,FDIFF_1:34;
    then
A11: f|].rn-1,rx+1.[ is non-decreasing by A1,A10,ROLLE:11,XBOOLE_1:1;
A12: r1 + 0 < rx + 1 by XREAL_1:10,XXREAL_0:25;
    rn - 1 < r1 - 0 by XREAL_1:17,XXREAL_0:17;
    then r1 in {g1: rn - 1 < g1 & g1 < rx + 1} by A12;
    then
A13: r1 in ].rn - 1, rx + 1.[ by RCOMP_1:def 2;
    r1 in dom f by A4,XBOOLE_0:def 4;
    then r1 in ].rn - 1, rx + 1.[ /\ dom f by A13,XBOOLE_0:def 4;
    hence f.r1 <= f.r2 by A6,A11,A9,RFUNCT_2:45;
  end;
  hence thesis by RFUNCT_2:45;
end;

theorem
  [#](REAL) c= dom f & f is_differentiable_on [#](REAL) & (for x0 holds
  diff(f,x0) <= 0) implies f|[#]REAL is non-increasing
proof
  assume
A1: [#](REAL) c= dom f;
  assume that
A2: f is_differentiable_on [#](REAL) and
A3: for x0 holds diff(f,x0) <= 0;
  now
    let r1,r2;
    assume that
A4: r1 in [#](REAL) /\ dom f and
A5: r2 in [#](REAL) /\ dom f and
A6: r1 < r2;
    set rx = max(r1,r2);
    set rn = min(r1,r2);
A7: r2 + 0 < rx + 1 by XREAL_1:10,XXREAL_0:25;
    rn - 1 < r2 - 0 by XREAL_1:17,XXREAL_0:17;
    then r2 in {g2: rn - 1 < g2 & g2 < rx + 1} by A7;
    then
A8: r2 in ].rn - 1, rx + 1.[ by RCOMP_1:def 2;
    r2 in dom f by A5,XBOOLE_0:def 4;
    then
A9: r2 in ].rn - 1, rx + 1.[ /\ dom f by A8,XBOOLE_0:def 4;
A10: for g1 holds g1 in ].rn - 1, rx + 1 .[ implies diff(f,g1) <= 0 by A3;
    f is_differentiable_on ].rn - 1, rx + 1.[ by A2,FDIFF_1:34;
    then
A11: f|].rn-1,rx+1.[ is non-increasing by A1,A10,ROLLE:12,XBOOLE_1:1;
A12: r1 + 0 < rx + 1 by XREAL_1:10,XXREAL_0:25;
    rn - 1 < r1 - 0 by XREAL_1:17,XXREAL_0:17;
    then r1 in {g1: rn - 1 < g1 & g1 < rx + 1} by A12;
    then
A13: r1 in ].rn - 1, rx + 1.[ by RCOMP_1:def 2;
    r1 in dom f by A4,XBOOLE_0:def 4;
    then r1 in ].rn - 1, rx + 1.[ /\ dom f by A13,XBOOLE_0:def 4;
    hence f.r2 <= f.r1 by A6,A11,A9,RFUNCT_2:46;
  end;
  hence thesis by RFUNCT_2:46;
end;

theorem Th41:
  ].p,g.[ c= dom f & f is_differentiable_on ].p,g.[ & ((for x0 st
x0 in ].p,g.[ holds 0 < diff(f,x0)) or for x0 st x0 in ].p,g.[ holds diff(f,x0)
  < 0) implies rng (f|].p,g.[) is open
proof
  assume
A1: ].p,g.[ c= dom f;
  assume that
A2: f is_differentiable_on ].p,g.[ and
A3: (for x0 st x0 in ].p,g.[ holds 0 < diff(f,x0)) or for x0 st x0 in ].
  p,g.[ holds diff(f,x0) < 0;
A4: f|].p,g.[ is continuous by A2,FDIFF_1:33;
  now
    per cases by A3;
    suppose
   for x0 st x0 in ].p,g.[ holds 0 < diff(f,x0);
       then f|].p,g.[ is increasing by A1,A2,ROLLE:9;
      hence thesis by A1,A4,FCONT_3:31;
    end;
    suppose
   for x0 st x0 in ].p,g.[ holds diff(f,x0) < 0;
       then f|].p,g.[ is decreasing by A1,A2,ROLLE:10;
      hence thesis by A1,A4,FCONT_3:31;
    end;
  end;
  hence thesis;
end;

theorem Th42:
  left_open_halfline(p) c= dom f & f is_differentiable_on
left_open_halfline(p) & ((for x0 st x0 in left_open_halfline(p) holds 0 < diff(
  f,x0)) or for x0 st x0 in left_open_halfline(p) holds diff(f,x0) < 0) implies
  rng (f|left_open_halfline(p)) is open
proof
  set L = left_open_halfline(p);
  assume
A1: L c= dom f;
  assume that
A2: f is_differentiable_on L and
A3: (for x0 st x0 in L holds 0 < diff(f,x0)) or for x0 st x0 in L holds
  diff(f,x0) < 0;
A4: f|L is continuous by A2,FDIFF_1:33;
  now
    per cases by A3;
    suppose
      for x0 st x0 in left_open_halfline(p) holds 0 < diff(f,x0);
      then f|left_open_halfline p is increasing by A1,A2,Th29;
      hence thesis by A1,A4,FCONT_3:32;
    end;
    suppose
      for x0 st x0 in left_open_halfline(p) holds diff(f,x0) < 0;
      then f|left_open_halfline p is decreasing by A1,A2,Th30;
      hence thesis by A1,A4,FCONT_3:32;
    end;
  end;
  hence thesis;
end;

theorem Th43:
  right_open_halfline(p) c= dom f& f is_differentiable_on
  right_open_halfline(p) & ((for x0 st x0 in right_open_halfline(p) holds 0 <
  diff(f,x0)) or for x0 st x0 in right_open_halfline(p) holds diff(f,x0) < 0)
  implies rng (f|right_open_halfline(p)) is open
proof
  set l = right_open_halfline(p);
  assume
A1: l c= dom f;
  assume that
A2: f is_differentiable_on l and
A3: (for x0 st x0 in l holds 0 < diff(f,x0)) or for x0 st x0 in l holds
  diff(f,x0) < 0;
A4: f|l is continuous by A2,FDIFF_1:33;
  now
    per cases by A3;
    suppose
      for x0 st x0 in right_open_halfline(p) holds 0 < diff(f,x0);
      then f|right_open_halfline p is increasing by A1,A2,Th33;
      hence thesis by A1,A4,FCONT_3:33;
    end;
    suppose
      for x0 st x0 in right_open_halfline(p) holds diff(f,x0) < 0;
      then f|right_open_halfline p is decreasing by A1,A2,Th34;
      hence thesis by A1,A4,FCONT_3:33;
    end;
  end;
  hence thesis;
end;

theorem Th44:
  [#](REAL) c= dom f & f is_differentiable_on [#](REAL) & ((for x0
  holds 0 < diff(f,x0)) or for x0 holds diff(f,x0) < 0) implies rng f is open
proof
  assume
A1: [#](REAL) c= dom f;
  assume that
A2: f is_differentiable_on [#](REAL) and
A3: (for x0 holds 0 < diff(f,x0)) or for x0 holds diff(f,x0) < 0;
A4: f|[#]REAL is continuous by A2,FDIFF_1:33;
  now
    per cases by A3;
    suppose
      for x0 holds 0 < diff(f,x0);
      then f|[#]REAL is increasing by A1,A2,Th37;
      hence thesis by A1,A4,FCONT_3:34;
    end;
    suppose
      for x0 holds diff(f,x0) < 0;
      then f|[#]REAL is decreasing by A1,A2,Th38;
      hence thesis by A1,A4,FCONT_3:34;
    end;
  end;
  hence thesis;
end;

theorem
  for f be one-to-one PartFunc of REAL,REAL st [#]REAL c= dom f & f
  is_differentiable_on [#](REAL ) & ((for x0 holds 0 < diff(f,x0)) or for x0
holds diff(f,x0) < 0) holds f is one-to-one & f" is_differentiable_on dom (f")
  & for x0 st x0 in dom (f") holds diff(f",x0) = 1/diff(f,(f").x0)
proof
  let f be one-to-one PartFunc of REAL,REAL;
  assume that
A1: [#]REAL c= dom f and
A2: f is_differentiable_on [#](REAL) and
A3: (for x0 holds 0 < diff(f,x0)) or for x0 holds diff(f,x0) < 0;
A4: rng f is open by A1,A2,A3,Th44;
  thus f is one-to-one;
A5: dom (f") = rng f by FUNCT_1:55;
A6: rng (f") = dom f by FUNCT_1:55;
A7: for y0 being Real st y0 in dom (f") holds f" is_differentiable_in y0 &
  diff(f",y0) = 1/diff(f,(f").y0)
  proof
    let y0 be Real;
    assume
A8: y0 in dom (f");
    then consider x0 such that
A9: x0 in dom f and
A10: y0 = f.x0 by A5,PARTFUN1:26;
A11: for h,c st rng c = {y0} & rng (h + c) c= dom (f") holds h"(#)((f")/*(
h+c) - (f")/*c) is convergent & lim (h"(#)((f")/*(h+c) - (f")/*c)) = 1/diff(f,(
    f").y0)
    proof
      reconsider a = NAT --> (f").y0 as Real_Sequence by FUNCOP_1:57;
      let h,c such that
A12:  rng c = {y0} and
A13:  rng (h + c) c= dom (f");
A14:  lim (h + c) = y0 by A12,Th4;
      reconsider a as constant Real_Sequence;
      defpred P[Element of NAT,real number] means for r1,r2 be real number st
      r1=(h + c).$1 & r2 = a.$1 holds r1 = f.(r2 + $2);
A15:  for n ex r be Real st P[n,r]
      proof
        let n;
        (h + c).n in rng (h + c) by VALUED_0:28;
        then consider g such that
        g in dom f and
A16:    (h + c).n = f.g by A5,A13,PARTFUN1:26;
        take r = g - x0;
        let r1,r2 be real number;
        assume that
A17:    r1 = (h + c).n and
A18:    r2 = a.n;
        a.n = (f").(f.x0) by A10,FUNCOP_1:13
          .= x0 by A9,FUNCT_1:56;
        hence r1 = f.(r2 + r) by A16,A17,A18;
      end;
      consider b such that
A19:  for n holds P[n,b.n] from FUNCT_2:sch 3(A15);
A20:  now
        let n;
        c.n in rng c by VALUED_0:28;
        hence c.n = f.x0 by A10,A12,TARSKI:def 1;
      end;
A21:  h is non-zero by FDIFF_1:def 1;
      now
        given n such that
A22:    b.n = 0;
A23:    (h + c).n = h.n + c.n by SEQ_1:11
          .= h.n + f.x0 by A20;
        f.(a.n + b.n) = f.((f").(f.x0)) by A10,A22,FUNCOP_1:13
          .= f.x0 by A9,FUNCT_1:56;
        then h.n + f.x0 = f.x0 by A19,A23;
        hence contradiction by A21,SEQ_1:7;
      end;
      then
A24:  b is non-zero by SEQ_1:7;
A25:  [#](REAL) c= dom f by A2,FDIFF_1:def 7;
      then dom f = REAL by XBOOLE_0:def 10;
      then
A26:  f is total by PARTFUN1:def 4;
A27:  y0 in dom(f"|rng f) by A5,A8,RELAT_1:98;
      f|[#]REAL is increasing or f|[#]REAL is decreasing by A1,A2,A3,Th37
      ,Th38;
      then f"|rng f is continuous by A26,FCONT_3:30;
      then (f")|dom(f") is_continuous_in y0 by A5,A27,FCONT_1:def 2;
      then
A28:  f" is_continuous_in y0 by RELAT_1:97;
A29:  now
        let n;
A30:    b.n + a.n in [#] REAL;
        thus (((f")/*(h+c)) - a).n = ((f")/*(h+c)).n - a.n by RFUNCT_2:6
          .= (f").((h + c).n) - a.n by A13,FUNCT_2:185
          .= (f").(f.(b.n + a.n)) - a.n by A19
          .= b.n + a.n - a.n by A25,A30,FUNCT_1:56
          .= b.n;
      end;
A31:  h + c is convergent by A12,Th4;
      then
A32:  (f")/*(h+c) is convergent by A13,A14,A28,FCONT_1:def 1;
      then ((f")/*(h+c)) - a is convergent by SEQ_2:25;
      then
A33:  b is convergent by A29,FUNCT_2:113;
A34:  lim a = a.0 by SEQ_4:41
        .= (f").y0 by FUNCOP_1:13;
      (f").y0 = lim ((f")/*(h+c)) by A13,A31,A14,A28,FCONT_1:def 1;
      then lim (((f")/*(h+c)) - a) = (f").y0 - (f").y0 by A32,
      A34,SEQ_2:26
        .= 0;
      then
A35:  lim b = 0 by A29,FUNCT_2:113;
A36:  rng (b + a) c= dom f
      proof
        let x;
        assume x in rng (b + a);
        then x in REAL;
        hence thesis by A25;
      end;
      reconsider b as convergent_to_0 Real_Sequence by A24,A33,A35, FDIFF_1:
      def 1;
A37:  b" is non-zero by A24,SEQ_1:41;
A38:  rng a = {(f").y0}
      proof
        thus rng a c= {(f").y0}
        proof
          let x;
          assume x in rng a;
          then ex n st x = a.n by FUNCT_2:190;
          then x = (f").y0 by FUNCOP_1:13;
          hence thesis by TARSKI:def 1;
        end;
        let x;
        assume x in {(f").y0};
        then x = (f").y0 by TARSKI:def 1;
        then a.0 = x by FUNCOP_1:13;
        hence thesis by VALUED_0:28;
      end;
A39:  rng a c= dom f
      proof
        let x;
        assume x in rng a;
        then x = (f").y0 by A38,TARSKI:def 1;
        hence thesis by A6,A8,FUNCT_1:def 5;
      end;
      now
        let n;
        c.n in rng c by VALUED_0:28;
        then
A40:    c.n = y0 by A12,TARSKI:def 1;
        thus (f/*a).n = f.(a.n) by A39,FUNCT_2:185
          .= f.((f").y0) by FUNCOP_1:13
          .= c.n by A5,A8,A40,FUNCT_1:57;
      end;
      then
A41:  f/*a = c by FUNCT_2:113;
      now
        let n;
        (h + c).n = f.(a.n + b.n) by A19;
        then h.n + c.n = f.(a.n + b.n) by SEQ_1:11;
        hence h.n = f.(b.n + a.n) - (f/*a).n by A41
          .= f.((b + a).n) - (f/*a).n by SEQ_1:11
          .= (f/*(b+a)).n - (f/*a).n by A36,FUNCT_2:185
          .= (f/*(b+a) - f/*a).n by RFUNCT_2:6;
      end;
      then
A42:  h = f/*(b+a) - f/*a by FUNCT_2:113;
      then f/*(b+a) - f/*a is non-zero by FDIFF_1:def 1;
      then
A43:  b"(#)(f/*(b+a) - f/*a) is non-zero by A37,SEQ_1:43;
A44:  rng c c= dom (f")
      proof
        let x;
        assume x in rng c;
        hence thesis by A8,A12,TARSKI:def 1;
      end;
      now
        let n;
A45:    a.n + b.n in [#] REAL;
        c.n in rng c by VALUED_0:28;
        then
A46:    c.n = y0 by A12,TARSKI:def 1;
        thus (h"(#)((f")/*(h+c) - (f")/*c)).n = (h").n * ((f")/*(h+c) - (f")/*
        c).n by SEQ_1:12
          .= (h").n * (((f")/*(h+c)).n - ((f")/*c).n) by RFUNCT_2:6
          .= (h").n * ((f").((h + c).n) - ((f")/*c).n) by A13,FUNCT_2:185
          .= (h").n * ((f").(f.(a.n + b.n)) - ((f")/*c).n) by A19
          .= (h").n * (a.n + b.n - ((f")/*c).n) by A25,A45,FUNCT_1:56
          .= (h").n * (a.n + b.n - (f").(c.n)) by A44,FUNCT_2:185
          .= (h").n * (a.n + b.n - a.n) by A46,FUNCOP_1:13
          .= (h" (#) (b"")).n by SEQ_1:12
          .= ((b" (#) (f/*(b+a) - f/*a))").n by A42,SEQ_1:44;
      end;
      then
A47:  h"(#)((f")/*(h+c) - (f")/*c) = (b"(#) (f/*(b+a) - f/*a))" by
      FUNCT_2:113;
A48:  f is_differentiable_in (f").y0 by A2,FDIFF_1:16;
      then
A49:  lim (b"(#)(f/*(b+a) - f/*a)) = diff(f,(f").y0) by A38,A36,
      Th12;
      diff(f,(f").y0) = diff(f,(f").y0);
      then
A50:  b"(#)(f/*(b+a) - f/*a) is convergent by A38,A36,A48,Th12;
A51:  0 <> diff(f,(f").y0) by A3;
      hence h"(#)((f")/*(h+c) - (f")/*c) is convergent by A47,A43,A50,A49,
      SEQ_2:35;
      thus lim (h"(#)((f")/*(h+c) - (f")/*c)) = diff(f,(f").y0)" by A47,A43,
      A50,A49,A51,SEQ_2:36
        .= 1/diff(f,(f").y0) by XCMPLX_1:217;
    end;
    ex N being Neighbourhood of y0 st N c= dom (f") by A4,A5,A8,RCOMP_1:
    39;
    hence thesis by A11,Th12;
  end;
  then for y0 being Real holds y0 in dom (f") implies f" is_differentiable_in
  y0;
  hence f" is_differentiable_on dom (f") by A4,A5,FDIFF_1:16;
  let x0;
  assume x0 in dom (f");
  hence thesis by A7;
end;

theorem
  for f be one-to-one PartFunc of REAL,REAL st left_open_halfline(p) c=
  dom f & f is_differentiable_on left_open_halfline(p) & ((for x0 st x0 in
  left_open_halfline(p) holds 0 < diff(f,x0)) or for x0 st x0 in
  left_open_halfline(p) holds diff(f,x0) < 0) holds f|left_open_halfline(p) is
  one-to-one & (f|left_open_halfline(p))" is_differentiable_on dom ((f|
  left_open_halfline(p))") & for x0 st x0 in dom ((f|left_open_halfline(p))")
holds diff((f|left_open_halfline(p))",x0) = 1/diff(f,((f|left_open_halfline(p))
  ").x0)
proof
  let f be one-to-one PartFunc of REAL,REAL;
  set l = left_open_halfline(p);
  assume that
A1: l c= dom f and
A2: f is_differentiable_on l and
A3: (for x0 st x0 in l holds 0 < diff(f,x0)) or for x0 st x0 in l holds
  diff(f,x0) < 0;
A4: rng (f|l) is open by A1,A2,A3,Th42;
  set f1 = f|l;
  thus f1 is one-to-one;
A5: dom (f1") = rng f1 by FUNCT_1:55;
A6: rng (f1") = dom f1 by FUNCT_1:55;
A7: for y0 being Real st y0 in dom (f1") holds f1" is_differentiable_in y0 &
  diff(f1",y0) = 1/diff(f,(f1").y0)
  proof
    let y0 be Real;
    assume
A8: y0 in dom (f1");
    then consider x0 such that
A9: x0 in dom f1 and
A10: y0 = f1.x0 by A5,PARTFUN1:26;
A11: for h,c st rng c = {y0} & rng (h + c) c= dom (f1") holds h"(#)((f1")
/*(h+c) - (f1")/*c) is convergent & lim (h"(#)((f1")/*(h+c) - (f1")/*c)) = 1/
    diff(f,(f1").y0)
    proof
A12:  l c= dom f by A2,FDIFF_1:def 7;
      f|l is increasing or f|l is decreasing by A1,A2,A3,Th29,Th30;
      then f1"|(f.:l) is continuous by A12,FCONT_3:26;
      then
A13:  f1"|rng f1 is continuous by RELAT_1:148;
      y0 in dom(f1"|rng f1) by A5,A8,RELAT_1:98;
      then (f1")|rng f1 is_continuous_in y0 by A13,FCONT_1:def 2;
      then
A14:  f1" is_continuous_in y0 by A5,RELAT_1:97;
      reconsider a = NAT --> (f1").y0 as Real_Sequence by FUNCOP_1:57;
      let h,c such that
A15:  rng c = {y0} and
A16:  rng (h + c) c= dom (f1");
A17:  lim (h + c) = y0 by A15,Th4;
      reconsider a as constant Real_Sequence;
      defpred P[Element of NAT, real number] means for r1,r2 be real number st
r1=(h + c).$1 & r2 = a.$1 holds r1 = f.(r2 + $2) & r2 + $2 in dom f & r2 + $2
      in dom (f|l);
A18:  for n ex r be Real st P[n,r]
      proof
        let n;
        (h + c).n in rng (h + c) by VALUED_0:28;
        then consider g such that
A19:    g in dom f1 and
A20:    (h + c).n = f1.g by A5,A16,PARTFUN1:26;
        take r = g - x0;
        let r1,r2 be real number;
        assume that
A21:    r1 = (h + c).n and
A22:    r2 = a.n;
A23:    a.n = (f1").(f1.x0) by A10,FUNCOP_1:13
          .= x0 by A9,FUNCT_1:56;
        hence r1 = f.(r2 + r) by A19,A20,A21,A22,FUNCT_1:70;
        g in dom f /\ l by A19,RELAT_1:90;
        hence thesis by A19,A23,A22,XBOOLE_0:def 4;
      end;
      consider b such that
A24:  for n holds P[n,b.n] from FUNCT_2:sch 3(A18);
A25:  now
        let n;
A26:    (h + c).n = (h + c).n;
        then
A27:    a.n + b.n in dom f1 by A24;
        thus (((f1")/*(h+c)) - a).n = ((f1")/*(h+c)).n - a.n by RFUNCT_2:6
          .= (f1").((h + c).n) - a.n by A16,FUNCT_2:185
          .= (f1").(f.(a.n + b.n)) - a.n by A24
          .= (f1").(f1.(a.n + b.n)) - a.n by A24,A26,FUNCT_1:70
          .= a.n + b.n - a.n by A27,FUNCT_1:56
          .= b.n;
      end;
A28:  h + c is convergent by A15,Th4;
      then
A29:  (f1")/*(h+c) is convergent by A16,A17,A14,FCONT_1:def 1;
      then ((f1")/*(h+c)) - a is convergent by SEQ_2:25;
      then
A30:  b is convergent by A25,FUNCT_2:113;
A31:  lim a = a.0 by SEQ_4:41
        .= (f1").y0 by FUNCOP_1:13;
      (f1").y0 = lim ((f1")/*(h+c)) by A16,A28,A17,A14,FCONT_1:def 1;
      then lim (((f1")/*(h+c)) - a) = (f1").y0 - (f1").y0 by A29,
      A31,SEQ_2
      :26
        .= 0;
      then
A32:  lim b = 0 by A25,FUNCT_2:113;
A33:  rng (b + a) c= dom f
      proof
        let x;
        assume x in rng (b + a);
        then consider n such that
A34:    x = (b + a).n by FUNCT_2:190;
A35:    (h+c).n = (h+c).n;
        x = a.n + b.n by A34,SEQ_1:11;
        hence thesis by A24,A35;
      end;
      (f1").y0 in dom f1 by A6,A8,FUNCT_1:def 5;
      then (f1").y0 in dom f /\ l by RELAT_1:90;
      then
A36:  (f1").y0 in l by XBOOLE_0:def 4;
      then
A37:  f is_differentiable_in (f1").y0 by A2,FDIFF_1:16;
A38:  now
        let n;
        c.n in rng c by VALUED_0:28;
        hence c.n = f1.x0 by A10,A15,TARSKI:def 1;
      end;
A39:  0 <> diff(f,(f1").y0) by A3,A36;
A40:  h is non-zero by FDIFF_1:def 1;
      now
        given n such that
A41:    b.n = 0;
        a.n = (f1").(f1.x0) by A10,FUNCOP_1:13
          .= x0 by A9,FUNCT_1:56;
        then
A42:    f.(a.n + b.n) = f1.x0 by A9,A41,FUNCT_1:70;
        (h + c).n = h.n + c.n by SEQ_1:11
          .= h.n + f1.x0 by A38;
        then h.n + f1.x0 = f1.x0 by A24,A42;
        hence contradiction by A40,SEQ_1:7;
      end;
      then
A43:  b is non-zero by SEQ_1:7;
      then reconsider b as convergent_to_0 Real_Sequence by A30,A32, FDIFF_1:
      def 1;
A44:  b" is non-zero by A43,SEQ_1:41;
A45:  rng a = {(f1").y0}
      proof
        thus rng a c= {(f1").y0}
        proof
          let x;
          assume x in rng a;
          then ex n st x = a.n by FUNCT_2:190;
          then x = (f1").y0 by FUNCOP_1:13;
          hence thesis by TARSKI:def 1;
        end;
        let x;
        assume x in {(f1").y0};
        then x = (f1").y0 by TARSKI:def 1;
        then a.0 = x by FUNCOP_1:13;
        hence thesis by VALUED_0:28;
      end;
A46:  rng a c= dom f
      proof
        let x;
        assume x in rng a;
        then x = (f1").y0 by A45,TARSKI:def 1;
        then x = x0 by A9,A10,FUNCT_1:56;
        then x in dom f /\ l by A9,RELAT_1:90;
        hence thesis by XBOOLE_0:def 4;
      end;
      now
        let n;
A47:    (f1").y0 in rng (f1") by A8,FUNCT_1:def 5;
        c.n in rng c by VALUED_0:28;
        then
A48:    c.n = y0 by A15,TARSKI:def 1;
        thus (f/*a).n = f.(a.n) by A46,FUNCT_2:185
          .= f.((f1").y0) by FUNCOP_1:13
          .= f1.((f1").y0) by A6,A47,FUNCT_1:70
          .= c.n by A5,A8,A48,FUNCT_1:57;
      end;
      then
A49:  f/*a = c by FUNCT_2:113;
      now
        let n;
        (h + c).n = f.(a.n + b.n) by A24;
        then h.n + c.n = f.(a.n + b.n) by SEQ_1:11;
        hence h.n = f.(b.n + a.n) - (f/*a).n by A49
          .= f.((b + a).n) - (f/*a).n by SEQ_1:11
          .= (f/*(b+a)).n - (f/*a).n by A33,FUNCT_2:185
          .= (f/*(b+a) - f/*a).n by RFUNCT_2:6;
      end;
      then
A50:  h = f/*(b+a) - f/*a by FUNCT_2:113;
      then f/*(b+a) - f/*a is non-zero by FDIFF_1:def 1;
      then
A51:  b"(#)(f/*(b+a) - f/*a) is non-zero by A44,SEQ_1:43;
A52:  rng c c= dom (f1")
      proof
        let x;
        assume x in rng c;
        hence thesis by A8,A15,TARSKI:def 1;
      end;
      now
        let n;
A53:    (h + c).n = (h + c).n;
        then
A54:    a.n + b.n in dom f1 by A24;
        c.n in rng c by VALUED_0:28;
        then
A55:    c.n = y0 by A15,TARSKI:def 1;
        thus (h"(#)((f1")/*(h+c) - (f1")/*c)).n = (h").n * ((f1")/*(h+c) - (f1
        ")/*c).n by SEQ_1:12
          .= (h").n * (((f1")/*(h+c)).n - ((f1")/*c).n) by RFUNCT_2:6
          .= (h").n * ((f1").((h + c).n) - ((f1")/*c).n) by A16,FUNCT_2:185
          .= (h").n * ((f1").(f.(a.n + b.n)) - ((f1")/*c).n) by A24
          .= (h").n * ((f1").(f1.(a.n + b.n)) - ((f1")/*c).n) by A24,A53,
        FUNCT_1:70
          .= (h").n * (a.n + b.n - ((f1")/*c).n) by A54,FUNCT_1:56
          .= (h").n * (a.n + b.n - (f1").(c.n)) by A52,FUNCT_2:185
          .= (h").n * (a.n + b.n - a.n) by A55,FUNCOP_1:13
          .= (h" (#) (b"")).n by SEQ_1:12
          .= ((b" (#) (f/*(b+a) - f/*a))").n by A50,SEQ_1:44;
      end;
      then
A56:  h"(#)((f1")/*(h+c) - (f1")/*c) = (b"(#) (f/*(b+a) - f/*a))" by
      FUNCT_2:113;
      diff(f,(f1").y0) = diff(f,(f1"). y0);
      then
A57:  b"(#)(f/*(b+a) - f/*a) is convergent by A45,A33,A37,Th12;
A58:  lim (b"(#)(f/*(b+a) - f/*a)) = diff(f,(f1").y0) by A45,A33,A37,Th12;
      hence h"(#) ((f1")/*(h+c) - (f1")/*c) is convergent by A56,A51,A57,
      A39,SEQ_2:35;
      thus lim (h"(#)((f1")/*(h+c) - (f1")/*c)) = diff(f,(f1").y0)" by A56,A51
      ,A57,A58,A39,SEQ_2:36
        .= 1/diff(f,(f1").y0) by XCMPLX_1:217;
    end;
    ex N being Neighbourhood of y0 st N c= dom (f1") by A4,A5,A8,RCOMP_1:
    39;
    hence thesis by A11,Th12;
  end;
  then for y0 being Real holds y0 in dom (f1") implies f1"
  is_differentiable_in y0;
  hence f1" is_differentiable_on dom (f1") by A4,A5,FDIFF_1:16;
  let x0;
  assume x0 in dom (f1");
  hence thesis by A7;
end;

theorem
  for f be one-to-one PartFunc of REAL,REAL st right_open_halfline(p) c=
  dom f & f is_differentiable_on right_open_halfline(p) & ((for x0 st x0 in
  right_open_halfline(p) holds 0 < diff(f,x0)) or for x0 st x0 in
right_open_halfline(p) holds diff(f,x0) < 0) holds f|right_open_halfline(p) is
  one-to-one & (f|right_open_halfline(p))" is_differentiable_on dom ((f|
  right_open_halfline(p))") & for x0 st x0 in dom ((f|right_open_halfline(p))")
holds diff((f|right_open_halfline(p))",x0) = 1/diff(f,((f|right_open_halfline(p
  ))").x0)
proof
  let f be one-to-one PartFunc of REAL,REAL;
  set l = right_open_halfline(p);
  assume that
A1: l c= dom f and
A2: f is_differentiable_on l and
A3: (for x0 st x0 in l holds 0 < diff(f,x0)) or for x0 st x0 in l holds
  diff(f,x0) < 0;
A4: rng (f|l) is open by A1,A2,A3,Th43;
  set f1 = f|l;
  thus f1 is one-to-one;
A5: dom (f1") = rng f1 by FUNCT_1:55;
A6: rng (f1") = dom f1 by FUNCT_1:55;
A7: for y0 being Real st y0 in dom (f1") holds f1" is_differentiable_in y0 &
  diff(f1",y0) = 1/diff(f,(f1").y0)
  proof
    let y0 be Real;
    assume
A8: y0 in dom (f1");
    then consider x0 such that
A9: x0 in dom f1 and
A10: y0 = f1.x0 by A5,PARTFUN1:26;
A11: for h,c st rng c = {y0} & rng (h + c) c= dom (f1") holds h"(#)((f1")
/*(h+c) - (f1")/*c) is convergent & lim (h"(#)((f1")/*(h+c) - (f1")/*c)) = 1/
    diff(f,(f1").y0)
    proof
A12:  l c= dom f by A2,FDIFF_1:def 7;
      f|l is increasing or f|l is decreasing by A1,A2,A3,Th33,Th34;
      then f1"|(f.:l) is continuous by A12,FCONT_3:27;
      then
A13:  f1"|rng f1 is continuous by RELAT_1:148;
      y0 in dom((f1")|rng f1) by A5,A8,RELAT_1:98;
      then (f1")|rng f1 is_continuous_in y0 by A13,FCONT_1:def 2;
      then
A14:  f1" is_continuous_in y0 by A5,RELAT_1:97;
      reconsider a = NAT --> (f1").y0 as Real_Sequence by FUNCOP_1:57;
      let h,c such that
A15:  rng c = {y0} and
A16:  rng (h + c) c= dom (f1");
A17:  lim (h + c) = y0 by A15,Th4;
      reconsider a as constant Real_Sequence;
      defpred P[Element of NAT, real number] means for r1,r2 be real number st
r1=(h + c).$1 & r2 = a.$1 holds r1 = f.(r2 + $2) & r2 + $2 in dom f & r2 + $2
      in dom (f|l);
A18:  for n ex r be Real st P[n,r]
      proof
        let n;
        (h + c).n in rng (h + c) by VALUED_0:28;
        then consider g such that
A19:    g in dom f1 and
A20:    (h + c).n = f1.g by A5,A16,PARTFUN1:26;
        take r = g - x0;
        let r1,r2 be real number;
        assume that
A21:    r1 = (h + c).n and
A22:    r2 = a.n;
A23:    a.n = (f1").(f1.x0) by A10,FUNCOP_1:13
          .= x0 by A9,FUNCT_1:56;
        hence r1 = f.(r2 + r) by A19,A20,A21,A22,FUNCT_1:70;
        g in dom f /\ l by A19,RELAT_1:90;
        hence thesis by A19,A23,A22,XBOOLE_0:def 4;
      end;
      consider b such that
A24:  for n holds P[n,b.n] from FUNCT_2:sch 3(A18);
A25:  now
        let n;
A26:    (h + c).n = (h + c).n;
        then
A27:    a.n + b.n in dom f1 by A24;
        thus (((f1")/*(h+c)) - a).n = ((f1")/*(h+c)).n - a.n by RFUNCT_2:6
          .= (f1").((h + c).n) - a.n by A16,FUNCT_2:185
          .= (f1").(f.(a.n + b.n)) - a.n by A24
          .= (f1").(f1.(a.n + b.n)) - a.n by A24,A26,FUNCT_1:70
          .= a.n + b.n - a.n by A27,FUNCT_1:56
          .= b.n;
      end;
A28:  h + c is convergent by A15,Th4;
      then
A29:  (f1")/*(h+c) is convergent by A16,A17,A14,FCONT_1:def 1;
      then ((f1")/*(h+c)) - a is convergent by SEQ_2:25;
      then
A30:  b is convergent by A25,FUNCT_2:113;
A31:  lim a = a.0 by SEQ_4:41
        .= (f1").y0 by FUNCOP_1:13;
      (f1").y0 = lim ((f1")/*(h+c)) by A16,A28,A17,A14,FCONT_1:def 1;
      then lim (((f1")/*(h+c)) - a) = (f1").y0 - (f1").y0 by A29,
      A31,SEQ_2
      :26
        .= 0;
      then
A32:  lim b = 0 by A25,FUNCT_2:113;
A33:  rng (b + a) c= dom f
      proof
        let x;
        assume x in rng (b + a);
        then consider n such that
A34:    x = (b + a).n by FUNCT_2:190;
A35:    (h+c).n = (h+c).n;
        x = a.n + b.n by A34,SEQ_1:11;
        hence thesis by A24,A35;
      end;
      (f1").y0 in dom f1 by A6,A8,FUNCT_1:def 5;
      then (f1").y0 in dom f /\ l by RELAT_1:90;
      then
A36:  (f1").y0 in l by XBOOLE_0:def 4;
      then
A37:  f is_differentiable_in (f1").y0 by A2,FDIFF_1:16;
A38:  now
        let n;
        c.n in rng c by VALUED_0:28;
        hence c.n = f1.x0 by A10,A15,TARSKI:def 1;
      end;
A39:  0 <> diff(f,(f1").y0) by A3,A36;
A40:  h is non-zero by FDIFF_1:def 1;
      now
        given n such that
A41:    b.n = 0;
        a.n = (f1").(f1.x0) by A10,FUNCOP_1:13
          .= x0 by A9,FUNCT_1:56;
        then
A42:    f.(a.n + b.n) = f1.x0 by A9,A41,FUNCT_1:70;
        (h + c).n = h.n + c.n by SEQ_1:11
          .= h.n + f1.x0 by A38;
        then h.n + f1.x0 = f1.x0 by A24,A42;
        hence contradiction by A40,SEQ_1:7;
      end;
      then
A43:  b is non-zero by SEQ_1:7;
      then reconsider b as convergent_to_0 Real_Sequence by A30,A32, FDIFF_1:
      def 1;
A44:  b" is non-zero by A43,SEQ_1:41;
A45:  rng a = {(f1").y0}
      proof
        thus rng a c= {(f1").y0}
        proof
          let x;
          assume x in rng a;
          then ex n st x = a.n by FUNCT_2:190;
          then x = (f1").y0 by FUNCOP_1:13;
          hence thesis by TARSKI:def 1;
        end;
        let x;
        assume x in {(f1").y0};
        then x = (f1").y0 by TARSKI:def 1;
        then a.0 = x by FUNCOP_1:13;
        hence thesis by VALUED_0:28;
      end;
A46:  rng a c= dom f
      proof
        let x;
        assume x in rng a;
        then x = (f1").y0 by A45,TARSKI:def 1;
        then x = x0 by A9,A10,FUNCT_1:56;
        then x in dom f /\ l by A9,RELAT_1:90;
        hence thesis by XBOOLE_0:def 4;
      end;
      now
        let n;
A47:    (f1").y0 in rng (f1") by A8,FUNCT_1:def 5;
        c.n in rng c by VALUED_0:28;
        then
A48:    c.n = y0 by A15,TARSKI:def 1;
        thus (f/*a).n = f.(a.n) by A46,FUNCT_2:185
          .= f.((f1").y0) by FUNCOP_1:13
          .= f1.((f1").y0) by A6,A47,FUNCT_1:70
          .= c.n by A5,A8,A48,FUNCT_1:57;
      end;
      then
A49:  f/*a = c by FUNCT_2:113;
      now
        let n;
        (h + c).n = f.(a.n + b.n) by A24;
        then h.n + c.n = f.(a.n + b.n) by SEQ_1:11;
        hence h.n = f.(b.n + a.n) - (f/*a).n by A49
          .= f.((b + a).n) - (f/*a).n by SEQ_1:11
          .= (f/*(b+a)).n - (f/*a).n by A33,FUNCT_2:185
          .= (f/*(b+a) - f/*a).n by RFUNCT_2:6;
      end;
      then
A50:  h = f/*(b+a) - f/*a by FUNCT_2:113;
      then f/*(b+a) - f/*a is non-zero by FDIFF_1:def 1;
      then
A51:  b"(#)(f/*(b+a) - f/*a) is non-zero by A44,SEQ_1:43;
A52:  rng c c= dom (f1")
      proof
        let x;
        assume x in rng c;
        hence thesis by A8,A15,TARSKI:def 1;
      end;
      now
        let n;
A53:    (h + c).n = (h + c).n;
        then
A54:    a.n + b.n in dom f1 by A24;
        c.n in rng c by VALUED_0:28;
        then
A55:    c.n = y0 by A15,TARSKI:def 1;
        thus (h"(#)((f1")/*(h+c) - (f1")/*c)).n = (h").n * ((f1")/*(h+c) - (f1
        ")/*c).n by SEQ_1:12
          .= (h").n * (((f1")/*(h+c)).n - ((f1")/*c).n) by RFUNCT_2:6
          .= (h").n * ((f1").((h + c).n) - ((f1")/*c).n) by A16,FUNCT_2:185
          .= (h").n * ((f1").(f.(a.n + b.n)) - ((f1")/*c).n) by A24
          .= (h").n * ((f1").(f1.(a.n + b.n)) - ((f1")/*c).n) by A24,A53,
        FUNCT_1:70
          .= (h").n * (a.n + b.n - ((f1")/*c).n) by A54,FUNCT_1:56
          .= (h").n * (a.n + b.n - (f1").(c.n)) by A52,FUNCT_2:185
          .= (h").n * (a.n + b.n - a.n) by A55,FUNCOP_1:13
          .= (h" (#) (b"")).n by SEQ_1:12
          .= ((b" (#) (f/*(b+a) - f/*a))").n by A50,SEQ_1:44;
      end;
      then
A56:  h"(#)((f1")/*(h+c) - (f1")/*c) = (b"(#) (f/*(b+a) - f/*a))" by
      FUNCT_2:113;
      diff(f,(f1").y0) = diff(f,(f1"). y0);
      then
A57:  b"(#)(f/*(b+a) - f/*a) is convergent by A45,A33,A37,Th12;
A58:  lim (b"(#)(f/*(b+a) - f/*a)) = diff(f,(f1").y0) by A45,A33,A37,Th12;
      hence h"(#) ((f1")/*(h+c) - (f1")/*c) is convergent by A56,A51,A57,
      A39,SEQ_2:35;
      thus lim (h"(#)((f1")/*(h+c) - (f1")/*c)) = diff(f,(f1").y0)" by A56,A51
      ,A57,A58,A39,SEQ_2:36
        .= 1/diff(f,(f1").y0) by XCMPLX_1:217;
    end;
    ex N being Neighbourhood of y0 st N c= dom (f1") by A4,A5,A8,RCOMP_1:
    39;
    hence thesis by A11,Th12;
  end;
  then for y0 being Real holds y0 in dom (f1") implies f1"
  is_differentiable_in y0;
  hence f1" is_differentiable_on dom (f1") by A4,A5,FDIFF_1:16;
  let x0;
  assume x0 in dom (f1");
  hence thesis by A7;
end;

theorem
  for f be one-to-one PartFunc of REAL,REAL st ].p,g.[ c= dom f & f
is_differentiable_on ].p,g.[ & ((for x0 st x0 in ].p,g.[ holds 0 < diff(f,x0))
or for x0 st x0 in ].p,g.[ holds diff(f,x0) < 0) holds f|].p,g.[ is one-to-one
& (f|].p,g.[)" is_differentiable_on dom ((f|].p,g.[)") & for x0 st x0 in dom ((
  f|].p,g.[)") holds diff((f|].p,g.[)",x0) = 1/diff(f,((f|].p,g.[)").x0)
proof
  let f be one-to-one PartFunc of REAL,REAL;
  set l = ].p,g.[;
  assume that
A1: l c= dom f and
A2: f is_differentiable_on l and
A3: (for x0 st x0 in l holds 0 < diff(f,x0)) or for x0 st x0 in l holds
  diff(f,x0) < 0;
A4: rng (f|l) is open by A1,A2,A3,Th41;
  set f1 = f|l;
  thus f1 is one-to-one;
A5: dom (f1") = rng f1 by FUNCT_1:55;
A6: rng (f1") = dom f1 by FUNCT_1:55;
A7: for y0 being Real st y0 in dom (f1") holds f1" is_differentiable_in y0 &
  diff(f1",y0) = 1/diff(f,(f1").y0)
  proof
    let y0 be Real;
    assume
A8: y0 in dom (f1");
    then consider x0 such that
A9: x0 in dom f1 and
A10: y0 = f1.x0 by A5,PARTFUN1:26;
A11: for h,c st rng c = {y0} & rng (h + c) c= dom (f1") holds h"(#)((f1")
/*(h+c) - (f1")/*c) is convergent & lim (h"(#)((f1")/*(h+c) - (f1")/*c)) = 1/
    diff(f,(f1").y0)
    proof
A12:  f|l is increasing or f|l is decreasing
      proof
          per cases by A3;
          suppose
        for x0 st x0 in l holds 0 < diff(f,x0);
            hence thesis by A1,A2,ROLLE:9;
          end;
          suppose
        for x0 st x0 in l holds diff(f,x0) < 0;
            hence thesis by A1,A2,ROLLE:10;
          end;
      end;
      l c= dom f by A2,FDIFF_1:def 7;
      then f1"|(f.:l) is continuous by A12,FCONT_3:25;
      then
A15:  f1"|rng f1 is continuous by RELAT_1:148;
      y0 in dom(f1"|rng f1) by A5,A8,RELAT_1:98;
      then (f1")|rng f1 is_continuous_in y0 by A15,FCONT_1:def 2;
      then
A16:  f1" is_continuous_in y0 by A5,RELAT_1:97;
      reconsider a = NAT --> (f1").y0 as Real_Sequence by FUNCOP_1:57;
      let h,c such that
A17:  rng c = {y0} and
A18:  rng (h + c) c= dom (f1");
A19:  lim (h + c) = y0 by A17,Th4;
      reconsider a as constant Real_Sequence;
      defpred P[Element of NAT,real number] means for r1,r2 be real number st
r1=(h + c).$1 & r2 = a.$1 holds r1 = f.(r2 + $2) & r2 + $2 in dom f & r2 + $2
      in dom (f|l);
A20:  for n ex r be Real st P[n,r]
      proof
        let n;
        (h + c).n in rng (h + c) by VALUED_0:28;
        then consider g such that
A21:    g in dom f1 and
A22:    (h + c).n = f1.g by A5,A18,PARTFUN1:26;
        take r = g - x0;
        let r1,r2 be real number;
        assume that
A23:    r1 = (h + c).n and
A24:    r2 = a.n;
A25:    a.n = (f1").(f1.x0) by A10,FUNCOP_1:13
          .= x0 by A9,FUNCT_1:56;
        hence r1 = f.(r2 + r) by A21,A22,A23,A24,FUNCT_1:70;
        g in dom f /\ l by A21,RELAT_1:90;
        hence thesis by A21,A25,A24,XBOOLE_0:def 4;
      end;
      consider b such that
A26:  for n holds P[n,b.n] from FUNCT_2:sch 3(A20);
A27:  now
        let n;
A28:    (h + c).n = (h + c).n;
        then
A29:    a.n + b.n in dom f1 by A26;
        thus (((f1")/*(h+c)) - a).n = ((f1")/*(h+c)).n - a.n by RFUNCT_2:6
          .= (f1").((h + c).n) - a.n by A18,FUNCT_2:185
          .= (f1").(f.(a.n + b.n)) - a.n by A26
          .= (f1").(f1.(a.n + b.n)) - a.n by A26,A28,FUNCT_1:70
          .= a.n + b.n - a.n by A29,FUNCT_1:56
          .= b.n;
      end;
A30:  h + c is convergent by A17,Th4;
      then
A31:  (f1")/*(h+c) is convergent by A18,A19,A16,FCONT_1:def 1;
      then ((f1")/*(h+c)) - a is convergent by SEQ_2:25;
      then
A32:  b is convergent by A27,FUNCT_2:113;
A33:  lim a = a.0 by SEQ_4:41
        .= (f1").y0 by FUNCOP_1:13;
      (f1").y0 = lim ((f1")/*(h+c)) by A18,A30,A19,A16,FCONT_1:def 1;
      then lim (((f1")/*(h+c)) - a) = (f1").y0 - (f1").y0 by A31,
      A33,SEQ_2
      :26
        .= 0;
      then
A34:  lim b = 0 by A27,FUNCT_2:113;
A35:  rng (b + a) c= dom f
      proof
        let x;
        assume x in rng (b + a);
        then consider n such that
A36:    x = (b + a).n by FUNCT_2:190;
A37:    (h+c).n = (h+c).n;
        x = a.n + b.n by A36,SEQ_1:11;
        hence thesis by A26,A37;
      end;
      (f1").y0 in dom f1 by A6,A8,FUNCT_1:def 5;
      then (f1").y0 in dom f /\ l by RELAT_1:90;
      then
A38:  (f1").y0 in l by XBOOLE_0:def 4;
      then
A39:  f is_differentiable_in (f1").y0 by A2,FDIFF_1:16;
A40:  now
        let n;
        c.n in rng c by VALUED_0:28;
        hence c.n = f1.x0 by A10,A17,TARSKI:def 1;
      end;
A41:  0 <> diff(f,(f1").y0) by A3,A38;
A42:  h is non-zero by FDIFF_1:def 1;
      now
        given n such that
A43:    b.n = 0;
        a.n = (f1").(f1.x0) by A10,FUNCOP_1:13
          .= x0 by A9,FUNCT_1:56;
        then
A44:    f.(a.n + b.n) = f1.x0 by A9,A43,FUNCT_1:70;
        (h + c).n = h.n + c.n by SEQ_1:11
          .= h.n + f1.x0 by A40;
        then h.n + f1.x0 = f1.x0 by A26,A44;
        hence contradiction by A42,SEQ_1:7;
      end;
      then
A45:  b is non-zero by SEQ_1:7;
      then reconsider b as convergent_to_0 Real_Sequence by A32,A34, FDIFF_1:
      def 1;
A46:  b" is non-zero by A45,SEQ_1:41;
A47:  rng a = {(f1").y0}
      proof
        thus rng a c= {(f1").y0}
        proof
          let x;
          assume x in rng a;
          then ex n st x = a.n by FUNCT_2:190;
          then x = (f1").y0 by FUNCOP_1:13;
          hence thesis by TARSKI:def 1;
        end;
        let x;
        assume x in {(f1").y0};
        then x = (f1").y0 by TARSKI:def 1;
        then a.0 = x by FUNCOP_1:13;
        hence thesis by VALUED_0:28;
      end;
A48:  rng a c= dom f
      proof
        let x;
        assume x in rng a;
        then x = (f1").y0 by A47,TARSKI:def 1;
        then x = x0 by A9,A10,FUNCT_1:56;
        then x in dom f /\ l by A9,RELAT_1:90;
        hence thesis by XBOOLE_0:def 4;
      end;
      now
        let n;
A49:    (f1").y0 in rng (f1") by A8,FUNCT_1:def 5;
        c.n in rng c by VALUED_0:28;
        then
A50:    c.n = y0 by A17,TARSKI:def 1;
        thus (f/*a).n = f.(a.n) by A48,FUNCT_2:185
          .= f.((f1").y0) by FUNCOP_1:13
          .= f1.((f1").y0) by A6,A49,FUNCT_1:70
          .= c.n by A5,A8,A50,FUNCT_1:57;
      end;
      then
A51:  f/*a = c by FUNCT_2:113;
      now
        let n;
        (h + c).n = f.(a.n + b.n) by A26;
        then h.n + c.n = f.(a.n + b.n) by SEQ_1:11;
        hence h.n = f.(b.n + a.n) - (f/*a).n by A51
          .= f.((b + a).n) - (f/*a).n by SEQ_1:11
          .= (f/*(b+a)).n - (f/*a).n by A35,FUNCT_2:185
          .= (f/*(b+a) - f/*a).n by RFUNCT_2:6;
      end;
      then
A52:  h = f/*(b+a) - f/*a by FUNCT_2:113;
      then f/*(b+a) - f/*a is non-zero by FDIFF_1:def 1;
      then
A53:  b"(#)(f/*(b+a) - f/*a) is non-zero by A46,SEQ_1:43;
A54:  rng c c= dom (f1")
      proof
        let x;
        assume x in rng c;
        hence thesis by A8,A17,TARSKI:def 1;
      end;
      now
        let n;
A55:    (h + c).n = (h + c).n;
        then
A56:    a.n + b.n in dom f1 by A26;
        c.n in rng c by VALUED_0:28;
        then
A57:    c.n = y0 by A17,TARSKI:def 1;
        thus (h"(#)((f1")/*(h+c) - (f1")/*c)).n = (h").n * ((f1")/*(h+c) - (f1
        ")/*c).n by SEQ_1:12
          .= (h").n * (((f1")/*(h+c)).n - ((f1")/*c).n) by RFUNCT_2:6
          .= (h").n * ((f1").((h + c).n) - ((f1")/*c).n) by A18,FUNCT_2:185
          .= (h").n * ((f1").(f.(a.n + b.n)) - ((f1")/*c).n) by A26
          .= (h").n * ((f1").(f1.(a.n + b.n)) - ((f1")/*c).n) by A26,A55,
        FUNCT_1:70
          .= (h").n * (a.n + b.n - ((f1")/*c).n) by A56,FUNCT_1:56
          .= (h").n * (a.n + b.n - (f1").(c.n)) by A54,FUNCT_2:185
          .= (h").n * (a.n + b.n - a.n) by A57,FUNCOP_1:13
          .= (h" (#) (b"")).n by SEQ_1:12
          .= ((b" (#) (f/*(b+a) - f/*a))").n by A52,SEQ_1:44;
      end;
      then
A58:  h
"(#)((f1")/*(h+c) - (f1")/*c) = (b"(#) (f/*(b+a) - f/*a))" by FUNCT_2:113;
      diff(f,(f1").y0) = diff(f,(f1"). y0);
      then
A59:  b"(#)(f/*(b+a) - f/*a) is convergent by A47,A35,A39,Th12;
A60:  lim (b"(#)(f/*(b+a) - f/*a)) = diff(f,(f1").y0) by A47,A35,A39,
      Th12;
      hence h"(#) ((f1")/*(h+c) - (f1")/*c) is convergent by A58,A53,A59,
      A41, SEQ_2:35;
      thus lim (h"(#)((f1")/*(h+c) - (f1")/*c)) = diff(f,(f1").y0)" by A58,
      A53,A59,A60,A41,SEQ_2:36
        .= 1/diff(f,(f1").y0) by XCMPLX_1:217;
    end;
    ex N being Neighbourhood of y0 st N c= dom (f1") by A4,A5,A8,RCOMP_1:
    39;
    hence thesis by A11,Th12;
  end;
  then for y0 being Real holds y0 in dom (f1") implies f1"
  is_differentiable_in y0;
  hence f1" is_differentiable_on dom (f1") by A4,A5,FDIFF_1:16;
  let x0;
  assume x0 in dom (f1");
  hence thesis by A7;
end;

theorem
  f is_differentiable_in x0 implies for h,c st rng c = {x0} & rng (h + c
  ) c= dom f & rng (-h + c) c= dom f holds (2(#)h)"(#)(f/*(c+h) - f/*(c-h)) is
  convergent & lim((2(#)h)"(#)(f/*(c+h) - f/*(c-h))) = diff(f,x0)
proof
  assume
A1: f is_differentiable_in x0;
  let h,c such that
A2: rng c = {x0} and
A3: rng (h + c) c= dom f and
A4: rng (-h + c) c= dom f;
  set fm = (-h)"(#)(f/*(-h + c) - f/*c);
A5: lim fm = diff(f,x0) by A1,A2,A4,Th12;
  set fp = h"(#)(f/*(h+c) - f/*c);
A6: diff(f,x0) = diff(f,x0);
  then
A7: fm is convergent by A1,A2,A4,Th12;
A8: fp is convergent by A1,A2,A3,A6,Th12;
  then
A9: fp + fm is convergent by A7,SEQ_2:19;
A10: now
    let n;
    thus (f/*(c+h) - f/*(c-h) + (f/*c - f/*c)).n = (f/*(c+h) - f/*(c-h)).n + (
    f/*c - f/*c).n by SEQ_1:11
      .= (f/*(c+h) - f/*(c-h)).n + ((f/*c).n - (f/*c).n) by RFUNCT_2:6
      .= (f/*(c+h) - f/*(c-h)).n;
  end;
A11: 2"(#)(fp + fm) = 2"(#)(h"(#)(f/*(c+h) - f/*c) + (-1)(#)h"(#)(f/*(c + -h
  ) - f/*c)) by SEQ_1:55
    .= 2"(#)(h"(#)(f/*(c+h) - f/*c) + (-1)(#)(h"(#) (f/*(c + -h) - f/*c)))
  by SEQ_1:26
    .= 2"(#)(h"(#)(f/*(c+h) - f/*c) + h"(#)((-1)(#) (f/*(c + -h) - f/*c)))
  by SEQ_1:27
    .= 2"(#)(h"(#)(f/*(c+h) - f/*c + (-1)(#)(f/*(c + -h) - f/*c))) by SEQ_1:
  24
    .= 2"(#)h"(#)(f/*(c+h) - f/*c + (-1)(#)(f/*(c + -h) - f/*c)) by SEQ_1:26
    .= (2(#)h)"(#)(f/*(c+h) - f/*c + (-1)(#)(f/*(c + -h) - f/*c)) by SEQ_1:
  54
    .= (2(#)h)"(#)(f/*(c+h) - (f/*c - -(f/*(c + -h) - f/*c))) by SEQ_1:38
    .= (2(#)h)"(#)(f/*(c+h) - (f/*(c + -h) - (f/*c - f/*c))) by SEQ_1:38
    .= (2(#)h)"(#)(f/*(c+h) - f/*(c-h) + (f/*c - f/*c)) by SEQ_1:38;
  lim fp = diff(f,x0) by A1,A2,A3,Th12;
  then lim (fp + fm) = 1*diff(f,x0) + diff(f,x0) by A8,A7,A5,SEQ_2:20
    .= 2*diff(f,x0);
  then
A12: lim (2"(#)(fp + fm)) = 2"*(2*diff(f,x0)) by A9,SEQ_2:22
    .= diff(f,x0);
  2" (#) (fp + fm) is convergent by A9,SEQ_2:21;
  hence thesis by A12,A11,A10,FUNCT_2:113;
end;
