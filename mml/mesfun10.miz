:: Fatou's Lemma and the {L}ebesgue's Convergence Theorem
::  by Noboru Endou , Keiko Narita and Yasunari Shidama
::
:: Received July 22, 2008
:: Copyright (c) 2008 Association of Mizar Users
 
environ

 vocabularies NUMBERS, XBOOLE_0, MESFUNC8, SEQFUNC, MESFUNC5, SUBSET_1,
      SUPINF_1, NAT_1, FUNCT_1, XXREAL_0, ORDINAL2, RELAT_1, ORDINAL1,
      XXREAL_2, RINFSUP1, ARYTM_3, PBOOLE, PARTFUN1, PROB_1, MEASURE1, CARD_1,
      SUPINF_2, MESFUNC1, VALUED_0, SEQ_2, ARYTM_1, TARSKI, REAL_1, INTEGRA5,
      VALUED_1, COMPLEX1, MEASURE6, RFUNCT_3, XREAL_0, MESFUNC2, MESFUN10;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0,
      REAL_1, XXREAL_0, XXREAL_2, RELAT_1, FUNCT_1, RELSET_1, DOMAIN_1, NAT_1,
      VALUED_0, FUNCT_2, PARTFUN1, PROB_1, SUPINF_1, SUPINF_2, MEASURE1,
      EXTREAL1, MESFUNC1, MEASURE6, MESFUNC2, SEQFUNC, MESFUNC5, MESFUNC8,
      RINFSUP2;
 constructors REAL_1, DOMAIN_1, EXTREAL1, MESFUNC1, MEASURE6, MESFUNC2,
      MESFUNC5, RINFSUP2, EXTREAL2, MESFUNC9, SUPINF_1, MESFUNC8, SEQFUNC,
      RELSET_1, BINOP_2;
 registrations NAT_1, XREAL_0, MEMBERED, ORDINAL1, PARTFUN1, RELAT_1, XBOOLE_0,
      SUPINF_2, NUMBERS, XXREAL_0, MESFUNC8, RINFSUP2, VALUED_0;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 definitions MESFUNC1, MEASURE6, RINFSUP2, XXREAL_3;
 theorems TARSKI, PARTFUN1, FUNCT_1, SUPINF_2, EXTREAL1, EXTREAL2, MESFUNC1,
      XREAL_0, XBOOLE_0, XBOOLE_1, MESFUNC2, XREAL_1, XXREAL_0, MESFUNC5,
      NAT_1, RELAT_1, FUNCT_2, SEQFUNC, ORDINAL1, MESFUNC8, RINFSUP2, MESFUNC7,
      MESFUNC9, NUMBERS, XXREAL_2, XXREAL_3;
 schemes FUNCT_2, SEQFUNC, FUNCT_1;

begin :: Fatou's Lemma

reserve X for non empty set,
  F for with_the_same_dom Functional_Sequence of X, ExtREAL,
  seq,seq1,seq2 for ExtREAL_sequence,
  x for Element of X,
  a,r for R_eal,
  n,m,k for Nat;

theorem Th1:
  (for n be Nat holds seq1.n <= seq2.n) implies inf rng seq1 <= inf rng seq2
proof
  assume
A1: for n be Nat holds seq1.n <= seq2.n;
  now
    let x be ext-real number;
A2: now
      let n be Element of NAT;
      dom seq1 = NAT by FUNCT_2:def 1;
      then
A3:   seq1.n in rng seq1 by FUNCT_1:def 5;
A4:   seq1.n <= seq2.n by A1;
      inf rng seq1 is LowerBound of rng seq1 by XXREAL_2:def 4;
      then inf rng seq1 <= seq1.n by A3,XXREAL_2:def 2;
      hence inf rng seq1 <= seq2.n by A4,XXREAL_0:2;
    end;
    assume x in rng seq2;
    then ex z be set st z in dom seq2 & x=seq2.z by FUNCT_1:def 5;
    hence inf rng seq1 <= x by A2;
  end;
  then inf rng seq1 is LowerBound of rng seq2 by XXREAL_2:def 2;
  hence thesis by XXREAL_2:def 4;
end;

theorem Th2:
  (for n be Nat holds seq1.n <= seq2.n) implies (
  inferior_realsequence seq1).k <= (inferior_realsequence seq2).k & (
  superior_realsequence seq1).k <= (superior_realsequence seq2).k
proof
  reconsider k1 = k as Element of NAT by ORDINAL1:def 13;
  assume
A1: for n be Nat holds seq1.n <= seq2.n;
A2: now
    let n be Nat;
A3: (seq2^\k1).n = seq2.(n+k) by NAT_1:def 3;
    (seq1^\k1).n = seq1.(n+k) by NAT_1:def 3;
    hence (seq1^\k1).n <= (seq2^\k1).n by A1,A3;
  end;
  then sup(seq1^\k1) <= sup(seq2^\k1) by MESFUNC5:61;
  then
A4: (superior_realsequence seq1).k <= sup(seq2^\k1) by RINFSUP2:27;
  inf(seq1^\k1) <= inf(seq2^\k1) by A2,Th1;
  then (inferior_realsequence seq1).k <= inf(seq2^\k1) by RINFSUP2:27;
  hence thesis by A4,RINFSUP2:27;
end;

theorem Th3:
  (for n be Nat holds seq1.n <= seq2.n) implies lim_inf seq1 <=
  lim_inf seq2 & lim_sup seq1 <= lim_sup seq2
proof
  assume for n be Nat holds seq1.n <= seq2.n;
  then for n be Nat holds (inferior_realsequence seq1).n <= (
  inferior_realsequence seq2).n & (superior_realsequence seq1).n <= (
  superior_realsequence seq2).n by Th2;
  hence thesis by Th1,MESFUNC5:61;
end;

theorem Th4:
  (for n be Nat holds seq.n >= a) implies inf seq >= a
proof
  assume
A1: for n be Nat holds seq.n >= a;
  now
    let x be ext-real number;
    assume x in rng seq;
    then ex z be set st z in dom seq & x = seq.z by FUNCT_1:def 5;
    hence x >= a by A1;
  end;
  then a is LowerBound of rng seq by XXREAL_2:def 2;
  hence thesis by XXREAL_2:def 4;
end;

theorem Th5:
  (for n be Nat holds seq.n <= a) implies sup seq <= a
proof
  assume
A1: for n be Nat holds seq.n <= a;
  now
    let x be ext-real number;
    assume x in rng seq;
    then ex z be set st z in dom seq & x = seq.z by FUNCT_1:def 5;
    hence x <= a by A1;
  end;
  then a is UpperBound of rng seq by XXREAL_2:def 1;
  hence thesis by XXREAL_2:def 3;
end;

theorem Th6:
  for n be Element of NAT st x in dom inf(F^\n) holds (inf(F^\n)).x
  =inf((F#x)^\n)
proof
  let n be Element of NAT;
  now
    reconsider g=F as Function of NAT,PFuncs(X,ExtREAL);
    let k be Element of NAT;
    ((F^\n)#x).k =((F^\n).k).x by MESFUNC5:def 13;
    then ((F^\n)#x).k = (g.(n+k)).x by NAT_1:def 3;
    then ((F^\n)#x).k =(F#x).(n+k) by MESFUNC5:def 13;
    hence ((F^\n)#x).k = ((F#x)^\n).k by NAT_1:def 3;
  end;
  then
A1: (F^\n)#x = (F#x)^\n by FUNCT_2:113;
  assume x in dom inf(F^\n);
  hence thesis by A1,MESFUNC8:def 3;
end;

reserve S for SigmaField of X,
  M for sigma_Measure of S,
  E for Element of S;
 
:: Fatou's Lemma

theorem Th7:
  E = dom(F.0) & (for n holds F.n is nonnegative & F.n
  is_measurable_on E) implies ex I be ExtREAL_sequence st (for n holds I.n =
  Integral(M,F.n)) & Integral(M,lim_inf F) <= lim_inf I
proof
  assume that
A1: E = dom(F.0) and
A2: for n holds F.n is nonnegative & F.n is_measurable_on E;
  set H = inferior_realsequence F;
  deffunc G(Element of NAT) = inf(F^\$1);
  consider G be Function such that
A3: dom G = NAT & for n be Element of NAT holds G.n = G(n) from FUNCT_1:
  sch 4;
  now
    let n be Element of NAT;
    G.n = inf(F^\n) by A3;
    hence G.n is PartFunc of X,ExtREAL;
  end;
  then reconsider G as Functional_Sequence of X,ExtREAL by A3,SEQFUNC:1;
A4: for n be Element of NAT holds G.n = (inferior_realsequence F).n
  proof
    let n be Element of NAT;
    (inferior_realsequence F).n = inf(F^\n) by MESFUNC8:8;
    hence thesis by A3;
  end;
  then
A5: G = inferior_realsequence F by FUNCT_2:113;
  reconsider G as with_the_same_dom Functional_Sequence of X,ExtREAL by A4,
  FUNCT_2:113;
A6: dom(H.0) = dom(G.0) by A4;
A7: for n,m be Nat, x be Element of X st n <= m & x in E holds (G.n).x <= (G
  .m).x & (G#x).n <= (G#x).m
  proof
    let n,m be Nat, x be Element of X;
    reconsider n1=n, m1=m as Element of NAT by ORDINAL1:def 13;
    assume that
A8: n <= m and
A9: x in E;
    H#x = inferior_realsequence(F#x) by A1,A9,MESFUNC8:7;
    then (H#x).n1 <= (H#x).m1 by A8,RINFSUP2:7;
    then (H.n).x <= (H#x).m by MESFUNC5:def 13;
    hence (G.n).x <= (G.m).x by A5,MESFUNC5:def 13;
    then (G#x).n <= (G.m).x by MESFUNC5:def 13;
    hence thesis by MESFUNC5:def 13;
  end;
A10: now
    let x be Element of X;
    assume x in E;
    then
    for n,m be Element of NAT st m <= n holds (G#x).m <= (G#x).n by A7;
    then G#x is non-decreasing by RINFSUP2:7;
    hence G#x is convergent by RINFSUP2:37;
  end;
  deffunc I(Element of NAT) = Integral(M,F.$1);
  consider I be Function of NAT,ExtREAL such that
A11: for n be Element of NAT holds I.n = I(n) from FUNCT_2:sch 4;
A12: for n be Nat holds G.n is_measurable_on E by A1,A2,A5,MESFUNC8:20;
A13: dom(H.0) = dom(F.0) by MESFUNC8:def 5;
  then
A14: dom lim G = E by A1,A6,MESFUNC8:def 10;
  for x be set st x in dom(G.0) holds 0 <= (G.0).x
  proof
    let x be set;
    assume
A15: x in dom(G.0);
    then reconsider x as Element of X;
A16: now
      let n be Nat;
      F.n is nonnegative by A2;
      then 0 <= (F.n).x by SUPINF_2:70;
      then 0 <= (F#x).(0+n) by MESFUNC5:def 13;
      hence 0. <= ((F#x)^\0).n by NAT_1:def 3;
    end;
    (F^\0).0 = F.(0+0) by NAT_1:def 3;
    then dom inf(F^\0) = dom(F.0) by MESFUNC8:def 3;
    then (inf(F^\0)).x =inf((F#x)^\0) by A13,A6,A15,Th6;
    then (G.0).x = inf((F#x)^\0) by A3;
    hence thesis by A16,Th4;
  end;
  then
A17: G.0 is nonnegative by SUPINF_2:71;
  for n,m be Nat st n <= m holds for x be Element of X st x in E holds (G.
  n).x <= (G.m).x by A7;
  then consider J be ExtREAL_sequence such that
A18: for n be Nat holds J.n = Integral(M,G.n) and
A19: J is convergent and
A20: Integral(M,lim G) = lim J by A1,A13,A6,A17,A12,A10,MESFUNC9:52;
  reconsider I as ExtREAL_sequence;
  for n be Nat holds J.n <= I.n
  proof
    let n be Nat;
A21: dom(F.n) = E by A1,MESFUNC8:def 2;
A22: F.n is nonnegative by A2;
A23: n is Element of NAT by ORDINAL1:def 13;
A24: now
      let x be Element of X;
      assume
A25:  x in dom(G.n);
      (inferior_realsequence(F#x)).n <= (F#x).n by A23,RINFSUP2:8;
      then (H.n).x <= (F#x).n by A5,A25,MESFUNC8:def 5;
      then (H.n).x <= (F.n).x by MESFUNC5:def 13;
      hence (G.n).x <= (F.n).x by A4,A23;
    end;
A26: F.n is_measurable_on E by A2;
A27: G.n is_measurable_on E by A1,A2,A5,MESFUNC8:20;
A28: dom(G.n) = E by A1,A13,A6,MESFUNC8:def 2;
A29: now
      let x be set;
      assume
A30:  x in dom(G.n);
      0 <= (G.0).x by A17,SUPINF_2:70;
      hence 0 <= (G.n).x by A7,A28,A30;
    end;
    then G.n is nonnegative by SUPINF_2:71;
    then integral+(M,G.n) <= integral+(M,F.n) by A21,A28,A26,A27,A22,A24,
    MESFUNC5:91;
    then Integral(M,G.n) <= integral+(M,F.n) by A28,A27,A29,MESFUNC5:94,
    SUPINF_2:71;
    then
A31: Integral(M,G.n) <= Integral(M,F.n) by A21,A26,A22,MESFUNC5:94;
    I.n = Integral(M,F.n) by A11,A23;
    hence thesis by A18,A31;
  end;
  then lim_inf J <= lim_inf I by Th3;
  then
A32: lim J <= lim_inf I by A19,RINFSUP2:41;
A33: dom sup G = E by A1,A13,A6,MESFUNC8:def 4;
  now
    let x be Element of X;
    assume
A34: x in dom lim G;
    then
    for n,m be Element of NAT st m <= n holds (G#x).m <= (G#x).n by A7,
    A14;
    then G#x is non-decreasing by RINFSUP2:7;
    then
A35: lim(G#x) = sup(G#x) by RINFSUP2:37;
    (sup G).x = sup(G#x) by A14,A33,A34,MESFUNC8:def 4;
    hence (lim G).x = (sup G).x by A34,A35,MESFUNC8:def 10;
  end;
  then
A36: lim G = sup G by A14,A33,PARTFUN1:34;
  take I;
  for n be Nat holds I.n = Integral(M,F.n)
  proof
    let n be Nat;
    n is Element of NAT by ORDINAL1:def 13;
    hence thesis by A11;
  end;
  hence thesis by A5,A20,A36,A32,MESFUNC8:11;
end;

begin :: Lebesgue's Convergence Theorem

theorem Th8:
  for X,Y being non empty Subset of ExtREAL, r be R_eal st X = {r}
  & r in REAL holds sup(X + Y) = sup X + sup Y
proof
  let X,Y be non empty Subset of ExtREAL;
  let r be R_eal;
  assume that
A1: X = {r} and
A2: r in REAL;
  set W = X+Y;
A3: -r <> -infty by A2,XXREAL_3:23;
A4: -r <> +infty by A2,XXREAL_3:23;
  now
    let z be set;
    assume z in -X + W;
    then consider x,y be R_eal such that
A5: x in -X and
A6: y in W and
A7: z = x + y by SUPINF_2:def 5;
    consider x1,y1 be R_eal such that
A8: x1 in X and
A9: y1 in Y and
A10: y = x1 + y1 by A6,SUPINF_2:def 5;
    -x in --X by A5,SUPINF_2:23;
    then -x in X by SUPINF_2:22;
    then -x = r by A1,TARSKI:def 1;
    then z = -r + (r + y1) by A1,A7,A8,A10,TARSKI:def 1;
    then z = (-r + r) + y1 by A2,A4,A3,XXREAL_3:30;
    then z = 0. + y1 by XXREAL_3:7;
    hence z in Y by A9,XXREAL_3:4;
  end;
  then
A11: -X + W c= Y by TARSKI:def 3;
A12: r in X by A1,TARSKI:def 1;
  now
    let z be set;
    assume
A13: z in Y;
    then reconsider y = z as Element of ExtREAL;
    r + y - r = (-r + r) + y by A2,A4,A3,XXREAL_3:30;
    then r + y - r = 0. + y by XXREAL_3:7;
    then
A14: y = r + y - r by XXREAL_3:4;
A15: -r in -X by A12,SUPINF_2:def 6;
    r + y in W by A12,A13,SUPINF_2:def 5;
    hence z in -X + W by A15,A14,SUPINF_2:def 5;
  end;
  then Y c= -X + W by TARSKI:def 3;
  then Y = -X + W by A11,XBOOLE_0:def 10;
  then sup Y <= sup W + sup -X by SUPINF_2:26;
  then sup Y <= sup(X+Y) + -inf X by SUPINF_2:33;
  then sup Y <= sup(X+Y) -r by A1,XXREAL_2:13;
  then r + sup Y <= sup(X+Y) by A2,XXREAL_3:68;
  then
A16: sup X + sup Y <= sup(X+Y) by A1,XXREAL_2:11;
  sup(X + Y) <= sup X + sup Y by SUPINF_2:26;
  hence thesis by A16,XXREAL_0:1;
end;

theorem Th9:
  for X,Y being non empty Subset of ExtREAL, r be R_eal st X = {r}
  & r in REAL holds inf(X + Y) = inf X + inf Y
proof
  let X,Y be non empty Subset of ExtREAL;
  let r be R_eal;
  assume that
A1: X = {r} and
A2: r in REAL;
  set W = X+Y;
  set Z = -X;
A3: -r <> -infty by A2,XXREAL_3:23;
A4: -r <> +infty by A2,XXREAL_3:23;
  now
    let z be set;
    assume z in Z + W;
    then consider x,y be R_eal such that
A5: x in Z and
A6: y in W and
A7: z = x + y by SUPINF_2:def 5;
    consider x1,y1 be R_eal such that
A8: x1 in X and
A9: y1 in Y and
A10: y = x1 + y1 by A6,SUPINF_2:def 5;
    -x in -Z by A5,SUPINF_2:23;
    then -x in X by SUPINF_2:22;
    then -x = r by A1,TARSKI:def 1;
    then z = -r + (r + y1) by A1,A7,A8,A10,TARSKI:def 1;
    then z = (-r + r) + y1 by A2,A4,A3,XXREAL_3:30;
    then z = 0. + y1 by XXREAL_3:7;
    hence z in Y by A9,XXREAL_3:4;
  end;
  then
A11: Z + W c= Y by TARSKI:def 3;
A12: r in X by A1,TARSKI:def 1;
  now
    let z be set;
    assume
A13: z in Y;
    then reconsider y = z as Element of ExtREAL;
    r + y - r = (-r + r) + y by A2,A4,A3,XXREAL_3:30;
    then r + y - r = 0. + y by XXREAL_3:7;
    then
A14: y = r + y - r by XXREAL_3:4;
A15: -r in Z by A12,SUPINF_2:def 6;
    r + y in W by A12,A13,SUPINF_2:def 5;
    hence z in Z + W by A15,A14,SUPINF_2:def 5;
  end;
  then Y c= Z + W by TARSKI:def 3;
  then Y = Z + W by A11,XBOOLE_0:def 10;
  then inf Y >= inf W + inf Z by SUPINF_2:27;
  then inf Y >= inf(X+Y) + -sup X by SUPINF_2:32;
  then inf Y >= inf(X+Y) -r by A1,XXREAL_2:11;
  then r + inf Y >= inf(X+Y) by A2,XXREAL_3:63;
  then
A16: inf X + inf Y >= inf(X+Y) by A1,XXREAL_2:13;
  inf(X + Y) >= inf X + inf Y by SUPINF_2:27;
  hence thesis by A16,XXREAL_0:1;
end;

theorem Th10:
  r in REAL & (for n be Nat holds seq1.n = r + seq2.n) implies
  lim_inf seq1 = r + lim_inf seq2 & lim_sup seq1 = r + lim_sup seq2
proof
  assume that
A1: r in REAL and
A2: for n be Nat holds seq1.n = r + seq2.n;
  reconsider R1 = rng inferior_realsequence seq1, R2 = rng
  inferior_realsequence seq2, P1 = rng superior_realsequence seq1, P2 = rng
  superior_realsequence seq2 as non empty Subset of ExtREAL;
  now
    let z be set;
    assume z in {r} + R2;
    then consider z2,z3 be R_eal such that
A3: z2 in {r} and
A4: z3 in R2 and
A5: z = z2 + z3 by SUPINF_2:def 5;
    consider n be set such that
A6: n in NAT and
A7: (inferior_realsequence seq2).n = z3 by A4,FUNCT_2:17;
    reconsider n as Element of NAT by A6;
    consider Y2 be non empty Subset of ExtREAL such that
A8: Y2 = {seq2.k where k is Element of NAT : n <= k} and
A9: z3 = inf Y2 by A7,RINFSUP2:def 6;
    consider Y1 be non empty Subset of ExtREAL such that
A10: Y1 = {seq1.k where k is Element of NAT : n <= k} and
A11: (inferior_realsequence seq1).n = inf Y1 by RINFSUP2:def 6;
    now
      let w be set;
A12:  r in {r} by TARSKI:def 1;
      assume w in Y1;
      then consider k1 be Element of NAT such that
A13:  w = seq1.k1 and
A14:  n <= k1 by A10;
      reconsider w1=w as R_eal by A13;
A15:  seq2.k1 in Y2 by A8,A14;
      w1 = r + seq2.k1 by A2,A13;
      hence w in {r} + Y2 by A12,A15,SUPINF_2:def 5;
    end;
    then
A16: Y1 c= {r} + Y2 by TARSKI:def 3;
    now
      let w be set;
      assume w in {r} + Y2;
      then consider w1,w2 be R_eal such that
A17:  w1 in {r} and
A18:  w2 in Y2 and
A19:  w = w1 + w2 by SUPINF_2:def 5;
      consider k2 be Element of NAT such that
A20:  w2 = seq2.k2 and
A21:  n <= k2 by A8,A18;
      w1 = r by A17,TARSKI:def 1;
      then w = seq1.k2 by A2,A19,A20;
      hence w in Y1 by A10,A21;
    end;
    then {r} + Y2 c= Y1 by TARSKI:def 3;
    then Y1 = {r} + Y2 by A16,XBOOLE_0:def 10;
    then inf Y1 = inf{r} + inf Y2 by A1,Th9;
    then inf Y1 = r + inf Y2 by XXREAL_2:13;
    then z = (inferior_realsequence seq1).n by A3,A5,A9,A11,TARSKI:def
    1;
    hence z in R1 by FUNCT_2:6;
  end;
  then
A22: {r} + R2 c= R1 by TARSKI:def 3;
  now
    let z be set;
    assume z in {r} + P2;
    then consider z2,z3 be R_eal such that
A23: z2 in {r} and
A24: z3 in P2 and
A25: z = z2 + z3 by SUPINF_2:def 5;
    consider n be set such that
A26: n in NAT and
A27: (superior_realsequence seq2).n = z3 by A24,FUNCT_2:17;
    reconsider n as Element of NAT by A26;
    consider Y2 be non empty Subset of ExtREAL such that
A28: Y2 = {seq2.k where k is Element of NAT : n <= k} and
A29: z3 = sup Y2 by A27,RINFSUP2:def 7;
    consider Y1 be non empty Subset of ExtREAL such that
A30: Y1 = {seq1.k where k is Element of NAT : n <= k} and
A31: (superior_realsequence seq1).n = sup Y1 by RINFSUP2:def 7;
    now
      let w be set;
A32:  r in {r} by TARSKI:def 1;
      assume w in Y1;
      then consider k1 be Element of NAT such that
A33:  w = seq1.k1 and
A34:  n <= k1 by A30;
      reconsider w1=w as R_eal by A33;
A35:  seq2.k1 in Y2 by A28,A34;
      w1 = r + seq2.k1 by A2,A33;
      hence w in {r} + Y2 by A32,A35,SUPINF_2:def 5;
    end;
    then
A36: Y1 c= {r} + Y2 by TARSKI:def 3;
    now
      let w be set;
      assume w in {r} + Y2;
      then consider w1,w2 be R_eal such that
A37:  w1 in {r} and
A38:  w2 in Y2 and
A39:  w = w1 + w2 by SUPINF_2:def 5;
      consider k2 be Element of NAT such that
A40:  w2 = seq2.k2 and
A41:  n <= k2 by A28,A38;
      w1 = r by A37,TARSKI:def 1;
      then w = seq1.k2 by A2,A39,A40;
      hence w in Y1 by A30,A41;
    end;
    then {r} + Y2 c= Y1 by TARSKI:def 3;
    then Y1 = {r} + Y2 by A36,XBOOLE_0:def 10;
    then sup Y1 = sup{r} + sup Y2 by A1,Th8;
    then sup Y1 = r + sup Y2 by XXREAL_2:11;
    then z = (superior_realsequence seq1).n by A23,A25,A29,A31,
    TARSKI:def 1;
    hence z in P1 by FUNCT_2:6;
  end;
  then
A42: {r} + P2 c= P1 by TARSKI:def 3;
  now
    let z be set;
    assume z in R1;
    then consider n be set such that
A43: n in NAT and
A44: (inferior_realsequence seq1).n = z by FUNCT_2:17;
    reconsider n as Element of NAT by A43;
    consider Y1 be non empty Subset of ExtREAL such that
A45: Y1 = {seq1.k where k is Element of NAT : n <= k} and
A46: z = inf Y1 by A44,RINFSUP2:def 6;
    consider Y2 be non empty Subset of ExtREAL such that
A47: Y2 = {seq2.k where k is Element of NAT : n <= k} and
A48: (inferior_realsequence seq2).n = inf Y2 by RINFSUP2:def 6;
    now
      let w be set;
A49:  r in {r} by TARSKI:def 1;
      assume w in Y1;
      then consider k1 be Element of NAT such that
A50:  w = seq1.k1 and
A51:  n <= k1 by A45;
      reconsider w1=w as R_eal by A50;
A52:  seq2.k1 in Y2 by A47,A51;
      w1 = r + seq2.k1 by A2,A50;
      hence w in {r} + Y2 by A49,A52,SUPINF_2:def 5;
    end;
    then
A53: Y1 c= {r} + Y2 by TARSKI:def 3;
    now
      let w be set;
      assume w in {r} + Y2;
      then consider w1,w2 be R_eal such that
A54:  w1 in {r} and
A55:  w2 in Y2 and
A56:  w = w1 + w2 by SUPINF_2:def 5;
      consider k2 be Element of NAT such that
A57:  w2 = seq2.k2 and
A58:  n <= k2 by A47,A55;
      w1 = r by A54,TARSKI:def 1;
      then w = seq1.k2 by A2,A56,A57;
      hence w in Y1 by A45,A58;
    end;
    then {r} + Y2 c= Y1 by TARSKI:def 3;
    then Y1 = {r} + Y2 by A53,XBOOLE_0:def 10;
    then inf Y1 = inf{r} + inf Y2 by A1,Th9;
    then
A59: inf Y1 = r + inf Y2 by XXREAL_2:13;
A60: (inferior_realsequence seq2).n in R2 by FUNCT_2:6;
    r in {r} by TARSKI:def 1;
    hence z in {r} + R2 by A46,A48,A59,A60,SUPINF_2:def 5;
  end;
  then R1 c= {r} + R2 by TARSKI:def 3;
  then R1 = {r} + R2 by A22,XBOOLE_0:def 10;
  then sup R1 = sup {r} + sup R2 by A1,Th8;
  hence lim_inf seq1 = r + lim_inf seq2 by XXREAL_2:11;
  now
    let z be set;
    assume z in P1;
    then consider n be set such that
A61: n in NAT and
A62: (superior_realsequence seq1).n = z by FUNCT_2:17;
    reconsider n as Element of NAT by A61;
    consider Y1 be non empty Subset of ExtREAL such that
A63: Y1 = {seq1.k where k is Element of NAT : n <= k} and
A64: z = sup Y1 by A62,RINFSUP2:def 7;
    consider Y2 be non empty Subset of ExtREAL such that
A65: Y2 = {seq2.k where k is Element of NAT : n <= k} and
A66: (superior_realsequence seq2).n = sup Y2 by RINFSUP2:def 7;
    now
      let w be set;
A67:  r in {r} by TARSKI:def 1;
      assume w in Y1;
      then consider k1 be Element of NAT such that
A68:  w = seq1.k1 and
A69:  n <= k1 by A63;
      reconsider w1=w as R_eal by A68;
A70:  seq2.k1 in Y2 by A65,A69;
      w1 = r + seq2.k1 by A2,A68;
      hence w in {r} + Y2 by A67,A70,SUPINF_2:def 5;
    end;
    then
A71: Y1 c= {r} + Y2 by TARSKI:def 3;
    now
      let w be set;
      assume w in {r} + Y2;
      then consider w1,w2 be R_eal such that
A72:  w1 in {r} and
A73:  w2 in Y2 and
A74:  w = w1 + w2 by SUPINF_2:def 5;
      consider k2 be Element of NAT such that
A75:  w2 = seq2.k2 and
A76:  n <= k2 by A65,A73;
      w1 = r by A72,TARSKI:def 1;
      then w = seq1.k2 by A2,A74,A75;
      hence w in Y1 by A63,A76;
    end;
    then {r} + Y2 c= Y1 by TARSKI:def 3;
    then Y1 = {r} + Y2 by A71,XBOOLE_0:def 10;
    then sup Y1 = sup{r} + sup Y2 by A1,Th8;
    then
A77: sup Y1 = r + sup Y2 by XXREAL_2:11;
A78: (superior_realsequence seq2).n in P2 by FUNCT_2:6;
    r in {r} by TARSKI:def 1;
    hence z in {r} + P2 by A64,A66,A77,A78,SUPINF_2:def 5;
  end;
  then P1 c= {r} + P2 by TARSKI:def 3;
  then P1 = {r} + P2 by A42,XBOOLE_0:def 10;
  then inf P1 = inf{r} + inf P2 by A1,Th9;
  hence thesis by XXREAL_2:13;
end;

reserve F1,F2 for Functional_Sequence of X,ExtREAL,
  f,g,P for PartFunc of X, ExtREAL;

theorem Th11:
  dom(F1.0) = dom(F2.0) & F1 is with_the_same_dom & f"{+infty} =
{} & f"{-infty} = {} & (for n be Nat holds F1.n = f + F2.n) implies lim_inf F1
  = f + lim_inf F2 & lim_sup F1 = f + lim_sup F2
proof
  assume that
A1: dom(F1.0) = dom(F2.0) and
A2: F1 is with_the_same_dom and
A3: f"{+infty} = {} and
A4: f"{-infty} = {} and
A5: for n be Nat holds F1.n = f + F2.n;
A6: F1.0 = f + F2.0 by A5;
A7: dom(f + F2.0) = (dom f /\ dom(F2.0))\((f"{-infty} /\ (F2.0)"{+infty}) \/
  (f"{+infty} /\ (F2.0)"{-infty})) by MESFUNC1:def 3;
A8: dom(f + lim_sup F2) = (dom f /\ dom(lim_sup F2))\((f"{-infty} /\ (
lim_sup F2)"{+infty}) \/ (f"{+infty} /\ (lim_sup F2)"{-infty})) by MESFUNC1:def
  3;
  then
A9: dom(f + lim_sup F2) = dom f /\ dom(F2.0) by A3,A4,MESFUNC8:def 9;
  then
A10: dom(lim_sup F1) = dom(f + lim_sup F2) by A3,A4,A6,A7,
  MESFUNC8:def 9;
A11: dom(f + lim_inf F2) = (dom f /\ dom(lim_inf F2))\((f"{-infty} /\ (
lim_inf F2)"{+infty}) \/ (f"{+infty} /\ (lim_inf F2)"{-infty})) by MESFUNC1:def
  3;
  then
A12: dom(f + lim_inf F2) = dom f /\ dom(F2.0) by A3,A4,MESFUNC8:def 8;
  then
A13: dom(lim_inf F1) = dom(f + lim_inf F2) by A3,A4,A6,A7,
  MESFUNC8:def 8;
A14: dom(lim_inf F2) = dom(F2.0) by MESFUNC8:def 8;
A15: dom(lim_inf F1) = dom(F1.0) by MESFUNC8:def 8;
  for x be Element of X st x in dom(lim_inf F1) holds (lim_inf F1).x = (f
  + lim_inf F2).x
  proof
    let x be Element of X;
    assume
A16: x in dom(lim_inf F1);
    then
A17: (lim_inf F1).x = lim_inf(F1#x) by MESFUNC8:def 8;
A18: for n be Nat holds (F1#x).n = f.x + (F2#x).n
    proof
      let n be Nat;
      F1.n = f + F2.n by A5;
      then dom(f + F2.n) = dom(F1.0) by A2,MESFUNC8:def 2;
      then
A19:  x in dom(f + F2.n) by A16,MESFUNC8:def 8;
      (F1.n).x = (f + F2.n).x by A5;
      then (F1.n).x = f.x + (F2.n).x by A19,MESFUNC1:def 3;
      then (F1#x).n = f.x + (F2.n).x by MESFUNC5:def 13;
      hence thesis by MESFUNC5:def 13;
    end;
A20: dom(f + lim_inf F2) c= dom f by A3,A4,A11,XBOOLE_1:17;
    then not f.x in {-infty} by A4,A13,A16,FUNCT_1:def 13;
    then
A21: f.x <> -infty by TARSKI:def 1;
    not f.x in {+infty} by A3,A13,A16,A20,FUNCT_1:def 13;
    then f.x <> +infty by TARSKI:def 1;
    then
A22: f.x is Real by A21,XXREAL_0:14;
    x in dom(f + lim_inf F2) by A3,A4,A6,A7,A12,A16,MESFUNC8:def 8;
    then
A23: (f + lim_inf F2).x = f.x + (lim_inf F2).x by MESFUNC1:def 3;
    (lim_inf F2).x = lim_inf(F2#x) by A1,A15,A14,A16,MESFUNC8:def 8;
    hence thesis by A22,A18,A17,A23,Th10;
  end;
  hence lim_inf F1 = f + lim_inf F2 by A13,PARTFUN1:34;
A24: dom(lim_sup F1) = dom(F1.0) by MESFUNC8:def 9;
A25: dom(lim_sup F2) = dom(F2.0) by MESFUNC8:def 9;
  for x be Element of X st x in dom(lim_sup F1) holds (lim_sup F1).x = (f
  + lim_sup F2).x
  proof
    let x be Element of X;
    assume
A26: x in dom(lim_sup F1);
    then
A27: (lim_sup F1).x = lim_sup(F1#x) by MESFUNC8:def 9;
A28: for n be Nat holds (F1#x).n = f.x + (F2#x).n
    proof
      let n be Nat;
      F1.n = f + F2.n by A5;
      then
A29:  dom(f + F2.n) = dom(F1.0) by A2,MESFUNC8:def 2;
      (F1.n).x = (f + F2.n).x by A5;
      then (F1.n).x = f.x + (F2.n).x by A24,A26,A29,MESFUNC1:def 3;
      then (F1#x).n = f.x + (F2.n).x by MESFUNC5:def 13;
      hence thesis by MESFUNC5:def 13;
    end;
A30: dom(f + lim_sup F2) c= dom f by A3,A4,A8,XBOOLE_1:17;
    then not f.x in {-infty} by A4,A10,A26,FUNCT_1:def 13;
    then
A31: f.x <> -infty by TARSKI:def 1;
    not f.x in {+infty} by A3,A10,A26,A30,FUNCT_1:def 13;
    then f.x <> +infty by TARSKI:def 1;
    then
A32: f.x is Real by A31,XXREAL_0:14;
    x in dom(f + lim_sup F2) by A3,A4,A6,A7,A9,A26,MESFUNC8:def 9;
    then
A33: (f + lim_sup F2).x = f.x + (lim_sup F2).x by MESFUNC1:def 3;
    (lim_sup F2).x = lim_sup(F2#x) by A1,A24,A25,A26,MESFUNC8:def 9;
    hence thesis by A32,A28,A27,A33,Th10;
  end;
  hence thesis by A10,PARTFUN1:34;
end;

canceled;

theorem Th13:
  f is_integrable_on M & g is_integrable_on M implies f-g is_integrable_on M
proof
  assume that
A1: f is_integrable_on M and
A2: g is_integrable_on M;
  (-1)(#)g is_integrable_on M by A2,MESFUNC5:116;
  then -g is_integrable_on M by MESFUNC2:11;
  then f+(-g) is_integrable_on M by A1,MESFUNC5:114;
  hence thesis by MESFUNC2:9;
end;

theorem Th14:
  f is_integrable_on M & g is_integrable_on M implies ex E be
Element of S st E = dom f /\ dom g & Integral(M,f-g)=Integral(M,f|E)+Integral(M
  ,(-g)|E)
proof
  assume that
A1: f is_integrable_on M and
A2: g is_integrable_on M;
  (-1)(#)g is_integrable_on M by A2,MESFUNC5:116;
  then -g is_integrable_on M by MESFUNC2:11;
  then consider E be Element of S such that
A3: E = dom f /\ dom(-g) and
A4: Integral(M,f+(-g))= Integral(M,f|E)+Integral(M,(-g)|E) by A1,
  MESFUNC5:115;
A5: dom g = dom(-g) by MESFUNC1:def 7;
  Integral(M,f-g)= Integral(M,f|E)+Integral(M,(-g)|E) by A4,MESFUNC2:9;
  hence thesis by A3,A5;
end;

theorem Th15:
  (for n be Nat holds seq1.n = -seq2.n) implies lim_inf seq2 = -
  lim_sup seq1 & lim_sup seq2 = -lim_inf seq1
proof
  assume
A1: for n be Nat holds seq1.n = -seq2.n;
  now
    let z be set;
    assume z in rng (inferior_realsequence seq2);
    then consider n be set such that
A2: n in dom (inferior_realsequence seq2) and
A3: z = (inferior_realsequence seq2).n by FUNCT_1:def 5;
    reconsider n as Element of NAT by A2;
    consider R2 be non empty Subset of ExtREAL such that
A4: R2 = {seq2.k where k is Element of NAT : n <= k} and
A5: z = inf R2 by A3,RINFSUP2:def 6;
    reconsider z2 = z as Element of ExtREAL by A3;
    set R1 = {seq1.k where k is Element of NAT : n <= k};
    reconsider R1 as non empty Subset of ExtREAL by RINFSUP2:5;
    set z1 = -z2;
A6: ex K1 be non empty Subset of ExtREAL st K1 = {seq1.k where k is
Element of NAT: n <= k} & (superior_realsequence seq1).n = sup K1 by RINFSUP2:
    def 7;
    now
      let x be set;
      assume x in R1;
      then consider k be Element of NAT such that
A7:   x = seq1.k and
A8:   n <= k;
      reconsider x1=x as Element of ExtREAL by A7;
      -x1 = -(-seq2.k) by A1,A7;
      then -x1 in {seq2.k2 where k2 is Element of NAT : n <= k2} by A8;
      then -(-x1) in -R2 by A4,SUPINF_2:23;
      hence x in -R2;
    end;
    then
A9: R1 c= -R2 by TARSKI:def 3;
    now
      let x be set;
      assume x in -R2;
      then consider y be R_eal such that
A10:  y in R2 and
A11:  x = -y by SUPINF_2:def 6;
      consider k be Element of NAT such that
A12:  y = seq2.k and
A13:  n <= k by A4,A10;
      seq1.k = -seq2.k by A1;
      hence x in R1 by A11,A12,A13;
    end;
    then -R2 c= R1 by TARSKI:def 3;
    then -R2 = R1 by A9,XBOOLE_0:def 10;
    then (superior_realsequence seq1).n = z1 by A5,A6,SUPINF_2:33;
    then
A14: z1 in rng(superior_realsequence seq1) by FUNCT_2:6;
    z2 = -z1;
    hence z in -rng(superior_realsequence seq1) by A14,SUPINF_2:def 6;
  end;
  then
A15: rng (inferior_realsequence seq2) c= -(rng (superior_realsequence seq1))
  by TARSKI:def 3;
  now
    let z be set;
    assume z in rng (superior_realsequence seq2);
    then consider n be set such that
A16: n in dom (superior_realsequence seq2) and
A17: z = (superior_realsequence seq2).n by FUNCT_1:def 5;
    reconsider n as Element of NAT by A16;
    consider R2 be non empty Subset of ExtREAL such that
A18: R2 = {seq2.k where k is Element of NAT : n <= k} and
A19: z = sup R2 by A17,RINFSUP2:def 7;
    reconsider z2 = z as Element of ExtREAL by A17;
    set R1 = {seq1.k where k is Element of NAT : n <= k};
    reconsider R1 as non empty Subset of ExtREAL by RINFSUP2:5;
    set z1 = -z2;
A20: ex K1 be non empty Subset of ExtREAL st K1 = {seq1.k where k is
Element of NAT: n <= k} & (inferior_realsequence seq1).n = inf K1 by RINFSUP2:
    def 6;
    now
      let x be set;
      assume x in R1;
      then consider k be Element of NAT such that
A21:  x = seq1.k and
A22:  n <= k;
      reconsider x1=x as Element of ExtREAL by A21;
      -x1 = -(-seq2.k) by A1,A21;
      then -x1 in {seq2.k2 where k2 is Element of NAT : n <= k2} by A22;
      then -(-x1) in -R2 by A18,SUPINF_2:23;
      hence x in -R2;
    end;
    then
A23: R1 c= -R2 by TARSKI:def 3;
    now
      let x be set;
      assume x in -R2;
      then consider y be R_eal such that
A24:  y in R2 and
A25:  x = -y by SUPINF_2:def 6;
      consider k be Element of NAT such that
A26:  y = seq2.k and
A27:  n <= k by A18,A24;
      seq1.k = -seq2.k by A1;
      hence x in R1 by A25,A26,A27;
    end;
    then -R2 c= R1 by TARSKI:def 3;
    then -R2 = R1 by A23,XBOOLE_0:def 10;
    then (inferior_realsequence seq1).n = z1 by A19,A20,SUPINF_2:32;
    then
A28: z1 in rng(inferior_realsequence seq1) by FUNCT_2:6;
    z2 = -z1;
    hence z in -rng(inferior_realsequence seq1) by A28,SUPINF_2:def 6;
  end;
  then
A29: rng (superior_realsequence seq2) c= -(rng (inferior_realsequence seq1))
  by TARSKI:def 3;
  now
    let z be set;
    assume z in -(rng (superior_realsequence seq1));
    then consider t be R_eal such that
A30: t in rng (superior_realsequence seq1) and
A31: z = -t by SUPINF_2:def 6;
    consider n be set such that
A32: n in dom (superior_realsequence seq1) and
A33: t = (superior_realsequence seq1).n by A30,FUNCT_1:def 5;
    reconsider n as Element of NAT by A32;
    consider R1 be non empty Subset of ExtREAL such that
A34: R1 = {seq1.k where k is Element of NAT : n <= k} and
A35: t = sup R1 by A33,RINFSUP2:def 7;
    reconsider z1 = z as Element of ExtREAL by A31;
    set R2 = {seq2.k where k is Element of NAT : n <= k};
    reconsider R2 as non empty Subset of ExtREAL by RINFSUP2:5;
A36: ex K2 be non empty Subset of ExtREAL st K2 = {seq2.k where k is
Element of NAT: n <= k} & (inferior_realsequence seq2).n = inf K2 by RINFSUP2:
    def 6;
    now
      let x be set;
      assume x in R2;
      then consider k be Element of NAT such that
A37:  x = seq2.k and
A38:  n <= k;
      reconsider x1=x as Element of ExtREAL by A37;
      -x1 = -(-seq1.k) by A1,A37;
      then -x1 in {seq1.k2 where k2 is Element of NAT : n <= k2} by A38;
      then -(-x1) in -R1 by A34,SUPINF_2:23;
      hence x in -R1;
    end;
    then
A39: R2 c= -R1 by TARSKI:def 3;
    now
      let x be set;
      assume x in -R1;
      then consider y be R_eal such that
A40:  y in R1 and
A41:  x = -y by SUPINF_2:def 6;
      consider k be Element of NAT such that
A42:  y = seq1.k and
A43:  n <= k by A34,A40;
      seq1.k = -seq2.k by A1;
      hence x in R2 by A41,A42,A43;
    end;
    then -R1 c= R2 by TARSKI:def 3;
    then -R1 = R2 by A39,XBOOLE_0:def 10;
    then (inferior_realsequence seq2).n = z1 by A31,A35,A36,SUPINF_2:32;
    hence z in rng (inferior_realsequence seq2) by FUNCT_2:6;
  end;
  then
  -(rng (superior_realsequence seq1)) c= rng (inferior_realsequence seq2)
  by TARSKI:def 3;
  then rng (inferior_realsequence seq2) = -(rng (superior_realsequence seq1))
  by A15,XBOOLE_0:def 10;
  hence lim_inf seq2 = - lim_sup seq1 by SUPINF_2:33;
  now
    let z be set;
    assume z in -(rng (inferior_realsequence seq1));
    then consider t be R_eal such that
A44: t in rng (inferior_realsequence seq1) and
A45: z = -t by SUPINF_2:def 6;
    consider n be set such that
A46: n in dom (inferior_realsequence seq1) and
A47: t = (inferior_realsequence seq1).n by A44,FUNCT_1:def 5;
    reconsider n as Element of NAT by A46;
    consider R1 be non empty Subset of ExtREAL such that
A48: R1 = {seq1.k where k is Element of NAT : n <= k} and
A49: t = inf R1 by A47,RINFSUP2:def 6;
    reconsider z1 = z as Element of ExtREAL by A45;
    set R2 = {seq2.k where k is Element of NAT : n <= k};
    reconsider R2 as non empty Subset of ExtREAL by RINFSUP2:5;
A50: ex K2 be non empty Subset of ExtREAL st K2 = {seq2.k where k is
Element of NAT: n <= k} & (superior_realsequence seq2).n = sup K2 by RINFSUP2:
    def 7;
    now
      let x be set;
      assume x in R2;
      then consider k be Element of NAT such that
A51:  x = seq2.k and
A52:  n <= k;
      reconsider x1=x as Element of ExtREAL by A51;
      seq1.k = -seq2.k by A1;
      then -x1 in R1 by A48,A51,A52;
      then -(-x1) in -R1 by SUPINF_2:23;
      hence x in -R1;
    end;
    then
A53: R2 c= -R1 by TARSKI:def 3;
    now
      let x be set;
      assume x in -R1;
      then consider y be R_eal such that
A54:  y in R1 and
A55:  x = -y by SUPINF_2:def 6;
      consider k be Element of NAT such that
A56:  y = seq1.k and
A57:  n <= k by A48,A54;
      seq1.k = -seq2.k by A1;
      hence x in R2 by A55,A56,A57;
    end;
    then -R1 c= R2 by TARSKI:def 3;
    then -R1 = R2 by A53,XBOOLE_0:def 10;
    then (superior_realsequence seq2).n = z1 by A45,A49,A50,SUPINF_2:33;
    hence z in rng (superior_realsequence seq2) by FUNCT_2:6;
  end;
  then -(rng (inferior_realsequence seq1)) c= rng (superior_realsequence seq2
  ) by TARSKI:def 3;
  then rng (superior_realsequence seq2) = -(rng (inferior_realsequence seq1))
  by A29,XBOOLE_0:def 10;
  hence thesis by SUPINF_2:32;
end;

theorem Th16:
  dom(F1.0) = dom(F2.0) & F1 is with_the_same_dom & (for n be Nat
  holds F1.n = - F2.n) implies lim_inf F1 = -lim_sup F2 & lim_sup F1 = -lim_inf
  F2
proof
  assume that
A1: dom(F1.0) = dom(F2.0) and
A2: F1 is with_the_same_dom and
A3: for n be Nat holds F1.n = - F2.n;
A4: dom lim_inf F1 = dom(F1.0) by MESFUNC8:def 8;
A5: dom lim_sup F2 = dom(F2.0) by MESFUNC8:def 9;
A6: now
    let x be Element of X;
    assume
A7: x in dom(F1.0);
    let n be Nat;
    dom(F1.n) = dom(F1.0) by A2,MESFUNC8:def 2;
    then
A8: x in dom(-F2.n) by A3,A7;
    (F1.n).x = (-F2.n).x by A3;
    then (F1.n).x = -(F2.n).x by A8,MESFUNC1:def 7;
    then (F1#x).n = -(F2.n).x by MESFUNC5:def 13;
    hence (F2#x).n = -(F1#x).n by MESFUNC5:def 13;
  end;
A9: now
    let x be Element of X;
    assume
A10: x in dom lim_inf F1;
    then
A11: (lim_inf F1).x = lim_inf(F1#x) by MESFUNC8:def 8;
    x in dom(-lim_sup F2) by A1,A4,A5,A10,MESFUNC1:def 7;
    then
A12: (-lim_sup F2).x = -(lim_sup F2).x by MESFUNC1:def 7;
A13: for n be Nat holds (F2#x).n = -(F1#x).n by A4,A6,A10;
    (lim_sup F2).x = lim_sup(F2#x) by A1,A4,A5,A10,MESFUNC8:def 9;
    hence (lim_inf F1).x = (-lim_sup F2).x by A13,A11,A12,Th15;
  end;
  dom(-lim_sup F2) = dom lim_sup F2 by MESFUNC1:def 7;
  hence lim_inf F1 = -lim_sup F2 by A1,A4,A5,A9,PARTFUN1:34;
A14: dom lim_sup F1 = dom(F1.0) by MESFUNC8:def 9;
A15: dom lim_inf F2 = dom(F2.0) by MESFUNC8:def 8;
A16: for x be Element of X st x in dom lim_sup F1 holds (lim_sup F1).x = (-
  lim_inf F2).x
  proof
    let x be Element of X;
    assume
A17: x in dom lim_sup F1;
    then
A18: (lim_sup F1).x = lim_sup(F1#x) by MESFUNC8:def 9;
    x in dom(-lim_inf F2) by A1,A14,A15,A17,MESFUNC1:def 7;
    then
A19: (-lim_inf F2).x = -(lim_inf F2).x by MESFUNC1:def 7;
A20: for n be Nat holds (F2#x).n = -(F1#x).n by A14,A6,A17;
    (lim_inf F2).x = lim_inf(F2#x) by A1,A14,A15,A17,MESFUNC8:def 8;
    hence thesis by A20,A18,A19,Th15;
  end;
  dom(-lim_inf F2) = dom lim_inf F2 by MESFUNC1:def 7;
  hence thesis by A1,A14,A15,A16,PARTFUN1:34;
end;

theorem Th17:
  E = dom(F.0) & E = dom P & (for n be Nat holds F.n
  is_measurable_on E) & P is_integrable_on M & (for x be Element of X, n be Nat
  st x in E holds (|. F.n .|).x <= P.x) implies (for n be Nat holds |. F.n .|
  is_integrable_on M) & |. lim_inf F .| is_integrable_on M & |. lim_sup F .|
  is_integrable_on M
proof
  assume that
A1: E = dom(F.0) and
A2: E = dom P and
A3: for n be Nat holds F.n is_measurable_on E and
A4: P is_integrable_on M and
A5: for x be Element of X, n be Nat st x in E holds (|. F.n .|).x <= P.x;
A6: (lim_inf F) is_measurable_on E by A1,A3,MESFUNC8:24;
  hereby
    let n be Nat;
A7: F.n is_measurable_on E by A3;
A8: E = dom(F.n) by A1,MESFUNC8:def 2;
    now
      let x be Element of X;
      assume
A9:   x in dom(F.n);
      then
A10:  x in dom(|. F.n .|) by MESFUNC1:def 10;
      (|. F.n .|).x <= P.x by A5,A8,A9;
      hence |. (F.n).x .| <= P.x by A10,MESFUNC1:def 10;
    end;
    then F.n is_integrable_on M by A2,A4,A8,A7,MESFUNC5:108;
    hence |. F.n .| is_integrable_on M by A8,A7,MESFUNC5:106;
  end;
A11: E = dom(lim_inf F) by A1,MESFUNC8:def 8;
A12: (lim_sup F) is_measurable_on E by A1,A3,MESFUNC8:23;
A13: for x be Element of X, k be Nat st x in E holds -(P.x) <= (F#x).k & (F#x
  ).k <= P.x
  proof
    let x be Element of X, k be Nat;
    assume
A14: x in E;
    then x in dom(F.k) by A1,MESFUNC8:def 2;
    then
A15: x in dom(|.(F.k).|) by MESFUNC1:def 10;
    (|. F.k .|).x <= P.x by A5,A14;
    then
A16: |. (F.k).x .| <= P.x by A15,MESFUNC1:def 10;
    then
A17: (F.k).x <= P.x by EXTREAL2:60;
    -(P.x) <= (F.k).x by A16,EXTREAL2:60;
    hence thesis by A17,MESFUNC5:def 13;
  end;
  now
    let x be Element of X;
    assume
A18: x in dom(lim_inf F);
    then
A19: x in E by A1,MESFUNC8:def 8;
    for k be Nat holds (inferior_realsequence(F#x)).k <= P.x
    proof
      let k be Nat;
      reconsider k1=k as Element of NAT by ORDINAL1:def 13;
A20:  (inferior_realsequence(F#x)).k1 <= (F#x).k1 by RINFSUP2:8;
      (F#x).k <= P.x by A13,A19;
      hence thesis by A20,XXREAL_0:2;
    end;
    then lim_inf(F#x) <= P.x by Th5;
    then
A21: (lim_inf F).x <= P.x by A18,MESFUNC8:def 8;
    now
      let y be ext-real number;
      assume y in rng(F#x);
      then ex k be set st k in dom(F#x) & y = (F#x).k by FUNCT_1:def 5;
      hence -(P.x) <= y by A13,A19;
    end;
    then -(P.x) is LowerBound of rng(F#x) by XXREAL_2:def 2;
    then -(P.x) <= inf(F#x) by XXREAL_2:def 4;
    then -(P.x) <= inf((F#x)^\0) by NAT_1:48;
    then
A22: -(P.x) <= (inferior_realsequence(F#x)).0 by RINFSUP2:27;
    (inferior_realsequence(F#x)).0 <= sup inferior_realsequence(F#x) by
    RINFSUP2:23;
    then -(P.x) <= lim_inf(F#x) by A22,XXREAL_0:2;
    then -(P.x) <= (lim_inf F).x by A18,MESFUNC8:def 8;
    hence |. (lim_inf F).x .| <= P.x by A21,EXTREAL2:60;
  end;
  then lim_inf F is_integrable_on M by A2,A4,A11,A6,MESFUNC5:108;
  hence |. lim_inf F .| is_integrable_on M by A11,A6,MESFUNC5:106;
A23: E = dom(lim_sup F) by A1,MESFUNC8:def 9;
  now
    let x be Element of X;
    assume
A24: x in dom(lim_sup F);
    for k be Nat holds (superior_realsequence(F#x)).k >= -(P.x)
    proof
      let k be Nat;
      reconsider k1=k as Element of NAT by ORDINAL1:def 13;
A25:  (superior_realsequence(F#x)).k1 >= (F#x).k1 by RINFSUP2:8;
      (F#x).k >= -(P.x) by A23,A13,A24;
      hence thesis by A25,XXREAL_0:2;
    end;
    then lim_sup(F#x) >= -(P.x) by Th4;
    then
A26: (lim_sup F).x >= -(P.x) by A24,MESFUNC8:def 9;
    now
      let y be ext-real number;
      assume y in rng(F#x);
      then ex k be set st k in dom(F#x) & y = (F#x).k by FUNCT_1:def 5;
      hence P.x >= y by A23,A13,A24;
    end;
    then P.x is UpperBound of rng(F#x) by XXREAL_2:def 1;
    then P.x >= sup rng(F#x) by XXREAL_2:def 3;
    then P.x >= sup((F#x)^\0) by NAT_1:48;
    then
A27: P.x >= (superior_realsequence(F#x)).0 by RINFSUP2:27;
    (superior_realsequence(F#x)).0 >= inf superior_realsequence(F#x) by
    RINFSUP2:23;
    then P.x >= lim_sup(F#x) by A27,XXREAL_0:2;
    then P.x >= (lim_sup F).x by A24,MESFUNC8:def 9;
    hence |. (lim_sup F).x .| <= P.x by A26,EXTREAL2:60;
  end;
  then lim_sup F is_integrable_on M by A2,A4,A23,A12,MESFUNC5:108;
  hence thesis by A23,A12,MESFUNC5:106;
end;

Lm1: E = dom(F.0) & E = dom P & (for n be Nat holds F.n is_measurable_on E) &
P is_integrable_on M & P is nonnegative & (for x be Element of X, n be Nat st x
in E holds (|. F.n .|).x <= P.x) & eq_dom(P,+infty) = {} implies ex I be
ExtREAL_sequence st (for n be Nat holds I.n = Integral(M,F.n)) & lim_inf I >=
Integral(M,lim_inf F) & lim_sup I <= Integral(M,lim_sup F) & ( (for x be
Element of X st x in E holds F#x is convergent) implies I is convergent & lim I
= Integral(M,lim F) )
proof
  assume that
A1: E = dom(F.0) and
A2: E = dom P and
A3: for n be Nat holds F.n is_measurable_on E and
A4: P is_integrable_on M and
A5: P is nonnegative and
A6: for x be Element of X, n be Nat st x in E holds (|. F.n .|).x <= P.x and
A7: eq_dom(P,+infty) = {};
A8: E = dom(lim_inf F) by A1,MESFUNC8:def 8;
  then
A9: (lim_inf F)|E = lim_inf F by RELAT_1:97;
  deffunc G(Element of NAT) = P + F.$1;
  consider G be Function such that
A10: dom G = NAT & for n be Element of NAT holds G.n = G(n) from FUNCT_1
  :sch 4;
A11: for x be Element of X, k be Nat st x in E holds -(P.x) <= (F#x).k & (F#x
  ).k <= P.x
  proof
    let x be Element of X, k be Nat;
    assume
A12: x in E;
    then x in dom(F.k) by A1,MESFUNC8:def 2;
    then
A13: x in dom |.(F.k).| by MESFUNC1:def 10;
    (|. F.k .|).x <= P.x by A6,A12;
    then
A14: |. (F.k).x .| <= P.x by A13,MESFUNC1:def 10;
    then
A15: (F.k).x <= P.x by EXTREAL2:60;
    -(P.x) <= (F.k).x by A14,EXTREAL2:60;
    hence thesis by A15,MESFUNC5:def 13;
  end;
A16: now
    let x be Element of X;
    assume
A17: x in dom lim_inf F;
    now
      let k be Nat;
      k is Element of NAT by ORDINAL1:def 13;
      then
A18:  (inferior_realsequence(F#x)).k <= (F#x).k by RINFSUP2:8;
      (F#x).k <= P.x by A8,A11,A17;
      hence (inferior_realsequence(F#x)).k <= P.x by A18,XXREAL_0:2;
    end;
    then lim_inf(F#x) <= P.x by Th5;
    then
A19: (lim_inf F).x <= P.x by A17,MESFUNC8:def 8;
    now
      let y be ext-real number;
      assume y in rng(F#x);
      then ex k be set st k in dom(F#x) & y = (F#x).k by FUNCT_1:def 5;
      hence -(P.x) <= y by A8,A11,A17;
    end;
    then -(P.x) is LowerBound of rng(F#x) by XXREAL_2:def 2;
    then -(P.x) <= inf rng(F#x) by XXREAL_2:def 4;
    then -(P.x) <= inf((F#x)^\0) by NAT_1:48;
    then
A20: -(P.x) <= (inferior_realsequence(F#x)).0 by RINFSUP2:27;
    (inferior_realsequence(F#x)).0 <= sup inferior_realsequence(F#x) by
    RINFSUP2:23;
    then -(P.x) <= lim_inf(F#x) by A20,XXREAL_0:2;
    then -(P.x) <= (lim_inf F).x by A17,MESFUNC8:def 8;
    hence |. (lim_inf F).x .| <= P.x by A19,EXTREAL2:60;
  end;
  lim_inf F is_measurable_on E by A1,A3,MESFUNC8:24;
  then lim_inf F is_integrable_on M by A2,A4,A8,A16,MESFUNC5:108;
  then
A21: ex E3 be Element of S st E3 = dom P /\ dom(lim_inf F) & Integral(M,P +
  lim_inf F) = Integral(M,P|E3) + Integral(M,(lim_inf F)|E3) by A4,MESFUNC5:
  115;
A22: P|E = P by A2,RELAT_1:97;
A23: Integral(M,P) < +infty by A4,MESFUNC5:102;
  -infty < Integral(M,P) by A4,MESFUNC5:102;
  then
A24: Integral(M,P) is Real by A23,XXREAL_0:14;
  now
    let x be set;
    assume x in P"{-infty};
    then
A25: P.x in {-infty} by FUNCT_1:def 13;
    0 <= P.x by A5,SUPINF_2:70;
    hence contradiction by A25,TARSKI:def 1;
  end;
  then
A26: P"{-infty} = {} by XBOOLE_0:def 1;
A27: now
    let n be Element of NAT;
    G.n = P + F.n by A10;
    hence G.n is PartFunc of X,ExtREAL;
  end;
A28: now
    let n be Nat;
    n is Element of NAT by ORDINAL1:def 13;
    hence G.n = P + F.n by A10;
  end;
  reconsider G as Functional_Sequence of X,ExtREAL by A10,A27,SEQFUNC:1;
A29: P"{+infty} = {} by A7,MESFUNC5:36;
A30: for n be Nat holds dom(G.n) = E
  proof
    let n be natural number;
    dom(G.n) = dom(P + F.n) by A28;
    then
A31: dom(G.n) = (dom P /\ dom(F.n))\((P"{-infty} /\ (F.n)"{+infty}) \/ (P"
    {+infty} /\ (F.n)"{-infty})) by MESFUNC1:def 3;
    dom(F.n) = E by A1,MESFUNC8:def 2;
    hence thesis by A2,A29,A26,A31;
  end;
A32: now
    let n,m be natural number;
    thus dom(G.n) = E by A30
      .= dom(G.m) by A30;
  end;
  deffunc H(Element of NAT) = P - F.$1;
  consider H be Function such that
A33: dom H = NAT & for n be Element of NAT holds H.n = H(n) from
  FUNCT_1:sch 4;
  reconsider G as with_the_same_dom Functional_Sequence of X,ExtREAL by A32,
  MESFUNC8:def 2;
A34: dom(G.0) = dom(F.0) by A1,A30;
A35: for n be Nat holds F.n is_integrable_on M
  proof
    let n be Nat;
A36: E = dom(F.n) by A1,MESFUNC8:def 2;
A37: now
      let x be Element of X;
      assume
A38:  x in dom(F.n);
      then
A39:  x in dom(|. F.n .|) by MESFUNC1:def 10;
      (|. F.n .|).x <= P.x by A6,A36,A38;
      hence |. (F.n).x .| <= P.x by A39,MESFUNC1:def 10;
    end;
    F.n is_measurable_on E by A3;
    hence thesis by A2,A4,A36,A37,MESFUNC5:108;
  end;
A40: now
    let n be Nat;
A41: G.n = P + F.n by A28;
    now
      let x be set;
      assume
A42:  x in dom(P+(F.n));
      then reconsider x1=x as Element of X;
      x in E by A30,A41,A42;
      then -(P.x) <= (F#x1).n by A11;
      then
A43:  -P.x <= (F.n).x by MESFUNC5:def 13;
      -P.x + P.x = 0 by XXREAL_3:7;
      then 0 <= (F.n).x + P.x by A43,XXREAL_3:38;
      hence 0 <= (P+(F.n)).x by A42,MESFUNC1:def 3;
    end;
    hence G.n is nonnegative by A41,SUPINF_2:71;
    F.n is_integrable_on M by A35;
    then G.n is_integrable_on M by A4,A41,MESFUNC5:114;
    then ex E1 be Element of S st E1 = dom(G.n) & G.n is_measurable_on E1 by
    MESFUNC5:def 17;
    hence G.n is_measurable_on E by A30;
  end;
  E = dom(G.0) by A30;
  then consider IG be ExtREAL_sequence such that
A44: for n be Nat holds IG.n = Integral(M,G.n) and
A45: Integral(M,lim_inf G) <= lim_inf IG by A40,Th7;
A46: Integral(M,P) < +infty by A4,MESFUNC5:102;
  deffunc I(Element of NAT) = Integral(M,F.$1);
  consider I be Function of NAT,ExtREAL such that
A47: for n be Element of NAT holds I.n = I(n) from FUNCT_2:sch 4;
  reconsider I as ExtREAL_sequence;
A48: -infty < Integral(M,P) by A4,MESFUNC5:102;
A49: for n be Nat holds I.n = Integral(M,F.n) & I.n is Real
  proof
    let n be Nat;
    n is Element of NAT by ORDINAL1:def 13;
    then
A50: I.n = Integral(M,F.n) by A47;
A51: F.n is_integrable_on M by A35;
    then
A52: Integral(M,F.n) < +infty by MESFUNC5:102;
    -infty < Integral(M,F.n) by A51,MESFUNC5:102;
    hence thesis by A50,A52,XXREAL_0:14;
  end;
  now
    let n be Nat;
A53: G.n=P+(F.n) by A28;
    F.n is_integrable_on M by A35;
    then
A54: ex E2 be Element of S st E2 = dom P /\ dom(F.n) & Integral(M,G.n) =
    Integral(M,P|E2) + Integral(M,(F.n)|E2) by A4,A53,MESFUNC5:115;
A55: P|E = P by A2,RELAT_1:97;
A56: dom(F.n) = E by A1,MESFUNC8:def 2;
    then (F.n)|E = F.n by RELAT_1:97;
    then Integral(M,G.n) = Integral(M,P) + I.n by A2,A49,A54,A56,A55;
    hence IG.n = Integral(M,P) + I.n by A44;
  end;
  then lim_inf IG = Integral(M,P) + lim_inf I by A24,Th10;
  then Integral(M,P) + Integral(M,lim_inf F) <= Integral(M,P) + lim_inf I by
  A2,A8,A28,A29,A26,A45,A21,A22,A9,A34,Th11;
  then Integral(M,lim_inf F) <= lim_inf I + Integral(M,P) - Integral(M,P) by
  A48,A46,XXREAL_3:67;
  then Integral(M,lim_inf F) <= lim_inf I + (Integral(M,P) - Integral(M,P))
  by A48,A46,XXREAL_3:31;
  then Integral(M,lim_inf F) <= lim_inf I + 0. by XXREAL_3:7;
  then
A57: Integral(M,lim_inf F) <= lim_inf I by XXREAL_3:4;
A58: now
    let n be Element of NAT;
    H.n = P - F.n by A33;
    hence H.n is PartFunc of X,ExtREAL;
  end;
A59: now
    let n be Nat;
    n is Element of NAT by ORDINAL1:def 13;
    hence H.n = P - F.n by A33;
  end;
A60: E = dom(lim_sup F) by A1,MESFUNC8:def 9;
A61: now
    let x be Element of X;
    assume
A62: x in dom lim_sup F;
    for k be Nat holds (superior_realsequence(F#x)).k >= -(P.x)
    proof
      let k be Nat;
      k is Element of NAT by ORDINAL1:def 13;
      then
A63:  (superior_realsequence(F#x)).k >= (F#x).k by RINFSUP2:8;
      (F#x).k >= -(P.x) by A60,A11,A62;
      hence thesis by A63,XXREAL_0:2;
    end;
    then lim_sup(F#x) >= -(P.x) by Th4;
    then
A64: (lim_sup F).x >= -(P.x) by A62,MESFUNC8:def 9;
    now
      let y be ext-real number;
      assume y in rng(F#x);
      then ex k be set st k in dom(F#x) & y = (F#x).k by FUNCT_1:def
      5;
      hence P.x >= y by A60,A11,A62;
    end;
    then P.x is UpperBound of rng(F#x) by XXREAL_2:def 1;
    then P.x >= sup rng(F#x) by XXREAL_2:def 3;
    then P.x >= sup((F#x)^\0) by NAT_1:48;
    then
A65: P.x >= (superior_realsequence(F#x)).0 by RINFSUP2:27;
    (superior_realsequence(F#x)).0 >= inf superior_realsequence(F#x) by
    RINFSUP2:23;
    then P.x >= lim_sup(F#x) by A65,XXREAL_0:2;
    then P.x >= (lim_sup F).x by A62,MESFUNC8:def 9;
    hence |. (lim_sup F).x .| <= P.x by A64,EXTREAL2:60;
  end;
  lim_sup F is_measurable_on E by A1,A3,MESFUNC8:23;
  then
A66: lim_sup F is_integrable_on M by A2,A4,A60,A61,MESFUNC5:108;
  then
A67: ex E6 be Element of S st E6 = dom P /\ dom lim_sup F & Integral(M,P -
  lim_sup F) = Integral(M,P|E6) + Integral(M,(-lim_sup F)|E6) by A4,Th14;
  set E1 = Integral(M,lim_sup F);
A68: E1 < +infty by A66,MESFUNC5:102;
  -infty < E1 by A66,MESFUNC5:102;
  then reconsider e1 = E1 as Real by A68,XXREAL_0:14;
A69: -E1 = -e1 by SUPINF_2:3;
A70: P|E = P by A2,RELAT_1:97;
  deffunc F1(Element of NAT) = - F.$1;
  consider F1 be Function such that
A71: dom F1 = NAT & for n be Element of NAT holds F1.n = F1(n) from
  FUNCT_1:sch 4;
  reconsider H as Functional_Sequence of X,ExtREAL by A33,A58,SEQFUNC:1;
A72: now
    let n be natural number;
A73: dom(H.n) = dom(P - F.n) by A59;
    dom(F.n) = E by A1,MESFUNC8:def 2;
    then dom(H.n) = (E /\ E)\(({} /\ (F.n)"{+infty}) \/ ({} /\ (F.n)"{-infty}
    )) by A2,A29,A26,A73,MESFUNC1:def 4;
    hence dom(H.n) = E;
  end;
  now
    let n,m be natural number;
    thus dom(H.n) = E by A72
      .= dom(H.m) by A72;
  end;
  then reconsider H as with_the_same_dom Functional_Sequence of X,ExtREAL by
  MESFUNC8:def 2;
A74: -infty < Integral(M,P) by A4,MESFUNC5:102;
A75: now
    let n be Nat;
A76: H.n = P - F.n by A59;
    now
      let x be Element of X;
      assume x in dom(F.n);
      then x in E by A1,MESFUNC8:def 2;
      then (F#x).n <= P.x by A11;
      hence (F.n).x <= P.x by MESFUNC5:def 13;
    end;
    hence H.n is nonnegative by A76,MESFUNC7:1;
    F.n is_integrable_on M by A35;
    then H.n is_integrable_on M by A4,A76,Th13;
    then ex E4 be Element of S st E4 = dom(H.n) & H.n is_measurable_on E4 by
    MESFUNC5:def 17;
    hence H.n is_measurable_on E by A72;
  end;
  E = dom(H.0) by A72;
  then consider IH be ExtREAL_sequence such that
A77: for n be Nat holds IH.n = Integral(M,H.n) and
A78: Integral(M,lim_inf H) <= lim_inf IH by A75,Th7;
A79: Integral(M,P) < +infty by A4,MESFUNC5:102;
  now
    let n be Element of NAT;
    F1.n = -F.n by A71;
    hence F1.n is PartFunc of X,ExtREAL;
  end;
  then reconsider F1 as Functional_Sequence of X,ExtREAL by A71,SEQFUNC:1;
A80: now
    let n be Nat;
    n is Element of NAT by ORDINAL1:def 13;
    hence F1.n = -F.n by A71;
  end;
  now
    let n,m be natural number;
    F1.m = -F.m by A80;
    then
A81: dom(F1.m) = dom(F.m) by MESFUNC1:def 7;
    F1.n = -F.n by A80;
    then dom(F1.n) = dom(F.n) by MESFUNC1:def 7;
    hence dom(F1.n) = dom(F1.m) by A81,MESFUNC8:def 2;
  end;
  then reconsider F1 as with_the_same_dom Functional_Sequence of X,ExtREAL by
  MESFUNC8:def 2;
A82: Integral(M,-lim_sup F) = Integral(M,(-1)(#)lim_sup F) by MESFUNC2:11;
A83: now
    let n be Nat;
    H.n = P - F.n by A59;
    then H.n = P + (- F.n) by MESFUNC2:9;
    hence H.n = P + F1.n by A80;
  end;
  F1.0 = -F.0 by A80;
  then
A84: dom(F1.0) = dom(F.0) by MESFUNC1:def 7;
  then dom(F1.0) = dom(H.0) by A1,A72;
  then lim_inf H = P + lim_inf F1 by A29,A26,A83,Th11;
  then lim_inf H = P + -lim_sup F by A80,A84,Th16;
  then
A85: lim_inf H = P - lim_sup F by MESFUNC2:9;
  E = dom lim_sup F by A1,MESFUNC8:def 9;
  then E = dom(-lim_sup F) by MESFUNC1:def 7;
  then
A86: (-lim_sup F)|E = -lim_sup F by RELAT_1:97;
  deffunc I1(Element of NAT) = - I.$1;
  consider I1 be Function of NAT,ExtREAL such that
A87: for n be Element of NAT holds I1.n = I1(n) from FUNCT_2:sch 4;
  reconsider I1 as ExtREAL_sequence;
A88: now
    let n be Nat;
    n is Element of NAT by ORDINAL1:def 13;
    hence I1.n = -I.n by A87;
  end;
  then
A89: -lim_inf I1 = lim_sup I by Th15;
  now
    let n be Nat;
A90: F.n is_integrable_on M by A35;
    Integral(M,-F.n) = Integral(M,(-1)(#)F.n) by MESFUNC2:11;
    then
A91: Integral(M,(-F.n)) = R_EAL (-1)*Integral(M,F.n) by A90,MESFUNC5
    :116;
A92: dom(F.n) = E by A1,MESFUNC8:def 2;
    then E = dom(-F.n) by MESFUNC1:def 7;
    then
A93: (-F.n)|E = -(F.n) by RELAT_1:97;
    H.n = P - F.n by A59;
    then
A94: ex E5 be Element of S st E5 = dom P /\ dom(F.n) & Integral(M,H.n) =
    Integral(M,P|E5) + Integral(M,(-F.n)|E5) by A4,A90,Th14;
    reconsider In = I.n as Real by A49;
A95: R_EAL (-1)*I.n = (-1)*In by EXTREAL1:13;
A96: -I.n = -In by SUPINF_2:3;
    P|E = P by A2,RELAT_1:97;
    then Integral(M,H.n) = Integral(M,P) + -I.n by A2,A49,A94,A92,A93
    ,A91,A95,A96;
    then IH.n = Integral(M,P) + -I.n by A77;
    hence IH.n = Integral(M,P) + I1.n by A88;
  end;
  then lim_inf IH = Integral(M,P) + lim_inf I1 by A24,Th10;
  then Integral(M,P)+ R_EAL(-1)*Integral(M,lim_sup F) <= Integral(M,P) + -
  lim_sup I by A2,A60,A78,A89,A85,A66,A67,A70,A86,A82,MESFUNC5:116;
  then R_EAL(-1)*Integral(M,lim_sup F) <= -lim_sup I + Integral(M,P) -
  Integral(M,P) by A74,A79, XXREAL_3:67;
  then R_EAL (-1)*Integral(M,lim_sup F) <= -lim_sup I + (Integral(M,P) -
  Integral(M,P)) by A74,A79, XXREAL_3:31;
  then
A97: R_EAL (-1)*Integral(M,lim_sup F) <= -lim_sup I + 0. by XXREAL_3:7;
  R_EAL (-1)*E1 = (-1)*e1 by EXTREAL1:13;
  then -Integral(M,lim_sup F) <= -lim_sup I by A97,A69,XXREAL_3:4;
  then
A98: Integral(M,lim_sup F) >= lim_sup I by XXREAL_3:40;
  now
A99: lim_inf I <= lim_sup I by RINFSUP2:39;
A100: dom lim F = dom(F.0) by MESFUNC8:def 10;
    assume
A101: for x be Element of X st x in E holds F#x is convergent;
A102: for x be Element of X st x in dom(lim F) holds (lim F).x = (lim_inf F).x
    proof
      let x be Element of X;
      assume
A103: x in dom(lim F);
      then F#x is convergent by A1,A101,A100;
      hence thesis by A103,MESFUNC8:14;
    end;
A104: for x be Element of X st x in dom lim F holds (lim F).x = (lim_sup F ).x
    proof
      let x be Element of X;
      assume
A105: x in dom lim F;
      then F#x is convergent by A1,A101,A100;
      hence thesis by A105,MESFUNC8:14;
    end;
A106: dom lim F = dom lim_sup F by A100,MESFUNC8:def 9;
    then
A107: lim F = lim_sup F by A104,PARTFUN1:34;
A108: dom lim F = dom lim_inf F by A100,MESFUNC8:def 8;
    then Integral(M,lim F) <= lim_inf I by A57,A102,PARTFUN1:34;
    then lim_sup I <= lim_inf I by A98,A107,XXREAL_0:2;
    then lim_inf I = lim_sup I by A99,XXREAL_0:1;
    hence
A109: I is convergent by RINFSUP2:40;
    then lim I = lim_sup I by RINFSUP2:41;
    then
A110: lim I <= Integral(M,lim F) by A98,A106,A104,PARTFUN1:34;
    lim I = lim_inf I by A109,RINFSUP2:41;
    then Integral(M,lim F) <= lim I by A57,A108,A102,PARTFUN1:34;
    hence lim I = Integral(M,lim F) by A110,XXREAL_0:1;
  end;
  hence thesis by A49,A57,A98;
end;
 
:: Lebesgue's Convergence theorem

theorem Th18:
  E = dom(F.0) & E = dom P & (for n be Nat holds F.n
  is_measurable_on E) & P is_integrable_on M & P is nonnegative & (for x be
  Element of X, n be Nat st x in E holds (|. F.n .|).x <= P.x) implies ex I be
  ExtREAL_sequence st (for n be Nat holds I.n = Integral(M,F.n)) & lim_inf I >=
  Integral(M,lim_inf F) & lim_sup I <= Integral(M,lim_sup F) & ( (for x be
Element of X st x in E holds F#x is convergent) implies I is convergent & lim I
  = Integral(M,lim F) )
proof
  assume that
A1: E = dom(F.0) and
A2: E = dom P and
A3: for n be Nat holds F.n is_measurable_on E and
A4: P is_integrable_on M and
A5: P is nonnegative and
A6: for x be Element of X, n be Nat st x in E holds (|. F.n .|).x <= P.x;
  for x be set st x in eq_dom(P,+infty) holds x in E by A2,MESFUNC1:def 16;
  then eq_dom(P,+infty) c= E by TARSKI:def 3;
  then
A7: eq_dom(P,+infty) = E /\ eq_dom(P,+infty) by XBOOLE_1:28;
  ex A be Element of S st A = dom P & P is_measurable_on A by A4,MESFUNC5:
  def 17;
  then reconsider E0 = eq_dom(P,+infty) as Element of S by A2,A7,MESFUNC1:37;
  reconsider E1 = E \ E0 as Element of S;
  deffunc F1(Nat) = (F.$1)|E1;
  consider F1 be Functional_Sequence of X,ExtREAL such that
A8: for n be Nat holds F1.n = F1(n) from SEQFUNC:sch 1;
A9: now
    let n be Nat;
    dom(F.n) = E by A1,MESFUNC8:def 2;
    then dom((F.n)|E1) = E1 by RELAT_1:91,XBOOLE_1:36;
    hence dom(F1.n) = E1 by A8;
  end;
  then
A10: E1 = dom(F1.0);
  now
    let n,m be Nat;
    thus dom(F1.n) = E1 by A9
      .= dom(F1.m) by A9;
  end;
  then reconsider F1 as with_the_same_dom Functional_Sequence of X,ExtREAL by
  MESFUNC8:def 2;
  set P1 = P|E1;
A11: P1 is nonnegative by A5,MESFUNC5:21;
A12: E1 c= E by XBOOLE_1:36;
A13: now
    let x be Element of X, n be Nat;
    assume
A14: x in E1;
    then
A15: P1.x = P.x by FUNCT_1:72;
    x in E by A12,A14;
    then x in dom(F.n) by A1,MESFUNC8:def 2;
    then x in dom(|. F.n .|) by MESFUNC1:def 10;
    then
A16: (|. F.n .|).x = |. (F.n).x .| by MESFUNC1:def 10;
    E1 = dom(F1.n) by A9;
    then
A17: E1 = dom(|. F1.n .|) by MESFUNC1:def 10;
    F1.n = (F.n)|E1 by A8;
    then (F1.n).x = (F.n).x by A14,FUNCT_1:72;
    then (|. F.n .|).x = (|. F1.n .|).x by A14,A16,A17,MESFUNC1:def 10;
    hence (|. F1.n .|).x <= P1.x by A6,A12,A14,A15;
  end;
A18: dom(lim F) = dom(F.0) by MESFUNC8:def 10;
  then
A19: dom(lim F) = dom(lim_inf F) by MESFUNC8:def 8;
A20: dom(lim_inf F) = E by A1,MESFUNC8:def 8;
A21: now
    let x be Element of X;
    assume
A22: x in dom(lim_inf F1);
    then
A23: x in E1 by A10,MESFUNC8:def 8;
    now
      let n be Element of NAT;
      ((F.n)|E1).x = (F.n).x by A23,FUNCT_1:72;
      then (F1.n).x = (F.n).x by A8;
      then (F1#x).n = (F.n).x by MESFUNC5:def 13;
      hence (F1#x).n = (F#x).n by MESFUNC5:def 13;
    end;
    then
A24: F1#x = F#x by FUNCT_2:113;
    E1 = dom(lim_inf F1) by A10,MESFUNC8:def 8;
    then lim_inf(F#x) = (lim_inf F).x by A12,A20,A22,MESFUNC8:def 8;
    then (lim_inf F1).x = (lim_inf F).x by A22,A24,MESFUNC8:def 8;
    hence ((lim_inf F)|(E\E0)).x = (lim_inf F1).x by A23,FUNCT_1:72;
  end;
  E1 = dom((lim_inf F)|(E\E0)) by A20,RELAT_1:91,XBOOLE_1:36;
  then dom((lim_inf F)|(E\E0)) = dom(lim_inf F1) by A10,MESFUNC8:def 8;
  then
A25: (lim_inf F)|(E\E0) = lim_inf F1 by A21,PARTFUN1:34;
A26: dom(lim_sup F) = E by A1,MESFUNC8:def 9;
A27: now
    let x be Element of X;
    assume
A28: x in dom(lim_sup F1);
    then
A29: x in E1 by A10,MESFUNC8:def 9;
    now
      let n be Element of NAT;
      ((F.n)|E1).x = (F.n).x by A29,FUNCT_1:72;
      then (F1.n).x = (F.n).x by A8;
      then (F1#x).n = (F.n).x by MESFUNC5:def 13;
      hence (F1#x).n = (F#x).n by MESFUNC5:def 13;
    end;
    then
A30: F1#x = F#x by FUNCT_2:113;
    E1 = dom(lim_sup F1) by A10,MESFUNC8:def 9;
    then lim_sup(F#x) = (lim_sup F).x by A12,A26,A28,MESFUNC8:def 9;
    then (lim_sup F1).x = (lim_sup F).x by A28,A30,MESFUNC8:def 9;
    hence ((lim_sup F)|(E\E0)).x = (lim_sup F1).x by A29,FUNCT_1:72;
  end;
  E1 = dom((lim_sup F)|(E\E0)) by A26,RELAT_1:91,XBOOLE_1:36;
  then dom((lim_sup F)|(E\E0)) = dom(lim_sup F1) by A10,MESFUNC8:def 9;
  then
A31: (lim_sup F)|(E\E0) = lim_sup F1 by A27,PARTFUN1:34;
A32: dom(P|E1) = E1 by A2,RELAT_1:91,XBOOLE_1:36;
A33: now
    assume eq_dom(P1,+infty) <> {};
    then consider x be set such that
A34: x in eq_dom(P1,+infty) by XBOOLE_0:def 1;
    reconsider x as Element of X by A34;
    P1.x = +infty by A34,MESFUNC1:def 16;
    then consider y be R_eal such that
A35: y = P1.x and
A36: +infty = y;
A37: x in E1 by A32,A34,MESFUNC1:def 16;
    then y = P.x by A35,FUNCT_1:72;
    then x in eq_dom(P,+infty) by A2,A12,A36,A37,MESFUNC1:def 16;
    hence contradiction by A37,XBOOLE_0:def 5;
  end;
A38: for n be Nat holds F1.n is_measurable_on E1
  proof
    let n be Nat;
    dom(F.n) = E by A1,MESFUNC8:def 2;
    then
A39: E1 = dom(F.n) /\ E1 by XBOOLE_1:28,36;
    F.n is_measurable_on E by A3;
    then F.n is_measurable_on E1 by MESFUNC1:34,XBOOLE_1:36;
    then (F.n)|E1 is_measurable_on E1 by A39,MESFUNC5:48;
    hence thesis by A8;
  end;
  P1 is_integrable_on M by A4,MESFUNC5:118;
  then consider I be ExtREAL_sequence such that
A40: for n be Nat holds I.n = Integral(M,F1.n) and
A41: lim_inf I >= Integral(M,lim_inf F1) and
A42: lim_sup I <= Integral(M,lim_sup F1) and
  (for x be Element of X st x in E1 holds F1#x is convergent) implies I is
  convergent & lim I = Integral(M,lim F1) by A32,A10,A11,A13,A38,A33,Lm1;
  P"{+infty} = E0 by MESFUNC5:36;
  then
A43: M.E0 = 0 by A4,MESFUNC5:111;
A44: for n be Nat holds I.n = Integral(M,F.n)
  proof
    let n be Nat;
A45: E = dom(F.n) by A1,MESFUNC8:def 2;
A46: F.n is_measurable_on E by A3;
    |. F.n .| is_integrable_on M by A1,A2,A3,A4,A6,Th17;
    then F.n is_integrable_on M by A45,A46,MESFUNC5:106;
    then
    Integral(M,F.n) = Integral(M,(F.n)|E0) + Integral(M,(F.n)|E1) by A45,
    MESFUNC5:105;
    then
A47: Integral(M,F.n) = 0. + Integral(M,(F.n)|E1) by A3,A43,A45,
    MESFUNC5:100;
    I.n = Integral(M,F1.n) by A40;
    then I.n = Integral(M,(F.n)|E1) by A8;
    hence thesis by A47,XXREAL_3:4;
  end;
  lim_inf F is_measurable_on E by A1,A3,MESFUNC8:24;
  then
A48: Integral(M,(lim_inf F)|(E\E0)) = Integral(M,lim_inf F) by A43,A20,
  MESFUNC5:101;
  lim_sup F is_measurable_on E by A1,A3,MESFUNC8:23;
  then
A49: Integral(M,(lim_sup F)|(E\E0)) = Integral(M,lim_sup F) by A43,A26,
  MESFUNC5:101;
A50: dom(lim F) = dom(lim_sup F) by A18,MESFUNC8:def 9;
  now
    assume
A51: for x be Element of X st x in E holds F#x is convergent;
A52: for x be Element of X st x in dom(lim F) holds (lim F).x = (lim_inf F).x
    proof
      let x be Element of X;
      assume
A53:  x in dom(lim F);
      then F#x is convergent by A1,A18,A51;
      hence thesis by A53,MESFUNC8:14;
    end;
    then
A54: lim F = lim_inf F by A19,PARTFUN1:34;
A55: lim_inf I <= lim_sup I by RINFSUP2:39;
A56: for x be Element of X st x in dom(lim F) holds (lim F).x = (lim_sup F).x
    proof
      let x be Element of X;
      assume
A57:  x in dom(lim F);
      then F#x is convergent by A1,A18,A51;
      hence thesis by A57,MESFUNC8:14;
    end;
    then lim F = lim_sup F by A50,PARTFUN1:34;
    then lim_sup I <= lim_inf I by A41,A42,A25,A31,A54,XXREAL_0:2;
    then lim_inf I = lim_sup I by A55,XXREAL_0:1;
    hence
A58: I is convergent by RINFSUP2:40;
    then lim I = lim_sup I by RINFSUP2:41;
    then
A59: lim I <= Integral(M,lim F) by A42,A49,A31,A50,A56,PARTFUN1:34;
    lim I = lim_inf I by A58,RINFSUP2:41;
    then Integral(M,lim F) <= lim I by A41,A48,A25,A19,A52,PARTFUN1:34;
    hence lim I = Integral(M,lim F) by A59,XXREAL_0:1;
  end;
  hence thesis by A41,A42,A44,A48,A49,A25,A31;
end;

theorem
  E = dom(F.0) & (for n holds F.n is nonnegative & F.n is_measurable_on
E) & (for x,n,m st x in E & n <= m holds (F.n).x >= (F.m).x) & Integral(M,(F.0)
  |E) < +infty implies ex I be ExtREAL_sequence st (for n be Nat holds I.n =
  Integral(M,F.n)) & I is convergent & lim I = Integral(M,lim F)
proof
  assume that
A1: E = dom(F.0) and
A2: for n holds F.n is nonnegative & F.n is_measurable_on E and
A3: for x,n,m st x in E & n <= m holds (F.n).x >= (F.m).x and
A4: Integral(M,(F.0)|E) < +infty;
A5: F.0 is nonnegative by A2;
A6: dom(F.0) = dom(|. F.0 .|) by MESFUNC1:def 10;
A7: for x be Element of X st x in dom(F.0) holds (F.0).x = |. F.0 .|.x
  proof
    let x be Element of X;
    0 <= (F.0).x by A5,SUPINF_2:70;
    then
A8: |.(F.0).x.| = (F.0).x by EXTREAL1:def 3;
    assume x in dom(F.0);
    hence thesis by A6,A8,MESFUNC1:def 10;
  end;
A9: F.0 is_measurable_on E by A2;
  then Integral(M,F.0) = integral+(M,F.0) by A1,A5,MESFUNC5:94;
  then integral+(M,F.0) < +infty by A1,A4,RELAT_1:97;
  then
A10: integral+(M,|. F.0 .|) < +infty by A6,A7,PARTFUN1:34;
A11: max+(F.0) is_measurable_on E by A2,MESFUNC2:27;
  for x be set st x in dom max-(F.0) holds 0. <= (max-(F.0)).x by
  MESFUNC2:15;
  then
A12: max-(F.0) is nonnegative by SUPINF_2:71;
A13: for x be Element of X, n be Nat st x in E holds (|. F.n .|).x <= (F.0). x
  proof
    let x be Element of X, n be Nat;
    assume
A14: x in E;
    F.n is nonnegative by A2;
    then 0 <= (F.n).x by SUPINF_2:70;
    then |.(F.n).x.| = (F.n).x by EXTREAL1:def 3;
    then
A15: |.(F.n).x.| <= (F.0).x by A3,A14;
    dom(F.n) = dom(|. F.n .|) by MESFUNC1:def 10;
    then x in dom(|. F.n .|) by A1,A14,MESFUNC8:def 2;
    hence thesis by A15,MESFUNC1:def 10;
  end;
A16: for x be Element of X st x in E holds F#x is convergent
  proof
    let x be Element of X;
    assume
A17: x in E;
    now
      let n,m be Element of NAT;
      assume m <= n;
      then (F.n).x <= (F.m).x by A3,A17;
      then (F#x).n <= (F.m).x by MESFUNC5:def 13;
      hence (F#x).n <= (F#x).m by MESFUNC5:def 13;
    end;
    then F#x is non-increasing by RINFSUP2:7;
    hence thesis by RINFSUP2:36;
  end;
A18: dom max+(F.0) = dom(F.0) by MESFUNC2:def 2;
  then
A19: max+(F.0)|E = max+(F.0) by A1,RELAT_1:97;
  for x be set st x in dom max+(F.0) holds 0. <= (max+(F.0)).x by
  MESFUNC2:14;
  then
A20: max+(F.0) is nonnegative by SUPINF_2:71;
  then
A21: dom(max+(F.0) + max-(F.0)) = dom max+(F.0) /\ dom max-(F.0) by A12,
  MESFUNC5:28;
A22: dom max-(F.0) = dom(F.0) by MESFUNC2:def 3;
  then
A23: max-(F.0)|E = max-(F.0) by A1,RELAT_1:97;
  max-(F.0) is_measurable_on E by A1,A2,MESFUNC2:28;
  then ex C be Element of S st C = dom(max+(F.0) + max-(F.0)) & integral+(M,
max+(F.0) + max-(F.0)) = integral+(M,max+(F.0)|C) + integral+(M,max-(F.0)|C) by
  A1,A18,A22,A20,A12,A11,MESFUNC5:84;
  then
A24: integral+(M,max+(F.0)) + integral+(M,max-(F.0)) < +infty by A1,A18,A22,
  A21,A19,A23,A10,MESFUNC2:26;
  0 <= integral+(M,max-(F.0)) by A1,A9,A22,A12,MESFUNC2:28,MESFUNC5:85;
  then integral+(M,max+(F.0)) <> +infty by A24,XXREAL_3:def 2;
  then
A25: integral+(M,max+(F.0)) < +infty by XXREAL_0:4;
  0 <= integral+(M,max+(F.0)) by A1,A9,A18,A20,MESFUNC2:27,MESFUNC5:85;
  then integral+(M,max-(F.0)) <> +infty by A24,XXREAL_3:def 2;
  then integral+(M,max-(F.0)) < +infty by XXREAL_0:4;
  then F.0 is_integrable_on M by A1,A9,A25,MESFUNC5:def 17;
  then
  ex I be ExtREAL_sequence st ( for n be Nat holds I.n = Integral(M,F.n))&
  lim_inf I >= Integral(M,lim_inf F) & lim_sup I <= Integral( M,lim_sup F) &( (
  for x be Element of X st x in E holds F#x is convergent) implies I is
  convergent & lim I = Integral(M,lim F)) by A1,A2,A5,A13,Th18;
  hence thesis by A16;
end;

definition
  let X be set, F be Functional_Sequence of X,ExtREAL;
  attr F is uniformly_bounded means
  :Def1:
  ex K be real number st for n be Nat
  , x be set st x in dom(F.0) holds |. (F.n).x .| <= K;
end;
 
:: Lebesgue's Bounded Convergence Theorem

theorem
  M.E < +infty & E = dom(F.0) & (for n be Nat holds F.n is_measurable_on
  E) & F is uniformly_bounded & (for x be Element of X st x in E holds F#x is
  convergent) implies (for n be Nat holds F.n is_integrable_on M) & lim F
  is_integrable_on M & ex I be ExtREAL_sequence st (for n be Nat holds I.n =
  Integral(M,F.n)) & I is convergent & lim I = Integral(M,lim F)
proof
  assume that
A1: M.E < +infty and
A2: E = dom(F.0) and
A3: for n be Nat holds F.n is_measurable_on E and
A4: F is uniformly_bounded and
A5: for x be Element of X st x in E holds F#x is convergent;
  consider K1 be real number such that
A6: for n be Nat, x be set st x in dom(F.0) holds |. (F.n).x .| <= K1 by
  A4,Def1;
  set K = max(K1,1);
  K in REAL by XREAL_0:def 1;
  then consider h be PartFunc of X,ExtREAL such that
A7: h is_simple_func_in S and
A8: dom h = E and
A9: for x be set st x in E holds h.x = K by MESFUNC5:47,NUMBERS:31;
A10: dom h = dom |.h.| by MESFUNC1:def 10;
A11: K > 0 by XXREAL_0:30;
  then
A12: R_EAL K * +infty = +infty by XXREAL_3:def 5;
  for x be set st x in E holds h.x >= 0. by A11,A9;
  then
A13: h is nonnegative by A8,SUPINF_2:71;
  then
A14: Integral(M,h) = integral'(M,h) by A7,MESFUNC5:95;
  K is Real by XREAL_0:def 1;
  then
A15: integral'(M,h) = R_EAL K * M.(dom h) by A11,A8,A9,MESFUNC5:110;
A16: for x be Element of X st x in dom h holds h.x = |.h.|.x
  proof
    let x be Element of X;
    assume x in dom h;
    then
A17: x in dom |.h.| by MESFUNC1:def 10;
    0 <= h.x by A13,SUPINF_2:70;
    then |. h.x .| = h.x by EXTREAL1:def 3;
    hence thesis by A17,MESFUNC1:def 10;
  end;
  Integral(M,h) = integral+(M,h) by A7,A13,MESFUNC5:95;
  then integral+(M,h) < +infty by A1,A11,A8,A15,A12,A14,XXREAL_3:83;
  then
A18: integral+(M,|.h.|) < +infty by A10,A16,PARTFUN1:34;
A19: dom max+h = dom h by MESFUNC2:def 2;
  then
A20: max+h|E = max+h by A8,RELAT_1:97;
A21: max+h is_measurable_on E by A7,MESFUNC2:27,37;
  for x be set st x in dom max-h holds 0. <= (max-h).x by MESFUNC2:15;
  then
A22: max-h is nonnegative by SUPINF_2:71;
A23: K >= K1 by XXREAL_0:25;
A24: for n be Nat, x be set st x in dom(F.0) holds |. (F.n).x .| <= K
  proof
    let n be Nat, x be set;
    assume x in dom(F.0);
    then |. (F.n).x .| <= K1 by A6;
    hence thesis by A23,XXREAL_0:2;
  end;
A25: for x be Element of X, n be Nat st x in E holds (|. F.n .|).x <= h.x
  proof
    let x be Element of X, n be Nat;
    assume
A26: x in E;
    dom(F.n) = dom(|.(F.n).|) by MESFUNC1:def 10;
    then x in dom(|.(F.n).|) by A2,A26,MESFUNC8:def 2;
    then
A27: |. F.n .|.x = |. (F.n).x .| by MESFUNC1:def 10;
    |. (F.n).x .| <= K by A2,A24,A26;
    hence thesis by A9,A26,A27;
  end;
  for x be set st x in dom max+h holds 0. <= (max+h).x by MESFUNC2:14;
  then
A28: max+h is nonnegative by SUPINF_2:71;
  then
A29: dom(max+h + max-h) = dom max+h /\ dom max-h by A22,MESFUNC5:28;
A30: dom max-h = dom h by MESFUNC2:def 3;
  then
A31: max-h|E = max-h by A8,RELAT_1:97;
  max-h is_measurable_on E by A7,A8,MESFUNC2:28,37;
  then ex C be Element of S st C = dom(max+h + max-h) & integral+(M,max+h +
  max-h) = integral+(M,max+h|C) + integral+(M,max-h|C) by A8,A19,A30,A21,A28
  ,A22,MESFUNC5:84;
  then
A32: integral+(M,max+h) + integral+(M,max-h) < +infty by A8,A19,A30,A29,
  A20,A31,A18,MESFUNC2:26;
A33: for x be Element of X st x in dom(lim F) holds (lim F).x = (lim_inf F). x
  proof
    let x be Element of X;
    assume
A34: x in dom(lim F);
    then x in E by A2,MESFUNC8:def 10;
    then F#x is convergent by A5;
    hence thesis by A34,MESFUNC8:14;
  end;
A35: dom(lim_inf F) = dom (F.0) by MESFUNC8:def 8;
A36: h is_measurable_on E by A7,MESFUNC2:37;
  then 0 <= integral+(M,max-h) by A8,A30,A22,MESFUNC2:28,MESFUNC5:85;
  then integral+(M,max+h) <> +infty by A32,XXREAL_3:def 2;
  then
A37: integral+(M,max+h) < +infty by XXREAL_0:4;
  0 <= integral+(M,max+h) by A8,A36,A19,A28,MESFUNC2:27,MESFUNC5:85;
  then integral+(M,max-h) <> +infty by A32,XXREAL_3:def 2;
  then integral+(M,max-h) < +infty by XXREAL_0:4;
  then
A38: h is_integrable_on M by A8,A36,A37,MESFUNC5:def 17;
  then
A39: ex I be ExtREAL_sequence st (for n be Nat holds I.n = Integral(M,F.n))
& lim_inf I >= Integral(M,lim_inf F) & lim_sup I <= Integral(M,lim_sup F) & ( (
  for x be Element of X st x in E holds F#x is convergent) implies I is
  convergent & lim I = Integral(M,lim F) ) by A2,A3,A8,A13,A25,Th18;
A40: now
    let n be Nat;
A41: E = dom(F.n) by A2,MESFUNC8:def 2;
A42: F.n is_measurable_on E by A3;
    |. F.n .| is_integrable_on M by A2,A3,A8,A38,A25,Th17;
    hence F.n is_integrable_on M by A41,A42,MESFUNC5:106;
  end;
A43: |. lim_inf F .| is_integrable_on M by A2,A3,A8,A38,A25,Th17;
  dom(lim F) = dom (F.0) by MESFUNC8:def 10;
  then
A44: lim F = lim_inf F by A35,A33,PARTFUN1:34;
  then lim F is_measurable_on E by A2,A3,MESFUNC8:24;
  hence thesis by A2,A5,A43,A40,A35,A44,A39,MESFUNC5:106;
end;

definition
  let X be set, F be Functional_Sequence of X,ExtREAL, f be PartFunc of X,
  ExtREAL;
  pred F is_uniformly_convergent_to f means
  :Def2:
  F is with_the_same_dom &
  dom(F.0) = dom f & for e be real number st e>0 ex N be Nat st for n be Nat, x
  be set st n >= N & x in dom(F.0) holds |. (F.n).x - f.x .| < e;
end;

theorem Th21:
  F1 is_uniformly_convergent_to f implies for x be Element of X st
  x in dom(F1.0) holds F1#x is convergent & lim(F1#x) = f.x
proof
  assume
A1: F1 is_uniformly_convergent_to f;
  let x be Element of X;
  assume
A2: x in dom(F1.0);
  per cases by XXREAL_0:14;
  suppose
    f.x in REAL;
    then reconsider g=f.x as real number;
A3: now
      let e be real number;
      assume 0 < e;
      then consider N be Nat such that
A4:   for n be Nat, x be set st n >= N & x in dom(F1.0) holds |. (F1.n
      ).x - f.x .| < e by A1,Def2;
      take N;
      hereby
        let m be Nat;
        assume N <= m;
        then |. (F1.m).x - f.x .| < e by A2,A4;
        hence |. (F1#x).m - R_EAL g .| < e by MESFUNC5:def 13;
      end;
    end;
    then
A5: F1#x is convergent_to_finite_number by MESFUNC5:def 8;
    then F1#x is convergent by MESFUNC5:def 11;
    hence thesis by A3,A5,MESFUNC5:def 12;
  end;
  suppose
A6: f.x = +infty;
    now
      let e be real number;
      assume 0 < e;
      then consider N be Nat such that
A7:   for n be Nat, x be set st n >= N & x in dom(F1.0) holds |. (F1.
      n).x - f.x .| < e by A1,Def2;
      take N;
      hereby
        let n be Nat;
        assume n >= N;
        then |. (F1.n).x - f.x .| < e by A2,A7;
        then
A8:     |. -( (F1.n).x - f.x ) .| < e by EXTREAL2:66;
        (F1.n).x = (F1#x).n by MESFUNC5:def 13;
        then -( (F1#x).n - f.x ) < R_EAL e by A8,EXTREAL2:58;
        then f.x - (F1#x).n < R_EAL e by XXREAL_3:27;
        then (F1#x).n = +infty by A6,XXREAL_3:65;
        hence e <= (F1#x).n by XXREAL_0:3;
      end;
    end;
    then
A9: F1#x is convergent_to_+infty by MESFUNC5:def 9;
    then F1#x is convergent by MESFUNC5:def 11;
    hence thesis by A6,A9,MESFUNC5:def 12;
  end;
  suppose
A10: f.x = -infty;
    now
      let e be real number;
      assume e < 0;
      then -0 < -e by XREAL_1:26;
      then consider N be Nat such that
A11:  for n be Nat, x be set st n >= N & x in dom(F1.0) holds |. (F1.
      n).x - f.x .| < -e by A1,Def2;
      take N;
      hereby
        let n be Nat;
        assume n >= N;
        then
A12:    |. (F1.n).x - f.x .| < -e by A2,A11;
        (F1.n).x = (F1#x).n by MESFUNC5:def 13;
        then (F1#x).n - f.x < R_EAL -e by A12,EXTREAL2:58;
        then (F1#x).n = -infty by A10,XXREAL_3:65;
        hence e >= (F1#x).n by XXREAL_0:5;
      end;
    end;
    then
A13: F1#x is convergent_to_-infty by MESFUNC5:def 10;
    then F1#x is convergent by MESFUNC5:def 11;
    hence thesis by A10,A13,MESFUNC5:def 12;
  end;
end;

theorem
  M.E < +infty & E = dom(F.0) & (for n be Nat holds F.n is_integrable_on
  M) & F is_uniformly_convergent_to f implies f is_integrable_on M & ex I be
  ExtREAL_sequence st (for n be Nat holds I.n = Integral(M,F.n)) & I is
  convergent & lim I = Integral(M,f)
proof
  assume that
A1: M.E < +infty and
A2: E = dom(F.0) and
A3: for n be Nat holds F.n is_integrable_on M and
A4: F is_uniformly_convergent_to f;
  consider N be Nat such that
A5: for n be Nat, x be set st n >= N & x in dom(F.0) holds |. (F.n).x -
  f.x .| < 1/2 by A4,Def2;
  reconsider N1=N as Element of NAT by ORDINAL1:def 13;
  consider h be PartFunc of X,ExtREAL such that
A6: h is_simple_func_in S and
A7: dom h = E and
A8: for x be set st x in E holds h.x=1. by MESFUNC5:47;
A9: max-h is_measurable_on E by A6,A7,MESFUNC2:28,37;
  for x be set st x in E holds h.x >= 0. by A8;
  then
A10: h is nonnegative by A7,SUPINF_2:71;
  then
A11: Integral(M,h) = integral'(M,h) by A6,MESFUNC5:95;
  set AFN = |. F.N .|;
A12: dom(F.N) = dom AFN by MESFUNC1:def 10;
  now
    let x be set;
    assume x in dom AFN;
    then AFN.x = |. (F.N).x .| by MESFUNC1:def 10;
    hence 0 <= AFN.x by EXTREAL2:51;
  end;
  then
A13: AFN is nonnegative by SUPINF_2:71;
  then
A14: dom(AFN + h) = dom AFN /\ dom h by A10,MESFUNC5:28;
A15: for x be set st x in dom(F.0) holds |. (F.N).x - f.x .| < 1/2 by A5;
A16: now
    let x be set, n be Nat;
    assume that
A17: n >= N and
A18: x in dom(F.0);
A19: |. (F.n).x - f.x .| < 1/2 by A5,A17,A18;
A20: |. (F.N).x - f.x .| < 1/2 by A5,A18;
A21: now
A22:  |. (F.n).x - f.x .| < 1. by A19,XXREAL_0:2;
      then
A23:  (F.n).x - f.x < 1. by EXTREAL2:58;
A24:  |. (F.N).x - f.x .| < 1. by A20,XXREAL_0:2;
      then
A25:  -1. < (F.N).x - f.x by EXTREAL2:58;
A26:  (F.N).x - f.x < 1. by A24,EXTREAL2:58;
      assume
A27:  f.x = +infty or f.x = -infty;
      -1. < (F.n).x - f.x by A22,EXTREAL2:58;
      then (F.n).x = +infty & (F.N).x = +infty or (F.n).x = -infty & (F.N).x
      = -infty by A27,A25,A23,A26, XXREAL_3:64,65;
      hence |. (F.n).x .| <= |. (F.N).x .| + 1. by EXTREAL2:67,XXREAL_3:63;
    end;
    dom(AFN + h) = dom AFN /\ dom h by A10,A13,MESFUNC5:28;
    then dom(AFN + h) = E /\ E by A2,A7,A12,MESFUNC8:def 2;
    then AFN.x + h.x = (AFN + h).x by A2,A18,MESFUNC1:def 3;
    then
A28: AFN.x + 1. = (AFN + h).x by A2,A8,A18;
    dom(F.n) = E by A2,MESFUNC8:def 2;
    then
A29: x in dom |. F.n .| by A2,A18,MESFUNC1:def 10;
A30: now
A31:  0 <= |. (F.n).x - f.x .| by EXTREAL2:51;
      |. (F.n).x - f.x .| < +infty by A19,XXREAL_0:2,9;
      then reconsider a = |. (F.n).x - f.x .| as Real by A31,XXREAL_0:14;
      |. f.x - (F.N).x .| < 1/2 by A20,MESFUNC5:1;
      then
A32:  |. f.x - (F.N).x .| < +infty by XXREAL_0:2,9;
      0 <= |. f.x - (F.N).x .| by EXTREAL2:51;
      then reconsider b = |. f.x - (F.N).x .| as Real by A32,XXREAL_0:14;
      assume
A33:  f.x in REAL;
A34:  now
        assume (F.N).x = +infty or (F.N).x = -infty;
        then (F.N).x - f.x = +infty or (F.N).x - f.x = -infty by A33,
        XXREAL_3:13,14;
        then 1. < |. (F.N).x - f.x .| by EXTREAL2:67,XXREAL_0:9;
        hence contradiction by A15,A18,XXREAL_0:2;
      end;
A35:  now
        assume (F.n).x = +infty or (F.n).x = -infty;
        then (F.n).x - f.x = +infty or (F.n).x - f.x = -infty by A33,
        XXREAL_3:13,14;
        hence contradiction by A19,EXTREAL2:67,XXREAL_0:9;
      end;
      then (F.n).x in REAL by XXREAL_0:14;
      then
A36:  |. (F.n).x .| - |. (F.N).x .| <= |. (F.n).x - (F.N).x .| by
      EXTREAL2:68;
      b <= 1/2 by A20,MESFUNC5:1;
      then a + b < 1/2 + 1/2 by A19,XREAL_1:10;
      then
A37:  |. (F.n).x - f.x .| + |. f.x - (F.N).x .| < 1 by SUPINF_2:1;
      -f.x + f.x = 0. by XXREAL_3:7;
      then (F.n).x - (F.N).x = (F.n).x + (-f.x + f.x) - (F.N).x by
      XXREAL_3:4;
      then (F.n).x - (F.N).x = (F.n).x + -f.x + f.x - (F.N).x by A33,A35,
      XXREAL_3:30;
      then (F.n).x - (F.N).x = (F.n).x - f.x + (f.x - (F.N).x) by A33,A34,
      XXREAL_3:31;
      then |. (F.n).x - (F.N).x .| <= |. (F.n).x - f.x .| + |. f.x - (F.N).x
      .| by EXTREAL2:61;
      then |. (F.n).x - (F.N).x .| < 1 by A37,XXREAL_0:2;
      then |. (F.n).x .| - |. (F.N).x .| <= 1. by A36,XXREAL_0:2;
      hence |. (F.n).x .| <= |. (F.N).x .| + 1. by XXREAL_3:63;
    end;
    x in dom AFN by A12,A18,MESFUNC8:def 2;
    then |. (F.n).x .| <= AFN.x + 1. by
    A30,A21,MESFUNC1:def 10,XXREAL_0:
    14;
    hence (|. F.n .|).x <= (AFN + h).x by A28,A29,MESFUNC1:def 10;
  end;
A38: for x be Element of X, n be Nat st x in E holds (|.(F^\N1).n.|).x <= (
  AFN + h).x
  proof
    let x be Element of X, n be Nat;
A39: (F^\N1).n = F.(n+N) by NAT_1:def 3;
    assume x in E;
    hence thesis by A2,A16,A39,NAT_1:11;
  end;
A40: max+h is_measurable_on E by A6,MESFUNC2:27,37;
  for x be set st x in dom max-h holds 0. <= (max-h).x by MESFUNC2:15;
  then
A41: max-h is nonnegative by SUPINF_2:71;
A42: for n be Nat holds F.n is_measurable_on E
  proof
    let n be Nat;
    F.n is_integrable_on M by A3;
    then ex A be Element of S st A = dom(F.n) & (F.n) is_measurable_on A by
    MESFUNC5:def 17;
    hence thesis by A2,MESFUNC8:def 2;
  end;
  then
A43: F.N is_measurable_on E;
  (F^\N1).0 = F.(0+N) by NAT_1:def 3;
  then
A44: dom((F^\N1).0) = E by A2,MESFUNC8:def 2;
A45: now
    let x be Element of X;
    assume x in dom f;
    then
A46: x in dom(F.0) by A4,Def2;
    then
A47: x in dom(lim F) by MESFUNC8:def 10;
    lim(F#x) = f.x by A4,A46,Th21;
    hence (lim F).x = f.x by A47,MESFUNC8:def 10;
  end;
  dom f = dom(F.0) by A4,Def2;
  then dom(lim F) = dom f by MESFUNC8:def 10;
  then
A48: lim F = f by A45,PARTFUN1:34;
A49: F.N is_integrable_on M by A3;
  E = dom(F.N) by A2,MESFUNC8:def 2;
  then
A50: AFN is_integrable_on M by A49,A43,MESFUNC5:106;
  deffunc I(Element of NAT) = Integral(M,F.$1);
A51: R_EAL 1 * +infty = +infty by XXREAL_3:def 5;
A52: now
    let x be Element of X;
    assume x in dom h;
    then
A53: x in dom(|.h.|) by MESFUNC1:def 10;
    0 <= h.x by A10,SUPINF_2:70;
    then |. h.x .| = h.x by EXTREAL1:def 3;
    hence |.h.|.x = h.x by A53,MESFUNC1:def 10;
  end;
  dom h = dom(|.h.|) by MESFUNC1:def 10;
  then
A54: h = |.h.| by A52,PARTFUN1:34;
  Integral(M,h) = integral+(M,h) by A6,A10,MESFUNC5:95;
  then integral+(M,h) = R_EAL 1 * M.(dom h) by A7,A8,A11,MESFUNC5:110;
  then
A55: integral+(M,|.h.|) < +infty by A1,A7,A54,A51,XXREAL_3:83;
A56: for n be Nat holds (F^\N1).n is_measurable_on E
  proof
    let n be Nat;
    (F^\N1).n = F.(n+N) by NAT_1:def 3;
    hence thesis by A42;
  end;
A57: for x be Element of X st x in E holds (F^\N1)#x is convergent & lim(F#x
  ) = lim((F^\N1)#x)
  proof
    let x be Element of X;
A58: now
      let n be Element of NAT;
      ((F^\N1)#x).n = ((F^\N1).n).x by MESFUNC5:def 13;
      then ((F^\N1)#x).n = (F.(n+N)).x by NAT_1:def 3;
      then ((F^\N1)#x).n = (F#x).(n+N) by MESFUNC5:def 13;
      hence ((F^\N1)#x).n = ((F#x)^\N1).n by NAT_1:def 3;
    end;
    assume x in E;
    then
A59: F#x is convergent by A2,A4,Th21;
    then (F#x)^\N1 is convergent by RINFSUP2:21;
    hence (F^\N1)#x is convergent by A58,FUNCT_2:113;
    lim(F#x) = lim((F#x)^\N1) by A59,RINFSUP2:21;
    hence thesis by A58,FUNCT_2:113;
  end;
  then for x be Element of X st x in E holds (F^\N1)#x is convergent;
  then
A60: lim(F^\N1) is_measurable_on E by A56,A44,MESFUNC8:25;
  dom(lim(F^\N1)) = E by A44,MESFUNC8:def 10;
  then
A61: dom(lim F) = dom(lim(F^\N1)) by A2,MESFUNC8:def 10;
A62: now
    let x be Element of X;
    assume
A63: x in dom(lim F);
    then x in E by A2,MESFUNC8:def 10;
    then
A64: lim(F#x) = lim((F^\N1)#x) by A57;
    lim((F^\N1)#x) = (lim (F^\N1)).x by A61,A63,MESFUNC8:def 10;
    hence (lim F).x = (lim (F^\N1)).x by A63,A64,MESFUNC8:def 10;
  end;
A65: dom max-h = dom h by MESFUNC2:def 3;
  then
A66: max-h|E = max-h by A7,RELAT_1:97;
A67: dom max+h = dom h by MESFUNC2:def 2;
  then
A68: max+h|E = max+h by A7,RELAT_1:97;
  for x be set st x in dom max+h holds 0. <= (max+h).x by MESFUNC2:14;
  then
A69: max+h is nonnegative by SUPINF_2:71;
  then dom(max+h + max-h) = dom max+h /\ dom max-h by A41,MESFUNC5:28;
  then
  ex C be Element of S st C = E & integral+(M,max+h + max-h) = integral+(
  M,max+h|C) + integral+(M,max-h|C) by A7,A40,A9,A69,A41,A67,A65,MESFUNC5:
  84;
  then
A70: integral+(M,|.h.|) = integral+(M,max+h) + integral+(M,max-h) by A68,A66,
  MESFUNC2:26;
A71: h is_measurable_on E by A6,MESFUNC2:37;
  then 0 <= integral+(M,max-h) by A7,A41,A65,MESFUNC2:28,MESFUNC5:85;
  then integral+(M,max+h) <> +infty by A70,A55,XXREAL_3:def 2;
  then
A72: integral+(M,max+h) < +infty by XXREAL_0:4;
  0 <= integral+(M,max+h) by A7,A71,A69,A67,MESFUNC2:27,MESFUNC5:85;
  then integral+(M,max-h) <> +infty by A70,A55,XXREAL_3:def 2;
  then integral+(M,max-h) < +infty by XXREAL_0:4;
  then h is_integrable_on M by A7,A71,A72,MESFUNC5:def 17;
  then
A73: AFN + h is_integrable_on M by A50,MESFUNC5:114;
A74: E = dom AFN by A2,A12,MESFUNC8:def 2;
  then
A75: |. lim_inf(F^\N1) .| is_integrable_on M by A7,A73,A14,A38,A56,
  A44,Th17;
  AFN + h is nonnegative by A10,A13,MESFUNC5:28;
  then consider J be ExtREAL_sequence such that
A76: for n be Nat holds J.n = Integral(M,(F^\N1).n) and
  lim_inf J >= Integral(M,lim_inf (F^\N1)) and
  lim_sup J <= Integral(M,lim_sup (F^\N1)) and
A77: (for x be Element of X st x in E holds (F^\N1)#x is convergent)
  implies J is convergent & lim J = Integral(M,lim (F^\N1)) by A7,A73,A14,
  A74,A38,A56,A44,Th18;
  consider I be Function of NAT,ExtREAL such that
A78: for n be Element of NAT holds I.n = I(n) from FUNCT_2:sch 4;
  reconsider I as ExtREAL_sequence;
A79: dom lim_inf(F^\N1) = dom((F^\N1).0) by MESFUNC8:def 8;
A80: now
    let x be Element of X;
    assume
A81: x in dom(lim(F^\N1));
    then x in E by A44,MESFUNC8:def 10;
    then (F^\N1)#x is convergent by A57;
    hence (lim(F^\N1)).x = (lim_inf(F^\N1)).x by A81,MESFUNC8:14;
  end;
  dom lim(F^\N1) = dom((F^\N1).0) by MESFUNC8:def 10;
  then lim(F^\N1) = lim_inf(F^\N1) by A79,A80,PARTFUN1:34;
  then
A82: lim(F^\N1) is_integrable_on M by A44,A75,A79,A60,MESFUNC5:106;
A83: for n be Nat holds I.n = Integral(M,F.n)
  proof
    let n be Nat;
    n is Element of NAT by ORDINAL1:def 13;
    hence thesis by A78;
  end;
  now
    let n be Element of NAT;
A84: (F^\N1).n = F.(n+N) by NAT_1:def 3;
A85: (I^\N1).n = I.(n+N) by NAT_1:def 3;
    J.n = Integral(M,(F^\N1).n) by A76;
    hence J.n = (I^\N1).n by A78,A84,A85;
  end;
  then
A86: J = I^\N1 by FUNCT_2:113;
  then
A87: I is convergent by A57,A77,RINFSUP2:17;
  lim I = lim J by A57,A77,A86,RINFSUP2:17;
  then lim I = Integral(M,lim F) by A57,A61,A62,A77,PARTFUN1:34;
  hence thesis by A48,A61,A62,A82,A83,A87,PARTFUN1:34;
end;
