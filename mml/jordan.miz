:: Jordan's Curve Theorem
::  by Artur Korni{\l}owicz
::
:: Received September 15, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies ARYTM, PRE_TOPC, EUCLID, TOPREAL1, RELAT_1, ARYTM_1, TOPS_1,
      SUBSET_1, JORDAN2C, METRIC_1, BOOLE, TOPREAL2, CONNSP_1, PSCOMP_1,
      TOPS_2, MCART_1, SQUARE_1, ARYTM_3, SPPOL_1, ASYMPT_0, PCOMPS_1, FUNCT_1,
      FUNCT_5, JORDAN1, JGRAPH_6, JORDAN6, JORDAN19, JORDAN21, JORDAN24,
      LATTICES, CONNSP_3, BORSUK_2, BORSUK_6, GRAPH_1, JORDAN5C, TOPGRP_1,
      COMPLEX1, BORSUK_1, ORDINAL2, TOPREALA, RELAT_2, RCOMP_1, TOPMETR,
      COMPTS_1, REALSET1, JORDAN1A, TOPREAL4, FUNCOP_1, TARSKI, JORDAN3, ABIAN,
      ZF_LANG, TREAL_1, SEQ_1, XREAL_0, COLLSP, CONNSP_2, SGRAPH1, FUNCT_4,
      SETFAM_1, PARTFUN3, PARTFUN1, KNASTER, FINSEQ_1, BROUWER, TOPREALB,
      ABSVALUE, TERMORD, PROB_1, JORDAN;
 notations TARSKI, XBOOLE_0, SUBSET_1, ZFMISC_1, ORDINAL1, SQUARE_1, NAT_1,
      MCART_1, RELSET_1, FUNCT_1, PARTFUN1, FUNCT_2, FUNCT_3, FUNCT_4,
      PSCOMP_1, NUMBERS, XCMPLX_0, XREAL_0, REAL_1, FINSEQ_1, COMPLEX1,
      RCOMP_1, STRUCT_0, PRE_TOPC, TOPS_1, BORSUK_1, TOPS_2, COMPTS_1, TREAL_1,
      CONNSP_1, CONNSP_2, CONNSP_3, TEX_2, METRIC_1, PCOMPS_1, TBSP_1, EUCLID,
      TOPRNS_1, BORSUK_2, TOPREAL1, TOPREAL2, TOPMETR, SPPOL_1, JORDAN1,
      JORDAN2C, JORDAN5C, TOPREAL4, JORDAN6, JGRAPH_6, JORDAN1A, GOBRD14,
      TOPREAL9, BORSUK_6, TOPREALA, JORDAN19, JORDAN21, TOPGRP_1, JORDAN24,
      ABIAN, PARTFUN3, TOPREALB, BROUWER, XXREAL_0;
 constructors TOPREAL1, COMPTS_1, TBSP_1, JORDAN2C, TOPREAL9, CONNSP_1, TOPS_1,
      TOPRNS_1, MONOID_0, TOPREALA, JORDAN1, JGRAPH_6, SPPOL_2, GOBRD14,
      JORDAN6, JORDAN19, JORDAN21, CONNSP_3, JORDAN5C, SPPOL_1, JCT_MISC,
      TEX_2, JORDAN1A, TOPREAL4, YELLOW_8, REALSET2, JORDAN24, TOPGRP_1, ABIAN,
      TREAL_1, PARTFUN3, BROUWER, BORSUK_3, LIMFUNC1, WAYBEL_3, COMPLEX1,
      TSEP_1, XXREAL_0;
 registrations XREAL_0, RELSET_1, SUBSET_1, STRUCT_0, EUCLID, PRE_TOPC,
      BORSUK_1, BORSUK_2, TOPREAL9, TOPS_1, NAT_1, TOPREALA, MEMBERED,
      JORDAN21, JORDAN22, JORDAN2C, TOPGEN_3, TOPREAL6, GOBRD14, SPPOL_2,
      YELLOW13, RCOMP_3, JORDAN5A, PSCOMP_1, SQUARE_1, JORDAN1A, FUNCT_1,
      JGRAPH_8, TOPGRP_1, WAYBEL_2, PARTFUN3, METRIC_1, REALSET1, BORSUK_4,
      BROUWER, WAYBEL_3, FUNCT_2, TOPREALB, TOPMETR, SEQ_1, ISOMICHI, TOPALG_5,
      JORDAN1K, TEX_4, SPRECT_4;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM, REAL;
 definitions TARSKI, XBOOLE_0, BORSUK_2, JORDAN1, SPPOL_1, JORDAN2C, JORDAN21,
      JORDAN24, TOPREAL2, CONNSP_1, BORSUK_1, PSCOMP_1, ABIAN, XREAL_0,
      SQUARE_1, JGRAPH_6, TOPS_1;
 definitions EUCLID;
 theorems JORDAN6, PRE_TOPC, XBOOLE_0, EUCLID, TARSKI, SQUARE_1, CONNSP_1,
      JORDAN2C, JGRAPH_6, XREAL_1, JORDAN1K, GOBRD14, BORSUK_2, JORDAN21,
      JORDAN22, TOPREAL1, TOPREAL3, SPPOL_1, SUBSET_1, TOPMETR, TOPREAL9,
      TOPS_1, SPRECT_3, TOPRNS_1, TOPS_2, NAT_1, JORDAN1, TOPREALA, BORSUK_1,
      FUNCT_2, RCOMP_3, TOPREAL5, RELAT_1, GOBOARD7, FUNCT_1, BORSUK_6,
      JORDAN5A, XBOOLE_1, ZFMISC_1, CONNSP_3, JORDAN1H, TOPALG_2, GOBOARD9,
      SPPOL_2, JORDAN1J, PSCOMP_1, JORDAN1C, JORDAN1I, JORDAN1A, JORDAN15,
      JORDAN18, TOPREAL4, TSEP_1, STRUCT_0, TOPALG_3, JGRAPH_8, COMPTS_1,
      SEQ_4, FUNCOP_1, WEIERSTR, REAL_1, JORDAN5C, JORDAN20, JORDAN16, XREAL_0,
      KURATO_2, XCMPLX_1, MEMBERED, JGRAPH_5, SPRECT_1, JORDAN1E, JORDAN24,
      TOPGRP_1, TOPREAL2, RCOMP_1, BORSUK_4, BROUWER, ABIAN, TOPREAL6,
      ENUMSET1, METRIC_1, PCOMPS_1, TOPREALB, JORDAN19, ABSVALUE, TEX_2,
      MCART_1, BORSUK_5, METRIC_6, COMPLEX1, JGRAPH_2, SEQ_2, TIETZE, CONNSP_2,
      GRCAT_1, WAYBEL29, FUNCT_4, TREAL_1, JGRAPH_1, SEQ_1, PARTFUN3, RFUNCT_1,
      YELLOW12, XCMPLX_0, FUNCT_3, JORDAN5B, TOPALG_5;
 schemes FUNCT_2;

begin :: Preliminaries

:: I would like to thank Professor Yatsuka Nakamura
:: for including me to the team working on the formalization
:: of the Jordan Curve Theorem. Especially, I am very grateful
:: to Professor Nakamura for inviting me to Shinshu University, Nagano
:: to work on the project together.
:: I am also thankful to Professor Andrzej Trybulec for his
:: continual help and fruitful discussions during the formalization.

reserve
  a, b, c, d, r, s for real number,
  n for Nat,
  p, p1, p2 for Point of TOP-REAL 2,
  x, y for Point of TOP-REAL n,
  C for Simple_closed_curve,
  A, B, P for Subset of TOP-REAL 2,
  U, V for Subset of (TOP-REAL 2)|C`,
  D for compact with_the_max_arc Subset of TOP-REAL 2;

  set T2 = TOP-REAL 2;

Lm1: for A, B, C, Z being set st A c= Z & B c= Z & C c= Z holds
      A \/ B \/ C c= Z
      proof
        let A, B, C, Z be set;
        assume A c= Z & B c= Z;
        then A \/ B c= Z by XBOOLE_1:8;
        hence thesis by XBOOLE_1:8;
      end;

Lm2:
      for A, B, C, D, Z being set st A c= Z & B c= Z & C c= Z & D c= Z holds
      A \/ B \/ C \/ D c= Z
      proof
        let A, B, C, D, Z be set;
        assume A c= Z & B c= Z & C c= Z;
        then A \/ B \/ C c= Z by Lm1;
        hence thesis by XBOOLE_1:8;
      end;

Lm3:
         for A, B, C, D, Z being set st A misses Z & B misses Z &
         C misses Z & D misses Z holds A \/ B \/ C \/ D misses Z
         proof
           let A, B, C, D, Z be set;
           assume A misses Z & B misses Z & C misses Z;
           then A \/ B \/ C misses Z by XBOOLE_1:114;
           hence thesis by XBOOLE_1:70;
         end;

registration
  let M be symmetric triangle Reflexive MetrStruct, x, y be Point of M;
  cluster dist(x,y) -> non negative;
  coherence by METRIC_1:5;
end;

registration
  let n be Nat, x, y be Point of TOP-REAL n;
  cluster dist(x,y) -> non negative;
  coherence
  proof
    ex p, q being Point of Euclid n st p = x & q = y & dist(x,y) = dist(p,q)
    by GOBRD14:def 1;
    hence 0 <= dist(x,y);
  end;
end;

registration
  let n be Nat, x, y be Point of TOP-REAL n;
  cluster |.x-y.| -> non negative;
  coherence
  proof
    reconsider x1 = x, y1 = y as Point of Euclid n by TOPREAL3:13;
    dist(x1,y1) = |.x-y.| by KURATO_2:31;
    hence 0 <= |.x-y.|;
  end;
end;

theorem Th1:
  for p1, p2 being Point of TOP-REAL n st p1 <> p2 holds 1/2*(p1+p2) <> p1
  proof
    let p1, p2 be Point of TOP-REAL n;
    set r = 1/2;
    assume that
A1: p1 <> p2 and
A2: r*(p1+p2) = p1;
    r*(p1+p2) = r*p1+r*p2 by EUCLID:36;
    then 0.REAL n = p1-(r*p1+r*p2) by A2,EUCLID:46
    .= p1-r*p1-r*p2 by EUCLID:50
    .= 1 * p1-r*p1-r*p2 by EUCLID:33
    .= (1-r)*p1-r*p2 by EUCLID:54
    .= r*(p1-p2) by EUCLID:53;
    then p1-p2 = 0.REAL n by EUCLID:35;
    hence thesis by A1,EUCLID:47;
  end;

theorem Th2:
  p1`2 < p2`2 implies p1`2 < (1/2*(p1+p2))`2
  proof
    assume
A1: p1`2 < p2`2;
    (1/2*(p1+p2))`2 = 1/2*((p1+p2)`2) by TOPREAL3:9
    .= 1/2*(p1`2+p2`2) by TOPREAL3:7
    .= (p1`2+p2`2)/2;
    hence thesis by A1,TOPREAL3:3;
  end;

theorem Th3:
  p1`2 < p2`2 implies (1/2*(p1+p2))`2 < p2`2
  proof
    assume
A1: p1`2 < p2`2;
    (1/2*(p1+p2))`2 = 1/2*((p1+p2)`2) by TOPREAL3:9
    .= 1/2*(p1`2+p2`2) by TOPREAL3:7
    .= (p1`2+p2`2)/2;
    hence thesis by A1,TOPREAL3:3;
  end;

theorem Th4:
  for A being vertical Subset of TOP-REAL 2 holds
  A /\ B is vertical
  proof
    let A be vertical Subset of TOP-REAL 2;
    let p, q be Point of T2;
    assume p in A /\ B & q in A /\ B;
    then p in A & q in A by XBOOLE_0:def 3;
    hence thesis by SPPOL_1:def 3;
  end;

theorem
  for A being horizontal Subset of TOP-REAL 2 holds
  A /\ B is horizontal
  proof
    let A be horizontal Subset of TOP-REAL 2;
    let p, q be Point of T2;
    assume p in A /\ B & q in A /\ B;
    then p in A & q in A by XBOOLE_0:def 3;
    hence thesis by SPPOL_1:def 2;
  end;

theorem
  p in LSeg(p1,p2) & LSeg(p1,p2) is vertical implies
  LSeg(p,p2) is vertical
  proof
    assume
A1: p in LSeg(p1,p2);
    assume
A2: LSeg(p1,p2) is vertical;
    then
A3: p1`1 = p2`1 by SPPOL_1:37;
    p1 in LSeg(p1,p2) by TOPREAL1:6;
    then p`1 = p1`1 by A1,A2,SPPOL_1:def 3;
    hence thesis by A3,SPPOL_1:37;
  end;

theorem
  p in LSeg(p1,p2) & LSeg(p1,p2) is horizontal implies
  LSeg(p,p2) is horizontal
  proof
    assume
A1: p in LSeg(p1,p2);
    assume
A2: LSeg(p1,p2) is horizontal;
    then
A3: p1`2 = p2`2 by SPPOL_1:36;
    p1 in LSeg(p1,p2) by TOPREAL1:6;
    then p`2 = p1`2 by A1,A2,SPPOL_1:def 2;
    hence thesis by A3,SPPOL_1:36;
  end;

registration
:: LSeg(NW-corner C,NE-corner C) is horizontal; ::SPRECT_3:29
  let P be Subset of TOP-REAL 2;
  cluster LSeg(SW-corner P,SE-corner P) -> horizontal;
  coherence
  proof
    (SW-corner P)`2 = S-bound P by PSCOMP_1:73
    .= (SE-corner P)`2 by PSCOMP_1:79;
    hence thesis by SPPOL_1:36;
  end;
  cluster LSeg(NW-corner P,SW-corner P) -> vertical;
  coherence
  proof
    (NW-corner P)`1 = W-bound P by PSCOMP_1:74
    .= (SW-corner P)`1 by PSCOMP_1:72;
    hence thesis by SPPOL_1:37;
  end;
  cluster LSeg(NE-corner P,SE-corner P) -> vertical;
  coherence
  proof
    (NE-corner P)`1 = E-bound P by PSCOMP_1:76
    .= (SE-corner P)`1 by PSCOMP_1:78;
    hence thesis by SPPOL_1:37;
  end;
end;

registration
  let P be Subset of TOP-REAL 2;
  cluster LSeg(SE-corner P,SW-corner P) -> horizontal;
  coherence;
  cluster LSeg(SW-corner P,NW-corner P) -> vertical;
  coherence;
  cluster LSeg(SE-corner P,NE-corner P) -> vertical;
  coherence;
end;

registration
  cluster vertical non empty compact -> with_the_max_arc Subset of TOP-REAL 2;
  coherence
  proof
    let A be Subset of TOP-REAL 2;
    assume
A1: A is vertical non empty compact;
    then
A2: W-bound A = E-bound A by SPRECT_1:17;
A3: E-min A in A by A1,SPRECT_1:16;
    (E-min A)`1 = E-bound A by EUCLID:56;
    then E-min A in Vertical_Line((W-bound A+E-bound A)/2) by A2,JORDAN6:34;
    hence A meets Vertical_Line((W-bound A+E-bound A)/2) by A3,XBOOLE_0:3;
  end;
end;

theorem Th8:
  for X, Y being non empty compact Subset of TOP-REAL 2 st
  X c= Y & (W-min Y in X or W-max Y in X) holds W-bound X = W-bound Y
  proof
    let X, Y be non empty compact Subset of T2;
    assume
A1: X c= Y & (W-min Y in X or W-max Y in X);
    (W-min X)`1 = W-bound X & (W-max X)`1 = W-bound X &
    (W-min Y)`1 = W-bound Y & (W-max Y)`1 = W-bound Y
    by PSCOMP_1:84;
    hence thesis by A1,JORDAN1J:21,22;
  end;

theorem Th9:
  for X, Y being non empty compact Subset of TOP-REAL 2 st
  X c= Y & (E-min Y in X or E-max Y in X) holds E-bound X = E-bound Y
  proof
    let X, Y be non empty compact Subset of T2;
    assume
A1: X c= Y & (E-min Y in X or E-max Y in X);
    (E-min X)`1 = E-bound X & (E-max X)`1 = E-bound X &
    (E-min Y)`1 = E-bound Y & (E-max Y)`1 = E-bound Y
    by PSCOMP_1:104;
    hence thesis by A1,JORDAN1J:17,18;
  end;

theorem
  for X, Y being non empty compact Subset of TOP-REAL 2 st
  X c= Y & (N-min Y in X or N-max Y in X) holds N-bound X = N-bound Y
  proof
    let X, Y be non empty compact Subset of T2;
    assume
A1: X c= Y & (N-min Y in X or N-max Y in X);
    (N-min X)`2 = N-bound X & (N-max X)`2 = N-bound X &
    (N-min Y)`2 = N-bound Y & (N-max Y)`2 = N-bound Y
    by PSCOMP_1:94;
    hence thesis by A1,JORDAN1J:15,16;
  end;

theorem
  for X, Y being non empty compact Subset of TOP-REAL 2 st
  X c= Y & (S-min Y in X or S-max Y in X) holds S-bound X = S-bound Y
  proof
    let X, Y be non empty compact Subset of T2;
    assume
A1: X c= Y & (S-min Y in X or S-max Y in X);
    (S-min X)`2 = S-bound X & (S-max X)`2 = S-bound X &
    (S-min Y)`2 = S-bound Y & (S-max Y)`2 = S-bound Y
    by PSCOMP_1:114;
    hence thesis by A1,JORDAN1J:19,20;
  end;

theorem
  W-bound C = W-bound North_Arc C
  proof
A1: North_Arc C c= C by JORDAN22:11;
    W-min C in North_Arc C by JORDAN22:5;
    hence thesis by A1,Th8;
end;

theorem
  E-bound C = E-bound North_Arc C
  proof
A1: North_Arc C c= C by JORDAN22:11;
    E-max C in North_Arc C by JORDAN22:6;
    hence thesis by A1,Th9;
end;

theorem
  W-bound C = W-bound South_Arc C
  proof
A1: South_Arc C c= C by JORDAN22:12;
    W-min C in South_Arc C by JORDAN22:7;
    hence thesis by A1,Th8;
end;

theorem
  E-bound C = E-bound South_Arc C
  proof
A1: South_Arc C c= C by JORDAN22:12;
    E-max C in South_Arc C by JORDAN22:8;
    hence thesis by A1,Th9;
end;

theorem Th16:
  p1`1 <= r & r <= p2`1 implies LSeg(p1,p2) meets Vertical_Line(r)
  proof
    assume that
A1: p1`1 <= r and
A2: r <= p2`1;
    set a = p1`1, b = p2`1;
    set l = (r-a) / (b-a);
    set k = (1-l)*p1+l*p2;
A3: l is Real by XREAL_0:def 1;
A4: a-a <= r-a by A1,XREAL_1:11;
    a <= b by A1,A2,XREAL_1:2;
    then b-b <= b-a by XREAL_1:12;
    then
A5: 0 <= l by A4,XREAL_1:138;
    r-a <= b-a by A2,XREAL_1:11;
    then l <= 1 by A4,XREAL_1:185;
    then
A6: k in LSeg(p1,p2) by A5,A3,SPPOL_1:22;
    per cases;
    suppose a <> b;
      then
A7:   b-a <> 0;
      k`1 = (1-l)*a+l*b by TOPREAL9:41
      .= a+l*(b-a)
      .= a+(r-a) by A7,XCMPLX_1:88;
      then k in Vertical_Line(r) by JORDAN6:34;
      hence thesis by A6,XBOOLE_0:3;
    end;
    suppose
A8:   a = b;
A9:   p1 in LSeg(p1,p2) by TOPREAL1:6;
      a = r by A1,A2,A8,XREAL_1:1;
      then p1 in Vertical_Line(r) by JORDAN6:34;
      hence thesis by A9,XBOOLE_0:3;
    end;
  end;

theorem
  p1`2 <= r & r <= p2`2 implies LSeg(p1,p2) meets Horizontal_Line(r)
  proof
    assume that
A1: p1`2 <= r and
A2: r <= p2`2;
    set a = p1`2, b = p2`2;
    set l = (r-a) / (b-a);
    set k = (1-l)*p1+l*p2;
A3: l is Real by XREAL_0:def 1;
A4: a-a <= r-a by A1,XREAL_1:11;
    a <= b by A1,A2,XREAL_1:2;
    then b-b <= b-a by XREAL_1:12;
    then
A5: 0 <= l by A4,XREAL_1:138;
    r-a <= b-a by A2,XREAL_1:11;
    then l <= 1 by A4,XREAL_1:185;
    then
A6: k in LSeg(p1,p2) by A5,A3,SPPOL_1:22;
    per cases;
    suppose a <> b;
      then
A7:   b-a <> 0;
      k`2 = (1-l)*a+l*b by TOPREAL9:42
      .= a+l*(b-a)
      .= a+(r-a) by A7,XCMPLX_1:88;
      then k in Horizontal_Line(r) by JORDAN6:35;
      hence thesis by A6,XBOOLE_0:3;
    end;
    suppose
A8:   a = b;
A9:   p1 in LSeg(p1,p2) by TOPREAL1:6;
      a = r by A1,A2,A8,XREAL_1:1;
      then p1 in Horizontal_Line(r) by JORDAN6:35;
      hence thesis by A9,XBOOLE_0:3;
    end;
  end;

Lm4: now
         let n;
         let A be Subset of TOP-REAL n such that
A1:      A is empty;
         consider p being Point of TOP-REAL n;
         A c= {p} by A1,XBOOLE_1:2;
         hence A is Bounded by JORDAN2C:16;
       end;

registration
  let n;
  cluster empty -> Bounded Subset of TOP-REAL n;
  coherence by Lm4;
  cluster non Bounded -> non empty Subset of TOP-REAL n;
  coherence by Lm4;
end;

registration
  let n be non empty Nat;
  cluster open closed non Bounded convex Subset of TOP-REAL n;
  existence
  proof
    take [#]TOP-REAL n;
    1 <= n by NAT_1:39;
    hence thesis by JORDAN2C:21,41;
  end;
end;

theorem Th18:
  for C being compact Subset of TOP-REAL 2 holds
  north_halfline UMP C \ {UMP C} misses C
  proof
    let C be compact Subset of TOP-REAL 2;
    set p = UMP C;
    set L = north_halfline p;
    set w = (W-bound C + E-bound C) / 2;
    assume L \ {p} meets C;
    then consider x being set such that
A1: x in L \ {p} and
A2: x in C by XBOOLE_0:3;
A3: x in L & x <> p by A1,ZFMISC_1:64;
    reconsider x as Point of T2 by A1;
A4: x`1 = p`1 & x`2 >= p`2 by A3,JORDAN1A:def 2;
    then x`2 <> p`2 by A3,TOPREAL3:11;
    then
A5: x`2 > p`2 by A4,REAL_1:def 5;
    x`1 = w by A4,JORDAN21:33;
    then x in Vertical_Line w by JORDAN6:34;
    then x in C /\ Vertical_Line w by A2,XBOOLE_0:def 3;
    hence thesis by A5,JORDAN21:41;
  end;

theorem Th19:
  for C being compact Subset of TOP-REAL 2 holds
  south_halfline LMP C \ {LMP C} misses C
  proof
    let C be compact Subset of TOP-REAL 2;
    set p = LMP C;
    set L = south_halfline p;
    set w = (W-bound C + E-bound C) / 2;
    assume L \ {p} meets C;
    then consider x being set such that
A1: x in L \ {p} and
A2: x in C by XBOOLE_0:3;
A3: x in L & x <> p by A1,ZFMISC_1:64;
    reconsider x as Point of T2 by A1;
A4: x`1 = p`1 & x`2 <= p`2 by A3,JORDAN1A:def 4;
    then x`2 <> p`2 by A3,TOPREAL3:11;
    then
A5: x`2 < p`2 by A4,REAL_1:def 5;
    x`1 = w by A4,JORDAN21:35;
    then x in Vertical_Line w by JORDAN6:34;
    then x in C /\ Vertical_Line w by A2,XBOOLE_0:def 3;
    hence thesis by A5,JORDAN21:42;
  end;

Lm5: for A being non empty convex Subset of T2 holds A is connected;

theorem Th20:
  for C being compact Subset of TOP-REAL 2 holds
  north_halfline UMP C \ {UMP C} c= UBD C
  proof
    let C be compact Subset of TOP-REAL 2;
    set A = north_halfline UMP C \ {UMP C};
    north_halfline UMP C is not Bounded by JORDAN1C:10;
    then reconsider A as non Bounded Subset of T2 by JORDAN1I:2;
A1: A misses C by Th18;
    A is convex by JORDAN21:6;
    then A is connected by Lm5;
    hence thesis by A1,JORDAN1C:13;
  end;

theorem Th21:
  for C being compact Subset of TOP-REAL 2 holds
  south_halfline LMP C \ {LMP C} c= UBD C
  proof
    let C be compact Subset of TOP-REAL 2;
    set A = south_halfline LMP C \ {LMP C};
    south_halfline LMP C is not Bounded by JORDAN1C:11;
    then reconsider A as non Bounded Subset of T2 by JORDAN1I:2;
A1: A misses C by Th19;
    A is convex by JORDAN21:7;
    then A is connected by Lm5;
    hence thesis by A1,JORDAN1C:13;
  end;

theorem Th22:
  A is_inside_component_of B implies UBD B misses A
  proof
    assume A is_inside_component_of B;
    then
A1: A c= BDD B by JORDAN2C:26;
    BDD B misses UBD B by JORDAN2C:28;
    hence thesis by A1,XBOOLE_1:63;
  end;

theorem
  A is_outside_component_of B implies BDD B misses A
  proof
    assume A is_outside_component_of B;
    then
A1: A c= UBD B by JORDAN2C:27;
    BDD B misses UBD B by JORDAN2C:28;
    hence thesis by A1,XBOOLE_1:63;
  end;

Lm6:
      p in C implies {p} misses U
      proof
        assume
A1:     p in C;
A2:     U is Subset of T2 by PRE_TOPC:39;
        the carrier of T2|C` = C` by JORDAN1:1;
        then U misses C by A2,SUBSET_1:43;
        then not p in U by A1,XBOOLE_0:3;
        hence thesis by ZFMISC_1:56;
      end;

  set C0 = Closed-Interval-TSpace(0,1);
  set C1 = Closed-Interval-TSpace(-1,1);
  set l0 = (#)(-1,1);
  set l1 = (-1,1)(#);
  set h1 = L[01](l0,l1);

Lm7: the carrier of [:T2,T2:] = [:the carrier of T2, the carrier of T2:]
       by BORSUK_1:def 5;

Lm8: [#]T2 = the carrier of T2 by PRE_TOPC:12;
Lm9:
now
  let T be non empty TopSpace;
  let a be Real;
  set c = the carrier of T;
  set f = c --> a;
  thus f is continuous
  proof
A1: dom f = c by FUNCT_2:def 1;
A2: rng f = {a} by FUNCOP_1:14;
  let Y be Subset of REAL; assume Y is closed;
  per cases;
  suppose a in Y;
  then
A3: rng f c= Y by A2,ZFMISC_1:37;
  f"Y = f"(rng f /\ Y) by RELAT_1:168
     .= f"rng f by A3,XBOOLE_1:28
     .= c by A1,RELAT_1:169
     .= [#]T by PRE_TOPC:12;
  hence f"Y is closed;
  end;
  suppose not a in Y;
  then
A4: rng f misses Y by A2,ZFMISC_1:56;
  f"Y = f"(rng f /\ Y) by RELAT_1:168
     .= f"{} by A4,XBOOLE_0:def 7
     .= {}T by RELAT_1:171;
  hence f"Y is closed;
  end;
end;
end;

theorem Th24:
  for r being positive (real number)
  for a being Point of TOP-REAL n holds a in Ball(a,r)
  proof
    let r be positive (real number);
    let a be Point of TOP-REAL n;
    |. a-a .| = 0 by TOPRNS_1:29;
    hence thesis by TOPREAL9:7;
  end;

theorem Th25:
  for r being non negative (real number)
  for p being Point of TOP-REAL n holds p is Point of Tdisk(p,r)
  proof
    let r be non negative (real number);
    let p be Point of TOP-REAL n;
A1: the carrier of Tdisk(p,r) = cl_Ball(p,r) by BROUWER:3;
    |. p-p .| = 0 by TOPRNS_1:29;
    hence thesis by A1,TOPREAL9:8;
  end;

registration
  let r be positive (real number);
  let n be non empty Nat;
  let p, q be Point of TOP-REAL n;
  cluster cl_Ball(p,r) \ {q} -> non empty;
  coherence
  proof
A1: the carrier of Tcircle(p,r) = Sphere(p,r) by TOPREALB:9;
A2: the carrier of Tdisk(p,r) = cl_Ball(p,r) by BROUWER:3;
A3: Sphere(p,r) c= cl_Ball(p,r) by TOPREAL9:17;
    consider a being Point of Tcircle(p,r);
A4: a in Sphere(p,r) by A1;
    per cases;
    suppose
A5:   a = q;
A6:   p is Point of Tdisk(p,r) by Th25;
      |. p-p .| <> r by TOPRNS_1:29;
      then p <> q by A5,A1,TOPREAL9:9;
      hence thesis by A6,A2,ZFMISC_1:64;
    end;
    suppose a <> q;
      hence thesis by A4,A3,ZFMISC_1:64;
    end;
  end;
end;

theorem Th26:
  r <= s implies Ball(x,r) c= Ball(x,s)
  proof
    reconsider xe = x as Point of Euclid n by TOPREAL3:13;
    Ball(x,r) = Ball(xe,r) & Ball(x,s) = Ball(xe,s) by TOPREAL9:13;
    hence thesis by PCOMPS_1:1;
  end;

theorem Th27:
  cl_Ball(x,r) \ Ball(x,r) = Sphere(x,r)
  proof
    thus cl_Ball(x,r) \ Ball(x,r) c= Sphere(x,r)
    proof
      let a be set;
      assume
A1:   a in cl_Ball(x,r) \ Ball(x,r);
      then reconsider a as Point of TOP-REAL n;
      a in cl_Ball(x,r) & not a in Ball(x,r) by A1,XBOOLE_0:def 4;
      then |. a-x .| <= r & |. a-x .| >= r by TOPREAL9:7,8;
      then |. a-x .| = r by XREAL_1:1;
      hence thesis by TOPREAL9:9;
    end;
    let a be set;
    assume
A2: a in Sphere(x,r);
    then reconsider a as Point of TOP-REAL n;
    |. a-x .| = r by A2,TOPREAL9:9;
    then a in cl_Ball(x,r) & not a in Ball(x,r) by TOPREAL9:7,8;
    hence thesis by XBOOLE_0:def 4;
  end;

theorem Th28:
  y in Sphere(x,r) implies LSeg(x,y) \ {x,y} c= Ball(x,r)
  proof
    assume
A1: y in Sphere(x,r);
    per cases;
    suppose
A2:   r = 0;
      reconsider xe = x as Point of Euclid n by TOPREAL3:13;
      Sphere(x,r) = Sphere(xe,r) by TOPREAL9:15;
      then Sphere(x,r) = {x} by A2,TOPREAL6:62;
      then
A3:   x = y by A1,TARSKI:def 1;
A4:   LSeg(x,x) = {x} by TOPREAL1:7;
A5:   {x,x} = {x} by ENUMSET1:69;
      {x} \ {x} = {} by XBOOLE_1:37;
      hence thesis by A4,A3,A5,XBOOLE_1:2;
    end;
    suppose
A6:   r <> 0;
    let k be set;
    assume
A7: k in LSeg(x,y) \ {x,y};
    then k in LSeg(y,x) by XBOOLE_0:def 4;
    then consider l being Real such that
A8: 0 <= l & l <= 1 and
A9: k = (1-l)*x + l*y by SPPOL_1:21;
    reconsider k as Point of TOP-REAL n by A9;
    not k in {x,y} by A7,XBOOLE_0:def 4;
    then k <> x & k <> y by TARSKI:def 2;
    then 0 <> l & l <> 1 by A9,TOPREAL9:4;
    then
A10: 0 < l & l < 1 by A8,REAL_1:def 5;
    k-x = (1-l)*x - x + l*y by A9,JORDAN2C:9
    .= 1 * x - l*x - x + l*y by EUCLID:54
    .= x - l*x - x + l*y by EUCLID:33
    .= x - x - l*x + l*y by TOPREAL9:1
    .= 0.REAL n - l*x + l*y by EUCLID:46
    .= -l*x + l*y by JORDAN2C:13
    .= l*y - l*x by EUCLID:45
    .= l*(y-x) by EUCLID:53;
    then
A11: |. k-x .| = abs(l) * |. y-x .| by TOPRNS_1:8
    .= l*|. y-x .| by A8,ABSVALUE:def 1
    .= l*r by A1,TOPREAL9:9;
    0 <= r by A1,TOPREAL9:39;
    then l*r < 1 * r by A6,A10,XREAL_1:70;
    hence thesis by A11,TOPREAL9:7;
  end;
  end;

theorem Th29:
  r < s implies cl_Ball(x,r) c= Ball(x,s)
  proof
    assume
A1: r < s;
    let a be set;
    assume
A2: a in cl_Ball(x,r);
    then reconsider a as Point of TOP-REAL n;
    |. a-x .| <= r by A2,TOPREAL9:8;
    then |. a-x .| < s by A1,XREAL_1:2;
    hence thesis by TOPREAL9:7;
  end;

theorem Th30:
  r < s implies Sphere(x,r) c= Ball(x,s)
  proof
    assume r < s;
    then
A1: cl_Ball(x,r) c= Ball(x,s) by Th29;
    Sphere(x,r) c= cl_Ball(x,r) by TOPREAL9:17;
    hence thesis by A1,XBOOLE_1:1;
  end;

theorem Th31:
  for r being non zero (real number) holds Cl Ball(x,r) = cl_Ball(x,r)
  proof
    let r be non zero (real number);
    Ball(x,r) c= cl_Ball(x,r) by TOPREAL9:16;
    hence Cl Ball(x,r) c= cl_Ball(x,r) by TOPS_1:31;
    per cases;
    suppose
A1:   Ball(x,r) is empty;
      then r < 0 by TOPREAL9:21;
      then
A2:   cl_Ball(x,r) is empty by TOPREAL9:22;
      for A being empty Subset of TOP-REAL n holds Cl A is empty;
      hence thesis by A1,A2;
    end;
    suppose
A3:   Ball(x,r) is non empty;
    let a be set;
    assume
A4: a in cl_Ball(x,r);
    then reconsider a as Point of TOP-REAL n;
    reconsider ae = a as Point of Euclid n by TOPREAL3:13;
A5: 0 < r by A3,TOPREAL9:20;
    for s being real number st 0 < s & s < r holds Ball(ae,s) meets Ball(x,r)
    proof
      let s be real number such that
A6:   0 < s and
A7:   s < r;
      now
A8:     Ball(x,r) \/ Sphere(x,r) = cl_Ball(x,r) by TOPREAL9:18;
        per cases by A4,A8,XBOOLE_0:def 2;
        suppose
A9:       a in Ball(x,r);
          |.a-a.| = 0 by TOPRNS_1:29;
          then a in Ball(a,s) by A6,TOPREAL9:7;
          hence Ball(a,s) meets Ball(x,r) by A9,XBOOLE_0:3;
        end;
        suppose
A10:       a in Sphere(x,r);
          then
A11:       |. a-x .| = r by TOPREAL9:9;
          |. x-x .| = 0 by TOPRNS_1:29;
          then
A12:       x in Ball(x,r) by A5,TOPREAL9:7;
          set z = s/(2*r);
A13:       z is Real by XREAL_0:def 1;
          set q = (1-z)*a+z*x;
          1 * r < 2*r by A5,XREAL_1:70;
          then s < 2*r by A7,XREAL_1:2;
          then
A14:       z < 1 by A6,XREAL_1:191;
          0 < 2*r by A5,XREAL_1:131;
          then
A15:       0 < z by A6,XREAL_1:141;
          then
A16:       q in LSeg(a,x) by A14,A13,SPPOL_1:22;
          Ball(x,r) misses Sphere(x,r) by TOPREAL9:19;
          then a <> x by A10,A12,XBOOLE_0:3;
          then q <> a & q <> x by A14,A15,TOPREAL9:4;
          then not q in {a,x} by TARSKI:def 2;
          then
A17:       q in LSeg(a,x) \ {a,x} by A16,XBOOLE_0:def 4;
A18:       LSeg(a,x) \ {a,x} c= Ball(x,r) by A10,Th28;
          q-a = (1-z)*a - a + z*x by JORDAN2C:9
          .= 1 * a - z*a - a + z*x by EUCLID:54
          .= a - z*a - a + z*x by EUCLID:33
          .= a - a - z*a + z*x by TOPREAL9:1
          .= 0.REAL n - z*a + z*x by EUCLID:46
          .= -z*a + z*x by JORDAN2C:13
          .= z*x - z*a by EUCLID:45
          .= z*(x-a) by EUCLID:53;
          then |.q-a.| = abs(z) * |.x-a.| by A13,TOPRNS_1:8
          .= z*|.x-a.| by A15,ABSVALUE:def 1
          .= z*|. a-x .| by TOPRNS_1:28
          .= s/2 by A11,XCMPLX_1:93;
          then
A19:       q in Sphere(a,s/2) by TOPREAL9:9;
          s/2 < s/1 by A6,XREAL_1:78;
          then Sphere(a,s/2) c= Ball(a,s) by Th30;
          hence Ball(a,s) meets Ball(x,r) by A18,A17,A19,XBOOLE_0:3;
        end;
      end;
      hence thesis by TOPREAL9:13;
    end;
    hence thesis by A5,JORDAN19:8;
  end;
  end;

theorem Th32:
  for r being non zero (real number) holds
  Fr Ball(x,r) = Sphere(x,r)
  proof
    let r be non zero (real number);
    set P = Ball(x,r);
    thus Fr P = Cl P \ P by TOPS_1:76
    .= cl_Ball(x,r) \ P by Th31
    .= Sphere(x,r) by Th27;
  end;

registration
  let n be non empty Nat;
  cluster Bounded -> proper Subset of TOP-REAL n;
  coherence
  proof
A1: the carrier of TOP-REAL n = [#]TOP-REAL n by PRE_TOPC:12;
    1 <= n by NAT_1:39;
    then [#]TOP-REAL n is not Bounded by JORDAN2C:41;
    hence thesis by A1,TEX_2:5;
  end;
end;

registration
  let n;
  cluster non empty closed convex Bounded Subset of TOP-REAL n;
  existence
  proof
    take cl_Ball(0.REAL n,1);
    thus thesis;
  end;
  cluster non empty open convex Bounded Subset of TOP-REAL n;
  existence
  proof
    take Ball(0.REAL n,1);
    thus thesis;
  end;
end;

registration
  let n be Nat; let A be Bounded Subset of TOP-REAL n;
  cluster Cl A -> Bounded;
  coherence by TOPREAL6:71;
end;

registration
  let n be Nat; let A be Bounded Subset of TOP-REAL n;
  cluster Fr A -> Bounded;
  coherence by JORDAN1I:1;
end;

theorem Th33:
  for A being closed Subset of TOP-REAL n, p being Point of TOP-REAL n st
  not p in A
  ex r being positive (real number) st Ball(p,r) misses A
  proof
    let A be closed Subset of TOP-REAL n, p be Point of TOP-REAL n;
    assume not p in A;
    then
A1: p in A` by SUBSET_1:50;
    reconsider e = p as Point of Euclid n by TOPREAL3:13;
    consider r being real number such that
A2: r > 0 and
A3: Ball(e,r) c= A` by A1,TOPMETR:22;
    reconsider r as positive (real number) by A2;
    take r;
    Ball(p,r) = Ball(e,r) by TOPREAL9:13;
    hence thesis by A3,SUBSET_1:43;
  end;

theorem Th34:
  for A being Bounded Subset of TOP-REAL n, a being Point of TOP-REAL n
  ex r being positive (real number) st A c= Ball(a,r)
  proof
    let A be Bounded Subset of TOP-REAL n;
    let a be Point of TOP-REAL n;
    reconsider C = A as bounded Subset of Euclid n by JORDAN2C:def 2;
    consider r being Real, x being Element of Euclid n such that
A1: 0 < r and
A2: C c= Ball(x,r) by METRIC_6:def 10;
    reconsider r as positive (real number) by A1;
    reconsider x1 = x as Point of TOP-REAL n by TOPREAL3:13;
    take s = r+|.x1-a.|;
    let p be set;
    assume
A3: p in A;
    then reconsider p1 = p as Point of TOP-REAL n;
    p = p1;
    then reconsider p as Point of Euclid n by TOPREAL3:13;
A4: dist(p,x) < r by A2,A3,METRIC_1:12;
A5: |.p1-x1.| = dist(p,x) by KURATO_2:31;
A6: |.p1-a.| <= |.p1-x1.| + |.x1-a.| by TOPRNS_1:35;
    |.p1-x1.| + |.x1-a.| < s by A5,A4,XREAL_1:8;
    then |.p1-a.| < s by A6,XREAL_1:2;
    hence thesis by TOPREAL9:7;
  end;

theorem Th35:
  for S, T being TopStruct, f being Function
 of S, T st f is_homeomorphism holds
  f is onto
  proof
    let S, T be TopStruct;
    let f be Function of S, T;
    assume f is_homeomorphism;
    then rng f = [#]T by TOPS_2:def 5
    .= the carrier of T by PRE_TOPC:12;
    hence f is onto by FUNCT_2:def 3;
  end;

registration
  let T be non empty being_T2 TopSpace;
  cluster -> being_T2 (non empty SubSpace of T);
  coherence by TOPMETR:3;
end;

registration
  let p, r;
  cluster Tdisk(p,r) -> closed;
  coherence
  proof
    let A be Subset of T2;
    assume A = the carrier of Tdisk(p,r);
    then A = cl_Ball(p,r) by BROUWER:3;
    hence A is closed;
  end;
end;

registration
  let p, r;
  cluster Tdisk(p,r) -> compact;
  coherence
  proof
    set D = Tdisk(p,r);
    the carrier of D is Subset of T2 by TSEP_1:1;
    then reconsider Q = [#]D as Subset of T2 by XBOOLE_1:1;
    [#]D = the carrier of D by PRE_TOPC:12
    .= cl_Ball(p,r) by BROUWER:3;
    then Q is compact by TOPREAL6:88;
    then [#]D is compact by COMPTS_1:11;
    hence thesis by COMPTS_1:10;
  end;
end;

begin :: Paths

theorem
  for T being non empty TopSpace, a, b being Point of T
  for f being Path of a,b st a,b are_connected holds rng f is connected
  proof
    let T be non empty TopSpace, a, b be Point of T;
    let f be Path of a,b such that
A1: a,b are_connected;
A2: dom f = the carrier of I[01] by FUNCT_2:def 1;
    reconsider A = [.0,1.] as Subset of R^1 by TOPMETR:24;
    reconsider B = A as Subset of I[01] by A2,BORSUK_1:83;
    A is connected by RCOMP_3:52;
    then
A3: B is connected by CONNSP_1:24;
A4: f is continuous by A1,BORSUK_2:def 2;
    f.:B = rng f by A2,BORSUK_1:83,RELAT_1:146;
    hence rng f is connected by A3,A4,TOPREAL5:5;
  end;

theorem Th38:
  for X being non empty TopSpace, Y being non empty SubSpace of X,
  x1, x2 being Point of X, y1, y2 being Point of Y,
  f being Path of x1,x2 st x1 = y1 & x2 = y2 & x1,x2 are_connected &
  rng f c= the carrier of Y holds
  y1,y2 are_connected & f is Path of y1,y2
  proof
    let X be non empty TopSpace, Y be non empty SubSpace of X,
    x1, x2 be Point of X, y1, y2 be Point of Y,
    f be Path of x1,x2 such that
A1: x1 = y1 & x2 = y2 and
A2: x1, x2 are_connected;
    assume rng f c= the carrier of Y;
    then reconsider g = f as Function of I[01], Y by FUNCT_2:8;
A3: f is continuous by A2,BORSUK_2:def 2;
    thus
A4: ex f being Function of I[01], Y st f is continuous & f.0 = y1 & f.1 = y2
    proof
      take g;
      thus g is continuous by A3,TOPMETR:8;
      thus thesis by A1,A2,BORSUK_2:def 2;
    end;
    g is Path of y1,y2
    proof
      thus ex f being Function
 of I[01], Y st f is continuous & f.0 = y1 & f.1 = y2
      by A4;
      thus g is continuous by A3,TOPMETR:8;
      thus g.0 = y1 & g.1 = y2 by A1,A2,BORSUK_2:def 2;
    end;
    hence f is Path of y1,y2;
  end;

theorem Th39:
  for X being arcwise_connected non empty TopSpace,
  Y being non empty SubSpace of X,
  x1, x2 being Point of X, y1, y2 being Point of Y,
  f being Path of x1,x2 st x1 = y1 & x2 = y2 &
  rng f c= the carrier of Y holds
  y1,y2 are_connected & f is Path of y1,y2
  proof
    let X be arcwise_connected non empty TopSpace,
    Y be non empty SubSpace of X,
    x1, x2 be Point of X, y1, y2 be Point of Y;
    x1,x2 are_connected by BORSUK_2:def 3;
    hence thesis by Th38;
  end;

Lm10:
  for T being non empty TopSpace, a, b being Point of T
  for f being Path of a,b st a,b are_connected holds
  rng f c= rng -f
  proof
    let T be non empty TopSpace;
    let a, b be Point of T;
    let f be Path of a,b;
    assume
A1: a,b are_connected;
    let y be set;
    assume y in rng f;
    then consider x being set such that
A2: x in dom f and
A3: f.x = y by FUNCT_1:def 5;
    reconsider x as Point of I[01] by A2;
A4: dom -f = the carrier of I[01] by FUNCT_2:def 1;
A5: 1-x is Point of I[01] by JORDAN5B:4;
    then (-f).(1-x) = f.(1-(1-x)) by A1,BORSUK_2:def 6;
    hence thesis by A3,A4,A5,FUNCT_1:def 5;
  end;

theorem Th40:
  for T being non empty TopSpace, a, b being Point of T
  for f being Path of a,b st a,b are_connected holds
  rng f = rng -f
  proof
    let T be non empty TopSpace;
    let a, b be Point of T;
    let f be Path of a,b;
    assume
A1: a,b are_connected;
    hence rng f c= rng -f by Lm10;
    f = --f by A1,BORSUK_6:50;
    hence thesis by A1,Lm10;
  end;

theorem Th41:
  for T being arcwise_connected non empty TopSpace, a, b being Point of T
  for f being Path of a,b holds rng f = rng -f
  proof
    let T be arcwise_connected non empty TopSpace;
    let a, b be Point of T;
    let f be Path of a,b;
    a,b are_connected by BORSUK_2:def 3;
    hence thesis by Th40;
  end;

theorem Th42:
  for T being non empty TopSpace, a, b, c being Point of T
  for f being Path of a,b, g being Path of b,c st
  a,b are_connected & b,c are_connected holds
  rng f c= rng(f+g)
  proof
    let T be non empty TopSpace;
    let a, b, c be Point of T;
    let f be Path of a,b;
    let g be Path of b,c;
    assume that
A1: a,b are_connected and
A2: b,c are_connected;
    let y be set;
    assume y in rng f;
    then consider x being set such that
A3: x in dom f and
A4: f.x = y by FUNCT_1:def 5;
A5: dom(f+g) = the carrier of I[01] by FUNCT_2:def 1;
    reconsider x as Point of I[01] by A3;
    1/2*x = x/2;
    then
A6: x/2 is Point of I[01] by BORSUK_6:9;
    x <= 1 by JORDAN5A:2;
    then x/2 <= 1/2 by XREAL_1:74;
    then (f+g).(x/2) = f.(2*(x/2)) by A1,A2,A6,BORSUK_2:def 5;
    hence y in rng(f+g) by A5,A4,A6,FUNCT_1:def 5;
  end;

theorem
  for T being arcwise_connected non empty TopSpace, a, b, c being Point of T
  for f being Path of a,b, g being Path of b,c holds
  rng f c= rng(f+g)
  proof
    let T be arcwise_connected non empty TopSpace;
    let a, b, c be Point of T;
    let f be Path of a,b;
    let g be Path of b,c;
    a,b are_connected & b,c are_connected by BORSUK_2:def 3;
    hence thesis by Th42;
  end;

theorem Th44:
  for T being non empty TopSpace, a, b, c being Point of T
  for f being Path of b,c, g being Path of a,b st
  a,b are_connected & b,c are_connected holds
  rng f c= rng(g+f)
  proof
    let T be non empty TopSpace;
    let a, b, c be Point of T;
    let f be Path of b,c;
    let g be Path of a,b;
    assume that
A1: a,b are_connected and
A2: b,c are_connected;
    let y be set;
    assume y in rng f;
    then consider x being set such that
A3: x in dom f and
A4: f.x = y by FUNCT_1:def 5;
A5: dom(g+f) = the carrier of I[01] by FUNCT_2:def 1;
    reconsider x as Point of I[01] by A3;
    0 <= x by JORDAN5A:2;
    then 0/2 <= x/2 by XREAL_1:74;
    then
A6: 0+1/2 <= x/2+1/2 by XREAL_1:8;
    x <= 1 by JORDAN5A:2;
    then x+1 <= 1+1 by XREAL_1:8;
    then (x+1)/2 <= 2/2 by XREAL_1:74;
    then
A7: x/2+1/2 is Point of I[01] by A6,JORDAN5A:2;
    then (g+f).(x/2+1/2) = f.(2*(x/2+1/2)-1) by A1,A2,A6,BORSUK_2:def 5;
    hence y in rng(g+f) by A5,A4,A7,FUNCT_1:def 5;
  end;

theorem
  for T being arcwise_connected non empty TopSpace, a, b, c being Point of T
  for f being Path of b,c, g being Path of a,b holds
  rng f c= rng(g+f)
  proof
    let T be arcwise_connected non empty TopSpace;
    let a, b, c be Point of T;
    let f be Path of b,c;
    let g be Path of a,b;
    a,b are_connected & b,c are_connected by BORSUK_2:def 3;
    hence thesis by Th44;
  end;

theorem Th46:
  for T being non empty TopSpace, a, b, c being Point of T
  for f being Path of a,b, g being Path of b,c st
  a,b are_connected & b,c are_connected holds
  rng(f+g) = rng f \/ rng g
  proof
    let T be non empty TopSpace;
    let a, b, c be Point of T;
    let f be Path of a,b;
    let g be Path of b,c;
    assume that
A1: a,b are_connected and
A2: b,c are_connected;
    thus rng(f+g) c= rng f \/ rng g
    proof
      let y be set;
      assume y in rng(f+g);
      then consider x being set such that
A3:   x in dom(f+g) and
A4:   y = (f+g).x by FUNCT_1:def 5;
      reconsider x as Point of I[01] by A3;
      per cases;
      suppose
A5:     x <= 1/2;
        then
A6:     (f+g).x = f.(2*x) by A1,A2,BORSUK_2:def 5;
A7:     rng f c= rng f \/ rng g by XBOOLE_1:7;
A8:     dom f = the carrier of I[01] by FUNCT_2:def 1;
        2*x is Point of I[01] by A5,BORSUK_6:6;
        then y in rng f by A4,A8,A6,FUNCT_1:def 5;
        hence thesis by A7;
      end;
      suppose
A9:     1/2 <= x;
        then
A10:     (f+g).x = g.(2*x-1) by A1,A2,BORSUK_2:def 5;
A11:     rng g c= rng f \/ rng g by XBOOLE_1:7;
A12:     dom g = the carrier of I[01] by FUNCT_2:def 1;
        2*x-1 is Point of I[01] by A9,BORSUK_6:7;
        then y in rng g by A4,A12,A10,FUNCT_1:def 5;
        hence thesis by A11;
      end;
    end;
    rng f c= rng(f+g) & rng g c= rng(f+g) by A1,A2,Th42,Th44;
    hence thesis by XBOOLE_1:8;
  end;

theorem
  for T being arcwise_connected non empty TopSpace, a, b, c being Point of T
  for f being Path of a,b, g being Path of b,c holds
  rng(f+g) = rng f \/ rng g
  proof
    let T be arcwise_connected non empty TopSpace;
    let a, b, c be Point of T;
    let f be Path of a,b;
    let g be Path of b,c;
    a,b are_connected & b,c are_connected by BORSUK_2:def 3;
    hence thesis by Th46;
  end;

theorem Th48:
  for T being non empty TopSpace, a, b, c, d being Point of T
  for f being Path of a,b, g being Path of b,c, h being Path of c,d st
  a,b are_connected & b,c are_connected & c,d are_connected holds
  rng(f+g+h) = rng f \/ rng g \/ rng h
  proof
    let T be non empty TopSpace;
    let a, b, c, d be Point of T;
    let f be Path of a,b;
    let g be Path of b,c;
    let h be Path of c,d;
    assume that
A1: a,b are_connected and
A2: b,c are_connected and
A3: c,d are_connected;
    a,c are_connected by A1,A2,BORSUK_6:46;
    hence rng(f+g+h) = rng(f+g) \/ rng h by A3,Th46
    .= rng f \/ rng g \/ rng h by A1,A2,Th46;
  end;

theorem Th49:
  for T being arcwise_connected non empty TopSpace, a, b, c, d being Point of T
  for f being Path of a,b, g being Path of b,c, h being Path of c,d
  holds rng(f+g+h) = rng f \/ rng g \/ rng h
  proof
    let T be arcwise_connected non empty TopSpace;
    let a, b, c, d be Point of T;
    let f be Path of a,b;
    let g be Path of b,c;
    let h be Path of c,d;
    a,b are_connected & b,c are_connected & c,d are_connected
    by BORSUK_2:def 3;
    hence thesis by Th48;
  end;

Lm11:
  for T being non empty TopSpace, a, b, c, d, e being Point of T
  for f being Path of a,b, g being Path of b,c,
  h being Path of c,d, i being Path of d,e st
  a,b are_connected & b,c are_connected &
  c,d are_connected & d,e are_connected holds
  rng(f+g+h+i) = rng f \/ rng g \/ rng h \/ rng i
  proof
    let T be non empty TopSpace;
    let a, b, c, d, e be Point of T;
    let f be Path of a,b;
    let g be Path of b,c;
    let h be Path of c,d;
    let i be Path of d,e;
    assume that
A1: a,b are_connected and
A2: b,c are_connected and
A3: c,d are_connected and
A4: d,e are_connected;
    a,c are_connected by A1,A2,BORSUK_6:46;
    then a,d are_connected by A3,BORSUK_6:46;
    hence rng(f+g+h+i) = rng(f+g+h) \/ rng i by A4,Th46
    .= rng f \/ rng g \/ rng h \/ rng i by A1,A2,A3,Th48;
  end;

Lm12:
  for T being arcwise_connected non empty TopSpace,
  a, b, c, d, e being Point of T
  for f being Path of a,b, g being Path of b,c,
  h being Path of c,d, i being Path of d,e
  holds rng(f+g+h+i) = rng f \/ rng g \/ rng h \/ rng i
  proof
    let T be arcwise_connected non empty TopSpace;
    let a, b, c, d, e be Point of T;
    let f be Path of a,b;
    let g be Path of b,c;
    let h be Path of c,d;
    let i be Path of d,e;
    a,b are_connected & b,c are_connected &
    c,d are_connected & d,e are_connected by BORSUK_2:def 3;
    hence thesis by Lm11;
  end;

Lm13:
  for T being non empty TopSpace, a, b, c, d, e, z being Point of T
  for f being Path of a,b, g being Path of b,c,
  h being Path of c,d, i being Path of d,e, j being Path of e,z st
  a,b are_connected & b,c are_connected & c,d are_connected &
  d,e are_connected & e,z are_connected holds
  rng(f+g+h+i+j) = rng f \/ rng g \/ rng h \/ rng i \/ rng j
  proof
    let T be non empty TopSpace;
    let a, b, c, d, e, z be Point of T;
    let f be Path of a,b;
    let g be Path of b,c;
    let h be Path of c,d;
    let i be Path of d,e;
    let j be Path of e,z;
    assume that
A1: a,b are_connected and
A2: b,c are_connected and
A3: c,d are_connected and
A4: d,e are_connected and
A5: e,z are_connected;
    a,c are_connected by A1,A2,BORSUK_6:46;
    then a,d are_connected by A3,BORSUK_6:46;
    then a,e are_connected by A4,BORSUK_6:46;
    hence rng(f+g+h+i+j) = rng(f+g+h+i) \/ rng j by A5,Th46
    .= rng f \/ rng g \/ rng h \/ rng i \/ rng j by A1,A2,A3,A4,Lm11;
  end;

Lm14:
  for T being arcwise_connected non empty TopSpace,
  a, b, c, d, e, z being Point of T
  for f being Path of a,b, g being Path of b,c,
  h being Path of c,d, i being Path of d,e, j being Path of e,z
  holds rng(f+g+h+i+j) = rng f \/ rng g \/ rng h \/ rng i \/ rng j
  proof
    let T be arcwise_connected non empty TopSpace;
    let a, b, c, d, e, z be Point of T;
    let f be Path of a,b;
    let g be Path of b,c;
    let h be Path of c,d;
    let i be Path of d,e;
    let j be Path of e,z;
    a,b are_connected & b,c are_connected & c,d are_connected &
    d,e are_connected & e,z are_connected by BORSUK_2:def 3;
    hence thesis by Lm13;
  end;

theorem Th50:
  for T being non empty TopSpace, a being Point of T holds
  I[01] --> a is Path of a,a
  proof
    let T be non empty TopSpace, a be Point of T;
    set f = I[01] --> a;
    thus a,a are_connected;
    thus thesis by TOPALG_3:5,BORSUK_1:def 17,def 18;
  end;

theorem Th51:
  for p1, p2 being Point of TOP-REAL n, P being Subset of TOP-REAL n
  holds P is_an_arc_of p1,p2 implies
  ex F being Path of p1,p2, f being Function of I[01], (TOP-REAL n)|P st
  rng f = P & F = f
  proof
    let p1, p2 be Point of TOP-REAL n,
    P be Subset of TOP-REAL n;
    assume
A1: P is_an_arc_of p1,p2;
    then reconsider P1 = P as non empty Subset of TOP-REAL n by TOPREAL1:4;
    consider h being Function of I[01], (TOP-REAL n)|P such that
A2: h is_homeomorphism and
A3: h.0 = p1 & h.1 = p2 by A1,TOPREAL1:def 2;
    h is Function of I[01], (TOP-REAL n)|P1;
    then reconsider h1 = h as Function of I[01],TOP-REAL n by TOPREALA:28;
    h1 is Path of p1,p2
    proof
      h is continuous by A2,TOPS_2:def 5;
      hence h1 is continuous & h1.0 = p1 & h1.1 = p2 by A3,TOPMETR:7;
    end;
    then reconsider f = h as Path of p1,p2;
    take f, h;
    thus rng h = [#]((TOP-REAL n)|P) by A2,TOPS_2:def 5
    .= the carrier of (TOP-REAL n)|P by PRE_TOPC:12
    .= P by JORDAN1:1;
    thus thesis;
  end;

theorem Th52:
  for p1, p2 being Point of TOP-REAL n
  ex F being Path of p1,p2, f being Function
 of I[01], (TOP-REAL n)|LSeg(p1,p2) st
  rng f = LSeg(p1,p2) & F = f
  proof
    let p1, p2 be Point of TOP-REAL n;
    per cases;
    suppose
A1:   p1 = p2;
      then reconsider g = I[01] --> p1 as Path of p1,p2 by Th50;
      take g;
A2:   LSeg(p1,p2) = {p1} by A1,TOPREAL1:7;
A3:   rng g = {p1} by  FUNCOP_1:14;
      the carrier of (TOP-REAL n)|LSeg(p1,p2) = LSeg(p1,p2)
      by JORDAN1:1;
      then reconsider f = g as Function of I[01],(TOP-REAL n)|LSeg(p1,p2)
      by A2,A3,FUNCT_2:8;
      take f;
      thus thesis by A3,A1,TOPREAL1:7;
    end;
    suppose p1 <> p2;
      then LSeg(p1,p2) is_an_arc_of p1,p2 by TOPREAL1:15;
      hence thesis by Th51;
    end;
  end;

theorem Th53:
  for p1,p2,q1,q2 being Point of TOP-REAL 2
  st P is_an_arc_of p1,p2 & q1 in P & q2 in P &
  q1 <> p1 & q1 <> p2 & q2 <> p1 & q2 <> p2
  ex f being Path of q1,q2 st rng f c= P & rng f misses {p1,p2}
  proof
    let p1,p2,q1,q2 be Point of TOP-REAL 2 such that
A1: P is_an_arc_of p1,p2 and
A2: q1 in P & q2 in P and
A3: q1 <> p1 & q1 <> p2 & q2 <> p1 & q2 <> p2;
    per cases;
    suppose q1 = q2;
      then reconsider f = I[01] --> q1 as Path of q1,q2 by Th50;
      take f;
A4:   rng f = {q1} by  FUNCOP_1:14;
      thus rng f c= P
      proof
        let x be set;
        assume x in rng f;
        hence thesis by A2,A4,TARSKI:def 1;
      end;
      not p1 in {q1} & not p2 in {q1} by A3,TARSKI:def 1;
      hence thesis by A4,ZFMISC_1:57;
    end;
    suppose q1 <> q2;
      then consider Q being non empty Subset of T2 such that
A5:   Q is_an_arc_of q1,q2 and
A6:   Q c= P & Q misses {p1,p2} by A1,A2,A3,JORDAN18:19;
      consider g being Path of q1,q2, f being Function of I[01], T2|Q such that
A7:   rng f = Q & g = f by A5,Th51;
      reconsider h = f as Function of I[01],T2 by TOPREALA:28;
      the carrier of T2|Q = Q by JORDAN1:1;
      then reconsider z1 = q1, z2 = q2 as Point of T2|Q by A5,TOPREAL1:4;
A8:   z1,z2 are_connected
      proof
        take f;
        thus f is continuous by A7,TOPMETR:8;
        thus f.0 = z1 & f.1 = z2 by A7,BORSUK_2:def 4;
      end;
      f is Path of z1,z2
      proof
        thus z1,z2 are_connected by A8;
        thus f is continuous by A7,TOPMETR:8;
        thus f.0 = z1 & f.1 = z2 by A7,BORSUK_2:def 4;
      end;
      then reconsider h as Path of q1,q2 by A8,TOPALG_2:1;
      take h;
      thus thesis by A6,A7;
    end;
  end;

begin :: Rectangles

theorem Th54:
  a <= b & c <= d implies
  rectangle(a,b,c,d) c= closed_inside_of_rectangle(a,b,c,d)
  proof
    assume
A1: a <= b & c <= d;
    let x be set;
    assume x in rectangle(a,b,c,d);
    then x in {p: p`1 = a & p`2 <= d & p`2 >= c or
    p`1 <= b & p`1 >= a & p`2 = d or
    p`1 <= b & p`1 >= a & p`2 = c or
    p`1 = b & p`2 <= d & p`2 >= c} by A1,SPPOL_2:58;
    then ex p st x = p & (p`1 = a & p`2 <= d & p`2 >= c or
    p`1 <= b & p`1 >= a & p`2 = d or
    p`1 <= b & p`1 >= a & p`2 = c or
    p`1 = b & p`2 <= d & p`2 >= c);
   hence thesis by A1;
  end;

theorem Th55:
  inside_of_rectangle(a,b,c,d) c= closed_inside_of_rectangle(a,b,c,d)
  proof
    let x be set;
    assume x in inside_of_rectangle(a,b,c,d);
    then ex p st x = p & (a < p`1 & p`1 < b & c < p`2 & p`2 < d);
    hence thesis;
  end;

theorem Th56:
  closed_inside_of_rectangle(a,b,c,d) = outside_of_rectangle(a,b,c,d)`
  proof
    set R = closed_inside_of_rectangle(a,b,c,d);
    set O = outside_of_rectangle(a,b,c,d);
    thus R c= O`
    proof
      let x be set;
      assume x in R;
      then consider p such that
A1:   x = p and
A2:   a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d;
      now
        assume p in O;
        then ex p1 st p1 = p &
        not(a <= p1`1 & p1`1 <= b & c <= p1`2 & p1`2 <= d);
        hence contradiction by A2;
      end;
      hence thesis by A1,SUBSET_1:50;
    end;
    let x be set;
    assume
A3: x in O`;
    then
A4: not x in O by SUBSET_1:54;
    reconsider x as Point of T2 by A3;
    a <= x`1 & x`1 <= b & c <= x`2 & x`2 <= d by A4;
    hence thesis;
  end;

registration
  let a, b, c, d be real number;
  cluster closed_inside_of_rectangle(a,b,c,d) -> closed;
  coherence
  proof
    set P2 = outside_of_rectangle(a,b,c,d);
A1: a is Real & b is Real & c is Real & d is Real by XREAL_0:def 1;
    reconsider P2 as open Subset of T2 by A1,JORDAN1:39;
    P2` is closed;
    hence thesis by Th56;
  end;
end;

theorem Th57:
  closed_inside_of_rectangle(a,b,c,d) misses outside_of_rectangle(a,b,c,d)
  proof
    set P = rectangle(a,b,c,d);
    set R = closed_inside_of_rectangle(a,b,c,d);
    set P1 = inside_of_rectangle(a,b,c,d);
    set P2 = outside_of_rectangle(a,b,c,d);
    assume R meets P2;
    then consider x being set such that
A1: x in R and
A2: x in P2 by XBOOLE_0:3;
A3: ex p st x = p & a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d by A1;
    ex p st x = p & not (a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d) by A2;
    hence thesis by A3;
  end;

theorem Th58:
  closed_inside_of_rectangle(a,b,c,d) /\ inside_of_rectangle(a,b,c,d)
  = inside_of_rectangle(a,b,c,d)
  proof
    set R = closed_inside_of_rectangle(a,b,c,d);
    set P1 = inside_of_rectangle(a,b,c,d);
    thus R /\ P1 c= P1 by XBOOLE_1:17;
    P1 c= R by Th55;
    then P1 /\ P1 c= P1 /\ R by XBOOLE_1:26;
    hence thesis;
  end;

theorem Th59:
  a < b & c < d implies
  Int closed_inside_of_rectangle(a,b,c,d) = inside_of_rectangle(a,b,c,d)
  proof
    assume
A1: a < b & c < d;
    set P = rectangle(a,b,c,d);
    set R = closed_inside_of_rectangle(a,b,c,d);
    set P1 = inside_of_rectangle(a,b,c,d);
    set P2 = outside_of_rectangle(a,b,c,d);
A2: a is Real & b is Real & c is Real & d is Real by XREAL_0:def 1;
A3: P = {p where p is Point of T2: p`1 = a & p`2 <= d & p`2 >= c or
    p`1 <= b & p`1 >= a & p`2 = d or
    p`1 <= b & p`1 >= a & p`2 = c or
    p`1 = b & p`2 <= d & p`2 >= c} by A1,SPPOL_2:58;
A4: R misses P2 by Th57;
    thus Int R = (Cl P2``)` by Th56
    .= (P2 \/ P)` by A1,A2,A3,JORDAN1:49
    .= P2` /\ P` by SUBSET_1:29
    .= R /\ P` by Th56
    .= R /\ (P1 \/ P2) by A1,A2,A3,JORDAN1:41
    .= R /\ P1 \/ R /\ P2 by XBOOLE_1:23
    .= R /\ P1 \/ {} by A4,XBOOLE_0:def 7
    .= P1 by Th58;
  end;

theorem Th60:
  a <= b & c <= d implies
  closed_inside_of_rectangle(a,b,c,d) \ inside_of_rectangle(a,b,c,d)
  = rectangle(a,b,c,d)
  proof
    assume
A1: a <= b & c <= d;
    set R = rectangle(a,b,c,d);
    set P = closed_inside_of_rectangle(a,b,c,d);
    set P1 = inside_of_rectangle(a,b,c,d);
    set P2 = outside_of_rectangle(a,b,c,d);
A2: R = {p where p is Point of T2: p`1 = a & p`2 <= d & p`2 >= c or
    p`1 <= b & p`1 >= a & p`2 = d or
    p`1 <= b & p`1 >= a & p`2 = c or
    p`1 = b & p`2 <= d & p`2 >= c} by A1,SPPOL_2:58;
    thus P \ P1 c= R
    proof
      let x be set;
      assume
A3:   x in P \ P1;
      then
A4:   not x in P1 by XBOOLE_0:def 4;
      x in P by A3,XBOOLE_0:def 4;
      then consider p such that
A5:   x = p and
A6:   a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d;
      not (a < p`1 & p`1 < b & c < p`2 & p`2 < d) by A5,A4;
      then p`1 = a & p`2 <= d & p`2 >= c or
      p`1 <= b & p`1 >= a & p`2 = d or
      p`1 <= b & p`1 >= a & p`2 = c or
      p`1 = b & p`2 <= d & p`2 >= c by A6,XREAL_1:1;
      hence thesis by A5,A2;
    end;
    let x be set;
    assume
A7: x in R;
    then
A8: ex p st p = x & (p`1 = a & p`2 <= d & p`2 >= c or
    p`1 <= b & p`1 >= a & p`2 = d or
    p`1 <= b & p`1 >= a & p`2 = c or
    p`1 = b & p`2 <= d & p`2 >= c) by A2;
A9: R c= P by A1,Th54;
    now
      assume x in P1;
      then ex p st x = p & a < p`1 & p`1 < b & c < p`2 & p`2 < d;
      hence contradiction by A8;
    end;
    hence thesis by A7,A9,XBOOLE_0:def 4;
  end;

theorem Th61:
  a < b & c < d implies
  Fr closed_inside_of_rectangle(a,b,c,d) = rectangle(a,b,c,d)
  proof
    assume
A1: a < b & c < d;
    set P = closed_inside_of_rectangle(a,b,c,d);
    thus Fr P = P \ Int P by TOPS_1:77
    .= P \ inside_of_rectangle(a,b,c,d) by A1,Th59
    .= rectangle(a,b,c,d) by A1,Th60;
  end;

theorem
  a <= b & c <= d implies W-bound closed_inside_of_rectangle(a,b,c,d) = a
  proof
    assume
A1: a <= b & c <= d;
    set X = closed_inside_of_rectangle(a,b,c,d);
    reconsider Z = (proj1||X).:the carrier of (T2|X)
    as Subset of REAL;
A2: X = the carrier of (T2|X) by JORDAN1:1;
A3: |[a,c]| in X by A1,TOPREALA:52;
A4: for p be real number st p in Z holds p >= a
    proof
      let p be real number;
      assume p in Z;
      then consider p0 being set such that
A5:   p0 in the carrier of T2|X and
      p0 in the carrier of T2|X and
A6:   p = (proj1||X).p0 by FUNCT_2:115;
      ex p1 st p0 = p1 &
      a <= p1`1 & p1`1 <= b & c <= p1`2 & p1`2 <= d by A2,A5;
      hence thesis by A2,A5,A6,PSCOMP_1:69;
    end;
A7: for q being real number st
    for p being real number st p in Z holds p >= q holds a >= q
    proof
      let q be real number such that
A8:   for p being real number st p in Z holds p >= q;
A9:   |[a,c]|`1 = a & |[a,c]|`2 = c by EUCLID:56;
      (proj1||X). |[a,c]| = |[a,c]|`1 by A3,PSCOMP_1:69;
      then a in Z by A2,A3,A9,FUNCT_2:43;
      hence thesis by A8;
    end;
    dom(proj1||X) = X by A2,FUNCT_2:def 1;
    then Z is non empty by A2,A3,RELAT_1:152;
   hence W-bound X = a by A4,A7,SEQ_4:61;
  end;

theorem
  a <= b & c <= d implies S-bound closed_inside_of_rectangle(a,b,c,d) = c
  proof
    assume
A1: a <= b & c <= d;
    set X = closed_inside_of_rectangle(a,b,c,d);
    reconsider Z = (proj2||X).:the carrier of (T2|X)
    as Subset of REAL;
A2: X = the carrier of (T2|X) by JORDAN1:1;
A3: |[a,c]| in X by A1,TOPREALA:52;
A4: for p be real number st p in Z holds p >= c
    proof
      let p be real number;
      assume p in Z;
      then consider p0 being set such that
A5:   p0 in the carrier of T2|X and
      p0 in the carrier of T2|X and
A6:   p = (proj2||X).p0 by FUNCT_2:115;
      ex p1 st p0 = p1 &
      a <= p1`1 & p1`1 <= b & c <= p1`2 & p1`2 <= d by A2,A5;
      hence thesis by A2,A5,A6,PSCOMP_1:70;
    end;
A7: for q being real number st
    for p being real number st p in Z holds p >= q holds c >= q
    proof
      let q be real number such that
A8:   for p being real number st p in Z holds p >= q;
A9:   |[a,c]|`1 = a & |[a,c]|`2 = c by EUCLID:56;
      (proj2||X). |[a,c]| = |[a,c]|`2 by A3,PSCOMP_1:70;
      then c in Z by A2,A3,A9,FUNCT_2:43;
      hence thesis by A8;
    end;
    dom(proj2||X) = X by A2,FUNCT_2:def 1;
    then Z is non empty by A2,A3,RELAT_1:152;
   hence S-bound X = c by A4,A7,SEQ_4:61;
  end;

theorem
  a <= b & c <= d implies E-bound closed_inside_of_rectangle(a,b,c,d) = b
  proof
    assume
A1: a <= b & c <= d;
    set X = closed_inside_of_rectangle(a,b,c,d);
    reconsider Z = (proj1||X).:the carrier of (T2|X)
    as Subset of REAL;
A2: X = the carrier of (T2|X) by JORDAN1:1;
A3: for p be real number st p in Z holds p <= b
    proof
      let p be real number;
      assume p in Z;
      then consider p0 being set such that
A4:   p0 in the carrier of T2|X and
      p0 in the carrier of T2|X and
A5:   p = (proj1||X).p0 by FUNCT_2:115;
      ex p1 st p0 = p1 &
      a <= p1`1 & p1`1 <= b & c <= p1`2 & p1`2 <= d by A2,A4;
      hence thesis by A2,A4,A5,PSCOMP_1:69;
    end;
A6: for q being real number st
    for p being real number st p in Z holds p <= q holds b <= q
    proof
      let q be real number such that
A7:   for p being real number st p in Z holds p <= q;
A8:   |[b,d]|`1 = b & |[b,d]|`2 = d by EUCLID:56;
      then
A9:   |[b,d]| in X by A1;
      then (proj1||X). |[b,d]| = |[b,d]|`1 by PSCOMP_1:69;
      then b in Z by A2,A9,A8,FUNCT_2:43;
      hence thesis by A7;
    end;
A10: |[a,c]| in X by A1,TOPREALA:52;
    dom(proj1||X) = X by A2,FUNCT_2:def 1;
    then Z is non empty by A2,A10,RELAT_1:152;
   hence E-bound X = b by A3,A6,SEQ_4:63;
  end;

theorem
  a <= b & c <= d implies N-bound closed_inside_of_rectangle(a,b,c,d) = d
  proof
    assume
A1: a <= b & c <= d;
    set X = closed_inside_of_rectangle(a,b,c,d);
    reconsider Z = (proj2||X).:the carrier of (T2|X)
    as Subset of REAL;
A2: X = the carrier of (T2|X) by JORDAN1:1;
A3: for p be real number st p in Z holds p <= d
    proof
      let p be real number;
      assume p in Z;
      then consider p0 being set such that
A4:   p0 in the carrier of T2|X and
      p0 in the carrier of T2|X and
A5:   p = (proj2||X).p0 by FUNCT_2:115;
      ex p1 st p0 = p1 &
      a <= p1`1 & p1`1 <= b & c <= p1`2 & p1`2 <= d by A2,A4;
      hence thesis by A2,A4,A5,PSCOMP_1:70;
    end;
A6: for q being real number st
    for p being real number st p in Z holds p <= q holds d <= q
    proof
      let q be real number such that
A7:   for p being real number st p in Z holds p <= q;
A8:   |[b,d]|`1 = b & |[b,d]|`2 = d by EUCLID:56;
      then
A9:   |[b,d]| in X by A1;
      then (proj2||X). |[b,d]| = |[b,d]|`2 by PSCOMP_1:70;
      then d in Z by A2,A9,A8,FUNCT_2:43;
      hence thesis by A7;
    end;
A10: |[a,c]| in X by A1,TOPREALA:52;
    dom(proj2||X) = X by A2,FUNCT_2:def 1;
    then Z is non empty by A2,A10,RELAT_1:152;
   hence N-bound X = d by A3,A6,SEQ_4:63;
  end;

theorem Th66:
  a < b & c < d &
  p1 in closed_inside_of_rectangle(a,b,c,d) &
  not p2 in closed_inside_of_rectangle(a,b,c,d) &
  P is_an_arc_of p1,p2 implies
  Segment(P,p1,p2,p1,First_Point(P,p1,p2,rectangle(a,b,c,d))) c=
  closed_inside_of_rectangle(a,b,c,d)
  proof
    set R = closed_inside_of_rectangle(a,b,c,d);
    set dR = rectangle(a,b,c,d);
    set n = First_Point(P,p1,p2,dR);
    assume that
A1: a < b & c < d and
A2: p1 in R and
A3: not p2 in R and
A4: P is_an_arc_of p1,p2;
    let x be set;
    assume that
A5: x in Segment(P,p1,p2,p1,n) and
A6: not x in R;
    reconsider x as Point of T2 by A5;
A7: Fr R = dR by A1,Th61;
A8: P is connected by A4,JORDAN6:11;
     p1 in P by A4,TOPREAL1:4;
     then
A9: P meets R by A2,XBOOLE_0:3;
     p2 in P by A4,TOPREAL1:4;
     then P \ R <> {}T2 by A3,XBOOLE_0:def 4;
     then
A10:  P meets dR by A7,A8,A9,CONNSP_1:23;
     P is closed by A4,JORDAN6:12;
     then
A11:  P /\ dR is closed by TOPS_1:35;
     then
A12:  n in P /\ dR by A10,A4,JORDAN5C:def 1;
     per cases;
     suppose x = n;
       then
A13:    x in dR by A12,XBOOLE_0:def 3;
       dR c= R by A1,Th54;
       hence thesis by A13,A6;
     end;
    suppose
A14:   x <> n;
      reconsider P as non empty Subset of T2 by A4,TOPREAL1:4;
      consider f being Function of I[01], T2|P such that
A15:   f is_homeomorphism and
A16:   f.0 = p1 & f.1 = p2 by A4,TOPREAL1:def 2;
A17:   rng f = [#](T2|P) by A15,TOPS_2:def 5
      .= P by PRE_TOPC:def 10;
      n in P by A12,XBOOLE_0:def 3;
      then consider na being set such that
A18:  na in dom f and
A19:   f.na = n by A17,FUNCT_1:def 5;
      na is real by A18,MEMBERED:def 2;
      then reconsider na as Real by XREAL_0:def 1;
A20:   0 <= na & na <= 1 by A18,JORDAN5A:2;
A21:  Segment(P,p1,p2,p1,n) c= P by JORDAN16:5;
      then consider xa being set such that
A22:  xa in dom f and
A23:  f.xa = x by A17,A5,FUNCT_1:def 5;
      xa is real by A22,MEMBERED:def 2;
      then reconsider xa as Real by XREAL_0:def 1;
A24:  0 <= xa & xa <= 1 by A22,JORDAN5A:2;
A25:  Segment(P,p1,p2,p1,x) is_an_arc_of p1,x by A4,A21,A2,A6,A5,JORDAN20:20;
      then
A26:  Segment(P,p1,p2,p1,x) is connected by JORDAN6:11;
      p1 in Segment(P,p1,p2,p1,x) by A25,TOPREAL1:4;
      then
A27:  Segment(P,p1,p2,p1,x) meets R by A2,XBOOLE_0:3;
      x in Segment(P,p1,p2,p1,x) by A25,TOPREAL1:4;
      then Segment(P,p1,p2,p1,x) \ R <> {}T2 by A6,XBOOLE_0:def 4;
      then Segment(P,p1,p2,p1,x) meets Fr R by A26,A27,CONNSP_1:23;
      then consider z being set such that
A28:  z in Segment(P,p1,p2,p1,x) and
A29:  z in dR by A7,XBOOLE_0:3;
      reconsider z as Point of T2 by A28;
      Segment(P,p1,p2,p1,x) = {p: LE p1,p,P,p1,p2 & LE p,x,P,p1,p2}
      by JORDAN6:29;
      then consider zz being Point of T2 such that
A30:  zz = z and
      LE p1,zz,P,p1,p2 and
A31:  LE zz,x,P,p1,p2 by A28;
      Segment(P,p1,p2,p1,x) c= P by JORDAN16:5;
      then consider za being set such that
A32:  za in dom f and
A33:  f.za = z by A17,A28,FUNCT_1:def 5;
      za is real by A32,MEMBERED:def 2;
      then reconsider za as Real by XREAL_0:def 1;
A34:  0 <= za & za <= 1 by A32,JORDAN5A:2;
A35:  na <= za by A29,A33,A34,A19,A20,A10,A11,A15,A16,A4,JORDAN5C:def 1;
A36:  za <= xa by A15,A16,A31,A30,A33,A34,A23,A24,JORDAN5C:def 3;
      Segment(P,p1,p2,p1,n) = {p: LE p1,p,P,p1,p2 & LE p,n,P,p1,p2}
      by JORDAN6:29;
      then consider xx being Point of T2 such that
A37:  xx = x and
      LE p1,xx,P,p1,p2 and
A38:  LE xx,n,P,p1,p2 by A5;
      xa <= na by A15,A16,A38,A37,A23,A24,A19,A20,JORDAN5C:def 3;
      then xa < na by A14,A19,A23,REAL_1:def 5;
      hence thesis by A35,A36,XREAL_1:2;
    end;
  end;

begin :: Some useful functions

definition
  let S, T be non empty TopSpace,
  x be Point of [:S,T:];
  redefine func x`1 -> Element of S;
  coherence
  proof
    the carrier of [:S,T:] = [:the carrier of S, the carrier of T:]
    by BORSUK_1:def 5;
    hence thesis by MCART_1:10;
  end;
  redefine func x`2 -> Element of T;
  coherence
  proof
    the carrier of [:S,T:] = [:the carrier of S, the carrier of T:]
    by BORSUK_1:def 5;
    hence thesis by MCART_1:10;
  end;
end;

definition
  let o be Point of TOP-REAL 2;
  func diffX2_1(o) -> RealMap of [:TOP-REAL 2,TOP-REAL 2:] means :Def1:
  for x being Point of [:TOP-REAL 2,TOP-REAL 2:] holds it.x = x`2`1 - o`1;
  existence
  proof
    deffunc F(Point of [:T2,T2:]) = $1`2`1 - o`1;
    consider xo being RealMap of [:T2,T2:] such that
A1: for x being Point of [:T2,T2:] holds xo.x = F(x) from FUNCT_2:sch 4;
    take xo;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let f, g be RealMap of [:T2,T2:] such that
A2: for x being Point of [:T2,T2:] holds f.x = x`2`1 - o`1 and
A3: for x being Point of [:T2,T2:] holds g.x = x`2`1 - o`1;
    now
      let x be Point of [:T2,T2:];
      thus f.x = x`2`1 - o`1 by A2
      .= g.x by A3;
    end;
    hence thesis by FUNCT_2:113;
  end;
  func diffX2_2(o) -> RealMap of [:TOP-REAL 2,TOP-REAL 2:] means :Def2:
  for x being Point of [:TOP-REAL 2,TOP-REAL 2:] holds it.x = x`2`2 - o`2;
  existence
  proof
    deffunc F(Point of [:T2,T2:]) = $1`2`2 - o`2;
    consider xo being RealMap of [:T2,T2:] such that
A4: for x being Point of [:T2,T2:] holds xo.x = F(x) from FUNCT_2:sch 4;
    take xo;
    thus thesis by A4;
  end;
  uniqueness
  proof
    let f, g be RealMap of [:T2,T2:] such that
A5: for x being Point of [:T2,T2:] holds f.x = x`2`2 - o`2 and
A6: for x being Point of [:T2,T2:] holds g.x = x`2`2 - o`2;
    now
      let x be Point of [:T2,T2:];
      thus f.x = x`2`2 - o`2 by A5
      .= g.x by A6;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

definition
  func diffX1_X2_1 -> RealMap of [:TOP-REAL 2, TOP-REAL 2:] means :Def3:
  for x being Point of [:TOP-REAL 2, TOP-REAL 2:] holds it.x = x`1`1 - x`2`1;
  existence
  proof
    deffunc F(Point of [:T2,T2:]) = $1`1`1 - $1`2`1;
    consider xo being RealMap of [:T2,T2:] such that
A1: for x being Point of [:T2,T2:] holds xo.x = F(x) from FUNCT_2:sch 4;
    take xo;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let f, g be RealMap of [:T2,T2:] such that
A2: for x being Point of [:T2,T2:] holds f.x = x`1`1 - x`2`1 and
A3: for x being Point of [:T2,T2:] holds g.x = x`1`1 - x`2`1;
    now
      let x be Point of [:T2,T2:];
      thus f.x = x`1`1 - x`2`1 by A2
      .= g.x by A3;
    end;
    hence thesis by FUNCT_2:113;
  end;
  func diffX1_X2_2 -> RealMap of [:TOP-REAL 2, TOP-REAL 2:] means :Def4:
  for x being Point of [:TOP-REAL 2, TOP-REAL 2:] holds it.x = x`1`2 - x`2`2;
  existence
  proof
    deffunc F(Point of [:T2,T2:]) = $1`1`2 - $1`2`2;
    consider xo being RealMap of [:T2,T2:] such that
A4: for x being Point of [:T2,T2:] holds xo.x = F(x) from FUNCT_2:sch 4;
    take xo;
    thus thesis by A4;
  end;
  uniqueness
  proof
    let f, g be RealMap of [:T2,T2:] such that
A5: for x being Point of [:T2,T2:] holds f.x = x`1`2 - x`2`2 and
A6: for x being Point of [:T2,T2:] holds g.x = x`1`2 - x`2`2;
    now
      let x be Point of [:T2,T2:];
      thus f.x = x`1`2 - x`2`2 by A5
      .= g.x by A6;
    end;
    hence thesis by FUNCT_2:113;
  end;
  func Proj2_1 -> RealMap of [:TOP-REAL 2, TOP-REAL 2:] means :Def5:
  for x being Point of [:TOP-REAL 2, TOP-REAL 2:] holds it.x = x`2`1;
  existence
  proof
    deffunc F(Point of [:T2,T2:]) = $1`2`1;
    consider xo being RealMap of [:T2,T2:] such that
A7: for x being Point of [:T2,T2:] holds xo.x = F(x) from FUNCT_2:sch 4;
    take xo;
    thus thesis by A7;
  end;
  uniqueness
  proof
    let f, g be RealMap of [:T2,T2:] such that
A8: for x being Point of [:T2,T2:] holds f.x = x`2`1 and
A9: for x being Point of [:T2,T2:] holds g.x = x`2`1;
    now
      let x be Point of [:T2,T2:];
      thus f.x = x`2`1 by A8
      .= g.x by A9;
    end;
    hence thesis by FUNCT_2:113;
  end;
  func Proj2_2 -> RealMap of [:TOP-REAL 2, TOP-REAL 2:] means :Def6:
  for x being Point of [:TOP-REAL 2, TOP-REAL 2:] holds it.x = x`2`2;
  existence
  proof
    deffunc F(Point of [:T2,T2:]) = $1`2`2;
    consider xo being RealMap of [:T2,T2:] such that
A10: for x being Point of [:T2,T2:] holds xo.x = F(x) from FUNCT_2:sch 4;
    take xo;
    thus thesis by A10;
  end;
  uniqueness
  proof
    let f, g be RealMap of [:T2,T2:] such that
A11: for x being Point of [:T2,T2:] holds f.x = x`2`2 and
A12: for x being Point of [:T2,T2:] holds g.x = x`2`2;
    now
      let x be Point of [:T2,T2:];
      thus f.x = x`2`2 by A11
      .= g.x by A12;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

theorem Th67:
  for o being Point of TOP-REAL 2 holds
  diffX2_1(o) is continuous Function of [:TOP-REAL 2, TOP-REAL 2:], R^1
  proof
    let o be Point of TOP-REAL 2;
    reconsider Xo = diffX2_1(o) as Function of [:T2,T2:],R^1 by TOPMETR:24;
  for p being Point of [:T2,T2:], V being Subset of R^1
   st Xo.p in V & V is open holds
    ex W being Subset of [:T2,T2:] st p in W & W is open & Xo.:W c= V
  proof
    let p be Point of [:T2,T2:],
        V be Subset of R^1 such that
A1:   Xo.p in V and
A2:   V is open;
A3:   Xo.p = p`2`1 - o`1 by Def1;
    set r = p`2`1 - o`1;
    reconsider V1 = V as open Subset of REAL by A2,TOPMETR:24,BORSUK_5:62;
    consider g being real number such that
A4:   0 < g and
A5:   ].r-g,r+g.[ c= V1 by A1,A3,RCOMP_1:40;
    reconsider g as Element of REAL by XREAL_0:def 1;
    set W2 = {|[x,y]| where x, y is Element of REAL:
    p`2`1-g < x & x < p`2`1+g};
    W2 c= the carrier of T2
    proof
      let a be set;
      assume a in W2;
      then ex x, y being Element of REAL st
        a = |[x,y]| & p`2`1-g < x & x < p`2`1+g;
      hence thesis;
    end;
    then reconsider W2 as Subset of T2;
    take [:[#]T2,W2:];
A6: p`2 = |[p`2`1,p`2`2]| by EUCLID:57;
A7: p = [p`1,p`2] by Lm7,MCART_1:23;
    p`2`1-g < p`2`1-0 & p`2`1+0 < p`2`1+g by A4,REAL_1:92,XREAL_1:8;
    then
    p`2 in W2 by A6;
    hence p in [:[#]T2,W2:] by A7,Lm8,ZFMISC_1:def 2;
    W2 is open by PSCOMP_1:66;
    hence [:[#]T2,W2:] is open by BORSUK_1:46;
    let b be set;
    assume b in Xo.:[:[#]T2,W2:];
    then consider a being Point of [:T2,T2:] such that
A8:  a in [:[#]T2,W2:] and
A9:  Xo.a = b by FUNCT_2:116;
A10:  a = [a`1,a`2] by Lm7,MCART_1:23;
A11:  (diffX2_1(o)).a = a`2`1 - o`1 by Def1;
    a`2 in W2 by A8,A10,ZFMISC_1:106;
    then consider x2, y2 being Element of REAL such that
A12:  a`2 = |[x2,y2]| and
A13:  p`2`1-g < x2 & x2 < p`2`1+g;
    a`2`1 = x2 by A12,EUCLID:56;
    then p`2`1 - g - o`1 < a`2`1 - o`1 & a`2`1 - o`1 < p`2`1 + g - o`1
      by A13,XREAL_1:11;
    then a`2`1 - o`1 in ].r-g,r+g.[ by RCOMP_1:47;
    hence b in V by A5,A9,A11;
  end;
  hence thesis by JGRAPH_2:20;
end;

theorem Th68:
  for o being Point of TOP-REAL 2 holds
  diffX2_2(o) is continuous Function of [:TOP-REAL 2, TOP-REAL 2:], R^1
  proof
    let o be Point of TOP-REAL 2;
    reconsider Yo = diffX2_2(o) as Function of [:T2,T2:],R^1 by TOPMETR:24;
  for p being Point of [:T2,T2:], V being Subset of R^1
   st Yo.p in V & V is open holds
    ex W being Subset of [:T2,T2:] st p in W & W is open & Yo.:W c= V
  proof
    let p be Point of [:T2,T2:],
        V be Subset of R^1 such that
A1:   Yo.p in V and
A2:   V is open;
A3:   p = [p`1,p`2] by Lm7,MCART_1:23;
A4:   Yo.p = p`2`2 - o`2 by Def2;
    set r = p`2`2 - o`2;
    reconsider V1 = V as open Subset of REAL by A2,TOPMETR:24,BORSUK_5:62;
    consider g being real number such that
A5:   0 < g and
A6:   ].r-g,r+g.[ c= V1 by A1,A4,RCOMP_1:40;
    reconsider g as Element of REAL by XREAL_0:def 1;
    set W2 = {|[x,y]| where x, y is Element of REAL:
    p`2`2-g < y & y < p`2`2+g};
    W2 c= the carrier of T2
    proof
      let a be set;
      assume a in W2;
      then ex x, y being Element of REAL st
        a = |[x,y]| & p`2`2-g < y & y < p`2`2+g;
      hence thesis;
    end;
    then reconsider W2 as Subset of T2;
    take [:[#]T2,W2:];
A7: p`2 = |[p`2`1,p`2`2]| by EUCLID:57;
    p`2`2-g < p`2`2-0 & p`2`2+0 < p`2`2+g by A5,REAL_1:92,XREAL_1:8;
    then
    p`2 in W2 by A7;
    hence p in [:[#]T2,W2:] by A3,Lm8,ZFMISC_1:def 2;
    W2 is open by PSCOMP_1:68;
    hence [:[#]T2,W2:] is open by BORSUK_1:46;
    let b be set;
    assume b in Yo.:[:[#]T2,W2:];
    then consider a being Point of [:T2,T2:] such that
A8:  a in [:[#]T2,W2:] and
A9:  Yo.a = b by FUNCT_2:116;
A10:  a = [a`1,a`2] by Lm7,MCART_1:23;
A11:  (diffX2_2(o)).a = a`2`2 - o`2 by Def2;
    a`2 in W2 by A8,A10,ZFMISC_1:106;
    then consider x2, y2 being Element of REAL such that
A12:  a`2 = |[x2,y2]| and
A13:  p`2`2-g < y2 & y2 < p`2`2+g;
    a`2`2 = y2 by A12,EUCLID:56;
    then p`2`2 - g - o`2 < a`2`2 - o`2 & a`2`2 - o`2 < p`2`2 + g - o`2
    by A13,XREAL_1:11;
    then a`2`2 - o`2 in ].r-g,r+g.[ by RCOMP_1:47;
    hence b in V by A6,A9,A11;
  end;
  hence thesis by JGRAPH_2:20;
  end;

theorem Th69:
  diffX1_X2_1 is continuous Function of [:TOP-REAL 2, TOP-REAL 2:], R^1
  proof
    reconsider Dx = diffX1_X2_1 as Function of [:T2,T2:],R^1 by TOPMETR:24;
  for p being Point of [:T2,T2:], V being Subset of R^1
   st Dx.p in V & V is open holds
    ex W being Subset of [:T2,T2:] st p in W & W is open & Dx.:W c= V
  proof
    let p be Point of [:T2,T2:],
        V be Subset of R^1 such that
A1:   Dx.p in V and
A2:   V is open;
A3: p = [p`1,p`2] by Lm7,MCART_1:23;
A4: diffX1_X2_1.p = p`1`1 - p`2`1 by Def3;
    set r = p`1`1 - p`2`1;
    reconsider V1 = V as open Subset of REAL by A2,TOPMETR:24,BORSUK_5:62;
    consider g being real number such that
A5:   0 < g and
A6:   ].r-g,r+g.[ c= V1 by A1,A4,RCOMP_1:40;
    reconsider g as Element of REAL by XREAL_0:def 1;
    set W1 = {|[x,y]| where x, y is Element of REAL:
      p`1`1-g/2 < x & x < p`1`1+g/2};
    set W2 = {|[x,y]| where x, y is Element of REAL:
      p`2`1-g/2 < x & x < p`2`1+g/2};
    W1 c= the carrier of T2
    proof
      let a be set;
      assume a in W1;
      then ex x, y being Element of REAL st
        a = |[x,y]| & p`1`1-g/2 < x & x < p`1`1+g/2;
      hence thesis;
    end;
    then reconsider W1 as Subset of T2;
    W2 c= the carrier of T2
    proof
      let a be set;
      assume a in W2;
      then ex x, y being Element of REAL st
        a = |[x,y]| & p`2`1-g/2 < x & x < p`2`1+g/2;
      hence thesis;
    end;
    then reconsider W2 as Subset of T2;
    take [:W1,W2:];
A7: p`1 = |[p`1`1,p`1`2]| by EUCLID:57;
A8: 0/2 < g/2 by A5,REAL_1:73;
    then p`1`1-g/2 < p`1`1-0 & p`1`1+0 < p`1`1+g/2 by REAL_1:92,XREAL_1:8;
    then
A9: p`1 in W1 by A7;
A10: p`2 = |[p`2`1,p`2`2]| by EUCLID:57;
    p`2`1-g/2 < p`2`1-0 & p`2`1+0 < p`2`1+g/2 by A8,REAL_1:92,XREAL_1:8;
    then
    p`2 in W2 by A10;
    hence p in [:W1,W2:] by A9,A3,ZFMISC_1:def 2;
    W1 is open & W2 is open by PSCOMP_1:66;
    hence [:W1,W2:] is open by BORSUK_1:46;
    let b be set;
    assume b in Dx.:[:W1,W2:];
    then consider a being Point of [:T2,T2:] such that
A11:  a in [:W1,W2:] and
A12:  Dx.a = b by FUNCT_2:116;
A13:  a = [a`1,a`2] by Lm7,MCART_1:23;
A14:  diffX1_X2_1.a = a`1`1 - a`2`1 by Def3;
    a`1 in W1 by A11,A13,ZFMISC_1:106;
    then consider x1, y1 being Element of REAL such that
A15:  a`1 = |[x1,y1]| and
A16:  p`1`1-g/2 < x1 and
A17:  x1 < p`1`1+g/2;
A18: a`1`1 = x1 by A15,EUCLID:56;
    p`1`1-g/2+g/2 < x1+g/2 & p`1`1-x1 > p`1`1-(p`1`1+g/2)
    by A16,A17,REAL_1:92,XREAL_1:8;
    then p`1`1-x1 < x1+g/2-x1 & p`1`1-x1 > -g/2 by XREAL_1:11;
    then
A19: abs(p`1`1-x1) < g/2 by SEQ_2:9;
    a`2 in W2 by A11,A13,ZFMISC_1:106;
    then consider x2, y2 being Element of REAL such that
A20:  a`2 = |[x2,y2]| and
A21:  p`2`1-g/2 < x2 and
A22:  x2 < p`2`1+g/2;
A23: a`2`1 = x2 by A20,EUCLID:56;
    p`2`1-g/2+g/2 < x2+g/2 & p`2`1-x2 > p`2`1-(p`2`1+g/2)
    by A21,A22,REAL_1:92,XREAL_1:8;
    then p`2`1-x2 < x2+g/2-x2 & p`2`1-x2 > -g/2 by XREAL_1:11;
    then
    abs(p`2`1-x2) < g/2 by SEQ_2:9;
    then
A24: abs(p`1`1-x1)+abs(p`2`1-x2) < g/2+g/2 by A19,XREAL_1:10;
    abs(p`1`1-x1-(p`2`1-x2)) <= abs(p`1`1-x1)+abs(p`2`1-x2) by COMPLEX1:143;
    then abs-(p`1`1-x1-(p`2`1-x2)) <= abs(p`1`1-x1)+abs(p`2`1-x2)
    by COMPLEX1:138;
    then abs(x1-x2-r) < g by A24,XREAL_1:2;
    then a`1`1 - a`2`1 in ].r-g,r+g.[ by A18,A23,RCOMP_1:8;
    hence b in V by A6,A12,A14;
  end;
  hence thesis by JGRAPH_2:20;
  end;

theorem Th70:
  diffX1_X2_2 is continuous Function of [:TOP-REAL 2, TOP-REAL 2:], R^1
  proof
    reconsider Dy = diffX1_X2_2 as Function of [:T2,T2:],R^1 by TOPMETR:24;
  for p being Point of [:T2,T2:], V being Subset of R^1
   st Dy.p in V & V is open holds
    ex W being Subset of [:T2,T2:] st p in W & W is open & Dy.:W c= V
  proof
    let p be Point of [:T2,T2:],
        V be Subset of R^1 such that
A1:   Dy.p in V and
A2:   V is open;
A3:   p = [p`1,p`2] by Lm7,MCART_1:23;
A4:   diffX1_X2_2.p = p`1`2 - p`2`2 by Def4;
    set r = p`1`2 - p`2`2;
    reconsider V1 = V as open Subset of REAL by A2,TOPMETR:24,BORSUK_5:62;
    consider g being real number such that
A5:   0 < g and
A6:   ].r-g,r+g.[ c= V1 by A1,A4,RCOMP_1:40;
    reconsider g as Element of REAL by XREAL_0:def 1;
    set W1 = {|[x,y]| where x, y is Element of REAL:
      p`1`2-g/2 < y & y < p`1`2+g/2};
    set W2 = {|[x,y]| where x, y is Element of REAL:
      p`2`2-g/2 < y & y < p`2`2+g/2};
    W1 c= the carrier of T2
    proof
      let a be set;
      assume a in W1;
      then ex x, y being Element of REAL st
        a = |[x,y]| & p`1`2-g/2 < y & y < p`1`2+g/2;
      hence thesis;
    end;
    then reconsider W1 as Subset of T2;
    W2 c= the carrier of T2
    proof
      let a be set;
      assume a in W2;
      then ex x, y being Element of REAL st
        a = |[x,y]| & p`2`2-g/2 < y & y < p`2`2+g/2;
      hence thesis;
    end;
    then reconsider W2 as Subset of T2;
    take [:W1,W2:];
A7: p`1 = |[p`1`1,p`1`2]| by EUCLID:57;
A8: 0/2 < g/2 by A5,REAL_1:73;
    then p`1`2-g/2 < p`1`2-0 & p`1`2+0 < p`1`2+g/2 by REAL_1:92,XREAL_1:8;
    then
A9: p`1 in W1 by A7;
A10: p`2 = |[p`2`1,p`2`2]| by EUCLID:57;
    p`2`2-g/2 < p`2`2-0 & p`2`2+0 < p`2`2+g/2 by A8,REAL_1:92,XREAL_1:8;
    then
    p`2 in W2 by A10;
    hence p in [:W1,W2:] by A9,A3,ZFMISC_1:def 2;
    W1 is open & W2 is open by PSCOMP_1:68;
    hence [:W1,W2:] is open by BORSUK_1:46;
    let b be set;
    assume b in Dy.:[:W1,W2:];
    then consider a being Point of [:T2,T2:] such that
A11:  a in [:W1,W2:] and
A12:  Dy.a = b by FUNCT_2:116;
A13:  a = [a`1,a`2] by Lm7,MCART_1:23;
A14:  diffX1_X2_2.a = a`1`2 - a`2`2 by Def4;
    a`1 in W1 by A11,A13,ZFMISC_1:106;
    then consider x1, y1 being Element of REAL such that
A15:  a`1 = |[x1,y1]| and
A16:  p`1`2-g/2 < y1 and
A17:  y1 < p`1`2+g/2;
A18: a`1`2 = y1 by A15,EUCLID:56;
    p`1`2-g/2+g/2 < y1+g/2 & p`1`2-y1 > p`1`2-(p`1`2+g/2)
    by A16,A17,REAL_1:92,XREAL_1:8;
    then p`1`2-y1 < y1+g/2-y1 & p`1`2-y1 > -g/2 by XREAL_1:11;
    then
A19: abs(p`1`2-y1) < g/2 by SEQ_2:9;
    a`2 in W2 by A11,A13,ZFMISC_1:106;
    then consider x2, y2 being Element of REAL such that
A20:  a`2 = |[x2,y2]| and
A21:  p`2`2-g/2 < y2 and
A22:  y2 < p`2`2+g/2;
A23: a`2`2 = y2 by A20,EUCLID:56;
    p`2`2-g/2+g/2 < y2+g/2 & p`2`2-y2 > p`2`2-(p`2`2+g/2)
    by A21,A22,REAL_1:92,XREAL_1:8;
    then p`2`2-y2 < y2+g/2-y2 & p`2`2-y2 > -g/2 by XREAL_1:11;
    then
    abs(p`2`2-y2) < g/2 by SEQ_2:9;
    then
A24: abs(p`1`2-y1)+abs(p`2`2-y2) < g/2+g/2 by A19,XREAL_1:10;
    abs(p`1`2-y1-(p`2`2-y2)) <= abs(p`1`2-y1)+abs(p`2`2-y2) by COMPLEX1:143;
    then abs-(p`1`2-y1-(p`2`2-y2)) <= abs(p`1`2-y1)+abs(p`2`2-y2)
    by COMPLEX1:138;
    then abs(y1-y2-r) < g by A24,XREAL_1:2;
    then a`1`2 - a`2`2 in ].r-g,r+g.[ by A18,A23,RCOMP_1:8;
    hence b in V by A6,A12,A14;
  end;
  hence thesis by JGRAPH_2:20;
end;

theorem Th71:
  Proj2_1 is continuous Function of [:TOP-REAL 2, TOP-REAL 2:], R^1
  proof
    reconsider fX2 = Proj2_1 as Function of [:T2,T2:],R^1 by TOPMETR:24;
  for p being Point of [:T2,T2:], V being Subset of R^1
   st fX2.p in V & V is open holds
    ex W being Subset of [:T2,T2:] st p in W & W is open & fX2.:W c= V
  proof
    let p be Point of [:T2,T2:],
        V be Subset of R^1 such that
A1:   fX2.p in V and
A2:   V is open;
A3: p = [p`1,p`2] by Lm7,MCART_1:23;
A4: fX2.p = p`2`1 by Def5;
    reconsider V1 = V as open Subset of REAL by A2,TOPMETR:24,BORSUK_5:62;
    consider g being real number such that
A5:   0 < g and
A6:   ].p`2`1-g,p`2`1+g.[ c= V1 by A1,A4,RCOMP_1:40;
    reconsider g as Element of REAL by XREAL_0:def 1;
    set W1 = {|[x,y]| where x, y is Element of REAL:
    p`2`1-g < x & x < p`2`1+g};
    W1 c= the carrier of T2
    proof
      let a be set;
      assume a in W1;
      then ex x, y being Element of REAL st
        a = |[x,y]| & p`2`1-g < x & x < p`2`1+g;
      hence thesis;
    end;
    then reconsider W1 as Subset of T2;
    take [:[#]T2,W1:];
A7: p`2 = |[p`2`1,p`2`2]| by EUCLID:57;
    p`2`1-g < p`2`1-0 & p`2`1+0 < p`2`1+g by A5,REAL_1:92,XREAL_1:8;
    then p`2 in W1 by A7;
    hence p in [:[#]T2,W1:] by A3,Lm8,ZFMISC_1:def 2;
    W1 is open by PSCOMP_1:66;
    hence [:[#]T2,W1:] is open by BORSUK_1:46;
    let b be set;
    assume b in fX2.:[:[#]T2,W1:];
    then consider a being Point of [:T2,T2:] such that
A8:  a in [:[#]T2,W1:] and
A9:  fX2.a = b by FUNCT_2:116;
A10:  a = [a`1,a`2] by Lm7,MCART_1:23;
A11:  fX2.a = a`2`1 by Def5;
    a`2 in W1 by A8,A10,ZFMISC_1:106;
    then consider x1, y1 being Element of REAL such that
A12:  a`2 = |[x1,y1]| and
A13:  p`2`1-g < x1 and
A14:  x1 < p`2`1+g;
A15: a`2`1 = x1 by A12,EUCLID:56;
    p`2`1-g+g < x1+g & p`2`1-x1 > p`2`1-(p`2`1+g)
    by A13,A14,REAL_1:92,XREAL_1:8;
    then p`2`1-x1 < x1+g-x1 & p`2`1-x1 > -g by XREAL_1:11;
    then abs(p`2`1-x1) < g by SEQ_2:9;
    then abs-(p`2`1-x1) < g by COMPLEX1:138;
    then abs(x1-p`2`1) < g;
    then a`2`1 in ].p`2`1-g,p`2`1+g.[ by A15,RCOMP_1:8;
    hence b in V by A6,A9,A11;
  end;
  hence thesis by JGRAPH_2:20;
end;

theorem Th72:
  Proj2_2 is continuous Function of [:TOP-REAL 2, TOP-REAL 2:], R^1
  proof
    reconsider fY2 = Proj2_2 as Function of [:T2,T2:],R^1 by TOPMETR:24;
  for p being Point of [:T2,T2:], V being Subset of R^1
   st fY2.p in V & V is open holds
    ex W being Subset of [:T2,T2:] st p in W & W is open & fY2.:W c= V
  proof
    let p be Point of [:T2,T2:],
        V be Subset of R^1 such that
A1:   fY2.p in V and
A2:   V is open;
A3:   p = [p`1,p`2] by Lm7,MCART_1:23;
A4:   fY2.p = p`2`2 by Def6;
    reconsider V1 = V as open Subset of REAL by A2,TOPMETR:24,BORSUK_5:62;
    consider g being real number such that
A5:   0 < g and
A6:   ].p`2`2-g,p`2`2+g.[ c= V1 by A1,A4,RCOMP_1:40;
    reconsider g as Element of REAL by XREAL_0:def 1;
    set W1 = {|[x,y]| where x, y is Element of REAL:
    p`2`2-g < y & y < p`2`2+g};
    W1 c= the carrier of T2
    proof
      let a be set;
      assume a in W1;
      then ex x, y being Element of REAL st
        a = |[x,y]| & p`2`2-g < y & y < p`2`2+g;
      hence thesis;
    end;
    then reconsider W1 as Subset of T2;
    take [:[#]T2,W1:];
A7: p`2 = |[p`2`1,p`2`2]| by EUCLID:57;
    p`2`2-g < p`2`2-0 & p`2`2+0 < p`2`2+g by A5,REAL_1:92,XREAL_1:8;
    then p`2 in W1 by A7;
    hence p in [:[#]T2,W1:] by A3,Lm8,ZFMISC_1:def 2;
    W1 is open by PSCOMP_1:68;
    hence [:[#]T2,W1:] is open by BORSUK_1:46;
    let b be set;
    assume b in fY2.:[:[#]T2,W1:];
    then consider a being Point of [:T2,T2:] such that
A8:  a in [:[#]T2,W1:] and
A9:  fY2.a = b by FUNCT_2:116;
A10:  a = [a`1,a`2] by Lm7,MCART_1:23;
A11:  fY2.a = a`2`2 by Def6;
    a`2 in W1 by A8,A10,ZFMISC_1:106;
    then consider x1, y1 being Element of REAL such that
A12:  a`2 = |[x1,y1]| and
A13:  p`2`2-g < y1 and
A14:  y1 < p`2`2+g;
A15: a`2`2 = y1 by A12,EUCLID:56;
    p`2`2-g+g < y1+g & p`2`2-y1 > p`2`2-(p`2`2+g)
    by A13,A14,REAL_1:92,XREAL_1:8;
    then p`2`2-y1 < y1+g-y1 & p`2`2-y1 > -g by XREAL_1:11;
    then abs(p`2`2-y1) < g by SEQ_2:9;
    then abs-(p`2`2-y1) < g by COMPLEX1:138;
    then abs(y1-p`2`2) < g;
    then a`2`2 in ].p`2`2-g,p`2`2+g.[ by A15,RCOMP_1:8;
    hence b in V by A6,A9,A11;
  end;
  hence thesis by JGRAPH_2:20;
  end;

registration
  let o be Point of TOP-REAL 2;
  cluster diffX2_1(o) -> continuous;
  coherence
  proof
    diffX2_1(o) is continuous Function of [:T2,T2:],R^1 by Th67;
    hence thesis by TOPREAL6:83;
  end;
  cluster diffX2_2(o) -> continuous;
  coherence
  proof
    diffX2_2(o) is continuous Function of [:T2,T2:],R^1 by Th68;
    hence thesis by TOPREAL6:83;
  end;
end;

registration
  cluster diffX1_X2_1 -> continuous;
  coherence by Th69,TOPREAL6:83;
  cluster diffX1_X2_2 -> continuous;
  coherence by Th70,TOPREAL6:83;
  cluster Proj2_1 -> continuous;
  coherence by Th71,TOPREAL6:83;
  cluster Proj2_2 -> continuous;
  coherence by Th72,TOPREAL6:83;
end;

definition
  let n be non empty Nat, o, p be Point of TOP-REAL n,
  r be positive (real number) such that
A1: p is Point of Tdisk(o,r);
    set X = (TOP-REAL n)|(cl_Ball(o,r)\{p});
  func DiskProj(o,r,p) -> Function
 of (TOP-REAL n)|(cl_Ball(o,r)\{p}), Tcircle(o,r)
  means :Def7:
  for x being Point of (TOP-REAL n)|(cl_Ball(o,r)\{p})
  ex y being Point of TOP-REAL n st x = y & it.x = HC(p,y,o,r);
  existence
  proof
A2: the carrier of X = cl_Ball(o,r)\{p} by JORDAN1:1;
    defpred P[set,set] means
    ex z being Point of TOP-REAL n st $1 = z & $2 = HC(p,z,o,r);
A3: for x being set st x in the carrier of X
    ex y being set st y in the carrier of Tcircle(o,r) & P[x,y]
    proof
      let x be set such that
A4:   x in the carrier of X;
      reconsider z = x as Point of TOP-REAL n by A4,TOPMETR:2;
      z in cl_Ball(o,r) by A4,A2,XBOOLE_0:def 4;
      then
A5:   z is Point of Tdisk(o,r) by BROUWER:3;
      p <> z by A2,A4,ZFMISC_1:64;
      then HC(p,z,o,r) is Point of Tcircle(o,r) by A1,A5,BROUWER:6;
      hence thesis;
    end;
    consider f being Function of the carrier of X, the carrier of Tcircle(o,r)
    such that
A6: for x being set st x in the carrier of X holds P[x,f.x]
    from FUNCT_2:sch 1(A3);
    reconsider f as Function of X, Tcircle(o,r);
    take f;
    let x be Point of X;
    thus thesis by A6;
  end;
  uniqueness
  proof
    let f, g be Function of X, Tcircle(o,r) such that
A7: for x being Point of X
    ex y being Point of TOP-REAL n st x = y & f.x = HC(p,y,o,r) and
A8: for x being Point of X
    ex y being Point of TOP-REAL n st x = y & g.x = HC(p,y,o,r);
    now
      let x be set such that
A9:   x in the carrier of X;
A10:   ex y being Point of TOP-REAL n st x = y & f.x = HC(p,y,o,r) by A7,A9;
      ex y being Point of TOP-REAL n st x = y & g.x = HC(p,y,o,r) by A8,A9;
      hence f.x = g.x by A10;
    end;
    hence thesis by FUNCT_2:18;
  end;
end;

theorem Th73:
  for o, p being Point of TOP-REAL 2,
  r being positive (real number) st p is Point of Tdisk(o,r) holds
  DiskProj(o,r,p) is continuous
  proof
    let o, p be Point of TOP-REAL 2;
    let r be positive (real number) such that
A1: p is Point of Tdisk(o,r);
     set D = Tdisk(o,r);
     set cB = cl_Ball(o,r);
     set Bp = cB \ {p};
     set OK = [:Bp,{p}:];
     set D1 = T2|Bp;
     set D2 = T2|{p};
     set S1 = Tcircle(o,r);
A2: p in {p} by TARSKI:def 1;
A3:  the carrier of D = cl_Ball(o,r) by BROUWER:3;
A4:   the carrier of D1 = Bp by JORDAN1:1;
A5:   the carrier of D2 = {p} by JORDAN1:1;
  set TD = [:T2,T2:] | OK;
  set gg = DiskProj(o,r,p);
A6: [#]D1 = the carrier of D1 by PRE_TOPC:12;
A7: [#]S1 = the carrier of S1 by PRE_TOPC:12;
    set xo = diffX2_1(o);
    set yo = diffX2_2(o);
    set dx = diffX1_X2_1;
    set dy = diffX1_X2_2;
    set fx2 = Proj2_1;
    set fy2 = Proj2_2;
  reconsider rr = r^2 as Real by XREAL_0:def 1;
  set f1 = (the carrier of [:T2,T2:]) --> rr;
  reconsider f1 as continuous RealMap of [:T2,T2:] by Lm9;
  set Zf1 = f1 || OK;
  set Zfx2 = fx2 || OK;
  set Zfy2 = fy2 || OK;
  set Zdx = dx || OK;
  set Zdy = dy || OK;
  set Zxo = xo || OK;
  set Zyo = yo || OK;
  set xx = Zxo(#)Zdx;
  set yy = Zyo(#)Zdy;
  set m = Zdx(#)Zdx + Zdy(#)Zdy;
A8: the carrier of TD = OK by JORDAN1:1;
A9: the carrier of TD = [#]TD by PRE_TOPC:12;
A10: dom m = the carrier of TD by FUNCT_2:def 1;
A11: dom (Zdx(#)Zdx) = the carrier of TD by FUNCT_2:def 1;
A12: dom (Zdy(#)Zdy) = the carrier of TD by FUNCT_2:def 1;
A13:  for y being Point of D1, z being Point of D2 holds Zdx. [y,z] = dx. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
A14: [y,z] in OK by A4,A5,ZFMISC_1:def 2;
    thus Zdx. [y,z] = dx. [y,z] by A14,FUNCT_1:72;
  end;
A15: for y being Point of D1, z being Point of D2 holds Zdy. [y,z] = dy. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
    [y,z] in OK by A4,A5,ZFMISC_1:def 2;
   hence Zdy. [y,z] = dy. [y,z] by FUNCT_1:72;
  end;
A16: for y being Point of D1, z being Point of D2 holds Zfx2. [y,z] = fx2. [y,z
]
  proof
    let y be Point of D1;
    let z be Point of D2;
A17: [y,z] in OK by A4,A5,ZFMISC_1:def 2;
    thus Zfx2. [y,z] = fx2. [y,z] by A17,FUNCT_1:72;
  end;
A18: for y being Point of D1, z being Point of D2 holds Zfy2. [y,z] = fy2. [y,z
]
  proof
    let y be Point of D1;
    let z be Point of D2;
A19: [y,z] in OK by A4,A5,ZFMISC_1:def 2;
    thus Zfy2. [y,z] = fy2. [y,z] by A19,FUNCT_1:72;
  end;
A20: for y being Point of D1, z being Point of D2 holds Zf1. [y,z] = f1. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
A21: [y,z] in OK by A4,A5,ZFMISC_1:def 2;
    thus Zf1. [y,z] = f1. [y,z] by A21,FUNCT_1:72;
  end;
A22: for y being Point of D1, z being Point of D2 holds Zxo. [y,z] = xo. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
A23: [y,z] in OK by A4,A5,ZFMISC_1:def 2;
    thus Zxo. [y,z] = xo. [y,z] by A23,FUNCT_1:72;
  end;
A24: for y being Point of D1, z being Point of D2 holds Zyo. [y,z] = yo. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
A25: [y,z] in OK by A4,A5,ZFMISC_1:def 2;
    thus Zyo. [y,z] = yo. [y,z] by A25,FUNCT_1:72;
  end;
  now
    let b be real number;
    assume b in rng m;
    then consider a being set such that
A26:   a in dom m and
A27:   m.a = b by FUNCT_1:def 5;
    consider y, z being set such that
A28: y in Bp & z in {p} & a = [y,z] by A26,A8,ZFMISC_1:def 2;
A29: z = p by A28,TARSKI:def 1;
      reconsider y, z as Point of T2 by A28;
A30: y <> z by A28,A29,ZFMISC_1:64;
    [y,z] = [[y,z]`1,[y,z]`2] by Lm7,MCART_1:23;
    then
A31: y = [y,z]`1 & z = [y,z]`2 by ZFMISC_1:33;
A32: dx. [y,z] = [y,z]`1`1 - [y,z]`2`1 by Def3;
A33: dy. [y,z] = [y,z]`1`2 - [y,z]`2`2 by Def4;
     set r1 = y`1-z`1;
     set r2 = y`2-z`2;
A34: Zdx. [y,z] = dx. [y,z] by A13,A28,A4,A5;
A35: Zdy. [y,z] = dy. [y,z] by A28,A4,A5,A15;
A36: m. [y,z] = (Zdx(#)Zdx). [y,z] + (Zdy(#)Zdy). [y,z] by A26,A28,SEQ_1:def 3
    .= Zdx. [y,z] * Zdx. [y,z] + (Zdy(#)Zdy). [y,z]
    by A26,A28,A11,SEQ_1:def 5
    .= r1^2+r2^2 by A35,A33,A34,A32,A31,A26,A28,A12,SEQ_1:def 5;
A37: 0+0 <= r1^2+r2^2 by XREAL_1:9;
    now
      assume r1^2+r2^2 = 0;
      then r1 = 0 & r2 = 0 by COMPLEX1:2;
      hence contradiction by A30,TOPREAL3:11;
    end;
    hence 0 < b by A27,A28,A36,A37;
  end;
  then reconsider m as positive-yielding continuous RealMap of TD
    by PARTFUN3:def 1;
  set p1 = (xx+yy)(#)(xx+yy);
  set p2 = Zxo(#)Zxo + Zyo(#)Zyo - Zf1;
A38: dom p2 = the carrier of TD by FUNCT_2:def 1;
A39: dom (Zxo(#)Zxo + Zyo(#)Zyo) = the carrier of TD by FUNCT_2:def 1;
A40: dom (Zxo(#)Zxo) = the carrier of TD by FUNCT_2:def 1;
A41: dom (Zyo(#)Zyo) = the carrier of TD by FUNCT_2:def 1;
  now
    let b be real number;
    assume b in rng p2;
    then consider a being set such that
A42:   a in dom p2 and
A43:   p2.a = b by FUNCT_1:def 5;
    consider y, z being set such that
A44: y in Bp & z in {p} & a = [y,z] by A42,A8,ZFMISC_1:def 2;
    reconsider y, z as Point of T2 by A44;
    set r3 = z`1-o`1, r4 = z`2-o`2;
A45: Zf1. [y,z] = f1. [y,z] by A20,A4,A5,A44;
A46: Zxo. [y,z] = xo. [y,z] by A4,A5,A44,A22;
A47: Zyo. [y,z] = yo. [y,z] by A4,A5,A44,A24;
A48: [y,z] = [[y,z]`1,[y,z]`2] by Lm7,MCART_1:23;
A49: xo. [y,z] = [y,z]`2`1 - o`1 by Def1;
A50: y = [y,z]`1 & z = [y,z]`2 by A48,ZFMISC_1:33;
A51:  yo. [y,z] = [y,z]`2`2 - o`2 by Def2;
A52: p2. [y,z] = (Zxo(#)Zxo + Zyo(#)Zyo). [y,z] - Zf1. [y,z]
    by A44,A42,SEQ_1:def 4
    .= (Zxo(#)Zxo + Zyo(#)Zyo). [y,z] - r^2 by A45,FUNCOP_1:13
    .= (Zxo(#)Zxo). [y,z] + (Zyo(#)Zyo). [y,z] - r^2
    by A44,A42,A39,SEQ_1:def 3
    .= Zxo. [y,z] * Zxo. [y,z] + (Zyo(#)Zyo). [y,z] - r^2
    by A44,A42,A40,SEQ_1:def 5
    .= r3^2+r4^2-r^2 by A51,A50,A47,A46,A49,A44,A42,A41,SEQ_1:def 5;
    z = p by A44,TARSKI:def 1;
    then |. z-o .| <= r by A1,A3,TOPREAL9:8;
    then
A53: |. z-o .|^2 <= r^2 by SQUARE_1:77;
    |. z-o .|^2 = ((z-o)`1)^2+((z-o)`2)^2 by JGRAPH_1:46
      .= r3^2+((z-o)`2)^2 by TOPREAL3:8
      .= r3^2+r4^2 by TOPREAL3:8;
    then r3^2+r4^2-r^2 <= r^2-r^2 by A53,XREAL_1:11;
    hence 0 >= b by A43,A44,A52;
  end;
  then reconsider p2 as nonpositive-yielding continuous RealMap of TD
    by PARTFUN3:def 3;
  set pp = p1 - m(#)p2;
  set k = (-(xx+yy) + sqrt(pp)) / m;
  set x3 = Zfx2 + k(#)Zdx;
  set y3 = Zfy2 + k(#)Zdy;
  reconsider X3 = x3, Y3 = y3 as Function of TD,R^1 by TOPMETR:24;
  set F = <:X3,Y3:>;
  set R = R2Homeomorphism;
A54: for x being Point of D1 holds gg.x = (R*F). [x,p]
  proof
    let x be Point of D1;
    consider y being Point of T2 such that
A55: x = y & gg.x = HC(p,y,o,r) by A1,Def7;
A56: x <> p by A4,ZFMISC_1:64;
A57: [y,p] in OK by A4,A2,A55,ZFMISC_1:def 2;
    set r1 = y`1-p`1, r2 = y`2-p`2,
        r3 = p`1-o`1, r4 = p`2-o`2;
    set l = (-(r3*r1+r4*r2)+sqrt((r3*r1+r4*r2)^2-(r1^2+r2^2)*(r3^2+r4^2-r^2)))
        / (r1^2+r2^2);
    [y,p] = [[y,p]`1,[y,p]`2] by Lm7,MCART_1:23;
    then
A58: y = [y,p]`1 & p = [y,p]`2 by ZFMISC_1:33;
A59: fx2. [y,p] = [y,p]`2`1 by Def5;
A60: fy2. [y,p] = [y,p]`2`2 by Def6;
A61: dx. [y,p] = [y,p]`1`1 - [y,p]`2`1 by Def3;
A62: dy. [y,p] = [y,p]`1`2 - [y,p]`2`2 by Def4;
A63: xo. [y,p] = [y,p]`2`1 - o`1 by Def1;
A64: yo. [y,p] = [y,p]`2`2 - o`2 by Def2;
A65: dom X3 = the carrier of TD by FUNCT_2:def 1;
A66: dom Y3 = the carrier of TD by FUNCT_2:def 1;
A67: dom (k(#)Zdx) = the carrier of TD by FUNCT_2:def 1;
A68: dom (k(#)Zdy) = the carrier of TD by FUNCT_2:def 1;
A69: dom (Zdx(#)Zdx) = the carrier of TD by FUNCT_2:def 1;
A70: dom (Zdy(#)Zdy) = the carrier of TD by FUNCT_2:def 1;
A71: dom (-(xx+yy) + sqrt(pp)) = the carrier of TD by FUNCT_2:def 1;
A72: dom -(xx+yy) = the carrier of TD by FUNCT_2:def 1;
A73: dom (xx+yy) = the carrier of TD by FUNCT_2:def 1;
A74: dom xx = the carrier of TD by FUNCT_2:def 1;
A75: dom yy = the carrier of TD by FUNCT_2:def 1;
A76: dom pp = the carrier of TD by FUNCT_2:def 1;
A77: dom p1 = the carrier of TD by FUNCT_2:def 1;
A78: dom (m(#)p2) = the carrier of TD by FUNCT_2:def 1;
A79: y is Point of D1 & p is Point of D2 by A5,A55,TARSKI:def 1;
     then
A80: Zdx. [y,p] = dx. [y,p] by A13;
A81: Zdy. [y,p] = dy. [y,p] by A79,A15;
A82: Zf1. [y,p] = f1. [y,p] by A79,A20;
A83: Zxo. [y,p] = xo. [y,p] by A79,A22;
A84: Zyo. [y,p] = yo. [y,p] by A79,A24;
A85: m. [y,p] = (Zdx(#)Zdx). [y,p] + (Zdy(#)Zdy). [y,p]
    by A57,A8,A10,SEQ_1:def 3
    .= Zdx. [y,p] * Zdx. [y,p] + (Zdy(#)Zdy). [y,p]
    by A57,A8,A69,SEQ_1:def 5
    .= r1^2+r2^2 by A81,A62,A58,A80,A61,A57,A8,A70,SEQ_1:def 5;
A86: xx. [y,p] = Zxo. [y,p] * Zdx. [y,p] by A57,A8,A74,SEQ_1:def 5;
A87: yy. [y,p] = Zyo. [y,p] * Zdy. [y,p] by A57,A8,A75,SEQ_1:def 5;
A88: (xx+yy). [y,p] = xx. [y,p] + yy. [y,p] by A57,A8,A73,SEQ_1:def 3;
A89: p1. [y,p] = (r3*r1+r4*r2)^2
    by A80,A61,A83,A63,A88,A86,A87,A81,A62,A58,A84,A64,A57,A8,A77,SEQ_1:def 5;
A90: p2. [y,p] = (Zxo(#)Zxo + Zyo(#)Zyo). [y,p] - Zf1. [y,p]
    by A57,A8,A38,SEQ_1:def 4
    .= (Zxo(#)Zxo + Zyo(#)Zyo). [y,p] - r^2 by A82,FUNCOP_1:13
    .= (Zxo(#)Zxo). [y,p] + (Zyo(#)Zyo). [y,p] - r^2
    by A57,A8,A39,SEQ_1:def 3
    .= Zxo. [y,p] * Zxo. [y,p] + (Zyo(#)Zyo). [y,p] - r^2
    by A57,A8,A40,SEQ_1:def 5
    .= r3^2+r4^2-r^2 by A64,A58,A84,A83,A63,A57,A8,A41,SEQ_1:def 5;
   dom sqrt pp = the carrier of TD by FUNCT_2:def 1;
   then
A91: sqrt(pp). [y,p] = sqrt(pp. [y,p]) by A57,A8,PARTFUN3:def 5
    .= sqrt(p1. [y,p] - (m(#)p2). [y,p]) by A57,A8,A76,SEQ_1:def 4
    .= sqrt((r3*r1+r4*r2)^2-(r1^2+r2^2)*(r3^2+r4^2-r^2))
    by A57,A85,A89,A90,A8,A78,SEQ_1:def 5;
   dom k = the carrier of TD by FUNCT_2:def 1;
   then
A92: k. [y,p] = (-(xx+yy) + sqrt(pp)). [y,p] * (m. [y,p])" by
   A57,A8,RFUNCT_1:def 4
  .= (-(xx+yy) + sqrt(pp)). [y,p] / m. [y,p] by XCMPLX_0:def 9
    .= ((-(xx+yy)). [y,p] + sqrt(pp). [y,p]) / (r1^2+r2^2)
    by A57,A8,A71,A85,SEQ_1:def 3
    .= l by A57,A8,A80,A61,A83,A63,A86,A81,A62,A58,A84,A64,A87,
    A88,A91,A72,SEQ_1:def 7;
A93: X3. [y,p] = Zfx2. [y,p] + (k(#)Zdx). [y,p] by A57,A8,A65,SEQ_1:def 3
     .= p`1 + (k(#)Zdx). [y,p] by A79,A16,A59,A58
     .= p`1+l*r1 by A57,A80,A61,A58,A92,A8,A67,SEQ_1:def 5;
A94: Y3. [y,p] = Zfy2. [y,p] + (k(#)Zdy). [y,p] by A57,A8,A66,SEQ_1:def 3
    .= p`2 + (k(#)Zdy). [y,p] by A79,A18,A60,A58
    .= p`2+l*r2 by A57,A81,A62,A58,A92,A8,A68,SEQ_1:def 5;
A95: y in Bp by A55,A4;
    Bp c= cB by XBOOLE_1:36;
    hence gg.x = |[ p`1+l*r1, p`2+l*r2 ]| by A1,A55,A56,A95,A3,BROUWER:8
    .= <*X3. [y,p], Y3. [y,p]*> by A93,A94
    .= R. [X3. [y,p], Y3. [y,p]] by TOPREALA:def 2
    .= R.(F. [y,p]) by A57,A65,A66,A8,FUNCT_3:69
    .= (R*F). [x,p] by A57,A55,A8,FUNCT_2:21;
  end;
  X3 is continuous & Y3 is continuous by TOPREAL6:83;
  then reconsider F as continuous Function of TD,[:R^1,R^1:] by YELLOW12:41;
  for pp being Point of D1, V being Subset of S1
   st gg.pp in V & V is open holds
    ex W being Subset of D1 st pp in W & W is open & gg.:W c= V
  proof
    let pp be Point of D1,
        V be Subset of S1 such that
A96:   gg.pp in V and
A97:   V is open;
    reconsider p1 = pp, fp = p as Point of T2 by TOPMETR:2;
A98: [pp,p] in OK by A4,A2,ZFMISC_1:def 2;
    consider V1 being Subset of T2 such that
A99:   V1 is open and
A100:   V1 /\ [#]S1 = V by A97,TOPS_2:32;
A101:  gg.pp = (R*F). [pp,p] by A54;
    R" is_homeomorphism by TOPS_2:70,TOPREALA:56;
    then
A102: R" .:V1 is open by A99,TOPGRP_1:25;
A103: dom F = the carrier of [:T2,T2:] | OK by FUNCT_2:def 1;
A104: dom R = the carrier of [:R^1,R^1:] by FUNCT_2:def 1;
    then
A105: rng F c= dom R;
    then
A106: dom (R*F) = dom F by RELAT_1:46;
A107: rng R = [#]T2 & R is one-to-one by TOPS_2:def 5,TOPREALA:56;
A108: R"*(R*F) = R"*R*F by RELAT_1:55
            .= id dom R*F by A107,TOPS_2:65;
    dom id dom R = dom R by RELAT_1:71;
    then
A109: dom (id dom R*F) = dom F by A105,RELAT_1:46;
    for x being set st x in dom F holds (id dom R*F).x = F.x
    proof
      let x be set such that
A110:     x in dom F;
A111:   F.x in rng F by A110,FUNCT_1:def 5;
      thus (id dom R*F).x = id dom R.(F.x) by A110,FUNCT_1:23
        .= F.x by A104,A111,FUNCT_1:35;
    end;
    then
A112: id dom R*F = F by A109,FUNCT_1:9;
    (R*F). [p1,fp] in V1 by A96,A100,A101,XBOOLE_0:def 3;
    then R" .((R*F). [p1,fp]) in R" .:V1 by FUNCT_2:43;
    then (R"*(R*F)). [p1,fp] in R" .:V1 by A98,A106,A103,A8,FUNCT_1:23;
    then consider W being Subset of TD such that
A113:  [p1,fp] in W and
A114:  W is open and
A115:  F.:W c= R" .:V1 by A98,A102,A108,A112,A8,JGRAPH_2:20;
    consider WW being Subset of [:T2,T2:] such that
A116:   WW is open and
A117:   WW /\ [#]TD = W by A114,TOPS_2:32;
    consider SF being Subset-Family of [:T2,T2:] such that
A118:  WW = union SF and
A119:  for e being set st e in SF
        ex X1 being Subset of T2, Y1 being Subset of T2 st
          e = [:X1,Y1:] & X1 is open & Y1 is open by A116,BORSUK_1:45;
    [p1,fp] in WW by A117,A113,XBOOLE_0:def 3;
    then consider Z being set such that
A120:  [p1,fp] in Z and
A121:  Z in SF by A118,TARSKI:def 4;
    consider X1, Y1 being Subset of T2 such that
A122:  Z = [:X1,Y1:] and
A123:  X1 is open and
      Y1 is open by A119,A121;
    set ZZ = Z /\ [#]TD;
    reconsider XX = X1 /\ [#]D1 as open Subset of D1
      by A6,A123,XBOOLE_1:17,TOPS_2:32;
    take XX;
    pp in X1 by A120,A122,ZFMISC_1:106;
    hence pp in XX by A6,XBOOLE_0:def 3;
    thus XX is open;
    let b be set;
    assume b in gg.:XX;
    then consider a being Point of D1 such that
A124:  a in XX and
A125:  b = gg.a by FUNCT_2:116;
    reconsider a1 = a, fa = fp as Point of T2 by TOPMETR:2;
A126: a in X1 by A124,XBOOLE_0:def 3;
A127:  [a,p] in OK by A4,A2,ZFMISC_1:def 2;
     fa in Y1 by A120,A122,ZFMISC_1:106;
     then [a,fa] in Z by A122,A126,ZFMISC_1:def 2;
    then [a,fa] in ZZ by A127,A8,A9,XBOOLE_0:def 3;
    then
A128: F. [a1,fa] in F.:ZZ by A127,A8,FUNCT_2:43;
A129: R qua Function" = R" by A107,TOPS_2:def 4;
A130: dom(R") = [#]T2 by A107,TOPS_2:62;
    Z c= WW by A118,A121,ZFMISC_1:92;
    then ZZ c= WW /\ [#]TD by XBOOLE_1:27;
    then F.:ZZ c= F.:W by A117,RELAT_1:156;
    then F. [a1,fa] in F.:W by A128;
    then R.(F. [a1,fa]) in R.:(R" .:V1) by A115,FUNCT_2:43;
    then (R*F). [a1,fa] in R.:(R" .:V1) by A127,A8,FUNCT_2:21;
    then (R*F). [a1,fa] in V1 by A107,A130,A129,Lm8,PARTFUN3:1;
    then gg.a in V1 by A54;
    hence b in V by A7,A100,A125,XBOOLE_0:def 3;
  end;
  hence thesis by JGRAPH_2:20;
end;

theorem Th74:
  for n being non empty Nat, o, p being Point of TOP-REAL n,
  r being positive (real number) st p in Ball(o,r) holds
  DiskProj(o,r,p)|Sphere(o,r) = id Sphere(o,r)
  proof
    let n be non empty Nat;
    let o, p be Point of TOP-REAL n;
    let r be positive (real number);
    assume
A1: p in Ball(o,r);
A2: the carrier of Tdisk(o,r) = cl_Ball(o,r) by BROUWER:3;
A3: the carrier of (TOP-REAL n)|(cl_Ball(o,r)\{p}) = cl_Ball(o,r)\{p}
     by JORDAN1:1;
A4: dom DiskProj(o,r,p) = the carrier of (TOP-REAL n)|(cl_Ball(o,r)\{p})
     by FUNCT_2:def 1;
A5: Sphere(o,r) misses Ball(o,r) by TOPREAL9:19;
A6: Sphere(o,r) c= cl_Ball(o,r) by TOPREAL9:17;
A7: Ball(o,r) c= cl_Ball(o,r) by TOPREAL9:16;
     Sphere(o,r) c= cl_Ball(o,r)\{p}
     proof
       let a be set;
       assume
A8:   a in Sphere(o,r);
       then a <> p by A5,A1,XBOOLE_0:3;
       hence thesis by A6,A8,ZFMISC_1:64;
     end;
     then
A9:  dom(DiskProj(o,r,p)|Sphere(o,r)) = Sphere(o,r) by A3,A4,RELAT_1:91;
A10:  dom id Sphere(o,r) = Sphere(o,r) by RELAT_1:71;
    now
      let x be set;
      assume
A11:   x in dom(DiskProj(o,r,p)|Sphere(o,r));
      then consider y being Point of TOP-REAL n such that
A12:   x = y and
A13:  (DiskProj(o,r,p)).x = HC(p,y,o,r) by A1,A7,A2,Def7;
      y in halfline(p,y) by TOPREAL9:28;
      then
A14:   x in halfline(p,y) /\ Sphere(o,r) by A9,A12,A11,XBOOLE_0:def 3;
A15:   x <> p by A9,A11,A1,A5,XBOOLE_0:3;
      thus (DiskProj(o,r,p)|Sphere(o,r)).x = (DiskProj(o,r,p)).x
      by A11,FUNCT_1:70
      .= x by A13,A1,A7,A2,A12,A9,A11,A6,A14,A15,BROUWER:def 3
      .= (id Sphere(o,r)).x by A9,A11,FUNCT_1:35;
    end;
    hence thesis by A9,A10,FUNCT_1:9;
  end;

definition
  let n be non empty Nat, o, p be Point of TOP-REAL n,
  r be positive (real number) such that
A1: p in Ball(o,r);
  set X = Tcircle(o,r);
  func RotateCircle(o,r,p) -> Function of Tcircle(o,r), Tcircle(o,r)
  means :Def8:
  for x being Point of Tcircle(o,r)
  ex y being Point of TOP-REAL n st x = y & it.x = HC(y,p,o,r);
  existence
  proof
A2: the carrier of X = Sphere(o,r) by TOPREALB:9;
    defpred P[set,set] means
    ex z being Point of TOP-REAL n st $1 = z & $2 = HC(z,p,o,r);
A3: for x being set st x in the carrier of X
    ex y being set st y in the carrier of X & P[x,y]
    proof
      let x be set such that
A4:   x in the carrier of X;
      reconsider z = x as Point of TOP-REAL n by A4,TOPMETR:2;
      Sphere(o,r) c= cl_Ball(o,r) by TOPREAL9:17;
      then
A5:   z is Point of Tdisk(o,r) by A4,A2,BROUWER:3;
      Ball(o,r) c= cl_Ball(o,r) by TOPREAL9:16;
      then
A6:   p is Point of Tdisk(o,r) by A1,BROUWER:3;
      Ball(o,r) misses Sphere(o,r) by TOPREAL9:19;
      then p <> z by A2,A4,A1,XBOOLE_0:3;
      then HC(z,p,o,r) is Point of X by A5,A6,BROUWER:6;
      hence thesis;
    end;
    consider f being Function of the carrier of X, the carrier of X
    such that
A7: for x being set st x in the carrier of X holds P[x,f.x]
    from FUNCT_2:sch 1(A3);
    reconsider f as Function of X, X;
    take f;
    let x be Point of X;
    thus thesis by A7;
  end;
  uniqueness
  proof
    let f, g be Function of X, X such that
A8: for x being Point of X
    ex y being Point of TOP-REAL n st x = y & f.x = HC(y,p,o,r) and
A9: for x being Point of X
    ex y being Point of TOP-REAL n st x = y & g.x = HC(y,p,o,r);
    now
      let x be set such that
A10:   x in the carrier of X;
A11:   ex y being Point of TOP-REAL n st x = y & f.x = HC(y,p,o,r) by A8,A10;
      ex y being Point of TOP-REAL n st x = y & g.x = HC(y,p,o,r) by A9,A10;
      hence f.x = g.x by A11;
    end;
    hence thesis by FUNCT_2:18;
  end;
end;

theorem Th75:
  for o, p being Point of TOP-REAL 2,
  r being positive (real number) st p in Ball(o,r) holds
  RotateCircle(o,r,p) is continuous
  proof
    let o, p be Point of TOP-REAL 2;
    let r be positive (real number) such that
A1: p in Ball(o,r);
     set D = Tdisk(o,r);
     set cB = cl_Ball(o,r);
     set Bp = Sphere(o,r);
     set OK = [:{p},Bp:];
     set D1 = T2|{p};
     set D2 = T2|Bp;
     set S1 = Tcircle(o,r);
A2: D2 = S1 by TOPREALB:def 6;
A3: Ball(o,r) misses Sphere(o,r) by TOPREAL9:19;
A4: p in {p} by TARSKI:def 1;
A5: Bp c= cB by TOPREAL9:17;
A6: Ball(o,r) c= cB by TOPREAL9:16;
A7:  the carrier of D = cB by BROUWER:3;
A8:   the carrier of D1 = {p} by JORDAN1:1;
A9:   the carrier of D2 = Bp by JORDAN1:1;
  set TD = [:T2,T2:] | OK;
  set gg = RotateCircle(o,r,p);
A10: [#]D2 = the carrier of D2 by PRE_TOPC:12;
A11: [#]S1 = the carrier of S1 by PRE_TOPC:12;
    set xo = diffX2_1(o);
    set yo = diffX2_2(o);
    set dx = diffX1_X2_1;
    set dy = diffX1_X2_2;
    set fx2 = Proj2_1;
    set fy2 = Proj2_2;
  reconsider rr = r^2 as Real by XREAL_0:def 1;
  set f1 = (the carrier of [:T2,T2:]) --> rr;
  reconsider f1 as continuous RealMap of [:T2,T2:] by Lm9;
  set Zf1 = f1 || OK;
  set Zfx2 = fx2 || OK;
  set Zfy2 = fy2 || OK;
  set Zdx = dx || OK;
  set Zdy = dy || OK;
  set Zxo = xo || OK;
  set Zyo = yo || OK;
  set xx = Zxo(#)Zdx;
  set yy = Zyo(#)Zdy;
  set m = Zdx(#)Zdx + Zdy(#)Zdy;
A12: the carrier of TD = OK by JORDAN1:1;
A13: the carrier of TD = [#]TD by PRE_TOPC:12;
A14: dom m = the carrier of TD by FUNCT_2:def 1;
A15: dom (Zdx(#)Zdx) = the carrier of TD by FUNCT_2:def 1;
A16: dom (Zdy(#)Zdy) = the carrier of TD by FUNCT_2:def 1;
A17:  for y being Point of D1, z being Point of D2 holds Zdx. [y,z] = dx. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
A18: [y,z] in OK by A8,A9,ZFMISC_1:def 2;
    thus Zdx. [y,z] = dx. [y,z] by A18,FUNCT_1:72;
  end;
A19: for y being Point of D1, z being Point of D2 holds Zdy. [y,z] = dy. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
A20: [y,z] in OK by A8,A9,ZFMISC_1:def 2;
    thus Zdy. [y,z] = dy. [y,z] by A20,FUNCT_1:72;
  end;
A21: for y being Point of D1, z being Point of D2 holds Zfx2. [y,z] = fx2. [y,z
]
  proof
    let y be Point of D1;
    let z be Point of D2;
A22: [y,z] in OK by A8,A9,ZFMISC_1:def 2;
    thus Zfx2. [y,z] = fx2. [y,z] by A22,FUNCT_1:72;
  end;
A23: for y being Point of D1, z being Point of D2 holds Zfy2. [y,z] = fy2. [y,z
]
  proof
    let y be Point of D1;
    let z be Point of D2;
A24: [y,z] in OK by A8,A9,ZFMISC_1:def 2;
    thus Zfy2. [y,z] = fy2. [y,z] by A24,FUNCT_1:72;
  end;
A25: for y being Point of D1, z being Point of D2 holds Zf1. [y,z] = f1. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
A26: [y,z] in OK by A8,A9,ZFMISC_1:def 2;
    thus Zf1. [y,z] = f1. [y,z] by A26,FUNCT_1:72;
  end;
A27: for y being Point of D1, z being Point of D2 holds Zxo. [y,z] = xo. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
A28: [y,z] in OK by A8,A9,ZFMISC_1:def 2;
    thus Zxo. [y,z] = xo. [y,z] by A28,FUNCT_1:72;
  end;
A29: for y being Point of D1, z being Point of D2 holds Zyo. [y,z] = yo. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
A30: [y,z] in OK by A8,A9,ZFMISC_1:def 2;
    thus Zyo. [y,z] = yo. [y,z] by A30,FUNCT_1:72;
  end;
  now
    let b be real number;
    assume b in rng m;
    then consider a being set such that
A31:   a in dom m and
A32:   m.a = b by FUNCT_1:def 5;
    consider y, z being set such that
A33: y in {p} & z in Bp & a = [y,z] by A31,A12,ZFMISC_1:def 2;
A34: y = p by A33,TARSKI:def 1;
      reconsider y, z as Point of T2 by A33;
A35:   y <> z by A3,A33,A34,A1,XBOOLE_0:3;
      [y,z] = [[y,z]`1,[y,z]`2] by Lm7,MCART_1:23;
     then
A36: y = [y,z]`1 & z = [y,z]`2 by ZFMISC_1:33;
A37:  dx. [y,z] = [y,z]`1`1 - [y,z]`2`1 by Def3;
A38:  dy. [y,z] = [y,z]`1`2 - [y,z]`2`2 by Def4;
     set r1 = y`1-z`1;
     set r2 = y`2-z`2;
A39: Zdx. [y,z] = dx. [y,z] by A17,A33,A8,A9;
A40: Zdy. [y,z] = dy. [y,z] by A33,A8,A9,A19;
A41: m. [y,z] = (Zdx(#)Zdx). [y,z] + (Zdy(#)Zdy). [y,z] by A31,A33,SEQ_1:def 3
    .= Zdx. [y,z] * Zdx. [y,z] + (Zdy(#)Zdy). [y,z]
    by A31,A33,A15,SEQ_1:def 5
    .= r1^2+r2^2 by A40,A38,A39,A37,A36,A31,A33,A16,SEQ_1:def 5;
A42: 0+0 <= r1^2+r2^2 by XREAL_1:9;
    now
      assume r1^2+r2^2 = 0;
      then r1 = 0 & r2 = 0 by COMPLEX1:2;
      hence contradiction by A35,TOPREAL3:11;
    end;
    hence 0 < b by A32,A33,A41,A42;
  end;
  then reconsider m as positive-yielding continuous RealMap of TD
    by PARTFUN3:def 1;
  set p1 = (xx+yy)(#)(xx+yy);
  set p2 = Zxo(#)Zxo + Zyo(#)Zyo - Zf1;
A43: dom p2 = the carrier of TD by FUNCT_2:def 1;
A44: dom (Zxo(#)Zxo + Zyo(#)Zyo) = the carrier of TD by FUNCT_2:def 1;
A45: dom (Zxo(#)Zxo) = the carrier of TD by FUNCT_2:def 1;
A46: dom (Zyo(#)Zyo) = the carrier of TD by FUNCT_2:def 1;
  now
    let b be real number;
    assume b in rng p2;
    then consider a being set such that
A47:   a in dom p2 and
A48:   p2.a = b by FUNCT_1:def 5;
    consider y, z being set such that
A49: y in {p} & z in Bp & a = [y,z] by A47,A12,ZFMISC_1:def 2;
    reconsider y, z as Point of T2 by A49;
    set r3 = z`1-o`1, r4 = z`2-o`2;
A50: Zf1. [y,z] = f1. [y,z] by A25,A8,A9,A49;
A51: Zxo. [y,z] = xo. [y,z] by A8,A9,A49,A27;
A52: Zyo. [y,z] = yo. [y,z] by A8,A9,A49,A29;
    [y,z] = [[y,z]`1,[y,z]`2] by Lm7,MCART_1:23;
     then
A53: y = [y,z]`1 & z = [y,z]`2 by ZFMISC_1:33;
A54: xo. [y,z] = [y,z]`2`1 - o`1 by Def1;
A55: yo. [y,z] = [y,z]`2`2 - o`2 by Def2;
A56: p2. [y,z] = (Zxo(#)Zxo + Zyo(#)Zyo). [y,z] - Zf1. [y,z]
    by A49,A47,SEQ_1:def 4
    .= (Zxo(#)Zxo + Zyo(#)Zyo). [y,z] - r^2 by A50,FUNCOP_1:13
    .= (Zxo(#)Zxo). [y,z] + (Zyo(#)Zyo). [y,z] - r^2
    by A49,A47,A44,SEQ_1:def 3
    .= Zxo. [y,z] * Zxo. [y,z] + (Zyo(#)Zyo). [y,z] - r^2
    by A49,A47,A45,SEQ_1:def 5
    .= r3^2+r4^2-r^2 by A55,A53,A52,A51,A54,A49,A47,A46,SEQ_1:def 5;
    |. z-o .| <= r by A5,A49,TOPREAL9:8;
    then
A57: |. z-o .|^2 <= r^2 by SQUARE_1:77;
    |. z-o .|^2 = ((z-o)`1)^2+((z-o)`2)^2 by JGRAPH_1:46
      .= r3^2+((z-o)`2)^2 by TOPREAL3:8
      .= r3^2+r4^2 by TOPREAL3:8;
    then r3^2+r4^2-r^2 <= r^2-r^2 by A57,XREAL_1:11;
    hence 0 >= b by A48,A49,A56;
  end;
  then reconsider p2 as nonpositive-yielding continuous RealMap of TD
    by PARTFUN3:def 3;
  set pp = p1 - m(#)p2;
  set k = (-(xx+yy) + sqrt(pp)) / m;
  set x3 = Zfx2 + k(#)Zdx;
  set y3 = Zfy2 + k(#)Zdy;
  reconsider X3 = x3, Y3 = y3 as Function of TD,R^1 by TOPMETR:24;
  set F = <:X3,Y3:>;
  set R = R2Homeomorphism;
A58: for x being Point of D2 holds gg.x = (R*F). [p,x]
  proof
    let x be Point of D2;
    consider y being Point of T2 such that
A59: x = y & gg.x = HC(y,p,o,r) by A2,A1,Def8;
A60: x <> p by A9,A3,A1,XBOOLE_0:3;
A61: [p,y] in OK by A9,A4,A59,ZFMISC_1:def 2;
    set r1 = p`1-y`1, r2 = p`2-y`2,
        r3 = y`1-o`1, r4 = y`2-o`2;
    set l = (-(r3*r1+r4*r2)+sqrt((r3*r1+r4*r2)^2-(r1^2+r2^2)*(r3^2+r4^2-r^2)))
        / (r1^2+r2^2);
    [p,y] = [[p,y]`1,[p,y]`2] by Lm7,MCART_1:23;
     then
A62: p = [p,y]`1 & y = [p,y]`2 by ZFMISC_1:33;
A63: fx2. [p,y] = [p,y]`2`1 by Def5;
A64: fy2. [p,y] = [p,y]`2`2 by Def6;
A65: dx. [p,y] = [p,y]`1`1 - [p,y]`2`1 by Def3;
A66: dy. [p,y] = [p,y]`1`2 - [p,y]`2`2 by Def4;
A67: xo. [p,y] = [p,y]`2`1 - o`1 by Def1;
A68: yo. [p,y] = [p,y]`2`2 - o`2 by Def2;
A69: dom X3 = the carrier of TD by FUNCT_2:def 1;
A70: dom Y3 = the carrier of TD by FUNCT_2:def 1;
A71: dom (k(#)Zdx) = the carrier of TD by FUNCT_2:def 1;
A72: dom (k(#)Zdy) = the carrier of TD by FUNCT_2:def 1;
A73: dom (Zdx(#)Zdx) = the carrier of TD by FUNCT_2:def 1;
A74: dom (Zdy(#)Zdy) = the carrier of TD by FUNCT_2:def 1;
A75: dom (-(xx+yy) + sqrt(pp)) = the carrier of TD by FUNCT_2:def 1;
A76: dom -(xx+yy) = the carrier of TD by FUNCT_2:def 1;
A77: dom (xx+yy) = the carrier of TD by FUNCT_2:def 1;
A78: dom xx = the carrier of TD by FUNCT_2:def 1;
A79: dom yy = the carrier of TD by FUNCT_2:def 1;
A80: dom pp = the carrier of TD by FUNCT_2:def 1;
A81: dom p1 = the carrier of TD by FUNCT_2:def 1;
A82: dom (m(#)p2) = the carrier of TD by FUNCT_2:def 1;
A83: y is Point of D2 & p is Point of D1 by A8,A59,TARSKI:def 1;
     then
A84: Zdx. [p,y] = dx. [p,y] by A17;
A85: Zdy. [p,y] = dy. [p,y] by A83,A19;
A86: Zf1. [p,y] = f1. [p,y] by A83,A25;
A87: Zxo. [p,y] = xo. [p,y] by A83,A27;
A88: Zyo. [p,y] = yo. [p,y] by A83,A29;
A89: m. [p,y] = (Zdx(#)Zdx). [p,y] + (Zdy(#)Zdy). [p,y]
    by A61,A12,A14,SEQ_1:def 3
    .= Zdx. [p,y] * Zdx. [p,y] + (Zdy(#)Zdy). [p,y]
    by A61,A12,A73,SEQ_1:def 5
    .= r1^2+r2^2 by A85,A66,A62,A84,A65,A61,A12,A74,SEQ_1:def 5;
A90: xx. [p,y] = Zxo. [p,y] * Zdx. [p,y] by A61,A12,A78,SEQ_1:def 5;
A91: yy. [p,y] = Zyo. [p,y] * Zdy. [p,y] by A61,A12,A79,SEQ_1:def 5;
A92: (xx+yy). [p,y] = xx. [p,y] + yy. [p,y] by A61,A12,A77,SEQ_1:def 3;
A93: p1. [p,y] = (r3*r1+r4*r2)^2
    by A84,A65,A87,A67,A92,A90,A91,A85,A66,A62,A88,A68,A61,A12,A81,SEQ_1:def 5;
A94: p2. [p,y] = (Zxo(#)Zxo + Zyo(#)Zyo). [p,y] - Zf1. [p,y]
    by A61,A12,A43,SEQ_1:def 4
    .= (Zxo(#)Zxo + Zyo(#)Zyo). [p,y] - r^2 by A86,FUNCOP_1:13
    .= (Zxo(#)Zxo). [p,y] + (Zyo(#)Zyo). [p,y] - r^2
    by A61,A12,A44,SEQ_1:def 3
    .= Zxo. [p,y] * Zxo. [p,y] + (Zyo(#)Zyo). [p,y] - r^2
    by A61,A12,A45,SEQ_1:def 5
    .= r3^2+r4^2-r^2 by A68,A62,A88,A87,A67,A61,A12,A46,SEQ_1:def 5;
   dom sqrt pp = the carrier of TD by FUNCT_2:def 1;
   then
A95: sqrt(pp). [p,y] = sqrt(pp. [p,y]) by A61,A12,PARTFUN3:def 5
    .= sqrt(p1. [p,y] - (m(#)p2). [p,y]) by A61,A12,A80,SEQ_1:def 4
    .= sqrt((r3*r1+r4*r2)^2-(r1^2+r2^2)*(r3^2+r4^2-r^2))
    by A61,A89,A93,A94,A12,A82,SEQ_1:def 5;
   dom k = the carrier of TD by FUNCT_2:def 1;
   then
A96: k. [p,y] = (-(xx+yy) + sqrt(pp)). [p,y] * (m. [p,y])" by
   A61,A12,RFUNCT_1:def 4
  .= (-(xx+yy) + sqrt(pp)). [p,y] / m. [p,y] by XCMPLX_0:def 9
    .= ((-(xx+yy)). [p,y] + sqrt(pp). [p,y]) / (r1^2+r2^2)
    by A61,A12,A75,A89,SEQ_1:def 3
    .= l by A61,A12,A84,A65,A62,A87,A67,A90,A85,A66,A88,A68,A91,
    A92,A95,A76,SEQ_1:def 7;
A97: X3. [p,y] = Zfx2. [p,y] + (k(#)Zdx). [p,y] by A61,A12,A69,SEQ_1:def 3
     .= y`1 + (k(#)Zdx). [p,y] by A83,A21,A63,A62
     .= y`1+l*r1 by A61,A84,A65,A62,A96,A12,A71,SEQ_1:def 5;
A98: Y3. [p,y] = Zfy2. [p,y] + (k(#)Zdy). [p,y] by A61,A12,A70,SEQ_1:def 3
    .= y`2 + (k(#)Zdy). [p,y] by A83,A23,A64,A62
    .= y`2+l*r2 by A61,A85,A66,A62,A96,A12,A72,SEQ_1:def 5;
    y in the carrier of D2 by A59;
    hence gg.x = |[ y`1+l*r1, y`2+l*r2 ]|
    by A59,A60,A6,A7,A1,A9,A5,BROUWER:8
    .= <*X3. [p,y], Y3. [p,y]*> by A97,A98
    .= R. [X3. [p,y], Y3. [p,y]] by TOPREALA:def 2
    .= R.(F. [p,y]) by A61,A69,A70,A12,FUNCT_3:69
    .= (R*F). [p,x] by A61,A59,A12,FUNCT_2:21;
  end;
  X3 is continuous & Y3 is continuous by TOPREAL6:83;
  then reconsider F as continuous Function of TD,[:R^1,R^1:] by YELLOW12:41;
  for pp being Point of D2, V being Subset of S1
   st gg.pp in V & V is open holds
    ex W being Subset of D2 st pp in W & W is open & gg.:W c= V
  proof
    let pp be Point of D2,
        V be Subset of S1 such that
A99:   gg.pp in V and
A100:   V is open;
    reconsider p1 = pp, fp = p as Point of T2 by TOPMETR:2;
A101: [p,pp] in OK by A9,A4,ZFMISC_1:def 2;
    consider V1 being Subset of T2 such that
A102:   V1 is open and
A103:   V1 /\ [#]S1 = V by A100,TOPS_2:32;
A104:  gg.pp = (R*F). [p,pp] by A58;
    R" is_homeomorphism by TOPS_2:70,TOPREALA:56;
    then
A105: R" .:V1 is open by A102,TOPGRP_1:25;
A106: dom F = the carrier of [:T2,T2:] | OK by FUNCT_2:def 1;
A107: dom R = the carrier of [:R^1,R^1:] by FUNCT_2:def 1;
    then
A108: rng F c= dom R;
    then
A109: dom (R*F) = dom F by RELAT_1:46;
A110: rng R = [#]T2 & R is one-to-one by TOPS_2:def 5,TOPREALA:56;
A111: R"*(R*F) = R"*R*F by RELAT_1:55
            .= id dom R*F by A110,TOPS_2:65;
    dom id dom R = dom R by RELAT_1:71;
    then
A112: dom (id dom R*F) = dom F by A108,RELAT_1:46;
    for x being set st x in dom F holds (id dom R*F).x = F.x
    proof
      let x be set such that
A113:     x in dom F;
A114:   F.x in rng F by A113,FUNCT_1:def 5;
      thus (id dom R*F).x = id dom R.(F.x) by A113,FUNCT_1:23
        .= F.x by A107,A114,FUNCT_1:35;
    end;
    then
A115: id dom R*F = F by A112,FUNCT_1:9;
    (R*F). [fp,p1] in V1 by A99,A103,A104,XBOOLE_0:def 3;
    then R" .((R*F). [fp,p1]) in R" .:V1 by FUNCT_2:43;
    then (R"*(R*F)). [fp,p1] in R" .:V1 by A101,A109,A106,A12,FUNCT_1:23;
    then consider W being Subset of TD such that
A116: [fp,p1] in W and
A117:  W is open and
A118:  F.:W c= R" .:V1 by A101,A105,A111,A115,A12,JGRAPH_2:20;
    consider WW being Subset of [:T2,T2:] such that
A119:   WW is open and
A120:   WW /\ [#]TD = W by A117,TOPS_2:32;
    consider SF being Subset-Family of [:T2,T2:] such that
A121:  WW = union SF and
A122:  for e being set st e in SF
        ex X1 being Subset of T2, Y1 being Subset of T2 st
          e = [:X1,Y1:] & X1 is open & Y1 is open by A119,BORSUK_1:45;
    [fp,p1] in WW by A120,A116,XBOOLE_0:def 3;
    then consider Z being set such that
A123:  [fp,p1] in Z and
A124:  Z in SF by A121,TARSKI:def 4;
    consider X1, Y1 being Subset of T2 such that
A125:  Z = [:X1,Y1:] and
      X1 is open and
A126:  Y1 is open by A122,A124;
    set ZZ = Z /\ [#]TD;
    reconsider XX = Y1 /\ [#]D2 as open Subset of D2
      by A10,A126,XBOOLE_1:17,TOPS_2:32;
    take XX;
    pp in Y1 by A123,A125,ZFMISC_1:106;
    hence pp in XX by A10,XBOOLE_0:def 3;
    thus XX is open;
    let b be set;
    assume b in gg.:XX;
    then consider a being Point of D2 such that
A127:  a in XX and
A128:  b = gg.a by A2,FUNCT_2:116;
    reconsider a1 = a, fa = fp as Point of T2 by TOPMETR:2;
A129: a in Y1 by A127,XBOOLE_0:def 3;
A130:  [p,a] in OK by A9,A4,ZFMISC_1:def 2;
     fa in X1 by A123,A125,ZFMISC_1:106;
     then [fa,a] in Z by A125,A129,ZFMISC_1:def 2;
    then [fa,a] in ZZ by A130,A12,A13,XBOOLE_0:def 3;
    then
A131: F. [fa,a1] in F.:ZZ by A130,A12,FUNCT_2:43;
A132: R qua Function" = R" by A110,TOPS_2:def 4;
A133: dom(R") = [#]T2 by A110,TOPS_2:62;
A134: gg.a1 in the carrier of S1 by A2,FUNCT_2:7;
    Z c= WW by A121,A124,ZFMISC_1:92;
    then ZZ c= WW /\ [#]TD by XBOOLE_1:27;
    then F.:ZZ c= F.:W by A120,RELAT_1:156;
    then F. [fa,a1] in F.:W by A131;
    then R.(F. [fa,a1]) in R.:(R" .:V1) by A118,FUNCT_2:43;
    then (R*F). [fa,a1] in R.:(R" .:V1) by A130,A12,FUNCT_2:21;
    then (R*F). [fa,a1] in V1 by A110,A133,A132,Lm8,PARTFUN3:1;
    then gg.a in V1 by A58;
    hence b in V by A11,A103,A128,A134,XBOOLE_0:def 3;
  end;
  hence thesis by A2,JGRAPH_2:20;
end;

theorem Th76:
  for n being non empty Nat
  for o, p being Point of TOP-REAL n,
  r being positive (real number) st p in Ball(o,r)
  holds RotateCircle(o,r,p) has_no_fixpoint
  proof
  let n be non empty Nat;
  let o, p be Point of TOP-REAL n;
  let r be positive (real number);
  assume
A1: p in Ball(o,r);
    set f = RotateCircle(o,r,p);
    let x be set;
    assume
A2: x in dom f;
    set S = Tcircle(o,r);
A3: dom f = the carrier of S by FUNCT_2:def 1;
    consider y being Point of TOP-REAL n such that
A4: x = y and
A5: f.x = HC(y,p,o,r) by A2,A1,Def8;
A6: the carrier of S = Sphere(o,r) by TOPREALB:9;
    Sphere(o,r) c= cl_Ball(o,r) by TOPREAL9:17;
    then
A7: y is Point of Tdisk(o,r) by A2,A3,A4,A6,BROUWER:3;
    Ball(o,r) c= cl_Ball(o,r) by TOPREAL9:16;
    then
A8: p is Point of Tdisk(o,r) by A1,BROUWER:3;
    Ball(o,r) misses Sphere(o,r) by TOPREAL9:19;
    then y <> p by A2,A4,A6,A1,XBOOLE_0:3;
    hence f.x <> x by A4,A5,A7,A8,BROUWER:def 3;
  end;

begin :: Jordan's Curve Theorem

theorem Th77:
  U = P & U is_a_component_of (TOP-REAL 2)|C` &
  V is_a_component_of (TOP-REAL 2)|C` & U <> V
  implies Cl P misses V
  proof
    assume that
A1: U = P and
A2: U is_a_component_of T2|C` and
A3: V is_a_component_of T2|C` and
A4: U <> V;
    assume Cl P meets V;
    then
A5: ex x being set st x in Cl P & x in V by XBOOLE_0:3;
    the carrier of T2|C` = C` by JORDAN1:1;
    then reconsider V1 = V as Subset of T2 by XBOOLE_1:1;
    reconsider T2C = T2|C` as non empty SubSpace of T2;
    T2C is locally_connected by JORDAN2C:89;
    then V is open by A3,CONNSP_2:21;
    then V1 is open by TSEP_1:17;
    then P meets V1 by A5,PRE_TOPC:def 13;
    hence thesis by A1,A2,A3,A4,CONNSP_1:37;
  end;

theorem Th78:
  U is_a_component_of (TOP-REAL 2)|C` implies
  (TOP-REAL 2)|C`|U is arcwise_connected
  proof
    set T = T2|C`;
    assume
A1: U is_a_component_of T;
    let a, b be Point of T|U;
A2: the carrier of T|U = U by JORDAN1:1;
A3: U <> {}T by A1,CONNSP_1:34;
    then
A4: T|U is non empty by A2,STRUCT_0:def 1;
    per cases;
    suppose
A5:   a = b;
      reconsider TU = T|U as non empty TopSpace by A3,A2,STRUCT_0:def 1;
      reconsider a as Point of TU;
      reconsider f = I[01] --> a as Function of I[01],T|U;
      take f;
      thus thesis by A5,TOPALG_3:5,BORSUK_1:def 17,def 18;
    end;
    suppose
A6:   a <> b;
A7:  T|U is SubSpace of T2 by TSEP_1:7;
      then reconsider a1 = a, b1 = b as Point of T2 by A4,TOPMETR:2;
      reconsider V = U as Subset of T2 by PRE_TOPC:39;
      V is_a_component_of C` by A1,CONNSP_1:def 6;
      then
A8:   V is open by SPRECT_3:18;
      U is connected by A1,CONNSP_1:def 5;
      then V is connected by CONNSP_1:24;
      then V is being_Region by A8,TOPREAL4:def 3;
      then consider P being Subset of T2 such that
A9:   P is_S-P_arc_joining a1,b1 and
A10:   P c= V by A6,A3,A2,TOPREAL4:30;
A11:  a1 in P by A9,TOPREAL4:4;
      P is_an_arc_of a1,b1 by A9,TOPREAL4:3;
      then consider g being Function of I[01], T2|P such that
A12:   g is_homeomorphism and
A13:   g.0 = a & g.1 = b by TOPREAL1:def 2;
A14:  the carrier of T2|P = P by JORDAN1:1;
      then reconsider f = g as Function of I[01], T|U by A2,A10,A11,FUNCT_2:9;
      take f;
A15:  g is continuous by A12,TOPS_2:def 5;
      T2|P is SubSpace of T|U by A14,A2,A10,A7,TSEP_1:4;
      hence f is continuous by A15,TOPMETR:7;
      thus f.0 = a & f.1 = b by A13;
    end;
  end;

::Th5_6: ex r ...
Lm15:
  for r being non negative (real number) st A is_an_arc_of p1,p2 &
  A is Subset of Tdisk(p,r)
  ex f being Function
 of Tdisk(p,r), (TOP-REAL 2)|A st f is continuous & f|A = id A
  proof
    let r be non negative (real number);
    set D = Tdisk(p,r);
    assume that
A1: A is_an_arc_of p1,p2 and
A2: A is Subset of D;
    reconsider A1 = A as non empty Subset of D by A1,A2,TOPREAL1:4;
    reconsider A2 = A as non empty Subset of T2 by A1,TOPREAL1:4;
    set TA = T2|A2;
    consider h being Function of I[01],TA such that
A3: h is_homeomorphism and
    h.0 = p1 and
    h.1 = p2 by A1,TOPREAL1:def 2;
A4: h1 is_homeomorphism by TREAL_1:20;
    then
A5: h1 is continuous by TOPS_2:def 5;
    reconsider hh = h as Function of C0,TA by TOPMETR:27;
A6: TA = D|A1 by TOPALG_5:4;
    then reconsider f = h1*hh" as Function of D|A1,C1;
    A is closed by A1,JORDAN6:12;
    then
A7: A1 is closed by TSEP_1:12;
    hh" is continuous by A3,TOPMETR:27,TOPS_2:def 5;
    then h1*hh" is continuous by A5,TOPS_2:58;
    then consider g being continuous Function of D,C1 such that
A8: g|A1 = f by A7,A6,TIETZE:25;
    reconsider R = hh*h1"*g as Function of D,(TOP-REAL 2)|A;
    take R;
A9: hh is continuous by A3,TOPMETR:27,TOPS_2:def 5;
    h1" is continuous by A4,TOPS_2:def 5;
    then hh*h1" is continuous by A9,TOPS_2:58;
    hence R is continuous by TOPS_2:58;
A10: the carrier of TA = A1 by JORDAN1:1;
    dom R = the carrier of D by FUNCT_2:def 1;
    then
A11: dom(R|A) = A by A2,RELAT_1:91;
A12: dom id A = A by RELAT_1:71;
    now
      let a be set;
      assume
A13:   a in dom(R|A);
A14:   dom g = the carrier of D by FUNCT_2:def 1;
A15:   dom(h1*hh") = the carrier of TA by FUNCT_2:def 1;
A16:   h1 is one-to-one by A4,TOPS_2:def 5;
A17:   h1 is onto by A4,Th35;
A18:   hh is one-to-one onto by A3,Th35,TOPMETR:27,TOPS_2:def 5;
A19:   hh*h1"*(h1*hh") = hh*h1"*h1*hh" by RELAT_1:55
      .= hh*(h1"*h1)*hh" by RELAT_1:55
      .= hh*id C0*hh" by A16,A17,WAYBEL29:1
      .= hh*hh" by GRCAT_1:12
      .= id TA by A18,WAYBEL29:1;
      thus (R|A).a = R.a by A11,A13,FUNCT_1:72
      .= (hh*h1").(g.a) by A13,A14,FUNCT_1:23
      .= (hh*h1").((h1*hh").a) by A8,A13,A11,FUNCT_1:72
      .= (hh*h1"*(h1*hh")).a by A10,A13,A11,A15,FUNCT_1:23
      .= a by A13,A11,A10,A19,GRCAT_1:11
      .= (id A).a by A11,A13,FUNCT_1:35;
    end;
    hence thesis by A11,A12,FUNCT_1:9;
  end;

::Th5_6: ex f ...
Lm16:
:: definicja funkcji r
  for r being positive (real number) st A is_an_arc_of p1,p2 & A c= C &
  C c= Ball(p,r) & p in U & Cl P /\ P` c= A & P c= Ball(p,r)
  for f being Function of Tdisk(p,r), (TOP-REAL 2)|A st f is continuous &
  f|A = id A &
:: komponenty
  U = P & U is_a_component_of (TOP-REAL 2)|C` & B = cl_Ball(p,r) \ {p}
  ex g being Function of Tdisk(p,r), (TOP-REAL 2)|B st g is continuous &
  for x being Point of Tdisk(p,r) holds (x in Cl P implies g.x = f.x) &
  (x in P` implies g.x = x)
  proof
    let r be positive (real number);
    set D = Tdisk(p,r);
    assume that
A1: A is_an_arc_of p1,p2 and
A2: A c= C and
A3: C c= Ball(p,r) and
A4: p in U and
A5: Cl P /\ P` c= A and
A6:  P c= Ball(p,r);
    let f be Function of D, T2|A;
    assume that
A7: f is continuous and
A8: f|A = id A and
A9: U = P and
A10: U is_a_component_of T2|C` and
A11: B = cl_Ball(p,r) \ {p};
     reconsider B1 = B as non empty Subset of T2 by A11;
     reconsider T2B1 = T2|B1 as non empty SubSpace of T2;
A12: the carrier of T2|C` = C` by JORDAN1:1;
A13: the carrier of T2|A = A by JORDAN1:1;
A14: the carrier of D = cl_Ball(p,r) by BROUWER:3;
A15:  Ball(p,r) c= cl_Ball(p,r) by TOPREAL9:16;
A16: A <> {} by A1,TOPREAL1:4;
    reconsider A1 = A as non empty Subset of T2 by A1,TOPREAL1:4;
A17: not p in C by A12,A4,SUBSET_1:54;
A18: [#]D = the carrier of D by PRE_TOPC:12;
     |. p-p .| = 0 by TOPRNS_1:29;
     then
A19: p in [#]D by A14,A18,TOPREAL9:8;
A20:  P c= Cl P by PRE_TOPC:48;
     then reconsider F1 = [#]D /\ Cl P as non empty Subset of D
     by A19,A18,A4,A9,XBOOLE_0:def 3,XBOOLE_1:17;
A21: Sphere(p,r) c= cl_Ball(p,r) by TOPREAL9:17;
A22: Ball(p,r) misses Sphere(p,r) by TOPREAL9:19;
     consider e being Point of T2 such that
A23:  e in Sphere(p,r) by SUBSET_1:10;
     not e in P by A23,A22,A6,XBOOLE_0:3;
     then e in P` by SUBSET_1:50;
     then reconsider F3 = [#]D /\ P` as non empty Subset of D
     by A18,A23,A14,A21,XBOOLE_0:def 3,XBOOLE_1:17;
     reconsider T1 = D|F1 as non empty SubSpace of D;
     reconsider T3 = D|F3 as non empty SubSpace of D;
A24: the carrier of T1 = F1 by JORDAN1:1;
A25: the carrier of T3 = F3 by JORDAN1:1;
A26: the carrier of T2B1 = B1 by JORDAN1:1;
A27: A c= B
    proof
      let a be set;
      assume a in A;
      then
A28:   a in C by A2;
      then a in Ball(p,r) by A3;
      hence thesis by A11,A17,A28,A15,ZFMISC_1:64;
    end;
A29: F3 c= B
    proof
      let a be set;
      assume
A30:   a in F3;
      then a in P` by XBOOLE_0:def 3;
      then not a in P by SUBSET_1:54;
      hence thesis by A30,A14,A4,A9,A11,ZFMISC_1:64;
    end;
    f|F1 is Function of F1,A by A13,A16,FUNCT_2:38;
    then reconsider f1 = f|F1 as Function
 of T1,T2B1 by A24,A26,A16,A27,FUNCT_2:9;
    reconsider g1 = id F3 as Function of T3,T2B1 by A25,A26,A29,FUNCT_2:9;
    the carrier of T1 = [#]T1 & the carrier of T3 = [#]T3 by PRE_TOPC:12;
    then
A31: F1 = [#]T1 & F3 = [#]T3 by JORDAN1:1;
A32: [#]T1 \/ [#]T3 = [#]D
    proof
      F1 c= [#]D & F3 c= [#]D by XBOOLE_1:17;
      hence [#]T1 \/ [#]T3 c= [#]D by A31,XBOOLE_1:8;
      let p be set;
      assume
A33:   p in [#]D;
      per cases;
      suppose p in P;
        then p in F1 by A20,A33,XBOOLE_0:def 3;
        hence thesis by A31,XBOOLE_0:def 2;
      end;
      suppose not p in P;
        then p in P` by A33,A14,A18,SUBSET_1:50;
        then p in F3 by A33,XBOOLE_0:def 3;
        hence thesis by A31,XBOOLE_0:def 2;
      end;
    end;
    reconsider DT = [#]D as closed Subset of T2
    by A18,BORSUK_1:def 14,TSEP_1:1;
    DT /\ Cl P is closed by TOPS_1:35;
    then
A34: F1 is closed by TSEP_1:8;
    P is_a_component_of C`
    proof
      take U;
      thus thesis by A9,A10;
    end;
    then P is open by SPRECT_3:18;
    then P` is closed by TOPS_1:30;
    then DT /\ P` is closed by TOPS_1:35;
    then
A35: F3 is closed by TSEP_1:8;
    reconsider f2 = f|F1 as Function of T1,T2|A1 by A24,FUNCT_2:38;
A36: T2|A1 is SubSpace of T2B1 by A26,A27,A13,TSEP_1:4;
    T3 is SubSpace of T2 by TSEP_1:7;
    then
A37: T3 is SubSpace of T2B1 by A25,A26,A29,TSEP_1:4;
    f2 is continuous by A7,TOPMETR:10;
    then
A38: f1 is continuous by A36,TOPMETR:7;
    reconsider g2 = id F3 as Function of T3,T3 by A25;
    g2 = id T3 by A25,GRCAT_1:def 11;
    then
A39: g1 is continuous by A37,TOPMETR:7;
A40: for x being set st x in Cl P & x in P` holds f.x = x
    proof
      let x be set;
      assume x in Cl P & x in P`;
      then
A41:   x in Cl P /\ P` by XBOOLE_0:def 3;
      then (id A).x = x by A5,FUNCT_1:35;
      hence f.x = x by A8,A41,A5,FUNCT_1:72;
    end;
    for x being set st x in [#]T1 /\ [#]T3 holds f1.x = g1.x
    proof
      let x be set;
      assume x in [#]T1 /\ [#]T3;
      then
A42:   x in [#]T1 & x in [#]T3 by XBOOLE_0:def 3;
      then x in Cl P & x in P` by A31,XBOOLE_0:def 3;
      then
A43:   f.x = x by A40;
      thus f1.x = f.x by A31,A42,FUNCT_1:72
      .= g1.x by A31,A42,A43,FUNCT_1:35;
    end;
    then consider g being Function of D,T2|B such that
A44: g = f1+*g1 and
A45: g is continuous by A31,A32,A34,A35,A38,A39,JGRAPH_2:9;
    take g;
    thus g is continuous by A45;
    let x be Point of D;
A46: dom g1 = the carrier of T3 by FUNCT_2:def 1;
    hereby
      assume
A47:   x in Cl P;
      then
A48:   x in F1 by A18,XBOOLE_0:def 3;
      per cases;
      suppose not x in dom g1;
        hence g.x = f1.x by A44,FUNCT_4:12
        .= f.x by A48,FUNCT_1:72;
      end;
      suppose
A49:     x in dom g1;
        then
A50:     x in Cl P & x in P` by A47,A25,XBOOLE_0:def 3;
        thus g.x = g1.x by A49,A44,FUNCT_4:14
        .= x by A49,A25,FUNCT_1:35
        .= f.x by A40,A50;
      end;
    end;
    assume x in P`;
    then
A51: x in F3 by A18,XBOOLE_0:def 3;
    thus g.x = g1.x by A46,A51,A25,A44,FUNCT_4:14
    .= x by A51,FUNCT_1:35;
  end;

Lm17:
:: definicja funkcji r
  for A being non empty Subset of T2
  for P1 being Subset of T2 st U <> V
  for r being positive (real number) st A c= C &
  C c= Ball(p,r) & p in V & Cl P /\ P` c= A &
  Ball(p,r) meets P
  for f being Function of Tdisk(p,r), (TOP-REAL 2)|A st f is continuous &
  f|A = id A &
:: komponenty
  U = P & U is_a_component_of (TOP-REAL 2)|C` &
  V is_a_component_of T2|C` & B = cl_Ball(p,r) \ {p}
  ex g being Function of Tdisk(p,r), (TOP-REAL 2)|B st g is continuous &
  for x being Point of Tdisk(p,r) holds (x in Cl P implies g.x = x) &
  (x in P` implies g.x = f.x)
  proof
    let A be non empty Subset of T2;
    let P1 be Subset of T2 such that
A1: U <> V;
    let r be positive (real number);
    set D = Tdisk(p,r);
    assume that
A2: A c= C and
A3: C c= Ball(p,r) and
A4: p in V and
A5: Cl P /\ P` c= A and
A6:  Ball(p,r) meets P;
    let f be Function of D, T2|A;
    assume that
A7: f is continuous and
A8: f|A = id A and
A9: U = P and
A10: U is_a_component_of T2|C` and
A11: V is_a_component_of T2|C` and
A12: B = cl_Ball(p,r) \ {p};
     reconsider B1 = B as non empty Subset of T2 by A12;
     reconsider T2B1 = T2|B1 as non empty SubSpace of T2;
A13: the carrier of T2|C` = C` by JORDAN1:1;
A14: the carrier of T2|A = A by JORDAN1:1;
A15: the carrier of D = cl_Ball(p,r) by BROUWER:3;
A16:  Ball(p,r) c= cl_Ball(p,r) by TOPREAL9:16;
A17: not p in C by A13,A4,SUBSET_1:54;
A18: [#]D = the carrier of D by PRE_TOPC:12;
     |. p-p .| = 0 by TOPRNS_1:29;
     then
A19: p in [#]D by A15,A18,TOPREAL9:8;
A20:  P c= Cl P by PRE_TOPC:48;
A21: ex j being set st j in Ball(p,r) & j in P by A6,XBOOLE_0:3;
     reconsider F1 = [#]D /\ Cl P as non empty Subset of D
     by A15,A16,A21,A18,A20,XBOOLE_0:def 3,XBOOLE_1:17;
     now
       assume
A22:    p in P;
       U misses V by A1,A10,A11,CONNSP_1:37;
       hence contradiction by A4,A9,A22,XBOOLE_0:3;
     end;
     then p in P` by SUBSET_1:50;
     then reconsider F3 = [#]D /\ P` as non empty Subset of D
     by A19,A18,XBOOLE_0:def 3,XBOOLE_1:17;
    set T1 = D|F1;
    set T3 = D|F3;
A23: the carrier of T1 = F1 by JORDAN1:1;
A24: the carrier of T3 = F3 by JORDAN1:1;
A25: the carrier of T2|B1 = B1 by JORDAN1:1;
A26: A c= B
    proof
      let a be set;
      assume a in A;
      then
A27:   a in C by A2;
      then a in Ball(p,r) by A3;
      hence thesis by A12,A17,A27,A16,ZFMISC_1:64;
    end;
A28: F1 c= B
    proof
      let a be set;
      assume
A29:  a in F1;
      then
A30:  a in Cl P by XBOOLE_0:def 3;
      now
        assume
A31:    p in Cl P;
        Cl P misses V by A1,A9,A10,A11,Th77;
        hence contradiction by A4,A31,XBOOLE_0:3;
      end;
      hence thesis by A30,A29,A15,A12,ZFMISC_1:64;
    end;
    reconsider f1 = id F1 as Function of T1,T2B1
    by A23,A25,A28,FUNCT_2:9;
    f|F3 is Function of F3,A by A14,FUNCT_2:38;
    then reconsider g1 = f|F3 as Function of T3,T2B1
    by A24,A25,A26,FUNCT_2:9;
A32: the carrier of T1 = [#]T1 & the carrier of T3 = [#]T3 by PRE_TOPC:12;
    then
A33: F1 = [#]T1 & F3 = [#]T3 by JORDAN1:1;
A34: [#]T1 \/ [#]T3 = [#]D
    proof
      F1 c= [#]D & F3 c= [#]D by XBOOLE_1:17;
      hence [#]T1 \/ [#]T3 c= [#]D by A33,XBOOLE_1:8;
      let p be set;
      assume
A35:   p in [#]D;
      per cases;
      suppose p in P;
        then p in F1 by A20,A35,XBOOLE_0:def 3;
        hence thesis by A33,XBOOLE_0:def 2;
      end;
      suppose not p in P;
        then p in P` by A35,A15,A18,SUBSET_1:50;
        then p in F3 by A35,XBOOLE_0:def 3;
        hence thesis by A33,XBOOLE_0:def 2;
      end;
    end;
    reconsider DT = [#]D as closed Subset of T2
    by A18,BORSUK_1:def 14,TSEP_1:1;
    DT /\ Cl P is closed by TOPS_1:35;
    then
A36: F1 is closed by TSEP_1:8;
    P is_a_component_of C`
    proof
      take U;
      thus thesis by A9,A10;
    end;
    then P is open by SPRECT_3:18;
    then P` is closed by TOPS_1:30;
    then DT /\ P` is closed by TOPS_1:35;
    then
A37: F3 is closed by TSEP_1:8;
A38: id T1 = id F1 by A32,A33,GRCAT_1:def 11;
     T1 is SubSpace of T2 by TSEP_1:7;
     then T1 is SubSpace of T2B1 by A23,A25,A28,TSEP_1:4;
     then
A39: f1 is continuous by A38,TOPMETR:7;
A40: T2|A is SubSpace of T2B1 by A14,A25,A26,TSEP_1:4;
     reconsider g2 = g1 as Function of T3,T2|A by A24,FUNCT_2:38;
     g2 is continuous by A7,TOPMETR:10;
     then
A41: g1 is continuous by A40,TOPMETR:7;
A42: for x being set st x in Cl P & x in P` holds f.x = x
    proof
      let x be set;
      assume x in Cl P & x in P`;
      then
A43:   x in Cl P /\ P` by XBOOLE_0:def 3;
      then (id A).x = x by A5,FUNCT_1:35;
      hence f.x = x by A8,A43,A5,FUNCT_1:72;
    end;
    for x being set st x in [#]T1 /\ [#]T3 holds f1.x = g1.x
    proof
      let x be set;
      assume x in [#]T1 /\ [#]T3;
      then
A44:  x in [#]T1 & x in [#]T3 by XBOOLE_0:def 3;
      then x in Cl P & x in P` by A33,XBOOLE_0:def 3;
      then
A45:   f.x = x by A42;
      thus f1.x = x by A33,A44,FUNCT_1:35
      .= g1.x by A33,A44,A45,FUNCT_1:72;
    end;
    then consider g being Function of D,T2|B such that
A46: g = f1+*g1 and
A47: g is continuous by A33,A34,A36,A37,A39,A41,JGRAPH_2:9;
    take g;
    thus g is continuous by A47;
    let x be Point of D;
A48: dom g1 = the carrier of T3 by FUNCT_2:def 1;
    hereby
      assume
A49:   x in Cl P;
      then
A50:   x in F1 by A18,XBOOLE_0:def 3;
      per cases;
      suppose not x in dom g1;
        hence g.x = f1.x by A46,FUNCT_4:12
        .= x by A50,FUNCT_1:35;
      end;
      suppose
A51:    x in dom g1;
        then
A52:    x in Cl P & x in P` by A49,A24,XBOOLE_0:def 3;
        thus g.x = g1.x by A51,A46,FUNCT_4:14
        .= f.x by A51,A24,FUNCT_1:72
        .= x by A42,A52;
      end;
    end;
    assume x in P`;
    then
A53: x in F3 by A18,XBOOLE_0:def 3;
    thus g.x = g1.x by A48,A53,A24,A46,FUNCT_4:14
    .= f.x by A53,FUNCT_1:72;
  end;

theorem Th79:
  U = P & U is_a_component_of (TOP-REAL 2)|C` implies C = Fr P
  proof
    assume that
A1: U = P and
A2: U is_a_component_of T2|C` and
A3: C <> Fr P;
A4: the carrier of T2|C` = C` by JORDAN1:1;
    reconsider T2C = T2|C` as non empty SubSpace of T2;
A5: T2C is locally_connected by JORDAN2C:89;
    then U is open by A2,CONNSP_2:21;
    then reconsider P as open Subset of T2 by A1,TSEP_1:17;
A6: Fr P = Cl P /\ P` by PRE_TOPC:52;
    set Z = {X where X is Subset of T2|C`: X is_a_component_of T2|C` & X <> U};
    set V = union Z;
A7: V \/ U \/ C = the carrier of T2
     proof
A8:    V c= the carrier of T2
       proof
         let a be set;
         assume a in V;
         then consider A being set such that
A9:      a in A and
A10:      A in Z by TARSKI:def 4;
         ex X being Subset of T2|C` st X = A & X is_a_component_of T2|C` &
         X <> U by A10;
         hence thesis by A4,A9,TARSKI:def 3;
       end;
       U c= the carrier of T2 by A4,XBOOLE_1:1;
       then V \/ U c= the carrier of T2 by A8,XBOOLE_1:8;
       hence V \/ U \/ C c= the carrier of T2 by XBOOLE_1:8;
       let a be set;
       assume
A11:    a in the carrier of T2;
       per cases;
       suppose a in C;
         hence thesis by XBOOLE_0:def 2;
       end;
       suppose not a in C;
         then reconsider a as Point of T2|C` by A4,A11,SUBSET_1:50;
A12:      a in skl a by CONNSP_1:40;
         per cases;
         suppose skl a = U;
           then a in V \/ U by A12,XBOOLE_0:def 2;
           hence thesis by XBOOLE_0:def 2;
         end;
         suppose
A13:        skl a <> U;
           skl a is_a_component_of T2|C` by CONNSP_1:43;
           then skl a in Z by A13;
           then a in V by A12,TARSKI:def 4;
           then a in V \/ U by XBOOLE_0:def 2;
           hence thesis by XBOOLE_0:def 2;
         end;
       end;
     end;
A14: P misses P` by SUBSET_1:26;
    Fr P c= C
    proof
      let a be set;
      assume
A15:   a in Fr P;
      then
A16:   a in Cl P & a in P` by A6,XBOOLE_0:def 3;
      assume not a in C;
      then a in V \/ U by A15,A7,XBOOLE_0:def 2;
      then a in V or a in U by XBOOLE_0:def 2;
      then consider O being set such that
A17:   a in O and
A18:   O in Z by A14,A16,A1,XBOOLE_0:3,TARSKI:def 4;
      consider X being Subset of T2|C` such that
A19:   X = O and
A20:  X is_a_component_of T2|C` and
A21:  X <> U by A18;
      Cl P misses X by A1,A2,A20,A21,Th77;
      hence thesis by A16,A17,A19,XBOOLE_0:3;
    end;
    then consider p1, p2, A such that
A22:  A is_an_arc_of p1,p2 and
A23:  Fr P c= A and
A24:  A c= C by A3,BORSUK_4:84;
A25: U <> {}(T2|C`) by A2,CONNSP_1:34;
    per cases;
    suppose P is Bounded;
      then reconsider P as Bounded Subset of T2;
      consider p being set such that
A26:   p in U by A25,XBOOLE_0:def 1;
      reconsider p as Point of T2 by A26,A1;
A27:   P \/ C is Bounded by TOPREAL6:76;
      then reconsider PC = P \/ C as bounded Subset of Euclid 2
      by JORDAN2C:def 2;
      consider r being positive (real number) such that
A28:   PC c= Ball(p,r) by A27,Th34;
      C c= PC by XBOOLE_1:7;
      then
A29:   C c= Ball(p,r) by A28,XBOOLE_1:1;
      set D = Tdisk(p,r);
      set S = Tcircle(p,r);
      set B = cl_Ball(p,r) \ {p};
A30:  the carrier of S = Sphere(p,r) by TOPREALB:9;
A31:  the carrier of D = cl_Ball(p,r) by BROUWER:3;
A32:  Sphere(p,r) c= cl_Ball(p,r) by TOPREAL9:17;
A33:  Ball(p,r) misses Sphere(p,r) by TOPREAL9:19;
A34:  Ball(p,r) c= cl_Ball(p,r) by TOPREAL9:16;
      A c= Ball(p,r) by A24,A29,XBOOLE_1:1;
      then A is Subset of D by A31,A34,XBOOLE_1:1;
      then consider R being Function of D, T2|A such that
A35:   R is continuous and
A36:   R|A = id A by A22,Lm15;
      P c= PC by XBOOLE_1:7;
      then
A37:   P c= Ball(p,r) by A28,XBOOLE_1:1;
      then consider f being Function of D, T2|B such that
A38:   f is continuous and
A39:  for x being Point of D holds (x in Cl P implies f.x = R.x) &
      (x in P` implies f.x = x)
      by A24,A22,A23,A6,A26,A29,A35,A36,A1,A2,Lm16;
      set g = DiskProj(p,r,p);
      set h = RotateCircle(p,r,p);
A40:  S is SubSpace of D by A30,A31,A32,TSEP_1:4;
      reconsider F = h*(g*f) as Function of D,D by A30,A31,A32,FUNCT_2:9;
      p is Point of D by Th25;
      then g is continuous by Th73;
      then
A41:  g*f is continuous by A38,TOPS_2:58;
      |. p-p .| = 0 by TOPRNS_1:29;
      then
A42:  p in Ball(p,r) by TOPREAL9:7;
      then h is continuous by Th75;
      then h*(g*f) is continuous by A41,TOPS_2:58;
      then
A43:  F is continuous by A40,TOPMETR:7;
      now
        let x be set;
        per cases;
        suppose
A44:      x in dom F;
A45:      Ball(p,r) \/ Sphere(p,r) = cl_Ball(p,r) by TOPREAL9:18;
          now per cases by A31,A44,A45,XBOOLE_0:def 2;
            suppose
A46:          x in Ball(p,r);
              F.x in the carrier of S by A44,FUNCT_2:7;
              hence F.x <> x by A30,A33,A46,XBOOLE_0:3;
            end;
            suppose
A47:          x in Sphere(p,r);
A48:          dom f = the carrier of D by FUNCT_2:def 1;
              not x in P by A47,A33,A37,XBOOLE_0:3;
              then
A49:          x in P` by A47,SUBSET_1:50;
A50:          g|Sphere(p,r) = id Sphere(p,r) by A42,Th74;
              h has_no_fixpoint by A42,Th76;
              then
A51:          not x is_a_fixpoint_of h by ABIAN:def 5;
A52:          dom h = the carrier of S by FUNCT_2:def 1;
              F.x = h.((g*f).x) by A44,FUNCT_1:22
              .= h.(g.(f.x)) by A31,A32,A47,A48,FUNCT_1:23
              .= h.(g.x) by A39,A49,A47,A31,A32
              .= h.((id Sphere(p,r)).x) by A47,A50,FUNCT_1:72
              .= h.x by A47,FUNCT_1:35;
              hence F.x <> x by A47,A30,A51,A52,ABIAN:def 3;
            end;
          end;
          hence not x is_a_fixpoint_of F by ABIAN:def 3;
        end;
        suppose not x in dom F;
          hence not x is_a_fixpoint_of F by ABIAN:def 3;
        end;
      end;
      then not F has_a_fixpoint by ABIAN:def 5;
      hence thesis by A43,BROUWER:14;
    end;
    suppose
A53:  not P is Bounded;
      consider p being set such that
A54:  p in BDD C by XBOOLE_0:def 1;
      consider Z being set such that
A55:  p in Z and
A56:  Z in {B where B is Subset of T2: B is_inside_component_of C}
      by A54,TARSKI:def 4;
      consider P1 being Subset of T2 such that
A57:  Z = P1 and
A58:  P1 is_inside_component_of C by A56;
      consider U1 being Subset of T2|C` such that
A59:  U1 = P1 & U1 is_a_component_of T2|C` and
      U1 is bounded Subset of Euclid 2 by A58,JORDAN2C:17;
      U1 is open by A59,A5,CONNSP_2:21;
      then reconsider P1 as non empty open Bounded Subset of T2
      by A55,A57,A58,A59,TSEP_1:17,JORDAN2C:def 3;
      reconsider p as Point of T2 by A55,A57;
A60:   p in P1 by A55,A57;
A61:   P1 \/ C is Bounded by TOPREAL6:76;
      then reconsider PC = P1 \/ C as bounded Subset of Euclid 2
      by JORDAN2C:def 2;
      consider rv being positive (real number) such that
A62:   PC c= Ball(p,rv) by A61,Th34;
      not P c= Ball(p,rv) by A53,JORDAN2C:16;
      then consider u being set such that
A63:  u in P and
A64:  not u in Ball(p,rv) by TARSKI:def 3;
      reconsider u as Point of T2 by A63;
      set r = |.u-p.|;
      P misses P1 by A53,A59,A2,A1,CONNSP_1:37;
      then p <> u by A55,A57,A63,XBOOLE_0:3;
      then reconsider r as non empty non negative (real number) by TOPRNS_1:29;
A65:  r >= rv by A64,TOPREAL9:7;
      then Ball(p,rv) c= Ball(p,r) by Th26;
      then
A66:  PC c= Ball(p,r) by A62,XBOOLE_1:1;
A67:  Fr Ball(p,r) = Sphere(p,r) by Th32;
      u in Sphere(p,r) by TOPREAL9:9;
      then
A68:  P meets Ball(p,r) by A63,A67,TOPS_1:61;
A69:  C c= PC by XBOOLE_1:7;
      then
A70:   C c= Ball(p,r) by A66,XBOOLE_1:1;
      set D = Tdisk(p,r);
      set S = Tcircle(p,r);
      set B = cl_Ball(p,r) \ {p};
A71:  the carrier of S = Sphere(p,r) by TOPREALB:9;
A72:  the carrier of D = cl_Ball(p,r) by BROUWER:3;
A73:  Sphere(p,r) c= cl_Ball(p,r) by TOPREAL9:17;
A74:  Ball(p,r) misses Sphere(p,r) by TOPREAL9:19;
A75:  Ball(p,r) c= cl_Ball(p,r) by TOPREAL9:16;
      A c= Ball(p,r) by A24,A70,XBOOLE_1:1;
      then A is Subset of D by A72,A75,XBOOLE_1:1;
      then consider R being Function of D, T2|A such that
A76:   R is continuous and
A77:   R|A = id A by A22,Lm15;
      p1 in A by A22,TOPREAL1:4;
      then consider f being Function of D, T2|B such that
A78:   f is continuous and
A79:  for x being Point of D holds (x in Cl P implies f.x = x) &
      (x in P` implies f.x = R.x)
      by A68,A24,A23,A6,A60,A70,A76,A77,A1,A2,A59,A53,Lm17;
      set g = DiskProj(p,r,p);
      set h = RotateCircle(p,r,p);
A80:  S is SubSpace of D by A71,A72,A73,TSEP_1:4;
      reconsider F = h*(g*f) as Function of D,D by A71,A72,A73,FUNCT_2:9;
      p is Point of D by Th25;
      then g is continuous by Th73;
      then
A81:  g*f is continuous by A78,TOPS_2:58;
      |. p-p .| = 0 by TOPRNS_1:29;
      then
A82:  p in Ball(p,r) by TOPREAL9:7;
      then h is continuous by Th75;
      then h*(g*f) is continuous by A81,TOPS_2:58;
      then
A83:  F is continuous by A80,TOPMETR:7;
      now
        let x be set;
        per cases;
        suppose
A84:      x in dom F;
A85:      Ball(p,r) \/ Sphere(p,r) = cl_Ball(p,r) by TOPREAL9:18;
          now per cases by A72,A84,A85,XBOOLE_0:def 2;
            suppose
A86:          x in Ball(p,r);
              F.x in the carrier of S by A84,FUNCT_2:7;
              hence F.x <> x by A71,A74,A86,XBOOLE_0:3;
            end;
            suppose
A87:          x in Sphere(p,r);
A88:          dom f = the carrier of D by FUNCT_2:def 1;
A89:           P c= Cl P by PRE_TOPC:48;
              set SS = Sphere(p,r);
              SS c= C`
              proof
                let a be set;
                assume
A90:            a in SS;
                assume not a in C`;
                then
A91:            a in C by A90,SUBSET_1:50;
                reconsider a as Point of T2 by A90;
                a in PC by A69,A91;
                then |.a-p.| < rv by A62,TOPREAL9:7;
                hence contradiction by A65,A90,TOPREAL9:9;
              end;
              then reconsider SS as Subset of T2|C` by JORDAN1:1;
A92:          u in SS by TOPREAL9:9;
              SS is connected by CONNSP_1:24;
              then SS misses U or SS c= U by A2,CONNSP_1:38;
              then
A93:          x in P by A87,A92,A1,A63,XBOOLE_0:3;
A94:          g|Sphere(p,r) = id Sphere(p,r) by A82,Th74;
              h has_no_fixpoint by A82,Th76;
              then
A95:          not x is_a_fixpoint_of h by ABIAN:def 5;
A96:          dom h = the carrier of S by FUNCT_2:def 1;
              F.x = h.((g*f).x) by A84,FUNCT_1:22
              .= h.(g.(f.x)) by A72,A73,A87,A88,FUNCT_1:23
              .= h.(g.x) by A79,A93,A87,A72,A73,A89
              .= h.((id Sphere(p,r)).x) by A87,A94,FUNCT_1:72
              .= h.x by A87,FUNCT_1:35;
              hence F.x <> x by A87,A71,A95,A96,ABIAN:def 3;
            end;
          end;
          hence not x is_a_fixpoint_of F by ABIAN:def 3;
        end;
        suppose not x in dom F;
          hence not x is_a_fixpoint_of F by ABIAN:def 3;
        end;
      end;
      then not F has_a_fixpoint by ABIAN:def 5;
      hence thesis by A83,BROUWER:14;
    end;
  end;

set rp = 1;
set rl = -rp;
set rg = 3;
set rd = -rg;
set a = |[rl,0]|;
set b = |[rp,0]|;
set c = |[0,rg]|;
set d = |[0,rd]|;
set lg = |[rl,rg]|;
set pg = |[rp,rg]|;
set ld = |[rl,rd]|;
set pd = |[rp,rd]|;
set R = closed_inside_of_rectangle(rl,rp,rd,rg);
set dR = rectangle(rl,rp,rd,rg);
set TR = Trectangle(rl,rp,rd,rg);

Lm18: a`1 = rl by EUCLID:56;
Lm19: b`1 = rp by EUCLID:56;
Lm20: a`2 = 0 by EUCLID:56;
Lm21: b`2 = 0 by EUCLID:56;
Lm22: c`1 = 0 by EUCLID:56;
Lm23: c`2 = rg by EUCLID:56;
Lm24: d`1 = 0 by EUCLID:56;
Lm25: d`2 = rd by EUCLID:56;
Lm26: lg`1 = rl by EUCLID:56;
Lm27: lg`2 = rg by EUCLID:56;
Lm28: ld`1 = rl by EUCLID:56;
Lm29: ld`2 = rd by EUCLID:56;
Lm30: pg`1 = rp by EUCLID:56;
Lm31: pg`2 = rg by EUCLID:56;
Lm32: pd`1 = rp by EUCLID:56;
Lm33: pd`2 = rd by EUCLID:56;

Lm34: ld = |[ld`1,ld`2]| by EUCLID:57;
Lm35: lg = |[lg`1,lg`2]| by EUCLID:57;
Lm36: pd = |[pd`1,pd`2]| by EUCLID:57;
Lm37: pg = |[pg`1,pg`2]| by EUCLID:57;

Lm38: dR = (LSeg(ld,lg) \/ LSeg(lg,pg)) \/ (LSeg(pg,pd) \/ LSeg(pd,ld))
       by SPPOL_2:def 3;
       LSeg(ld,lg) c= LSeg(ld,lg) \/ LSeg(lg,pg) &
       LSeg(ld,lg) \/ LSeg(lg,pg) c= dR by Lm38,XBOOLE_1:7;
       then
Lm39: LSeg(ld,lg) c= dR by XBOOLE_1:1;
       LSeg(lg,pg) c= LSeg(ld,lg) \/ LSeg(lg,pg) &
       LSeg(ld,lg) \/ LSeg(lg,pg) c= dR by Lm38,XBOOLE_1:7;
       then
Lm40: LSeg(lg,pg) c= dR by XBOOLE_1:1;
       LSeg(pg,pd) c= LSeg(pg,pd) \/ LSeg(pd,ld) &
       LSeg(pg,pd) \/ LSeg(pd,ld) c= dR by Lm38,XBOOLE_1:7;
       then
Lm41: LSeg(pg,pd) c= dR by XBOOLE_1:1;
       LSeg(pd,ld) c= LSeg(pg,pd) \/ LSeg(pd,ld) &
       LSeg(pg,pd) \/ LSeg(pd,ld) c= dR by Lm38,XBOOLE_1:7;
       then
Lm42: LSeg(pd,ld) c= dR by XBOOLE_1:1;

Lm43: LSeg(ld,lg) is vertical by Lm26,Lm28,SPPOL_1:37;
Lm44: LSeg(pd,pg) is vertical by Lm30,Lm32,SPPOL_1:37;
Lm45: LSeg(a,lg) is vertical by Lm18,Lm26,SPPOL_1:37;
Lm46: LSeg(a,ld) is vertical by Lm18,Lm28,SPPOL_1:37;
Lm47: LSeg(b,pg) is vertical by Lm19,Lm30,SPPOL_1:37;
Lm48: LSeg(b,pd) is vertical by Lm19,Lm32,SPPOL_1:37;

Lm49: LSeg(ld,d) is horizontal by Lm29,Lm25,SPPOL_1:36;
Lm50: LSeg(pd,d) is horizontal by Lm33,Lm25,SPPOL_1:36;
Lm51: LSeg(lg,c) is horizontal by Lm27,Lm23,SPPOL_1:36;
Lm52: LSeg(pg,c) is horizontal by Lm31,Lm23,SPPOL_1:36;
Lm53: LSeg(lg,pg) is horizontal by Lm27,Lm31,SPPOL_1:36;
Lm54: LSeg(ld,pd) is horizontal by Lm29,Lm33,SPPOL_1:36;

Lm55: LSeg(a,lg) c= LSeg(ld,lg)
       by Lm43,Lm45,Lm18,Lm28,Lm20,Lm27,Lm29,JORDAN15:5;
Lm56: LSeg(a,ld) c= LSeg(ld,lg)
        by Lm43,Lm46,Lm28,Lm20,Lm27,Lm29,JORDAN15:5;
Lm57:  LSeg(b,pg) c= LSeg(pd,pg)
        by Lm47,Lm44,Lm19,Lm32,Lm31,Lm33,Lm21,JORDAN15:5;
Lm58: LSeg(b,pd) c= LSeg(pd,pg)
        by Lm48,Lm44,Lm32,Lm21,Lm31,Lm33,JORDAN15:5;


Lm59:  dR = {p where p is Point of T2: p`1 = rl & p`2 <= rg & p`2 >= rd or
       p`1 <= rp & p`1 >= rl & p`2 = rg or
       p`1 <= rp & p`1 >= rl & p`2 = rd or
       p`1 = rp & p`2 <= rg & p`2 >= rd} by SPPOL_2:58;
       then
Lm60:  c in dR by Lm22,Lm23;
Lm61:  d in dR by Lm24,Lm25,Lm59;
Lm62: (2+1)^2 = 4 + 4 + 1;
       then
Lm63:  sqrt(9) = 3 by SQUARE_1:def 4;
Lm64:   dist(a,b) = sqrt ((a`1-b`1)^2 + (a`2-b`2)^2) by GOBRD14:9
       .= --2 by Lm18,Lm19,Lm20,Lm21,SQUARE_1:90;

theorem Th80:
  for h being Homeomorphism of TOP-REAL 2 holds
  h.:C is being_simple_closed_curve
  proof
    let h be Homeomorphism of T2;
    consider f being Function of T2|R^2-unit_square, T2|C such that
A1: f is_homeomorphism by TOPREAL2:def 1;
    reconsider g = h|C as Function of T2|C,T2|(h.:C) by JORDAN24:12;
    take g*f;
    g is_homeomorphism by JORDAN24:14;
    hence g*f is_homeomorphism by A1,TOPS_2:71;
  end;

theorem Th81:
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies
  P c= closed_inside_of_rectangle(-1,1,-3,3)
  proof
    assume that
A1: a in P and
A2: b in P and
A3: for x, y being Point of TOP-REAL 2 st x in P & y in P holds
    dist(a,b) >= dist(x,y);
    let p be set;
    assume
A4: p in P;
    then reconsider p as Point of TOP-REAL 2;
A5: dist(a,p) = sqrt((rl-p`1)^2 + (0-p`2)^2) by Lm18,Lm20,GOBRD14:9
    .= sqrt((rl-p`1)^2 + p`2^2);
A6: now
      assume
A7:   9 < p`2^2;
      0+9 < (rl-p`1)^2+p`2^2 by A7,XREAL_1:10;
      then 3 < sqrt((rl-p`1)^2+p`2^2) by Lm63,SQUARE_1:95;
      then 2 < sqrt((rl-p`1)^2+p`2^2) by XREAL_1:2;
      hence contradiction by A5,A1,A3,A4,Lm64;
    end;
A8: now
      assume
A9:   rl > p`1;
      then LSeg(p,b) meets Vertical_Line(rl) by Lm19,Th16;
      then consider x being set such that
A10:   x in LSeg(p,b) and
A11:   x in Vertical_Line(rl) by XBOOLE_0:3;
      reconsider x as Point of T2 by A10;
A12:   x`1 = rl by A11,JORDAN6:34;
A13:   dist(p,b) = dist(p,x)+dist(x,b) by A10,JORDAN1K:29;
A14:   dist(x,b) = sqrt((x`1-b`1)^2 + (x`2-b`2)^2) by GOBRD14:9
      .= sqrt((-2)^2 + (x`2-0)^2) by A12,Lm19,EUCLID:56
      .= sqrt(4 + x`2^2);
A15:   now
        assume dist(x,b) < dist(a,b);
        then 4 + x`2^2 < 4 + 0 by A14,Lm64,SQUARE_1:85,94;
        hence contradiction by XREAL_1:8;
      end;
      dist(p,x) > 0 by A9,A12,JORDAN1K:22;
      then dist(p,b) + 0 > dist(a,b) + 0 by A13,A15,XREAL_1:10;
      hence contradiction by A2,A3,A4;
    end;
A16: now
      assume
A17:   p`1 > rp;
      then LSeg(p,a) meets Vertical_Line(rp) by Lm18,Th16;
      then consider x being set such that
A18:   x in LSeg(p,a) and
A19:   x in Vertical_Line(rp) by XBOOLE_0:3;
      reconsider x as Point of T2 by A18;
A20:   x`1 = rp by A19,JORDAN6:34;
A21:   dist(p,a) = dist(p,x)+dist(x,a) by A18,JORDAN1K:29;
A22:   dist(x,a) = sqrt((x`1-a`1)^2 + (x`2-a`2)^2) by GOBRD14:9
      .= sqrt(4 + x`2^2) by A20,Lm18,Lm20;
A23:   now
        assume dist(x,a) < dist(a,b);
        then 4 + x`2^2 < 4 + 0 by A22,Lm64,SQUARE_1:85,94;
        hence contradiction by XREAL_1:8;
      end;
      dist(p,x) > 0 by A17,A20,JORDAN1K:22;
      then dist(p,a) + 0 > dist(a,b) + 0 by A21,A23,XREAL_1:10;
      hence contradiction by A1,A3,A4;
    end;
A24: now
      assume rd > p`2;
      then p`2^2 > rd^2 by JGRAPH_5:2;
      hence contradiction by A6;
    end;
    rg >= p`2 by A6,Lm62,SQUARE_1:78;
    hence thesis by A8,A16,A24;
  end;

Lm65: dR c= R by Th54;

      lg`2 = lg`2 & lg`1 <= c`1 & c`1 <= pg`1 by Lm26,Lm30,EUCLID:56;
      then LSeg(lg,c) c= LSeg(lg,pg) by Lm51,Lm53,JORDAN15:6;
      then
Lm66:
          LSeg(lg,c) c= dR by Lm40,XBOOLE_1:1;

          LSeg(pg,c) c= LSeg(lg,pg)
          by Lm52,Lm53,Lm22,Lm23,Lm26,Lm27,Lm30,JORDAN15:6;
          then
Lm67:
           LSeg(pg,c) c= dR by Lm40,XBOOLE_1:1;

      ld`2 = ld`2 & ld`1 <= d`1 & d`1 <= pd`1 by Lm28,Lm32,EUCLID:56;
      then LSeg(ld,d) c= LSeg(ld,pd) by Lm49,Lm54,JORDAN15:6;
      then
Lm68:
          LSeg(ld,d) c= dR by Lm42,XBOOLE_1:1;

          LSeg(pd,d) c= LSeg(ld,pd)
          by Lm50,Lm54,Lm24,Lm25,Lm28,Lm29,Lm32,JORDAN15:6;
      then
Lm69:
            LSeg(pd,d) c= dR by Lm42,XBOOLE_1:1;

Lm70:
  0 <= p`2 & p in dR implies
  p in LSeg(a,lg) or p in LSeg(lg,c) or
  p in LSeg(c,pg) or p in LSeg(pg,b)
  proof
    assume
A1: 0 <= p`2;
    assume p in dR;
    then consider p1 such that
A2: p1 = p and
A3: p1`1 = rl & p1`2 <= rg & p1`2 >= rd or
    p1`1 <= rp & p1`1 >= rl & p1`2 = rg or
    p1`1 <= rp & p1`1 >= rl & p1`2 = rd or
    p1`1 = rp & p1`2 <= rg & p1`2 >= rd by Lm59;
    per cases by A3;
    suppose p1`1 = rl & p1`2 <= rg & p1`2 >= rd;
      hence thesis by A1,A2,Lm18,Lm20,Lm26,Lm27,GOBOARD7:8;
    end;
    suppose
A4:   p1`1 <= rp & p1`1 >= rl & p1`2 = rg;
      per cases;
      suppose p1`1 <= c`1;
        hence thesis by A2,A4,Lm23,Lm26,Lm27,GOBOARD7:9;
      end;
      suppose c`1 <= p1`1;
        hence thesis by A2,A4,Lm23,Lm30,Lm31,GOBOARD7:9;
      end;
    end;
    suppose p1`1 <= rp & p1`1 >= rl & p1`2 = rd;
      hence thesis by A1,A2;
    end;
    suppose p1`1 = rp & p1`2 <= rg & p1`2 >= rd;
      hence thesis by A1,A2,Lm19,Lm21,Lm30,Lm31,GOBOARD7:8;
    end;
  end;

Lm71:
  p`2 <= 0 & p in dR implies
  p in LSeg(a,ld) or p in LSeg(ld,d) or
  p in LSeg(d,pd) or p in LSeg(pd,b)
  proof
    assume
A1: p`2 <= 0;
    assume p in dR;
    then consider p1 such that
A2: p1 = p and
A3: p1`1 = rl & p1`2 <= rg & p1`2 >= rd or
    p1`1 <= rp & p1`1 >= rl & p1`2 = rg or
    p1`1 <= rp & p1`1 >= rl & p1`2 = rd or
    p1`1 = rp & p1`2 <= rg & p1`2 >= rd by Lm59;
    per cases by A3;
    suppose p1`1 = rl & p1`2 <= rg & p1`2 >= rd;
      hence thesis by A1,A2,Lm18,Lm20,Lm28,Lm29,GOBOARD7:8;
    end;
    suppose p1`1 <= rp & p1`1 >= rl & p1`2 = rg;
      hence thesis by A1,A2;
    end;
    suppose
A4:   p1`1 <= rp & p1`1 >= rl & p1`2 = rd;
      per cases;
      suppose p1`1 <= d`1;
        hence thesis by A2,A4,Lm25,Lm28,Lm29,GOBOARD7:9;
      end;
      suppose d`1 <= p1`1;
        hence thesis by A2,A4,Lm25,Lm32,Lm33,GOBOARD7:9;
      end;
    end;
    suppose p1`1 = rp & p1`2 <= rg & p1`2 >= rd;
      hence thesis by A1,A2,Lm19,Lm21,Lm32,Lm33,GOBOARD7:8;
    end;
  end;

theorem Th82:
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies
  P misses LSeg(|[-1,3]|,|[1,3]|)
  proof
    assume
A1:  a,b realize-max-dist-in P;
    assume P meets LSeg(lg,pg);
    then consider x being set such that
A2: x in P and
A3: x in LSeg(lg,pg) by XBOOLE_0:3;
    reconsider x as Point of T2 by A2;
    lg in LSeg(lg,pg) by TOPREAL1:6;
    then
A4: x`2 = rg by A3,Lm27,Lm53,SPPOL_1:def 2;
A5: a in P by A1,JORDAN24:def 1;
A6: dist(a,x) = sqrt ((a`1-x`1)^2 + (a`2-x`2)^2) by GOBRD14:9
    .= sqrt ((rl-x`1)^2 + rg^2) by A4,Lm20,EUCLID:56;
    0+4 < (rl-x`1)^2+9 by XREAL_1:10;
    then 2 < dist(a,x) by A6,SQUARE_1:85,95;
    hence thesis by A1,A2,A5,JORDAN24:def 1,Lm64;
  end;

theorem Th83:
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies
  P misses LSeg(|[-1,-3]|,|[1,-3]|)
  proof
    assume
A1:  a,b realize-max-dist-in P;
    assume P meets LSeg(ld,pd);
    then consider x being set such that
A2: x in P and
A3: x in LSeg(ld,pd) by XBOOLE_0:3;
    reconsider x as Point of T2 by A2;
    ld in LSeg(ld,pd) by TOPREAL1:6;
    then
A4: x`2 = rd by A3,Lm29,Lm54,SPPOL_1:def 2;
A5: a in P by A1,JORDAN24:def 1;
A6: dist(a,x) = sqrt ((a`1-x`1)^2 + (a`2-x`2)^2) by GOBRD14:9
    .= sqrt ((rl-x`1)^2 + (-rd)^2) by A4,Lm20,EUCLID:56;
    0+4 < (rl-x`1)^2+9 by XREAL_1:10;
    then 2 < dist(a,x) by A6,SQUARE_1:85,95;
    hence thesis by A1,A2,A5,JORDAN24:def 1,Lm64;
  end;

theorem Th84:
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies
  P /\ rectangle(-1,1,-3,3) = {|[-1,0]|,|[1,0]|}
  proof
    assume
A1: a,b realize-max-dist-in P;
    then
A2: a in P & b in P by JORDAN24:def 1;
    thus P /\ dR c= {a,b}
    proof
      let x be set;
      assume
A3:   x in P /\ dR;
      then
A4:   x in P by XBOOLE_0:def 3;
      x in dR by A3,XBOOLE_0:def 3;
      then
A5:   x in LSeg(ld,lg) \/ LSeg(lg,pg) or
      x in LSeg(pg,pd) \/ LSeg(pd,ld) by Lm38,XBOOLE_0:def 2;
      reconsider x as Point of T2 by A3;
      per cases by A5,XBOOLE_0:def 2;
      suppose
A6:     x in LSeg(ld,lg);
        ld in LSeg(ld,lg) by TOPREAL1:6;
        then
A7:     x`1 = rl by A6,Lm43,Lm28,SPPOL_1:def 3;
        per cases;
        suppose x`2 = 0;
          then x = a by A7,Lm18,Lm20,TOPREAL3:11;
          hence thesis by TARSKI:def 2;
        end;
        suppose x`2 <> 0;
          then
A8:       x`2^2 > 0 by SQUARE_1:74;
A9:       dist(b,x) = sqrt ((rp-rl)^2 + (0-x`2)^2) by A7,Lm19,Lm21,GOBRD14:9
          .= sqrt (4 + x`2^2);
          0+4 < x`2^2+4 by A8,XREAL_1:8;
          then 2 < sqrt(x`2^2+4) by SQUARE_1:85,95;
          hence thesis by A1,A2,A4,A9,JORDAN24:def 1,Lm64;
        end;
      end;
      suppose x in LSeg(lg,pg);
        then LSeg(lg,pg) meets P by A4,XBOOLE_0:3;
        hence thesis by A1,Th82;
      end;
      suppose
A10:     x in LSeg(pg,pd);
        pd in LSeg(pd,pg) by TOPREAL1:6;
        then
A11:     x`1 = rp by A10,Lm44,Lm32,SPPOL_1:def 3;
        per cases;
        suppose x`2 = 0;
          then x = b by A11,Lm19,Lm21,TOPREAL3:11;
          hence thesis by TARSKI:def 2;
        end;
        suppose x`2 <> 0;
          then
A12:       x`2^2 > 0 by SQUARE_1:74;
A13:       dist(x,a) = sqrt ((x`1-a`1)^2 + (x`2-a`2)^2) by GOBRD14:9
          .= sqrt (4 + x`2^2) by A11,Lm18,Lm20;
          0+4 < x`2^2+4 by A12,XREAL_1:8;
          then 2 < sqrt(x`2^2+4) by SQUARE_1:85,95;
          hence thesis by A1,A2,A4,A13,JORDAN24:def 1,Lm64;
        end;
      end;
      suppose x in LSeg(pd,ld);
        then LSeg(pd,ld) meets P by A4,XBOOLE_0:3;
        hence thesis by A1,Th83;
      end;
    end;
    let x be set;
    assume x in {a,b};
    then
A14: x = a or x = b by TARSKI:def 2;
    a in dR & b in dR by Lm18,Lm19,Lm20,Lm21,Lm59;
    hence thesis by A2,A14,XBOOLE_0:def 3;
  end;

Lm72:
          |[-1,0]|,|[1,0]| realize-max-dist-in C implies LSeg(lg,c) misses C
          proof
            assume a,b realize-max-dist-in C;
            then
A1:        C /\ dR = {a,b} by Th84;
            assume LSeg(lg,c) meets C;
            then consider q being set such that
A2:        q in LSeg(lg,c) and
A3:        q in C by XBOOLE_0:3;
            reconsider q as Point of T2 by A3;
            q in dR /\ C by A3,A2,Lm66,XBOOLE_0:def 3;
            then q = a or q = b by A1,TARSKI:def 2;
            hence contradiction by Lm20,Lm21,A2,TOPREAL3:18;
          end;

Lm73:
           |[-1,0]|,|[1,0]| realize-max-dist-in C implies LSeg(pg,c) misses C
           proof
             assume a,b realize-max-dist-in C;
             then
A1:         C /\ dR = {a,b} by Th84;
             assume LSeg(pg,c) meets C;
             then consider q being set such that
A2:         q in LSeg(pg,c) and
A3:         q in C by XBOOLE_0:3;
             reconsider q as Point of T2 by A3;
             q in dR /\ C by A3,A2,Lm67,XBOOLE_0:def 3;
             then q = a or q = b by A1,TARSKI:def 2;
             hence contradiction by Lm20,Lm21,A2,TOPREAL3:18;
           end;

Lm74:
          |[-1,0]|,|[1,0]| realize-max-dist-in C implies LSeg(ld,d) misses C
          proof
            assume a,b realize-max-dist-in C;
            then
A1:        C /\ dR = {a,b} by Th84;
            assume LSeg(ld,d) meets C;
            then consider q being set such that
A2:        q in LSeg(ld,d) and
A3:        q in C by XBOOLE_0:3;
            reconsider q as Point of T2 by A3;
            q in dR /\ C by A3,A2,Lm68,XBOOLE_0:def 3;
            then q = a or q = b by A1,TARSKI:def 2;
            hence contradiction by Lm20,Lm21,A2,TOPREAL3:18;
          end;

Lm75:
            |[-1,0]|,|[1,0]| realize-max-dist-in C implies LSeg(pd,d) misses C
           proof
             assume a,b realize-max-dist-in C;
             then
A1:         C /\ dR = {a,b} by Th84;
             assume LSeg(pd,d) meets C;
             then consider q being set such that
A2:         q in LSeg(pd,d) and
A3:         q in C by XBOOLE_0:3;
             reconsider q as Point of T2 by A3;
             q in dR /\ C by A3,A2,Lm69,XBOOLE_0:def 3;
             then q = a or q = b by A1,TARSKI:def 2;
             hence contradiction by Lm20,Lm21,A2,TOPREAL3:18;
           end;

Lm76:
  |[-1,0]|,|[1,0]| realize-max-dist-in C & p in C` & p in LSeg(a,lg) implies
  LSeg(p,lg) misses C
  proof
    assume that
A1: a,b realize-max-dist-in C and
A2: p in C` and
A3: p in LSeg(a,lg);
A4: C /\ dR = {a,b} by A1,Th84;
     assume LSeg(p,lg) meets C;
     then consider q being set such that
A5: q in LSeg(p,lg) and
A6: q in C by XBOOLE_0:3;
     reconsider q as Point of T2 by A6;
     lg in LSeg(a,lg) by TOPREAL1:6;
     then
A7: p`1 = lg`1 by A3,Lm45,SPPOL_1:def 3;
A8: p`2 <= lg`2 by A3,Lm27,JGRAPH_6:9;
A9: LSeg(p,lg) is vertical by A7,SPPOL_1:37;
     a`2 <= p`2 by A3,Lm20,JGRAPH_6:9;
     then
     LSeg(p,lg) c= LSeg(ld,lg)
     by A8,A9,Lm43,A7,Lm20,Lm26,Lm28,Lm29,JORDAN15:5;
     then LSeg(p,lg) c= dR by Lm39,XBOOLE_1:1;
     then q in dR /\ C by A6,A5,XBOOLE_0:def 3;
     then
A10: q = a or q = b by A4,TARSKI:def 2;
     a in LSeg(a,lg) by TOPREAL1:6;
     then
A11:  a`1 = p`1 by A3,Lm45,SPPOL_1:def 3;
A12:  a in C by A1,JORDAN24:def 1;
     not p in C by A2,SUBSET_1:54;
     then a`2 <> p`2 by A12,A11,TOPREAL3:11;
     then
A13:  a`2 < p`2 by A3,Lm20,JGRAPH_6:9;
     p = |[p`1,p`2]| by EUCLID:57;
     hence contradiction by A13,A5,A10,Lm19,Lm26,
     A7,A8,Lm35,JGRAPH_6:9;
   end;

Lm77:
  |[-1,0]|,|[1,0]| realize-max-dist-in C & p in C` & p in LSeg(b,pg) implies
  LSeg(p,pg) misses C
  proof
    assume that
A1: a,b realize-max-dist-in C and
A2: p in C` and
A3: p in LSeg(b,pg);
A4: C /\ dR = {a,b} by A1,Th84;
     assume LSeg(p,pg) meets C;
     then consider q being set such that
A5: q in LSeg(p,pg) and
A6: q in C by XBOOLE_0:3;
     reconsider q as Point of T2 by A6;
     pg in LSeg(b,pg) by TOPREAL1:6;
     then
A7: p`1 = pg`1 by A3,Lm47,SPPOL_1:def 3;
A8: p`2 <= pg`2 by A3,Lm31,JGRAPH_6:9;
A9: LSeg(p,pg) is vertical by A7,SPPOL_1:37;
     b`2 <= p`2 by A3,Lm21,JGRAPH_6:9;
     then LSeg(p,pg) c= LSeg(pd,pg)
     by A8,A9,Lm44,A7,Lm21,Lm30,Lm32,Lm33,JORDAN15:5;
     then LSeg(p,pg) c= dR by Lm41,XBOOLE_1:1;
     then q in dR /\ C by A6,A5,XBOOLE_0:def 3;
     then
A10: q = a or q = b by A4,TARSKI:def 2;
     b in LSeg(b,pg) by TOPREAL1:6;
     then
A11:  b`1 = p`1 by A3,Lm47,SPPOL_1:def 3;
A12:  b in C by A1,JORDAN24:def 1;
     not p in C by A2,SUBSET_1:54;
     then b`2 <> p`2 by A12,A11,TOPREAL3:11;
     then
A13:  b`2 < p`2 by A3,Lm21,JGRAPH_6:9;
     p = |[p`1,p`2]| by EUCLID:57;
     hence contradiction by A13,A10,Lm18,A5,Lm30,
     A7,A8,Lm37,JGRAPH_6:9;
   end;

Lm78:
  |[-1,0]|,|[1,0]| realize-max-dist-in C & p in C` & p in LSeg(a,ld) implies
  LSeg(p,ld) misses C
  proof
    assume that
A1: a,b realize-max-dist-in C and
A2: p in C` and
A3: p in LSeg(a,ld);
A4: C /\ dR = {a,b} by A1,Th84;
     assume LSeg(p,ld) meets C;
     then consider q being set such that
A5: q in LSeg(p,ld) and
A6: q in C by XBOOLE_0:3;
     reconsider q as Point of T2 by A6;
     ld in LSeg(a,ld) by TOPREAL1:6;
     then
A7: p`1 = ld`1 by A3,Lm46,SPPOL_1:def 3;
A8: ld`2 <= p`2 by A3,Lm29,JGRAPH_6:9;
A9: LSeg(p,ld) is vertical by A7,SPPOL_1:37;
     p`2 <= a`2 by A3,Lm20,JGRAPH_6:9;
     then
     LSeg(p,ld) c= LSeg(ld,lg) by A8,A9,Lm43,A7,Lm20,Lm27,JORDAN15:5;
     then LSeg(p,ld) c= dR by Lm39,XBOOLE_1:1;
     then q in dR /\ C by A6,A5,XBOOLE_0:def 3;
     then
A10: q = a or q = b by A4,TARSKI:def 2;
     a in LSeg(a,ld) by TOPREAL1:6;
     then
A11:  a`1 = p`1 by A3,Lm46,SPPOL_1:def 3;
A12:  a in C by A1,JORDAN24:def 1;
     not p in C by A2,SUBSET_1:54;
     then a`2 <> p`2 by A12,A11,TOPREAL3:11;
     then
A13:  p`2 < a`2 by A3,Lm20,JGRAPH_6:9;
     p = |[p`1,p`2]| by EUCLID:57;
     hence contradiction by A13,A10,Lm19,A5,Lm28,
     A7,A8,Lm34,JGRAPH_6:9;
   end;

Lm79:
  |[-1,0]|,|[1,0]| realize-max-dist-in C & p in C` & p in LSeg(b,pd) implies
  LSeg(p,pd) misses C
  proof
    assume that
A1: a,b realize-max-dist-in C and
A2: p in C` and
A3: p in LSeg(b,pd);
A4: C /\ dR = {a,b} by A1,Th84;
     assume LSeg(p,pd) meets C;
     then consider q being set such that
A5: q in LSeg(p,pd) and
A6: q in C by XBOOLE_0:3;
     reconsider q as Point of T2 by A6;
     pd in LSeg(b,pd) by TOPREAL1:6;
     then
A7: p`1 = pd`1 by A3,Lm48,SPPOL_1:def 3;
A8: pd`2 <= p`2 by A3,Lm33,JGRAPH_6:9;
A9: LSeg(p,pd) is vertical by A7,SPPOL_1:37;
     p`2 <= b`2 by A3,Lm21,JGRAPH_6:9;
     then
     LSeg(p,pd) c= LSeg(pd,pg) by A8,A9,Lm44,A7,Lm21,Lm31,JORDAN15:5;
     then LSeg(p,pd) c= dR by Lm41,XBOOLE_1:1;
     then q in dR /\ C by A6,A5,XBOOLE_0:def 3;
     then
A10: q = a or q = b by A4,TARSKI:def 2;
     b in LSeg(b,pd) by TOPREAL1:6;
     then
A11:  b`1 = p`1 by A3,Lm48,SPPOL_1:def 3;
A12:  b in C by A1,JORDAN24:def 1;
     not p in C by A2,SUBSET_1:54;
     then b`2 <> p`2 by A12,A11,TOPREAL3:11;
     then
A13:  p`2 < b`2 by A3,Lm21,JGRAPH_6:9;
     p = |[p`1,p`2]| by EUCLID:57;
     hence contradiction by A13,A10,Lm18,A5,Lm32,
     A7,A8,Lm36,JGRAPH_6:9;
   end;

Lm80:
  |[0,r]| in rectangle(rl,rp,rd,rg) implies r = rd or r = rg
  proof
    assume |[0,r]| in dR;
    then ex p st p = |[0,r]| &
    (p`1 = rl & p`2 <= rg & p`2 >= rd or
    p`1 <= rp & p`1 >= rl & p`2 = rg or
    p`1 <= rp & p`1 >= rl & p`2 = rd or
    p`1 = rp & p`2 <= rg & p`2 >= rd) by Lm59;
    hence thesis by EUCLID:56;
  end;

theorem Th85:
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies W-bound P = -1
  proof
    assume
A1: a,b realize-max-dist-in P;
    then
A2: P c= R by Th81;
A3: P = the carrier of (T2|P) by JORDAN1:1;
A4: a in P by A1,JORDAN24:def 1;
    reconsider P as non empty Subset of T2 by A1,JORDAN24:def 1;
    reconsider Z = (proj1||P).:the carrier of (T2|P)
    as Subset of REAL;
A5: for p be real number st p in Z holds p >= rl
    proof
      let p be real number;
      assume p in Z;
      then consider p0 being set such that
A6:   p0 in the carrier of T2|P and
      p0 in the carrier of T2|P and
A7:   p = (proj1||P).p0 by FUNCT_2:115;
      p0 in R by A6,A2,A3;
      then ex p1 st p0 = p1 &
      rl <= p1`1 & p1`1 <= rp & rd <= p1`2 & p1`2 <= rg;
      hence thesis by A3,A6,A7,PSCOMP_1:69;
    end;
    for q being real number st
    for p being real number st p in Z holds p >= q holds rl >= q
    proof
      let q be real number such that
A8:   for p being real number st p in Z holds p >= q;
      (proj1||P).a = a`1 by A4,PSCOMP_1:69;
      then rl in Z by A3,A4,Lm18,FUNCT_2:43;
      hence thesis by A8;
    end;
    hence thesis by A5,SEQ_4:61;
  end;

theorem Th86:
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies E-bound P = 1
  proof
    assume
A1: a,b realize-max-dist-in P;
    then
A2: P c= R by Th81;
A3: b in P by A1,JORDAN24:def 1;
    reconsider P as non empty Subset of T2 by A1,JORDAN24:def 1;
    reconsider Z = (proj1||P).:the carrier of (T2|P)
    as Subset of REAL;
A4: P = the carrier of (T2|P) by JORDAN1:1;
A5: for p be real number st p in Z holds p <= rp
    proof
      let p be real number;
      assume p in Z;
      then consider p0 being set such that
A6:   p0 in the carrier of T2|P and
      p0 in the carrier of T2|P and
A7:   p = (proj1||P).p0 by FUNCT_2:115;
      p0 in R by A6,A2,A4;
      then ex p1 st p0 = p1 &
      rl <= p1`1 & p1`1 <= rp & rd <= p1`2 & p1`2 <= rg;
      hence thesis by A4,A6,A7,PSCOMP_1:69;
    end;
    for q being real number st
    for p being real number st p in Z holds p <= q holds rp <= q
    proof
      let q be real number such that
A8:   for p being real number st p in Z holds p <= q;
      (proj1||P).b = b`1 by A3,PSCOMP_1:69;
      then rp in Z by A4,A3,Lm19,FUNCT_2:43;
      hence thesis by A8;
    end;
    hence thesis by A5,SEQ_4:63;
  end;

theorem Th87:
  for P being compact Subset of TOP-REAL 2 holds
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies W-most P = {|[-1,0]|}
  proof
    let P be compact Subset of T2;
    assume
A1: a,b realize-max-dist-in P;
    then
A2: P c= R by Th81;
    set L = LSeg(SW-corner P, NW-corner P);
A3: a in P by A1,JORDAN24:def 1;
A4: (SW-corner P)`1 = |[rl,S-bound P]|`1 by A1,Th85
    .= rl by EUCLID:56;
A5: (NW-corner P)`1 = |[rl,N-bound P]|`1 by A1,Th85
    .= rl by EUCLID:56;
    thus W-most P c= {a}
    proof
      let x be set;
      assume
A6:   x in W-most P;
      then
A7:   x in P by XBOOLE_0:def 3;
      reconsider x as Point of T2 by A6;
A8:   x in L by A6,XBOOLE_0:def 3;
      SW-corner P in L by TOPREAL1:6;
      then
A9:   x`1 = rl by A8,A4,SPPOL_1:def 3;
      x in R by A2,A7;
      then ex p st x = p & rl <= p`1 & p`1 <= rp & rd <= p`2 & p`2 <= rg;
      then x in dR by A9,Lm59;
      then x in P /\ dR by A7,XBOOLE_0:def 3;
      then x in {a,b} by A1,Th84;
      then x = a or x = b by TARSKI:def 2;
      hence thesis by A9,EUCLID:56,TARSKI:def 1;
    end;
    let x be set;
    assume x in {a};
    then
A10: x = a by TARSKI:def 1;
A11: (SW-corner P)`2 = S-bound P by PSCOMP_1:73;
    (NW-corner P)`2 = N-bound P by PSCOMP_1:75;
    then (SW-corner P)`2 <= a`2 & a`2 <= (NW-corner P)`2 by A11,A3,PSCOMP_1:71;
    then a in L by A4,A5,Lm18,GOBOARD7:8;
    hence thesis by A3,A10,XBOOLE_0:def 3;
  end;

theorem Th88:
  for P being compact Subset of TOP-REAL 2 holds
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies E-most P = {|[1,0]|}
  proof
    let P be compact Subset of T2;
    assume
A1: a,b realize-max-dist-in P;
    then
A2: P c= R by Th81;
    set L = LSeg(SE-corner P, NE-corner P);
A3: b in P by A1,JORDAN24:def 1;
A4: (SE-corner P)`1 = |[rp,S-bound P]|`1 by A1,Th86
    .= rp by EUCLID:56;
A5: (NE-corner P)`1 = |[rp,N-bound P]|`1 by A1,Th86
    .= rp by EUCLID:56;
    thus E-most P c= {b}
    proof
      let x be set;
      assume
A6:   x in E-most P;
      then
A7:   x in P by XBOOLE_0:def 3;
      reconsider x as Point of T2 by A6;
A8:   x in L by A6,XBOOLE_0:def 3;
      SE-corner P in L by TOPREAL1:6;
      then
A9:   x`1 = rp by A8,A4,SPPOL_1:def 3;
      x in R by A2,A7;
      then ex p st x = p & rl <= p`1 & p`1 <= rp & rd <= p`2 & p`2 <= rg;
      then x in dR by A9,Lm59;
      then x in P /\ dR by A7,XBOOLE_0:def 3;
      then x in {a,b} by A1,Th84;
      then x = a or x = b by TARSKI:def 2;
      hence thesis by A9,EUCLID:56,TARSKI:def 1;
    end;
    let x be set;
    assume x in {b};
    then
A10: x = b by TARSKI:def 1;
A11: (SE-corner P)`2 = S-bound P by PSCOMP_1:79;
    (NE-corner P)`2 = N-bound P by PSCOMP_1:77;
    then (SE-corner P)`2 <= b`2 & b`2 <= (NE-corner P)`2 by A11,A3,PSCOMP_1:71;
    then b in L by A4,A5,Lm19,GOBOARD7:8;
    hence thesis by A3,A10,XBOOLE_0:def 3;
  end;

theorem Th89:
  for P being compact Subset of TOP-REAL 2 holds
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies
  W-min P = |[-1,0]| & W-max P = |[-1,0]|
  proof
    let P be compact Subset of T2;
    set M = W-most P;
    assume
A1: a,b realize-max-dist-in P;
    then
A2: M = {a} by Th87;
    set f = proj2||M;
A3: dom f = the carrier of (T2|M) by FUNCT_2:def 1;
A4: the carrier of (T2|M) = M by JORDAN1:1;
A5: a in {a} by TARSKI:def 1;
A6: f.:the carrier of (T2|M) = {f.a} by A3,A4,A5,A2,FUNCT_1:117
    .= {proj2.a} by A2,A5,FUNCT_1:72
    .= {a`2} by PSCOMP_1:def 29;
A7: inf (proj2||M) = a`2 by A6,SEQ_4:22;
A8: sup (proj2||M) = a`2 by A6,SEQ_4:22;
    a = |[a`1,a`2]| by EUCLID:57;
    hence thesis by A7,A8,Lm18,A1,Th85;
  end;

theorem Th90:
  for P being compact Subset of TOP-REAL 2 holds
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies
  E-min P = |[1,0]| & E-max P = |[1,0]|
  proof
    let P be compact Subset of T2;
    set M = E-most P;
    assume
A1: a,b realize-max-dist-in P;
    then
A2: M = {b} by Th88;
    set f = proj2||M;
A3: dom f = the carrier of (T2|M) by FUNCT_2:def 1;
A4: the carrier of (T2|M) = M by JORDAN1:1;
A5: b in {b} by TARSKI:def 1;
A6: f.:the carrier of (T2|M) = {f.b} by A3,A4,A5,A2,FUNCT_1:117
    .= {proj2.b} by A2,A5,FUNCT_1:72
    .= {b`2} by PSCOMP_1:def 29;
A7: inf (proj2||M) = b`2 by A6,SEQ_4:22;
A8: sup (proj2||M) = b`2 by A6,SEQ_4:22;
    b = |[b`1,b`2]| by EUCLID:57;
    hence thesis by A7,A8,Lm19,A1,Th86;
  end;

Lm81:
      |[-1,0]|,|[1,0]| realize-max-dist-in P implies
      c`1 = (W-bound P + E-bound P) / 2
      proof
        assume a,b realize-max-dist-in P;
        then W-bound P = rl & E-bound P = rp by Th85,Th86;
        hence thesis by EUCLID:56;
      end;

Lm82:
      |[-1,0]|,|[1,0]| realize-max-dist-in P implies
      d`1 = (W-bound P + E-bound P) / 2
      proof
        assume a,b realize-max-dist-in P;
        then W-bound P = rl & E-bound P = rp by Th85,Th86;
        hence thesis by EUCLID:56;
      end;

theorem Th91:
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies
  LSeg(|[0,3]|,UMP P) is vertical
  proof
    assume a,b realize-max-dist-in P;
    then c`1 = (W-bound P + E-bound P) / 2 by Lm81
    .= (UMP P)`1 by JORDAN21:33;
    hence thesis by SPPOL_1:37;
  end;

theorem Th92:
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies
  LSeg(LMP P,|[0,-3]|) is vertical
  proof
    assume a,b realize-max-dist-in P;
    then d`1 = (W-bound P + E-bound P) / 2 by Lm82
    .= (LMP P)`1 by JORDAN21:35;
    hence thesis by SPPOL_1:37;
  end;

theorem Th93:
  |[-1,0]|,|[1,0]| realize-max-dist-in P & p in P implies p`2 < 3
  proof
    assume that
A1: a,b realize-max-dist-in P and
A2: p in P;
A3: P /\ dR = {a,b} by A1,Th84;
    P c= R by A1,Th81;
    then
A4: p in R by A2;
A5: ex p1 st p1 = p & rl <= p1`1 & p1`1 <= rp & rd <= p1`2 & p1`2 <= rg
    by A4;
    now
      assume
A6:   p`2 = c`2;
      then p in LSeg(lg,pg) by A5,Lm23,Lm26,Lm27,Lm30,Lm31,GOBOARD7:9;
      then p in P /\ dR by A2,Lm40,XBOOLE_0:def 3;
      hence contradiction by A6,Lm20,Lm21,Lm23,A3,TARSKI:def 2;
    end;
    hence thesis by A5,Lm23,REAL_1:def 5;
  end;

theorem Th94:
  |[-1,0]|,|[1,0]| realize-max-dist-in P & p in P implies -3 < p`2
  proof
    assume that
A1: a,b realize-max-dist-in P and
A2: p in P;
A3: P /\ dR = {a,b} by A1,Th84;
    P c= R by A1,Th81;
    then
A4: p in R by A2;
A5: ex p1 st p1 = p & rl <= p1`1 & p1`1 <= rp & rd <= p1`2 & p1`2 <= rg
    by A4;
    now
      assume
A6:   p`2 = d`2;
      then p in LSeg(ld,pd) by A5,Lm25,Lm28,Lm29,Lm32,Lm33,GOBOARD7:9;
      then p in P /\ dR by A2,Lm42,XBOOLE_0:def 3;
      then p = a or p = b by A3,TARSKI:def 2;
      hence contradiction by A6,Lm25,EUCLID:56;
    end;
    hence thesis by A5,Lm25,REAL_1:def 5;
  end;

theorem Th95:
  |[-1,0]|,|[1,0]| realize-max-dist-in D & p in LSeg(|[0,3]|,UMP D) implies
  (UMP D)`2 <= p`2
  proof
    set x = UMP D;
    assume that
A1: a,b realize-max-dist-in D and
A2: p in LSeg(c,x);
A3: x in LSeg(c,x) by TOPREAL1:6;
A4: LSeg(c,x) is vertical by A1,Th91;
A5: c = |[c`1,c`2]| & x = |[x`1,x`2]| by EUCLID:57;
    c in LSeg(c,x) by TOPREAL1:6;
    then
A6: c`1 = x`1 by A3,A4,SPPOL_1:def 3;
    x in D by JORDAN21:43;
    then x`2 <= c`2 by A1,Lm23,Th93;
    hence thesis by A2,A5,A6,JGRAPH_6:9;
  end;

theorem Th96:
  |[-1,0]|,|[1,0]| realize-max-dist-in D & p in LSeg(LMP D,|[0,-3]|) implies
  p`2 <= (LMP D)`2
  proof
    set x = LMP D;
    assume that
A1: a,b realize-max-dist-in D and
A2: p in LSeg(x,d);
A3: x in LSeg(x,d) by TOPREAL1:6;
A4: LSeg(x,d) is vertical by A1,Th92;
A5: d = |[d`1,d`2]| & x = |[x`1,x`2]| by EUCLID:57;
    d in LSeg(x,d) by TOPREAL1:6;
    then
A6: d`1 = x`1 by A3,A4,SPPOL_1:def 3;
    x in D by JORDAN21:44;
    then d`2 <= x`2 by A1,Lm25,Th94;
    hence thesis by A2,A5,A6,JGRAPH_6:9;
  end;

theorem Th97:
  |[-1,0]|,|[1,0]| realize-max-dist-in D implies
  LSeg(|[0,3]|,UMP D) c= north_halfline UMP D
  proof
    set p = UMP D;
    assume
A1: a,b realize-max-dist-in D;
    let x be set;
    assume
A2: x in LSeg(c,p);
    then reconsider x as Point of T2;
A3: p in LSeg(c,p) by TOPREAL1:6;
    LSeg(c,p) is vertical by A1,Th91;
    then
A4: x`1 = p`1 by A2,A3,SPPOL_1:def 3;
    p`2 <= x`2 by A1,A2,Th95;
    hence thesis by A4,JORDAN1A:def 2;
  end;

theorem Th98:
  |[-1,0]|,|[1,0]| realize-max-dist-in D implies
  LSeg(LMP D,|[0,-3]|) c= south_halfline LMP D
  proof
    set p = LMP D;
    assume
A1: a,b realize-max-dist-in D;
    let x be set;
    assume
A2: x in LSeg(p,d);
    then reconsider x as Point of T2;
A3: p in LSeg(p,d) by TOPREAL1:6;
A4: LSeg(p,d) is vertical by A1,Th92;
    then
A5: x`1 = p`1 by A2,A3,SPPOL_1:def 3;
A6: d = |[d`1,d`2]| & p = |[p`1,p`2]| by EUCLID:57;
    d in LSeg(p,d) by TOPREAL1:6;
    then
A7: d`1 = p`1 by A3,A4,SPPOL_1:def 3;
    p in D by JORDAN21:44;
    then d`2 <= p`2 by A1,Lm25,Th94;
    then x`2 <= p`2 by A2,A6,A7,JGRAPH_6:9;
    hence thesis by A5,JORDAN1A:def 4;
  end;

theorem Th99:
  |[-1,0]|,|[1,0]| realize-max-dist-in C & P is_inside_component_of C implies
  LSeg(|[0,3]|,UMP C) misses P
  proof
    set m = UMP C;
    set L = LSeg(c,m);
    assume that
A1: a,b realize-max-dist-in C and
A2: P is_inside_component_of C;
    consider VP being Subset of T2|C` such that
A3: VP = P and
     VP is_a_component_of T2|C` and
     VP is bounded Subset of Euclid 2 by A2,JORDAN2C:17;
     m in L by TOPREAL1:6;
     then {m} c= L by ZFMISC_1:37;
     then
A4:  L = L \ {m} \/ {m} by XBOOLE_1:45;
     L c= north_halfline m by A1,Th97;
     then
A5:  L \ {m} c= north_halfline m \ {m} by XBOOLE_1:33;
     north_halfline m \ {m} c= UBD C by Th20;
     then
A6:  L \ {m} c= UBD C by A5,XBOOLE_1:1;
     UBD C misses P by A2,Th22;
     then
A7:  L \ {m} misses P by A6,XBOOLE_1:63;
     m in C by JORDAN21:43;
     then {m} misses P by A3,Lm6;
     hence thesis by A4,A7,XBOOLE_1:70;
   end;

theorem Th100:
  |[-1,0]|,|[1,0]| realize-max-dist-in C & P is_inside_component_of C implies
  LSeg(LMP C,|[0,-3]|) misses P
  proof
    set m = LMP C;
    set L = LSeg(m,d);
    assume that
A1: a,b realize-max-dist-in C and
A2: P is_inside_component_of C;
    consider VP being Subset of T2|C` such that
A3: VP = P and
     VP is_a_component_of T2|C` and
     VP is bounded Subset of Euclid 2 by A2,JORDAN2C:17;
     m in L by TOPREAL1:6;
     then {m} c= L by ZFMISC_1:37;
     then
A4:  L = L \ {m} \/ {m} by XBOOLE_1:45;
     L c= south_halfline m by A1,Th98;
     then
A5:  L \ {m} c= south_halfline m \ {m} by XBOOLE_1:33;
     south_halfline m \ {m} c= UBD C by Th21;
     then
A6:  L \ {m} c= UBD C by A5,XBOOLE_1:1;
     UBD C misses P by A2,Th22;
     then
A7:  L \ {m} misses P by A6,XBOOLE_1:63;
     m in C by JORDAN21:44;
     then {m} misses P by A3,Lm6;
     hence thesis by A4,A7,XBOOLE_1:70;
   end;

theorem Th101:
  |[-1,0]|,|[1,0]| realize-max-dist-in D implies
  LSeg(|[0,3]|,UMP D) /\ D = {UMP D}
  proof
    assume
A1: a,b realize-max-dist-in D;
    set m = UMP D;
    set w = (W-bound D + E-bound D) / 2;
A2: c`1 = w by A1,Lm81;
A3: m`1 = w by JORDAN21:33;
A4: m in LSeg(c,m) by TOPREAL1:6;
A5: m in D by JORDAN21:43;
     thus LSeg(c,m) /\ D c= {m}
     proof
      let x be set;
      assume
A6:  x in LSeg(c,m) /\ D;
      then
A7:  x in LSeg(c,m) & x in D by XBOOLE_0:def 3;
      reconsider x as Point of T2 by A6;
      LSeg(c,m) is vertical by A3,A2,SPPOL_1:37;
      then
A8:  x`1 = m`1 by A4,A7,SPPOL_1:def 3;
      then x in Vertical_Line w by A3,JORDAN6:34;
      then x in D /\ Vertical_Line w by A7,XBOOLE_0:def 3;
      then
A9:  x`2 <= m`2 by JORDAN21:41;
      m`2 <= x`2 by A1,A7,Th95;
      then x`2 = m`2 by A9,XREAL_1:1;
      then x = m by A8,TOPREAL3:11;
      hence thesis by TARSKI:def 1;
    end;
    let x be set;
    assume x in {m};
    then x = m by TARSKI:def 1;
    hence thesis by A4,A5,XBOOLE_0:def 3;
  end;

theorem
  |[-1,0]|,|[1,0]| realize-max-dist-in D implies
  LSeg(|[0,-3]|,LMP D) /\ D = {LMP D}
  proof
    assume
A1: a,b realize-max-dist-in D;
    set m = LMP D;
    set w = (W-bound D + E-bound D) / 2;
A2: d`1 = w by A1,Lm82;
A3: m`1 = w by JORDAN21:35;
A4: m in LSeg(d,m) by TOPREAL1:6;
A5: m in D by JORDAN21:44;
     thus LSeg(d,m) /\ D c= {m}
     proof
      let x be set;
      assume
A6:  x in LSeg(d,m) /\ D;
      then
A7:  x in LSeg(d,m) & x in D by XBOOLE_0:def 3;
      reconsider x as Point of T2 by A6;
      LSeg(d,m) is vertical by A3,A2,SPPOL_1:37;
      then
A8:  x`1 = m`1 by A4,A7,SPPOL_1:def 3;
      then x in Vertical_Line w by A3,JORDAN6:34;
      then x in D /\ Vertical_Line w by A7,XBOOLE_0:def 3;
      then
A9:  m`2 <= x`2 by JORDAN21:42;
      x`2 <= m`2 by A1,A7,Th96;
      then x`2 = m`2 by A9,XREAL_1:1;
      then x = m by A8,TOPREAL3:11;
      hence thesis by TARSKI:def 1;
    end;
    let x be set;
    assume x in {m};
    then x = m by TARSKI:def 1;
    hence thesis by A4,A5,XBOOLE_0:def 3;
  end;

theorem Th103:
  P is compact &
  |[-1,0]|,|[1,0]| realize-max-dist-in P & A is_inside_component_of P implies
  A c= closed_inside_of_rectangle(-1,1,-3,3)
  proof
    assume that
A1: P is compact and
A2: |[-1,0]|,|[1,0]| realize-max-dist-in P and
A3: A is_inside_component_of P;
    let x be set;
    assume that
A4: x in A and
A5: not x in R;
    P c= R by A2,Th81;
    then
A6: R` c= P` by SUBSET_1:31;
    reconsider x as Point of T2 by A4;
A7: not (rl <= x`1 & x`1 <= rp & rd <= x`2 & x`2 <= rg) by A5;
    per cases;
    suppose
A8:   0 <= x`1;
      set E = east_halfline(x);
      E c= R`
      proof
        let e be set;
        assume
A9:     e in E;
        then reconsider e as Point of T2;
A10:     e`1 >= x`1 & e`2 = x`2 by A9,JORDAN1A:def 3;
        now
          assume e in R;
          then ex p st e = p & rl <= p`1 & p`1 <= rp & rd <= p`2 & p`2 <= rg;
          hence contradiction by A10,A8,A7,XREAL_1:2;
        end;
        hence thesis by SUBSET_1:50;
      end;
      then E c= P` by A6,XBOOLE_1:1;
      then E misses P by SUBSET_1:43;
      then
A11:   E c= UBD P by A1,JORDAN1C:15;
      x in E by JORDAN1C:7;
      then A meets UBD P by A4,A11,XBOOLE_0:3;
      hence thesis by A3,Th22;
    end;
    suppose
A12:   x`1 < 0;
      set E = west_halfline(x);
      E c= R`
      proof
        let e be set;
        assume
A13:     e in E;
        then reconsider e as Point of T2;
A14:     e`1 <= x`1 & e`2 = x`2 by A13,JORDAN1A:def 5;
        now
          assume e in R;
          then ex p st e = p & rl <= p`1 & p`1 <= rp & rd <= p`2 & p`2 <= rg;
          hence contradiction by A14,A12,A7,XREAL_1:2;
        end;
        hence thesis by SUBSET_1:50;
      end;
      then E c= P` by A6,XBOOLE_1:1;
      then E misses P by SUBSET_1:43;
      then
A15:   E c= UBD P by A1,JORDAN1C:14;
      x in E by JORDAN1C:7;
      then A meets UBD P by A4,A15,XBOOLE_0:3;
      hence thesis by A3,Th22;
    end;
  end;

Lm83:
  p in R implies R c= Ball(p,10)
  proof
    assume p in R;
    then consider p1 such that
A1: p1 = p and
A2: rl <= p1`1 & p1`1 <= rp & rd <= p1`2 & p1`2 <= rg;
    let x be set;
    assume
A3: x in R;
    then reconsider x as Point of T2;
    consider p2 such that
A4: p2 = x and
A5: rl <= p2`1 & p2`1 <= rp & rd <= p2`2 & p2`2 <= rg by A3;
A6: ex s, t being Point of Euclid 2 st
    s = p1 & t = p2 & dist(p1,p2) = dist(s,t) by GOBRD14:def 1;
    dist(p1,p2) <= (rp-rl) + (rg-rd) by A2,A5,GOBRD14:12;
    then dist(p1,p2) < 10 by XREAL_1:2;
    then |.x-p.| < 10 by A1,A4,A6,KURATO_2:31;
    hence thesis by TOPREAL9:7;
  end;

theorem
  |[-1,0]|,|[1,0]| realize-max-dist-in C implies LSeg(|[0,3]|,|[0,-3]|) meets C
  proof
    assume
A1: a,b realize-max-dist-in C;
    set Jc = Upper_Arc C;
    consider Pf being Path of c,d, f being Function
 of I[01], T2|LSeg(c,d) such that
A2: rng f = LSeg(c,d) & Pf = f by Th52;
    a = W-min C & b = E-max C by A1,Th89,Th90;
    then Jc is_an_arc_of a,b by JORDAN6:def 8;
    then consider Pg being Path of a,b, g being Function
 of I[01], T2|Jc such that
A3: rng g = Jc & Pg = g by Th51;
A4: Jc c= C by JORDAN1A:16;
A5: C c= R by A1,Th81;
A6: a in C & b in C by A1,JORDAN24:def 1;
A7: the carrier of TR = R by TOPREALA:53;
     reconsider AR = a, BR = b, CR = c, DR = d
     as Point of TR by A6,A5,Lm60,Lm61,Lm65,TOPREALA:53;
     rng Pg c= the carrier of TR by A3,A4,A5,A7,XBOOLE_1:1;
     then reconsider h = Pg as Path of AR,BR by Th39;
     LSeg(c,d) c= R by Lm60,Lm65,Lm61,JORDAN1:def 1;
     then reconsider v = Pf as Path of CR,DR by A2,A7,Th39;
     consider s, t being Point of I[01] such that
A8: h.s = v.t by Lm18,Lm19,Lm23,Lm25,JGRAPH_8:6;
     dom h = the carrier of I[01] & dom v = the carrier of I[01]
     by FUNCT_2:def 1;
     then v.t in rng Pf & h.s in rng Pg by FUNCT_1:def 5;
     hence thesis by A4, A2,A3,A8,XBOOLE_0:3;
   end;

Lm84:
  |[-1,0]|,|[1,0]| realize-max-dist-in C implies
  ex Jc, Jd being compact with_the_max_arc Subset of T2 st
  Jc is_an_arc_of |[-1,0]|,|[1,0]| & Jd is_an_arc_of |[-1,0]|,|[1,0]| &
  C = Jc \/ Jd &
  Jc /\ Jd = {|[-1,0]|,|[1,0]|} & UMP C in Jc & LMP C in Jd &
  W-bound C = W-bound Jc & E-bound C = E-bound Jc
  proof
    assume
A1: a,b realize-max-dist-in C;
    set U = Upper_Arc C;
    set L = Lower_Arc C;
A2: U \/ L = C by JORDAN6:def 9;
A3: UMP C in C by JORDAN21:43;
    LMP C in C by JORDAN21:44;
    then
A4: LMP C in U or LMP C in L by A2,XBOOLE_0:def 2;
A5: W-min C = a & E-max C = b by A1,Th89,Th90;
    per cases by A2,A3,XBOOLE_0:def 2;
    suppose
A6:   UMP C in U;
      take U, L;
      thus thesis by A5,A6,A4,JORDAN6:65,JORDAN21:63,26,27;
    end;
    suppose
A7:   UMP C in L;
      take L, U;
      thus thesis by A5,A7,A4,JORDAN6:65,JORDAN21:62,28,29;
    end;
  end;

theorem Th105:
  |[-1,0]|,|[1,0]| realize-max-dist-in C implies
  for Jc, Jd being compact with_the_max_arc Subset of T2 st
  Jc is_an_arc_of |[-1,0]|,|[1,0]| & Jd is_an_arc_of |[-1,0]|,|[1,0]| &
  C = Jc \/ Jd & Jc /\ Jd = {|[-1,0]|,|[1,0]|} & UMP C in Jc & LMP C in Jd &
  W-bound C = W-bound Jc & E-bound C = E-bound Jc
  for Ux being Subset of TOP-REAL 2 st
  Ux = skl Down ((1/2) * ((UMP (LSeg(LMP Jc,|[0,-3]|) /\ Jd)) + LMP Jc), C`)
  holds Ux is_inside_component_of C &
  for V being Subset of T2 st V is_inside_component_of C holds
  V = Ux
  proof
    set m = UMP C;
    set j = LMP C;
    assume
A1: a,b realize-max-dist-in C;
    let Jc, Jd being compact with_the_max_arc Subset of T2 such that
A2: Jc is_an_arc_of a,b and
A3: Jd is_an_arc_of a,b and
A4: C = Jc \/ Jd and
A5: Jc /\ Jd = {a,b} and
A6: UMP C in Jc and
A7: LMP C in Jd and
A8: W-bound C = W-bound Jc and
A9: E-bound C = E-bound Jc;
     set l = LMP Jc;
     set LJ = LSeg(l,d) /\ Jd;
     set k = UMP LJ;
     set x = (1/2)*(k+l);
     set w = (W-bound C + E-bound C) / 2;
     let Ux be Subset of TOP-REAL 2 such that
A10:  Ux = skl Down (x,C`);
A11: C c= R by A1,Th81;
A12: W-bound C = rl by A1,Th85;
A13: E-bound C = rp by A1,Th86;
A14:  a in C & b in C by A1,JORDAN24:def 1;
A15:  m in C by JORDAN21:43;
A16: l in Jc by JORDAN21:44;
A17: Jd c= C by A4,XBOOLE_1:7;
A18: Jc c= C by A4,XBOOLE_1:7;
     then
A19: l in C by A16;
A20: m`2 < c`2 by A1,A15,Lm23,Th93;
A21: l`1 = 0 by A8,A9,A12,A13,JORDAN21:35;
A22: c`1 = w by A1,Lm81;
A23: m`1 = w by JORDAN21:33;
A24: m <> a & m <> b & l <> a & l <> b
       by A12,A13,Lm18,Lm19,A8,A9,JORDAN21:35,33;
     then consider Pml being Path of m,l such that
A25:  rng Pml c= Jc and
A26: rng Pml misses {a,b} by A2,A16,A6,Th53;
     set ml = rng Pml;
A27: ml c= C by A25,A18,XBOOLE_1:1;
A28: j in C by A17,A7;
A29: LSeg(l,d) is vertical by A21,Lm24,SPPOL_1:37;
A30: d`2 <= j`2 by A1,A17,A7,Lm25,Th94;
A31: j`1 = 0 by A12,A13,JORDAN21:35;
     l in Vertical_Line w by A21,A12,A13,JORDAN6:34;
     then
A32: l in C /\ Vertical_Line w by A18,A16,XBOOLE_0:def 3;
     then j`2 <= l`2 by JORDAN21:42;
     then j in LSeg(l,d) by A30,A21,A31,Lm24,GOBOARD7:8;
     then
A33: LJ is non empty by A7,XBOOLE_0:def 3;
A34: LJ is compact by COMPTS_1:20;
A35: for A being vertical non empty compact Subset of TOP-REAL 2 holds
     A is with_the_max_arc;
A36: LJ is vertical by A29,Th4;
     then
A37: LJ is with_the_max_arc by A35,A33,A34;
A38: k in LJ by A37,A34,JORDAN21:43;
     then
A39: k in LSeg(l,d) by XBOOLE_0:def 3;
A40: k in Jd by A38,XBOOLE_0:def 3;
     then
A41: k in C by A17;
A42: d in LSeg(l,d) by TOPREAL1:6;
A43: k`1 = 0 by A39,A29,A42,Lm24,SPPOL_1:def 3;
     k <> a & k <> b & j <> a & j <> b by A43,A31,EUCLID:56;
     then consider Pkj being Path of k,j such that
A44: rng Pkj c= Jd and
A45: rng Pkj misses {a,b} by A3,A7,A40,Th53;
     set kj = rng Pkj;
A46: kj c= C by A17,A44,XBOOLE_1:1;
A47: x in LSeg(k,l) by GOBOARD7:7;
A48: skl Down(x,C`) is_a_component_of T2|C` by CONNSP_1:43;
A49: the carrier of T2|C` = C` by JORDAN1:1;
A50: LSeg(l,k) is vertical by A21,A43,SPPOL_1:37;
A51: k in LSeg(l,k) by TOPREAL1:6;
A52: l = |[l`1,l`2]| & k = |[k`1,k`2]| by EUCLID:57;
A53: d = |[d`1,d`2]| by EUCLID:57;
     d`2 <= l`2 by A1,A18,A16,Lm25,Th94;
     then
A54: k`2 <= l`2 by A52,A39,A53,A21,Lm24,JGRAPH_6:9;
     a <> k & b <> k & a <> l & b <> l by A21,A43,EUCLID:56;
     then not k in {a,b} & not l in {a,b} by TARSKI:def 2;
     then
A55: k <> l by A16,A40,A5,XBOOLE_0:def 3;
     then k`2 <> l`2 by A21,A43,TOPREAL3:11;
     then
A56: k`2 < l`2 by A54,REAL_1:def 5;
     k in Vertical_Line w by A43,A12,A13,JORDAN6:34;
     then k in C /\ Vertical_Line w by A17,A40,XBOOLE_0:def 3;
     then j`2 <= k`2 by JORDAN21:42;
     then d`2 <= k`2 by A30,XREAL_1:2;
     then
A57: LSeg(l,k) c= LSeg(l,d) by A50,A29,A43,A54,Lm24,JORDAN15:5;
A58: LSeg(l,k) \ {l,k} c= C`
     proof
       let q be set;
       assume that
A59:   q in LSeg(l,k) \ {l,k} and
A60:   not q in C`;
A61:   q in LSeg(l,k) by A59,XBOOLE_0:def 4;
       reconsider q as Point of T2 by A59;
A62:   q in C by A60,SUBSET_1:50;
A63:   q`1 = w by A51,A61,A50,A43,A12,A13,SPPOL_1:def 3;
       then
A64:   q in Vertical_Line w by JORDAN6:34;
       per cases by A62,A4,XBOOLE_0:def 2;
       suppose q in Jc;
         then q in Jc /\ Vertical_Line w by A64,XBOOLE_0:def 3;
         then
A65:     l`2 <= q`2 by A8,A9,JORDAN21:42;
         q`2 <= l`2 by A61,A54,A21,A43,A52,JGRAPH_6:9;
         then l`2 = q`2 by A65,XREAL_1:1;
         then l = q by A63,A21,A12,A13,TOPREAL3:11;
         then q in {l,k} by TARSKI:def 2;
         hence contradiction by A59,XBOOLE_0:def 4;
       end;
       suppose q in Jd;
         then
A66:     q in LJ by A57,A61,XBOOLE_0:def 3;
A67:     q`1 = d`1 by A57,A61,A29,A42,SPPOL_1:def 3;
         LJ c= LSeg(l,d) by XBOOLE_1:17;
         then
A68:     W-bound LSeg(l,d) <= W-bound LJ & E-bound LJ <= E-bound LSeg(l,d)
         by A66,JORDAN1E:2,4;
A69:     W-bound LJ = E-bound LJ by A34,A33,A36,SPRECT_1:17;
A70:     W-bound LSeg(l,d) = d`1 & E-bound LSeg(l,d) = d`1
         by A21,Lm24,SPRECT_1:62,65;
         then W-bound LSeg(l,d) = W-bound LJ & E-bound LSeg(l,d) = E-bound LJ
         by A68,A69,XREAL_1:1;
         then q in Vertical_Line ((W-bound LJ + E-bound LJ) / 2)
         by A70,A67,JORDAN6:34;
         then q in LJ /\ Vertical_Line ((W-bound LJ + E-bound LJ) / 2)
         by A66,XBOOLE_0:def 3;
         then
A71:     q`2 <= k`2 by A34,JORDAN21:41;
         k`2 <= q`2 by A61,A54,A21,A43,A52,JGRAPH_6:9;
         then k`2 = q`2 by A71,XREAL_1:1;
         then k = q by A43,A63,A12,A13,TOPREAL3:11;
         then q in {l,k} by TARSKI:def 2;
         hence contradiction by A59,XBOOLE_0:def 4;
       end;
     end;
     then reconsider X = LSeg(l,k) \ {l,k} as Subset of T2|C` by JORDAN1:1;
     now
       assume x in {l,k};
       then x = l or x = k by TARSKI:def 2;
       hence contradiction by A55,Th1;
     end;
     then
A72: x in LSeg(l,k) \ {l,k} by A47,XBOOLE_0:def 4;
     then skl(x,C`) = skl Down(x,C`) by A58,CONNSP_3:27;
     then
A73: x in skl Down(x,C`) by A72,A58,CONNSP_3:26;
A74: X meets Ux by A10,A72,A73,XBOOLE_0:3;
     LSeg(l,k) \ {l,k} is connected by JORDAN1H:5;
     then X is connected by CONNSP_1:24;
     then
A75: X c= skl Down(x,C`) by A10,A48,A74,CONNSP_1:38;
A76: LSeg(l,k) c= R by A11,A19,A41,JORDAN1:def 1;
A77: the carrier of TR = R by TOPREALA:53;
     reconsider AR = a, BR = b, CR = c, DR = d
     as Point of TR by A14,A11,Lm60,Lm65,Lm61,TOPREALA:53;

     consider Pcm being Path of c,m, fcm being Function of I[01], T2|LSeg(c,m)
     such that
A78: rng fcm = LSeg(c,m) & Pcm = fcm by Th52;
A79: LSeg(c,m) c= R by A15,A11,Lm60,Lm65,JORDAN1:def 1;
A80: ml c= R by A27,A11,XBOOLE_1:1;

:: component containg x is bounded
     thus Ux is_inside_component_of C
     proof
     thus
A81: Ux is_a_component_of C` by A48,A10,CONNSP_1:def 6;
     assume not Ux is Bounded;
     then not Ux c= Ball(x,10) by JORDAN2C:16;
     then consider u being set such that
A82: u in Ux and
A83: not u in Ball(x,10) by TARSKI:def 3;
A84:  R c= Ball(x,10) by A47,A76,Lm83;
      reconsider u as Point of T2 by A82;
      reconsider xs = x, us = u as Point of T2|C`|skl Down(x,C`)
      by A10,A82,A73,JORDAN1:1;
A85:  Ux is open by A81,SPRECT_3:18;
      skl Down(x,C`) is connected by A48,CONNSP_1:def 5;
      then Ux is connected by A10,CONNSP_1:24;
      then
A86:  Ux is_Region by A85,TOPREAL4:def 3;
      x in Ball(x,10) by Th24;
      then consider P1 being Subset of T2 such that
A87:  P1 is_S-P_arc_joining x,u and
A88:  P1 c= Ux by A10,A82,A73,A86,A83,TOPREAL4:30;
A89:  P1 is_an_arc_of x,u by A87,TOPREAL4:3;
      reconsider P2 = P1 as Subset of T2|C` by A88,A10,XBOOLE_1:1;
A90:  P1 is connected by A89,JORDAN6:11;
A91:  P2 c= skl Down(x,C`) by A10,A88;
A92:  P2 misses C by A49,SUBSET_1:43;
      then
A93:  P2 misses Jc by A18,XBOOLE_1:63;
A94:  P2 misses Jd by A17,A92,XBOOLE_1:63;
A95:  x`1 = 1/2*((k+l)`1) by TOPREAL3:9
      .= 1/2*(k`1+l`1) by TOPREAL3:7
      .= 0 by A43,A21;
      then
A96:  LSeg(d,x) is vertical by Lm24,SPPOL_1:37;
A97:  x = |[x`1,x`2]| by EUCLID:57;
A98:  x`2 < l`2 by A56,Th3;
A99:  k`2 < x`2 by A56,Th2;
      d`2 <= k`2 by A17,A40,A1,Lm25,Th94;
      then
A100:  d`2 <= x`2 by A99,XREAL_1:2;
      d`1 = d`1;
      then
A101:  LSeg(d,x) c= LSeg(d,l) by A96,A29,A98,A100,JORDAN15:5;
A102:  LSeg(d,x) misses Jc
      proof
        assume not thesis;
        then consider q being set such that
A103:    q in LSeg(d,x) and
A104:    q in Jc by XBOOLE_0:3;
        reconsider q as Point of T2 by A103;
        q`2 <= x`2 by A53,A103,A97,A100,A95,Lm24,JGRAPH_6:9;
        then
A105:    q`2 < l`2 by A98,XREAL_1:2;
        q`1 = 0 by A101,A103,A29,A42,Lm24,SPPOL_1:def 3;
        then q in Vertical_Line w by A12,A13,JORDAN6:34;
        then q in Jc /\ Vertical_Line w by A104,XBOOLE_0:def 3;
        hence contradiction by A105,A8,A9,JORDAN21:42;
      end;
      set n = First_Point(P1,x,u,dR);
A106:  not u in R by A83,A84;
A107:  Fr R = dR by Th61;
      u in P1 by A89,TOPREAL1:4;
      then
A108:  P1 \ R <> {}T2 by A106,XBOOLE_0:def 4;
      x in P1 by A89,TOPREAL1:4;
      then P1 meets R by A47,A76,XBOOLE_0:3;
      then
A109:  P1 meets dR by A90,A107,A108,CONNSP_1:23;
      P1 is closed by A89,JORDAN6:12;
      then
A110:  P1 /\ dR is closed by TOPS_1:35;
A111:  n in P1 /\ dR by A109,A110,A89,JORDAN5C:def 1;
      then
A112:  n in dR by XBOOLE_0:def 3;
A113:  n in P1 by A111,XBOOLE_0:def 3;
      set alpha = Segment(P1,x,u,x,n);
A114:  rd < k`2 by A1,A17,A40,Th94;
      l`2 <= m`2 by A32,JORDAN21:41;
      then x`2 < m`2 by A98,XREAL_1:2;
      then not x in dR by A114,A97,A95,Lm80,A20,A99;
      then
A115:  alpha is_an_arc_of x,n by A113,A89,A112,JORDAN20:20;
A116:  alpha c= P1 by JORDAN16:5;
      then
A117:  alpha misses Jc by A93,XBOOLE_1:63;
A118:  alpha misses Jd by A116,A94,XBOOLE_1:63;
      consider Pdx being Path of d,x,
      fdx being Function of I[01], T2|LSeg(d,x) such that
A119:  rng fdx = LSeg(d,x) & Pdx = fdx by Th52;
      consider PJc being Path of a,b, fJc being Function
 of I[01], T2|Jc such that
A120:  rng fJc = Jc & PJc = fJc by A2,Th51;
      consider PJd being Path of a,b, fJd being Function
 of I[01], T2|Jd such that
A121:  rng fJd = Jd & PJd = fJd by A3,Th51;
      consider Palpha being Path of x,n,
      falpha being Function of I[01], T2|alpha such that
A122:  rng falpha = alpha & Palpha = falpha by A115,Th51;
      n in R by A112,Lm65;
      then
A123:  ex p st p = n & rl <= p`1 & p`1 <= rp & rd <= p`2 & p`2 <= rg;
      rng PJc c= the carrier of TR by A77,A11,A18,A120,XBOOLE_1:1;
      then reconsider h = PJc as Path of AR,BR by Th39;
      rng PJd c= the carrier of TR by A77,A11,A17,A121,XBOOLE_1:1;
      then reconsider H = PJd as Path of AR,BR by Th39;
A124:  LSeg(d,x) c= R by Lm61,Lm65,A47,A76,JORDAN1:def 1;
A125:  alpha c= R by A89,A47,A76,A106,Th66;
A126:  ld in LSeg(ld,lg) by TOPREAL1:6;
A127:  pd in LSeg(pd,pg) by TOPREAL1:6;
      LSeg(lg,c) misses C by A1,Lm72;
      then
A128:  LSeg(lg,c) misses Jc by A18,XBOOLE_1:63;
A129:  LSeg(lg,c) c= R by Lm65,Lm66,XBOOLE_1:1;
A130:  LSeg(pg,c) c= R by Lm65,Lm67,XBOOLE_1:1;
      LSeg(pg,c) misses C by A1,Lm73;
      then
A131:  LSeg(pg,c) misses Jc by A18,XBOOLE_1:63;
      consider Plx being Path of l,x, flx being Function of I[01], T2|LSeg(l,x)
      such that
A132:  rng flx = LSeg(l,x) & Plx = flx by Th52;

      set PCX = Pcm + Pml + Plx;

A133:  rng PCX = rng Pcm \/ rng Pml \/ rng Plx by Th49;

A134:  ml misses Jd
      proof
        assume ml meets Jd;
        then consider q being set such that
A135:    q in ml and
A136:    q in Jd by XBOOLE_0:3;
        q in {a,b} by A25,A135,A136,A5,XBOOLE_0:def 3;
        hence contradiction by A26,A135,XBOOLE_0:3;
      end;
A137:  LSeg(c,m) /\ C = {m} by A1,Th101;
A138:  LSeg(c,m) misses Jd
      proof
        assume LSeg(c,m) meets Jd;
        then consider q being set such that
A139:    q in LSeg(c,m) and
A140:    q in Jd by XBOOLE_0:3;
        q in {m} by A140,A17,A137,A139,XBOOLE_0:def 3;
        then q = m by TARSKI:def 1;
        then m in {a,b} by A140,A6,A5,XBOOLE_0:def 3;
        hence contradiction by A24,TARSKI:def 2;
      end;
      LSeg(l,x) is vertical by A95,A21,SPPOL_1:37;
      then
A141:  LSeg(l,x) c= LSeg(l,k) by A50,A43,A95,A99,A98,JORDAN15:5;
      l in LSeg(l,x) by TOPREAL1:6;
      then {l} c= LSeg(l,x) by ZFMISC_1:37;
      then
A142:  LSeg(l,x) = LSeg(l,x) \ {l} \/ {l} by XBOOLE_1:45;
      LSeg(l,x) \ {l} c= LSeg(l,k) \ {l,k}
      proof
        let q be set;
        assume q in LSeg(l,x) \ {l};
        then
A143:    q in LSeg(l,x) & q <> l by ZFMISC_1:64;
        then q <> k by A97,A52,A98,A99,A21,A95,JGRAPH_6:9;
        then not q in {l,k} by A143,TARSKI:def 2;
        hence thesis by A143,A141,XBOOLE_0:def 4;
      end;
      then LSeg(l,x) \ {l} c= C` by A58,XBOOLE_1:1;
      then LSeg(l,x) \ {l} misses C by SUBSET_1:43;
      then
A144:  LSeg(l,x) \ {l} misses Jd by A17,XBOOLE_1:63;
      {l} misses Jd
      proof
        assume {l} meets Jd;
        then l in Jd by ZFMISC_1:56;
        then l in {a,b} by A16,A5,XBOOLE_0:def 3;
        hence thesis by A24,TARSKI:def 2;
      end;
      then LSeg(l,x) misses Jd by A142,A144,XBOOLE_1:70;
      then
A145:  rng PCX misses Jd by A133,A78,A134,A138,A132,XBOOLE_1:114;

      LSeg(l,x) c= R by A76,A141,XBOOLE_1:1;
      then
A146:  rng PCX c= R by A133,A78,A79,A80,A132,Lm1;
A147:  LSeg(ld,d) misses C by A1,Lm74;
      then
A148:  LSeg(ld,d) misses Jd by A17,XBOOLE_1:63;
A149:  LSeg(pd,d) misses C by A1,Lm75;
      then
A150:  LSeg(pd,d) misses Jd by A17,XBOOLE_1:63;


      per cases;
      suppose
A151:    n`2 < 0;
        per cases by A112,A151,Lm71;
        suppose
A152:      n in LSeg(a,ld);
          consider Pnld being Path of n,ld,
          fnld being Function of I[01], T2|LSeg(n,ld) such that
A153:      rng fnld = LSeg(n,ld) & Pnld = fnld by Th52;
          consider Pldd being Path of ld,d,
          fldd being Function of I[01], T2|LSeg(ld,d) such that
A154:      rng fldd = LSeg(ld,d) & Pldd = fldd by Th52;
A155:      ld`1 = n`1 by A126,A152,Lm56,Lm43,SPPOL_1:def 3;
          then LSeg(n,ld) is vertical by SPPOL_1:37;
          then LSeg(n,ld) c= LSeg(ld,lg) by A155,Lm29,A123,Lm27,Lm43,JORDAN15:5
;
          then
A156:      LSeg(n,ld) c= dR by Lm39,XBOOLE_1:1;
          set K1 = PCX + Palpha + Pnld + Pldd;
          LSeg(n,ld) misses C by A1,A91,A113,A49,A152,Lm78;
          then
A157:      LSeg(n,ld) misses Jd by A17,XBOOLE_1:63;
A158:      rng K1 = rng PCX \/ rng Palpha \/ rng Pnld \/ rng Pldd by Lm12;
          then
A159:      rng PJd misses rng K1 by A121,A118,A122,A153,A157,A154,A145,A148,Lm3
;
A160:      LSeg(ld,d) c= R by Lm65,Lm68,XBOOLE_1:1;
          LSeg(n,ld) c= R by A156,Lm65,XBOOLE_1:1;
          then rng K1 c= the carrier of TR
          by A158,A77,A125,A122,A153,A154,A146,A160,Lm2;
          then reconsider v = K1 as Path of CR,DR by Th39;
          consider s, t being Point of I[01] such that
A161:      H.s = v.t by Lm18,Lm19,Lm23,Lm25,JGRAPH_8:6;
          dom H = the carrier of I[01] & dom v = the carrier of I[01]
          by FUNCT_2:def 1;
          then H.s in rng PJd & v.t in rng K1 by FUNCT_1:def 5;
          hence contradiction by A161,A159,XBOOLE_0:3;
        end;

        suppose
A162:      n in LSeg(ld,d);
          consider Pnd being Path of n,d,
          fnd being Function of I[01], T2|LSeg(n,d) such that
A163:      rng fnd = LSeg(n,d) & Pnd = fnd by Th52;
          set K1 = PCX + Palpha + Pnd;
          ld in LSeg(ld,d) by TOPREAL1:6;
          then
A164:      ld`2 = n`2 by Lm49,A162,SPPOL_1:def 2;
          then
A165:      LSeg(n,d) is horizontal by Lm25,Lm29,SPPOL_1:36;
          ld`1 <= n`1 & n`1 <= d`1 by A162,Lm24,Lm28,JGRAPH_6:11;
          then
A166:      LSeg(n,d) c= LSeg(ld,d) by Lm49,A165,A164,JORDAN15:6;
          then
A167:      LSeg(n,d) c= dR by Lm68,XBOOLE_1:1;
          LSeg(n,d) misses C by A166,A147,XBOOLE_1:63;
          then
A168:      LSeg(n,d) misses Jd by A17,XBOOLE_1:63;
A169:      rng K1 = rng PCX \/ rng Palpha \/ rng Pnd by Th49;
          then
A170:      rng K1 misses Jd by A118,A145,A122,A163,A168,XBOOLE_1:114;
A171:      LSeg(n,d) c= R by A167,Lm65,XBOOLE_1:1;
          rng K1 c= the carrier of TR
          by A146,A169,A77,A125,A122,A163,A171,Lm1;
          then reconsider v = K1 as Path of CR,DR by Th39;
          consider s, t being Point of I[01] such that
A172:      H.s = v.t by Lm18,Lm19,Lm23,Lm25,JGRAPH_8:6;
          dom H = the carrier of I[01] & dom v = the carrier of I[01]
          by FUNCT_2:def 1;
          then H.s in rng PJd & v.t in rng K1 by FUNCT_1:def 5;
          hence contradiction by A172,A121,A170,XBOOLE_0:3;
        end;

        suppose
A173:      n in LSeg(d,pd);
          consider Pnd being Path of n,d,
          fnd being Function of I[01], T2|LSeg(n,d) such that
A174:      rng fnd = LSeg(n,d) & Pnd = fnd by Th52;
          set K1 = PCX + Palpha + Pnd;
          pd in LSeg(pd,d) by TOPREAL1:6;
          then pd`2 = n`2 by Lm50,A173,SPPOL_1:def 2;
          then
A175:      LSeg(n,d) is horizontal by Lm25,Lm33,SPPOL_1:36;
A176:      d`2 = d`2;
          d`1 <= n`1 & n`1 <= pd`1 by A173,Lm24,Lm32,JGRAPH_6:11;
          then
A177:      LSeg(n,d) c= LSeg(pd,d) by Lm50,A175,A176,JORDAN15:6;
          then
A178:      LSeg(n,d) c= dR by Lm69,XBOOLE_1:1;
          LSeg(n,d) misses C by A177,A149,XBOOLE_1:63;
          then
A179:      LSeg(n,d) misses Jd by A17,XBOOLE_1:63;
A180:      rng K1 = rng PCX \/ rng Palpha \/ rng Pnd by Th49;
          then
A181:      rng K1 misses Jd by A118,A145,A122,A174,A179,XBOOLE_1:114;
A182:      LSeg(n,d) c= R by A178,Lm65,XBOOLE_1:1;
          rng K1 c= the carrier of TR
          by A146,A180,A77,A125,A122,A174,A182,Lm1;
          then reconsider v = K1 as Path of CR,DR by Th39;
          consider s, t being Point of I[01] such that
A183:      H.s = v.t by Lm18,Lm19,Lm23,Lm25,JGRAPH_8:6;
          dom H = the carrier of I[01] & dom v = the carrier of I[01]
          by FUNCT_2:def 1;
          then H.s in rng PJd & v.t in rng K1 by FUNCT_1:def 5;
          hence contradiction by A183,A121,A181,XBOOLE_0:3;
        end;

        suppose
A184:      n in LSeg(pd,b);
          consider Pnpd being Path of n,pd,
          fnpd being Function of I[01], T2|LSeg(n,pd) such that
A185:      rng fnpd = LSeg(n,pd) & Pnpd = fnpd by Th52;
          consider Ppdd being Path of pd,d,
          fpdd being Function of I[01], T2|LSeg(pd,d) such that
A186:      rng fpdd = LSeg(pd,d) & Ppdd = fpdd by Th52;
A187:      pd`1 = n`1 by A127,Lm44,A184,Lm58,SPPOL_1:def 3;
          then LSeg(n,pd) is vertical by SPPOL_1:37;
          then LSeg(n,pd) c= LSeg(pd,pg) by A187,Lm33,A123,Lm31,Lm44,JORDAN15:5
;
          then
A188:      LSeg(n,pd) c= dR by Lm41,XBOOLE_1:1;
          set K1 = PCX + Palpha + Pnpd + Ppdd;
          LSeg(n,pd) misses C by A1,A91,A113,A49,A184,Lm79;
          then
A189:      LSeg(n,pd) misses Jd by A17,XBOOLE_1:63;
A190:      rng K1 = rng PCX \/ rng Palpha \/ rng Pnpd \/ rng Ppdd by Lm12;
          then
A191:      rng PJd misses rng K1 by A121,A118,A122,A185,A189,A186,A145,A150,Lm3
;
A192:      LSeg(pd,d) c= R by Lm65,Lm69,XBOOLE_1:1;
          LSeg(n,pd) c= R by A188,Lm65,XBOOLE_1:1;
          then rng K1 c= the carrier of TR
          by A190,A77,A125,A122,A185,A186,A146,A192,Lm2;
          then reconsider v = K1 as Path of CR,DR by Th39;
          consider s, t being Point of I[01] such that
A193:      H.s = v.t by Lm18,Lm19,Lm23,Lm25,JGRAPH_8:6;
          dom H = the carrier of I[01] & dom v = the carrier of I[01]
          by FUNCT_2:def 1;
          then H.s in rng PJd & v.t in rng K1 by FUNCT_1:def 5;
          hence contradiction by A193,A191,XBOOLE_0:3;
        end;
      end;

      suppose
A194:    n`2 >= 0;
        per cases by A112,A194,Lm70;
        suppose
A195:      n in LSeg(a,lg);
          consider Pnlg being Path of n,lg,
          fnlg being Function of I[01], T2|LSeg(n,lg) such that
A196:      rng fnlg = LSeg(n,lg) & Pnlg = fnlg by Th52;
          consider Plgc being Path of lg,c,
          flgc being Function of I[01], T2|LSeg(lg,c) such that
A197:      rng flgc = LSeg(lg,c) & Plgc = flgc by Th52;
A198:      ld`1 = n`1 by A126,Lm43,A195,Lm55,SPPOL_1:def 3;
          then LSeg(n,lg) is vertical by Lm26,Lm28,SPPOL_1:37;
          then LSeg(n,lg) c= LSeg(ld,lg) by A198,Lm29,A123,Lm27,Lm43,JORDAN15:5
;
          then
A199:      LSeg(n,lg) c= dR by Lm39,XBOOLE_1:1;
          set K1 = Pdx + Palpha + Pnlg + Plgc;
          LSeg(n,lg) misses C by A1,A91,A113,A49,A195,Lm76;
          then
A200:      LSeg(n,lg) misses Jc by A18,XBOOLE_1:63;
A201:      rng K1 = rng Pdx \/ rng Palpha \/ rng Pnlg \/ rng Plgc by Lm12;
          then
A202:      rng K1 misses Jc by A102,A117,A119,A122,A196,A200,A128,A197,Lm3;
A203:      rng K1 = rng -K1 by Th41;
          LSeg(n,lg) c= R by A199,Lm65,XBOOLE_1:1;
          then rng K1 c= the carrier of TR
          by A129,A201,A119,A77,A124,A125,A122,A196,A197,Lm2;
          then reconsider v = -K1 as Path of CR,DR by A203,Th39;
          consider s, t being Point of I[01] such that
A204:      h.s = v.t by Lm18,Lm19,Lm23,Lm25,JGRAPH_8:6;
          dom h = the carrier of I[01] & dom v = the carrier of I[01]
          by FUNCT_2:def 1;
          then h.s in rng PJc & v.t in rng -K1 by FUNCT_1:def 5;
          hence contradiction by A204,A120,A202,A203,XBOOLE_0:3;
        end;

        suppose
A205:      n in LSeg(lg,c);
          consider Pnc being Path of n,c,
          fnc being Function of I[01], T2|LSeg(n,c) such that
A206:      rng fnc = LSeg(n,c) & Pnc = fnc by Th52;
          set K1 = Pdx + Palpha + Pnc;
          lg in LSeg(lg,c) by TOPREAL1:6;
          then
A207:      lg`2 = n`2 by Lm51,A205,SPPOL_1:def 2;
          then
A208:      LSeg(n,c) is horizontal by Lm23,Lm27,SPPOL_1:36;
          lg`1 <= n`1 & n`1 <= c`1 by A205,Lm22,Lm26,JGRAPH_6:11;
          then
A209:      LSeg(n,c) c= LSeg(lg,c) by Lm51,A208,A207,JORDAN15:6;
          then
A210:      LSeg(n,c) c= dR by Lm66,XBOOLE_1:1;
          LSeg(lg,c) misses C by A1,Lm72;
          then LSeg(n,c) misses C by A209,XBOOLE_1:63;
          then
A211:      LSeg(n,c) misses Jc by A18,XBOOLE_1:63;
A212:      rng K1 = rng Pdx \/ rng Palpha \/ rng Pnc by Th49;
          then
A213:      rng K1 misses Jc by A102,A117,A119,A122,A206,A211,XBOOLE_1:114;
A214:      rng K1 = rng -K1 by Th41;
A215:      LSeg(n,c) c= R by A210,Lm65,XBOOLE_1:1;
          rng K1 c= the carrier of TR
          by A212,A119,A77,A124,A125,A122,A206,A215,Lm1;
          then reconsider v = -K1 as Path of CR,DR by A214,Th39;
          consider s, t being Point of I[01] such that
A216:      h.s = v.t by Lm18,Lm19,Lm23,Lm25,JGRAPH_8:6;
          dom h = the carrier of I[01] & dom v = the carrier of I[01]
          by FUNCT_2:def 1;
          then h.s in rng PJc & v.t in rng -K1 by FUNCT_1:def 5;
          hence contradiction by A216,A120,A213,A214,XBOOLE_0:3;
        end;

        suppose
A217:      n in LSeg(c,pg);
          consider Pnc being Path of n,c,
          fnc being Function of I[01], T2|LSeg(n,c) such that
A218:      rng fnc = LSeg(n,c) & Pnc = fnc by Th52;
          set K1 = Pdx + Palpha + Pnc;
          pg in LSeg(pg,c) by TOPREAL1:6;
          then pg`2 = n`2 by Lm52,A217,SPPOL_1:def 2;
          then
A219:      LSeg(n,c) is horizontal by Lm23,Lm31,SPPOL_1:36;
A220:      c`2 = c`2;
          c`1 <= n`1 & n`1 <= pg`1 by A217,Lm22,Lm30,JGRAPH_6:11;
          then
A221:      LSeg(c,n) c= LSeg(c,pg) by A220,Lm52,A219,JORDAN15:6;
          then
A222:      LSeg(n,c) c= dR by Lm67,XBOOLE_1:1;
          LSeg(pg,c) misses C by A1,Lm73;
          then LSeg(n,c) misses C by A221,XBOOLE_1:63;
          then
A223:      LSeg(n,c) misses Jc by A18,XBOOLE_1:63;
A224:      rng K1 = rng Pdx \/ rng Palpha \/ rng Pnc by Th49;
          then
A225:      rng K1 misses Jc by A102,A117,A119,A122,A218,A223,XBOOLE_1:114;
A226:      rng K1 = rng -K1 by Th41;
A227:      LSeg(n,c) c= R by A222,Lm65,XBOOLE_1:1;
          rng K1 c= the carrier of TR
          by A224,A119,A77,A124,A125,A122,A218,A227,Lm1;
          then reconsider v = -K1 as Path of CR,DR by A226,Th39;
          consider s, t being Point of I[01] such that
A228:      h.s = v.t by Lm18,Lm19,Lm23,Lm25,JGRAPH_8:6;
          dom h = the carrier of I[01] & dom v = the carrier of I[01]
          by FUNCT_2:def 1;
          then h.s in rng PJc & v.t in rng -K1 by FUNCT_1:def 5;
          hence contradiction by A228,A120,A225,A226,XBOOLE_0:3;
        end;

        suppose
A229:      n in LSeg(pg,b);
          consider Pnpg being Path of n,pg,
          fnpg being Function of I[01], T2|LSeg(n,pg) such that
A230:      rng fnpg = LSeg(n,pg) & Pnpg = fnpg by Th52;
          consider Ppgc being Path of pg,c,
          fpgc being Function of I[01], T2|LSeg(pg,c) such that
A231:      rng fpgc = LSeg(pg,c) & Ppgc = fpgc by Th52;
A232:      pd`1 = n`1 by A127,Lm44,A229,Lm57,SPPOL_1:def 3;
          then LSeg(n,pg) is vertical by Lm30,Lm32,SPPOL_1:37;
          then LSeg(n,pg) c= LSeg(pd,pg) by A232,Lm33,A123,Lm31,Lm44,JORDAN15:5
;
          then
A233:      LSeg(n,pg) c= dR by Lm41,XBOOLE_1:1;
          set K1 = Pdx + Palpha + Pnpg + Ppgc;
          LSeg(n,pg) misses C by A1,A91,A113,A49,A229,Lm77;
          then
A234:      LSeg(n,pg) misses Jc by A18,XBOOLE_1:63;
A235:      rng K1 = rng Pdx \/ rng Palpha \/ rng Pnpg \/ rng Ppgc by Lm12;
          then
A236:      rng K1 misses Jc by A102,A117,A119,A122,A230,A234,A231,A131,Lm3;
A237:      rng K1 = rng -K1 by Th41;
          LSeg(n,pg) c= R by A233,Lm65,XBOOLE_1:1;
          then rng K1 c= the carrier of TR
          by A130,A235,A119,A77,A124,A125,A122,A230,A231,Lm2;
          then reconsider v = -K1 as Path of CR,DR by A237,Th39;
          consider s, t being Point of I[01] such that
A238:      h.s = v.t by Lm18,Lm19,Lm23,Lm25,JGRAPH_8:6;
          dom h = the carrier of I[01] & dom v = the carrier of I[01]
          by FUNCT_2:def 1;
          then h.s in rng PJc & v.t in rng -K1 by FUNCT_1:def 5;
          hence contradiction by A238,A120,A236,A237,XBOOLE_0:3;
        end;
      end;
    end;

:: uniqueness
    let V be Subset of T2;
    assume
A239: V is_inside_component_of C;
     assume
A240: V <> Ux;
     consider VP being Subset of T2|C` such that
A241: VP = V and
A242: VP is_a_component_of T2|C` and
     VP is bounded Subset of Euclid 2 by A239,JORDAN2C:17;
     reconsider T2C = T2|C` as non empty SubSpace of T2;
     VP <> {}(T2|C`) by A242,CONNSP_1:34;
     then reconsider VP as non empty Subset of T2C;
A243: V misses C by A241,A49,SUBSET_1:43;
     consider Pjd being Path of j,d,
     fjd being Function of I[01], T2|LSeg(j,d) such that
A244: rng fjd = LSeg(j,d) & Pjd = fjd by Th52;
     consider Plk being Path of l,k,
     flk being Function of I[01], T2|LSeg(l,k) such that
A245: rng flk = LSeg(l,k) & Plk = flk by Th52;
     set beta = Pcm + Pml + Plk + Pkj + Pjd;
A246: rng beta = rng Pcm \/ rng Pml \/ rng Plk \/ rng Pkj \/ rng Pjd by Lm14;
     dom beta = the carrier of I[01] by FUNCT_2:def 1
     .= [#]I[01] by PRE_TOPC:12;
     then beta.:dom beta is compact by WEIERSTR:14;
     then rng beta is compact by RELAT_1:146;
     then
A247: rng beta is closed by COMPTS_1:16;
A248: ml misses V by A243,A27,XBOOLE_1:63;
     {l,k} c= LSeg(l,k)
     proof
       let x be set;
       assume x in {l,k};
       then x = l or x = k by TARSKI:def 2;
       hence thesis by TOPREAL1:6;
     end;
     then
A249: LSeg(l,k) = LSeg(l,k) \ {l,k} \/ {l,k} by XBOOLE_1:45;
A250: LSeg(l,k) \ {l,k} misses V
     proof
       assume not thesis;
       then consider q being set such that
A251:   q in LSeg(l,k) \ {l,k} and
A252:   q in V by XBOOLE_0:3;
       V meets Ux by A10,A75,A251,A252,XBOOLE_0:3;
       hence contradiction by A10,A241,A242,A48,A240,CONNSP_1:37;
     end;
     not l in V & not k in V by A18,A16,A17,A40,A243,XBOOLE_0:3;
     then {l,k} misses V by ZFMISC_1:57;
     then
A253: LSeg(l,k) misses V by A249,A250,XBOOLE_1:70;
A254: kj misses V by A243,A46,XBOOLE_1:63;
A255: LSeg(j,d) misses V by A1,A239,Th100;
     LSeg(c,m) misses V by A1,A239,Th99;
     then LSeg(c,m) \/ ml \/ LSeg(l,k) misses V by A248,A253,XBOOLE_1:114;
     then
A256: rng beta misses V by A78,A244,A245,A254,A255,A246,XBOOLE_1:114;
A257: m = |[m`1,m`2]| by EUCLID:57;
A258: c = |[c`1,c`2]| by EUCLID:57;
A259: j = |[j`1,j`2]| by EUCLID:57;
A260: not a in LSeg(c,m) by A12,A13,A23,A22,A257,A258,A20,Lm18,JGRAPH_6:9;
     not a in ml by A26,ZFMISC_1:55;
     then
A261: not a in LSeg(c,m) \/ ml by A260,XBOOLE_0:def 2;
     not a in LSeg(l,k) by A43,A21,A54,A52,Lm18,JGRAPH_6:9;
     then
A262: not a in LSeg(c,m) \/ ml \/ LSeg(l,k) by A261,XBOOLE_0:def 2;
     not a in kj by A45,ZFMISC_1:55;
     then
A263: not a in LSeg(c,m) \/ ml \/ LSeg(l,k) \/ kj by A262,XBOOLE_0:def 2;
     not a in LSeg(j,d) by A31,A30,A53,A259,Lm18,Lm24,JGRAPH_6:9;
     then not a in rng beta by A263,A246,A78,A244,A245,XBOOLE_0:def 2;
     then consider ra being positive (real number)
     such that
A264: Ball(a,ra) misses rng beta by A247,Th33;
A265: not b in LSeg(c,m) by A12,A13,A23,A22,A257,A258,A20,Lm19,JGRAPH_6:9;
     not b in ml by A26,ZFMISC_1:55;
     then
A266: not b in LSeg(c,m) \/ ml by A265,XBOOLE_0:def 2;
     not b in LSeg(l,k) by A43,A21,A54,A52,Lm19,JGRAPH_6:9;
     then
A267: not b in LSeg(c,m) \/ ml \/ LSeg(l,k) by A266,XBOOLE_0:def 2;
     not b in kj by A45,ZFMISC_1:55;
     then
A268: not b in LSeg(c,m) \/ ml \/ LSeg(l,k) \/ kj by A267,XBOOLE_0:def 2;
     not b in LSeg(j,d) by A31,A30,A53,A259,Lm19,Lm24,JGRAPH_6:9;
     then not b in rng beta by A268,A246,A78,A244,A245,XBOOLE_0:def 2;
     then consider rb being positive (real number)
     such that
A269: Ball(b,rb) misses rng beta by A247,Th33;
     set A = Ball(a,ra), B = Ball(b,rb);
A270: a in A by Th24;
A271: b in B by Th24;
A272: C = Fr V by A241,A242,Th79;
     a in Cl V by A14,A272,XBOOLE_0:def 3;
     then A meets V by A270,PRE_TOPC:def 13;
     then consider u being set such that
A273: u in A and
A274: u in V by XBOOLE_0:3;
     b in Cl V by A14,A272,XBOOLE_0:def 3;
     then B meets V by A271,PRE_TOPC:def 13;
     then consider v being set such that
A275: v in B and
A276: v in V by XBOOLE_0:3;
     reconsider u, v as Point of T2 by A273,A275;
A277: the carrier of T2C|VP = VP by JORDAN1:1;
     reconsider u1 = u, v1 = v as Point of T2C|VP by A274,A276,A241,JORDAN1:1;
     T2C|VP is arcwise_connected by A242,Th78;
     then
A278: u1,v1 are_connected by BORSUK_2:def 3;
     then consider fuv being Function of I[01], T2C|VP such that
A279: fuv is continuous & fuv.0 = u1 & fuv.1 = v1 by BORSUK_2:def 1;
A280: T2C|VP = T2|V by A241,GOBOARD9:4;
     fuv is Path of u1,v1 by A279,A278,BORSUK_2:def 2;
     then reconsider uv = fuv as Path of u,v by A278,A280,TOPALG_2:1;
A281: rng fuv c= the carrier of T2C|VP;
A282: rng uv misses rng beta by A281,A241,A277,A256,XBOOLE_1:63;
     consider au being Path of a,u,
     fau being Function of I[01], T2|LSeg(a,u) such that
A283: rng fau = LSeg(a,u) & au = fau by Th52;
     consider vb being Path of v,b,
     fvb being Function of I[01], T2|LSeg(v,b) such that
A284: rng fvb = LSeg(v,b) & vb = fvb by Th52;
     set AB = au + uv + vb;
A285: rng AB = rng au \/ rng uv \/ rng vb by Th49;
     a in A by Th24;
     then LSeg(a,u) c= A by A273,JORDAN1:def 1;
     then
A286: LSeg(a,u) misses rng beta by A264,XBOOLE_1:63;
     b in B by Th24;
     then LSeg(v,b) c= B by A275,JORDAN1:def 1;
     then LSeg(v,b) misses rng beta by A269,XBOOLE_1:63;
     then
A287: rng AB misses rng beta by A285,A283,A284,A286,A282,XBOOLE_1:114;
A288: a,b are_connected by BORSUK_2:def 3;
A289: V c= R by A1,A239,Th103;
A290: LSeg(a,u) c= R by A274,A14,A11,A289,JORDAN1:def 1;
A291: LSeg(v,b) c= R by A276,A289,A14,A11,JORDAN1:def 1;
     rng uv c= R by A281,A241,A277,A289,XBOOLE_1:1;
     then LSeg(a,u) \/ rng uv c= R by A290,XBOOLE_1:8;
     then rng AB c= the carrier of TR by A77,A291,A283,A284,A285,XBOOLE_1:8;
     then reconsider h = AB as Path of AR,BR by A288,Th38;
A292: c,d are_connected by BORSUK_2:def 3;
A293: LSeg(c,m) \/ ml c= R by A79,A80,XBOOLE_1:8;
A294: LSeg(c,m) \/ ml \/ LSeg(l,k) c= R by A293,A76,XBOOLE_1:8;
     kj c= R by A46,A11,XBOOLE_1:1;
     then
A295: LSeg(c,m) \/ ml \/ LSeg(l,k) \/ kj c= R by A294,XBOOLE_1:8;
     LSeg(j,d) c= R by A11,A28,Lm61,Lm65,JORDAN1:def 1;
     then rng beta c= the carrier of TR by A77,A295,A246,A78,A244,A245,XBOOLE_1
:8;
     then reconsider v = beta as Path of CR,DR by A292,Th38;
     consider s, t being Point of I[01] such that
A296: h.s = v.t by Lm18,Lm19,Lm23,Lm25,JGRAPH_8:6;
     dom h = the carrier of I[01] & dom v = the carrier of I[01]
     by FUNCT_2:def 1;
     then h.s in rng AB & v.t in rng beta by FUNCT_1:def 5;
     hence contradiction by A296,A287,XBOOLE_0:3;
   end;

theorem Th106:
  |[-1,0]|,|[1,0]| realize-max-dist-in C implies
  for Jc, Jd being compact with_the_max_arc Subset of T2 st
  Jc is_an_arc_of |[-1,0]|,|[1,0]| & Jd is_an_arc_of |[-1,0]|,|[1,0]| &
  C = Jc \/ Jd & Jc /\ Jd = {|[-1,0]|,|[1,0]|} & UMP C in Jc & LMP C in Jd &
  W-bound C = W-bound Jc & E-bound C = E-bound Jc holds
  BDD C = skl Down ((1/2) * ((UMP (LSeg(LMP Jc,|[0,-3]|) /\ Jd)) + LMP Jc), C`)
  proof
    assume
A1: a,b realize-max-dist-in C;
    let Jc, Jd being compact with_the_max_arc Subset of T2 such that
A2: Jc is_an_arc_of a,b and
A3: Jd is_an_arc_of a,b and
A4: C = Jc \/ Jd and
A5: Jc /\ Jd = {a,b} and
A6: UMP C in Jc and
A7: LMP C in Jd and
A8: W-bound C = W-bound Jc and
A9: E-bound C = E-bound Jc;
     reconsider
     Ux = skl Down((1/2) * ((UMP (LSeg(LMP Jc,d) /\ Jd)) + LMP Jc),C`)
     as Subset of T2 by PRE_TOPC:39;
     Ux = BDD C
     proof
       Ux is_inside_component_of C
       by Th105,A1,A2,A3,A4,A5,A6,A7,A8,A9;
       hence Ux c= BDD C by JORDAN2C:26;
       set F = {B where B is Subset of T2: B is_inside_component_of C};
       let q be set;
       assume q in BDD C;
       then consider Z being set such that
A10:   q in Z and
A11:   Z in F by TARSKI:def 4;
       ex B being Subset of T2 st Z = B & B is_inside_component_of C by A11;
       hence thesis by A10,Th105,A1,A2,A3,A4,A5,A6,A7,A8,A9;
     end;
     hence thesis;
   end;

Lm85:
  |[-1,0]|,|[1,0]| realize-max-dist-in C implies C is Jordan
  proof
    assume
A1: a,b realize-max-dist-in C;
    then consider Jc, Jd being compact with_the_max_arc Subset of T2 such that
A2: Jc is_an_arc_of a,b and
A3: Jd is_an_arc_of a,b and
A4: C = Jc \/ Jd and
A5: Jc /\ Jd = {a,b} and
A6: UMP C in Jc and
A7: LMP C in Jd and
A8: W-bound C = W-bound Jc and
A9: E-bound C = E-bound Jc by Lm84;
     set l = LMP Jc;
     set LJ = LSeg(l,d) /\ Jd;
     set k = UMP LJ;
     set x = (1/2)*(k+l);
A10: skl Down(x,C`) is_a_component_of T2|C` by CONNSP_1:43;
A11: skl Down(x,C`) = BDD C by A1,A2,A3,A4,A5,A6,A7,A8,A9,Th106;
     thus C` <>  {};
     take A1 = UBD C, A2 = BDD C;
     thus C` = A1 \/ A2 by JORDAN2C:31;
     thus A1 misses A2 by JORDAN2C:28;
     A1 is_a_component_of C` by JORDAN1C:12;
     then
A12: ex B1 being Subset of T2|C` st B1 = A1 & B1 is_a_component_of T2|C`
     by CONNSP_1:def 6;
     then C = Fr A1 by Th79
     .= Cl A1 /\ Cl A1`;
     then
A13: C c= Cl A1 by XBOOLE_1:17;
     C = Fr A2 by A11,A10,Th79
     .= Cl A2 /\ Cl A2`;
     then
A14: C c= Cl A2 by XBOOLE_1:17;
     A2 c= C` by JORDAN2C:29;
     then C misses A2 by SUBSET_1:43;
     then
A15:  C c= Cl A2 \ A2 by A14,XBOOLE_1:86;
A16:  A1 misses A2 by JORDAN2C:28;
     then A2 c= A1` by SUBSET_1:43;
     then
A17:  Cl A2 c= A1` by TOPS_1:31;
     A1 \/ A2 = C` by JORDAN2C:31;
     then A1 \/ A2 misses C by SUBSET_1:43;
     then C misses A1 by XBOOLE_1:70;
     then
A18:  A2 \/ C misses A1 by A16,XBOOLE_1:70;
     A2 \/ A1 = C` by JORDAN2C:31;
     then (A2 \/ A1)` misses C` by SUBSET_1:43;
     then (A2 \/ A1)` /\ C` = {} by XBOOLE_0:def 7;
     then (A2 \/ A1 \/ C)` = {} by SUBSET_1:29;
     then ((A2 \/ C) \/ A1)` = {} by XBOOLE_1:4;
     then (A2 \/ C)` /\ A1` = {} by SUBSET_1:29;
     then (A2 \/ C)` misses A1` by XBOOLE_0:def 7;
     then Cl A2 c= A2 \/ C by A17,A18,SUBSET_1:46;
     then
A19:  Cl A2 \ A2 c= C by XBOOLE_1:43;
     A1 c= C` by JORDAN2C:30;
     then C misses A1 by SUBSET_1:43;
     then
A20:  C c= Cl A1 \ A1 by A13,XBOOLE_1:86;
     A1 c= A2` by A16,SUBSET_1:43;
     then
A21:  Cl A1 c= A2` by TOPS_1:31;
     A2 \/ A1 = C` by JORDAN2C:31;
     then A2 \/ A1 misses C by SUBSET_1:43;
     then C misses A2 by XBOOLE_1:70;
     then
A22:  A1 \/ C misses A2 by A16,XBOOLE_1:70;
     A1 \/ A2 = C` by JORDAN2C:31;
     then (A1 \/ A2)` misses C` by SUBSET_1:43;
     then (A1 \/ A2)` /\ C` = {} by XBOOLE_0:def 7;
     then (A1 \/ A2 \/ C)` = {} by SUBSET_1:29;
     then ((A1 \/ C) \/ A2)` = {} by XBOOLE_1:4;
     then (A1 \/ C)` /\ A2` = {} by SUBSET_1:29;
     then (A1 \/ C)` misses A2` by XBOOLE_0:def 7;
     then Cl A1 c= A1 \/ C by A21,A22,SUBSET_1:46;
     then
     Cl A1 \ A1 c= C by XBOOLE_1:43;
     hence (Cl A1) \ A1 = C by A20,XBOOLE_0:def 10
     .= (Cl A2) \ A2 by A15,A19,XBOOLE_0:def 10;
     thus thesis by A11,A12,CONNSP_1:43;
   end;

Lm86:
  C is Jordan
  proof
    consider f being Homeomorphism of T2 such that
A1: a,b realize-max-dist-in f.:C by JORDAN24:7;
A2: f" is Homeomorphism of T2 by TOPGRP_1:30;
    f.:C is Simple_closed_curve by Th80;
    then f.:C is Jordan by A1,Lm85;
    then
A3: f".:(f.:C) is Jordan by A2,JORDAN24:16;
    rng f = [#]T2 by TOPS_2:def 5;
    then
A4: f" = f qua Function" by TOPS_2:def 4;
    dom f = the carrier of T2 by FUNCT_2:def 1;
    hence thesis by A3,A4,JGRAPH_6:22;
  end;

theorem :: Jordan's Curve Theorem
  for C being Simple_closed_curve
  ex A1, A2 being Subset of TOP-REAL 2 st
  C` = A1 \/ A2 & A1 misses A2 & (Cl A1) \ A1 = (Cl A2) \ A2 &
     for C1, C2 being Subset of (TOP-REAL 2)|C`
     st C1 = A1 & C2 = A2 holds
      C1 is_a_component_of (TOP-REAL 2)|C` &
      C2 is_a_component_of (TOP-REAL 2)|C`
  proof
    let C;
    C is Jordan by Lm86;
    hence thesis by JORDAN1:def 2;
  end;

theorem
  for C being Simple_closed_curve holds C is Jordan by Lm86;
