:: Jordan's Curve Theorem
::  by Artur Korni{\l}owicz
:: 
:: Received September 15, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies ARYTM, PRE_TOPC, EUCLID, TOPREAL1, RELAT_1, ARYTM_1, TOPS_1,
      SUBSET_1, JORDAN2C, METRIC_1, BOOLE, TOPREAL2, CONNSP_1, PSCOMP_1,
      TOPS_2, MCART_1, SQUARE_1, ARYTM_3, SPPOL_1, ASYMPT_0, PCOMPS_1, FUNCT_1,
      FUNCT_5, JORDAN1, JGRAPH_6, JORDAN6, JORDAN19, JORDAN21, JORDAN24,
      LATTICES, CONNSP_3, BORSUK_2, BORSUK_6, GRAPH_1, JORDAN5C, TOPGRP_1,
      COMPLEX1, BORSUK_1, ORDINAL2, TOPREALA, RELAT_2, RCOMP_1, TOPMETR,
      COMPTS_1, REALSET1, JORDAN1A, TOPREAL4, FUNCOP_1, TARSKI, JORDAN3, ABIAN,
      ZF_LANG, TREAL_1, SEQ_1, XREAL_0, COLLSP, CONNSP_2, SGRAPH1, FUNCT_4,
      SETFAM_1, PARTFUN3, PARTFUN1, KNASTER, FINSEQ_1, BROUWER, TOPREALB,
      ABSVALUE, TERMORD, PROB_1, JORDAN;
 notations TARSKI, XBOOLE_0, SUBSET_1, ZFMISC_1, ORDINAL1, SQUARE_1, NAT_1,
      MCART_1, RELSET_1, FUNCT_1, PARTFUN1, FUNCT_2, FUNCT_3, FUNCT_4,
      PSCOMP_1, NUMBERS, XCMPLX_0, XREAL_0, REAL_1, FINSEQ_1, COMPLEX1,
      RCOMP_1, STRUCT_0, PRE_TOPC, TOPS_1, BORSUK_1, TOPS_2, COMPTS_1, TREAL_1,
      CONNSP_1, CONNSP_2, CONNSP_3, TEX_2, METRIC_1, PCOMPS_1, TBSP_1, EUCLID,
      TOPRNS_1, BORSUK_2, TOPREAL1, TOPREAL2, TOPMETR, SPPOL_1, JORDAN1,
      JORDAN2C, JORDAN5C, TOPREAL4, JORDAN6, JGRAPH_6, JORDAN1A, GOBRD14,
      TOPREAL9, BORSUK_6, TOPREALA, JORDAN19, JORDAN21, TOPGRP_1, JORDAN24,
      ABIAN, PARTFUN3, TOPREALB, BROUWER;
 constructors TOPREAL1, COMPTS_1, TBSP_1, JORDAN2C, TOPREAL9, CONNSP_1, TOPS_1,
      TOPRNS_1, MONOID_0, TOPREALA, JORDAN1, JGRAPH_6, SPPOL_2, GOBRD14,
      JORDAN6, JORDAN19, JORDAN21, CONNSP_3, JORDAN5C, SPPOL_1, JCT_MISC,
      TEX_2, JORDAN1A, TOPREAL4, YELLOW_8, REALSET2, JORDAN24, TOPGRP_1, ABIAN,
      TREAL_1, PARTFUN3, BROUWER, BORSUK_3, LIMFUNC1, WAYBEL_3, COMPLEX1,
      TSEP_1;
 registrations XREAL_0, RELSET_1, SUBSET_1, STRUCT_0, EUCLID, PRE_TOPC,
      BORSUK_1, BORSUK_2, TOPREAL9, TOPS_1, NAT_1, TOPREALA, MEMBERED,
      JORDAN21, JORDAN22, JORDAN2C, TOPGEN_3, TOPREAL6, GOBRD14, SPPOL_2,
      YELLOW13, RCOMP_3, JORDAN5A, PSCOMP_1, SQUARE_1, JORDAN1A, FUNCT_1,
      JGRAPH_8, TOPGRP_1, WAYBEL_2, PARTFUN3, METRIC_1, REALSET1, BORSUK_4,
      BROUWER, WAYBEL_3, FUNCT_2, TOPREALB, TOPMETR, SEQ_1, ISOMICHI, TOPALG_5,
      JORDAN1K, TEX_4, SPRECT_4;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM, REAL;
 definitions TARSKI, XBOOLE_0, BORSUK_2, JORDAN1, SPPOL_1, JORDAN2C, JORDAN21,
      JORDAN24, TOPREAL2, CONNSP_1, BORSUK_1, PSCOMP_1, ABIAN, XREAL_0;
 theorems JORDAN6, PRE_TOPC, XBOOLE_0, EUCLID, TARSKI, SQUARE_1, CONNSP_1,
      JORDAN2C, JGRAPH_6, XREAL_1, JORDAN1K, GOBRD14, BORSUK_2, JORDAN21,
      JORDAN22, TOPREAL1, TOPREAL3, SPPOL_1, SUBSET_1, TOPMETR, TOPREAL9,
      TOPS_1, SPRECT_3, TOPRNS_1, TOPS_2, NAT_1, JORDAN1, TOPREALA, BORSUK_1,
      FUNCT_2, RCOMP_3, TOPREAL5, RELAT_1, GOBOARD7, FUNCT_1, BORSUK_6,
      JORDAN5A, XBOOLE_1, ZFMISC_1, CONNSP_3, JORDAN1H, TOPALG_2, GOBOARD9,
      SPPOL_2, JORDAN1J, PSCOMP_1, JORDAN1C, JORDAN1I, JORDAN1A, JORDAN15,
      JORDAN18, TOPREAL4, TSEP_1, STRUCT_0, TOPALG_3, JGRAPH_8, COMPTS_1,
      SEQ_4, FUNCOP_1, WEIERSTR, REAL_1, JORDAN5C, JORDAN20, JORDAN16, XREAL_0,
      KURATO_2, XCMPLX_1, MEMBERED, JGRAPH_5, SPRECT_1, JORDAN1E, JORDAN24,
      TOPGRP_1, TOPREAL2, RCOMP_1, BORSUK_4, BROUWER, ABIAN, TOPREAL6,
      ENUMSET1, METRIC_1, PCOMPS_1, TOPREALB, JORDAN19, ABSVALUE, TEX_2,
      MCART_1, BORSUK_5, METRIC_6, COMPLEX1, JGRAPH_2, SEQ_2, TIETZE, CONNSP_2,
      GRCAT_1, WAYBEL29, FUNCT_4, TREAL_1, JGRAPH_1, SEQ_1, PARTFUN3, RFUNCT_1,
      YELLOW12, XCMPLX_0, FUNCT_3, JORDAN5B;
 schemes FUNCT_2;

begin :: Preliminaries
  
reserve
  a, b, c, d, r, s for real number,
  n for Nat,
  p, p1, p2 for Point of TOP-REAL 2,
  x, y for Point of TOP-REAL n,
  C for Simple_closed_curve,
  A, B, P for Subset of TOP-REAL 2,
  U, V for Subset of (TOP-REAL 2)|C`,
  D for compact with_the_max_arc Subset of TOP-REAL 2;
  
  set T2 = TOP-REAL 2;
  
LmZ3: for A, B, C, Z being set st A c= Z & B c= Z & C c= Z holds
      A \/ B \/ C c= Z
      proof 
        let A, B, C, Z be set;
        assume A c= Z & B c= Z;
        then A \/ B c= Z by XBOOLE_1:8;
        hence thesis by XBOOLE_1:8;
      end;

LmZ4: 
      for A, B, C, D, Z being set st A c= Z & B c= Z & C c= Z & D c= Z holds
      A \/ B \/ C \/ D c= Z
      proof 
        let A, B, C, D, Z be set;
        assume A c= Z & B c= Z & C c= Z;
        then A \/ B \/ C c= Z by LmZ3;
        hence thesis by XBOOLE_1:8;
      end;

Lmmisses4:
         for A, B, C, D, Z being set st A misses Z & B misses Z &
         C misses Z & D misses Z holds A \/ B \/ C \/ D misses Z
         proof 
           let A, B, C, D, Z be set;
           assume A misses Z & B misses Z & C misses Z;
           then A \/ B \/ C misses Z by XBOOLE_1:114;
           hence thesis by XBOOLE_1:70;
         end;
                  
registration
  let M be symmetric triangle Reflexive MetrStruct, x, y be Point of M;
  cluster dist(x,y) -> non negative;
  coherence by METRIC_1:5;
end;

registration
  let n be Nat, x, y be Point of TOP-REAL n;
  cluster dist(x,y) -> non negative;
  coherence
  proof 
    ex p, q being Point of Euclid n st p = x & q = y & dist(x,y) = dist(p,q)
    by GOBRD14:def 1;
    hence 0 <= dist(x,y);
  end;
end;

registration
  let n be Nat, x, y be Point of TOP-REAL n;
  cluster |.x-y.| -> non negative;
  coherence
  proof 
    reconsider x1 = x, y1 = y as Point of Euclid n by TOPREAL3:13;
    dist(x1,y1) = |.x-y.| by KURATO_2:31;
    hence 0 <= |.x-y.|;
  end;
end;

theorem Taba:
  for p1, p2 being Point of TOP-REAL n st p1 <> p2 holds 1/2*(p1+p2) <> p1
  proof 
    let p1, p2 be Point of TOP-REAL n;
    set r = 1/2;
    assume that
a3: p1 <> p2 and
a1: r*(p1+p2) = p1;
    r*(p1+p2) = r*p1+r*p2 by EUCLID:36;
    then 0.REAL n = p1-(r*p1+r*p2) by a1,EUCLID:46
    .= p1-r*p1-r*p2 by EUCLID:50
    .= 1 * p1-r*p1-r*p2 by EUCLID:33
    .= (1-r)*p1-r*p2 by EUCLID:54
    .= r*(p1-p2) by EUCLID:53;
    then p1-p2 = 0.REAL n by EUCLID:35;
    hence thesis by a3,EUCLID:47;
  end;
  
theorem Lmnowy61:
  p1`2 < p2`2 implies p1`2 < (1/2*(p1+p2))`2
  proof 
    assume
a1: p1`2 < p2`2;
    (1/2*(p1+p2))`2 = 1/2*((p1+p2)`2) by TOPREAL3:9
    .= 1/2*(p1`2+p2`2) by TOPREAL3:7
    .= (p1`2+p2`2)/2;
    hence thesis by a1,TOPREAL3:3;
  end;
  
theorem Lmnowy60:
  p1`2 < p2`2 implies (1/2*(p1+p2))`2 < p2`2
  proof 
    assume
a1: p1`2 < p2`2;
    (1/2*(p1+p2))`2 = 1/2*((p1+p2)`2) by TOPREAL3:9
    .= 1/2*(p1`2+p2`2) by TOPREAL3:7
    .= (p1`2+p2`2)/2;
    hence thesis by a1,TOPREAL3:3;
  end;

theorem Thpver:
  for A being vertical Subset of TOP-REAL 2 holds
  A /\ B is vertical
  proof 
    let A be vertical Subset of TOP-REAL 2;
    let p, q be Point of T2;
    assume p in A /\ B & q in A /\ B;
    then p in A & q in A by XBOOLE_0:def 3;
    hence thesis by SPPOL_1:def 3;
  end;
  
theorem
  for A being horizontal Subset of TOP-REAL 2 holds
  A /\ B is horizontal
  proof 
    let A be horizontal Subset of TOP-REAL 2;
    let p, q be Point of T2;
    assume p in A /\ B & q in A /\ B;
    then p in A & q in A by XBOOLE_0:def 3;
    hence thesis by SPPOL_1:def 2;
  end;
  
theorem
  p in LSeg(p1,p2) & LSeg(p1,p2) is vertical implies
  LSeg(p,p2) is vertical
  proof 
    assume
a1: p in LSeg(p1,p2);
    assume
a3: LSeg(p1,p2) is vertical;
    then
a2: p1`1 = p2`1 by SPPOL_1:37;
    p1 in LSeg(p1,p2) by TOPREAL1:6;
    then p`1 = p1`1 by a1,a3,SPPOL_1:def 3;
    hence thesis by a2,SPPOL_1:37;
  end;

theorem
  p in LSeg(p1,p2) & LSeg(p1,p2) is horizontal implies
  LSeg(p,p2) is horizontal
  proof 
    assume
a1: p in LSeg(p1,p2);
    assume
a3: LSeg(p1,p2) is horizontal;
    then
a2: p1`2 = p2`2 by SPPOL_1:36;
    p1 in LSeg(p1,p2) by TOPREAL1:6;
    then p`2 = p1`2 by a1,a3,SPPOL_1:def 2;
    hence thesis by a2,SPPOL_1:36;
  end;

registration
:: LSeg(NW-corner C,NE-corner C) is horizontal; ::SPRECT_3:29  
  let P be Subset of TOP-REAL 2;
  cluster LSeg(SW-corner P,SE-corner P) -> horizontal;
  coherence
  proof 
    (SW-corner P)`2 = S-bound P by PSCOMP_1:73
    .= (SE-corner P)`2 by PSCOMP_1:79;
    hence thesis by SPPOL_1:36;
  end;
  cluster LSeg(NW-corner P,SW-corner P) -> vertical;
  coherence
  proof 
    (NW-corner P)`1 = W-bound P by PSCOMP_1:74
    .= (SW-corner P)`1 by PSCOMP_1:72;
    hence thesis by SPPOL_1:37;
  end;
  cluster LSeg(NE-corner P,SE-corner P) -> vertical;
  coherence
  proof 
    (NE-corner P)`1 = E-bound P by PSCOMP_1:76
    .= (SE-corner P)`1 by PSCOMP_1:78;
    hence thesis by SPPOL_1:37;
  end;
end;

registration
  let P be Subset of TOP-REAL 2;
  cluster LSeg(SE-corner P,SW-corner P) -> horizontal;
  coherence
  proof 
    LSeg(SW-corner P,SE-corner P) = LSeg(SE-corner P,SW-corner P);
    hence thesis;
  end;
  cluster LSeg(SW-corner P,NW-corner P) -> vertical;
  coherence
  proof 
    LSeg(SW-corner P,NW-corner P) = LSeg(NW-corner P,SW-corner P);
    hence thesis;
  end;
  cluster LSeg(SE-corner P,NE-corner P) -> vertical;
  coherence
  proof 
    LSeg(NE-corner P,SE-corner P) = LSeg(SE-corner P,NE-corner P);
    hence thesis;
  end;
end;

registration
  cluster vertical non empty compact -> with_the_max_arc Subset of TOP-REAL 2;
  coherence
  proof 
    let A be Subset of TOP-REAL 2;
    assume
a0: A is vertical non empty compact;
    then
a1: W-bound A = E-bound A by SPRECT_1:17;
a2: E-min A in A by a0,SPRECT_1:16;
    E-min A = |[E-bound A, inf (proj2||E-most A)]| by PSCOMP_1:def 47;
    then (E-min A)`1 = E-bound A by EUCLID:56;
    then E-min A in Vertical_Line((W-bound A+E-bound A)/2) by a1,JORDAN6:34;
    hence A meets Vertical_Line((W-bound A+E-bound A)/2) by a2,XBOOLE_0:3;
  end;
end;

theorem Twbwb:
  for X, Y being non empty compact Subset of TOP-REAL 2 st
  X c= Y & (W-min Y in X or W-max Y in X) holds W-bound X = W-bound Y
  proof 
    let X, Y be non empty compact Subset of T2;
    assume
a1: X c= Y & (W-min Y in X or W-max Y in X);
    (W-min X)`1 = W-bound X & (W-max X)`1 = W-bound X &
    (W-min Y)`1 = W-bound Y & (W-max Y)`1 = W-bound Y
    by PSCOMP_1:84;
    hence thesis by a1,JORDAN1J:21,22;
  end;
  
theorem Tebeb:
  for X, Y being non empty compact Subset of TOP-REAL 2 st
  X c= Y & (E-min Y in X or E-max Y in X) holds E-bound X = E-bound Y
  proof 
    let X, Y be non empty compact Subset of T2;
    assume
a1: X c= Y & (E-min Y in X or E-max Y in X);
    (E-min X)`1 = E-bound X & (E-max X)`1 = E-bound X &
    (E-min Y)`1 = E-bound Y & (E-max Y)`1 = E-bound Y
    by PSCOMP_1:104;
    hence thesis by a1,JORDAN1J:17,18;
  end;
  
theorem
  for X, Y being non empty compact Subset of TOP-REAL 2 st
  X c= Y & (N-min Y in X or N-max Y in X) holds N-bound X = N-bound Y
  proof 
    let X, Y be non empty compact Subset of T2;
    assume
a1: X c= Y & (N-min Y in X or N-max Y in X);
    (N-min X)`2 = N-bound X & (N-max X)`2 = N-bound X &
    (N-min Y)`2 = N-bound Y & (N-max Y)`2 = N-bound Y
    by PSCOMP_1:94;
    hence thesis by a1,JORDAN1J:15,16;
  end;
  
theorem
  for X, Y being non empty compact Subset of TOP-REAL 2 st
  X c= Y & (S-min Y in X or S-max Y in X) holds S-bound X = S-bound Y
  proof 
    let X, Y be non empty compact Subset of T2;
    assume
a1: X c= Y & (S-min Y in X or S-max Y in X);
    (S-min X)`2 = S-bound X & (S-max X)`2 = S-bound X &
    (S-min Y)`2 = S-bound Y & (S-max Y)`2 = S-bound Y
    by PSCOMP_1:114;
    hence thesis by a1,JORDAN1J:19,20;
  end;

theorem
  W-bound C = W-bound North_Arc C
  proof 
a1: North_Arc C c= C by JORDAN22:11;
    W-min C in North_Arc C by JORDAN22:5;
    hence thesis by a1,Twbwb;
end;

theorem
  E-bound C = E-bound North_Arc C
  proof 
a1: North_Arc C c= C by JORDAN22:11;
    E-max C in North_Arc C by JORDAN22:6;
    hence thesis by a1,Tebeb;
end;

theorem
  W-bound C = W-bound South_Arc C
  proof 
a1: South_Arc C c= C by JORDAN22:12;
    W-min C in South_Arc C by JORDAN22:7;
    hence thesis by a1,Twbwb;
end;

theorem
  E-bound C = E-bound South_Arc C
  proof 
a1: South_Arc C c= C by JORDAN22:12;
    E-max C in South_Arc C by JORDAN22:8;
    hence thesis by a1,Tebeb;
end;

theorem TSV:
  p1`1 <= r & r <= p2`1 implies LSeg(p1,p2) meets Vertical_Line(r)
  proof 
    assume that
a1: p1`1 <= r and
a2: r <= p2`1;
    set a = p1`1, b = p2`1;
    set l = (r-a) / (b-a);
    set k = (1-l)*p1+l*p2;
b1: l is Real by XREAL_0:def 1;
a5: a-a <= r-a by a1,XREAL_1:11;
    a <= b by a1,a2,XREAL_1:2;
    then b-b <= b-a by XREAL_1:12;
    then
a3: 0 <= l by a5,XREAL_1:138;
    r-a <= b-a by a2,XREAL_1:11;
    then l <= 1 by a5,XREAL_1:185;
    then
a4: k in LSeg(p1,p2) by a3,b1,SPPOL_1:22;
    per cases;
    suppose a <> b;
      then
a6:   b-a <> 0 by XCMPLX_1:15;
      k`1 = (1-l)*a+l*b by TOPREAL9:41
      .= a+l*(b-a)
      .= a+(r-a) by a6,XCMPLX_1:88;
      then k in Vertical_Line(r) by JORDAN6:34;
      hence thesis by a4,XBOOLE_0:3;
    end;
    suppose
s1:   a = b;
c1:   p1 in LSeg(p1,p2) by TOPREAL1:6;
      a = r by a1,a2,s1,XREAL_1:1;
      then p1 in Vertical_Line(r) by JORDAN6:34;
      hence thesis by c1,XBOOLE_0:3;
    end;
  end;
  
theorem
  p1`2 <= r & r <= p2`2 implies LSeg(p1,p2) meets Horizontal_Line(r)
  proof 
    assume that
a1: p1`2 <= r and
a2: r <= p2`2;
    set a = p1`2, b = p2`2;
    set l = (r-a) / (b-a);
    set k = (1-l)*p1+l*p2;
b1: l is Real by XREAL_0:def 1;
a5: a-a <= r-a by a1,XREAL_1:11;
    a <= b by a1,a2,XREAL_1:2;
    then b-b <= b-a by XREAL_1:12;
    then
a3: 0 <= l by a5,XREAL_1:138;
    r-a <= b-a by a2,XREAL_1:11;
    then l <= 1 by a5,XREAL_1:185;
    then
a4: k in LSeg(p1,p2) by a3,b1,SPPOL_1:22;
    per cases;
    suppose a <> b;
      then
a6:   b-a <> 0 by XCMPLX_1:15;
      k`2 = (1-l)*a+l*b by TOPREAL9:42
      .= a+l*(b-a)
      .= a+(r-a) by a6,XCMPLX_1:88;
      then k in Horizontal_Line(r) by JORDAN6:35;
      hence thesis by a4,XBOOLE_0:3;
    end;
    suppose
s1:   a = b;
c1:   p1 in LSeg(p1,p2) by TOPREAL1:6;
      a = r by a1,a2,s1,XREAL_1:1;
      then p1 in Horizontal_Line(r) by JORDAN6:35;
      hence thesis by c1,XBOOLE_0:3;
    end;
  end;
  
Lm100: now
         let n;
         let A be Subset of TOP-REAL n such that
a1:      A is empty;
         consider p being Point of TOP-REAL n;
         A c= {p} by a1,XBOOLE_1:2;
         hence A is Bounded by JORDAN2C:16;
       end;
       
registration
  let n;
  cluster empty -> Bounded Subset of TOP-REAL n;
  coherence by Lm100;
  cluster non Bounded -> non empty Subset of TOP-REAL n;
  coherence by Lm100;
end;

registration
  let n be non empty Nat;
  cluster open closed non Bounded convex Subset of TOP-REAL n;
  existence
  proof 
    take [#]TOP-REAL n;
    1 <= n by NAT_1:39;
    hence thesis by JORDAN2C:21,41;
  end;
end;

theorem Th25:
  for C being compact Subset of TOP-REAL 2 holds
  north_halfline UMP C \ {UMP C} misses C
  proof 
    let C be compact Subset of TOP-REAL 2;
    set p = UMP C;
    set L = north_halfline p;
    set w = (W-bound C + E-bound C) / 2;
    assume L \ {p} meets C;
    then consider x being set such that
a1: x in L \ {p} and
a2: x in C by XBOOLE_0:3;
a3: x in L & x <> p by a1,ZFMISC_1:64;
    reconsider x as Point of T2 by a1;
a5: x`1 = p`1 & x`2 >= p`2 by a3,JORDAN1A:def 2;
    then x`2 <> p`2 by a3,TOPREAL3:11;
    then
a4: x`2 > p`2 by a5,REAL_1:def 5;
    x`1 = w by a5,JORDAN21:33;
    then x in Vertical_Line w by JORDAN6:34;
    then x in C /\ Vertical_Line w by a2,XBOOLE_0:def 3;
    hence thesis by a4,JORDAN21:41;
  end;

theorem Th24:
  for C being compact Subset of TOP-REAL 2 holds
  south_halfline LMP C \ {LMP C} misses C
  proof 
    let C be compact Subset of TOP-REAL 2;
    set p = LMP C;
    set L = south_halfline p;
    set w = (W-bound C + E-bound C) / 2;
    assume L \ {p} meets C;
    then consider x being set such that
a1: x in L \ {p} and
a2: x in C by XBOOLE_0:3;
a3: x in L & x <> p by a1,ZFMISC_1:64;
    reconsider x as Point of T2 by a1;
a5: x`1 = p`1 & x`2 <= p`2 by a3,JORDAN1A:def 4;
    then x`2 <> p`2 by a3,TOPREAL3:11;
    then
a4: x`2 < p`2 by a5,REAL_1:def 5;
    x`1 = w by a5,JORDAN21:35;
    then x in Vertical_Line w by JORDAN6:34;
    then x in C /\ Vertical_Line w by a2,XBOOLE_0:def 3;
    hence thesis by a4,JORDAN21:42;
  end;

Lm101: for A being non empty convex Subset of T2 holds A is connected;

theorem Th26:
  for C being compact Subset of TOP-REAL 2 holds
  north_halfline UMP C \ {UMP C} c= UBD C
  proof 
    let C be compact Subset of TOP-REAL 2;
    set A = north_halfline UMP C \ {UMP C};
    north_halfline UMP C is not Bounded by JORDAN1C:10;
    then reconsider A as non Bounded Subset of T2 by JORDAN1I:2;
a2: A misses C by Th25;
    A is convex by JORDAN21:6;
    then A is connected by Lm101;
    hence thesis by a2,JORDAN1C:13;
  end;

theorem Th27:
  for C being compact Subset of TOP-REAL 2 holds
  south_halfline LMP C \ {LMP C} c= UBD C
  proof 
    let C be compact Subset of TOP-REAL 2;
    set A = south_halfline LMP C \ {LMP C};
    south_halfline LMP C is not Bounded by JORDAN1C:11;
    then reconsider A as non Bounded Subset of T2 by JORDAN1I:2;
a2: A misses C by Th24;
    A is convex by JORDAN21:7;
    then A is connected by Lm101;
    hence thesis by a2,JORDAN1C:13;
  end;

theorem Th30:
  A is_inside_component_of B implies UBD B misses A
  proof 
    assume A is_inside_component_of B;
    then
a1: A c= BDD B by JORDAN2C:26;
    BDD B misses UBD B by JORDAN2C:28;
    hence thesis by a1,XBOOLE_1:63;
  end;

theorem
  A is_outside_component_of B implies BDD B misses A
  proof 
    assume A is_outside_component_of B;
    then
a1: A c= UBD B by JORDAN2C:27;
    BDD B misses UBD B by JORDAN2C:28;
    hence thesis by a1,XBOOLE_1:63;
  end;

Lm27: 
      p in C implies {p} misses U
      proof 
        assume
a1:     p in C;
a4:     U is Subset of T2 by PRE_TOPC:39;
        the carrier of T2|C` = C` by JORDAN1:1;
        then U misses C by a4,SUBSET_1:43;
        then not p in U by a1,XBOOLE_0:3;
        hence thesis by ZFMISC_1:56;
      end;
      
  set C0 = Closed-Interval-TSpace(0,1);
  set C1 = Closed-Interval-TSpace(-1,1);
  set l0 = (#)(-1,1);
  set l1 = (-1,1)(#);
  set h1 = L[01](l0,l1);

Lmcar: the carrier of [:T2,T2:] = [:the carrier of T2, the carrier of T2:]
       by BORSUK_1:def 5;

Lm1: [#]T2 = the carrier of T2 by PRE_TOPC:12;
Lm10:
now
  let T be non empty TopSpace;
  let a be Real;
  set c = the carrier of T;
  set f = c --> a;
  thus f is continuous
  proof 
A1: dom f = c by FUNCT_2:def 1;
A2: rng f = {a} by FUNCOP_1:14;
  let Y be Subset of REAL; assume Y is closed;
  per cases;
  suppose a in Y;
  then
A3: rng f c= Y by A2,ZFMISC_1:37;
  f"Y = f"(rng f /\ Y) by RELAT_1:168
     .= f"rng f by A3,XBOOLE_1:28
     .= c by A1,RELAT_1:169
     .= [#]T by PRE_TOPC:12;
  hence f"Y is closed;
  end;
  suppose not a in Y;
  then
A4: rng f misses Y by A2,ZFMISC_1:56;
  f"Y = f"(rng f /\ Y) by RELAT_1:168
     .= f"{} by A4,XBOOLE_0:def 7
     .= {}T by RELAT_1:171;
  hence f"Y is closed;
  end;
end;
end;

theorem Taa:
  for r being positive (real number)
  for a being Point of TOP-REAL n holds a in Ball(a,r)
  proof 
    let r be positive (real number);
    let a be Point of TOP-REAL n;
    |. a-a .| = 0 by TOPRNS_1:29;
    hence thesis by TOPREAL9:7;
  end;
  
theorem Th6:
  for r being non negative (real number)
  for p being Point of TOP-REAL n holds p is Point of Tdisk(p,r)
  proof 
    let r be non negative (real number);
    let p be Point of TOP-REAL n;
a1: the carrier of Tdisk(p,r) = cl_Ball(p,r) by BROUWER:3;
    |. p-p .| = 0 by TOPRNS_1:29;
    hence thesis by a1,TOPREAL9:8;
  end;

registration
  let r be positive (real number);
  let n be non empty Nat;
  let p, q be Point of TOP-REAL n;
  cluster cl_Ball(p,r) \ {q} -> non empty;
  coherence
  proof 
d2: the carrier of Tcircle(p,r) = Sphere(p,r) by TOPREALB:9;
d6: the carrier of Tdisk(p,r) = cl_Ball(p,r) by BROUWER:3;
d4: Sphere(p,r) c= cl_Ball(p,r) by TOPREAL9:17;
    consider a being Point of Tcircle(p,r);
d3: a in Sphere(p,r) by d2;
    per cases;
    suppose
d1:   a = q;
d5:   p is Point of Tdisk(p,r) by Th6;
      |. p-p .| <> r by TOPRNS_1:29;
      then p <> q by d1,d2,TOPREAL9:9;
      hence thesis by d5,d6,ZFMISC_1:64;
    end;
    suppose a <> q;
      hence thesis by d3,d4,ZFMISC_1:64;
    end;
  end;
end;

theorem TBB:
  r <= s implies Ball(x,r) c= Ball(x,s)
  proof 
    reconsider xe = x as Point of Euclid n by TOPREAL3:13;
    Ball(x,r) = Ball(xe,r) & Ball(x,s) = Ball(xe,s) by TOPREAL9:13;
    hence thesis by PCOMPS_1:1;
  end;

theorem ThrB:
  cl_Ball(x,r) \ Ball(x,r) = Sphere(x,r)
  proof 
    thus cl_Ball(x,r) \ Ball(x,r) c= Sphere(x,r)
    proof
      let a be set;
      assume
a1:   a in cl_Ball(x,r) \ Ball(x,r);
      then reconsider a as Point of TOP-REAL n;
      a in cl_Ball(x,r) & not a in Ball(x,r) by a1,XBOOLE_0:def 4;
      then |. a-x .| <= r & |. a-x .| >= r by TOPREAL9:7,8;
      then |. a-x .| = r by XREAL_1:1;
      hence thesis by TOPREAL9:9;
    end;
    let a be set;
    assume
a2: a in Sphere(x,r);
    then reconsider a as Point of TOP-REAL n;
    |. a-x .| = r by a2,TOPREAL9:9;
    then a in cl_Ball(x,r) & not a in Ball(x,r) by TOPREAL9:7,8;
    hence thesis by XBOOLE_0:def 4;
  end;

theorem TLes:
  y in Sphere(x,r) implies LSeg(x,y) \ {x,y} c= Ball(x,r)
  proof 
    assume
w1: y in Sphere(x,r);
    per cases;
    suppose
w2:   r = 0;
      reconsider xe = x as Point of Euclid n by TOPREAL3:13;
      Sphere(x,r) = Sphere(xe,r) by TOPREAL9:15;
      then Sphere(x,r) = {x} by w2,TOPREAL6:62;
      then
e2:   x = y by w1,TARSKI:def 1;
e1:   LSeg(x,x) = {x} by TOPREAL1:7;
e3:   {x,x} = {x} by ENUMSET1:69;
      {x} \ {x} = {} by XBOOLE_1:37;
      hence thesis by e1,e2,e3,XBOOLE_1:2;
    end;
    suppose
dd:   r <> 0;
    let k be set;
    assume
a0: k in LSeg(x,y) \ {x,y};
    then k in LSeg(y,x) by XBOOLE_0:def 4;
    then consider l being Real such that
a1: 0 <= l & l <= 1 and
a2: k = (1-l)*x + l*y by SPPOL_1:21;
    reconsider k as Point of TOP-REAL n by a2;
    not k in {x,y} by a0,XBOOLE_0:def 4;
    then k <> x & k <> y by TARSKI:def 2;
    then 0 <> l & l <> 1 by a2,TOPREAL9:4;
    then
c8: 0 < l & l < 1 by a1,REAL_1:def 5;
    k-x = (1-l)*x - x + l*y by a2,JORDAN2C:9
    .= 1 * x - l*x - x + l*y by EUCLID:54
    .= x - l*x - x + l*y by EUCLID:33
    .= x - x - l*x + l*y by TOPREAL9:1
    .= 0.REAL n - l*x + l*y by EUCLID:46
    .= -l*x + l*y by JORDAN2C:13
    .= l*y - l*x by EUCLID:45
    .= l*(y-x) by EUCLID:53;
    then
m1: |. k-x .| = abs(l) * |. y-x .| by TOPRNS_1:8
    .= l*|. y-x .| by a1,ABSVALUE:def 1
    .= l*r by w1,TOPREAL9:9;
    0 <= r by w1,TOPREAL9:39;
    then l*r < 1 * r by dd,c8,XREAL_1:70;
    hence thesis by m1,TOPREAL9:7;
  end;
  end;

theorem Tdb:
  r < s implies cl_Ball(x,r) c= Ball(x,s)
  proof 
    assume
a1: r < s;
    let a be set;
    assume
a2: a in cl_Ball(x,r);
    then reconsider a as Point of TOP-REAL n;
    |. a-x .| <= r by a2,TOPREAL9:8;
    then |. a-x .| < s by a1,XREAL_1:2;
    hence thesis by TOPREAL9:7;
  end;

theorem Tsb:
  r < s implies Sphere(x,r) c= Ball(x,s)
  proof 
    assume r < s;
    then
a1: cl_Ball(x,r) c= Ball(x,s) by Tdb;
    Sphere(x,r) c= cl_Ball(x,r) by TOPREAL9:17;
    hence thesis by a1,XBOOLE_1:1;
  end;

theorem ThClB:
  for r being non zero (real number) holds Cl Ball(x,r) = cl_Ball(x,r)
  proof 
    let r be non zero (real number);
    Ball(x,r) c= cl_Ball(x,r) by TOPREAL9:16;
    hence Cl Ball(x,r) c= cl_Ball(x,r) by TOPS_1:31;
    per cases;
    suppose
s1:   Ball(x,r) is empty;
      then r < 0 by TOPREAL9:21;
      then
w1:   cl_Ball(x,r) is empty by TOPREAL9:22;
      for A being empty Subset of TOP-REAL n holds Cl A is empty;
      hence thesis by s1,w1;
    end;
    suppose
s1:   Ball(x,r) is non empty;
    let a be set;
    assume
a1: a in cl_Ball(x,r);
    then reconsider a as Point of TOP-REAL n;
    reconsider ae = a as Point of Euclid n by TOPREAL3:13;
b4: 0 < r by s1,TOPREAL9:20;
    for s being real number st 0 < s & s < r holds Ball(ae,s) meets Ball(x,r)
    proof
      let s be real number such that
a2:   0 < s and
c1:   s < r;
      now
b1:     Ball(x,r) \/ Sphere(x,r) = cl_Ball(x,r) by TOPREAL9:18;
        per cases by a1,b1,XBOOLE_0:def 2;
        suppose
b2:       a in Ball(x,r);
          |.a-a.| = 0 by TOPRNS_1:29;
          then a in Ball(a,s) by a2,TOPREAL9:7;
          hence Ball(a,s) meets Ball(x,r) by b2,XBOOLE_0:3;
        end;
        suppose
b3:       a in Sphere(x,r);
          then
d2:       |. a-x .| = r by TOPREAL9:9;
          |. x-x .| = 0 by TOPRNS_1:29;
          then
d6:       x in Ball(x,r) by b4,TOPREAL9:7;
          set z = s/(2*r);
c3:       z is Real by XREAL_0:def 1;
          set q = (1-z)*a+z*x;
          1 * r < 2*r by b4,XREAL_1:70;
          then s < 2*r by c1,XREAL_1:2;
          then
c2:       z < 1 by a2,XREAL_1:191;
          0 < 2*r by b4,XREAL_1:131;
          then
c8:       0 < z by a2,XREAL_1:141;
          then
c7:       q in LSeg(a,x) by c2,c3,SPPOL_1:22;
          Ball(x,r) misses Sphere(x,r) by TOPREAL9:19;
          then a <> x by b3,d6,XBOOLE_0:3;
          then q <> a & q <> x by c2,c8,TOPREAL9:4;
          then not q in {a,x} by TARSKI:def 2;
          then
c6:       q in LSeg(a,x) \ {a,x} by c7,XBOOLE_0:def 4;
c5:       LSeg(a,x) \ {a,x} c= Ball(x,r) by b3,TLes;
          q-a = (1-z)*a - a + z*x by JORDAN2C:9
          .= 1 * a - z*a - a + z*x by EUCLID:54
          .= a - z*a - a + z*x by EUCLID:33
          .= a - a - z*a + z*x by TOPREAL9:1
          .= 0.REAL n - z*a + z*x by EUCLID:46
          .= -z*a + z*x by JORDAN2C:13
          .= z*x - z*a by EUCLID:45
          .= z*(x-a) by EUCLID:53;
          then |.q-a.| = abs(z) * |.x-a.| by c3,TOPRNS_1:8
          .= z*|.x-a.| by c8,ABSVALUE:def 1
          .= z*|. a-x .| by TOPRNS_1:28
          .= s/2 by d2,XCMPLX_1:93;
          then
d1:       q in Sphere(a,s/2) by TOPREAL9:9;
          s/2 < s/1 by a2,XREAL_1:78;
          then Sphere(a,s/2) c= Ball(a,s) by Tsb;
          hence Ball(a,s) meets Ball(x,r) by c5,c6,d1,XBOOLE_0:3;
        end;
      end;
      hence thesis by TOPREAL9:13;
    end;
    hence thesis by b4,JORDAN19:8;
  end;
  end;

theorem ThFrB:
  for r being non zero (real number) holds
  Fr Ball(x,r) = Sphere(x,r)
  proof 
    let r be non zero (real number);
    set P = Ball(x,r);
    thus Fr P = Cl P \ P by TOPS_1:76
    .= cl_Ball(x,r) \ P by ThClB
    .= Sphere(x,r) by ThrB;
  end;

registration
  let n be non empty Nat;
  cluster Bounded -> proper Subset of TOP-REAL n;
  coherence
  proof 
a1: the carrier of TOP-REAL n = [#]TOP-REAL n by PRE_TOPC:12;
    1 <= n by NAT_1:39;
    then [#]TOP-REAL n is not Bounded by JORDAN2C:41;
    hence thesis by a1,TEX_2:5;
  end;
end;

registration
  let n;
  cluster non empty closed convex Bounded Subset of TOP-REAL n;
  existence
  proof 
    take cl_Ball(0.REAL n,1);
    thus thesis;
  end;
  cluster non empty open convex Bounded Subset of TOP-REAL n;
  existence
  proof 
    take Ball(0.REAL n,1);
    thus thesis;
  end;
end;

registration
  let n be Nat; let A be Bounded Subset of TOP-REAL n;
  cluster Cl A -> Bounded;
  coherence by TOPREAL6:71;
end;

registration
  let n be Nat; let A be Bounded Subset of TOP-REAL n;
  cluster Fr A -> Bounded;
  coherence
  proof 
    Fr A = Cl A /\ Cl A` by TOPS_1:def 2;
    hence thesis by JORDAN1I:1;
  end;
end;

theorem TexK:
  for A being closed Subset of TOP-REAL n, p being Point of TOP-REAL n st
  not p in A
  ex r being positive (real number) st Ball(p,r) misses A
  proof 
    let A be closed Subset of TOP-REAL n, p be Point of TOP-REAL n;
    assume not p in A;
    then
a2: p in A` by SUBSET_1:50;
    reconsider e = p as Point of Euclid n by TOPREAL3:13;
    TopSpaceMetr Euclid n = TOP-REAL n by EUCLID:def 8;    
    then consider r being real number such that
a3: r > 0 and
a4: Ball(e,r) c= A` by a2,TOPMETR:22;
    reconsider r as positive (real number) by a3;
    take r;
    Ball(p,r) = Ball(e,r) by TOPREAL9:13;
    hence thesis by a4,SUBSET_1:43;
  end;
  
theorem Th11:
  for A being Bounded Subset of TOP-REAL n, a being Point of TOP-REAL n
  ex r being positive (real number) st A c= Ball(a,r)
  proof 
    let A be Bounded Subset of TOP-REAL n;
    let a be Point of TOP-REAL n;
    reconsider C = A as bounded Subset of Euclid n by JORDAN2C:def 2;
    consider r being Real, x being Element of Euclid n such that
a2: 0 < r and
a3: C c= Ball(x,r) by METRIC_6:def 10;
    reconsider r as positive (real number) by a2;
    reconsider x1 = x as Point of TOP-REAL n by TOPREAL3:13;
    take s = r+|.x1-a.|;
    let p be set;
    assume
a4: p in A;
    then reconsider p1 = p as Point of TOP-REAL n;
    p = p1;
    then reconsider p as Point of Euclid n by TOPREAL3:13;
b1: dist(p,x) < r by a3,a4,METRIC_1:12;
a8: |.p1-x1.| = dist(p,x) by KURATO_2:31;
a9: |.p1-a.| <= |.p1-x1.| + |.x1-a.| by TOPRNS_1:35;
    |.p1-x1.| + |.x1-a.| < s by a8,b1,XREAL_1:8;
    then |.p1-a.| < s by a9,XREAL_1:2;
    hence thesis by TOPREAL9:7;
  end;

theorem Th2:
  for S, T being TopStruct, f being map of S, T st f is_homeomorphism holds
  f is onto
  proof 
    let S, T be TopStruct;
    let f be map of S, T;
    assume f is_homeomorphism;
    then rng f = [#]T by TOPS_2:def 5
    .= the carrier of T by PRE_TOPC:12;
    hence f is onto by FUNCT_2:def 3;
  end;

theorem Th3:
  for T being TopSpace, S being SubSpace of T,
  A being Subset of T, B being Subset of S st A = B holds
  T|A = S|B
  proof 
    let T be TopSpace;
    let S be SubSpace of T;
    let A be Subset of T;
    let B be Subset of S;
    assume
A1: A = B;
A2: S|B is SubSpace of T by TSEP_1:7;
    [#](S|B) = the carrier of S|B by PRE_TOPC:12
    .= A by A1,JORDAN1:1;
    hence T|A = S|B by A2,PRE_TOPC:def 10;
  end;

registration
  let T be non empty being_T2 TopSpace;
  cluster -> being_T2 (non empty SubSpace of T);
  coherence by TOPMETR:3;
end;

registration
  let p, r;
  cluster Tdisk(p,r) -> closed;
  coherence
  proof 
    let A be Subset of T2;
    assume A = the carrier of Tdisk(p,r);
    then A = cl_Ball(p,r) by BROUWER:3;
    hence A is closed;
  end;
end;

registration
  let p, r;
  cluster Tdisk(p,r) -> compact;
  coherence
  proof 
    set D = Tdisk(p,r);
    the carrier of D is Subset of T2 by TSEP_1:1;
    then reconsider Q = [#]D as Subset of T2 by XBOOLE_1:1;
    [#]D = the carrier of D by PRE_TOPC:12
    .= cl_Ball(p,r) by BROUWER:3;
    then Q is compact by TOPREAL6:88;
    then [#]D is compact by COMPTS_1:11;
    hence thesis by COMPTS_1:10;
  end;
end;

begin :: Paths
  
theorem
  for T being non empty TopSpace, a, b being Point of T
  for f being Path of a,b st a,b are_connected holds rng f is connected
  proof 
    let T be non empty TopSpace, a, b be Point of T;
    let f be Path of a,b such that
a0: a,b are_connected;
a1: dom f = the carrier of I[01] by FUNCT_2:def 1;
    reconsider A = [.0,1.] as Subset of R^1 by TOPMETR:24;
    reconsider B = A as Subset of I[01] by a1,BORSUK_1:83;
    A is connected by RCOMP_3:52;
    then
a3: B is connected by CONNSP_1:24;
a4: f is continuous by a0,BORSUK_2:def 2;
    f.:B = rng f by a1,BORSUK_1:83,RELAT_1:146;
    hence rng f is connected by a3,a4,TOPREAL5:5;
  end;
  
theorem Th1:
  for X being non empty TopSpace, Y being non empty SubSpace of X,
  x1, x2 being Point of X, y1, y2 being Point of Y,
  f being Path of x1,x2 st x1 = y1 & x2 = y2 & x1,x2 are_connected &
  rng f c= the carrier of Y holds
  y1,y2 are_connected & f is Path of y1,y2
  proof 
    let X be non empty TopSpace, Y be non empty SubSpace of X,
    x1, x2 be Point of X, y1, y2 be Point of Y,
    f be Path of x1,x2 such that
A1: x1 = y1 & x2 = y2 and
A2: x1, x2 are_connected;
    assume rng f c= the carrier of Y;
    then reconsider g = f as map of I[01], Y by FUNCT_2:8;
A3: f is continuous by A2,BORSUK_2:def 2;
    thus
A4: ex f being map of I[01], Y st f is continuous & f.0 = y1 & f.1 = y2
    proof
      take g;
      thus g is continuous by A3,TOPMETR:8;
      thus thesis by A1,A2,BORSUK_2:def 2;
    end;
    g is Path of y1,y2
    proof
      thus ex f being map of I[01], Y st f is continuous & f.0 = y1 & f.1 = y2
      by A4;
      thus g is continuous by A3,TOPMETR:8;
      thus g.0 = y1 & g.1 = y2 by A1,A2,BORSUK_2:def 2;
    end;
    hence f is Path of y1,y2;
  end;

theorem Th1a:
  for X being arcwise_connected non empty TopSpace,
  Y being non empty SubSpace of X,
  x1, x2 being Point of X, y1, y2 being Point of Y,
  f being Path of x1,x2 st x1 = y1 & x2 = y2 & 
  rng f c= the carrier of Y holds
  y1,y2 are_connected & f is Path of y1,y2
  proof 
    let X be arcwise_connected non empty TopSpace,
    Y be non empty SubSpace of X,
    x1, x2 be Point of X, y1, y2 be Point of Y;
    x1,x2 are_connected by BORSUK_2:def 3;
    hence thesis by Th1;
  end;

Lmrcm: 
  for T being non empty TopSpace, a, b being Point of T
  for f being Path of a,b st a,b are_connected holds
  rng f c= rng -f
  proof 
    let T be non empty TopSpace;
    let a, b be Point of T;
    let f be Path of a,b;
    assume
Z0: a,b are_connected;
    let y be set;
    assume y in rng f;
    then consider x being set such that
a1: x in dom f and
a2: f.x = y by FUNCT_1:def 5;
    reconsider x as Point of I[01] by a1;
a4: dom -f = the carrier of I[01] by FUNCT_2:def 1;
a5: 1-x is Point of I[01] by JORDAN5B:4;
    then (-f).(1-x) = f.(1-(1-x)) by Z0,BORSUK_2:def 6;
    hence thesis by a2,a4,a5,FUNCT_1:def 5;
  end;

theorem Trcm:
  for T being non empty TopSpace, a, b being Point of T
  for f being Path of a,b st a,b are_connected holds
  rng f = rng -f
  proof 
    let T be non empty TopSpace;
    let a, b be Point of T;
    let f be Path of a,b;
    assume
Z0: a,b are_connected;
    hence rng f c= rng -f by Lmrcm;
    f = --f by Z0,BORSUK_6:50;
    hence thesis by Z0,Lmrcm;
  end;

theorem Trcm1:
  for T being arcwise_connected non empty TopSpace, a, b being Point of T
  for f being Path of a,b holds rng f = rng -f
  proof 
    let T be arcwise_connected non empty TopSpace;
    let a, b be Point of T;
    let f be Path of a,b;
    a,b are_connected by BORSUK_2:def 3;
    hence thesis by Trcm;
  end;

theorem Trc1:
  for T being non empty TopSpace, a, b, c being Point of T
  for f being Path of a,b, g being Path of b,c st
  a,b are_connected & b,c are_connected holds
  rng f c= rng(f+g)
  proof 
    let T be non empty TopSpace;
    let a, b, c be Point of T;
    let f be Path of a,b;
    let g be Path of b,c;
    assume that
Z0: a,b are_connected and
Z1: b,c are_connected;
    let y be set;
    assume y in rng f;
    then consider x being set such that
a1: x in dom f and
a2: f.x = y by FUNCT_1:def 5;
a4: dom(f+g) = the carrier of I[01] by FUNCT_2:def 1;
    reconsider x as Point of I[01] by a1;
    1/2*x = x/2;
    then
a6: x/2 is Point of I[01] by BORSUK_6:9;
    x <= 1 by JORDAN5A:2;
    then x/2 <= 1/2 by XREAL_1:74;
    then (f+g).(x/2) = f.(2*(x/2)) by Z0,Z1,a6,BORSUK_2:def 5;
    hence y in rng(f+g) by a4,a2,a6,FUNCT_1:def 5;
  end;
  
theorem
  for T being arcwise_connected non empty TopSpace, a, b, c being Point of T
  for f being Path of a,b, g being Path of b,c holds
  rng f c= rng(f+g)
  proof 
    let T be arcwise_connected non empty TopSpace;
    let a, b, c be Point of T;
    let f be Path of a,b;
    let g be Path of b,c;
    a,b are_connected & b,c are_connected by BORSUK_2:def 3;
    hence thesis by Trc1;
  end;
  
theorem Trc2:
  for T being non empty TopSpace, a, b, c being Point of T
  for f being Path of b,c, g being Path of a,b st
  a,b are_connected & b,c are_connected holds
  rng f c= rng(g+f)
  proof 
    let T be non empty TopSpace;
    let a, b, c be Point of T;
    let f be Path of b,c;
    let g be Path of a,b;
    assume that
Z0: a,b are_connected and
Z1: b,c are_connected;
    let y be set;
    assume y in rng f;
    then consider x being set such that
a1: x in dom f and
a2: f.x = y by FUNCT_1:def 5;
a4: dom(g+f) = the carrier of I[01] by FUNCT_2:def 1;
    reconsider x as Point of I[01] by a1;
    0 <= x by JORDAN5A:2;
    then 0/2 <= x/2 by XREAL_1:74;
    then
a8: 0+1/2 <= x/2+1/2 by XREAL_1:8;
    x <= 1 by JORDAN5A:2;
    then x+1 <= 1+1 by XREAL_1:8;
    then (x+1)/2 <= 2/2 by XREAL_1:74;
    then
a6: x/2+1/2 is Point of I[01] by a8,JORDAN5A:2;
    then (g+f).(x/2+1/2) = f.(2*(x/2+1/2)-1) by Z0,Z1,a8,BORSUK_2:def 5;
    hence y in rng(g+f) by a4,a2,a6,FUNCT_1:def 5;
  end;
  
theorem
  for T being arcwise_connected non empty TopSpace, a, b, c being Point of T
  for f being Path of b,c, g being Path of a,b holds
  rng f c= rng(g+f)
  proof 
    let T be arcwise_connected non empty TopSpace;
    let a, b, c be Point of T;
    let f be Path of b,c;
    let g be Path of a,b;
    a,b are_connected & b,c are_connected by BORSUK_2:def 3;
    hence thesis by Trc2;    
  end;
  
theorem Trc0:
  for T being non empty TopSpace, a, b, c being Point of T
  for f being Path of a,b, g being Path of b,c st
  a,b are_connected & b,c are_connected holds
  rng(f+g) = rng f \/ rng g
  proof 
    let T be non empty TopSpace;
    let a, b, c be Point of T;
    let f be Path of a,b;
    let g be Path of b,c;
    assume that
Z0: a,b are_connected and
Z1: b,c are_connected;
    thus rng(f+g) c= rng f \/ rng g
    proof
      let y be set;
      assume y in rng(f+g);
      then consider x being set such that
a1:   x in dom(f+g) and
a2:   y = (f+g).x by FUNCT_1:def 5;
      reconsider x as Point of I[01] by a1;
      per cases;
      suppose
a6:     x <= 1/2;
        then
a5:     (f+g).x = f.(2*x) by Z0,Z1,BORSUK_2:def 5;
a3:     rng f c= rng f \/ rng g by XBOOLE_1:7;
a4:     dom f = the carrier of I[01] by FUNCT_2:def 1;
        2*x is Point of I[01] by a6,BORSUK_6:6;
        then y in rng f by a2,a4,a5,FUNCT_1:def 5;
        hence thesis by a3;
      end;
      suppose
a6:     1/2 <= x;
        then
a5:     (f+g).x = g.(2*x-1) by Z0,Z1,BORSUK_2:def 5;
a3:     rng g c= rng f \/ rng g by XBOOLE_1:7;
a4:     dom g = the carrier of I[01] by FUNCT_2:def 1;
        2*x-1 is Point of I[01] by a6,BORSUK_6:7;
        then y in rng g by a2,a4,a5,FUNCT_1:def 5;
        hence thesis by a3;
      end;
    end;
    rng f c= rng(f+g) & rng g c= rng(f+g) by Z0,Z1,Trc1,Trc2;
    hence thesis by XBOOLE_1:8;
  end;
  
theorem
  for T being arcwise_connected non empty TopSpace, a, b, c being Point of T
  for f being Path of a,b, g being Path of b,c holds
  rng(f+g) = rng f \/ rng g
  proof 
    let T be arcwise_connected non empty TopSpace;
    let a, b, c be Point of T;
    let f be Path of a,b;
    let g be Path of b,c;
    a,b are_connected & b,c are_connected by BORSUK_2:def 3;
    hence thesis by Trc0;
  end;
  
theorem Trc30:
  for T being non empty TopSpace, a, b, c, d being Point of T
  for f being Path of a,b, g being Path of b,c, h being Path of c,d st
  a,b are_connected & b,c are_connected & c,d are_connected holds
  rng(f+g+h) = rng f \/ rng g \/ rng h
  proof 
    let T be non empty TopSpace;
    let a, b, c, d be Point of T;
    let f be Path of a,b;
    let g be Path of b,c;
    let h be Path of c,d;
    assume that
Z0: a,b are_connected and
Z1: b,c are_connected and
Z2: c,d are_connected;
    a,c are_connected by Z0,Z1,BORSUK_6:46;
    hence rng(f+g+h) = rng(f+g) \/ rng h by Z2,Trc0
    .= rng f \/ rng g \/ rng h by Z0,Z1,Trc0;
  end;
  
theorem Trc31:
  for T being arcwise_connected non empty TopSpace, a, b, c, d being Point of T
  for f being Path of a,b, g being Path of b,c, h being Path of c,d
  holds rng(f+g+h) = rng f \/ rng g \/ rng h
  proof 
    let T be arcwise_connected non empty TopSpace;
    let a, b, c, d be Point of T;
    let f be Path of a,b;
    let g be Path of b,c;
    let h be Path of c,d;
    a,b are_connected & b,c are_connected & c,d are_connected
    by BORSUK_2:def 3;
    hence thesis by Trc30;
  end;

Trc32:
  for T being non empty TopSpace, a, b, c, d, e being Point of T
  for f being Path of a,b, g being Path of b,c,
  h being Path of c,d, i being Path of d,e st
  a,b are_connected & b,c are_connected &
  c,d are_connected & d,e are_connected holds
  rng(f+g+h+i) = rng f \/ rng g \/ rng h \/ rng i
  proof 
    let T be non empty TopSpace;
    let a, b, c, d, e be Point of T;
    let f be Path of a,b;
    let g be Path of b,c;
    let h be Path of c,d;
    let i be Path of d,e;
    assume that
Z0: a,b are_connected and
Z1: b,c are_connected and
Z2: c,d are_connected and
Z3: d,e are_connected;
    a,c are_connected by Z0,Z1,BORSUK_6:46;
    then a,d are_connected by Z2,BORSUK_6:46;
    hence rng(f+g+h+i) = rng(f+g+h) \/ rng i by Z3,Trc0
    .= rng f \/ rng g \/ rng h \/ rng i by Z0,Z1,Z2,Trc30;
  end;
  
Trc33:
  for T being arcwise_connected non empty TopSpace,
  a, b, c, d, e being Point of T
  for f being Path of a,b, g being Path of b,c,
  h being Path of c,d, i being Path of d,e
  holds rng(f+g+h+i) = rng f \/ rng g \/ rng h \/ rng i
  proof 
    let T be arcwise_connected non empty TopSpace;
    let a, b, c, d, e be Point of T;
    let f be Path of a,b;
    let g be Path of b,c;
    let h be Path of c,d;
    let i be Path of d,e;
    a,b are_connected & b,c are_connected &
    c,d are_connected & d,e are_connected by BORSUK_2:def 3;
    hence thesis by Trc32;
  end;

Trc34:
  for T being non empty TopSpace, a, b, c, d, e, z being Point of T
  for f being Path of a,b, g being Path of b,c,
  h being Path of c,d, i being Path of d,e, j being Path of e,z st
  a,b are_connected & b,c are_connected & c,d are_connected &
  d,e are_connected & e,z are_connected holds
  rng(f+g+h+i+j) = rng f \/ rng g \/ rng h \/ rng i \/ rng j
  proof 
    let T be non empty TopSpace;
    let a, b, c, d, e, z be Point of T;
    let f be Path of a,b;
    let g be Path of b,c;
    let h be Path of c,d;
    let i be Path of d,e;
    let j be Path of e,z;
    assume that
Z0: a,b are_connected and
Z1: b,c are_connected and
Z2: c,d are_connected and
Z3: d,e are_connected and
Z4: e,z are_connected;
    a,c are_connected by Z0,Z1,BORSUK_6:46;
    then a,d are_connected by Z2,BORSUK_6:46;
    then a,e are_connected by Z3,BORSUK_6:46;
    hence rng(f+g+h+i+j) = rng(f+g+h+i) \/ rng j by Z4,Trc0
    .= rng f \/ rng g \/ rng h \/ rng i \/ rng j by Z0,Z1,Z2,Z3,Trc32;
  end;
  
Trc35:
  for T being arcwise_connected non empty TopSpace,
  a, b, c, d, e, z being Point of T
  for f being Path of a,b, g being Path of b,c,
  h being Path of c,d, i being Path of d,e, j being Path of e,z
  holds rng(f+g+h+i+j) = rng f \/ rng g \/ rng h \/ rng i \/ rng j
  proof 
    let T be arcwise_connected non empty TopSpace;
    let a, b, c, d, e, z be Point of T;
    let f be Path of a,b;
    let g be Path of b,c;
    let h be Path of c,d;
    let i be Path of d,e;
    let j be Path of e,z;
    a,b are_connected & b,c are_connected & c,d are_connected &
    d,e are_connected & e,z are_connected by BORSUK_2:def 3;
    hence thesis by Trc34;
  end;

theorem Tloop:
  for T being non empty TopSpace, a being Point of T holds
  I[01] --> a is Path of a,a
  proof 
    let T be non empty TopSpace, a be Point of T;
    set f = I[01] --> a;
    thus a,a are_connected;
    thus thesis by TOPALG_3:5,BORSUK_1:def 17,def 18;
  end;
  
theorem defa2:
  for p1, p2 being Point of TOP-REAL n, P being Subset of TOP-REAL n
  holds P is_an_arc_of p1,p2 implies
  ex F being Path of p1,p2, f being map of I[01], (TOP-REAL n)|P st
  rng f = P & F = f
  proof 
    let p1, p2 be Point of TOP-REAL n,
    P be Subset of TOP-REAL n;
    assume
a1: P is_an_arc_of p1,p2;
    then reconsider P1 = P as non empty Subset of TOP-REAL n by TOPREAL1:4;
    consider h being map of I[01], (TOP-REAL n)|P such that
a2: h is_homeomorphism and
a3: h.0 = p1 & h.1 = p2 by a1,TOPREAL1:def 2;
    h is map of I[01], (TOP-REAL n)|P1;
    then reconsider h1 = h as map of I[01],TOP-REAL n by TOPREALA:28;
    h1 is Path of p1,p2
    proof
      h is continuous by a2,TOPS_2:def 5;
      hence h1 is continuous & h1.0 = p1 & h1.1 = p2 by a3,TOPMETR:7;
    end;
    then reconsider f = h as Path of p1,p2;
    take f, h;
    thus rng h = [#]((TOP-REAL n)|P) by a2,TOPS_2:def 5
    .= the carrier of (TOP-REAL n)|P by PRE_TOPC:12
    .= P by JORDAN1:1;
    thus thesis;
  end;

theorem defs2:
  for p1, p2 being Point of TOP-REAL n
  ex F being Path of p1,p2, f being map of I[01], (TOP-REAL n)|LSeg(p1,p2) st
  rng f = LSeg(p1,p2) & F = f
  proof 
    let p1, p2 be Point of TOP-REAL n;
    per cases;
    suppose
a1:   p1 = p2;
      then reconsider g = I[01] --> p1 as Path of p1,p2 by Tloop;
      take g;
a6:   LSeg(p1,p2) = {p1} by a1,TOPREAL1:7;
      g = (the carrier of I[01]) --> p1 by BORSUK_1:def 3;
      then
a5:   rng g = {p1} by  FUNCOP_1:14;
      the carrier of (TOP-REAL n)|LSeg(p1,p2) = LSeg(p1,p2)
      by JORDAN1:1;
      then reconsider f = g as map of I[01],(TOP-REAL n)|LSeg(p1,p2)
      by a6,a5,FUNCT_2:8;
      take f;
      thus thesis by a5,a1,TOPREAL1:7;
    end;
    suppose p1 <> p2;
      then LSeg(p1,p2) is_an_arc_of p1,p2 by TOPREAL1:15;
      hence thesis by defa2;
    end;
  end;

theorem JORDAN1819:
  for p1,p2,q1,q2 being Point of TOP-REAL 2
  st P is_an_arc_of p1,p2 & q1 in P & q2 in P &
  q1 <> p1 & q1 <> p2 & q2 <> p1 & q2 <> p2
  ex f being Path of q1,q2 st rng f c= P & rng f misses {p1,p2} 
  proof 
    let p1,p2,q1,q2 be Point of TOP-REAL 2 such that
a1: P is_an_arc_of p1,p2 and
a2: q1 in P & q2 in P and
a3: q1 <> p1 & q1 <> p2 & q2 <> p1 & q2 <> p2;
    per cases;
    suppose q1 = q2;
      then reconsider f = I[01] --> q1 as Path of q1,q2 by Tloop;
      take f;
      f = (the carrier of I[01]) --> q1 by BORSUK_1:def 3;
      then
a5:   rng f = {q1} by  FUNCOP_1:14;
      thus rng f c= P
      proof
        let x be set;
        assume x in rng f;
        hence thesis by a2,a5,TARSKI:def 1;
      end;
      not p1 in {q1} & not p2 in {q1} by a3,TARSKI:def 1;
      hence thesis by a5,ZFMISC_1:57;
    end;
    suppose q1 <> q2;
      then consider Q being non empty Subset of T2 such that
a4:   Q is_an_arc_of q1,q2 and
a5:   Q c= P & Q misses {p1,p2} by a1,a2,a3,JORDAN18:19;
      consider g being Path of q1,q2, f being map of I[01], T2|Q such that
a6:   rng f = Q & g = f by a4,defa2;
      reconsider h = f as map of I[01],T2 by TOPREALA:28;
      the carrier of T2|Q = Q by JORDAN1:1;
      then reconsider z1 = q1, z2 = q2 as Point of T2|Q by a4,TOPREAL1:4;
a7:   z1,z2 are_connected
      proof
        take f;
        thus f is continuous by a6,TOPMETR:8;
        thus f.0 = z1 & f.1 = z2 by a6,BORSUK_2:def 4;
      end;
      f is Path of z1,z2
      proof
        thus z1,z2 are_connected by a7;
        thus f is continuous by a6,TOPMETR:8;
        thus f.0 = z1 & f.1 = z2 by a6,BORSUK_2:def 4;
      end;
      then reconsider h as Path of q1,q2 by a7,TOPALG_2:1;
      take h;
      thus thesis by a5,a6;
    end;
  end;
  
begin :: Rectangles

theorem ThdRR:
  a <= b & c <= d implies
  rectangle(a,b,c,d) c= closed_inside_of_rectangle(a,b,c,d)
  proof 
    assume
a1: a <= b & c <= d;
    let x be set;
    assume x in rectangle(a,b,c,d);
    then x in {p: p`1 = a & p`2 <= d & p`2 >= c or
    p`1 <= b & p`1 >= a & p`2 = d or
    p`1 <= b & p`1 >= a & p`2 = c or
    p`1 = b & p`2 <= d & p`2 >= c} by a1,SPPOL_2:58;
    then
a2: ex p st x = p & (p`1 = a & p`2 <= d & p`2 >= c or
    p`1 <= b & p`1 >= a & p`2 = d or
    p`1 <= b & p`1 >= a & p`2 = c or
    p`1 = b & p`2 <= d & p`2 >= c);
    closed_inside_of_rectangle(a,b,c,d) =
    {p: a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d} by JGRAPH_6:def 2;
    hence thesis by a1,a2;
  end;

theorem ThdRRa:
  inside_of_rectangle(a,b,c,d) c= closed_inside_of_rectangle(a,b,c,d)
  proof 
    let x be set;
    assume x in inside_of_rectangle(a,b,c,d);
    then x in {p: a < p`1 & p`1 < b & c < p`2 & p`2 < d} by JGRAPH_6:def 1;
    then
a2: ex p st x = p & (a < p`1 & p`1 < b & c < p`2 & p`2 < d);
    closed_inside_of_rectangle(a,b,c,d) =
    {p: a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d} by JGRAPH_6:def 2;
    hence thesis by a2;
  end;

theorem Th80:
  closed_inside_of_rectangle(a,b,c,d) = outside_of_rectangle(a,b,c,d)`
  proof 
    set R = closed_inside_of_rectangle(a,b,c,d);
    set O = outside_of_rectangle(a,b,c,d);
a2: R = {p: a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d} by JGRAPH_6:def 2;
a3: O = {p: not(a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d)} by JGRAPH_6:def 3;
    thus R c= O`
    proof
      let x be set;
      assume x in R;
      then consider p such that
a6:   x = p and
a7:   a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d by a2;
      now
        assume p in O;
        then ex p1 st p1 = p &
        not(a <= p1`1 & p1`1 <= b & c <= p1`2 & p1`2 <= d) by a3;
        hence contradiction by a7;
      end;
      hence thesis by a6,SUBSET_1:50;
    end;
    let x be set;
    assume
a5: x in O`;
    then
a4: not x in O by SUBSET_1:54;
    reconsider x as Point of T2 by a5;
    a <= x`1 & x`1 <= b & c <= x`2 & x`2 <= d by a3,a4;
    hence thesis by a2;
  end;
   
registration
  let a, b, c, d be real number;
  cluster closed_inside_of_rectangle(a,b,c,d) -> closed;
  coherence
  proof 
    set P2 = outside_of_rectangle(a,b,c,d);
a5: a is Real & b is Real & c is Real & d is Real by XREAL_0:def 1;
    P2 = {pb where pb is Point of T2:
    not(a <= pb`1 & pb`1 <= b & c <= pb`2 & pb`2 <= d)} by JGRAPH_6:def 3;
    then reconsider P2 as open Subset of T2 by a5,JORDAN1:39;
    P2` is closed;
    hence thesis by Th80;
  end;
end;

theorem TRmissesP2:
  closed_inside_of_rectangle(a,b,c,d) misses outside_of_rectangle(a,b,c,d)
  proof 
    set P = rectangle(a,b,c,d);
    set R = closed_inside_of_rectangle(a,b,c,d);
    set P1 = inside_of_rectangle(a,b,c,d);
    set P2 = outside_of_rectangle(a,b,c,d);
a2: R = {p: a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d} by JGRAPH_6:def 2;
a4: P2 = {p: not (a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d)}
    by JGRAPH_6:def 3;
    assume R meets P2;
    then consider x being set such that
a5: x in R and
a6: x in P2 by XBOOLE_0:3;
a7: ex p st x = p & a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d by a2,a5;
    ex p st x = p & not (a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d) by a4,a6;
    hence thesis by a7;
  end;
  
theorem TRP2:
  closed_inside_of_rectangle(a,b,c,d) /\ inside_of_rectangle(a,b,c,d)
  = inside_of_rectangle(a,b,c,d)
  proof 
    set R = closed_inside_of_rectangle(a,b,c,d);
    set P1 = inside_of_rectangle(a,b,c,d);
    thus R /\ P1 c= P1 by XBOOLE_1:17;
    P1 c= R by ThdRRa;
    then P1 /\ P1 c= P1 /\ R by XBOOLE_1:26;
    hence thesis;
  end;
  
theorem TRecInt:
  a < b & c < d implies
  Int closed_inside_of_rectangle(a,b,c,d) = inside_of_rectangle(a,b,c,d)
  proof 
    assume
a0: a < b & c < d;
    set P = rectangle(a,b,c,d);
    set R = closed_inside_of_rectangle(a,b,c,d);
    set P1 = inside_of_rectangle(a,b,c,d);
    set P2 = outside_of_rectangle(a,b,c,d);
a1: a is Real & b is Real & c is Real & d is Real by XREAL_0:def 1;
a3: P1 = {p: a < p`1 & p`1 < b & c < p`2 & p`2 < d} by JGRAPH_6:def 1;
a4: P2 = {p: not (a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d)}
    by JGRAPH_6:def 3;
a5: P = {p where p is Point of T2: p`1 = a & p`2 <= d & p`2 >= c or
    p`1 <= b & p`1 >= a & p`2 = d or
    p`1 <= b & p`1 >= a & p`2 = c or
    p`1 = b & p`2 <= d & p`2 >= c} by a0,SPPOL_2:58;
a6: R misses P2 by TRmissesP2;
    thus Int R = (Cl R`)` by TOPS_1:def 1
    .= (Cl P2``)` by Th80
    .= (P2 \/ P)` by a0,a1,a4,a5,JORDAN1:49
    .= P2` /\ P` by SUBSET_1:29
    .= R /\ P` by Th80
    .= R /\ (P1 \/ P2) by a0,a1,a3,a4,a5,JORDAN1:41
    .= R /\ P1 \/ R /\ P2 by XBOOLE_1:23
    .= R /\ P1 \/ {} by a6,XBOOLE_0:def 7
    .= P1 by TRP2;
  end;
  
theorem Th81:
  a <= b & c <= d implies
  closed_inside_of_rectangle(a,b,c,d) \ inside_of_rectangle(a,b,c,d)
  = rectangle(a,b,c,d)
  proof 
    assume
a0: a <= b & c <= d;
    set R = rectangle(a,b,c,d);
    set P = closed_inside_of_rectangle(a,b,c,d);
    set P1 = inside_of_rectangle(a,b,c,d);
    set P2 = outside_of_rectangle(a,b,c,d);
a2: P = {p: a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d} by JGRAPH_6:def 2;
a3: P1 = {p: a < p`1 & p`1 < b & c < p`2 & p`2 < d} by JGRAPH_6:def 1;
a5: R = {p where p is Point of T2: p`1 = a & p`2 <= d & p`2 >= c or
    p`1 <= b & p`1 >= a & p`2 = d or
    p`1 <= b & p`1 >= a & p`2 = c or
    p`1 = b & p`2 <= d & p`2 >= c} by a0,SPPOL_2:58;
    thus P \ P1 c= R
    proof
      let x be set;
      assume
a8:   x in P \ P1;
      then
a9:   not x in P1 by XBOOLE_0:def 4;
      x in P by a8,XBOOLE_0:def 4;
      then consider p such that
a6:   x = p and
a7:   a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d by a2;
      not (a < p`1 & p`1 < b & c < p`2 & p`2 < d) by a3,a6,a9;
      then p`1 = a & p`2 <= d & p`2 >= c or
      p`1 <= b & p`1 >= a & p`2 = d or
      p`1 <= b & p`1 >= a & p`2 = c or
      p`1 = b & p`2 <= d & p`2 >= c by a7,XREAL_1:1;
      hence thesis by a6,a5;
    end;
    let x be set;
    assume
b1: x in R;
    then
b3: ex p st p = x & (p`1 = a & p`2 <= d & p`2 >= c or
    p`1 <= b & p`1 >= a & p`2 = d or
    p`1 <= b & p`1 >= a & p`2 = c or
    p`1 = b & p`2 <= d & p`2 >= c) by a5;
b2: R c= P by a0,ThdRR;
    now
      assume x in P1;
      then ex p st x = p & a < p`1 & p`1 < b & c < p`2 & p`2 < d by a3;
      hence contradiction by b3;
    end;
    hence thesis by b1,b2,XBOOLE_0:def 4;
  end;

theorem TRecFr:
  a < b & c < d implies
  Fr closed_inside_of_rectangle(a,b,c,d) = rectangle(a,b,c,d)
  proof 
    assume
a0: a < b & c < d;
    set P = closed_inside_of_rectangle(a,b,c,d);
    thus Fr P = P \ Int P by TOPS_1:77
    .= P \ inside_of_rectangle(a,b,c,d) by a0,TRecInt
    .= rectangle(a,b,c,d) by a0,Th81;
  end;
  
theorem
  a <= b & c <= d implies W-bound closed_inside_of_rectangle(a,b,c,d) = a
  proof 
    assume
A1: a <= b & c <= d;
    set X = closed_inside_of_rectangle(a,b,c,d);
    reconsider Z = (proj1||X).:the carrier of (T2|X)
    as Subset of REAL;
A2: X = the carrier of (T2|X) by JORDAN1:1;
a2: X = {p1: a <= p1`1 & p1`1 <= b & c <= p1`2 & p1`2 <= d}
    by JGRAPH_6:def 2;
b1: |[a,c]| in X by A1,TOPREALA:52;
A3: for p be real number st p in Z holds p >= a
    proof
      let p be real number;
      assume p in Z;
      then consider p0 being set such that
A4:   p0 in the carrier of T2|X and
      p0 in the carrier of T2|X and
A5:   p = (proj1||X).p0 by FUNCT_2:115;
      ex p1 st p0 = p1 &
      a <= p1`1 & p1`1 <= b & c <= p1`2 & p1`2 <= d by a2,A2,A4;
      hence thesis by A2,A4,A5,PSCOMP_1:69;
    end;
A6: for q being real number st
    for p being real number st p in Z holds p >= q holds a >= q
    proof
      let q be real number such that
A7:   for p being real number st p in Z holds p >= q;
a3:   |[a,c]|`1 = a & |[a,c]|`2 = c by EUCLID:56;
      (proj1||X). |[a,c]| = |[a,c]|`1 by b1,PSCOMP_1:69;
      then a in Z by A2,b1,a3,FUNCT_2:43;
      hence thesis by A7;
    end;
    dom(proj1||X) = X by A2,FUNCT_2:def 1;
    then
a1: Z is non empty by A2,b1,RELAT_1:152;
    thus W-bound X = inf (proj1||X) by PSCOMP_1:def 30
    .= inf Z by PSCOMP_1:def 20
    .= a by A3,A6,a1,SEQ_4:61;
  end;
  
theorem
  a <= b & c <= d implies S-bound closed_inside_of_rectangle(a,b,c,d) = c
  proof 
    assume
A1: a <= b & c <= d;
    set X = closed_inside_of_rectangle(a,b,c,d);
    reconsider Z = (proj2||X).:the carrier of (T2|X)
    as Subset of REAL;
A2: X = the carrier of (T2|X) by JORDAN1:1;
a2: X = {p1: a <= p1`1 & p1`1 <= b & c <= p1`2 & p1`2 <= d}
    by JGRAPH_6:def 2;
b1: |[a,c]| in X by A1,TOPREALA:52;
A3: for p be real number st p in Z holds p >= c
    proof
      let p be real number;
      assume p in Z;
      then consider p0 being set such that
A4:   p0 in the carrier of T2|X and
      p0 in the carrier of T2|X and
A5:   p = (proj2||X).p0 by FUNCT_2:115;
      ex p1 st p0 = p1 &
      a <= p1`1 & p1`1 <= b & c <= p1`2 & p1`2 <= d by a2,A2,A4;
      hence thesis by A2,A4,A5,PSCOMP_1:70;
    end;
A6: for q being real number st
    for p being real number st p in Z holds p >= q holds c >= q
    proof
      let q be real number such that
A7:   for p being real number st p in Z holds p >= q;
a3:   |[a,c]|`1 = a & |[a,c]|`2 = c by EUCLID:56;
      (proj2||X). |[a,c]| = |[a,c]|`2 by b1,PSCOMP_1:70;
      then c in Z by A2,b1,a3,FUNCT_2:43;
      hence thesis by A7;
    end;
    dom(proj2||X) = X by A2,FUNCT_2:def 1;
    then
a1: Z is non empty by A2,b1,RELAT_1:152;
    thus S-bound X = inf (proj2||X) by PSCOMP_1:def 33
    .= inf Z by PSCOMP_1:def 20
    .= c by A3,A6,a1,SEQ_4:61;
  end;

theorem
  a <= b & c <= d implies E-bound closed_inside_of_rectangle(a,b,c,d) = b
  proof 
    assume
A1: a <= b & c <= d;
    set X = closed_inside_of_rectangle(a,b,c,d);
    reconsider Z = (proj1||X).:the carrier of (T2|X)
    as Subset of REAL;
A2: X = the carrier of (T2|X) by JORDAN1:1;
a2: X = {p1: a <= p1`1 & p1`1 <= b & c <= p1`2 & p1`2 <= d}
    by JGRAPH_6:def 2;
A3: for p be real number st p in Z holds p <= b
    proof
      let p be real number;
      assume p in Z;
      then consider p0 being set such that
A4:   p0 in the carrier of T2|X and
      p0 in the carrier of T2|X and
A5:   p = (proj1||X).p0 by FUNCT_2:115;
      ex p1 st p0 = p1 &
      a <= p1`1 & p1`1 <= b & c <= p1`2 & p1`2 <= d by a2,A2,A4;
      hence thesis by A2,A4,A5,PSCOMP_1:69;
    end;
A6: for q being real number st
    for p being real number st p in Z holds p <= q holds b <= q
    proof
      let q be real number such that
A7:   for p being real number st p in Z holds p <= q;
a3:   |[b,d]|`1 = b & |[b,d]|`2 = d by EUCLID:56;
      then
A9:   |[b,d]| in X by a2,A1;
      then (proj1||X). |[b,d]| = |[b,d]|`1 by PSCOMP_1:69;
      then b in Z by A2,A9,a3,FUNCT_2:43;
      hence thesis by A7;
    end;
b1: |[a,c]| in X by A1,TOPREALA:52;
    dom(proj1||X) = X by A2,FUNCT_2:def 1;
    then
a1: Z is non empty by A2,b1,RELAT_1:152;
    thus E-bound X = sup (proj1||X) by PSCOMP_1:def 32
    .= sup Z by PSCOMP_1:def 21
    .= b by A3,A6,a1,SEQ_4:63;
  end;

theorem
  a <= b & c <= d implies N-bound closed_inside_of_rectangle(a,b,c,d) = d
  proof 
    assume
A1: a <= b & c <= d;
    set X = closed_inside_of_rectangle(a,b,c,d);
    reconsider Z = (proj2||X).:the carrier of (T2|X)
    as Subset of REAL;
A2: X = the carrier of (T2|X) by JORDAN1:1;
a2: X = {p1: a <= p1`1 & p1`1 <= b & c <= p1`2 & p1`2 <= d}
    by JGRAPH_6:def 2;
A3: for p be real number st p in Z holds p <= d
    proof
      let p be real number;
      assume p in Z;
      then consider p0 being set such that
A4:   p0 in the carrier of T2|X and
      p0 in the carrier of T2|X and
A5:   p = (proj2||X).p0 by FUNCT_2:115;
      ex p1 st p0 = p1 &
      a <= p1`1 & p1`1 <= b & c <= p1`2 & p1`2 <= d by a2,A2,A4;
      hence thesis by A2,A4,A5,PSCOMP_1:70;
    end;
A6: for q being real number st
    for p being real number st p in Z holds p <= q holds d <= q
    proof
      let q be real number such that
A7:   for p being real number st p in Z holds p <= q;
a3:   |[b,d]|`1 = b & |[b,d]|`2 = d by EUCLID:56;
      then
A9:   |[b,d]| in X by a2,A1;
      then (proj2||X). |[b,d]| = |[b,d]|`2 by PSCOMP_1:70;
      then d in Z by A2,A9,a3,FUNCT_2:43;
      hence thesis by A7;
    end;
b1: |[a,c]| in X by A1,TOPREALA:52;
    dom(proj2||X) = X by A2,FUNCT_2:def 1;
    then
a1: Z is non empty by A2,b1,RELAT_1:152;
    thus N-bound X = sup (proj2||X) by PSCOMP_1:def 31
    .= sup Z by PSCOMP_1:def 21
    .= d by A3,A6,a1,SEQ_4:63;
  end;

theorem TSegZaw:
  a < b & c < d &
  p1 in closed_inside_of_rectangle(a,b,c,d) &
  not p2 in closed_inside_of_rectangle(a,b,c,d) &
  P is_an_arc_of p1,p2 implies
  Segment(P,p1,p2,p1,First_Point(P,p1,p2,rectangle(a,b,c,d))) c=
  closed_inside_of_rectangle(a,b,c,d)
  proof 
    set R = closed_inside_of_rectangle(a,b,c,d);
    set dR = rectangle(a,b,c,d);
    set n = First_Point(P,p1,p2,dR);
    assume that
a0: a < b & c < d and
Z0: p1 in R and
Z1: not p2 in R and
Z2: P is_an_arc_of p1,p2;
    let x be set;
    assume that
a1: x in Segment(P,p1,p2,p1,n) and
a2: not x in R;
    reconsider x as Point of T2 by a1;
a12: Fr R = dR by a0,TRecFr;
a30: P is connected by Z2,JORDAN6:11;
     p1 in P by Z2,TOPREAL1:4;
     then
a31: P meets R by Z0,XBOOLE_0:3;
     p2 in P by Z2,TOPREAL1:4;
     then P \ R <> {}T2 by Z1,XBOOLE_0:def 4;
     then
a7:  P meets dR by a12,a30,a31,CONNSP_1:23;
     P is closed by Z2,JORDAN6:12;
     then
a8:  P /\ dR is closed by TOPS_1:35;
     then
nn:  n in P /\ dR by a7,Z2,JORDAN5C:def 1;
     per cases;
     suppose x = n;
       then
er:    x in dR by nn,XBOOLE_0:def 3;
       dR c= R by a0,ThdRR;
       hence thesis by er,a2;
     end;
    suppose
ww:   x <> n;
      reconsider P as non empty Subset of T2 by Z2,TOPREAL1:4;
      consider f being map of I[01], T2|P such that
a3:   f is_homeomorphism and
a4:   f.0 = p1 & f.1 = p2 by Z2,TOPREAL1:def 2;
rr:   rng f = [#](T2|P) by a3,TOPS_2:def 5
      .= P by PRE_TOPC:def 10;
      n in P by nn,XBOOLE_0:def 3;
      then consider na being set such that
a43:  na in dom f and
a5:   f.na = n by rr,FUNCT_1:def 5;
      na is real by a43,MEMBERED:def 2;
      then reconsider na as Real by XREAL_0:def 1;
a6:   0 <= na & na <= 1 by a43,JORDAN5A:2;
a25:  Segment(P,p1,p2,p1,n) c= P by JORDAN16:5;
      then consider xa being set such that
a41:  xa in dom f and
a10:  f.xa = x by rr,a1,FUNCT_1:def 5;
      xa is real by a41,MEMBERED:def 2;
      then reconsider xa as Real by XREAL_0:def 1;
a11:  0 <= xa & xa <= 1 by a41,JORDAN5A:2;
a22:  Segment(P,p1,p2,p1,x) is_an_arc_of p1,x by Z2,a25,Z0,a2,a1,JORDAN20:20;
      then
a13:  Segment(P,p1,p2,p1,x) is connected by JORDAN6:11;
      p1 in Segment(P,p1,p2,p1,x) by a22,TOPREAL1:4;
      then
a14:  Segment(P,p1,p2,p1,x) meets R by Z0,XBOOLE_0:3;
      x in Segment(P,p1,p2,p1,x) by a22,TOPREAL1:4;
      then Segment(P,p1,p2,p1,x) \ R <> {}T2 by a2,XBOOLE_0:def 4;
      then Segment(P,p1,p2,p1,x) meets Fr R by a13,a14,CONNSP_1:23;
      then consider z being set such that
a15:  z in Segment(P,p1,p2,p1,x) and
a16:  z in dR by a12,XBOOLE_0:3;
      reconsider z as Point of T2 by a15;
      Segment(P,p1,p2,p1,x) = {p: LE p1,p,P,p1,p2 & LE p,x,P,p1,p2}
      by JORDAN6:29;
      then consider zz being Point of T2 such that
a31:  zz = z and
      LE p1,zz,P,p1,p2 and
a33:  LE zz,x,P,p1,p2 by a15;
      Segment(P,p1,p2,p1,x) c= P by JORDAN16:5;
      then consider za being set such that
a50:  za in dom f and
a17:  f.za = z by rr,a15,FUNCT_1:def 5;
      za is real by a50,MEMBERED:def 2;
      then reconsider za as Real by XREAL_0:def 1;
a18:  0 <= za & za <= 1 by a50,JORDAN5A:2;
a21:  na <= za by a16,a17,a18,a5,a6,a7,a8,a3,a4,Z2,JORDAN5C:def 1;
a20:  za <= xa by a3,a4,a33,a31,a17,a18,a10,a11,JORDAN5C:def 3;
      Segment(P,p1,p2,p1,n) = {p: LE p1,p,P,p1,p2 & LE p,n,P,p1,p2}
      by JORDAN6:29;
      then consider xx being Point of T2 such that
a34:  xx = x and
      LE p1,xx,P,p1,p2 and
a36:  LE xx,n,P,p1,p2 by a1;
      xa <= na by a3,a4,a36,a34,a10,a11,a5,a6,JORDAN5C:def 3;
      then xa < na by ww,a5,a10,REAL_1:def 5;
      hence thesis by a21,a20,XREAL_1:2;
    end;
  end;

begin :: Some useful functions

definition
  let S, T be non empty TopSpace,
  x be Point of [:S,T:];
  redefine func x`1 -> Element of S;
  coherence
  proof 
    the carrier of [:S,T:] = [:the carrier of S, the carrier of T:]
    by BORSUK_1:def 5;
    hence thesis by MCART_1:10;
  end;
  redefine func x`2 -> Element of T;
  coherence
  proof 
    the carrier of [:S,T:] = [:the carrier of S, the carrier of T:]
    by BORSUK_1:def 5;
    hence thesis by MCART_1:10;
  end;
end;

definition
  let o be Point of TOP-REAL 2;
  func diffX2_1(o) -> RealMap of [:TOP-REAL 2,TOP-REAL 2:] means :dX21:
  for x being Point of [:TOP-REAL 2,TOP-REAL 2:] holds it.x = x`2`1 - o`1;
  existence
  proof 
    deffunc F(Point of [:T2,T2:]) = $1`2`1 - o`1;
    consider xo being RealMap of [:T2,T2:] such that
u1: for x being Point of [:T2,T2:] holds xo.x = F(x) from FUNCT_2:sch 4;
    take xo;
    thus thesis by u1;
  end;
  uniqueness
  proof 
    let f, g be RealMap of [:T2,T2:] such that
a1: for x being Point of [:T2,T2:] holds f.x = x`2`1 - o`1 and
a2: for x being Point of [:T2,T2:] holds g.x = x`2`1 - o`1;
    now
      let x be Point of [:T2,T2:];
      thus f.x = x`2`1 - o`1 by a1
      .= g.x by a2;
    end;
    hence thesis by FUNCT_2:113;
  end;
  func diffX2_2(o) -> RealMap of [:TOP-REAL 2,TOP-REAL 2:] means :dX22:
  for x being Point of [:TOP-REAL 2,TOP-REAL 2:] holds it.x = x`2`2 - o`2;
  existence
  proof 
    deffunc F(Point of [:T2,T2:]) = $1`2`2 - o`2;
    consider xo being RealMap of [:T2,T2:] such that
u1: for x being Point of [:T2,T2:] holds xo.x = F(x) from FUNCT_2:sch 4;
    take xo;
    thus thesis by u1;
  end;
  uniqueness
  proof 
    let f, g be RealMap of [:T2,T2:] such that
a1: for x being Point of [:T2,T2:] holds f.x = x`2`2 - o`2 and
a2: for x being Point of [:T2,T2:] holds g.x = x`2`2 - o`2;
    now
      let x be Point of [:T2,T2:];
      thus f.x = x`2`2 - o`2 by a1
      .= g.x by a2;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

definition
  func diffX1_X2_1 -> RealMap of [:TOP-REAL 2, TOP-REAL 2:] means :dX1X21:
  for x being Point of [:TOP-REAL 2, TOP-REAL 2:] holds it.x = x`1`1 - x`2`1;
  existence
  proof 
    deffunc F(Point of [:T2,T2:]) = $1`1`1 - $1`2`1;
    consider xo being RealMap of [:T2,T2:] such that
u1: for x being Point of [:T2,T2:] holds xo.x = F(x) from FUNCT_2:sch 4;
    take xo;
    thus thesis by u1;
  end;
  uniqueness
  proof 
    let f, g be RealMap of [:T2,T2:] such that
a1: for x being Point of [:T2,T2:] holds f.x = x`1`1 - x`2`1 and
a2: for x being Point of [:T2,T2:] holds g.x = x`1`1 - x`2`1;
    now
      let x be Point of [:T2,T2:];
      thus f.x = x`1`1 - x`2`1 by a1
      .= g.x by a2;
    end;
    hence thesis by FUNCT_2:113;
  end;  
  func diffX1_X2_2 -> RealMap of [:TOP-REAL 2, TOP-REAL 2:] means :dX1X22:
  for x being Point of [:TOP-REAL 2, TOP-REAL 2:] holds it.x = x`1`2 - x`2`2;
  existence
  proof 
    deffunc F(Point of [:T2,T2:]) = $1`1`2 - $1`2`2;
    consider xo being RealMap of [:T2,T2:] such that
u1: for x being Point of [:T2,T2:] holds xo.x = F(x) from FUNCT_2:sch 4;
    take xo;
    thus thesis by u1;
  end;
  uniqueness
  proof 
    let f, g be RealMap of [:T2,T2:] such that
a1: for x being Point of [:T2,T2:] holds f.x = x`1`2 - x`2`2 and
a2: for x being Point of [:T2,T2:] holds g.x = x`1`2 - x`2`2;
    now
      let x be Point of [:T2,T2:];
      thus f.x = x`1`2 - x`2`2 by a1
      .= g.x by a2;
    end;
    hence thesis by FUNCT_2:113;
  end;  
  func Proj2_1 -> RealMap of [:TOP-REAL 2, TOP-REAL 2:] means :dP21:
  for x being Point of [:TOP-REAL 2, TOP-REAL 2:] holds it.x = x`2`1;
  existence
  proof 
    deffunc F(Point of [:T2,T2:]) = $1`2`1;
    consider xo being RealMap of [:T2,T2:] such that
u1: for x being Point of [:T2,T2:] holds xo.x = F(x) from FUNCT_2:sch 4;
    take xo;
    thus thesis by u1;
  end;
  uniqueness
  proof 
    let f, g be RealMap of [:T2,T2:] such that
a1: for x being Point of [:T2,T2:] holds f.x = x`2`1 and
a2: for x being Point of [:T2,T2:] holds g.x = x`2`1;
    now
      let x be Point of [:T2,T2:];
      thus f.x = x`2`1 by a1
      .= g.x by a2;
    end;
    hence thesis by FUNCT_2:113;
  end;  
  func Proj2_2 -> RealMap of [:TOP-REAL 2, TOP-REAL 2:] means :dP22:
  for x being Point of [:TOP-REAL 2, TOP-REAL 2:] holds it.x = x`2`2;
  existence
  proof 
    deffunc F(Point of [:T2,T2:]) = $1`2`2;
    consider xo being RealMap of [:T2,T2:] such that
u1: for x being Point of [:T2,T2:] holds xo.x = F(x) from FUNCT_2:sch 4;
    take xo;
    thus thesis by u1;
  end;
  uniqueness
  proof 
    let f, g be RealMap of [:T2,T2:] such that
a1: for x being Point of [:T2,T2:] holds f.x = x`2`2 and
a2: for x being Point of [:T2,T2:] holds g.x = x`2`2;
    now
      let x be Point of [:T2,T2:];
      thus f.x = x`2`2 by a1
      .= g.x by a2;
    end;
    hence thesis by FUNCT_2:113;
  end;  
end;

theorem TX21:
  for o being Point of TOP-REAL 2 holds
  diffX2_1(o) is continuous map of [:TOP-REAL 2, TOP-REAL 2:], R^1
  proof 
    let o be Point of TOP-REAL 2;
    reconsider Xo = diffX2_1(o) as map of [:T2,T2:],R^1 by TOPMETR:24;
  for p being Point of [:T2,T2:], V being Subset of R^1
   st Xo.p in V & V is open holds
    ex W being Subset of [:T2,T2:] st p in W & W is open & Xo.:W c= V
  proof
    let p be Point of [:T2,T2:],
        V be Subset of R^1 such that
a3:   Xo.p in V and
a4:   V is open;
a6:   Xo.p = p`2`1 - o`1 by dX21;
    set r = p`2`1 - o`1;
    reconsider V1 = V as open Subset of REAL by a4,TOPMETR:24,BORSUK_5:62;
    consider g being real number such that
a7:   0 < g and
a8:   ].r-g,r+g.[ c= V1 by a3,a6,RCOMP_1:40;
    reconsider g as Element of REAL by XREAL_0:def 1;
    set W2 = {|[x,y]| where x, y is Element of REAL:
    p`2`1-g < x & x < p`2`1+g};
    W2 c= the carrier of T2
    proof
      let a be set;
      assume a in W2;
      then ex x, y being Element of REAL st
        a = |[x,y]| & p`2`1-g < x & x < p`2`1+g;
      hence thesis;
    end;
    then reconsider W2 as Subset of T2;
    take [:[#]T2,W2:];
b1: p`2 = |[p`2`1,p`2`2]| by EUCLID:57;
ww: p = [p`1,p`2] by Lmcar,MCART_1:23;
    p`2`1-g < p`2`1-0 & p`2`1+0 < p`2`1+g by a7,REAL_1:92,XREAL_1:8;
    then
    p`2 in W2 by b1;
    hence p in [:[#]T2,W2:] by ww,Lm1,ZFMISC_1:def 2;
    W2 is open by PSCOMP_1:66;
    hence [:[#]T2,W2:] is open by BORSUK_1:46;
    let b be set;
    assume b in Xo.:[:[#]T2,W2:];
    then consider a being Point of [:T2,T2:] such that
a10:  a in [:[#]T2,W2:] and
a11:  Xo.a = b by FUNCT_2:116;
a12:  a = [a`1,a`2] by Lmcar,MCART_1:23;
a13:  (diffX2_1(o)).a = a`2`1 - o`1 by dX21;
    a`2 in W2 by a10,a12,ZFMISC_1:106;
    then consider x2, y2 being Element of REAL such that
a18:  a`2 = |[x2,y2]| and
a19:  p`2`1-g < x2 & x2 < p`2`1+g;
    a`2`1 = x2 by a18,EUCLID:56;
    then p`2`1 - g - o`1 < a`2`1 - o`1 & a`2`1 - o`1 < p`2`1 + g - o`1
      by a19,XREAL_1:11;
    then a`2`1 - o`1 in ].r-g,r+g.[ by RCOMP_1:47;
    hence b in V by a8,a11,a13;
  end;
  hence thesis by JGRAPH_2:20;
end;
  
theorem TX22:
  for o being Point of TOP-REAL 2 holds
  diffX2_2(o) is continuous map of [:TOP-REAL 2, TOP-REAL 2:], R^1
  proof 
    let o be Point of TOP-REAL 2;
    reconsider Yo = diffX2_2(o) as map of [:T2,T2:],R^1 by TOPMETR:24;
  for p being Point of [:T2,T2:], V being Subset of R^1
   st Yo.p in V & V is open holds
    ex W being Subset of [:T2,T2:] st p in W & W is open & Yo.:W c= V
  proof
    let p be Point of [:T2,T2:],
        V be Subset of R^1 such that
a3:   Yo.p in V and
a4:   V is open;
a5:   p = [p`1,p`2] by Lmcar,MCART_1:23;
a6:   Yo.p = p`2`2 - o`2 by dX22;
    set r = p`2`2 - o`2;
    reconsider V1 = V as open Subset of REAL by a4,TOPMETR:24,BORSUK_5:62;
    consider g being real number such that
a7:   0 < g and
a8:   ].r-g,r+g.[ c= V1 by a3,a6,RCOMP_1:40;
    reconsider g as Element of REAL by XREAL_0:def 1;
    set W2 = {|[x,y]| where x, y is Element of REAL:
    p`2`2-g < y & y < p`2`2+g};
    W2 c= the carrier of T2
    proof
      let a be set;
      assume a in W2;
      then ex x, y being Element of REAL st
        a = |[x,y]| & p`2`2-g < y & y < p`2`2+g;
      hence thesis;
    end;
    then reconsider W2 as Subset of T2;
    take [:[#]T2,W2:];
b1: p`2 = |[p`2`1,p`2`2]| by EUCLID:57;
    p`2`2-g < p`2`2-0 & p`2`2+0 < p`2`2+g by a7,REAL_1:92,XREAL_1:8;
    then
    p`2 in W2 by b1;
    hence p in [:[#]T2,W2:] by a5,Lm1,ZFMISC_1:def 2;
    W2 is open by PSCOMP_1:68;
    hence [:[#]T2,W2:] is open by BORSUK_1:46;
    let b be set;
    assume b in Yo.:[:[#]T2,W2:];
    then consider a being Point of [:T2,T2:] such that
a10:  a in [:[#]T2,W2:] and
a11:  Yo.a = b by FUNCT_2:116;
a12:  a = [a`1,a`2] by Lmcar,MCART_1:23;
a13:  (diffX2_2(o)).a = a`2`2 - o`2 by dX22;
    a`2 in W2 by a10,a12,ZFMISC_1:106;
    then consider x2, y2 being Element of REAL such that
a18:  a`2 = |[x2,y2]| and
a19:  p`2`2-g < y2 & y2 < p`2`2+g;
    a`2`2 = y2 by a18,EUCLID:56;
    then p`2`2 - g - o`2 < a`2`2 - o`2 & a`2`2 - o`2 < p`2`2 + g - o`2
    by a19,XREAL_1:11;
    then a`2`2 - o`2 in ].r-g,r+g.[ by RCOMP_1:47;
    hence b in V by a8,a11,a13;
  end;
  hence thesis by JGRAPH_2:20;
  end;

theorem TX1X21:
  diffX1_X2_1 is continuous map of [:TOP-REAL 2, TOP-REAL 2:], R^1
  proof 
    reconsider Dx = diffX1_X2_1 as map of [:T2,T2:],R^1 by TOPMETR:24;
  for p being Point of [:T2,T2:], V being Subset of R^1
   st Dx.p in V & V is open holds
    ex W being Subset of [:T2,T2:] st p in W & W is open & Dx.:W c= V
  proof
    let p be Point of [:T2,T2:],
        V be Subset of R^1 such that
a3:   Dx.p in V and
a4:   V is open;
a5: p = [p`1,p`2] by Lmcar,MCART_1:23;
a6: diffX1_X2_1.p = p`1`1 - p`2`1 by dX1X21;
    set r = p`1`1 - p`2`1;
    reconsider V1 = V as open Subset of REAL by a4,TOPMETR:24,BORSUK_5:62;
    consider g being real number such that
a7:   0 < g and
a8:   ].r-g,r+g.[ c= V1 by a3,a6,RCOMP_1:40;
    reconsider g as Element of REAL by XREAL_0:def 1;
    set W1 = {|[x,y]| where x, y is Element of REAL:
      p`1`1-g/2 < x & x < p`1`1+g/2};
    set W2 = {|[x,y]| where x, y is Element of REAL:
      p`2`1-g/2 < x & x < p`2`1+g/2};
    W1 c= the carrier of T2
    proof
      let a be set;
      assume a in W1;
      then ex x, y being Element of REAL st
        a = |[x,y]| & p`1`1-g/2 < x & x < p`1`1+g/2;
      hence thesis;
    end;
    then reconsider W1 as Subset of T2;
    W2 c= the carrier of T2
    proof
      let a be set;
      assume a in W2;
      then ex x, y being Element of REAL st
        a = |[x,y]| & p`2`1-g/2 < x & x < p`2`1+g/2;
      hence thesis;
    end;
    then reconsider W2 as Subset of T2;
    take [:W1,W2:];
b1: p`1 = |[p`1`1,p`1`2]| by EUCLID:57;
c30: 0/2 < g/2 by a7,REAL_1:73;
    then p`1`1-g/2 < p`1`1-0 & p`1`1+0 < p`1`1+g/2 by REAL_1:92,XREAL_1:8;
    then
a9: p`1 in W1 by b1;
b1: p`2 = |[p`2`1,p`2`2]| by EUCLID:57;
    p`2`1-g/2 < p`2`1-0 & p`2`1+0 < p`2`1+g/2 by c30,REAL_1:92,XREAL_1:8;
    then
    p`2 in W2 by b1;
    hence p in [:W1,W2:] by a9,a5,ZFMISC_1:def 2;
    W1 is open & W2 is open by PSCOMP_1:66;
    hence [:W1,W2:] is open by BORSUK_1:46;
    let b be set;
    assume b in Dx.:[:W1,W2:];
    then consider a being Point of [:T2,T2:] such that
a10:  a in [:W1,W2:] and
a11:  Dx.a = b by FUNCT_2:116;
a12:  a = [a`1,a`2] by Lmcar,MCART_1:23;
a13:  diffX1_X2_1.a = a`1`1 - a`2`1 by dX1X21;
    a`1 in W1 by a10,a12,ZFMISC_1:106;
    then consider x1, y1 being Element of REAL such that
a15:  a`1 = |[x1,y1]| and
a16:  p`1`1-g/2 < x1 and
a17:  x1 < p`1`1+g/2;
a21: a`1`1 = x1 by a15,EUCLID:56;
    p`1`1-g/2+g/2 < x1+g/2 & p`1`1-x1 > p`1`1-(p`1`1+g/2)
    by a16,a17,REAL_1:92,XREAL_1:8; 
    then p`1`1-x1 < x1+g/2-x1 & p`1`1-x1 > -g/2 by XREAL_1:11;
    then
a24: abs(p`1`1-x1) < g/2 by SEQ_2:9;
    a`2 in W2 by a10,a12,ZFMISC_1:106;
    then consider x2, y2 being Element of REAL such that
a18:  a`2 = |[x2,y2]| and
a19:  p`2`1-g/2 < x2 and
a20:  x2 < p`2`1+g/2;
a22: a`2`1 = x2 by a18,EUCLID:56;
    p`2`1-g/2+g/2 < x2+g/2 & p`2`1-x2 > p`2`1-(p`2`1+g/2)
    by a19,a20,REAL_1:92,XREAL_1:8; 
    then p`2`1-x2 < x2+g/2-x2 & p`2`1-x2 > -g/2 by XREAL_1:11;
    then
    abs(p`2`1-x2) < g/2 by SEQ_2:9;
    then
a26: abs(p`1`1-x1)+abs(p`2`1-x2) < g/2+g/2 by a24,XREAL_1:10;
    abs(p`1`1-x1-(p`2`1-x2)) <= abs(p`1`1-x1)+abs(p`2`1-x2) by COMPLEX1:143;
    then abs-(p`1`1-x1-(p`2`1-x2)) <= abs(p`1`1-x1)+abs(p`2`1-x2)
    by COMPLEX1:138;
    then abs(x1-x2-r) < g by a26,XREAL_1:2;
    then a`1`1 - a`2`1 in ].r-g,r+g.[ by a21,a22,RCOMP_1:8;
    hence b in V by a8,a11,a13;
  end;
  hence thesis by JGRAPH_2:20;
  end;

theorem TX1X22:
  diffX1_X2_2 is continuous map of [:TOP-REAL 2, TOP-REAL 2:], R^1
  proof 
    reconsider Dy = diffX1_X2_2 as map of [:T2,T2:],R^1 by TOPMETR:24;
  for p being Point of [:T2,T2:], V being Subset of R^1
   st Dy.p in V & V is open holds
    ex W being Subset of [:T2,T2:] st p in W & W is open & Dy.:W c= V
  proof
    let p be Point of [:T2,T2:],
        V be Subset of R^1 such that
a3:   Dy.p in V and
a4:   V is open;
a5:   p = [p`1,p`2] by Lmcar,MCART_1:23;
a6:   diffX1_X2_2.p = p`1`2 - p`2`2 by dX1X22;
    set r = p`1`2 - p`2`2;
    reconsider V1 = V as open Subset of REAL by a4,TOPMETR:24,BORSUK_5:62;
    consider g being real number such that
a7:   0 < g and
a8:   ].r-g,r+g.[ c= V1 by a3,a6,RCOMP_1:40;
    reconsider g as Element of REAL by XREAL_0:def 1;
    set W1 = {|[x,y]| where x, y is Element of REAL:
      p`1`2-g/2 < y & y < p`1`2+g/2};
    set W2 = {|[x,y]| where x, y is Element of REAL:
      p`2`2-g/2 < y & y < p`2`2+g/2};
    W1 c= the carrier of T2
    proof
      let a be set;
      assume a in W1;
      then ex x, y being Element of REAL st
        a = |[x,y]| & p`1`2-g/2 < y & y < p`1`2+g/2;
      hence thesis;
    end;
    then reconsider W1 as Subset of T2;
    W2 c= the carrier of T2
    proof
      let a be set;
      assume a in W2;
      then ex x, y being Element of REAL st
        a = |[x,y]| & p`2`2-g/2 < y & y < p`2`2+g/2;
      hence thesis;
    end;
    then reconsider W2 as Subset of T2;
    take [:W1,W2:];
b1: p`1 = |[p`1`1,p`1`2]| by EUCLID:57;
c30: 0/2 < g/2 by a7,REAL_1:73;
    then p`1`2-g/2 < p`1`2-0 & p`1`2+0 < p`1`2+g/2 by REAL_1:92,XREAL_1:8;
    then
a9: p`1 in W1 by b1;
b1: p`2 = |[p`2`1,p`2`2]| by EUCLID:57;
    p`2`2-g/2 < p`2`2-0 & p`2`2+0 < p`2`2+g/2 by c30,REAL_1:92,XREAL_1:8;
    then
    p`2 in W2 by b1;
    hence p in [:W1,W2:] by a9,a5,ZFMISC_1:def 2;
    W1 is open & W2 is open by PSCOMP_1:68;
    hence [:W1,W2:] is open by BORSUK_1:46;
    let b be set;
    assume b in Dy.:[:W1,W2:];
    then consider a being Point of [:T2,T2:] such that
a10:  a in [:W1,W2:] and
a11:  Dy.a = b by FUNCT_2:116;
a12:  a = [a`1,a`2] by Lmcar,MCART_1:23;
a13:  diffX1_X2_2.a = a`1`2 - a`2`2 by dX1X22;
    a`1 in W1 by a10,a12,ZFMISC_1:106;
    then consider x1, y1 being Element of REAL such that
a15:  a`1 = |[x1,y1]| and
a16:  p`1`2-g/2 < y1 and
a17:  y1 < p`1`2+g/2;
a21: a`1`2 = y1 by a15,EUCLID:56;
    p`1`2-g/2+g/2 < y1+g/2 & p`1`2-y1 > p`1`2-(p`1`2+g/2)
    by a16,a17,REAL_1:92,XREAL_1:8; 
    then p`1`2-y1 < y1+g/2-y1 & p`1`2-y1 > -g/2 by XREAL_1:11;
    then
a24: abs(p`1`2-y1) < g/2 by SEQ_2:9;
    a`2 in W2 by a10,a12,ZFMISC_1:106;
    then consider x2, y2 being Element of REAL such that
a18:  a`2 = |[x2,y2]| and
a19:  p`2`2-g/2 < y2 and
a20:  y2 < p`2`2+g/2;
a22: a`2`2 = y2 by a18,EUCLID:56;
    p`2`2-g/2+g/2 < y2+g/2 & p`2`2-y2 > p`2`2-(p`2`2+g/2)
    by a19,a20,REAL_1:92,XREAL_1:8; 
    then p`2`2-y2 < y2+g/2-y2 & p`2`2-y2 > -g/2 by XREAL_1:11;
    then
    abs(p`2`2-y2) < g/2 by SEQ_2:9;
    then
a26: abs(p`1`2-y1)+abs(p`2`2-y2) < g/2+g/2 by a24,XREAL_1:10;
    abs(p`1`2-y1-(p`2`2-y2)) <= abs(p`1`2-y1)+abs(p`2`2-y2) by COMPLEX1:143;
    then abs-(p`1`2-y1-(p`2`2-y2)) <= abs(p`1`2-y1)+abs(p`2`2-y2)
    by COMPLEX1:138;
    then abs(y1-y2-r) < g by a26,XREAL_1:2;
    then a`1`2 - a`2`2 in ].r-g,r+g.[ by a21,a22,RCOMP_1:8;
    hence b in V by a8,a11,a13;
  end;
  hence thesis by JGRAPH_2:20;
end;

theorem TP1:
  Proj2_1 is continuous map of [:TOP-REAL 2, TOP-REAL 2:], R^1
  proof 
    reconsider fX2 = Proj2_1 as map of [:T2,T2:],R^1 by TOPMETR:24;
  for p being Point of [:T2,T2:], V being Subset of R^1
   st fX2.p in V & V is open holds
    ex W being Subset of [:T2,T2:] st p in W & W is open & fX2.:W c= V
  proof
    let p be Point of [:T2,T2:],
        V be Subset of R^1 such that
a3:   fX2.p in V and
a4:   V is open;
a5: p = [p`1,p`2] by Lmcar,MCART_1:23;
a6: fX2.p = p`2`1 by dP21;
    reconsider V1 = V as open Subset of REAL by a4,TOPMETR:24,BORSUK_5:62;
    consider g being real number such that
a7:   0 < g and
a8:   ].p`2`1-g,p`2`1+g.[ c= V1 by a3,a6,RCOMP_1:40;
    reconsider g as Element of REAL by XREAL_0:def 1;
    set W1 = {|[x,y]| where x, y is Element of REAL:
    p`2`1-g < x & x < p`2`1+g};
    W1 c= the carrier of T2
    proof
      let a be set;
      assume a in W1;
      then ex x, y being Element of REAL st
        a = |[x,y]| & p`2`1-g < x & x < p`2`1+g;
      hence thesis;
    end;
    then reconsider W1 as Subset of T2;
    take [:[#]T2,W1:];
b1: p`2 = |[p`2`1,p`2`2]| by EUCLID:57;
    p`2`1-g < p`2`1-0 & p`2`1+0 < p`2`1+g by a7,REAL_1:92,XREAL_1:8;
    then p`2 in W1 by b1;
    hence p in [:[#]T2,W1:] by a5,Lm1,ZFMISC_1:def 2;
    W1 is open by PSCOMP_1:66;
    hence [:[#]T2,W1:] is open by BORSUK_1:46;
    let b be set;
    assume b in fX2.:[:[#]T2,W1:];
    then consider a being Point of [:T2,T2:] such that
a10:  a in [:[#]T2,W1:] and
a11:  fX2.a = b by FUNCT_2:116;
a12:  a = [a`1,a`2] by Lmcar,MCART_1:23;
a13:  fX2.a = a`2`1 by dP21;
    a`2 in W1 by a10,a12,ZFMISC_1:106;
    then consider x1, y1 being Element of REAL such that
a15:  a`2 = |[x1,y1]| and
a16:  p`2`1-g < x1 and
a17:  x1 < p`2`1+g;
a21: a`2`1 = x1 by a15,EUCLID:56;
    p`2`1-g+g < x1+g & p`2`1-x1 > p`2`1-(p`2`1+g)
    by a16,a17,REAL_1:92,XREAL_1:8; 
    then p`2`1-x1 < x1+g-x1 & p`2`1-x1 > -g by XREAL_1:11;
    then abs(p`2`1-x1) < g by SEQ_2:9;
    then abs-(p`2`1-x1) < g by COMPLEX1:138;
    then abs(x1-p`2`1) < g;
    then a`2`1 in ].p`2`1-g,p`2`1+g.[ by a21,RCOMP_1:8;
    hence b in V by a8,a11,a13;
  end;
  hence thesis by JGRAPH_2:20;
end;

theorem TP2:
  Proj2_2 is continuous map of [:TOP-REAL 2, TOP-REAL 2:], R^1
  proof 
    reconsider fY2 = Proj2_2 as map of [:T2,T2:],R^1 by TOPMETR:24;
  for p being Point of [:T2,T2:], V being Subset of R^1
   st fY2.p in V & V is open holds
    ex W being Subset of [:T2,T2:] st p in W & W is open & fY2.:W c= V
  proof
    let p be Point of [:T2,T2:],
        V be Subset of R^1 such that
a3:   fY2.p in V and
a4:   V is open;
a5:   p = [p`1,p`2] by Lmcar,MCART_1:23;
a6:   fY2.p = p`2`2 by dP22;
    reconsider V1 = V as open Subset of REAL by a4,TOPMETR:24,BORSUK_5:62;
    consider g being real number such that
a7:   0 < g and
a8:   ].p`2`2-g,p`2`2+g.[ c= V1 by a3,a6,RCOMP_1:40;
    reconsider g as Element of REAL by XREAL_0:def 1;
    set W1 = {|[x,y]| where x, y is Element of REAL:
    p`2`2-g < y & y < p`2`2+g};
    W1 c= the carrier of T2
    proof
      let a be set;
      assume a in W1;
      then ex x, y being Element of REAL st
        a = |[x,y]| & p`2`2-g < y & y < p`2`2+g;
      hence thesis;
    end;
    then reconsider W1 as Subset of T2;
    take [:[#]T2,W1:];
b1: p`2 = |[p`2`1,p`2`2]| by EUCLID:57;
    p`2`2-g < p`2`2-0 & p`2`2+0 < p`2`2+g by a7,REAL_1:92,XREAL_1:8;
    then p`2 in W1 by b1;
    hence p in [:[#]T2,W1:] by a5,Lm1,ZFMISC_1:def 2;
    W1 is open by PSCOMP_1:68;
    hence [:[#]T2,W1:] is open by BORSUK_1:46;
    let b be set;
    assume b in fY2.:[:[#]T2,W1:];
    then consider a being Point of [:T2,T2:] such that
a10:  a in [:[#]T2,W1:] and
a11:  fY2.a = b by FUNCT_2:116;
a12:  a = [a`1,a`2] by Lmcar,MCART_1:23;
a13:  fY2.a = a`2`2 by dP22;
    a`2 in W1 by a10,a12,ZFMISC_1:106;
    then consider x1, y1 being Element of REAL such that
a15:  a`2 = |[x1,y1]| and
a16:  p`2`2-g < y1 and
a17:  y1 < p`2`2+g;
a21: a`2`2 = y1 by a15,EUCLID:56;
    p`2`2-g+g < y1+g & p`2`2-y1 > p`2`2-(p`2`2+g)
    by a16,a17,REAL_1:92,XREAL_1:8; 
    then p`2`2-y1 < y1+g-y1 & p`2`2-y1 > -g by XREAL_1:11;
    then abs(p`2`2-y1) < g by SEQ_2:9;
    then abs-(p`2`2-y1) < g by COMPLEX1:138;
    then abs(y1-p`2`2) < g;
    then a`2`2 in ].p`2`2-g,p`2`2+g.[ by a21,RCOMP_1:8;
    hence b in V by a8,a11,a13;
  end;
  hence thesis by JGRAPH_2:20;
  end;

registration
  let o be Point of TOP-REAL 2;
  cluster diffX2_1(o) -> continuous;
  coherence
  proof 
    diffX2_1(o) is continuous map of [:T2,T2:],R^1 by TX21;
    hence thesis by TOPREAL6:83;
  end;
  cluster diffX2_2(o) -> continuous;
  coherence
  proof 
    diffX2_2(o) is continuous map of [:T2,T2:],R^1 by TX22;
    hence thesis by TOPREAL6:83;
  end;
end;

registration
  cluster diffX1_X2_1 -> continuous;
  coherence by TX1X21,TOPREAL6:83;
  cluster diffX1_X2_2 -> continuous;
  coherence by TX1X22,TOPREAL6:83;
  cluster Proj2_1 -> continuous;
  coherence by TP1,TOPREAL6:83;
  cluster Proj2_2 -> continuous;
  coherence by TP2,TOPREAL6:83;
end;

definition
  let n be non empty Nat, o, p be Point of TOP-REAL n,
  r be positive (real number) such that
b2: p is Point of Tdisk(o,r);
    set X = (TOP-REAL n)|(cl_Ball(o,r)\{p});
  func DiskProj(o,r,p) -> map of (TOP-REAL n)|(cl_Ball(o,r)\{p}), Tcircle(o,r)
  means :dBR:
  for x being Point of (TOP-REAL n)|(cl_Ball(o,r)\{p})
  ex y being Point of TOP-REAL n st x = y & it.x = HC(p,y,o,r);
  existence
  proof 
d1: the carrier of X = cl_Ball(o,r)\{p} by JORDAN1:1;
    defpred P[set,set] means
    ex z being Point of TOP-REAL n st $1 = z & $2 = HC(p,z,o,r);
a1: for x being set st x in the carrier of X
    ex y being set st y in the carrier of Tcircle(o,r) & P[x,y]
    proof
      let x be set such that
b1:   x in the carrier of X;
      reconsider z = x as Point of TOP-REAL n by b1,TOPMETR:2;
      z in cl_Ball(o,r) by b1,d1,XBOOLE_0:def 4;
      then
b3:   z is Point of Tdisk(o,r) by BROUWER:3;
      p <> z by d1,b1,ZFMISC_1:64;
      then HC(p,z,o,r) is Point of Tcircle(o,r) by b2,b3,BROUWER:6;
      hence thesis;
    end;
    consider f being Function of the carrier of X, the carrier of Tcircle(o,r)
    such that
a2: for x being set st x in the carrier of X holds P[x,f.x]
    from FUNCT_2:sch 1(a1);
    reconsider f as map of X, Tcircle(o,r);
    take f;
    let x be Point of X;
    thus thesis by a2;
  end;
  uniqueness
  proof 
    let f, g be map of X, Tcircle(o,r) such that
a1: for x being Point of X
    ex y being Point of TOP-REAL n st x = y & f.x = HC(p,y,o,r) and
a2: for x being Point of X
    ex y being Point of TOP-REAL n st x = y & g.x = HC(p,y,o,r);
    now
      let x be set such that
a3:   x in the carrier of X; 
a4:   ex y being Point of TOP-REAL n st x = y & f.x = HC(p,y,o,r) by a1,a3;
      ex y being Point of TOP-REAL n st x = y & g.x = HC(p,y,o,r) by a2,a3;
      hence f.x = g.x by a4;
    end;
    hence thesis by FUNCT_2:18;
  end;
end;
  
theorem ThDPC:
  for o, p being Point of TOP-REAL 2,
  r being positive (real number) st p is Point of Tdisk(o,r) holds
  DiskProj(o,r,p) is continuous
  proof 
    let o, p be Point of TOP-REAL 2;
    let r be positive (real number) such that
no3: p is Point of Tdisk(o,r);
     set D = Tdisk(o,r);
     set cB = cl_Ball(o,r);
     set Bp = cB \ {p};
     set OK = [:Bp,{p}:];
     set D1 = T2|Bp;
     set D2 = T2|{p};
     set S1 = Tcircle(o,r);
no11: p in {p} by TARSKI:def 1;
A13:  the carrier of D = cl_Ball(o,r) by BROUWER:3;
b0:   the carrier of D1 = Bp by JORDAN1:1;
b1:   the carrier of D2 = {p} by JORDAN1:1;
  set TD = [:T2,T2:] | OK;
  set gg = DiskProj(o,r,p);
aa: [#]D1 = the carrier of D1 by PRE_TOPC:12;
bb: [#]S1 = the carrier of S1 by PRE_TOPC:12;
    set xo = diffX2_1(o);
    set yo = diffX2_2(o);
    set dx = diffX1_X2_1;
    set dy = diffX1_X2_2;
    set fx2 = Proj2_1;
    set fy2 = Proj2_2;
  reconsider rr = r^2 as Real by XREAL_0:def 1;
  set f1 = (the carrier of [:T2,T2:]) --> rr; 
  reconsider f1 as continuous RealMap of [:T2,T2:] by Lm10;
  set Zf1 = f1 || OK;
  set Zfx2 = fx2 || OK; 
  set Zfy2 = fy2 || OK;
  set Zdx = dx || OK;
  set Zdy = dy || OK;
  set Zxo = xo || OK;
  set Zyo = yo || OK;
  set xx = Zxo(#)Zdx;
  set yy = Zyo(#)Zdy;
  set m = Zdx(#)Zdx + Zdy(#)Zdy;
D0: the carrier of TD = OK by JORDAN1:1;
D1: the carrier of TD = [#]TD by PRE_TOPC:12;
z53: dom m = the carrier of TD by FUNCT_2:def 1;
z23: dom (Zdx(#)Zdx) = the carrier of TD by FUNCT_2:def 1;
z24: dom (Zdy(#)Zdy) = the carrier of TD by FUNCT_2:def 1;
P1:  for y being Point of D1, z being Point of D2 holds Zdx. [y,z] = dx. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
a1: [y,z] in OK by b0,b1,ZFMISC_1:def 2;
    thus Zdx. [y,z] = (dx | OK). [y,z] by PSCOMP_1:def 26
    .= dx. [y,z] by a1,FUNCT_1:72;
  end;
P2: for y being Point of D1, z being Point of D2 holds Zdy. [y,z] = dy. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
a1: [y,z] in OK by b0,b1,ZFMISC_1:def 2;
    thus Zdy. [y,z] = (dy | OK). [y,z] by PSCOMP_1:def 26
    .= dy. [y,z] by a1,FUNCT_1:72;
  end;
P3: for y being Point of D1, z being Point of D2 holds Zfx2. [y,z] = fx2. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
a1: [y,z] in OK by b0,b1,ZFMISC_1:def 2;
    thus Zfx2. [y,z] = (fx2 | OK). [y,z] by PSCOMP_1:def 26
    .= fx2. [y,z] by a1,FUNCT_1:72;
  end;
P4: for y being Point of D1, z being Point of D2 holds Zfy2. [y,z] = fy2. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
a1: [y,z] in OK by b0,b1,ZFMISC_1:def 2;
    thus Zfy2. [y,z] = (fy2 | OK). [y,z] by PSCOMP_1:def 26
    .= fy2. [y,z] by a1,FUNCT_1:72;
  end;
P5: for y being Point of D1, z being Point of D2 holds Zf1. [y,z] = f1. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
a1: [y,z] in OK by b0,b1,ZFMISC_1:def 2;
    thus Zf1. [y,z] = (f1 | OK). [y,z] by PSCOMP_1:def 26
    .= f1. [y,z] by a1,FUNCT_1:72;
  end;
P6: for y being Point of D1, z being Point of D2 holds Zxo. [y,z] = xo. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
a1: [y,z] in OK by b0,b1,ZFMISC_1:def 2;
    thus Zxo. [y,z] = (xo | OK). [y,z] by PSCOMP_1:def 26
    .= xo. [y,z] by a1,FUNCT_1:72;
  end;
P7: for y being Point of D1, z being Point of D2 holds Zyo. [y,z] = yo. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
a1: [y,z] in OK by b0,b1,ZFMISC_1:def 2;
    thus Zyo. [y,z] = (yo | OK). [y,z] by PSCOMP_1:def 26
    .= yo. [y,z] by a1,FUNCT_1:72;
  end;
  now
    let b be real number;
    assume b in rng m;
    then consider a being set such that
e1:   a in dom m and
e2:   m.a = b by FUNCT_1:def 5;
    consider y, z being set such that
e3: y in Bp & z in {p} & a = [y,z] by e1,D0,ZFMISC_1:def 2;
no10: z = p by e3,TARSKI:def 1;
      reconsider y, z as Point of T2 by e3;
e7: y <> z by e3,no10,ZFMISC_1:64;
    [y,z] = [[y,z]`1,[y,z]`2] by Lmcar,MCART_1:23;
    then
z20: y = [y,z]`1 & z = [y,z]`2 by ZFMISC_1:33;
z19: dx. [y,z] = [y,z]`1`1 - [y,z]`2`1 by dX1X21;
z26: dy. [y,z] = [y,z]`1`2 - [y,z]`2`2 by dX1X22;
     set r1 = y`1-z`1;
     set r2 = y`2-z`2;
s1: Zdx. [y,z] = dx. [y,z] by P1,e3,b0,b1;
s2: Zdy. [y,z] = dy. [y,z] by e3,b0,b1,P2;
e4: m. [y,z] = (Zdx(#)Zdx). [y,z] + (Zdy(#)Zdy). [y,z] by e1,e3,SEQ_1:def 3
    .= Zdx. [y,z] * Zdx. [y,z] + (Zdy(#)Zdy). [y,z]
    by e1,e3,z23,SEQ_1:def 5
    .= Zdx. [y,z] * Zdx. [y,z] + Zdy. [y,z] * Zdy. [y,z]
       by e1,e3,z24,SEQ_1:def 5
    .= r1^2+r2*r2 by s2,z26,s1,z19,z20,SQUARE_1:def 3
    .= r1^2+r2^2 by SQUARE_1:def 3;
    0 <= r1^2 & 0 <= r2^2 by SQUARE_1:72;
    then
e5: 0+0 <= r1^2+r2^2 by XREAL_1:9;
    now
      assume r1^2+r2^2 = 0;
      then r1 = 0 & r2 = 0 by COMPLEX1:2;
      then y`1 = z`1 & y`2 = z`2 by XCMPLX_1:15;
      hence contradiction by e7,TOPREAL3:11;
    end;
    hence 0 < b by e2,e3,e4,e5;
  end;
  then reconsider m as positive-yielding continuous RealMap of TD
    by PARTFUN3:def 1;
  set p1 = (xx+yy)(#)(xx+yy);
  set p2 = Zxo(#)Zxo + Zyo(#)Zyo - Zf1;
z44: dom p2 = the carrier of TD by FUNCT_2:def 1;
z50: dom (Zxo(#)Zxo + Zyo(#)Zyo) = the carrier of TD by FUNCT_2:def 1;
z51: dom (Zxo(#)Zxo) = the carrier of TD by FUNCT_2:def 1;
z52: dom (Zyo(#)Zyo) = the carrier of TD by FUNCT_2:def 1;
  now
    let b be real number;
    assume b in rng p2;
    then consider a being set such that
e1:   a in dom p2 and
e2:   p2.a = b by FUNCT_1:def 5;
    consider y, z being set such that
e3: y in Bp & z in {p} & a = [y,z] by e1,D0,ZFMISC_1:def 2;
    reconsider y, z as Point of T2 by e3;
    set r3 = z`1-o`1, r4 = z`2-o`2;
s5: Zf1. [y,z] = f1. [y,z] by P5,b0,b1,e3;
s6: Zxo. [y,z] = xo. [y,z] by b0,b1,e3,P6;
s7: Zyo. [y,z] = yo. [y,z] by b0,b1,e3,P7;
z62: [y,z] = [[y,z]`1,[y,z]`2] by Lmcar,MCART_1:23;
z63: xo. [y,z] = [y,z]`2`1 - o`1 by dX21;
z64: y = [y,z]`1 & z = [y,z]`2 by z62,ZFMISC_1:33;
z66:  yo. [y,z] = [y,z]`2`2 - o`2 by dX22;
z40: p2. [y,z] = (Zxo(#)Zxo + Zyo(#)Zyo). [y,z] - Zf1. [y,z]
    by e3,e1,SEQ_1:def 4
    .= (Zxo(#)Zxo + Zyo(#)Zyo). [y,z] - r^2 by s5,FUNCOP_1:13
    .= (Zxo(#)Zxo). [y,z] + (Zyo(#)Zyo). [y,z] - r^2
    by e3,e1,z50,SEQ_1:def 3
    .= Zxo. [y,z] * Zxo. [y,z] + (Zyo(#)Zyo). [y,z] - r^2
    by e3,e1,z51,SEQ_1:def 5
    .= Zxo. [y,z] * Zxo. [y,z] + Zyo. [y,z] * Zyo. [y,z] - r^2
    by e3,e1,z52,SEQ_1:def 5
    .= r3^2 + yo. [y,z] * yo. [y,z] - r^2 by s7,s6,z63,z64,SQUARE_1:def 3
    .= r3^2+r4^2-r^2 by z66,z64,SQUARE_1:def 3;
    z = p by e3,TARSKI:def 1;
    then |. z-o .| <= r by no3,A13,TOPREAL9:8;
    then
d1: |. z-o .|^2 <= r^2 by SQUARE_1:77;
    |. z-o .|^2 = ((z-o)`1)^2+((z-o)`2)^2 by JGRAPH_1:46
      .= r3^2+((z-o)`2)^2 by TOPREAL3:8
      .= r3^2+r4^2 by TOPREAL3:8;
    then r3^2+r4^2-r^2 <= r^2-r^2 by d1,XREAL_1:11;
    hence 0 >= b by e2,e3,z40;
  end;
  then reconsider p2 as nonpositive-yielding continuous RealMap of TD
    by PARTFUN3:def 3;
  set pp = p1 - m(#)p2;
  set k = (-(xx+yy) + sqrt(pp)) / m;
  set x3 = Zfx2 + k(#)Zdx;
  set y3 = Zfy2 + k(#)Zdy;
  reconsider X3 = x3, Y3 = y3 as map of TD,R^1 by TOPMETR:24;
  set F = <:X3,Y3:>;
  set R = R2Homeomorphism;
ww: for x being Point of D1 holds gg.x = (R*F). [x,p]
  proof
    let x be Point of D1;
    consider y being Point of T2 such that
no2: x = y & gg.x = HC(p,y,o,r) by no3,dBR;
z6: x <> p by b0,ZFMISC_1:64;
RR: [y,p] in OK by b0,no11,no2,ZFMISC_1:def 2;
    set r1 = y`1-p`1, r2 = y`2-p`2,
        r3 = p`1-o`1, r4 = p`2-o`2;
    set l = (-(r3*r1+r4*r2)+sqrt((r3*r1+r4*r2)^2-(r1^2+r2^2)*(r3^2+r4^2-r^2)))
        / (r1^2+r2^2);
    [y,p] = [[y,p]`1,[y,p]`2] by Lmcar,MCART_1:23;
    then
z16: y = [y,p]`1 & p = [y,p]`2 by ZFMISC_1:33;
z15: fx2. [y,p] = [y,p]`2`1 by dP21;
z34: fy2. [y,p] = [y,p]`2`2 by dP22;
z19: dx. [y,p] = [y,p]`1`1 - [y,p]`2`1 by dX1X21;
z26: dy. [y,p] = [y,p]`1`2 - [y,p]`2`2 by dX1X22;
z63: xo. [y,p] = [y,p]`2`1 - o`1 by dX21;
z66: yo. [y,p] = [y,p]`2`2 - o`2 by dX22;
z7: dom X3 = the carrier of TD by FUNCT_2:def 1;
z8: dom Y3 = the carrier of TD by FUNCT_2:def 1;
z17: dom (k(#)Zdx) = the carrier of TD by FUNCT_2:def 1;
z41: dom (k(#)Zdy) = the carrier of TD by FUNCT_2:def 1;
z23: dom (Zdx(#)Zdx) = the carrier of TD by FUNCT_2:def 1;
z24: dom (Zdy(#)Zdy) = the carrier of TD by FUNCT_2:def 1;
z28: dom (-(xx+yy) + sqrt(pp)) = the carrier of TD by FUNCT_2:def 1;
z29: dom -(xx+yy) = the carrier of TD by FUNCT_2:def 1;
z30: dom (xx+yy) = the carrier of TD by FUNCT_2:def 1;
z31: dom xx = the carrier of TD by FUNCT_2:def 1;
z32: dom yy = the carrier of TD by FUNCT_2:def 1;
z37: dom pp = the carrier of TD by FUNCT_2:def 1;
z43: dom p1 = the carrier of TD by FUNCT_2:def 1;
z38: dom (m(#)p2) = the carrier of TD by FUNCT_2:def 1;
no12: y is Point of D1 & p is Point of D2 by b1,no2,TARSKI:def 1;
     then
s1: Zdx. [y,p] = dx. [y,p] by P1;
s2: Zdy. [y,p] = dy. [y,p] by no12,P2;
s5: Zf1. [y,p] = f1. [y,p] by no12,P5;
s6: Zxo. [y,p] = xo. [y,p] by no12,P6;
s7: Zyo. [y,p] = yo. [y,p] by no12,P7;
z22: m. [y,p] = (Zdx(#)Zdx). [y,p] + (Zdy(#)Zdy). [y,p]
    by RR,D0,z53,SEQ_1:def 3
    .= Zdx. [y,p] * Zdx. [y,p] + (Zdy(#)Zdy). [y,p]
    by RR,D0,z23,SEQ_1:def 5
    .= Zdx. [y,p] * Zdx. [y,p] + Zdy. [y,p] * Zdy. [y,p]
    by RR,D0,z24,SEQ_1:def 5
    .= r1^2 + Zdy. [y,p] * Zdy. [y,p] by s1,z19,z16,SQUARE_1:def 3
    .= r1^2+r2^2 by s2,z26,z16,SQUARE_1:def 3;
z60: xx. [y,p] = Zxo. [y,p] * Zdx. [y,p] by RR,D0,z31,SEQ_1:def 5;
z61: yy. [y,p] = Zyo. [y,p] * Zdy. [y,p] by RR,D0,z32,SEQ_1:def 5;
z42: (xx+yy). [y,p] = xx. [y,p] + yy. [y,p] by RR,D0,z30,SEQ_1:def 3;
z39: p1. [y,p] = (xx+yy). [y,p] * (xx+yy). [y,p] by RR,D0,z43,SEQ_1:def 5
    .= (r3*r1+r4*r2)^2
    by s1,z19,s6,z63,z42,z60,z61,s2,z26,z16,s7,z66,SQUARE_1:def 3;
z40: p2. [y,p] = (Zxo(#)Zxo + Zyo(#)Zyo). [y,p] - Zf1. [y,p]
    by RR,D0,z44,SEQ_1:def 4
    .= (Zxo(#)Zxo + Zyo(#)Zyo). [y,p] - r^2 by s5,FUNCOP_1:13
    .= (Zxo(#)Zxo). [y,p] + (Zyo(#)Zyo). [y,p] - r^2
    by RR,D0,z50,SEQ_1:def 3
    .= Zxo. [y,p] * Zxo. [y,p] + (Zyo(#)Zyo). [y,p] - r^2
    by RR,D0,z51,SEQ_1:def 5
    .= Zxo. [y,p] * Zxo. [y,p] + Zyo. [y,p] * Zyo. [y,p] - r^2
    by RR,D0,z52,SEQ_1:def 5
    .= r3^2 + yo. [y,p] * yo. [y,p] - r^2 by s7,s6,z63,z16,SQUARE_1:def 3
    .= r3^2+r4^2-r^2 by z66,z16,SQUARE_1:def 3;
   dom sqrt pp = the carrier of TD by FUNCT_2:def 1;
   then
z36: sqrt(pp). [y,p] = sqrt(pp. [y,p]) by RR,D0,PARTFUN3:def 5
    .= sqrt(p1. [y,p] - (m(#)p2). [y,p]) by RR,D0,z37,SEQ_1:def 4
    .= sqrt((r3*r1+r4*r2)^2-(r1^2+r2^2)*(r3^2+r4^2-r^2))
    by RR,z22,z39,z40,D0,z38,SEQ_1:def 5;
   dom k = the carrier of TD by FUNCT_2:def 1;
   then
z21: k. [y,p] = (-(xx+yy) + sqrt(pp)). [y,p] * (m. [y,p])" by
   RR,D0,RFUNCT_1:def 4
  .= (-(xx+yy) + sqrt(pp)). [y,p] / m. [y,p] by XCMPLX_0:def 9
    .= ((-(xx+yy)). [y,p] + sqrt(pp). [y,p]) / (r1^2+r2^2)
    by RR,D0,z28,z22,SEQ_1:def 3
    .= l by RR,D0,s1,z19,s6,z63,z60,s2,z26,z16,s7,z66,z61,
    z42,z36,z29,SEQ_1:def 7;
z10: X3. [y,p] = Zfx2. [y,p] + (k(#)Zdx). [y,p] by RR,D0,z7,SEQ_1:def 3
     .= p`1 + (k(#)Zdx). [y,p] by no12,P3,z15,z16
     .= p`1+l*r1 by RR,s1,z19,z16,z21,D0,z17,SEQ_1:def 5;
z11: Y3. [y,p] = Zfy2. [y,p] + (k(#)Zdy). [y,p] by RR,D0,z8,SEQ_1:def 3
    .= p`2 + (k(#)Zdy). [y,p] by no12,P4,z34,z16
    .= p`2+l*r2 by RR,s2,z26,z16,z21,D0,z41,SEQ_1:def 5;
no21: y in Bp by no2,b0;
    Bp c= cB by XBOOLE_1:36;
    hence gg.x = |[ p`1+l*r1, p`2+l*r2 ]| by no3,no2,z6,no21,A13,BROUWER:8
    .= <*X3. [y,p], Y3. [y,p]*> by z10,z11,EUCLID:def 16
    .= R. [X3. [y,p], Y3. [y,p]] by TOPREALA:def 2
    .= R.(F. [y,p]) by RR,z7,z8,D0,FUNCT_3:69
    .= (R*F). [x,p] by RR,no2,D0,FUNCT_2:21;
  end;
  X3 is continuous & Y3 is continuous by TOPREAL6:83;
  then reconsider F as continuous map of TD,[:R^1,R^1:] by YELLOW12:41;
  for pp being Point of D1, V being Subset of S1
   st gg.pp in V & V is open holds
    ex W being Subset of D1 st pp in W & W is open & gg.:W c= V
  proof
    let pp be Point of D1,
        V be Subset of S1 such that
w1:   gg.pp in V and
w2:   V is open;
    reconsider p1 = pp, fp = p as Point of T2 by TOPMETR:2;
RR: [pp,p] in OK by b0,no11,ZFMISC_1:def 2;
    consider V1 being Subset of T2 such that
w3:   V1 is open and
w4:   V1 /\ [#]S1 = V by w2,TOPS_2:32;
w11:  gg.pp = (R*F). [pp,p] by ww;
    R" is_homeomorphism by TOPS_2:70,TOPREALA:56;
    then
q1: R" .:V1 is open by w3,TOPGRP_1:25;
q3: dom F = the carrier of [:T2,T2:] | OK by FUNCT_2:def 1;
q4: dom R = the carrier of [:R^1,R^1:] by FUNCT_2:def 1;
    then
e5: rng F c= dom R;
    then
q2: dom (R*F) = dom F by RELAT_1:46;
e2: rng R = [#]T2 & R is one-to-one by TOPS_2:def 5,TOPREALA:56;
e1: R"*(R*F) = R"*R*F by RELAT_1:55
            .= id dom R*F by e2,TOPS_2:65;
    dom id dom R = dom R by RELAT_1:71;
    then
e3: dom (id dom R*F) = dom F by e5,RELAT_1:46;
    for x being set st x in dom F holds (id dom R*F).x = F.x
    proof
      let x be set such that
e6:     x in dom F;
e7:   F.x in rng F by e6,FUNCT_1:def 5;
      thus (id dom R*F).x = id dom R.(F.x) by e6,FUNCT_1:23
        .= F.x by q4,e7,FUNCT_1:35;
    end;
    then
E2: id dom R*F = F by e3,FUNCT_1:9;
    (R*F). [p1,fp] in V1 by w1,w4,w11,XBOOLE_0:def 3;
    then R" .((R*F). [p1,fp]) in R" .:V1 by FUNCT_2:43;
    then (R"*(R*F)). [p1,fp] in R" .:V1 by RR,q2,q3,D0,FUNCT_1:23;
    then consider W being Subset of TD such that
q5:  [p1,fp] in W and
q6:  W is open and
q7:  F.:W c= R" .:V1 by RR,q1,e1,E2,D0,JGRAPH_2:20;
    consider WW being Subset of [:T2,T2:] such that
t3:   WW is open and
t4:   WW /\ [#]TD = W by q6,TOPS_2:32;
    consider SF being Subset-Family of [:T2,T2:] such that
w13:  WW = union SF and
w14:  for e being set st e in SF
        ex X1 being Subset of T2, Y1 being Subset of T2 st
          e = [:X1,Y1:] & X1 is open & Y1 is open by t3,BORSUK_1:45;
    [p1,fp] in WW by t4,q5,XBOOLE_0:def 3;
    then consider Z being set such that
w15:  [p1,fp] in Z and
w16:  Z in SF by w13,TARSKI:def 4;
    consider X1, Y1 being Subset of T2 such that
w17:  Z = [:X1,Y1:] and
w19:  X1 is open and
      Y1 is open by w14,w16;
    set ZZ = Z /\ [#]TD;
    reconsider XX = X1 /\ [#]D1 as open Subset of D1
      by aa,w19,XBOOLE_1:17,TOPS_2:32;
    take XX;
    pp in X1 by w15,w17,ZFMISC_1:106;
    hence pp in XX by aa,XBOOLE_0:def 3;
    thus XX is open;
    let b be set;
    assume b in gg.:XX;
    then consider a being Point of D1 such that
w20:  a in XX and
w21:  b = gg.a by FUNCT_2:116;
    reconsider a1 = a, fa = fp as Point of T2 by TOPMETR:2;
w33: a in X1 by w20,XBOOLE_0:def 3;
RR:  [a,p] in OK by b0,no11,ZFMISC_1:def 2;
     fa in Y1 by w15,w17,ZFMISC_1:106;
     then [a,fa] in Z by w17,w33,ZFMISC_1:def 2;
    then [a,fa] in ZZ by RR,D0,D1,XBOOLE_0:def 3;
    then
w34: F. [a1,fa] in F.:ZZ by RR,D0,FUNCT_2:43;
E3: R qua Function" = R" by e2,TOPS_2:def 4;
r1: dom(R") = [#]T2 by e2,TOPS_2:62;
    Z c= WW by w13,w16,ZFMISC_1:92;
    then ZZ c= WW /\ [#]TD by XBOOLE_1:27;
    then F.:ZZ c= F.:W by t4,RELAT_1:156;
    then F. [a1,fa] in F.:W by w34;
    then R.(F. [a1,fa]) in R.:(R" .:V1) by q7,FUNCT_2:43;
    then (R*F). [a1,fa] in R.:(R" .:V1) by RR,D0,FUNCT_2:21;
    then (R*F). [a1,fa] in V1 by e2,r1,E3,Lm1,PARTFUN3:1;
    then gg.a in V1 by ww;
    hence b in V by bb,w4,w21,XBOOLE_0:def 3;
  end;
  hence thesis by JGRAPH_2:20;
end;

theorem Th8:
  for n being non empty Nat, o, p being Point of TOP-REAL n,
  r being positive (real number) st p in Ball(o,r) holds
  DiskProj(o,r,p)|Sphere(o,r) = id Sphere(o,r)
  proof 
    let n be non empty Nat;
    let o, p be Point of TOP-REAL n;
    let r be positive (real number);
    assume
Z0: p in Ball(o,r);
a1: the carrier of Tdisk(o,r) = cl_Ball(o,r) by BROUWER:3;
a12: the carrier of (TOP-REAL n)|(cl_Ball(o,r)\{p}) = cl_Ball(o,r)\{p}
     by JORDAN1:1;
a11: dom DiskProj(o,r,p) = the carrier of (TOP-REAL n)|(cl_Ball(o,r)\{p})
     by FUNCT_2:def 1;
a14: Sphere(o,r) misses Ball(o,r) by TOPREAL9:19;
a13: Sphere(o,r) c= cl_Ball(o,r) by TOPREAL9:17;
a16: Ball(o,r) c= cl_Ball(o,r) by TOPREAL9:16;
     Sphere(o,r) c= cl_Ball(o,r)\{p}
     proof
       let a be set;
       assume
a15:   a in Sphere(o,r);
       then a <> p by a14,Z0,XBOOLE_0:3;
       hence thesis by a13,a15,ZFMISC_1:64;
     end;
     then
a2:  dom(DiskProj(o,r,p)|Sphere(o,r)) = Sphere(o,r) by a12,a11,RELAT_1:91;
a3:  dom id Sphere(o,r) = Sphere(o,r) by RELAT_1:71;
    now
      let x be set;
      assume
a5:   x in dom(DiskProj(o,r,p)|Sphere(o,r));
      then consider y being Point of TOP-REAL n such that
a4:   x = y and
a10:  (DiskProj(o,r,p)).x = HC(p,y,o,r) by Z0,a16,a1,dBR;
      y in halfline(p,y) by TOPREAL9:28;
      then
a8:   x in halfline(p,y) /\ Sphere(o,r) by a2,a4,a5,XBOOLE_0:def 3;
a9:   x <> p by a2,a5,Z0,a14,XBOOLE_0:3;
      thus (DiskProj(o,r,p)|Sphere(o,r)).x = (DiskProj(o,r,p)).x
      by a5,FUNCT_1:70
      .= x by a10,Z0,a16,a1,a4,a2,a5,a13,a8,a9,BROUWER:def 3
      .= (id Sphere(o,r)).x by a2,a5,FUNCT_1:35;
    end;
    hence thesis by a2,a3,FUNCT_1:9;
  end;

definition
  let n be non empty Nat, o, p be Point of TOP-REAL n,
  r be positive (real number) such that
b2: p in Ball(o,r);
  set X = Tcircle(o,r);
  func RotateCircle(o,r,p) -> map of Tcircle(o,r), Tcircle(o,r)
  means :dCR:
  for x being Point of Tcircle(o,r)
  ex y being Point of TOP-REAL n st x = y & it.x = HC(y,p,o,r);
  existence
  proof 
d1: the carrier of X = Sphere(o,r) by TOPREALB:9;
    defpred P[set,set] means
    ex z being Point of TOP-REAL n st $1 = z & $2 = HC(z,p,o,r);
a1: for x being set st x in the carrier of X
    ex y being set st y in the carrier of X & P[x,y]
    proof
      let x be set such that
b1:   x in the carrier of X;
      reconsider z = x as Point of TOP-REAL n by b1,TOPMETR:2;
      Sphere(o,r) c= cl_Ball(o,r) by TOPREAL9:17;
      then
b3:   z is Point of Tdisk(o,r) by b1,d1,BROUWER:3;
      Ball(o,r) c= cl_Ball(o,r) by TOPREAL9:16;
      then
b4:   p is Point of Tdisk(o,r) by b2,BROUWER:3;
      Ball(o,r) misses Sphere(o,r) by TOPREAL9:19;
      then p <> z by d1,b1,b2,XBOOLE_0:3;
      then HC(z,p,o,r) is Point of X by b3,b4,BROUWER:6;
      hence thesis;
    end;
    consider f being Function of the carrier of X, the carrier of X
    such that
a2: for x being set st x in the carrier of X holds P[x,f.x]
    from FUNCT_2:sch 1(a1);
    reconsider f as map of X, X;
    take f;
    let x be Point of X;
    thus thesis by a2;
  end;
  uniqueness
  proof 
    let f, g be map of X, X such that
a1: for x being Point of X
    ex y being Point of TOP-REAL n st x = y & f.x = HC(y,p,o,r) and
a2: for x being Point of X
    ex y being Point of TOP-REAL n st x = y & g.x = HC(y,p,o,r);
    now
      let x be set such that
a3:   x in the carrier of X; 
a4:   ex y being Point of TOP-REAL n st x = y & f.x = HC(y,p,o,r) by a1,a3;
      ex y being Point of TOP-REAL n st x = y & g.x = HC(y,p,o,r) by a2,a3;
      hence f.x = g.x by a4;
    end;
    hence thesis by FUNCT_2:18;
  end;
end;
  
theorem ThRCC:
  for o, p being Point of TOP-REAL 2,
  r being positive (real number) st p in Ball(o,r) holds
  RotateCircle(o,r,p) is continuous
  proof 
    let o, p be Point of TOP-REAL 2;
    let r be positive (real number) such that
no3: p in Ball(o,r);
     set D = Tdisk(o,r);
     set cB = cl_Ball(o,r);
     set Bp = Sphere(o,r);
     set OK = [:{p},Bp:];
     set D1 = T2|{p};
     set D2 = T2|Bp;
     set S1 = Tcircle(o,r);
dd1: D2 = S1 by TOPREALB:def 6;
dd2: Ball(o,r) misses Sphere(o,r) by TOPREAL9:19;
no11: p in {p} by TARSKI:def 1;
no50: Bp c= cB by TOPREAL9:17;
no60: Ball(o,r) c= cB by TOPREAL9:16;
A13:  the carrier of D = cB by BROUWER:3;
b0:   the carrier of D1 = {p} by JORDAN1:1;
b1:   the carrier of D2 = Bp by JORDAN1:1;
  set TD = [:T2,T2:] | OK;
  set gg = RotateCircle(o,r,p);
aa: [#]D2 = the carrier of D2 by PRE_TOPC:12;
bb: [#]S1 = the carrier of S1 by PRE_TOPC:12;
    set xo = diffX2_1(o);
    set yo = diffX2_2(o);
    set dx = diffX1_X2_1;
    set dy = diffX1_X2_2;
    set fx2 = Proj2_1;
    set fy2 = Proj2_2;
  reconsider rr = r^2 as Real by XREAL_0:def 1;
  set f1 = (the carrier of [:T2,T2:]) --> rr; 
  reconsider f1 as continuous RealMap of [:T2,T2:] by Lm10;
  set Zf1 = f1 || OK;
  set Zfx2 = fx2 || OK; 
  set Zfy2 = fy2 || OK;
  set Zdx = dx || OK;
  set Zdy = dy || OK;
  set Zxo = xo || OK;
  set Zyo = yo || OK;
  set xx = Zxo(#)Zdx;
  set yy = Zyo(#)Zdy;
  set m = Zdx(#)Zdx + Zdy(#)Zdy;
D0: the carrier of TD = OK by JORDAN1:1;
D1: the carrier of TD = [#]TD by PRE_TOPC:12;
z53: dom m = the carrier of TD by FUNCT_2:def 1;
z23: dom (Zdx(#)Zdx) = the carrier of TD by FUNCT_2:def 1;
z24: dom (Zdy(#)Zdy) = the carrier of TD by FUNCT_2:def 1;
P1:  for y being Point of D1, z being Point of D2 holds Zdx. [y,z] = dx. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
a1: [y,z] in OK by b0,b1,ZFMISC_1:def 2;
    thus Zdx. [y,z] = (dx | OK). [y,z] by PSCOMP_1:def 26
    .= dx. [y,z] by a1,FUNCT_1:72;
  end;
P2: for y being Point of D1, z being Point of D2 holds Zdy. [y,z] = dy. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
a1: [y,z] in OK by b0,b1,ZFMISC_1:def 2;
    thus Zdy. [y,z] = (dy | OK). [y,z] by PSCOMP_1:def 26
    .= dy. [y,z] by a1,FUNCT_1:72;
  end;
P3: for y being Point of D1, z being Point of D2 holds Zfx2. [y,z] = fx2. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
a1: [y,z] in OK by b0,b1,ZFMISC_1:def 2;
    thus Zfx2. [y,z] = (fx2 | OK). [y,z] by PSCOMP_1:def 26
    .= fx2. [y,z] by a1,FUNCT_1:72;
  end;
P4: for y being Point of D1, z being Point of D2 holds Zfy2. [y,z] = fy2. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
a1: [y,z] in OK by b0,b1,ZFMISC_1:def 2;
    thus Zfy2. [y,z] = (fy2 | OK). [y,z] by PSCOMP_1:def 26
    .= fy2. [y,z] by a1,FUNCT_1:72;
  end;
P5: for y being Point of D1, z being Point of D2 holds Zf1. [y,z] = f1. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
a1: [y,z] in OK by b0,b1,ZFMISC_1:def 2;
    thus Zf1. [y,z] = (f1 | OK). [y,z] by PSCOMP_1:def 26
    .= f1. [y,z] by a1,FUNCT_1:72;
  end;
P6: for y being Point of D1, z being Point of D2 holds Zxo. [y,z] = xo. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
a1: [y,z] in OK by b0,b1,ZFMISC_1:def 2;
    thus Zxo. [y,z] = (xo | OK). [y,z] by PSCOMP_1:def 26
    .= xo. [y,z] by a1,FUNCT_1:72;
  end;
P7: for y being Point of D1, z being Point of D2 holds Zyo. [y,z] = yo. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
a1: [y,z] in OK by b0,b1,ZFMISC_1:def 2;
    thus Zyo. [y,z] = (yo | OK). [y,z] by PSCOMP_1:def 26
    .= yo. [y,z] by a1,FUNCT_1:72;
  end;
  now
    let b be real number;
    assume b in rng m;
    then consider a being set such that
e1:   a in dom m and
e2:   m.a = b by FUNCT_1:def 5;
    consider y, z being set such that
e3: y in {p} & z in Bp & a = [y,z] by e1,D0,ZFMISC_1:def 2;
no10: y = p by e3,TARSKI:def 1;
      reconsider y, z as Point of T2 by e3;
e7:   y <> z by dd2,e3,no10,no3,XBOOLE_0:3;
      [y,z] = [[y,z]`1,[y,z]`2] by Lmcar,MCART_1:23;
     then
z20: y = [y,z]`1 & z = [y,z]`2 by ZFMISC_1:33;
z19:  dx. [y,z] = [y,z]`1`1 - [y,z]`2`1 by dX1X21;
z26:  dy. [y,z] = [y,z]`1`2 - [y,z]`2`2 by dX1X22;
     set r1 = y`1-z`1;
     set r2 = y`2-z`2;
s1: Zdx. [y,z] = dx. [y,z] by P1,e3,b0,b1;
s2: Zdy. [y,z] = dy. [y,z] by e3,b0,b1,P2;
e4: m. [y,z] = (Zdx(#)Zdx). [y,z] + (Zdy(#)Zdy). [y,z] by e1,e3,SEQ_1:def 3
    .= Zdx. [y,z] * Zdx. [y,z] + (Zdy(#)Zdy). [y,z]
    by e1,e3,z23,SEQ_1:def 5
    .= Zdx. [y,z] * Zdx. [y,z] + Zdy. [y,z] * Zdy. [y,z]
       by e1,e3,z24,SEQ_1:def 5
    .= r1^2+r2*r2 by s2,z26,s1,z19,z20,SQUARE_1:def 3
    .= r1^2+r2^2 by SQUARE_1:def 3;
    0 <= r1^2 & 0 <= r2^2 by SQUARE_1:72;
    then
e5: 0+0 <= r1^2+r2^2 by XREAL_1:9;
    now
      assume r1^2+r2^2 = 0;
      then r1 = 0 & r2 = 0 by COMPLEX1:2;
      then y`1 = z`1 & y`2 = z`2 by XCMPLX_1:15;
      hence contradiction by e7,TOPREAL3:11;
    end;
    hence 0 < b by e2,e3,e4,e5;
  end;
  then reconsider m as positive-yielding continuous RealMap of TD
    by PARTFUN3:def 1;
  set p1 = (xx+yy)(#)(xx+yy);
  set p2 = Zxo(#)Zxo + Zyo(#)Zyo - Zf1;
z44: dom p2 = the carrier of TD by FUNCT_2:def 1;
z50: dom (Zxo(#)Zxo + Zyo(#)Zyo) = the carrier of TD by FUNCT_2:def 1;
z51: dom (Zxo(#)Zxo) = the carrier of TD by FUNCT_2:def 1;
z52: dom (Zyo(#)Zyo) = the carrier of TD by FUNCT_2:def 1;
  now
    let b be real number;
    assume b in rng p2;
    then consider a being set such that
e1:   a in dom p2 and
e2:   p2.a = b by FUNCT_1:def 5;
    consider y, z being set such that
e3: y in {p} & z in Bp & a = [y,z] by e1,D0,ZFMISC_1:def 2;
    reconsider y, z as Point of T2 by e3;
    set r3 = z`1-o`1, r4 = z`2-o`2;
s5: Zf1. [y,z] = f1. [y,z] by P5,b0,b1,e3;
s6: Zxo. [y,z] = xo. [y,z] by b0,b1,e3,P6;
s7: Zyo. [y,z] = yo. [y,z] by b0,b1,e3,P7;
    [y,z] = [[y,z]`1,[y,z]`2] by Lmcar,MCART_1:23;
     then
z64: y = [y,z]`1 & z = [y,z]`2 by ZFMISC_1:33;
z63: xo. [y,z] = [y,z]`2`1 - o`1 by dX21;
z66: yo. [y,z] = [y,z]`2`2 - o`2 by dX22;
z40: p2. [y,z] = (Zxo(#)Zxo + Zyo(#)Zyo). [y,z] - Zf1. [y,z]
    by e3,e1,SEQ_1:def 4
    .= (Zxo(#)Zxo + Zyo(#)Zyo). [y,z] - r^2 by s5,FUNCOP_1:13
    .= (Zxo(#)Zxo). [y,z] + (Zyo(#)Zyo). [y,z] - r^2
    by e3,e1,z50,SEQ_1:def 3
    .= Zxo. [y,z] * Zxo. [y,z] + (Zyo(#)Zyo). [y,z] - r^2
    by e3,e1,z51,SEQ_1:def 5
    .= Zxo. [y,z] * Zxo. [y,z] + Zyo. [y,z] * Zyo. [y,z] - r^2
    by e3,e1,z52,SEQ_1:def 5
    .= r3^2 + yo. [y,z] * yo. [y,z] - r^2 by s7,s6,z63,z64,SQUARE_1:def 3
    .= r3^2+r4^2-r^2 by z66,z64,SQUARE_1:def 3;
    |. z-o .| <= r by no50,e3,TOPREAL9:8;
    then
d1: |. z-o .|^2 <= r^2 by SQUARE_1:77;
    |. z-o .|^2 = ((z-o)`1)^2+((z-o)`2)^2 by JGRAPH_1:46
      .= r3^2+((z-o)`2)^2 by TOPREAL3:8
      .= r3^2+r4^2 by TOPREAL3:8;
    then r3^2+r4^2-r^2 <= r^2-r^2 by d1,XREAL_1:11;
    hence 0 >= b by e2,e3,z40;
  end;
  then reconsider p2 as nonpositive-yielding continuous RealMap of TD
    by PARTFUN3:def 3;
  set pp = p1 - m(#)p2;
  set k = (-(xx+yy) + sqrt(pp)) / m;
  set x3 = Zfx2 + k(#)Zdx;
  set y3 = Zfy2 + k(#)Zdy;
  reconsider X3 = x3, Y3 = y3 as map of TD,R^1 by TOPMETR:24;
  set F = <:X3,Y3:>;
  set R = R2Homeomorphism;
ww: for x being Point of D2 holds gg.x = (R*F). [p,x]
  proof
    let x be Point of D2;
    consider y being Point of T2 such that
no2: x = y & gg.x = HC(y,p,o,r) by dd1,no3,dCR;
z6: x <> p by b1,dd2,no3,XBOOLE_0:3;
RR: [p,y] in OK by b1,no11,no2,ZFMISC_1:def 2;
    set r1 = p`1-y`1, r2 = p`2-y`2,
        r3 = y`1-o`1, r4 = y`2-o`2;
    set l = (-(r3*r1+r4*r2)+sqrt((r3*r1+r4*r2)^2-(r1^2+r2^2)*(r3^2+r4^2-r^2)))
        / (r1^2+r2^2);
    [p,y] = [[p,y]`1,[p,y]`2] by Lmcar,MCART_1:23;
     then
z16: p = [p,y]`1 & y = [p,y]`2 by ZFMISC_1:33;
z15: fx2. [p,y] = [p,y]`2`1 by dP21;
z34: fy2. [p,y] = [p,y]`2`2 by dP22;
z19: dx. [p,y] = [p,y]`1`1 - [p,y]`2`1 by dX1X21;
z26: dy. [p,y] = [p,y]`1`2 - [p,y]`2`2 by dX1X22;
z63: xo. [p,y] = [p,y]`2`1 - o`1 by dX21;
z66: yo. [p,y] = [p,y]`2`2 - o`2 by dX22;
z7: dom X3 = the carrier of TD by FUNCT_2:def 1;
z8: dom Y3 = the carrier of TD by FUNCT_2:def 1;
z17: dom (k(#)Zdx) = the carrier of TD by FUNCT_2:def 1;
z41: dom (k(#)Zdy) = the carrier of TD by FUNCT_2:def 1;
z23: dom (Zdx(#)Zdx) = the carrier of TD by FUNCT_2:def 1;
z24: dom (Zdy(#)Zdy) = the carrier of TD by FUNCT_2:def 1;
z28: dom (-(xx+yy) + sqrt(pp)) = the carrier of TD by FUNCT_2:def 1;
z29: dom -(xx+yy) = the carrier of TD by FUNCT_2:def 1;
z30: dom (xx+yy) = the carrier of TD by FUNCT_2:def 1;
z31: dom xx = the carrier of TD by FUNCT_2:def 1;
z32: dom yy = the carrier of TD by FUNCT_2:def 1;
z37: dom pp = the carrier of TD by FUNCT_2:def 1;
z43: dom p1 = the carrier of TD by FUNCT_2:def 1;
z38: dom (m(#)p2) = the carrier of TD by FUNCT_2:def 1;
no12: y is Point of D2 & p is Point of D1 by b0,no2,TARSKI:def 1;
     then
s1: Zdx. [p,y] = dx. [p,y] by P1;
s2: Zdy. [p,y] = dy. [p,y] by no12,P2;
s5: Zf1. [p,y] = f1. [p,y] by no12,P5;
s6: Zxo. [p,y] = xo. [p,y] by no12,P6;
s7: Zyo. [p,y] = yo. [p,y] by no12,P7;
z22: m. [p,y] = (Zdx(#)Zdx). [p,y] + (Zdy(#)Zdy). [p,y]
    by RR,D0,z53,SEQ_1:def 3
    .= Zdx. [p,y] * Zdx. [p,y] + (Zdy(#)Zdy). [p,y]
    by RR,D0,z23,SEQ_1:def 5
    .= Zdx. [p,y] * Zdx. [p,y] + Zdy. [p,y] * Zdy. [p,y]
    by RR,D0,z24,SEQ_1:def 5
    .= r1^2 + Zdy. [p,y] * Zdy. [p,y] by s1,z19,z16,SQUARE_1:def 3
    .= r1^2+r2^2 by s2,z26,z16,SQUARE_1:def 3;
z60: xx. [p,y] = Zxo. [p,y] * Zdx. [p,y] by RR,D0,z31,SEQ_1:def 5;
z61: yy. [p,y] = Zyo. [p,y] * Zdy. [p,y] by RR,D0,z32,SEQ_1:def 5;
z42: (xx+yy). [p,y] = xx. [p,y] + yy. [p,y] by RR,D0,z30,SEQ_1:def 3;
z39: p1. [p,y] = (xx+yy). [p,y] * (xx+yy). [p,y] by RR,D0,z43,SEQ_1:def 5
    .= (r3*r1+r4*r2)^2
    by s1,z19,s6,z63,z42,z60,z61,s2,z26,z16,s7,z66,SQUARE_1:def 3;
z40: p2. [p,y] = (Zxo(#)Zxo + Zyo(#)Zyo). [p,y] - Zf1. [p,y]
    by RR,D0,z44,SEQ_1:def 4
    .= (Zxo(#)Zxo + Zyo(#)Zyo). [p,y] - r^2 by s5,FUNCOP_1:13
    .= (Zxo(#)Zxo). [p,y] + (Zyo(#)Zyo). [p,y] - r^2
    by RR,D0,z50,SEQ_1:def 3
    .= Zxo. [p,y] * Zxo. [p,y] + (Zyo(#)Zyo). [p,y] - r^2
    by RR,D0,z51,SEQ_1:def 5
    .= Zxo. [p,y] * Zxo. [p,y] + Zyo. [p,y] * Zyo. [p,y] - r^2
    by RR,D0,z52,SEQ_1:def 5
    .= r3^2 + yo. [p,y] * yo. [p,y] - r^2 by s7,s6,z63,z16,SQUARE_1:def 3
    .= r3^2+r4^2-r^2 by z66,z16,SQUARE_1:def 3;
   dom sqrt pp = the carrier of TD by FUNCT_2:def 1;
   then
z36: sqrt(pp). [p,y] = sqrt(pp. [p,y]) by RR,D0,PARTFUN3:def 5
    .= sqrt(p1. [p,y] - (m(#)p2). [p,y]) by RR,D0,z37,SEQ_1:def 4
    .= sqrt((r3*r1+r4*r2)^2-(r1^2+r2^2)*(r3^2+r4^2-r^2))
    by RR,z22,z39,z40,D0,z38,SEQ_1:def 5;
   dom k = the carrier of TD by FUNCT_2:def 1;
   then
z21: k. [p,y] = (-(xx+yy) + sqrt(pp)). [p,y] * (m. [p,y])" by
   RR,D0,RFUNCT_1:def 4
  .= (-(xx+yy) + sqrt(pp)). [p,y] / m. [p,y] by XCMPLX_0:def 9
    .= ((-(xx+yy)). [p,y] + sqrt(pp). [p,y]) / (r1^2+r2^2)
    by RR,D0,z28,z22,SEQ_1:def 3
    .= l by RR,D0,s1,z19,z16,s6,z63,z60,s2,z26,s7,z66,z61,
    z42,z36,z29,SEQ_1:def 7;
z10: X3. [p,y] = Zfx2. [p,y] + (k(#)Zdx). [p,y] by RR,D0,z7,SEQ_1:def 3
     .= y`1 + (k(#)Zdx). [p,y] by no12,P3,z15,z16
     .= y`1+l*r1 by RR,s1,z19,z16,z21,D0,z17,SEQ_1:def 5;
z11: Y3. [p,y] = Zfy2. [p,y] + (k(#)Zdy). [p,y] by RR,D0,z8,SEQ_1:def 3
    .= y`2 + (k(#)Zdy). [p,y] by no12,P4,z34,z16
    .= y`2+l*r2 by RR,s2,z26,z16,z21,D0,z41,SEQ_1:def 5;
    y in the carrier of D2 by no2;
    hence gg.x = |[ y`1+l*r1, y`2+l*r2 ]|
    by no2,z6,no60,A13,no3,b1,no50,BROUWER:8
    .= <*X3. [p,y], Y3. [p,y]*> by z10,z11,EUCLID:def 16
    .= R. [X3. [p,y], Y3. [p,y]] by TOPREALA:def 2
    .= R.(F. [p,y]) by RR,z7,z8,D0,FUNCT_3:69
    .= (R*F). [p,x] by RR,no2,D0,FUNCT_2:21;
  end;
  X3 is continuous & Y3 is continuous by TOPREAL6:83;
  then reconsider F as continuous map of TD,[:R^1,R^1:] by YELLOW12:41;
  for pp being Point of D2, V being Subset of S1
   st gg.pp in V & V is open holds
    ex W being Subset of D2 st pp in W & W is open & gg.:W c= V
  proof
    let pp be Point of D2,
        V be Subset of S1 such that
w1:   gg.pp in V and
w2:   V is open;
    reconsider p1 = pp, fp = p as Point of T2 by TOPMETR:2;
RR: [p,pp] in OK by b1,no11,ZFMISC_1:def 2;
    consider V1 being Subset of T2 such that
w3:   V1 is open and
w4:   V1 /\ [#]S1 = V by w2,TOPS_2:32;
w11:  gg.pp = (R*F). [p,pp] by ww;
    R" is_homeomorphism by TOPS_2:70,TOPREALA:56;
    then
q1: R" .:V1 is open by w3,TOPGRP_1:25;
q3: dom F = the carrier of [:T2,T2:] | OK by FUNCT_2:def 1;
q4: dom R = the carrier of [:R^1,R^1:] by FUNCT_2:def 1;
    then
e5: rng F c= dom R;
    then
q2: dom (R*F) = dom F by RELAT_1:46;
e2: rng R = [#]T2 & R is one-to-one by TOPS_2:def 5,TOPREALA:56;
e1: R"*(R*F) = R"*R*F by RELAT_1:55
            .= id dom R*F by e2,TOPS_2:65;
    dom id dom R = dom R by RELAT_1:71;
    then
e3: dom (id dom R*F) = dom F by e5,RELAT_1:46;
    for x being set st x in dom F holds (id dom R*F).x = F.x
    proof
      let x be set such that
e6:     x in dom F;
e7:   F.x in rng F by e6,FUNCT_1:def 5;
      thus (id dom R*F).x = id dom R.(F.x) by e6,FUNCT_1:23
        .= F.x by q4,e7,FUNCT_1:35;
    end;
    then
E2: id dom R*F = F by e3,FUNCT_1:9;
    (R*F). [fp,p1] in V1 by w1,w4,w11,XBOOLE_0:def 3;
    then R" .((R*F). [fp,p1]) in R" .:V1 by FUNCT_2:43;
    then (R"*(R*F)). [fp,p1] in R" .:V1 by RR,q2,q3,D0,FUNCT_1:23;
    then consider W being Subset of TD such that
q5: [fp,p1] in W and
q6:  W is open and
q7:  F.:W c= R" .:V1 by RR,q1,e1,E2,D0,JGRAPH_2:20;
    consider WW being Subset of [:T2,T2:] such that
t3:   WW is open and
t4:   WW /\ [#]TD = W by q6,TOPS_2:32;
    consider SF being Subset-Family of [:T2,T2:] such that
w13:  WW = union SF and
w14:  for e being set st e in SF
        ex X1 being Subset of T2, Y1 being Subset of T2 st
          e = [:X1,Y1:] & X1 is open & Y1 is open by t3,BORSUK_1:45;
    [fp,p1] in WW by t4,q5,XBOOLE_0:def 3;
    then consider Z being set such that
w15:  [fp,p1] in Z and
w16:  Z in SF by w13,TARSKI:def 4;
    consider X1, Y1 being Subset of T2 such that
w17:  Z = [:X1,Y1:] and
      X1 is open and
w19:  Y1 is open by w14,w16;
    set ZZ = Z /\ [#]TD;
    reconsider XX = Y1 /\ [#]D2 as open Subset of D2
      by aa,w19,XBOOLE_1:17,TOPS_2:32;
    take XX;
    pp in Y1 by w15,w17,ZFMISC_1:106;
    hence pp in XX by aa,XBOOLE_0:def 3;
    thus XX is open;
    let b be set;
    assume b in gg.:XX;
    then consider a being Point of D2 such that
w20:  a in XX and
w21:  b = gg.a by dd1,FUNCT_2:116;
    reconsider a1 = a, fa = fp as Point of T2 by TOPMETR:2;
w33: a in Y1 by w20,XBOOLE_0:def 3;
RR:  [p,a] in OK by b1,no11,ZFMISC_1:def 2;
     fa in X1 by w15,w17,ZFMISC_1:106;
     then [fa,a] in Z by w17,w33,ZFMISC_1:def 2;
    then [fa,a] in ZZ by RR,D0,D1,XBOOLE_0:def 3;
    then
w34: F. [fa,a1] in F.:ZZ by RR,D0,FUNCT_2:43;
E3: R qua Function" = R" by e2,TOPS_2:def 4;
r1: dom(R") = [#]T2 by e2,TOPS_2:62;
no51: gg.a1 in the carrier of S1 by dd1,FUNCT_2:7;
    Z c= WW by w13,w16,ZFMISC_1:92;
    then ZZ c= WW /\ [#]TD by XBOOLE_1:27;
    then F.:ZZ c= F.:W by t4,RELAT_1:156;
    then F. [fa,a1] in F.:W by w34;
    then R.(F. [fa,a1]) in R.:(R" .:V1) by q7,FUNCT_2:43;
    then (R*F). [fa,a1] in R.:(R" .:V1) by RR,D0,FUNCT_2:21;
    then (R*F). [fa,a1] in V1 by e2,r1,E3,Lm1,PARTFUN3:1;
    then gg.a in V1 by ww;
    hence b in V by bb,w4,w21,no51,XBOOLE_0:def 3;
  end;
  hence thesis by dd1,JGRAPH_2:20;
end;

theorem Th9:
  for n being non empty Nat
  for o, p being Point of TOP-REAL n,
  r being positive (real number) st p in Ball(o,r)
  holds RotateCircle(o,r,p) has_no_fixpoint
  proof 
  let n be non empty Nat;
  let o, p be Point of TOP-REAL n;
  let r be positive (real number);
  assume
Z4: p in Ball(o,r);
    set f = RotateCircle(o,r,p);
    let x be set;
    assume
a1: x in dom f;
    set S = Tcircle(o,r);
a6: dom f = the carrier of S by FUNCT_2:def 1;
    consider y being Point of TOP-REAL n such that
a2: x = y and
a3: f.x = HC(y,p,o,r) by a1,Z4,dCR;
a7: the carrier of S = Sphere(o,r) by TOPREALB:9;
    Sphere(o,r) c= cl_Ball(o,r) by TOPREAL9:17;
    then
a4: y is Point of Tdisk(o,r) by a1,a6,a2,a7,BROUWER:3;
    Ball(o,r) c= cl_Ball(o,r) by TOPREAL9:16;
    then
a5: p is Point of Tdisk(o,r) by Z4,BROUWER:3;
    Ball(o,r) misses Sphere(o,r) by TOPREAL9:19;
    then y <> p by a1,a2,a7,Z4,XBOOLE_0:3;
    hence f.x <> x by a2,a3,a4,a5,BROUWER:def 3;
  end;

begin :: Jordan's Curve Theorem

theorem Th10:
  U = P & U is_a_component_of (TOP-REAL 2)|C` &
  V is_a_component_of (TOP-REAL 2)|C` & U <> V
  implies Cl P misses V
  proof 
    assume that
a1: U = P and
a2: U is_a_component_of T2|C` and
a3: V is_a_component_of T2|C` and
a4: U <> V;
    assume Cl P meets V;
    then
a5: ex x being set st x in Cl P & x in V by XBOOLE_0:3;
    the carrier of T2|C` = C` by JORDAN1:1;
    then reconsider V1 = V as Subset of T2 by XBOOLE_1:1;
    reconsider T2C = T2|C` as non empty SubSpace of T2;
    T2C is locally_connected by JORDAN2C:89;
    then V is open by a3,CONNSP_2:21;
    then V1 is open by TSEP_1:17;
    then P meets V1 by a5,PRE_TOPC:def 13;
    hence thesis by a1,a2,a3,a4,CONNSP_1:37;
  end;

theorem Tarcwise:
  U is_a_component_of (TOP-REAL 2)|C` implies
  (TOP-REAL 2)|C`|U is arcwise_connected
  proof 
    set T = T2|C`;
    assume
a1: U is_a_component_of T;
    let a, b be Point of T|U;
a9: the carrier of T|U = U by JORDAN1:1;
x1: U <> {}T by a1,CONNSP_1:34;
    then 
g1: T|U is non empty by a9,STRUCT_0:def 1;
    per cases;
    suppose
k1:   a = b;
      reconsider TU = T|U as non empty TopSpace by x1,a9,STRUCT_0:def 1;
      reconsider a as Point of TU;
      reconsider f = I[01] --> a as map of I[01],T|U;
      take f;
      thus thesis by k1,TOPALG_3:5,BORSUK_1:def 17,def 18;
    end;
    suppose
s1:   a <> b;
a13:  T|U is SubSpace of T2 by TSEP_1:7;
      then reconsider a1 = a, b1 = b as Point of T2 by g1,TOPMETR:2;
      reconsider V = U as Subset of T2 by PRE_TOPC:39;
      V is_a_component_of C` by a1,CONNSP_1:def 6;
      then
w1:   V is open by SPRECT_3:18;
      U is connected by a1,CONNSP_1:def 5;
      then V is connected by CONNSP_1:24;
      then V is being_Region by w1,TOPREAL4:def 3;
      then consider P being Subset of T2 such that
a2:   P is_S-P_arc_joining a1,b1 and
a5:   P c= V by s1,x1,a9,TOPREAL4:30;
a10:  a1 in P by a2,TOPREAL4:4;
      P is_an_arc_of a1,b1 by a2,TOPREAL4:3;
      then consider g being map of I[01], T2|P such that
a3:   g is_homeomorphism and
a4:   g.0 = a & g.1 = b by TOPREAL1:def 2;
a12:  the carrier of T2|P = P by JORDAN1:1;
      then reconsider f = g as map of I[01], T|U by a9,a5,a10,FUNCT_2:9;
      take f;
a11:  g is continuous by a3,TOPS_2:def 5;
      T2|P is SubSpace of T|U by a12,a9,a5,a13,TSEP_1:4;
      hence f is continuous by a11,TOPMETR:7;
      thus f.0 = a & f.1 = b by a4;
    end;
  end;

::Th5_6: ex r ...
LmExr:
  for r being non negative (real number) st A is_an_arc_of p1,p2 &
  A is Subset of Tdisk(p,r)
  ex f being map of Tdisk(p,r), (TOP-REAL 2)|A st f is continuous & f|A = id A
  proof 
    let r be non negative (real number);
    set D = Tdisk(p,r);
    assume that
A1: A is_an_arc_of p1,p2 and
A2: A is Subset of D;
    reconsider A1 = A as non empty Subset of D by A1,A2,TOPREAL1:4;
    reconsider A2 = A as non empty Subset of T2 by A1,TOPREAL1:4;
    set TA = T2|A2;
    consider h being map of I[01],TA such that
A3: h is_homeomorphism and
    h.0 = p1 and
    h.1 = p2 by A1,TOPREAL1:def 2;
A4: h1 is_homeomorphism by TREAL_1:20;
    then
A5: h1 is continuous by TOPS_2:def 5;
    reconsider hh = h as map of C0,TA by TOPMETR:27;
A6: TA = D|A1 by Th3;
    then reconsider f = h1*hh" as map of D|A1,C1;
    A is closed by A1,JORDAN6:12;
    then
A7: A1 is closed by TSEP_1:12;
    hh" is continuous by A3,TOPMETR:27,TOPS_2:def 5;
    then h1*hh" is continuous by A5,TOPS_2:58;
    then consider g being continuous map of D,C1 such that
A8: g|A1 = f by A7,A6,TIETZE:25;
    reconsider R = hh*h1"*g as map of D,(TOP-REAL 2)|A;
    take R;
A9: hh is continuous by A3,TOPMETR:27,TOPS_2:def 5;
    h1" is continuous by A4,TOPS_2:def 5;
    then hh*h1" is continuous by A9,TOPS_2:58;
    hence R is continuous by TOPS_2:58;
A10: the carrier of TA = A1 by JORDAN1:1;
    dom R = the carrier of D by FUNCT_2:def 1;
    then
A11: dom(R|A) = A by A2,RELAT_1:91;
A12: dom id A = A by RELAT_1:71;
    now
      let a be set;
      assume
A13:   a in dom(R|A);
A14:   dom g = the carrier of D by FUNCT_2:def 1;
A15:   dom(h1*hh") = the carrier of TA by FUNCT_2:def 1;
A16:   h1 is one-to-one by A4,TOPS_2:def 5;
A17:   h1 is onto by A4,Th2;
A18:   hh is one-to-one onto by A3,Th2,TOPMETR:27,TOPS_2:def 5;
A19:   hh*h1"*(h1*hh") = hh*h1"*h1*hh" by RELAT_1:55
      .= hh*(h1"*h1)*hh" by RELAT_1:55
      .= hh*id C0*hh" by A16,A17,WAYBEL29:1
      .= hh*hh" by GRCAT_1:12
      .= id TA by A18,WAYBEL29:1;
      thus (R|A).a = R.a by A11,A13,FUNCT_1:72
      .= (hh*h1").(g.a) by A13,A14,FUNCT_1:23
      .= (hh*h1").((h1*hh").a) by A8,A13,A11,FUNCT_1:72
      .= (hh*h1"*(h1*hh")).a by A10,A13,A11,A15,FUNCT_1:23
      .= a by A13,A11,A10,A19,GRCAT_1:11
      .= (id A).a by A11,A13,FUNCT_1:35;
    end;
    hence thesis by A11,A12,FUNCT_1:9;
  end;

::Th5_6: ex f ...
LmExf:
:: definicja funkcji r
  for r being positive (real number) st A is_an_arc_of p1,p2 & A c= C &
  C c= Ball(p,r) & p in U & Cl P /\ P` c= A & P c= Ball(p,r)
  for f being map of Tdisk(p,r), (TOP-REAL 2)|A st f is continuous &
  f|A = id A &
:: komponenty
  U = P & U is_a_component_of (TOP-REAL 2)|C` & B = cl_Ball(p,r) \ {p}
  ex g being map of Tdisk(p,r), (TOP-REAL 2)|B st g is continuous &
  for x being Point of Tdisk(p,r) holds (x in Cl P implies g.x = f.x) &
  (x in P` implies g.x = x)
  proof 
    let r be positive (real number);
    set D = Tdisk(p,r);
    assume that
A1: A is_an_arc_of p1,p2 and
A2: A c= C and
A3: C c= Ball(p,r) and
A4: p in U and
A14: Cl P /\ P` c= A and
k2:  P c= Ball(p,r);
    let f be map of D, T2|A;
    assume that
A5: f is continuous and
A6: f|A = id A and
A7: U = P and
A8: U is_a_component_of T2|C` and
A10: B = cl_Ball(p,r) \ {p};
     reconsider B1 = B as non empty Subset of T2 by A10;
     reconsider T2B1 = T2|B1 as non empty SubSpace of T2;
A11: the carrier of T2|C` = C` by JORDAN1:1;
A12: the carrier of T2|A = A by JORDAN1:1;
A13: the carrier of D = cl_Ball(p,r) by BROUWER:3;
f1:  Ball(p,r) c= cl_Ball(p,r) by TOPREAL9:16;
A16: A <> {} by A1,TOPREAL1:4;
    reconsider A1 = A as non empty Subset of T2 by A1,TOPREAL1:4;
A17: not p in C by A11,A4,SUBSET_1:54;
A18: [#]D = the carrier of D by PRE_TOPC:12;
     |. p-p .| = 0 by TOPRNS_1:29;
     then
a02: p in [#]D by A13,A18,TOPREAL9:8;
y1:  P c= Cl P by PRE_TOPC:48;
     then reconsider F1 = [#]D /\ Cl P as non empty Subset of D
     by a02,A18,A4,A7,XBOOLE_0:def 3,XBOOLE_1:17;
a13: Sphere(p,r) c= cl_Ball(p,r) by TOPREAL9:17;
a23: Ball(p,r) misses Sphere(p,r) by TOPREAL9:19;
     consider e being Point of T2 such that
w2:  e in Sphere(p,r) by SUBSET_1:10;
     not e in P by w2,a23,k2,XBOOLE_0:3;
     then e in P` by SUBSET_1:50;
     then reconsider F3 = [#]D /\ P` as non empty Subset of D
     by A18,w2,A13,a13,XBOOLE_0:def 3,XBOOLE_1:17;
     reconsider T1 = D|F1 as non empty SubSpace of D;
     reconsider T3 = D|F3 as non empty SubSpace of D;
A19: the carrier of T1 = F1 by JORDAN1:1;
A20: the carrier of T3 = F3 by JORDAN1:1;
A21: the carrier of T2B1 = B1 by JORDAN1:1;
A22: A c= B
    proof
      let a be set;
      assume a in A;
      then
k1:   a in C by A2;
      then a in Ball(p,r) by A3;
      hence thesis by A10,A17,k1,f1,ZFMISC_1:64;
    end;
A23: F3 c= B
    proof
      let a be set;
      assume
A24:   a in F3;
      then a in P` by XBOOLE_0:def 3;
      then not a in P by SUBSET_1:54;
      hence thesis by A24,A13,A4,A7,A10,ZFMISC_1:64;
    end;
    f|F1 is Function of F1,A by A12,A16,FUNCT_2:38;
    then reconsider f1 = f|F1 as map of T1,T2B1 by A19,A21,A16,A22,FUNCT_2:9;
    reconsider g1 = id F3 as map of T3,T2B1 by A20,A21,A23,FUNCT_2:9;
    the carrier of T1 = [#]T1 & the carrier of T3 = [#]T3 by PRE_TOPC:12;
    then
A25: F1 = [#]T1 & F3 = [#]T3 by JORDAN1:1;
A26: [#]T1 \/ [#]T3 = [#]D
    proof
      F1 c= [#]D & F3 c= [#]D by XBOOLE_1:17;
      hence [#]T1 \/ [#]T3 c= [#]D by A25,XBOOLE_1:8;
      let p be set;
      assume
a1:   p in [#]D;
      per cases;
      suppose p in P;
        then p in F1 by y1,a1,XBOOLE_0:def 3;
        hence thesis by A25,XBOOLE_0:def 2;
      end;
      suppose not p in P;
        then p in P` by a1,A13,A18,SUBSET_1:50;
        then p in F3 by a1,XBOOLE_0:def 3;
        hence thesis by A25,XBOOLE_0:def 2;
      end;
    end;
    reconsider DT = [#]D as closed Subset of T2
    by A18,BORSUK_1:def 14,TSEP_1:1;
    DT /\ Cl P is closed by TOPS_1:35;
    then
A27: F1 is closed by TSEP_1:8;
    P is_a_component_of C`
    proof
      take U;
      thus thesis by A7,A8;
    end;
    then P is open by SPRECT_3:18;
    then P` is closed by TOPS_1:30;
    then DT /\ P` is closed by TOPS_1:35;
    then
A28: F3 is closed by TSEP_1:8;
    reconsider f2 = f|F1 as map of T1,T2|A1 by A19,FUNCT_2:38;
A29: T2|A1 is SubSpace of T2B1 by A21,A22,A12,TSEP_1:4;
    T3 is SubSpace of T2 by TSEP_1:7;
    then
A30: T3 is SubSpace of T2B1 by A20,A21,A23,TSEP_1:4;
    f2 is continuous by A5,TOPMETR:10;
    then
A31: f1 is continuous by A29,TOPMETR:7;
    reconsider g2 = id F3 as map of T3,T3 by A20;
    g2 = id T3 by A20,GRCAT_1:def 11;
    then
A32: g1 is continuous by A30,TOPMETR:7;
A33: for x being set st x in Cl P & x in P` holds f.x = x
    proof
      let x be set;
      assume x in Cl P & x in P`;
      then
A34:   x in Cl P /\ P` by XBOOLE_0:def 3;
      then (id A).x = x by A14,FUNCT_1:35;
      hence f.x = x by A6,A34,A14,FUNCT_1:72;
    end;
    for x being set st x in [#]T1 /\ [#]T3 holds f1.x = g1.x
    proof
      let x be set;
      assume x in [#]T1 /\ [#]T3;
      then
A35:   x in [#]T1 & x in [#]T3 by XBOOLE_0:def 3;
      then x in Cl P & x in P` by A25,XBOOLE_0:def 3;
      then
A36:   f.x = x by A33;
      thus f1.x = f.x by A25,A35,FUNCT_1:72
      .= g1.x by A25,A35,A36,FUNCT_1:35;
    end;
    then consider g being map of D,T2|B such that
A37: g = f1+*g1 and
A38: g is continuous by A25,A26,A27,A28,A31,A32,JGRAPH_2:9;
    take g;
    thus g is continuous by A38;
    let x be Point of D;
A39: dom g1 = the carrier of T3 by FUNCT_2:def 1;
    hereby
      assume
A40:   x in Cl P;
      then
A41:   x in F1 by A18,XBOOLE_0:def 3;
      per cases;
      suppose not x in dom g1;
        hence g.x = f1.x by A37,FUNCT_4:12
        .= f.x by A41,FUNCT_1:72;
      end;
      suppose
A42:     x in dom g1;
        then
A43:     x in Cl P & x in P` by A40,A20,XBOOLE_0:def 3;
        thus g.x = g1.x by A42,A37,FUNCT_4:14
        .= x by A42,A20,FUNCT_1:35
        .= f.x by A33,A43;
      end;
    end;
    assume x in P`;
    then
A44: x in F3 by A18,XBOOLE_0:def 3;
    thus g.x = g1.x by A39,A44,A20,A37,FUNCT_4:14
    .= x by A44,FUNCT_1:35;
  end;

LmExf2:
:: definicja funkcji r
  for A being non empty Subset of T2
  for P1 being Subset of T2 st U <> V
  for r being positive (real number) st A c= C &
  C c= Ball(p,r) & p in V & Cl P /\ P` c= A &
  Ball(p,r) meets P
  for f being map of Tdisk(p,r), (TOP-REAL 2)|A st f is continuous &
  f|A = id A &
:: komponenty
  U = P & U is_a_component_of (TOP-REAL 2)|C` &
  V is_a_component_of T2|C` & B = cl_Ball(p,r) \ {p}
  ex g being map of Tdisk(p,r), (TOP-REAL 2)|B st g is continuous &
  for x being Point of Tdisk(p,r) holds (x in Cl P implies g.x = x) &
  (x in P` implies g.x = f.x)
  proof 
    let A be non empty Subset of T2;
    let P1 be Subset of T2 such that
l11: U <> V;
    let r be positive (real number);
    set D = Tdisk(p,r);
    assume that
A2: A c= C and
A3: C c= Ball(p,r) and
A4: p in V and
A14: Cl P /\ P` c= A and
a2:  Ball(p,r) meets P;
    let f be map of D, T2|A;
    assume that
A5: f is continuous and
A6: f|A = id A and
A7: U = P and
A8: U is_a_component_of T2|C` and
aA8: V is_a_component_of T2|C` and
A10: B = cl_Ball(p,r) \ {p};
     reconsider B1 = B as non empty Subset of T2 by A10;
     reconsider T2B1 = T2|B1 as non empty SubSpace of T2;
A11: the carrier of T2|C` = C` by JORDAN1:1;
A12: the carrier of T2|A = A by JORDAN1:1;
A13: the carrier of D = cl_Ball(p,r) by BROUWER:3;
f1:  Ball(p,r) c= cl_Ball(p,r) by TOPREAL9:16;
A17: not p in C by A11,A4,SUBSET_1:54;
A18: [#]D = the carrier of D by PRE_TOPC:12;
     |. p-p .| = 0 by TOPRNS_1:29;
     then
a02: p in [#]D by A13,A18,TOPREAL9:8;
y1:  P c= Cl P by PRE_TOPC:48;
l40: ex j being set st j in Ball(p,r) & j in P by a2,XBOOLE_0:3;
     reconsider F1 = [#]D /\ Cl P as non empty Subset of D
     by A13,f1,l40,A18,y1,XBOOLE_0:def 3,XBOOLE_1:17;
     now
       assume
a1:    p in P;
       U misses V by l11,A8,aA8,CONNSP_1:37;
       hence contradiction by A4,A7,a1,XBOOLE_0:3;
     end;
     then p in P` by SUBSET_1:50;
     then reconsider F3 = [#]D /\ P` as non empty Subset of D
     by a02,A18,XBOOLE_0:def 3,XBOOLE_1:17;
    set T1 = D|F1;
    set T3 = D|F3;
A19: the carrier of T1 = F1 by JORDAN1:1;
A20: the carrier of T3 = F3 by JORDAN1:1;
A21: the carrier of T2|B1 = B1 by JORDAN1:1;
A22: A c= B
    proof
      let a be set;
      assume a in A;
      then
k1:   a in C by A2;
      then a in Ball(p,r) by A3;
      hence thesis by A10,A17,k1,f1,ZFMISC_1:64;
    end;
A23: F1 c= B
    proof
      let a be set;
      assume
A24:  a in F1;
      then
m01:  a in Cl P by XBOOLE_0:def 3;
      now
        assume
l12:    p in Cl P;
        Cl P misses V by l11,A7,A8,aA8,Th10;
        hence contradiction by A4,l12,XBOOLE_0:3;
      end;
      hence thesis by m01,A24,A13,A10,ZFMISC_1:64;
    end;
    reconsider f1 = id F1 as map of T1,T2B1
    by A19,A21,A23,FUNCT_2:9;
    f|F3 is Function of F3,A by A12,FUNCT_2:38;
    then reconsider g1 = f|F3 as map of T3,T2B1
    by A20,A21,A22,FUNCT_2:9;
l21: the carrier of T1 = [#]T1 & the carrier of T3 = [#]T3 by PRE_TOPC:12;
    then
A25: F1 = [#]T1 & F3 = [#]T3 by JORDAN1:1;
A26: [#]T1 \/ [#]T3 = [#]D
    proof
      F1 c= [#]D & F3 c= [#]D by XBOOLE_1:17;
      hence [#]T1 \/ [#]T3 c= [#]D by A25,XBOOLE_1:8;
      let p be set;
      assume
a1:   p in [#]D;
      per cases;
      suppose p in P;
        then p in F1 by y1,a1,XBOOLE_0:def 3;
        hence thesis by A25,XBOOLE_0:def 2;
      end;
      suppose not p in P;
        then p in P` by a1,A13,A18,SUBSET_1:50;
        then p in F3 by a1,XBOOLE_0:def 3;
        hence thesis by A25,XBOOLE_0:def 2;
      end;
    end;
    reconsider DT = [#]D as closed Subset of T2
    by A18,BORSUK_1:def 14,TSEP_1:1;
    DT /\ Cl P is closed by TOPS_1:35;
    then
A27: F1 is closed by TSEP_1:8;
    P is_a_component_of C`
    proof
      take U;
      thus thesis by A7,A8;
    end;
    then P is open by SPRECT_3:18;
    then P` is closed by TOPS_1:30;
    then DT /\ P` is closed by TOPS_1:35;
    then
A28: F3 is closed by TSEP_1:8;
l25: id T1 = id F1 by l21,A25,GRCAT_1:def 11;
     T1 is SubSpace of T2 by TSEP_1:7;
     then T1 is SubSpace of T2B1 by A19,A21,A23,TSEP_1:4;
     then
A31: f1 is continuous by l25,TOPMETR:7;
l30: T2|A is SubSpace of T2B1 by A12,A21,A22,TSEP_1:4;
     reconsider g2 = g1 as map of T3,T2|A by A20,FUNCT_2:38;
     g2 is continuous by A5,TOPMETR:10;
     then
A32: g1 is continuous by l30,TOPMETR:7;
A33: for x being set st x in Cl P & x in P` holds f.x = x
    proof
      let x be set;
      assume x in Cl P & x in P`;
      then
A34:   x in Cl P /\ P` by XBOOLE_0:def 3;
      then (id A).x = x by A14,FUNCT_1:35;
      hence f.x = x by A6,A34,A14,FUNCT_1:72;
    end;
    for x being set st x in [#]T1 /\ [#]T3 holds f1.x = g1.x
    proof
      let x be set;
      assume x in [#]T1 /\ [#]T3;
      then
A35:  x in [#]T1 & x in [#]T3 by XBOOLE_0:def 3;
      then x in Cl P & x in P` by A25,XBOOLE_0:def 3;
      then
A36:   f.x = x by A33;
      thus f1.x = x by A25,A35,FUNCT_1:35
      .= g1.x by A25,A35,A36,FUNCT_1:72;
    end;
    then consider g being map of D,T2|B such that
A37: g = f1+*g1 and
A38: g is continuous by A25,A26,A27,A28,A31,A32,JGRAPH_2:9;
    take g;
    thus g is continuous by A38;
    let x be Point of D;
A39: dom g1 = the carrier of T3 by FUNCT_2:def 1;
    hereby
      assume
A40:   x in Cl P;
      then
A41:   x in F1 by A18,XBOOLE_0:def 3;
      per cases;
      suppose not x in dom g1;
        hence g.x = f1.x by A37,FUNCT_4:12
        .= x by A41,FUNCT_1:35;
      end;
      suppose
A42:    x in dom g1;
        then
A43:    x in Cl P & x in P` by A40,A20,XBOOLE_0:def 3;
        thus g.x = g1.x by A42,A37,FUNCT_4:14
        .= f.x by A42,A20,FUNCT_1:72
        .= x by A33,A43;
      end;
    end;
    assume x in P`;
    then
A44: x in F3 by A18,XBOOLE_0:def 3;
    thus g.x = g1.x by A39,A44,A20,A37,FUNCT_4:14
    .= f.x by A44,FUNCT_1:72;
  end;

theorem LEMAT56:
  U = P & U is_a_component_of (TOP-REAL 2)|C` implies C = Fr P
  proof 
    assume that
Z3: U = P and
Z0: U is_a_component_of T2|C` and
Z2: C <> Fr P;
h1: the carrier of T2|C` = C` by JORDAN1:1;
    reconsider T2C = T2|C` as non empty SubSpace of T2;
l07: T2C is locally_connected by JORDAN2C:89;
    then U is open by Z0,CONNSP_2:21;
    then reconsider P as open Subset of T2 by Z3,TSEP_1:17;
u2: Fr P = Cl P /\ Cl P` by TOPS_1:def 2
    .= Cl P /\ P` by PRE_TOPC:52;
    set Z = {X where X is Subset of T2|C`: X is_a_component_of T2|C` & X <> U};
    set V = union Z;
c12: V \/ U \/ C = the carrier of T2
     proof
a1:    V c= the carrier of T2
       proof
         let a be set;
         assume a in V;
         then consider A being set such that
a2:      a in A and
a3:      A in Z by TARSKI:def 4;
         ex X being Subset of T2|C` st X = A & X is_a_component_of T2|C` &
         X <> U by a3;
         hence thesis by h1,a2,TARSKI:def 3;
       end;
       U c= the carrier of T2 by h1,XBOOLE_1:1;
       then V \/ U c= the carrier of T2 by a1,XBOOLE_1:8;
       hence V \/ U \/ C c= the carrier of T2 by XBOOLE_1:8;
       let a be set;
       assume
a4:    a in the carrier of T2;
       per cases;
       suppose a in C;
         hence thesis by XBOOLE_0:def 2;
       end;
       suppose not a in C;
         then reconsider a as Point of T2|C` by h1,a4,SUBSET_1:50;
a5:      a in skl a by CONNSP_1:40;
         per cases;
         suppose skl a = U;
           then a in V \/ U by a5,XBOOLE_0:def 2;
           hence thesis by XBOOLE_0:def 2;
         end;
         suppose
a6:        skl a <> U;
           skl a is_a_component_of T2|C` by CONNSP_1:43;
           then skl a in Z by a6;
           then a in V by a5,TARSKI:def 4;
           then a in V \/ U by XBOOLE_0:def 2;
           hence thesis by XBOOLE_0:def 2;
         end;
       end;
     end;
c6: P misses P` by SUBSET_1:26;
    Fr P c= C
    proof
      let a be set;
      assume
C5:   a in Fr P;
      then
c5:   a in Cl P & a in P` by u2,XBOOLE_0:def 3;
      assume not a in C;
      then a in V \/ U by C5,c12,XBOOLE_0:def 2;
      then a in V or a in U by XBOOLE_0:def 2;
      then consider O being set such that
c8:   a in O and
c9:   O in Z by c6,c5,Z3,XBOOLE_0:3,TARSKI:def 4;
      consider X being Subset of T2|C` such that
c7:   X = O and
c10:  X is_a_component_of T2|C` and
c11:  X <> U by c9;
      Cl P misses X by Z3,Z0,c10,c11,Th10;
      hence thesis by c5,c8,c7,XBOOLE_0:3;
    end;
    then consider p1, p2, A such that
a1:  A is_an_arc_of p1,p2 and
a2:  Fr P c= A and
ee:  A c= C by Z2,BORSUK_4:84;
l50: U <> {}(T2|C`) by Z0,CONNSP_1:34;
    per cases;
    suppose P is Bounded;
      then reconsider P as Bounded Subset of T2;
      consider p being set such that
a3:   p in U by l50,XBOOLE_0:def 1;
      reconsider p as Point of T2 by a3,Z3;
b4:   P \/ C is Bounded by TOPREAL6:76;
      then reconsider PC = P \/ C as bounded Subset of Euclid 2
      by JORDAN2C:def 2;
      consider r being positive (real number) such that
b3:   PC c= Ball(p,r) by b4,Th11;
      C c= PC by XBOOLE_1:7;
      then
a4:   C c= Ball(p,r) by b3,XBOOLE_1:1;
      set D = Tdisk(p,r);
      set S = Tcircle(p,r);
      set B = cl_Ball(p,r) \ {p};
a15:  the carrier of S = Sphere(p,r) by TOPREALB:9;
a16:  the carrier of D = cl_Ball(p,r) by BROUWER:3;
a17:  Sphere(p,r) c= cl_Ball(p,r) by TOPREAL9:17;
a23:  Ball(p,r) misses Sphere(p,r) by TOPREAL9:19;
a18:  Ball(p,r) c= cl_Ball(p,r) by TOPREAL9:16;
      A c= Ball(p,r) by ee,a4,XBOOLE_1:1;
      then A is Subset of D by a16,a18,XBOOLE_1:1;
      then consider R being map of D, T2|A such that
a6:   R is continuous and
a7:   R|A = id A by a1,LmExr;
      P c= PC by XBOOLE_1:7;
      then
w1:   P c= Ball(p,r) by b3,XBOOLE_1:1;
      then consider f being map of D, T2|B such that
a9:   f is continuous and
a10:  for x being Point of D holds (x in Cl P implies f.x = R.x) &
      (x in P` implies f.x = x)
      by ee,a1,a2,u2,a3,a4,a6,a7,Z3,Z0,LmExf;
      set g = DiskProj(p,r,p);
      set h = RotateCircle(p,r,p);
a12:  S is SubSpace of D by a15,a16,a17,TSEP_1:4;
      reconsider F = h*(g*f) as map of D,D by a15,a16,a17,FUNCT_2:9;
      p is Point of D by Th6;
      then g is continuous by ThDPC;
      then
a11:  g*f is continuous by a9,TOPS_2:58;
      |. p-p .| = 0 by TOPRNS_1:29;
      then
a31:  p in Ball(p,r) by TOPREAL9:7;
      then h is continuous by ThRCC;
      then h*(g*f) is continuous by a11,TOPS_2:58;
      then
a13:  F is continuous by a12,TOPMETR:7;
      now
        let x be set;
        per cases;
        suppose
a20:      x in dom F;
a22:      Ball(p,r) \/ Sphere(p,r) = cl_Ball(p,r) by TOPREAL9:18;
          now per cases by a16,a20,a22,XBOOLE_0:def 2;
            suppose
a26:          x in Ball(p,r);
              F.x in the carrier of S by a20,FUNCT_2:7;
              hence F.x <> x by a15,a23,a26,XBOOLE_0:3;
            end;
            suppose
a27:          x in Sphere(p,r);
a28:          dom f = the carrier of D by FUNCT_2:def 1;
              not x in P by a27,a23,w1,XBOOLE_0:3;
              then
a29:          x in P` by a27,SUBSET_1:50;
a30:          g|Sphere(p,r) = id Sphere(p,r) by a31,Th8;
              h has_no_fixpoint by a31,Th9;
              then
a32:          not x is_a_fixpoint_of h by ABIAN:def 5;
a33:          dom h = the carrier of S by FUNCT_2:def 1;
              F.x = h.((g*f).x) by a20,FUNCT_1:22
              .= h.(g.(f.x)) by a16,a17,a27,a28,FUNCT_1:23
              .= h.(g.x) by a10,a29,a27,a16,a17
              .= h.((id Sphere(p,r)).x) by a27,a30,FUNCT_1:72
              .= h.x by a27,FUNCT_1:35;
              hence F.x <> x by a27,a15,a32,a33,ABIAN:def 3;
            end;
          end;
          hence not x is_a_fixpoint_of F by ABIAN:def 3;
        end;
        suppose not x in dom F;
          hence not x is_a_fixpoint_of F by ABIAN:def 3;
        end;
      end;
      then not F has_a_fixpoint by ABIAN:def 5;
      hence thesis by a13,BROUWER:14;
    end;
    suppose
l13:  not P is Bounded;
      consider p being set such that
l01:  p in BDD C by XBOOLE_0:def 1;
      BDD C = union{B where B is Subset of T2: B is_inside_component_of C}
      by JORDAN2C:def 5;
      then consider Z being set such that
l02:  p in Z and
l03:  Z in {B where B is Subset of T2: B is_inside_component_of C}
      by l01,TARSKI:def 4;
      consider P1 being Subset of T2 such that
l04:  Z = P1 and
l05:  P1 is_inside_component_of C by l03;
      consider U1 being Subset of T2|C` such that
l06:  U1 = P1 & U1 is_a_component_of T2|C` and
      U1 is bounded Subset of Euclid 2 by l05,JORDAN2C:17;
      U1 is open by l06,l07,CONNSP_2:21;
      then reconsider P1 as non empty open Bounded Subset of T2
      by l02,l04,l05,l06,TSEP_1:17,JORDAN2C:def 3;
      reconsider p as Point of T2 by l02,l04;
a3:   p in P1 by l02,l04;
b4:   P1 \/ C is Bounded by TOPREAL6:76;
      then reconsider PC = P1 \/ C as bounded Subset of Euclid 2
      by JORDAN2C:def 2;
      consider rv being positive (real number) such that
b3:   PC c= Ball(p,rv) by b4,Th11;
      not P c= Ball(p,rv) by l13,JORDAN2C:16;
      then consider u being set such that
l51:  u in P and
o05:  not u in Ball(p,rv) by TARSKI:def 3;
      reconsider u as Point of T2 by l51;
      set r = |.u-p.|;
      P misses P1 by l13,l06,Z0,Z3,CONNSP_1:37;
      then p <> u by l02,l04,l51,XBOOLE_0:3;
      then reconsider r as non empty non negative (real number) by TOPRNS_1:29;
o04:  r >= rv by o05,TOPREAL9:7; 
      then Ball(p,rv) c= Ball(p,r) by TBB;
      then
l55:  PC c= Ball(p,r) by b3,XBOOLE_1:1;
o01:  Fr Ball(p,r) = Sphere(p,r) by ThFrB;
      u in Sphere(p,r) by TOPREAL9:9;
      then
l10:  P meets Ball(p,r) by l51,o01,TOPS_1:61;
l73:  C c= PC by XBOOLE_1:7;
      then
a4:   C c= Ball(p,r) by l55,XBOOLE_1:1;
      set D = Tdisk(p,r);
      set S = Tcircle(p,r);
      set B = cl_Ball(p,r) \ {p};
a15:  the carrier of S = Sphere(p,r) by TOPREALB:9;
a16:  the carrier of D = cl_Ball(p,r) by BROUWER:3;
a17:  Sphere(p,r) c= cl_Ball(p,r) by TOPREAL9:17;
a23:  Ball(p,r) misses Sphere(p,r) by TOPREAL9:19;
a18:  Ball(p,r) c= cl_Ball(p,r) by TOPREAL9:16;
      A c= Ball(p,r) by ee,a4,XBOOLE_1:1;
      then A is Subset of D by a16,a18,XBOOLE_1:1;
      then consider R being map of D, T2|A such that
a6:   R is continuous and
a7:   R|A = id A by a1,LmExr;
      p1 in A by a1,TOPREAL1:4;
      then consider f being map of D, T2|B such that
a9:   f is continuous and
a10:  for x being Point of D holds (x in Cl P implies f.x = x) &
      (x in P` implies f.x = R.x)
      by l10,ee,a2,u2,a3,a4,a6,a7,Z3,Z0,l06,l13,LmExf2;
      set g = DiskProj(p,r,p);
      set h = RotateCircle(p,r,p);
a12:  S is SubSpace of D by a15,a16,a17,TSEP_1:4;
      reconsider F = h*(g*f) as map of D,D by a15,a16,a17,FUNCT_2:9;
      p is Point of D by Th6;
      then g is continuous by ThDPC;
      then
a11:  g*f is continuous by a9,TOPS_2:58;
      |. p-p .| = 0 by TOPRNS_1:29;
      then
a31:  p in Ball(p,r) by TOPREAL9:7;
      then h is continuous by ThRCC;
      then h*(g*f) is continuous by a11,TOPS_2:58;
      then
a13:  F is continuous by a12,TOPMETR:7;
      now
        let x be set;
        per cases;
        suppose
a20:      x in dom F;
a22:      Ball(p,r) \/ Sphere(p,r) = cl_Ball(p,r) by TOPREAL9:18;
          now per cases by a16,a20,a22,XBOOLE_0:def 2;
            suppose
a26:          x in Ball(p,r);
              F.x in the carrier of S by a20,FUNCT_2:7;
              hence F.x <> x by a15,a23,a26,XBOOLE_0:3;
            end;
            suppose
a27:          x in Sphere(p,r);
a28:          dom f = the carrier of D by FUNCT_2:def 1;
y1:           P c= Cl P by PRE_TOPC:48;
              set SS = Sphere(p,r);
              SS c= C`
              proof
                let a be set;
                assume
l71:            a in SS;
                assume not a in C`;
                then
l72:            a in C by l71,SUBSET_1:50;
                reconsider a as Point of T2 by l71;
                a in PC by l73,l72;
                then |.a-p.| < rv by b3,TOPREAL9:7;
                hence contradiction by o04,l71,TOPREAL9:9;
              end;
              then reconsider SS as Subset of T2|C` by JORDAN1:1;
l12:          u in SS by TOPREAL9:9;
              SS is connected by CONNSP_1:24;
              then SS misses U or SS c= U by Z0,CONNSP_1:38;
              then
a29:          x in P by a27,l12,Z3,l51,XBOOLE_0:3;
a30:          g|Sphere(p,r) = id Sphere(p,r) by a31,Th8;
              h has_no_fixpoint by a31,Th9;
              then
a32:          not x is_a_fixpoint_of h by ABIAN:def 5;
a33:          dom h = the carrier of S by FUNCT_2:def 1;
              F.x = h.((g*f).x) by a20,FUNCT_1:22
              .= h.(g.(f.x)) by a16,a17,a27,a28,FUNCT_1:23
              .= h.(g.x) by a10,a29,a27,a16,a17,y1
              .= h.((id Sphere(p,r)).x) by a27,a30,FUNCT_1:72
              .= h.x by a27,FUNCT_1:35;
              hence F.x <> x by a27,a15,a32,a33,ABIAN:def 3;
            end;
          end;
          hence not x is_a_fixpoint_of F by ABIAN:def 3;
        end;
        suppose not x in dom F;
          hence not x is_a_fixpoint_of F by ABIAN:def 3;
        end;
      end;
      then not F has_a_fixpoint by ABIAN:def 5;
      hence thesis by a13,BROUWER:14;
    end;
  end;

set rp = 1;
set rl = -rp;
set rg = 3;
set rd = -rg;
set a = |[rl,0]|;
set b = |[rp,0]|;
set c = |[0,rg]|;
set d = |[0,rd]|;
set lg = |[rl,rg]|;
set pg = |[rp,rg]|;
set ld = |[rl,rd]|;
set pd = |[rp,rd]|;
set R = closed_inside_of_rectangle(rl,rp,rd,rg);
set dR = rectangle(rl,rp,rd,rg);
set TR = Trectangle(rl,rp,rd,rg);

Lm4: a`1 = rl by EUCLID:56;
Lm5: b`1 = rp by EUCLID:56;
Lm6: a`2 = 0 by EUCLID:56;
Lm7: b`2 = 0 by EUCLID:56;
Lm10: c`1 = 0 by EUCLID:56;
Lm11: c`2 = rg by EUCLID:56;
Lm12: d`1 = 0 by EUCLID:56;
Lm13: d`2 = rd by EUCLID:56;
Lm70: lg`1 = rl by EUCLID:56;
Lm71: lg`2 = rg by EUCLID:56;
Lm72: ld`1 = rl by EUCLID:56;
Lm73: ld`2 = rd by EUCLID:56;
Lm74: pg`1 = rp by EUCLID:56;
Lm75: pg`2 = rg by EUCLID:56;
Lm76: pd`1 = rp by EUCLID:56;
Lm77: pd`2 = rd by EUCLID:56;

Lm67d: ld = |[ld`1,ld`2]| by EUCLID:57;
Lm67g: lg = |[lg`1,lg`2]| by EUCLID:57;
Lm67D: pd = |[pd`1,pd`2]| by EUCLID:57;
Lm67G: pg = |[pg`1,pg`2]| by EUCLID:57;
       
Lm32a: dR = (LSeg(ld,lg) \/ LSeg(lg,pg)) \/ (LSeg(pg,pd) \/ LSeg(pd,ld))
       by SPPOL_2:def 3;
       LSeg(ld,lg) c= LSeg(ld,lg) \/ LSeg(lg,pg) &
       LSeg(ld,lg) \/ LSeg(lg,pg) c= dR by Lm32a,XBOOLE_1:7;
       then
Lm32b: LSeg(ld,lg) c= dR by XBOOLE_1:1;
       LSeg(lg,pg) c= LSeg(ld,lg) \/ LSeg(lg,pg) &
       LSeg(ld,lg) \/ LSeg(lg,pg) c= dR by Lm32a,XBOOLE_1:7;
       then
Lm32c: LSeg(lg,pg) c= dR by XBOOLE_1:1;
       LSeg(pg,pd) c= LSeg(pg,pd) \/ LSeg(pd,ld) &
       LSeg(pg,pd) \/ LSeg(pd,ld) c= dR by Lm32a,XBOOLE_1:7;
       then
Lm32d: LSeg(pg,pd) c= dR by XBOOLE_1:1;
       LSeg(pd,ld) c= LSeg(pg,pd) \/ LSeg(pd,ld) &
       LSeg(pg,pd) \/ LSeg(pd,ld) c= dR by Lm32a,XBOOLE_1:7;
       then
Lm32e: LSeg(pd,ld) c= dR by XBOOLE_1:1;

Lm32f: LSeg(ld,lg) is vertical by Lm70,Lm72,SPPOL_1:37;
Lm32F: LSeg(pd,pg) is vertical by Lm74,Lm76,SPPOL_1:37;
Lm32g: LSeg(a,lg) is vertical by Lm4,Lm70,SPPOL_1:37;
Lm32G: LSeg(a,ld) is vertical by Lm4,Lm72,SPPOL_1:37;
Lm33a: LSeg(b,pg) is vertical by Lm5,Lm74,SPPOL_1:37;
Lm33b: LSeg(b,pd) is vertical by Lm5,Lm76,SPPOL_1:37;

Lm32ix: LSeg(ld,d) is horizontal by Lm73,Lm13,SPPOL_1:36;
Lm32iy: LSeg(pd,d) is horizontal by Lm77,Lm13,SPPOL_1:36;
Lm32i: LSeg(lg,c) is horizontal by Lm71,Lm11,SPPOL_1:36;
Lm32I: LSeg(pg,c) is horizontal by Lm75,Lm11,SPPOL_1:36;
Lm80a: LSeg(lg,pg) is horizontal by Lm71,Lm75,SPPOL_1:36;
Lm80A: LSeg(ld,pd) is horizontal by Lm73,Lm77,SPPOL_1:36;

Lm32h: LSeg(a,lg) c= LSeg(ld,lg)
       by Lm32f,Lm32g,Lm4,Lm72,Lm6,Lm71,Lm73,JORDAN15:5;
Lm32hy: LSeg(a,ld) c= LSeg(ld,lg)
        by Lm32f,Lm32G,Lm72,Lm6,Lm71,Lm73,JORDAN15:5;
Lm32H:  LSeg(b,pg) c= LSeg(pd,pg)
        by Lm33a,Lm32F,Lm5,Lm76,Lm75,Lm77,Lm7,JORDAN15:5;
Lm32Hy: LSeg(b,pd) c= LSeg(pd,pg)
        by Lm33b,Lm32F,Lm76,Lm7,Lm75,Lm77,JORDAN15:5;


Lm32:  dR = {p where p is Point of T2: p`1 = rl & p`2 <= rg & p`2 >= rd or
       p`1 <= rp & p`1 >= rl & p`2 = rg or
       p`1 <= rp & p`1 >= rl & p`2 = rd or
       p`1 = rp & p`2 <= rg & p`2 >= rd} by SPPOL_2:58;
       then
Lm30:  c in dR by Lm10,Lm11;
Lm31:  d in dR by Lm12,Lm13,Lm32;
Lm9:   (1+1)^2 = 1^2 + 2 * 1 * 1 + 1^2 by SQUARE_1:63
       .= 4 by SQUARE_1:59;
Lm14a: (2+1)^2 = 2^2 + 2 * 2 * 1 + 1^2 by SQUARE_1:63
       .= 4 + 4 + 1 by Lm9,SQUARE_1:59;
       then
Lms9:  sqrt(9) = 3 by SQUARE_1:def 4;
Lm8:   dist(a,b) = sqrt ((a`1-b`1)^2 + (a`2-b`2)^2) by GOBRD14:9
       .= --2 by Lm4,Lm5,Lm6,Lm7,SQUARE_1:60,90;
Lm0:   
       R = {p: rl <= p`1 & p`1 <= rp & rd <= p`2 & p`2 <= rg}
       by JGRAPH_6:def 2;

theorem Tsim:
  for h being Homeomorphism of TOP-REAL 2 holds
  h.:C is being_simple_closed_curve
  proof 
    let h be Homeomorphism of T2;
    consider f being map of T2|R^2-unit_square, T2|C such that
a1: f is_homeomorphism by TOPREAL2:def 1;
    reconsider g = h|C as map of T2|C,T2|(h.:C) by JORDAN24:12;
    take g*f;
    g is_homeomorphism by JORDAN24:14;
    hence g*f is_homeomorphism by a1,TOPS_2:71;
  end;
  
theorem Lm1: 
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies
  P c= closed_inside_of_rectangle(-1,1,-3,3)
  proof 
    assume that
Z0: a in P and
Z1: b in P and
Z2: for x, y being Point of TOP-REAL 2 st x in P & y in P holds
    dist(a,b) >= dist(x,y);
    let p be set;
    assume
a6: p in P;
    then reconsider p as Point of TOP-REAL 2;
b4: dist(a,p) = sqrt((rl-p`1)^2 + (0-p`2)^2) by Lm4,Lm6,GOBRD14:9
    .= sqrt((rl-p`1)^2 + (-p`2)^2)
    .= sqrt((rl-p`1)^2 + p`2^2) by SQUARE_1:61;
nn: now
      assume 
q3:   9 < p`2^2;
      0 <= (rl-p`1)^2 by SQUARE_1:72;
      then 0+9 < (rl-p`1)^2+p`2^2 by q3,XREAL_1:10;
      then 3 < sqrt((rl-p`1)^2+p`2^2) by Lms9,SQUARE_1:95;
      then 2 < sqrt((rl-p`1)^2+p`2^2) by XREAL_1:2;
      hence contradiction by b4,Z0,Z2,a6,Lm8;
    end;
a1: now
      assume
b1:   rl > p`1;
      then LSeg(p,b) meets Vertical_Line(rl) by Lm5,TSV;
      then consider x being set such that
a8:   x in LSeg(p,b) and
a7:   x in Vertical_Line(rl) by XBOOLE_0:3;
      reconsider x as Point of T2 by a8;
b3:   x`1 = rl by a7,JORDAN6:34;
a4:   dist(p,b) = dist(p,x)+dist(x,b) by a8,JORDAN1K:29;
b4:   dist(x,b) = sqrt((x`1-b`1)^2 + (x`2-b`2)^2) by GOBRD14:9
      .= sqrt((-2)^2 + (x`2-0)^2) by b3,Lm5,EUCLID:56
      .= sqrt(4 + x`2^2) by Lm9,SQUARE_1:61;
a5:   now
        assume dist(x,b) < dist(a,b);
        then 4 + x`2^2 < 4 + 0 by b4,Lm8,SQUARE_1:85,94;
        then x`2^2 < 0 by XREAL_1:8;
        hence contradiction by SQUARE_1:72;
      end;
      dist(p,x) > 0 by b1,b3,JORDAN1K:22;
      then dist(p,b) + 0 > dist(a,b) + 0 by a4,a5,XREAL_1:10;
      hence contradiction by Z1,Z2,a6;
    end;
a2: now
      assume
b1:   p`1 > rp;
      then LSeg(p,a) meets Vertical_Line(rp) by Lm4,TSV;
      then consider x being set such that
a8:   x in LSeg(p,a) and
a7:   x in Vertical_Line(rp) by XBOOLE_0:3;
      reconsider x as Point of T2 by a8;
b3:   x`1 = rp by a7,JORDAN6:34;
a4:   dist(p,a) = dist(p,x)+dist(x,a) by a8,JORDAN1K:29;
b4:   dist(x,a) = sqrt((x`1-a`1)^2 + (x`2-a`2)^2) by GOBRD14:9
      .= sqrt(4 + x`2^2) by b3,Lm4,Lm6,Lm9;
a5:   now
        assume dist(x,a) < dist(a,b);
        then 4 + x`2^2 < 4 + 0 by b4,Lm8,SQUARE_1:85,94;
        then x`2^2 < 0 by XREAL_1:8;
        hence contradiction by SQUARE_1:72;
      end;
      dist(p,x) > 0 by b1,b3,JORDAN1K:22;
      then dist(p,a) + 0 > dist(a,b) + 0 by a4,a5,XREAL_1:10;
      hence contradiction by Z0,Z2,a6;
    end;
a3: now
      assume rd > p`2;
      then p`2^2 > rd^2 by JGRAPH_5:2;
      hence contradiction by nn,Lm14a,SQUARE_1:61;
    end;
    rg >= p`2 by nn,Lm14a,SQUARE_1:78;
    hence thesis by a1,a2,a3,Lm0;
  end;
  
Lmw59: dR c= R by ThdRR;

      lg`2 = lg`2 & lg`1 <= c`1 & c`1 <= pg`1 by Lm70,Lm74,EUCLID:56;
      then LSeg(lg,c) c= LSeg(lg,pg) by Lm32i,Lm80a,JORDAN15:6;
      then
Lmnowy62: 
          LSeg(lg,c) c= dR by Lm32c,XBOOLE_1:1;

          LSeg(pg,c) c= LSeg(lg,pg)
          by Lm32I,Lm80a,Lm10,Lm11,Lm70,Lm71,Lm74,JORDAN15:6;
          then
Lmnowy62a: 
           LSeg(pg,c) c= dR by Lm32c,XBOOLE_1:1;

      ld`2 = ld`2 & ld`1 <= d`1 & d`1 <= pd`1 by Lm72,Lm76,EUCLID:56;
      then LSeg(ld,d) c= LSeg(ld,pd) by Lm32ix,Lm80A,JORDAN15:6;
      then
Lmnowy62x: 
          LSeg(ld,d) c= dR by Lm32e,XBOOLE_1:1;

          LSeg(pd,d) c= LSeg(ld,pd)
          by Lm32iy,Lm80A,Lm12,Lm13,Lm72,Lm73,Lm76,JORDAN15:6;
      then
Lmnowy62ax: 
            LSeg(pd,d) c= dR by Lm32e,XBOOLE_1:1;

Lmnowy64: 
  0 <= p`2 & p in dR implies
  p in LSeg(a,lg) or p in LSeg(lg,c) or
  p in LSeg(c,pg) or p in LSeg(pg,b)
  proof 
    assume
a1: 0 <= p`2;
    assume p in dR;
    then consider p1 such that
a2: p1 = p and
a3: p1`1 = rl & p1`2 <= rg & p1`2 >= rd or
    p1`1 <= rp & p1`1 >= rl & p1`2 = rg or
    p1`1 <= rp & p1`1 >= rl & p1`2 = rd or
    p1`1 = rp & p1`2 <= rg & p1`2 >= rd by Lm32;
    per cases by a3;
    suppose p1`1 = rl & p1`2 <= rg & p1`2 >= rd;
      hence thesis by a1,a2,Lm4,Lm6,Lm70,Lm71,GOBOARD7:8;
    end;
    suppose
s1:   p1`1 <= rp & p1`1 >= rl & p1`2 = rg;
      per cases;
      suppose p1`1 <= c`1;
        hence thesis by a2,s1,Lm11,Lm70,Lm71,GOBOARD7:9;
      end;
      suppose c`1 <= p1`1;
        hence thesis by a2,s1,Lm11,Lm74,Lm75,GOBOARD7:9;
      end;
    end;
    suppose p1`1 <= rp & p1`1 >= rl & p1`2 = rd;
      hence thesis by a1,a2;
    end;
    suppose p1`1 = rp & p1`2 <= rg & p1`2 >= rd;
      hence thesis by a1,a2,Lm5,Lm7,Lm74,Lm75,GOBOARD7:8;
    end;
  end;

Lmnowy64a: 
  p`2 <= 0 & p in dR implies
  p in LSeg(a,ld) or p in LSeg(ld,d) or
  p in LSeg(d,pd) or p in LSeg(pd,b)
  proof 
    assume
a1: p`2 <= 0;
    assume p in dR;
    then consider p1 such that
a2: p1 = p and
a3: p1`1 = rl & p1`2 <= rg & p1`2 >= rd or
    p1`1 <= rp & p1`1 >= rl & p1`2 = rg or
    p1`1 <= rp & p1`1 >= rl & p1`2 = rd or
    p1`1 = rp & p1`2 <= rg & p1`2 >= rd by Lm32;
    per cases by a3;
    suppose p1`1 = rl & p1`2 <= rg & p1`2 >= rd;
      hence thesis by a1,a2,Lm4,Lm6,Lm72,Lm73,GOBOARD7:8;
    end;
    suppose p1`1 <= rp & p1`1 >= rl & p1`2 = rg;
      hence thesis by a1,a2;
    end;
    suppose
s1:   p1`1 <= rp & p1`1 >= rl & p1`2 = rd;
      per cases;
      suppose p1`1 <= d`1;
        hence thesis by a2,s1,Lm13,Lm72,Lm73,GOBOARD7:9;
      end;
      suppose d`1 <= p1`1;
        hence thesis by a2,s1,Lm13,Lm76,Lm77,GOBOARD7:9;
      end;
    end;
    suppose p1`1 = rp & p1`2 <= rg & p1`2 >= rd;
      hence thesis by a1,a2,Lm5,Lm7,Lm76,Lm77,GOBOARD7:8;
    end;
  end;

theorem Tmg:
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies
  P misses LSeg(|[-1,3]|,|[1,3]|)
  proof 
    assume
a1: a,b realize-max-dist-in P;
    assume P meets LSeg(lg,pg);
    then consider x being set such that
a2: x in P and
a3: x in LSeg(lg,pg) by XBOOLE_0:3;
    reconsider x as Point of T2 by a2;
    lg in LSeg(lg,pg) by TOPREAL1:6;
    then
a4: x`2 = rg by a3,Lm71,Lm80a,SPPOL_1:def 2;
a6: a in P by a1,JORDAN24:def 1;
a5: dist(a,x) = sqrt ((a`1-x`1)^2 + (a`2-x`2)^2) by GOBRD14:9
    .= sqrt ((rl-x`1)^2 + (-rg)^2) by a4,Lm6,EUCLID:56
    .= sqrt ((rl-x`1)^2 + rg^2) by SQUARE_1:61;
    0 <= (rl-x`1)^2 by SQUARE_1:72;
    then 0+4 < (rl-x`1)^2+9 by XREAL_1:10;
    then 2 < dist(a,x) by a5,Lm14a,SQUARE_1:85,95;
    hence thesis by a1,a2,a6,JORDAN24:def 1,Lm8;
  end;

theorem Tmd:
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies
  P misses LSeg(|[-1,-3]|,|[1,-3]|)
  proof 
    assume
a1: a,b realize-max-dist-in P;
    assume P meets LSeg(ld,pd);
    then consider x being set such that
a2: x in P and
a3: x in LSeg(ld,pd) by XBOOLE_0:3;
    reconsider x as Point of T2 by a2;
    ld in LSeg(ld,pd) by TOPREAL1:6;
    then
a4: x`2 = rd by a3,Lm73,Lm80A,SPPOL_1:def 2;
a6: a in P by a1,JORDAN24:def 1;
a5: dist(a,x) = sqrt ((a`1-x`1)^2 + (a`2-x`2)^2) by GOBRD14:9
    .= sqrt ((rl-x`1)^2 + (-rd)^2) by a4,Lm6,EUCLID:56;
    0 <= (rl-x`1)^2 by SQUARE_1:72;
    then 0+4 < (rl-x`1)^2+9 by XREAL_1:10;
    then 2 < dist(a,x) by a5,Lm14a,SQUARE_1:85,95;
    hence thesis by a1,a2,a6,JORDAN24:def 1,Lm8;
  end;

theorem Lm2:      
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies
  P /\ rectangle(-1,1,-3,3) = {|[-1,0]|,|[1,0]|}
  proof 
    assume
a1: a,b realize-max-dist-in P;
    then
a2: a in P & b in P by JORDAN24:def 1;
    thus P /\ dR c= {a,b}
    proof
      let x be set;
      assume
a4:   x in P /\ dR;
      then
a6:   x in P by XBOOLE_0:def 3;
      x in dR by a4,XBOOLE_0:def 3;
      then
a5:   x in LSeg(ld,lg) \/ LSeg(lg,pg) or
      x in LSeg(pg,pd) \/ LSeg(pd,ld) by Lm32a,XBOOLE_0:def 2;
      reconsider x as Point of T2 by a4;
      per cases by a5,XBOOLE_0:def 2;
      suppose
s1:     x in LSeg(ld,lg);
        ld in LSeg(ld,lg) by TOPREAL1:6;
        then
a8:     x`1 = rl by s1,Lm32f,Lm72,SPPOL_1:def 3;
        per cases;
        suppose x`2 = 0;
          then x = a by a8,Lm4,Lm6,TOPREAL3:11;
          hence thesis by TARSKI:def 2;
        end;
        suppose x`2 <> 0;
          then
w1:       x`2^2 > 0 by SQUARE_1:74;
a7:       dist(b,x) = sqrt ((rp-rl)^2 + (0-x`2)^2) by a8,Lm5,Lm7,GOBRD14:9
          .= sqrt (4 + (-x`2)^2) by Lm9
          .= sqrt (4 + x`2^2) by SQUARE_1:61;
          0+4 < x`2^2+4 by w1,XREAL_1:8;
          then 2 < sqrt(x`2^2+4) by SQUARE_1:85,95;
          hence thesis by a1,a2,a6,a7,JORDAN24:def 1,Lm8;
        end;
      end;
      suppose x in LSeg(lg,pg);
        then LSeg(lg,pg) meets P by a6,XBOOLE_0:3;
        hence thesis by a1,Tmg;
      end;
      suppose
s1:     x in LSeg(pg,pd);
        pd in LSeg(pd,pg) by TOPREAL1:6;
        then
a8:     x`1 = rp by s1,Lm32F,Lm76,SPPOL_1:def 3;
        per cases;
        suppose x`2 = 0;
          then x = b by a8,Lm5,Lm7,TOPREAL3:11;
          hence thesis by TARSKI:def 2;
        end;
        suppose x`2 <> 0;
          then
w1:       x`2^2 > 0 by SQUARE_1:74;
a7:       dist(x,a) = sqrt ((x`1-a`1)^2 + (x`2-a`2)^2) by GOBRD14:9
          .= sqrt (4 + x`2^2) by a8,Lm4,Lm6,Lm9;
          0+4 < x`2^2+4 by w1,XREAL_1:8;
          then 2 < sqrt(x`2^2+4) by SQUARE_1:85,95;
          hence thesis by a1,a2,a6,a7,JORDAN24:def 1,Lm8;
        end;
      end;
      suppose x in LSeg(pd,ld);
        then LSeg(pd,ld) meets P by a6,XBOOLE_0:3;
        hence thesis by a1,Tmd;
      end;
    end;
    let x be set;
    assume x in {a,b};
    then
a3: x = a or x = b by TARSKI:def 2;
    a in dR & b in dR by Lm4,Lm5,Lm6,Lm7,Lm32;
    hence thesis by a2,a3,XBOOLE_0:def 3;
  end;

Lmnowy65: 
          |[-1,0]|,|[1,0]| realize-max-dist-in C implies LSeg(lg,c) misses C
          proof 
            assume a,b realize-max-dist-in C;
            then
b20:        C /\ dR = {a,b} by Lm2;
            assume LSeg(lg,c) meets C;
            then consider q being set such that
i13:        q in LSeg(lg,c) and
i14:        q in C by XBOOLE_0:3;
            reconsider q as Point of T2 by i14;
            q in dR /\ C by i14,i13,Lmnowy62,XBOOLE_0:def 3;
            then q = a or q = b by b20,TARSKI:def 2;
            hence contradiction by Lm6,Lm7,i13,TOPREAL3:18;
          end;

Lmnowy65a: 
           |[-1,0]|,|[1,0]| realize-max-dist-in C implies LSeg(pg,c) misses C
           proof 
             assume a,b realize-max-dist-in C;
             then
b20:         C /\ dR = {a,b} by Lm2;
             assume LSeg(pg,c) meets C;
             then consider q being set such that
i13:         q in LSeg(pg,c) and
i14:         q in C by XBOOLE_0:3;
             reconsider q as Point of T2 by i14;
             q in dR /\ C by i14,i13,Lmnowy62a,XBOOLE_0:def 3;
             then q = a or q = b by b20,TARSKI:def 2;
             hence contradiction by Lm6,Lm7,i13,TOPREAL3:18;
           end;
           
Lmnowy65x: 
          |[-1,0]|,|[1,0]| realize-max-dist-in C implies LSeg(ld,d) misses C
          proof 
            assume a,b realize-max-dist-in C;
            then
b20:        C /\ dR = {a,b} by Lm2;
            assume LSeg(ld,d) meets C;
            then consider q being set such that
i13:        q in LSeg(ld,d) and
i14:        q in C by XBOOLE_0:3;
            reconsider q as Point of T2 by i14;
            q in dR /\ C by i14,i13,Lmnowy62x,XBOOLE_0:def 3;
            then q = a or q = b by b20,TARSKI:def 2;
            hence contradiction by Lm6,Lm7,i13,TOPREAL3:18;
          end;
           
Lmnowy65ax: 
            |[-1,0]|,|[1,0]| realize-max-dist-in C implies LSeg(pd,d) misses C
           proof 
             assume a,b realize-max-dist-in C;
             then
b20:         C /\ dR = {a,b} by Lm2;
             assume LSeg(pd,d) meets C;
             then consider q being set such that
i13:         q in LSeg(pd,d) and
i14:         q in C by XBOOLE_0:3;
             reconsider q as Point of T2 by i14;
             q in dR /\ C by i14,i13,Lmnowy62ax,XBOOLE_0:def 3;
             then q = a or q = b by b20,TARSKI:def 2;
             hence contradiction by Lm6,Lm7,i13,TOPREAL3:18;
           end;

Lmnowy66: 
  |[-1,0]|,|[1,0]| realize-max-dist-in C & p in C` & p in LSeg(a,lg) implies
  LSeg(p,lg) misses C
  proof 
    assume that
a1: a,b realize-max-dist-in C and
a2: p in C` and
a3: p in LSeg(a,lg);
b20: C /\ dR = {a,b} by a1,Lm2;
     assume LSeg(p,lg) meets C;
     then consider q being set such that
i13: q in LSeg(p,lg) and
i14: q in C by XBOOLE_0:3;
     reconsider q as Point of T2 by i14;
     lg in LSeg(a,lg) by TOPREAL1:6;
     then
a10: p`1 = lg`1 by a3,Lm32g,SPPOL_1:def 3;
a11: p`2 <= lg`2 by a3,Lm71,JGRAPH_6:9;
a20: LSeg(p,lg) is vertical by a10,SPPOL_1:37;
     a`2 <= p`2 by a3,Lm6,JGRAPH_6:9;
     then
     LSeg(p,lg) c= LSeg(ld,lg)
     by a11,a20,Lm32f,a10,Lm6,Lm70,Lm72,Lm73,JORDAN15:5;
     then LSeg(p,lg) c= dR by Lm32b,XBOOLE_1:1;
     then q in dR /\ C by i14,i13,XBOOLE_0:def 3;
     then
i57: q = a or q = b by b20,TARSKI:def 2;
     a in LSeg(a,lg) by TOPREAL1:6;
     then
a6:  a`1 = p`1 by a3,Lm32g,SPPOL_1:def 3;
a5:  a in C by a1,JORDAN24:def 1;
     not p in C by a2,SUBSET_1:54;
     then a`2 <> p`2 by a5,a6,TOPREAL3:11;
     then
a8:  a`2 < p`2 by a3,Lm6,JGRAPH_6:9;
     p = |[p`1,p`2]| by EUCLID:57;
     hence contradiction by a8,i13,i57,Lm5,Lm70,
     a10,a11,Lm67g,JGRAPH_6:9;
   end;

Lmnowy66a: 
  |[-1,0]|,|[1,0]| realize-max-dist-in C & p in C` & p in LSeg(b,pg) implies
  LSeg(p,pg) misses C
  proof 
    assume that
a1: a,b realize-max-dist-in C and
a2: p in C` and
a3: p in LSeg(b,pg);
b20: C /\ dR = {a,b} by a1,Lm2;
     assume LSeg(p,pg) meets C;
     then consider q being set such that
i13: q in LSeg(p,pg) and
i14: q in C by XBOOLE_0:3;
     reconsider q as Point of T2 by i14;
     pg in LSeg(b,pg) by TOPREAL1:6;
     then
a10: p`1 = pg`1 by a3,Lm33a,SPPOL_1:def 3;
a11: p`2 <= pg`2 by a3,Lm75,JGRAPH_6:9;
a20: LSeg(p,pg) is vertical by a10,SPPOL_1:37;
     b`2 <= p`2 by a3,Lm7,JGRAPH_6:9;     
     then LSeg(p,pg) c= LSeg(pd,pg)
     by a11,a20,Lm32F,a10,Lm7,Lm74,Lm76,Lm77,JORDAN15:5;
     then LSeg(p,pg) c= dR by Lm32d,XBOOLE_1:1;
     then q in dR /\ C by i14,i13,XBOOLE_0:def 3;
     then
i57: q = a or q = b by b20,TARSKI:def 2;
     b in LSeg(b,pg) by TOPREAL1:6;
     then
a6:  b`1 = p`1 by a3,Lm33a,SPPOL_1:def 3;
a5:  b in C by a1,JORDAN24:def 1;
     not p in C by a2,SUBSET_1:54;
     then b`2 <> p`2 by a5,a6,TOPREAL3:11;
     then
a8:  b`2 < p`2 by a3,Lm7,JGRAPH_6:9;
     p = |[p`1,p`2]| by EUCLID:57;
     hence contradiction by a8,i57,Lm4,i13,Lm74,
     a10,a11,Lm67G,JGRAPH_6:9;
   end;

Lmnowy66y: 
  |[-1,0]|,|[1,0]| realize-max-dist-in C & p in C` & p in LSeg(a,ld) implies
  LSeg(p,ld) misses C
  proof 
    assume that
a1: a,b realize-max-dist-in C and
a2: p in C` and
a3: p in LSeg(a,ld);
b20: C /\ dR = {a,b} by a1,Lm2;
     assume LSeg(p,ld) meets C;
     then consider q being set such that
i13: q in LSeg(p,ld) and
i14: q in C by XBOOLE_0:3;
     reconsider q as Point of T2 by i14;
     ld in LSeg(a,ld) by TOPREAL1:6;
     then
a10: p`1 = ld`1 by a3,Lm32G,SPPOL_1:def 3;
a11: ld`2 <= p`2 by a3,Lm73,JGRAPH_6:9;
a20: LSeg(p,ld) is vertical by a10,SPPOL_1:37;
     p`2 <= a`2 by a3,Lm6,JGRAPH_6:9;     
     then
     LSeg(p,ld) c= LSeg(ld,lg) by a11,a20,Lm32f,a10,Lm6,Lm71,JORDAN15:5;
     then LSeg(p,ld) c= dR by Lm32b,XBOOLE_1:1;
     then q in dR /\ C by i14,i13,XBOOLE_0:def 3;
     then
i57: q = a or q = b by b20,TARSKI:def 2;
     a in LSeg(a,ld) by TOPREAL1:6;
     then
a6:  a`1 = p`1 by a3,Lm32G,SPPOL_1:def 3;
a5:  a in C by a1,JORDAN24:def 1;
     not p in C by a2,SUBSET_1:54;
     then a`2 <> p`2 by a5,a6,TOPREAL3:11;
     then
a8:  p`2 < a`2 by a3,Lm6,JGRAPH_6:9;
     p = |[p`1,p`2]| by EUCLID:57;
     hence contradiction by a8,i57,Lm5,i13,Lm72,
     a10,a11,Lm67d,JGRAPH_6:9;
   end;

Lmnowy66ay: 
  |[-1,0]|,|[1,0]| realize-max-dist-in C & p in C` & p in LSeg(b,pd) implies
  LSeg(p,pd) misses C
  proof 
    assume that
a1: a,b realize-max-dist-in C and
a2: p in C` and
a3: p in LSeg(b,pd);
b20: C /\ dR = {a,b} by a1,Lm2;
     assume LSeg(p,pd) meets C;
     then consider q being set such that
i13: q in LSeg(p,pd) and
i14: q in C by XBOOLE_0:3;
     reconsider q as Point of T2 by i14;
     pd in LSeg(b,pd) by TOPREAL1:6;
     then
a10: p`1 = pd`1 by a3,Lm33b,SPPOL_1:def 3;
a11: pd`2 <= p`2 by a3,Lm77,JGRAPH_6:9;
a20: LSeg(p,pd) is vertical by a10,SPPOL_1:37;
     p`2 <= b`2 by a3,Lm7,JGRAPH_6:9;     
     then
     LSeg(p,pd) c= LSeg(pd,pg) by a11,a20,Lm32F,a10,Lm7,Lm75,JORDAN15:5;
     then LSeg(p,pd) c= dR by Lm32d,XBOOLE_1:1;
     then q in dR /\ C by i14,i13,XBOOLE_0:def 3;
     then
i57: q = a or q = b by b20,TARSKI:def 2;
     b in LSeg(b,pd) by TOPREAL1:6;
     then
a6:  b`1 = p`1 by a3,Lm33b,SPPOL_1:def 3;
a5:  b in C by a1,JORDAN24:def 1;
     not p in C by a2,SUBSET_1:54;
     then b`2 <> p`2 by a5,a6,TOPREAL3:11;
     then
a8:  p`2 < b`2 by a3,Lm7,JGRAPH_6:9;
     p = |[p`1,p`2]| by EUCLID:57;
     hence contradiction by a8,i57,Lm4,i13,Lm76,
     a10,a11,Lm67D,JGRAPH_6:9;
   end;

Lmnowy67:
  |[0,r]| in rectangle(rl,rp,rd,rg) implies r = rd or r = rg
  proof 
    assume |[0,r]| in dR;
    then ex p st p = |[0,r]| &
    (p`1 = rl & p`2 <= rg & p`2 >= rd or
    p`1 <= rp & p`1 >= rl & p`2 = rg or
    p`1 <= rp & p`1 >= rl & p`2 = rd or
    p`1 = rp & p`2 <= rg & p`2 >= rd) by Lm32;
    hence thesis by EUCLID:56;
  end;

theorem Lm21:
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies W-bound P = -1
  proof 
    assume
a1: a,b realize-max-dist-in P;
    then
gg: P c= R by Lm1;
A2: P = the carrier of (T2|P) by JORDAN1:1;
a2: R = {p1: rl <= p1`1 & p1`1 <= rp & rd <= p1`2 & p1`2 <= rg}
    by JGRAPH_6:def 2;
A9: a in P by a1,JORDAN24:def 1;
    then reconsider P as non empty Subset of T2;
    reconsider Z = (proj1||P).:the carrier of (T2|P)
    as Subset of REAL;
A3: for p be real number st p in Z holds p >= rl
    proof
      let p be real number;
      assume p in Z;
      then consider p0 being set such that
A4:   p0 in the carrier of T2|P and
      p0 in the carrier of T2|P and
A5:   p = (proj1||P).p0 by FUNCT_2:115;
      p0 in R by A4,gg,A2;
      then ex p1 st p0 = p1 &
      rl <= p1`1 & p1`1 <= rp & rd <= p1`2 & p1`2 <= rg by a2;
      hence thesis by A2,A4,A5,PSCOMP_1:69;
    end;
A6: for q being real number st
    for p being real number st p in Z holds p >= q holds rl >= q
    proof
      let q be real number such that
A7:   for p being real number st p in Z holds p >= q;
      (proj1||P).a = a`1 by A9,PSCOMP_1:69;
      then rl in Z by A2,A9,Lm4,FUNCT_2:43;
      hence thesis by A7;
    end;
    W-bound P = inf (proj1||P) by PSCOMP_1:def 30
    .= inf Z by PSCOMP_1:def 20
    .= rl by A3,A6,SEQ_4:61;
    hence thesis;
  end;

theorem Lm22: 
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies E-bound P = 1
  proof 
    assume
a1: a,b realize-max-dist-in P;
    then
gg: P c= R by Lm1;
A9: b in P by a1,JORDAN24:def 1;
    then reconsider P as non empty Subset of T2;
    reconsider Z = (proj1||P).:the carrier of (T2|P)
    as Subset of REAL;
A2: P = the carrier of (T2|P) by JORDAN1:1;
a2: R = {p1: rl <= p1`1 & p1`1 <= rp & rd <= p1`2 & p1`2 <= rg}
    by JGRAPH_6:def 2;
A3: for p be real number st p in Z holds p <= rp
    proof
      let p be real number;
      assume p in Z;
      then consider p0 being set such that
A4:   p0 in the carrier of T2|P and
      p0 in the carrier of T2|P and
A5:   p = (proj1||P).p0 by FUNCT_2:115;
      p0 in R by A4,gg,A2;
      then ex p1 st p0 = p1 &
      rl <= p1`1 & p1`1 <= rp & rd <= p1`2 & p1`2 <= rg by a2;
      hence thesis by A2,A4,A5,PSCOMP_1:69;
    end;
A6: for q being real number st
    for p being real number st p in Z holds p <= q holds rp <= q
    proof
      let q be real number such that
A7:   for p being real number st p in Z holds p <= q;
      (proj1||P).b = b`1 by A9,PSCOMP_1:69;
      then rp in Z by A2,A9,Lm5,FUNCT_2:43;
      hence thesis by A7;
    end;
    E-bound P = sup (proj1||P) by PSCOMP_1:def 32
    .= sup Z by PSCOMP_1:def 21
    .= rp by A3,A6,SEQ_4:63;
    hence thesis;
  end;

theorem ThSW:
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies SW-corner P = |[-1,S-bound P]|
  proof 
    assume
a1: a,b realize-max-dist-in P;
    thus SW-corner P = |[W-bound P, S-bound P]| by PSCOMP_1:def 34
    .= |[rl,S-bound P]| by a1,Lm21;
  end;
  
theorem ThNW:
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies NW-corner P = |[-1,N-bound P]|
  proof 
    assume
a1: a,b realize-max-dist-in P;
    thus NW-corner P = |[W-bound P, N-bound P]| by PSCOMP_1:def 35
    .= |[rl,N-bound P]| by a1,Lm21;
  end;

theorem ThSE:
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies SE-corner P = |[1,S-bound P]|
  proof 
    assume
a1: a,b realize-max-dist-in P;
    thus SE-corner P = |[E-bound P, S-bound P]| by PSCOMP_1:def 37
    .= |[rp,S-bound P]| by a1,Lm22;
  end;
  
theorem ThNE:
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies NE-corner P = |[1,N-bound P]|
  proof 
    assume
a1: a,b realize-max-dist-in P;
    thus NE-corner P = |[E-bound P, N-bound P]| by PSCOMP_1:def 36
    .= |[rp,N-bound P]| by a1,Lm22;
  end;

theorem Lm520a:
  for P being compact Subset of TOP-REAL 2 holds
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies W-most P = {|[-1,0]|}
  proof 
    let P be compact Subset of T2;
    assume
a1: a,b realize-max-dist-in P;
    then
a0: P c= R by Lm1;
    set L = LSeg(SW-corner P, NW-corner P);
a2: W-most P = L /\ P by PSCOMP_1:def 38;
b2: a in P by a1,JORDAN24:def 1;
a4: (SW-corner P)`1 = |[rl,S-bound P]|`1 by a1,ThSW
    .= rl by EUCLID:56;
a6: (NW-corner P)`1 = |[rl,N-bound P]|`1 by a1,ThNW
    .= rl by EUCLID:56;
    thus W-most P c= {a}
    proof
      let x be set;
      assume
a3:   x in W-most P;
      then
a8:   x in P by a2,XBOOLE_0:def 3;
      reconsider x as Point of T2 by a3;
a5:   x in L by a2,a3,XBOOLE_0:def 3;
      SW-corner P in L by TOPREAL1:6;
      then
a9:   x`1 = rl by a5,a4,SPPOL_1:def 3;
      x in R by a0,a8;
      then ex p st x = p & rl <= p`1 & p`1 <= rp & rd <= p`2 & p`2 <= rg
      by Lm0;
      then x in dR by a9,Lm32;
      then x in P /\ dR by a8,XBOOLE_0:def 3;
      then x in {a,b} by a1,Lm2;
      then x = a or x = b by TARSKI:def 2;
      hence thesis by a9,EUCLID:56,TARSKI:def 1;
    end;
    let x be set;
    assume x in {a};
    then
b1: x = a by TARSKI:def 1;
b3: (SW-corner P)`2 = S-bound P by PSCOMP_1:73;
    (NW-corner P)`2 = N-bound P by PSCOMP_1:75;
    then (SW-corner P)`2 <= a`2 & a`2 <= (NW-corner P)`2 by b3,b2,PSCOMP_1:71;
    then a in L by a4,a6,Lm4,GOBOARD7:8;
    hence thesis by a2,b2,b1,XBOOLE_0:def 3;
  end;

theorem Lm520b: 
  for P being compact Subset of TOP-REAL 2 holds
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies E-most P = {|[1,0]|}
  proof 
    let P be compact Subset of T2;
    assume
a1: a,b realize-max-dist-in P;
    then
a0: P c= R by Lm1;
    set L = LSeg(SE-corner P, NE-corner P);
a2: E-most P = L /\ P by PSCOMP_1:def 40;
b2: b in P by a1,JORDAN24:def 1;
a4: (SE-corner P)`1 = |[rp,S-bound P]|`1 by a1,ThSE
    .= rp by EUCLID:56;
a6: (NE-corner P)`1 = |[rp,N-bound P]|`1 by a1,ThNE
    .= rp by EUCLID:56;
    thus E-most P c= {b}
    proof
      let x be set;
      assume
a3:   x in E-most P;
      then
a8:   x in P by a2,XBOOLE_0:def 3;
      reconsider x as Point of T2 by a3;
a5:   x in L by a2,a3,XBOOLE_0:def 3;
      SE-corner P in L by TOPREAL1:6;
      then
a9:   x`1 = rp by a5,a4,SPPOL_1:def 3;
      x in R by a0,a8;
      then ex p st x = p & rl <= p`1 & p`1 <= rp & rd <= p`2 & p`2 <= rg
      by Lm0;
      then x in dR by a9,Lm32;
      then x in P /\ dR by a8,XBOOLE_0:def 3;
      then x in {a,b} by a1,Lm2;
      then x = a or x = b by TARSKI:def 2;
      hence thesis by a9,EUCLID:56,TARSKI:def 1;
    end;
    let x be set;
    assume x in {b};
    then
b1: x = b by TARSKI:def 1;
b3: (SE-corner P)`2 = S-bound P by PSCOMP_1:79;
    (NE-corner P)`2 = N-bound P by PSCOMP_1:77;
    then (SE-corner P)`2 <= b`2 & b`2 <= (NE-corner P)`2 by b3,b2,PSCOMP_1:71;
    then b in L by a4,a6,Lm5,GOBOARD7:8;
    hence thesis by a2,b2,b1,XBOOLE_0:def 3;
  end;

theorem Lm52: 
  for P being compact Subset of TOP-REAL 2 holds
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies
  W-min P = |[-1,0]| & W-max P = |[-1,0]|
  proof 
    let P be compact Subset of T2;
    set M = W-most P;
    assume
a1: a,b realize-max-dist-in P;
    then
a0: M = {a} by Lm520a;
    set f = proj2||M;
a7: f = proj2|M by PSCOMP_1:def 26;
a4: dom f = the carrier of (T2|M) by FUNCT_2:def 1;
a5: the carrier of (T2|M) = M by JORDAN1:1;
a6: a in {a} by TARSKI:def 1;
A3: f.:the carrier of (T2|M) = {f.a} by a4,a5,a6,a0,FUNCT_1:117
    .= {proj2.a} by a7,a0,a6,FUNCT_1:72
    .= {a`2} by PSCOMP_1:def 29;
a3: inf (proj2||M) = inf (f.:the carrier of (T2|M)) by PSCOMP_1:def 20
    .= a`2 by A3,SEQ_4:22;
b3: sup (proj2||M) = sup (f.:the carrier of (T2|M)) by PSCOMP_1:def 21
    .= a`2 by A3,SEQ_4:22;
a4: W-min P = |[W-bound P, inf (proj2||M)]| by PSCOMP_1:def 42;
A4: W-max P = |[W-bound P, sup (proj2||M)]| by PSCOMP_1:def 43;
    a = |[a`1,a`2]| by EUCLID:57;
    hence thesis by a3,a4,b3,A4,Lm4,a1,Lm21;
  end;

theorem Lm52e: 
  for P being compact Subset of TOP-REAL 2 holds
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies
  E-min P = |[1,0]| & E-max P = |[1,0]|
  proof 
    let P be compact Subset of T2;
    set M = E-most P;
    assume
a1: a,b realize-max-dist-in P;
    then
a0: M = {b} by Lm520b;
    set f = proj2||M;
a7: f = proj2|M by PSCOMP_1:def 26;
a4: dom f = the carrier of (T2|M) by FUNCT_2:def 1;
a5: the carrier of (T2|M) = M by JORDAN1:1;
a6: b in {b} by TARSKI:def 1;
A3: f.:the carrier of (T2|M) = {f.b} by a4,a5,a6,a0,FUNCT_1:117
    .= {proj2.b} by a7,a0,a6,FUNCT_1:72
    .= {b`2} by PSCOMP_1:def 29;
a3: inf (proj2||M) = inf (f.:the carrier of (T2|M)) by PSCOMP_1:def 20
    .= b`2 by A3,SEQ_4:22;
b3: sup (proj2||M) = sup (f.:the carrier of (T2|M)) by PSCOMP_1:def 21
    .= b`2 by A3,SEQ_4:22;
a4: E-min P = |[E-bound P, inf (proj2||M)]| by PSCOMP_1:def 47;
A4: E-max P = |[E-bound P, sup (proj2||M)]| by PSCOMP_1:def 46;
    b = |[b`1,b`2]| by EUCLID:57;
    hence thesis by a3,a4,b3,A4,Lm5,a1,Lm22;
  end;
  
Lm23: 
      |[-1,0]|,|[1,0]| realize-max-dist-in P implies
      c`1 = (W-bound P + E-bound P) / 2
      proof 
        assume a,b realize-max-dist-in P;
        then W-bound P = rl & E-bound P = rp by Lm21,Lm22;
        hence thesis by EUCLID:56;
      end;

Lm45: 
      |[-1,0]|,|[1,0]| realize-max-dist-in P implies
      d`1 = (W-bound P + E-bound P) / 2
      proof 
        assume a,b realize-max-dist-in P;
        then W-bound P = rl & E-bound P = rp by Lm21,Lm22;
        hence thesis by EUCLID:56;
      end;

theorem Lm43: 
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies
  LSeg(|[0,3]|,UMP P) is vertical
  proof 
    assume a,b realize-max-dist-in P;
    then c`1 = (W-bound P + E-bound P) / 2 by Lm23
    .= (UMP P)`1 by JORDAN21:33;
    hence thesis by SPPOL_1:37;
  end;
  
theorem Lm44: 
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies
  LSeg(LMP P,|[0,-3]|) is vertical
  proof 
    assume a,b realize-max-dist-in P;
    then d`1 = (W-bound P + E-bound P) / 2 by Lm45
    .= (LMP P)`1 by JORDAN21:35;
    hence thesis by SPPOL_1:37;
  end;
  
theorem Lm50: 
  |[-1,0]|,|[1,0]| realize-max-dist-in P & p in P implies p`2 < 3
  proof 
    assume that
a1: a,b realize-max-dist-in P and
a2: p in P;
a7: P /\ dR = {a,b} by a1,Lm2;
    P c= R by a1,Lm1;
    then
a4: p in R by a2;
a5: ex p1 st p1 = p & rl <= p1`1 & p1`1 <= rp & rd <= p1`2 & p1`2 <= rg
    by a4,Lm0;
    now
      assume
a8:   p`2 = c`2;
      then p in LSeg(lg,pg) by a5,Lm11,Lm70,Lm71,Lm74,Lm75,GOBOARD7:9;
      then p in P /\ dR by a2,Lm32c,XBOOLE_0:def 3;
      hence contradiction by a8,Lm6,Lm7,Lm11,a7,TARSKI:def 2;
    end;
    hence thesis by a5,Lm11,REAL_1:def 5;
  end;

theorem Lm51: 
  |[-1,0]|,|[1,0]| realize-max-dist-in P & p in P implies -3 < p`2
  proof 
    assume that
a1: a,b realize-max-dist-in P and
a2: p in P;
a7: P /\ dR = {a,b} by a1,Lm2;
    P c= R by a1,Lm1;
    then
a4: p in R by a2;
a5: ex p1 st p1 = p & rl <= p1`1 & p1`1 <= rp & rd <= p1`2 & p1`2 <= rg
    by a4,Lm0;
    now
      assume
a8:   p`2 = d`2;
      then p in LSeg(ld,pd) by a5,Lm13,Lm72,Lm73,Lm76,Lm77,GOBOARD7:9;
      then p in P /\ dR by a2,Lm32e,XBOOLE_0:def 3;
      then p = a or p = b by a7,TARSKI:def 2;
      hence contradiction by a8,Lm13,EUCLID:56;
    end;
    hence thesis by a5,Lm13,REAL_1:def 5;
  end;
  
theorem Lm53: 
  |[-1,0]|,|[1,0]| realize-max-dist-in D & p in LSeg(|[0,3]|,UMP D) implies
  (UMP D)`2 <= p`2
  proof 
    set x = UMP D;
    assume that
a1: a,b realize-max-dist-in D and
a3: p in LSeg(c,x);
a4: x in LSeg(c,x) by TOPREAL1:6;
a7: LSeg(c,x) is vertical by a1,Lm43;
a5: c = |[c`1,c`2]| & x = |[x`1,x`2]| by EUCLID:57;
    c in LSeg(c,x) by TOPREAL1:6;
    then
a6: c`1 = x`1 by a4,a7,SPPOL_1:def 3;
    x in D by JORDAN21:43;
    then x`2 <= c`2 by a1,Lm11,Lm50;
    hence thesis by a3,a5,a6,JGRAPH_6:9;
  end;
  
theorem Lmxx:
  |[-1,0]|,|[1,0]| realize-max-dist-in D & p in LSeg(LMP D,|[0,-3]|) implies
  p`2 <= (LMP D)`2
  proof 
    set x = LMP D;
    assume that
a1: a,b realize-max-dist-in D and
a3: p in LSeg(x,d);
a4: x in LSeg(x,d) by TOPREAL1:6;
a7: LSeg(x,d) is vertical by a1,Lm44;
a5: d = |[d`1,d`2]| & x = |[x`1,x`2]| by EUCLID:57;
    d in LSeg(x,d) by TOPREAL1:6;
    then
a6: d`1 = x`1 by a4,a7,SPPOL_1:def 3;
    x in D by JORDAN21:44;
    then d`2 <= x`2 by a1,Lm13,Lm51;
    hence thesis by a3,a5,a6,JGRAPH_6:9;
  end;
  
theorem Lm41: 
  |[-1,0]|,|[1,0]| realize-max-dist-in D implies
  LSeg(|[0,3]|,UMP D) c= north_halfline UMP D
  proof 
    set p = UMP D;
    assume
a1: a,b realize-max-dist-in D;
    let x be set;
    assume
a3: x in LSeg(c,p);
    then reconsider x as Point of T2;
a4: p in LSeg(c,p) by TOPREAL1:6;
    LSeg(c,p) is vertical by a1,Lm43;
    then
a2: x`1 = p`1 by a3,a4,SPPOL_1:def 3;
    p`2 <= x`2 by a1,a3,Lm53;
    hence thesis by a2,JORDAN1A:def 2;
  end;
  
theorem Lm42: 
  |[-1,0]|,|[1,0]| realize-max-dist-in D implies
  LSeg(LMP D,|[0,-3]|) c= south_halfline LMP D
  proof 
    set p = LMP D;
    assume
a1: a,b realize-max-dist-in D;
    let x be set;
    assume
a3: x in LSeg(p,d);
    then reconsider x as Point of T2;
a4: p in LSeg(p,d) by TOPREAL1:6;
a7: LSeg(p,d) is vertical by a1,Lm44;
    then
a2: x`1 = p`1 by a3,a4,SPPOL_1:def 3;
a5: d = |[d`1,d`2]| & p = |[p`1,p`2]| by EUCLID:57;
    d in LSeg(p,d) by TOPREAL1:6;
    then
a6: d`1 = p`1 by a4,a7,SPPOL_1:def 3;
    p in D by JORDAN21:44;
    then d`2 <= p`2 by a1,Lm13,Lm51;
    then x`2 <= p`2 by a3,a5,a6,JGRAPH_6:9;
    hence thesis by a2,JORDAN1A:def 4;
  end;
  
theorem Lm24: 
  |[-1,0]|,|[1,0]| realize-max-dist-in C & P is_inside_component_of C implies
  LSeg(|[0,3]|,UMP C) misses P
  proof 
    set m = UMP C;
    set L = LSeg(c,m);
    assume that
a1: a,b realize-max-dist-in C and
a2: P is_inside_component_of C;
    consider VP being Subset of T2|C` such that
a20: VP = P and
     VP is_a_component_of T2|C` and
     VP is bounded Subset of Euclid 2 by a2,JORDAN2C:17;
     m in L by TOPREAL1:6;
     then {m} c= L by ZFMISC_1:37;
     then
a3:  L = L \ {m} \/ {m} by XBOOLE_1:45;
     L c= north_halfline m by a1,Lm41;
     then
a5:  L \ {m} c= north_halfline m \ {m} by XBOOLE_1:33;
     north_halfline m \ {m} c= UBD C by Th26;
     then
a6:  L \ {m} c= UBD C by a5,XBOOLE_1:1;
     UBD C misses P by a2,Th30;
     then
a4:  L \ {m} misses P by a6,XBOOLE_1:63;
     m in C by JORDAN21:43;
     then {m} misses P by a20,Lm27;
     hence thesis by a3,a4,XBOOLE_1:70;
   end;
   
theorem Lm25: 
  |[-1,0]|,|[1,0]| realize-max-dist-in C & P is_inside_component_of C implies
  LSeg(LMP C,|[0,-3]|) misses P
  proof 
    set m = LMP C;
    set L = LSeg(m,d);
    assume that
a1: a,b realize-max-dist-in C and
a2: P is_inside_component_of C;
    consider VP being Subset of T2|C` such that
a20: VP = P and
     VP is_a_component_of T2|C` and
     VP is bounded Subset of Euclid 2 by a2,JORDAN2C:17;
     m in L by TOPREAL1:6;
     then {m} c= L by ZFMISC_1:37;
     then
a3:  L = L \ {m} \/ {m} by XBOOLE_1:45;
     L c= south_halfline m by a1,Lm42;
     then
a5:  L \ {m} c= south_halfline m \ {m} by XBOOLE_1:33;
     south_halfline m \ {m} c= UBD C by Th27;
     then
a6:  L \ {m} c= UBD C by a5,XBOOLE_1:1;
     UBD C misses P by a2,Th30;
     then
a4:  L \ {m} misses P by a6,XBOOLE_1:63;
     m in C by JORDAN21:44;
     then {m} misses P by a20,Lm27;
     hence thesis by a3,a4,XBOOLE_1:70;
   end;
      
theorem ThUMP:
  |[-1,0]|,|[1,0]| realize-max-dist-in D implies
  LSeg(|[0,3]|,UMP D) /\ D = {UMP D}
  proof 
    assume
a1: a,b realize-max-dist-in D;
    set m = UMP D;
    set w = (W-bound D + E-bound D) / 2;
a15: c`1 = w by a1,Lm23;
a14: m`1 = w by JORDAN21:33;
a11: m in LSeg(c,m) by TOPREAL1:6;
a12: m in D by JORDAN21:43;
     thus LSeg(c,m) /\ D c= {m}
     proof
      let x be set;
      assume
x02:  x in LSeg(c,m) /\ D;
      then
a16:  x in LSeg(c,m) & x in D by XBOOLE_0:def 3;
      reconsider x as Point of T2 by x02;
      LSeg(c,m) is vertical by a14,a15,SPPOL_1:37;
      then 
a13:  x`1 = m`1 by a11,a16,SPPOL_1:def 3;
      then x in Vertical_Line w by a14,JORDAN6:34;
      then x in D /\ Vertical_Line w by a16,XBOOLE_0:def 3;
      then
c01:  x`2 <= m`2 by JORDAN21:41;
      m`2 <= x`2 by a1,a16,Lm53;
      then x`2 = m`2 by c01,XREAL_1:1;
      then x = m by a13,TOPREAL3:11;
      hence thesis by TARSKI:def 1;
    end;
    let x be set;
    assume x in {m};
    then x = m by TARSKI:def 1;
    hence thesis by a11,a12,XBOOLE_0:def 3;
  end;
  
theorem
  |[-1,0]|,|[1,0]| realize-max-dist-in D implies
  LSeg(|[0,-3]|,LMP D) /\ D = {LMP D}
  proof 
    assume
a1: a,b realize-max-dist-in D;
    set m = LMP D;
    set w = (W-bound D + E-bound D) / 2;
a15: d`1 = w by a1,Lm45;
a14: m`1 = w by JORDAN21:35;
a11: m in LSeg(d,m) by TOPREAL1:6;
a12: m in D by JORDAN21:44;
     thus LSeg(d,m) /\ D c= {m}
     proof
      let x be set;
      assume
x02:  x in LSeg(d,m) /\ D;
      then
a16:  x in LSeg(d,m) & x in D by XBOOLE_0:def 3;
      reconsider x as Point of T2 by x02;
      LSeg(d,m) is vertical by a14,a15,SPPOL_1:37;
      then 
a13:  x`1 = m`1 by a11,a16,SPPOL_1:def 3;
      then x in Vertical_Line w by a14,JORDAN6:34;
      then x in D /\ Vertical_Line w by a16,XBOOLE_0:def 3;
      then
c01:  m`2 <= x`2 by JORDAN21:42;
      x`2 <= m`2 by a1,a16,Lmxx;
      then x`2 = m`2 by c01,XREAL_1:1;
      then x = m by a13,TOPREAL3:11;
      hence thesis by TARSKI:def 1;
    end;
    let x be set;
    assume x in {m};
    then x = m by TARSKI:def 1;
    hence thesis by a11,a12,XBOOLE_0:def 3;
  end;
  
theorem TCompRec:
  P is compact &
  |[-1,0]|,|[1,0]| realize-max-dist-in P & A is_inside_component_of P implies
  A c= closed_inside_of_rectangle(-1,1,-3,3)
  proof 
    assume that
we: P is compact and
a1: |[-1,0]|,|[1,0]| realize-max-dist-in P and
a2: A is_inside_component_of P;
    let x be set;
    assume that
a3: x in A and
a4: not x in R;
    P c= R by a1,Lm1;
    then
a5: R` c= P` by SUBSET_1:31;
    reconsider x as Point of T2 by a3;
c1: not (rl <= x`1 & x`1 <= rp & rd <= x`2 & x`2 <= rg) by a4,Lm0;
    per cases;
    suppose
v2:   0 <= x`1;
      set E = east_halfline(x);
      E c= R`
      proof
        let e be set;
        assume
a7:     e in E;
        then reconsider e as Point of T2;
v1:     e`1 >= x`1 & e`2 = x`2 by a7,JORDAN1A:def 3;
        now
          assume e in R;
          then ex p st e = p & rl <= p`1 & p`1 <= rp & rd <= p`2 & p`2 <= rg
          by Lm0;
          hence contradiction by v1,v2,c1,XREAL_1:2;
        end;
        hence thesis by SUBSET_1:50;
      end;
      then E c= P` by a5,XBOOLE_1:1;
      then E misses P by SUBSET_1:43;
      then
a6:   E c= UBD P by we,JORDAN1C:15;
      x in E by JORDAN1C:7;
      then A meets UBD P by a3,a6,XBOOLE_0:3;
      hence thesis by a2,Th30;
    end;
    suppose
v2:   x`1 < 0;
      set E = west_halfline(x);
      E c= R`
      proof
        let e be set;
        assume
a7:     e in E;
        then reconsider e as Point of T2;
v1:     e`1 <= x`1 & e`2 = x`2 by a7,JORDAN1A:def 5;
        now
          assume e in R;
          then ex p st e = p & rl <= p`1 & p`1 <= rp & rd <= p`2 & p`2 <= rg
          by Lm0;
          hence contradiction by v1,v2,c1,XREAL_1:2;
        end;
        hence thesis by SUBSET_1:50;
      end;
      then E c= P` by a5,XBOOLE_1:1;
      then E misses P by SUBSET_1:43;
      then
a6:   E c= UBD P by we,JORDAN1C:14;
      x in E by JORDAN1C:7;
      then A meets UBD P by a3,a6,XBOOLE_0:3;
      hence thesis by a2,Th30;
    end;
  end;

LmBall:
  p in R implies R c= Ball(p,10)
  proof 
    assume p in R;
    then consider p1 such that
a1: p1 = p and
A1: rl <= p1`1 & p1`1 <= rp & rd <= p1`2 & p1`2 <= rg by Lm0;
    let x be set;
    assume
a2: x in R;
    then reconsider x as Point of T2;
    consider p2 such that
a3: p2 = x and
A3: rl <= p2`1 & p2`1 <= rp & rd <= p2`2 & p2`2 <= rg by a2,Lm0;
a4: ex s, t being Point of Euclid 2 st
    s = p1 & t = p2 & dist(p1,p2) = dist(s,t) by GOBRD14:def 1;
    dist(p1,p2) <= (rp-rl) + (rg-rd) by A1,A3,GOBRD14:12;
    then dist(p1,p2) < 10 by XREAL_1:2;
    then |.x-p.| < 10 by a1,a3,a4,KURATO_2:31;
    hence thesis by TOPREAL9:7;
  end;

theorem      
  |[-1,0]|,|[1,0]| realize-max-dist-in C implies LSeg(|[0,3]|,|[0,-3]|) meets C
  proof 
    assume
a1: a,b realize-max-dist-in C;
    set Jc = Upper_Arc C;
    consider Pf being Path of c,d, f being map of I[01], T2|LSeg(c,d) such that
a2: rng f = LSeg(c,d) & Pf = f by defs2;
    a = W-min C & b = E-max C by a1,Lm52,Lm52e;
    then Jc is_an_arc_of a,b by JORDAN6:def 8;
    then consider Pg being Path of a,b, g being map of I[01], T2|Jc such that
a3: rng g = Jc & Pg = g by defa2;
a4: Jc c= C by JORDAN1A:16;
a72: C c= R by a1,Lm1;
a71: a in C & b in C by a1,JORDAN24:def 1;
w04: the carrier of TR = R by TOPREALA:53;
     reconsider AR = a, BR = b, CR = c, DR = d
     as Point of TR by a71,a72,Lm30,Lm31,Lmw59,TOPREALA:53;
     rng Pg c= the carrier of TR by a3,a4,a72,w04,XBOOLE_1:1;
     then reconsider h = Pg as Path of AR,BR by Th1a;
     LSeg(c,d) c= R by Lm30,Lmw59,Lm31,JORDAN1:def 1;
     then reconsider v = Pf as Path of CR,DR by a2,w04,Th1a;
     consider s, t being Point of I[01] such that
w50: h.s = v.t by Lm4,Lm5,Lm11,Lm13,JGRAPH_8:6;
     dom h = the carrier of I[01] & dom v = the carrier of I[01]
     by FUNCT_2:def 1;
     then v.t in rng Pf & h.s in rng Pg by FUNCT_1:def 5;
     hence thesis by a4, a2,a3,w50,XBOOLE_0:3;
   end;
   
LEMACISKO: 
  |[-1,0]|,|[1,0]| realize-max-dist-in C implies
  ex Jc, Jd being compact with_the_max_arc Subset of T2 st
  Jc is_an_arc_of |[-1,0]|,|[1,0]| & Jd is_an_arc_of |[-1,0]|,|[1,0]| &
  C = Jc \/ Jd &
  Jc /\ Jd = {|[-1,0]|,|[1,0]|} & UMP C in Jc & LMP C in Jd &
  W-bound C = W-bound Jc & E-bound C = E-bound Jc
  proof 
    assume
a0: a,b realize-max-dist-in C;
    set U = Upper_Arc C;
    set L = Lower_Arc C;
a1: U \/ L = C by JORDAN6:def 9;
a2: UMP C in C by JORDAN21:43;
    LMP C in C by JORDAN21:44;
    then
a6: LMP C in U or LMP C in L by a1,XBOOLE_0:def 2;
a3: W-min C = a & E-max C = b by a0,Lm52,Lm52e;
    per cases by a1,a2,XBOOLE_0:def 2;
    suppose
a4:   UMP C in U;
      take U, L;
      thus thesis by a3,a4,a6,JORDAN6:65,JORDAN21:63,26,27;
    end;
    suppose
a4:   UMP C in L;
      take L, U;
      thus thesis by a3,a4,a6,JORDAN6:65,JORDAN21:62,28,29;
    end;
  end;

theorem TLEMAT:
  |[-1,0]|,|[1,0]| realize-max-dist-in C implies
  for Jc, Jd being compact with_the_max_arc Subset of T2 st
  Jc is_an_arc_of |[-1,0]|,|[1,0]| & Jd is_an_arc_of |[-1,0]|,|[1,0]| &
  C = Jc \/ Jd & Jc /\ Jd = {|[-1,0]|,|[1,0]|} & UMP C in Jc & LMP C in Jd &
  W-bound C = W-bound Jc & E-bound C = E-bound Jc
  for Ux being Subset of TOP-REAL 2 st
  Ux = skl Down ((1/2) * ((UMP (LSeg(LMP Jc,|[0,-3]|) /\ Jd)) + LMP Jc), C`)
  holds Ux is_inside_component_of C &
  for V being Subset of T2 st V is_inside_component_of C holds
  V = Ux
  proof 
    set m = UMP C;
    set j = LMP C;
    assume
a1: a,b realize-max-dist-in C;
    let Jc, Jd being compact with_the_max_arc Subset of T2 such that
d01: Jc is_an_arc_of a,b and
d02: Jd is_an_arc_of a,b and
d03: C = Jc \/ Jd and
d04: Jc /\ Jd = {a,b} and
d05: UMP C in Jc and
d06: LMP C in Jd and
b60: W-bound C = W-bound Jc and
b71: E-bound C = E-bound Jc;
     set l = LMP Jc;
     set LJ = LSeg(l,d) /\ Jd;
     set k = UMP LJ;
     set x = (1/2)*(k+l);
     set w = (W-bound C + E-bound C) / 2;
     let Ux be Subset of TOP-REAL 2 such that
UU:  Ux = skl Down (x,C`);
a72: C c= R by a1,Lm1;
b72: W-bound C = rl by a1,Lm21;
b73: E-bound C = rp by a1,Lm22;
     then
b79: w = 0 by b72;
a0:  a in C & b in C by a1,JORDAN24:def 1;
a9:  m in C by JORDAN21:43;
a10: l in Jc by JORDAN21:44;
a89: Jd c= C by d03,XBOOLE_1:7;
a78: Jc c= C by d03,XBOOLE_1:7;
     then
a86: l in C by a10;
e34: m`2 < c`2 by a1,a9,Lm11,Lm50;
b55: l`1 = (W-bound Jc + E-bound Jc) / 2 by JORDAN21:35
     .= 0 by b60,b71,b72,b73;
a15: c`1 = w by a1,Lm23;
a14: m`1 = w by JORDAN21:33;
z11: m <> a & m <> b & l <> a & l <> b by b79,b55,Lm4,Lm5,JORDAN21:33;
     then consider Pml being Path of m,l such that
a7:  rng Pml c= Jc and
d12: rng Pml misses {a,b} by d01,a10,d05,JORDAN1819;
     set ml = rng Pml;
a81: ml c= C by a7,a78,XBOOLE_1:1;
b24: j in C by a89,d06;
c04: LSeg(l,d) is vertical by b55,Lm12,SPPOL_1:37;
c18: d`2 <= j`2 by a1,a89,d06,Lm13,Lm51;
d30: j`1 = 0 by b79,JORDAN21:35;
     l in Vertical_Line w by b55,b72,b73,JORDAN6:34;
     then
i83: l in C /\ Vertical_Line w by a78,a10,XBOOLE_0:def 3;
     then j`2 <= l`2 by JORDAN21:42;
     then j in LSeg(l,d) by c18,b55,d30,Lm12,GOBOARD7:8;
     then
p05: LJ is non empty by d06,XBOOLE_0:def 3;
p04: LJ is compact by COMPTS_1:20;
p06: for A being vertical non empty compact Subset of TOP-REAL 2 holds
     A is with_the_max_arc;
p11: LJ is vertical by c04,Thpver;
     then
p01: LJ is with_the_max_arc by p06,p05,p04;
p02: k in LJ by p01,p04,JORDAN21:43;
     then
b52: k in LSeg(l,d) by XBOOLE_0:def 3;
a90: k in Jd by p02,XBOOLE_0:def 3;
     then
a87: k in C by a89;
c05: d in LSeg(l,d) by TOPREAL1:6;
c08: k`1 = 0 by b52,c04,c05,Lm12,SPPOL_1:def 3;
     k <> a & k <> b & j <> a & j <> b by c08,d30,EUCLID:56;
     then consider Pkj being Path of k,j such that
d20: rng Pkj c= Jd and
e10: rng Pkj misses {a,b} by d02,d06,a90,JORDAN1819;
     set kj = rng Pkj;
a82: kj c= C by a89,d20,XBOOLE_1:1;
a40: x in LSeg(k,l) by GOBOARD7:7;
a99: skl Down(x,C`) is_a_component_of T2|C` by CONNSP_1:43;
a60: the carrier of T2|C` = C` by JORDAN1:1;
c07: LSeg(l,k) is vertical by b55,c08,SPPOL_1:37;
c11: k in LSeg(l,k) by TOPREAL1:6;
c13: l = |[l`1,l`2]| & k = |[k`1,k`2]| by EUCLID:57;
c31: d = |[d`1,d`2]| by EUCLID:57;
     d`2 <= l`2 by a1,a78,a10,Lm13,Lm51;
     then
c12: k`2 <= l`2 by c13,b52,c31,b55,Lm12,JGRAPH_6:9;
     a <> k & b <> k & a <> l & b <> l by b55,c08,EUCLID:56;
     then not k in {a,b} & not l in {a,b} by TARSKI:def 2;
     then
b40: k <> l by a10,a90,d04,XBOOLE_0:def 3;
     then k`2 <> l`2 by b55,c08,TOPREAL3:11;
     then
i50: k`2 < l`2 by c12,REAL_1:def 5;
     k in Vertical_Line w by c08,b72,b73,JORDAN6:34;
     then k in C /\ Vertical_Line w by a89,a90,XBOOLE_0:def 3;
     then j`2 <= k`2 by JORDAN21:42;
     then d`2 <= k`2 by c18,XREAL_1:2;
     then
c17: LSeg(l,k) c= LSeg(l,d) by c07,c04,c08,c12,Lm12,JORDAN15:5;
b33: LSeg(l,k) \ {l,k} c= C`
     proof
       let q be set;
       assume that
b30:   q in LSeg(l,k) \ {l,k} and
b31:   not q in C`;
c06:   q in LSeg(l,k) by b30,XBOOLE_0:def 4;
       reconsider q as Point of T2 by b30;
b32:   q in C by b31,SUBSET_1:50;
b78:   q`1 = w by c11,c06,c07,c08,b72,b73,SPPOL_1:def 3;
       then
b74:   q in Vertical_Line w by JORDAN6:34;
       per cases by b32,d03,XBOOLE_0:def 2;
       suppose q in Jc;
         then q in Jc /\ Vertical_Line w by b74,XBOOLE_0:def 3;
         then
b77:     l`2 <= q`2 by b60,b71,JORDAN21:42;
         q`2 <= l`2 by c06,c12,b55,c08,c13,JGRAPH_6:9;
         then l`2 = q`2 by b77,XREAL_1:1;
         then l = q by b78,b55,b72,b73,TOPREAL3:11;
         then q in {l,k} by TARSKI:def 2;
         hence contradiction by b30,XBOOLE_0:def 4;
       end;
       suppose q in Jd;
         then
p10:     q in LJ by c17,c06,XBOOLE_0:def 3;
p20:     q`1 = d`1 by c17,c06,c04,c05,SPPOL_1:def 3;
         LJ c= LSeg(l,d) by XBOOLE_1:17;
         then
p12:     W-bound LSeg(l,d) <= W-bound LJ & E-bound LJ <= E-bound LSeg(l,d)
         by p10,JORDAN1E:2,4;
p14:     W-bound LJ = E-bound LJ by p04,p05,p11,SPRECT_1:17;
p16:     W-bound LSeg(l,d) = d`1 & E-bound LSeg(l,d) = d`1
         by b55,Lm12,SPRECT_1:62,65;
         then W-bound LSeg(l,d) = W-bound LJ & E-bound LSeg(l,d) = E-bound LJ
         by p12,p14,XREAL_1:1;
         then q in Vertical_Line ((W-bound LJ + E-bound LJ) / 2)
         by p16,p20,JORDAN6:34;
         then q in LJ /\ Vertical_Line ((W-bound LJ + E-bound LJ) / 2)
         by p10,XBOOLE_0:def 3;
         then
b77:     q`2 <= k`2 by p04,JORDAN21:41;    
         k`2 <= q`2 by c06,c12,b55,c08,c13,JGRAPH_6:9;
         then k`2 = q`2 by b77,XREAL_1:1;
         then k = q by c08,b78,b72,b73,TOPREAL3:11;
         then q in {l,k} by TARSKI:def 2;
         hence contradiction by b30,XBOOLE_0:def 4;
       end;
     end;
     then reconsider X = LSeg(l,k) \ {l,k} as Subset of T2|C` by JORDAN1:1;
     now
       assume x in {l,k};
       then x = l or x = k by TARSKI:def 2;
       hence contradiction by b40,Taba;
     end;
     then
b01: x in LSeg(l,k) \ {l,k} by a40,XBOOLE_0:def 4;
     then skl(x,C`) = skl Down(x,C`) by b33,CONNSP_3:27;
     then
b03: x in skl Down(x,C`) by b01,b33,CONNSP_3:26;
b04: X meets Ux by UU,b01,b03,XBOOLE_0:3;
     LSeg(l,k) \ {l,k} is connected by JORDAN1H:5;
     then X is connected by CONNSP_1:24;
     then
a93: X c= skl Down(x,C`) by UU,a99,b04,CONNSP_1:38;
w55: LSeg(l,k) c= R by a72,a86,a87,JORDAN1:def 1;
w04: the carrier of TR = R by TOPREALA:53;
     reconsider AR = a, BR = b, CR = c, DR = d
     as Point of TR by a0,a72,Lm30,Lmw59,Lm31,TOPREALA:53;
     
     consider Pcm being Path of c,m, fcm being map of I[01], T2|LSeg(c,m)
     such that
a61: rng fcm = LSeg(c,m) & Pcm = fcm by defs2;
w07: LSeg(c,m) c= R by a9,a72,Lm30,Lmw59,JORDAN1:def 1;
w40: ml c= R by a81,a72,XBOOLE_1:1;

:: component containg x is bounded
     thus Ux is_inside_component_of C
     proof
     thus
www: Ux is_a_component_of C` by a99,UU,CONNSP_1:def 6;
     assume not Ux is Bounded;
     then not Ux c= Ball(x,10) by JORDAN2C:16;
     then consider u being set such that
l51: u in Ux and
o05: not u in Ball(x,10) by TARSKI:def 3;
y12:  R c= Ball(x,10) by a40,w55,LmBall;
      reconsider u as Point of T2 by l51;
      reconsider xs = x, us = u as Point of T2|C`|skl Down(x,C`)
      by UU,l51,b03,JORDAN1:1;
i76:  Ux is open by www,SPRECT_3:18;
      skl Down(x,C`) is connected by a99,CONNSP_1:def 5;
      then Ux is connected by UU,CONNSP_1:24;
      then
i73:  Ux is_Region by i76,TOPREAL4:def 3;
      x in Ball(x,10) by Taa;
      then consider P1 being Subset of T2 such that
i74:  P1 is_S-P_arc_joining x,u and
i75:  P1 c= Ux by UU,l51,b03,i73,o05,TOPREAL4:30;
i71:  P1 is_an_arc_of x,u by i74,TOPREAL4:3;
      reconsider P2 = P1 as Subset of T2|C` by i75,UU,XBOOLE_1:1;
y20:  P1 is connected by i71,JORDAN6:11;
i24:  P2 c= skl Down(x,C`) by UU,i75;
i25:  P2 misses C by a60,SUBSET_1:43;
      then
i26:  P2 misses Jc by a78,XBOOLE_1:63;
i27:  P2 misses Jd by a89,i25,XBOOLE_1:63;
i41:  x`1 = 1/2*((k+l)`1) by TOPREAL3:9
      .= 1/2*(k`1+l`1) by TOPREAL3:7
      .= 0 by c08,b55;
      then
i36:  LSeg(d,x) is vertical by Lm12,SPPOL_1:37;
i39:  x = |[x`1,x`2]| by EUCLID:57;
i37:  x`2 < l`2 by i50,Lmnowy60;
i53:  k`2 < x`2 by i50,Lmnowy61;
      d`2 <= k`2 by a89,a90,a1,Lm13,Lm51;
      then
i38:  d`2 <= x`2 by i53,XREAL_1:2;
      d`1 = d`1;
      then
i33:  LSeg(d,x) c= LSeg(d,l) by i36,c04,i37,i38,JORDAN15:5;
i11:  LSeg(d,x) misses Jc
      proof
        assume not thesis;
        then consider q being set such that
i30:    q in LSeg(d,x) and
i31:    q in Jc by XBOOLE_0:3;
        reconsider q as Point of T2 by i30;
        q`2 <= x`2 by c31,i30,i39,i38,i41,Lm12,JGRAPH_6:9;
        then
i32:    q`2 < l`2 by i37,XREAL_1:2;
        q`1 = 0 by i33,i30,c04,c05,Lm12,SPPOL_1:def 3;
        then q in Vertical_Line w by b72,b73,JORDAN6:34;
        then q in Jc /\ Vertical_Line w by i31,XBOOLE_0:def 3;
        hence contradiction by i32,b60,b71,JORDAN21:42;
      end;
      set n = First_Point(P1,x,u,dR);
y13:  not u in R by o05,y12;
y21:  Fr R = dR by TRecFr;
      u in P1 by i71,TOPREAL1:4;
      then
y22:  P1 \ R <> {}T2 by y13,XBOOLE_0:def 4;
      x in P1 by i71,TOPREAL1:4;
      then P1 meets R by a40,w55,XBOOLE_0:3;
      then
i55:  P1 meets dR by y20,y21,y22,CONNSP_1:23;
      P1 is closed by i71,JORDAN6:12;
      then
i56:  P1 /\ dR is closed by TOPS_1:35;
i61:  n in P1 /\ dR by i55,i56,i71,JORDAN5C:def 1;
      then
i51:  n in dR by XBOOLE_0:def 3;
i70:  n in P1 by i61,XBOOLE_0:def 3;
      set alpha = Segment(P1,x,u,x,n);
i82:  rd < k`2 by a1,a89,a90,Lm51;
      l`2 <= m`2 by i83,JORDAN21:41;
      then x`2 < m`2 by i37,XREAL_1:2;
      then not x in dR by i82,i39,i41,Lmnowy67,e34,i53;
      then
i40:  alpha is_an_arc_of x,n by i70,i71,i51,JORDAN20:20;
I41:  alpha c= P1 by JORDAN16:5;
      then
i12:  alpha misses Jc by i26,XBOOLE_1:63;
I12:  alpha misses Jd by I41,i27,XBOOLE_1:63;
      consider Pdx being Path of d,x,
      fdx being map of I[01], T2|LSeg(d,x) such that
i77:  rng fdx = LSeg(d,x) & Pdx = fdx by defs2;
      consider PJc being Path of a,b, fJc being map of I[01], T2|Jc such that
w23:  rng fJc = Jc & PJc = fJc by d01,defa2;
      consider PJd being Path of a,b, fJd being map of I[01], T2|Jd such that
w24:  rng fJd = Jd & PJd = fJd by d02,defa2;
      consider Palpha being Path of x,n,
      falpha being map of I[01], T2|alpha such that
w28:  rng falpha = alpha & Palpha = falpha by i40,defa2;
      n in R by i51,Lmw59;
      then
w46:  ex p st p = n & rl <= p`1 & p`1 <= rp & rd <= p`2 & p`2 <= rg by Lm0;
      rng PJc c= the carrier of TR by w04,a72,a78,w23,XBOOLE_1:1;
      then reconsider h = PJc as Path of AR,BR by Th1a;
      rng PJd c= the carrier of TR by w04,a72,a89,w24,XBOOLE_1:1;
      then reconsider H = PJd as Path of AR,BR by Th1a;
w54:  LSeg(d,x) c= R by Lm31,Lmw59,a40,w55,JORDAN1:def 1;
w56:  alpha c= R by i71,a40,w55,y13,TSegZaw;
w44:  ld in LSeg(ld,lg) by TOPREAL1:6;
W44:  pd in LSeg(pd,pg) by TOPREAL1:6;
      LSeg(lg,c) misses C by a1,Lmnowy65;
      then
w32:  LSeg(lg,c) misses Jc by a78,XBOOLE_1:63;
j01:  LSeg(lg,c) c= R by Lmw59,Lmnowy62,XBOOLE_1:1;
J01:  LSeg(pg,c) c= R by Lmw59,Lmnowy62a,XBOOLE_1:1;
      LSeg(pg,c) misses C by a1,Lmnowy65a;
      then
W32:  LSeg(pg,c) misses Jc by a78,XBOOLE_1:63;
      consider Plx being Path of l,x, flx being map of I[01], T2|LSeg(l,x)
      such that
z01:  rng flx = LSeg(l,x) & Plx = flx by defs2;
      
      set PCX = Pcm + Pml + Plx;

z02:  rng PCX = rng Pcm \/ rng Pml \/ rng Plx by Trc31;

z03:  ml misses Jd
      proof
        assume ml meets Jd;
        then consider q being set such that
z05:    q in ml and
z06:    q in Jd by XBOOLE_0:3;
        q in {a,b} by a7,z05,z06,d04,XBOOLE_0:def 3;
        hence contradiction by d12,z05,XBOOLE_0:3;
      end;
a14:  LSeg(c,m) /\ C = {m} by a1,ThUMP;
z04:  LSeg(c,m) misses Jd
      proof
        assume LSeg(c,m) meets Jd;
        then consider q being set such that
z12:    q in LSeg(c,m) and
z13:    q in Jd by XBOOLE_0:3;
        q in {m} by z13,a89,a14,z12,XBOOLE_0:def 3;
        then q = m by TARSKI:def 1;
        then m in {a,b} by z13,d05,d04,XBOOLE_0:def 3;
        hence contradiction by z11,TARSKI:def 2;
      end;
      LSeg(l,x) is vertical by i41,b55,SPPOL_1:37;
      then
z14:  LSeg(l,x) c= LSeg(l,k) by c07,c08,i41,i53,i37,JORDAN15:5;
      l in LSeg(l,x) by TOPREAL1:6;
      then {l} c= LSeg(l,x) by ZFMISC_1:37;
      then
z07:  LSeg(l,x) = LSeg(l,x) \ {l} \/ {l} by XBOOLE_1:45;
      LSeg(l,x) \ {l} c= LSeg(l,k) \ {l,k}
      proof
        let q be set;
        assume q in LSeg(l,x) \ {l};
        then
z16:    q in LSeg(l,x) & q <> l by ZFMISC_1:64;
        then q <> k by i39,c13,i37,i53,b55,i41,JGRAPH_6:9;
        then not q in {l,k} by z16,TARSKI:def 2;
        hence thesis by z16,z14,XBOOLE_0:def 4;
      end;
      then LSeg(l,x) \ {l} c= C` by b33,XBOOLE_1:1;
      then LSeg(l,x) \ {l} misses C by SUBSET_1:43;
      then
z08:  LSeg(l,x) \ {l} misses Jd by a89,XBOOLE_1:63;
      {l} misses Jd
      proof
        assume {l} meets Jd;
        then l in Jd by ZFMISC_1:56;
        then l in {a,b} by a10,d04,XBOOLE_0:def 3;
        hence thesis by z11,TARSKI:def 2;
      end;
      then LSeg(l,x) misses Jd by z07,z08,XBOOLE_1:70;
      then
z20:  rng PCX misses Jd by z02,a61,z03,z04,z01,XBOOLE_1:114;

      LSeg(l,x) c= R by w55,z14,XBOOLE_1:1;
      then
z30:  rng PCX c= R by z02,a61,w07,w40,z01,LmZ3;
www:  LSeg(ld,d) misses C by a1,Lmnowy65x;
      then
ww1:  LSeg(ld,d) misses Jd by a89,XBOOLE_1:63;
y02:  LSeg(pd,d) misses C by a1,Lmnowy65ax;
      then
y03:  LSeg(pd,d) misses Jd by a89,XBOOLE_1:63;

      
      per cases;
      suppose
i60:    n`2 < 0;
        per cases by i51,i60,Lmnowy64a;
        suppose
i69:      n in LSeg(a,ld);
          consider Pnld being Path of n,ld,
          fnld being map of I[01], T2|LSeg(n,ld) such that
w21:      rng fnld = LSeg(n,ld) & Pnld = fnld by defs2;
          consider Pldd being Path of ld,d,
          fldd being map of I[01], T2|LSeg(ld,d) such that
w22:      rng fldd = LSeg(ld,d) & Pldd = fldd by defs2;
w41:      ld`1 = n`1 by w44,i69,Lm32hy,Lm32f,SPPOL_1:def 3;
          then LSeg(n,ld) is vertical by SPPOL_1:37;
          then LSeg(n,ld) c= LSeg(ld,lg) by w41,Lm73,w46,Lm71,Lm32f,JORDAN15:5;
          then
w57:      LSeg(n,ld) c= dR by Lm32b,XBOOLE_1:1;
          set K1 = PCX + Palpha + Pnld + Pldd;
          LSeg(n,ld) misses C by a1,i24,i70,a60,i69,Lmnowy66y;
          then
w31:      LSeg(n,ld) misses Jd by a89,XBOOLE_1:63;
w53:      rng K1 = rng PCX \/ rng Palpha \/ rng Pnld \/ rng Pldd by Trc33;
          then
w51:      rng PJd misses rng K1 by w24,I12,w28,w21,w31,w22,z20,ww1,Lmmisses4;
y01:      LSeg(ld,d) c= R by Lmw59,Lmnowy62x,XBOOLE_1:1;
          LSeg(n,ld) c= R by w57,Lmw59,XBOOLE_1:1;
          then rng K1 c= the carrier of TR
          by w53,w04,w56,w28,w21,w22,z30,y01,LmZ4;
          then reconsider v = K1 as Path of CR,DR by Th1a;
          consider s, t being Point of I[01] such that
w50:      H.s = v.t by Lm4,Lm5,Lm11,Lm13,JGRAPH_8:6;
          dom H = the carrier of I[01] & dom v = the carrier of I[01]
          by FUNCT_2:def 1;
          then H.s in rng PJd & v.t in rng K1 by FUNCT_1:def 5;
          hence contradiction by w50,w51,XBOOLE_0:3;
        end;

        suppose
i69:      n in LSeg(ld,d);
          consider Pnd being Path of n,d,
          fnd being map of I[01], T2|LSeg(n,d) such that
w21:      rng fnd = LSeg(n,d) & Pnd = fnd by defs2; 
          set K1 = PCX + Palpha + Pnd;
          ld in LSeg(ld,d) by TOPREAL1:6;
          then
w81:      ld`2 = n`2 by Lm32ix,i69,SPPOL_1:def 2;
          then
w80:      LSeg(n,d) is horizontal by Lm13,Lm73,SPPOL_1:36;
          ld`1 <= n`1 & n`1 <= d`1 by i69,Lm12,Lm72,JGRAPH_6:11;
          then
w98:      LSeg(n,d) c= LSeg(ld,d) by Lm32ix,w80,w81,JORDAN15:6;
          then
w57:      LSeg(n,d) c= dR by Lmnowy62x,XBOOLE_1:1;
          LSeg(n,d) misses C by w98,www,XBOOLE_1:63;
          then
w31:      LSeg(n,d) misses Jd by a89,XBOOLE_1:63;
w53:      rng K1 = rng PCX \/ rng Palpha \/ rng Pnd by Trc31;
          then
w52:      rng K1 misses Jd by I12,z20,w28,w21,w31,XBOOLE_1:114;
w58:      LSeg(n,d) c= R by w57,Lmw59,XBOOLE_1:1;
          rng K1 c= the carrier of TR
          by z30,w53,w04,w56,w28,w21,w58,LmZ3;
          then reconsider v = K1 as Path of CR,DR by Th1a;
          consider s, t being Point of I[01] such that
w50:      H.s = v.t by Lm4,Lm5,Lm11,Lm13,JGRAPH_8:6;
          dom H = the carrier of I[01] & dom v = the carrier of I[01]
          by FUNCT_2:def 1;
          then H.s in rng PJd & v.t in rng K1 by FUNCT_1:def 5;
          hence contradiction by w50,w24,w52,XBOOLE_0:3;
        end;

        suppose
i69:      n in LSeg(d,pd);
          consider Pnd being Path of n,d,
          fnd being map of I[01], T2|LSeg(n,d) such that
w21:      rng fnd = LSeg(n,d) & Pnd = fnd by defs2; 
          set K1 = PCX + Palpha + Pnd;
          pd in LSeg(pd,d) by TOPREAL1:6;
          then pd`2 = n`2 by Lm32iy,i69,SPPOL_1:def 2;
          then
w80:      LSeg(n,d) is horizontal by Lm13,Lm77,SPPOL_1:36;
zzz:      d`2 = d`2;
          d`1 <= n`1 & n`1 <= pd`1 by i69,Lm12,Lm76,JGRAPH_6:11;
          then
w98:      LSeg(n,d) c= LSeg(pd,d) by Lm32iy,w80,zzz,JORDAN15:6;
          then
w57:      LSeg(n,d) c= dR by Lmnowy62ax,XBOOLE_1:1;
          LSeg(n,d) misses C by w98,y02,XBOOLE_1:63;
          then
w31:      LSeg(n,d) misses Jd by a89,XBOOLE_1:63;
w53:      rng K1 = rng PCX \/ rng Palpha \/ rng Pnd by Trc31;
          then
w52:      rng K1 misses Jd by I12,z20,w28,w21,w31,XBOOLE_1:114;
w58:      LSeg(n,d) c= R by w57,Lmw59,XBOOLE_1:1;
          rng K1 c= the carrier of TR
          by z30,w53,w04,w56,w28,w21,w58,LmZ3;
          then reconsider v = K1 as Path of CR,DR by Th1a;
          consider s, t being Point of I[01] such that
w50:      H.s = v.t by Lm4,Lm5,Lm11,Lm13,JGRAPH_8:6;
          dom H = the carrier of I[01] & dom v = the carrier of I[01]
          by FUNCT_2:def 1;
          then H.s in rng PJd & v.t in rng K1 by FUNCT_1:def 5;
          hence contradiction by w50,w24,w52,XBOOLE_0:3;
        end;

        suppose
i69:      n in LSeg(pd,b);
          consider Pnpd being Path of n,pd,
          fnpd being map of I[01], T2|LSeg(n,pd) such that
w21:      rng fnpd = LSeg(n,pd) & Pnpd = fnpd by defs2;
          consider Ppdd being Path of pd,d,
          fpdd being map of I[01], T2|LSeg(pd,d) such that
w22:      rng fpdd = LSeg(pd,d) & Ppdd = fpdd by defs2;
w41:      pd`1 = n`1 by W44,Lm32F,i69,Lm32Hy,SPPOL_1:def 3;
          then LSeg(n,pd) is vertical by SPPOL_1:37;
          then LSeg(n,pd) c= LSeg(pd,pg) by w41,Lm77,w46,Lm75,Lm32F,JORDAN15:5;
          then
w57:      LSeg(n,pd) c= dR by Lm32d,XBOOLE_1:1;
          set K1 = PCX + Palpha + Pnpd + Ppdd;
          LSeg(n,pd) misses C by a1,i24,i70,a60,i69,Lmnowy66ay;
          then
w31:      LSeg(n,pd) misses Jd by a89,XBOOLE_1:63;
w53:      rng K1 = rng PCX \/ rng Palpha \/ rng Pnpd \/ rng Ppdd by Trc33;
          then
w51:      rng PJd misses rng K1 by w24,I12,w28,w21,w31,w22,z20,y03,Lmmisses4;
y01:      LSeg(pd,d) c= R by Lmw59,Lmnowy62ax,XBOOLE_1:1;
          LSeg(n,pd) c= R by w57,Lmw59,XBOOLE_1:1;
          then rng K1 c= the carrier of TR
          by w53,w04,w56,w28,w21,w22,z30,y01,LmZ4;
          then reconsider v = K1 as Path of CR,DR by Th1a;
          consider s, t being Point of I[01] such that
w50:      H.s = v.t by Lm4,Lm5,Lm11,Lm13,JGRAPH_8:6;
          dom H = the carrier of I[01] & dom v = the carrier of I[01]
          by FUNCT_2:def 1;
          then H.s in rng PJd & v.t in rng K1 by FUNCT_1:def 5;
          hence contradiction by w50,w51,XBOOLE_0:3;
        end;
      end;

      suppose
i60:    n`2 >= 0;
        per cases by i51,i60,Lmnowy64;
        suppose
i69:      n in LSeg(a,lg);
          consider Pnlg being Path of n,lg,
          fnlg being map of I[01], T2|LSeg(n,lg) such that
w21:      rng fnlg = LSeg(n,lg) & Pnlg = fnlg by defs2;
          consider Plgc being Path of lg,c,
          flgc being map of I[01], T2|LSeg(lg,c) such that
w22:      rng flgc = LSeg(lg,c) & Plgc = flgc by defs2;
w41:      ld`1 = n`1 by w44,Lm32f,i69,Lm32h,SPPOL_1:def 3;
          then LSeg(n,lg) is vertical by Lm70,Lm72,SPPOL_1:37;
          then LSeg(n,lg) c= LSeg(ld,lg) by w41,Lm73,w46,Lm71,Lm32f,JORDAN15:5;
          then
w57:      LSeg(n,lg) c= dR by Lm32b,XBOOLE_1:1;
          set K1 = Pdx + Palpha + Pnlg + Plgc;
          LSeg(n,lg) misses C by a1,i24,i70,a60,i69,Lmnowy66;
          then
w31:      LSeg(n,lg) misses Jc by a78,XBOOLE_1:63;
w53:      rng K1 = rng Pdx \/ rng Palpha \/ rng Pnlg \/ rng Plgc by Trc33;
          then
w52:      rng K1 misses Jc by i11,i12,i77,w28,w21,w31,w32,w22,Lmmisses4;
aaa:      rng K1 = rng -K1 by Trcm1;
          LSeg(n,lg) c= R by w57,Lmw59,XBOOLE_1:1;
          then rng K1 c= the carrier of TR
          by j01,w53,i77,w04,w54,w56,w28,w21,w22,LmZ4;
          then reconsider v = -K1 as Path of CR,DR by aaa,Th1a;
          consider s, t being Point of I[01] such that
w50:      h.s = v.t by Lm4,Lm5,Lm11,Lm13,JGRAPH_8:6;
          dom h = the carrier of I[01] & dom v = the carrier of I[01]
          by FUNCT_2:def 1;
          then h.s in rng PJc & v.t in rng -K1 by FUNCT_1:def 5;
          hence contradiction by w50,w23,w52,aaa,XBOOLE_0:3;
        end;

        suppose
i69:      n in LSeg(lg,c);
          consider Pnc being Path of n,c,
          fnc being map of I[01], T2|LSeg(n,c) such that
w21:      rng fnc = LSeg(n,c) & Pnc = fnc by defs2; 
          set K1 = Pdx + Palpha + Pnc;
          lg in LSeg(lg,c) by TOPREAL1:6;
          then
w81:      lg`2 = n`2 by Lm32i,i69,SPPOL_1:def 2;
          then
w80:      LSeg(n,c) is horizontal by Lm11,Lm71,SPPOL_1:36;
          lg`1 <= n`1 & n`1 <= c`1 by i69,Lm10,Lm70,JGRAPH_6:11;
          then
w98:      LSeg(n,c) c= LSeg(lg,c) by Lm32i,w80,w81,JORDAN15:6;
          then
w57:      LSeg(n,c) c= dR by Lmnowy62,XBOOLE_1:1;
          LSeg(lg,c) misses C by a1,Lmnowy65;
          then LSeg(n,c) misses C by w98,XBOOLE_1:63;
          then
w31:      LSeg(n,c) misses Jc by a78,XBOOLE_1:63;
w53:      rng K1 = rng Pdx \/ rng Palpha \/ rng Pnc by Trc31;
          then
w52:      rng K1 misses Jc by i11,i12,i77,w28,w21,w31,XBOOLE_1:114;
aaa:      rng K1 = rng -K1 by Trcm1;
w58:      LSeg(n,c) c= R by w57,Lmw59,XBOOLE_1:1;
          rng K1 c= the carrier of TR
          by w53,i77,w04,w54,w56,w28,w21,w58,LmZ3;
          then reconsider v = -K1 as Path of CR,DR by aaa,Th1a;
          consider s, t being Point of I[01] such that
w50:      h.s = v.t by Lm4,Lm5,Lm11,Lm13,JGRAPH_8:6;
          dom h = the carrier of I[01] & dom v = the carrier of I[01]
          by FUNCT_2:def 1;
          then h.s in rng PJc & v.t in rng -K1 by FUNCT_1:def 5;
          hence contradiction by w50,w23,w52,aaa,XBOOLE_0:3;
        end;

        suppose
i69:      n in LSeg(c,pg);
          consider Pnc being Path of n,c,
          fnc being map of I[01], T2|LSeg(n,c) such that
w21:      rng fnc = LSeg(n,c) & Pnc = fnc by defs2; 
          set K1 = Pdx + Palpha + Pnc;
          pg in LSeg(pg,c) by TOPREAL1:6;
          then pg`2 = n`2 by Lm32I,i69,SPPOL_1:def 2;
          then
w80:      LSeg(n,c) is horizontal by Lm11,Lm75,SPPOL_1:36;
w92:      c`2 = c`2;
          c`1 <= n`1 & n`1 <= pg`1 by i69,Lm10,Lm74,JGRAPH_6:11;
          then
w98:      LSeg(c,n) c= LSeg(c,pg) by w92,Lm32I,w80,JORDAN15:6;
          then
w57:      LSeg(n,c) c= dR by Lmnowy62a,XBOOLE_1:1;
          LSeg(pg,c) misses C by a1,Lmnowy65a;
          then LSeg(n,c) misses C by w98,XBOOLE_1:63;
          then
w31:      LSeg(n,c) misses Jc by a78,XBOOLE_1:63;
w53:      rng K1 = rng Pdx \/ rng Palpha \/ rng Pnc by Trc31;
          then
w52:      rng K1 misses Jc by i11,i12,i77,w28,w21,w31,XBOOLE_1:114;
aaa:      rng K1 = rng -K1 by Trcm1;
w58:      LSeg(n,c) c= R by w57,Lmw59,XBOOLE_1:1;
          rng K1 c= the carrier of TR
          by w53,i77,w04,w54,w56,w28,w21,w58,LmZ3;
          then reconsider v = -K1 as Path of CR,DR by aaa,Th1a;
          consider s, t being Point of I[01] such that
w50:      h.s = v.t by Lm4,Lm5,Lm11,Lm13,JGRAPH_8:6;
          dom h = the carrier of I[01] & dom v = the carrier of I[01]
          by FUNCT_2:def 1;
          then h.s in rng PJc & v.t in rng -K1 by FUNCT_1:def 5;
          hence contradiction by w50,w23,w52,aaa,XBOOLE_0:3;
        end;

        suppose
i69:      n in LSeg(pg,b);
          consider Pnpg being Path of n,pg,
          fnpg being map of I[01], T2|LSeg(n,pg) such that
w21:      rng fnpg = LSeg(n,pg) & Pnpg = fnpg by defs2;
          consider Ppgc being Path of pg,c,
          fpgc being map of I[01], T2|LSeg(pg,c) such that
w22:      rng fpgc = LSeg(pg,c) & Ppgc = fpgc by defs2;
w41:      pd`1 = n`1 by W44,Lm32F,i69,Lm32H,SPPOL_1:def 3;
          then LSeg(n,pg) is vertical by Lm74,Lm76,SPPOL_1:37;
          then LSeg(n,pg) c= LSeg(pd,pg) by w41,Lm77,w46,Lm75,Lm32F,JORDAN15:5;
          then
w57:      LSeg(n,pg) c= dR by Lm32d,XBOOLE_1:1;
          set K1 = Pdx + Palpha + Pnpg + Ppgc;
          LSeg(n,pg) misses C by a1,i24,i70,a60,i69,Lmnowy66a;
          then
w31:      LSeg(n,pg) misses Jc by a78,XBOOLE_1:63;
w53:      rng K1 = rng Pdx \/ rng Palpha \/ rng Pnpg \/ rng Ppgc by Trc33;
          then
w52:      rng K1 misses Jc by i11,i12,i77,w28,w21,w31,w22,W32,Lmmisses4;
aaa:      rng K1 = rng -K1 by Trcm1;
          LSeg(n,pg) c= R by w57,Lmw59,XBOOLE_1:1;
          then rng K1 c= the carrier of TR
          by J01,w53,i77,w04,w54,w56,w28,w21,w22,LmZ4;
          then reconsider v = -K1 as Path of CR,DR by aaa,Th1a;
          consider s, t being Point of I[01] such that
w50:      h.s = v.t by Lm4,Lm5,Lm11,Lm13,JGRAPH_8:6;
          dom h = the carrier of I[01] & dom v = the carrier of I[01]
          by FUNCT_2:def 1;
          then h.s in rng PJc & v.t in rng -K1 by FUNCT_1:def 5;
          hence contradiction by w50,w23,w52,aaa,XBOOLE_0:3;
        end;
      end;
    end;
    
:: uniqueness
    let V be Subset of T2;
    assume
a23: V is_inside_component_of C;
     assume
a95: V <> Ux;
     consider VP being Subset of T2|C` such that
a20: VP = V and
a21: VP is_a_component_of T2|C` and
     VP is bounded Subset of Euclid 2 by a23,JORDAN2C:17;
     reconsider T2C = T2|C` as non empty SubSpace of T2;
     VP <> {}(T2|C`) by a21,CONNSP_1:34;
     then reconsider VP as non empty Subset of T2C;
a80: V misses C by a20,a60,SUBSET_1:43;
     consider Pjd being Path of j,d,
     fjd being map of I[01], T2|LSeg(j,d) such that
a62: rng fjd = LSeg(j,d) & Pjd = fjd by defs2;
     consider Plk being Path of l,k,
     flk being map of I[01], T2|LSeg(l,k) such that
a41: rng flk = LSeg(l,k) & Plk = flk by defs2;
     set beta = Pcm + Pml + Plk + Pkj + Pjd;
a70: rng beta = rng Pcm \/ rng Pml \/ rng Plk \/ rng Pkj \/ rng Pjd by Trc35;
     dom beta = the carrier of I[01] by FUNCT_2:def 1
     .= [#]I[01] by PRE_TOPC:12;
     then beta.:dom beta is compact by WEIERSTR:14;
     then rng beta is compact by RELAT_1:146;
     then
a17: rng beta is closed by COMPTS_1:16;
a74: ml misses V by a80,a81,XBOOLE_1:63;
     {l,k} c= LSeg(l,k)
     proof
       let x be set;
       assume x in {l,k};
       then x = l or x = k by TARSKI:def 2;
       hence thesis by TOPREAL1:6;
     end;
     then
a84: LSeg(l,k) = LSeg(l,k) \ {l,k} \/ {l,k} by XBOOLE_1:45;
a85: LSeg(l,k) \ {l,k} misses V
     proof
       assume not thesis;
       then consider q being set such that
a91:   q in LSeg(l,k) \ {l,k} and
a92:   q in V by XBOOLE_0:3;
       V meets Ux by UU,a93,a91,a92,XBOOLE_0:3;
       hence contradiction by UU,a20,a21,a99,a95,CONNSP_1:37;
     end;
     not l in V & not k in V by a78,a10,a89,a90,a80,XBOOLE_0:3;
     then {l,k} misses V by ZFMISC_1:57;
     then
a75: LSeg(l,k) misses V by a84,a85,XBOOLE_1:70;
a76: kj misses V by a80,a82,XBOOLE_1:63;
a77: LSeg(j,d) misses V by a1,a23,Lm25;
     LSeg(c,m) misses V by a1,a23,Lm24;
     then LSeg(c,m) \/ ml \/ LSeg(l,k) misses V by a74,a75,XBOOLE_1:114;
     then
a52: rng beta misses V by a61,a62,a41,a76,a77,a70,XBOOLE_1:114;
e30: m = |[m`1,m`2]| by EUCLID:57;
e31: c = |[c`1,c`2]| by EUCLID:57;
e33: j = |[j`1,j`2]| by EUCLID:57;
e01: not a in LSeg(c,m) by b72,b73,a14,a15,e30,e31,e34,Lm4,JGRAPH_6:9;
     not a in ml by d12,ZFMISC_1:55;
     then
e02: not a in LSeg(c,m) \/ ml by e01,XBOOLE_0:def 2;
     not a in LSeg(l,k) by c08,b55,c12,c13,Lm4,JGRAPH_6:9;
     then 
e03: not a in LSeg(c,m) \/ ml \/ LSeg(l,k) by e02,XBOOLE_0:def 2;
     not a in kj by e10,ZFMISC_1:55;
     then 
e04: not a in LSeg(c,m) \/ ml \/ LSeg(l,k) \/ kj by e03,XBOOLE_0:def 2;
     not a in LSeg(j,d) by d30,c18,c31,e33,Lm4,Lm12,JGRAPH_6:9;
     then not a in rng beta by e04,a70,a61,a62,a41,XBOOLE_0:def 2;
     then consider ra being positive (real number)
     such that
a18: Ball(a,ra) misses rng beta by a17,TexK;
e01: not b in LSeg(c,m) by b72,b73,a14,a15,e30,e31,e34,Lm5,JGRAPH_6:9;
     not b in ml by d12,ZFMISC_1:55;
     then
e02: not b in LSeg(c,m) \/ ml by e01,XBOOLE_0:def 2;
     not b in LSeg(l,k) by c08,b55,c12,c13,Lm5,JGRAPH_6:9;
     then 
e03: not b in LSeg(c,m) \/ ml \/ LSeg(l,k) by e02,XBOOLE_0:def 2;
     not b in kj by e10,ZFMISC_1:55;
     then 
e04: not b in LSeg(c,m) \/ ml \/ LSeg(l,k) \/ kj by e03,XBOOLE_0:def 2;
     not b in LSeg(j,d) by d30,c18,c31,e33,Lm5,Lm12,JGRAPH_6:9;
     then not b in rng beta by e04,a70,a61,a62,a41,XBOOLE_0:def 2;
     then consider rb being positive (real number)
     such that
a19: Ball(b,rb) misses rng beta by a17,TexK;
     set A = Ball(a,ra), B = Ball(b,rb);
a28: a in A by Taa;
a29: b in B by Taa;
a25: C = Fr V by a20,a21,LEMAT56;
a26: Fr V = Cl V /\ Cl V` by TOPS_1:def 2;
     then a in Cl V by a0,a25,XBOOLE_0:def 3;
     then A meets V by a28,PRE_TOPC:def 13;
     then consider u being set such that
a30: u in A and
a31: u in V by XBOOLE_0:3;
     b in Cl V by a0,a25,a26,XBOOLE_0:def 3;
     then B meets V by a29,PRE_TOPC:def 13;
     then consider v being set such that
a32: v in B and
a33: v in V by XBOOLE_0:3;
     reconsider u, v as Point of T2 by a30,a32;
b12: the carrier of T2C|VP = VP by JORDAN1:1;
     reconsider u1 = u, v1 = v as Point of T2C|VP by a31,a33,a20,JORDAN1:1;
     T2C|VP is arcwise_connected by a21,Tarcwise;
     then
b11: u1,v1 are_connected by BORSUK_2:def 3;
     then consider fuv being map of I[01], T2C|VP such that
b10: fuv is continuous & fuv.0 = u1 & fuv.1 = v1 by BORSUK_2:def 1;
b13: T2C|VP = T2|V by a20,GOBOARD9:4;
     fuv is Path of u1,v1 by b10,b11,BORSUK_2:def 2;
     then reconsider uv = fuv as Path of u,v by b11,b13,TOPALG_2:1;
a34: rng fuv c= the carrier of T2C|VP;
a54: rng uv misses rng beta by a34,a20,b12,a52,XBOOLE_1:63;
     consider au being Path of a,u,
     fau being map of I[01], T2|LSeg(a,u) such that
a50: rng fau = LSeg(a,u) & au = fau by defs2;
     consider vb being Path of v,b,
     fvb being map of I[01], T2|LSeg(v,b) such that
a51: rng fvb = LSeg(v,b) & vb = fvb by defs2;
     set AB = au + uv + vb;
a56: rng AB = rng au \/ rng uv \/ rng vb by Trc31;
     a in A by Taa;
     then LSeg(a,u) c= A by a30,JORDAN1:def 1;
     then
a57: LSeg(a,u) misses rng beta by a18,XBOOLE_1:63;
     b in B by Taa;
     then LSeg(v,b) c= B by a32,JORDAN1:def 1;
     then LSeg(v,b) misses rng beta by a19,XBOOLE_1:63;
     then
a58: rng AB misses rng beta by a56,a50,a51,a57,a54,XBOOLE_1:114;
w02: a,b are_connected by BORSUK_2:def 3;
w20: V c= R by a1,a23,TCompRec;
w05: LSeg(a,u) c= R by a31,a0,a72,w20,JORDAN1:def 1;
w06: LSeg(v,b) c= R by a33,w20,a0,a72,JORDAN1:def 1;
     rng uv c= R by a34,a20,b12,w20,XBOOLE_1:1;
     then LSeg(a,u) \/ rng uv c= R by w05,XBOOLE_1:8;
     then rng AB c= the carrier of TR by w04,w06,a50,a51,a56,XBOOLE_1:8;
     then reconsider h = AB as Path of AR,BR by w02,Th1;
w03: c,d are_connected by BORSUK_2:def 3;
w08: LSeg(c,m) \/ ml c= R by w07,w40,XBOOLE_1:8;
w09: LSeg(c,m) \/ ml \/ LSeg(l,k) c= R by w08,w55,XBOOLE_1:8;
     kj c= R by a82,a72,XBOOLE_1:1;
     then
w10: LSeg(c,m) \/ ml \/ LSeg(l,k) \/ kj c= R by w09,XBOOLE_1:8;
     LSeg(j,d) c= R by a72,b24,Lm31,Lmw59,JORDAN1:def 1;
     then rng beta c= the carrier of TR by w04,w10,a70,a61,a62,a41,XBOOLE_1:8;
     then reconsider v = beta as Path of CR,DR by w03,Th1;
     consider s, t being Point of I[01] such that
a35: h.s = v.t by Lm4,Lm5,Lm11,Lm13,JGRAPH_8:6;
     dom h = the carrier of I[01] & dom v = the carrier of I[01]
     by FUNCT_2:def 1;
     then h.s in rng AB & v.t in rng beta by FUNCT_1:def 5;
     hence contradiction by a35,a58,XBOOLE_0:3;
   end;

theorem TBDD:
  |[-1,0]|,|[1,0]| realize-max-dist-in C implies
  for Jc, Jd being compact with_the_max_arc Subset of T2 st
  Jc is_an_arc_of |[-1,0]|,|[1,0]| & Jd is_an_arc_of |[-1,0]|,|[1,0]| &
  C = Jc \/ Jd & Jc /\ Jd = {|[-1,0]|,|[1,0]|} & UMP C in Jc & LMP C in Jd &
  W-bound C = W-bound Jc & E-bound C = E-bound Jc holds
  BDD C = skl Down ((1/2) * ((UMP (LSeg(LMP Jc,|[0,-3]|) /\ Jd)) + LMP Jc), C`)
  proof 
    assume
a1: a,b realize-max-dist-in C;
    let Jc, Jd being compact with_the_max_arc Subset of T2 such that
d01: Jc is_an_arc_of a,b and
d02: Jd is_an_arc_of a,b and
d03: C = Jc \/ Jd and
d04: Jc /\ Jd = {a,b} and
d05: UMP C in Jc and
d06: LMP C in Jd and
b60: W-bound C = W-bound Jc and
b71: E-bound C = E-bound Jc;
     reconsider
     Ux = skl Down((1/2) * ((UMP (LSeg(LMP Jc,d) /\ Jd)) + LMP Jc),C`)
     as Subset of T2 by PRE_TOPC:39;
     Ux = BDD C
     proof
       Ux is_inside_component_of C
       by TLEMAT,a1,d01,d02,d03,d04,d05,d06,b60,b71;
       hence Ux c= BDD C by JORDAN2C:26;
       set F = {B where B is Subset of T2: B is_inside_component_of C};
       let q be set;
       assume q in BDD C;
       then q in union F by JORDAN2C:def 5;
       then consider Z being set such that
z03:   q in Z and
z04:   Z in F by TARSKI:def 4;
       ex B being Subset of T2 st Z = B & B is_inside_component_of C by z04;
       hence thesis by z03,TLEMAT,a1,d01,d02,d03,d04,d05,d06,b60,b71;
     end;
     hence thesis;
   end;
   
LAST:
  |[-1,0]|,|[1,0]| realize-max-dist-in C implies C is Jordan
  proof 
    assume
a1: a,b realize-max-dist-in C;
    then consider Jc, Jd being compact with_the_max_arc Subset of T2 such that
d01: Jc is_an_arc_of a,b and
d02: Jd is_an_arc_of a,b and
d03: C = Jc \/ Jd and
d04: Jc /\ Jd = {a,b} and
d05: UMP C in Jc and
d06: LMP C in Jd and
b60: W-bound C = W-bound Jc and
b71: E-bound C = E-bound Jc by LEMACISKO;
     set l = LMP Jc;
     set LJ = LSeg(l,d) /\ Jd;
     set k = UMP LJ;
     set x = (1/2)*(k+l);
a99: skl Down(x,C`) is_a_component_of T2|C` by CONNSP_1:43;
z02: skl Down(x,C`) = BDD C by a1,d01,d02,d03,d04,d05,d06,b60,b71,TBDD;
     thus C` <>  {};
     take A1 = UBD C, A2 = BDD C;     
     thus C` = A1 \/ A2 by JORDAN2C:31;
     thus A1 misses A2 by JORDAN2C:28;
     A1 is_a_component_of C` by JORDAN1C:12;
     then
z30: ex B1 being Subset of T2|C` st B1 = A1 & B1 is_a_component_of T2|C`
     by CONNSP_1:def 6;
     then C = Fr A1 by LEMAT56
     .= Cl A1 /\ Cl A1` by TOPS_1:def 2;
     then
zw1: C c= Cl A1 by XBOOLE_1:17;
     C = Fr A2 by z02,a99,LEMAT56
     .= Cl A2 /\ Cl A2` by TOPS_1:def 2;
     then
zw2: C c= Cl A2 by XBOOLE_1:17;
     A2 c= C` by JORDAN2C:29;
     then C misses A2 by SUBSET_1:43;
     then
z2:  C c= Cl A2 \ A2 by zw2,XBOOLE_1:86;
f4:  A1 misses A2 by JORDAN2C:28;
     then A2 c= A1` by SUBSET_1:43;
     then
f2:  Cl A2 c= A1` by TOPS_1:31;
     A1 \/ A2 = C` by JORDAN2C:31;
     then A1 \/ A2 misses C by SUBSET_1:43;
     then C misses A1 by XBOOLE_1:70;
     then
f3:  A2 \/ C misses A1 by f4,XBOOLE_1:70;
     A2 \/ A1 = C` by JORDAN2C:31;
     then (A2 \/ A1)` misses C` by SUBSET_1:43;
     then (A2 \/ A1)` /\ C` = {} by XBOOLE_0:def 7;
     then (A2 \/ A1 \/ C)` = {} by SUBSET_1:29;
     then ((A2 \/ C) \/ A1)` = {} by XBOOLE_1:4;
     then (A2 \/ C)` /\ A1` = {} by SUBSET_1:29;
     then (A2 \/ C)` misses A1` by XBOOLE_0:def 7;
     then Cl A2 c= A2 \/ C by f2,f3,SUBSET_1:46;
     then
z3:  Cl A2 \ A2 c= C by XBOOLE_1:43;
     A1 c= C` by JORDAN2C:30;
     then C misses A1 by SUBSET_1:43;
     then
z1:  C c= Cl A1 \ A1 by zw1,XBOOLE_1:86;
     A1 c= A2` by f4,SUBSET_1:43;
     then
f2:  Cl A1 c= A2` by TOPS_1:31;
     A2 \/ A1 = C` by JORDAN2C:31;
     then A2 \/ A1 misses C by SUBSET_1:43;
     then C misses A2 by XBOOLE_1:70;
     then
f3:  A1 \/ C misses A2 by f4,XBOOLE_1:70;
     A1 \/ A2 = C` by JORDAN2C:31;
     then (A1 \/ A2)` misses C` by SUBSET_1:43;
     then (A1 \/ A2)` /\ C` = {} by XBOOLE_0:def 7;
     then (A1 \/ A2 \/ C)` = {} by SUBSET_1:29;
     then ((A1 \/ C) \/ A2)` = {} by XBOOLE_1:4;
     then (A1 \/ C)` /\ A2` = {} by SUBSET_1:29;
     then (A1 \/ C)` misses A2` by XBOOLE_0:def 7;
     then Cl A1 c= A1 \/ C by f2,f3,SUBSET_1:46;
     then
     Cl A1 \ A1 c= C by XBOOLE_1:43;
     hence (Cl A1) \ A1 = C by z1,XBOOLE_0:def 10
     .= (Cl A2) \ A2 by z2,z3,XBOOLE_0:def 10;
     thus thesis by z02,z30,CONNSP_1:43;
end;
  
registration
:: Jordan's Curve Theorem
  cluster -> Jordan Simple_closed_curve;
  coherence
  proof 
    let C;
    consider f being Homeomorphism of T2 such that
a1: a,b realize-max-dist-in f.:C by JORDAN24:7;
a2: f" is Homeomorphism of T2 by TOPGRP_1:30;
    f.:C is Simple_closed_curve by Tsim;
    then f.:C is Jordan by a1,LAST;
    then
a5: f".:(f.:C) is Jordan by a2,JORDAN24:16;
    rng f = [#]T2 by TOPS_2:def 5;
    then
a3: f" = f qua Function" by TOPS_2:def 4;
    dom f = the carrier of T2 by FUNCT_2:def 1;
    hence thesis by a5,a3,JGRAPH_6:22;
  end;
end;
