:: Jordan's Curve Theorem
::  by Artur Korni{\l}owicz
::
:: Received September 15, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies ARYTM, PRE_TOPC, EUCLID, TOPREAL1, RELAT_1, ARYTM_1, TOPS_1,
      SUBSET_1, JORDAN2C, METRIC_1, BOOLE, TOPREAL2, CONNSP_1, PSCOMP_1,
      TOPS_2, MCART_1, SQUARE_1, ARYTM_3, SPPOL_1, ASYMPT_0, FUNCT_1, FUNCT_5,
      JORDAN1, JGRAPH_6, JORDAN6, JORDAN21, JORDAN24, LATTICES, CONNSP_3,
      BORSUK_2, BORSUK_6, GRAPH_1, JORDAN5C, TOPGRP_1, COMPLEX1, BORSUK_1,
      ORDINAL2, TOPREALA, RELAT_2, RCOMP_1, TOPMETR, COMPTS_1, REALSET1,
      JORDAN1A, TOPREAL4, FUNCOP_1, TARSKI, JORDAN3, ABIAN, ZF_LANG, TREAL_1,
      SEQ_1, XREAL_0, COLLSP, CONNSP_2, SGRAPH1, FUNCT_4, SETFAM_1, PARTFUN3,
      PARTFUN1, KNASTER, BROUWER, TOPREALB, ABSVALUE, TERMORD, PROB_1, JORDAN;
 notations TARSKI, XBOOLE_0, SUBSET_1, ZFMISC_1, ORDINAL1, SQUARE_1, MCART_1,
      RELAT_1, FUNCT_1, RELSET_1, PARTFUN1, FUNCT_2, FUNCT_3, FUNCT_4,
      FUNCOP_1, PSCOMP_1, NUMBERS, XCMPLX_0, XXREAL_0, XREAL_0, REAL_1,
      COMPLEX1, RCOMP_1, DOMAIN_1, STRUCT_0, PRE_TOPC, TOPS_1, BORSUK_1,
      TOPS_2, COMPTS_1, TREAL_1, CONNSP_1, CONNSP_2, CONNSP_3, TEX_2, METRIC_1,
      TBSP_1, EUCLID, BORSUK_2, TOPREAL1, TOPREAL2, TOPMETR, SPPOL_1, JORDAN1,
      JORDAN2C, JORDAN5C, TOPREAL4, JORDAN6, JGRAPH_6, TOPREAL6, TOPREAL9,
      BORSUK_6, TOPREALA, JORDAN21, TOPGRP_1, JORDAN24, ABIAN, PARTFUN3,
      TOPREALB, BROUWER;
 constructors SETFAM_1, FUNCT_4, SQUARE_1, COMPLEX1, REALSET1, ABIAN, TOPS_1,
      CONNSP_1, COMPTS_1, TBSP_1, TSEP_1, TEX_2, MONOID_0, TOPREAL1, TREAL_1,
      TOPREAL4, SPPOL_1, JORDAN1, SPPOL_2, CONNSP_3, WAYBEL_3, JORDAN5C,
      JORDAN6, TOPGRP_1, JORDAN2C, TOPREAL6, JCT_MISC, BORSUK_4, JORDAN21,
      JGRAPH_6, TOPREAL9, TOPREALA, PARTFUN3, BROUWER, JORDAN24, SEQ_1;
 registrations XBOOLE_0, SUBSET_1, FUNCT_1, RELSET_1, FUNCT_2, NUMBERS,
      XXREAL_0, XREAL_0, SQUARE_1, NAT_1, MEMBERED, REALSET1, STRUCT_0,
      PRE_TOPC, TOPS_1, COMPTS_1, METRIC_1, BORSUK_1, TEX_2, TEX_4, MONOID_0,
      EUCLID, TOPMETR, TOPREAL1, TOPREAL2, SPPOL_1, JORDAN1, SPPOL_2, PSCOMP_1,
      BORSUK_2, WAYBEL_2, WAYBEL_3, JORDAN5A, JORDAN6, TOPGRP_1, YELLOW13,
      JORDAN2C, TOPREAL6, BORSUK_4, JORDAN21, TOPREAL9, TOPREALA, TOPREALB,
      RCOMP_3, PARTFUN3, TOPALG_5, BROUWER, JGRAPH_8, VALUED_0;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM, REAL;
 definitions TARSKI, XBOOLE_0, BORSUK_2, JORDAN1, SPPOL_1, JORDAN2C, JORDAN21,
      JORDAN24, TOPREAL2, CONNSP_1, BORSUK_1, PSCOMP_1, ABIAN, SQUARE_1,
      JGRAPH_6, TOPS_1, EUCLID, XXREAL_0, SUBSET_1, TOPREALA, STRUCT_0,
      RELAT_1, VALUED_1;
 theorems JORDAN6, PRE_TOPC, XBOOLE_0, EUCLID, TARSKI, SQUARE_1, CONNSP_1,
      JORDAN2C, JGRAPH_6, XREAL_1, JORDAN1K, TOPREAL6, BORSUK_2, JORDAN21,
      TOPREAL1, TOPREAL3, SPPOL_1, SUBSET_1, TOPMETR, TOPREAL9, TOPS_1,
      SPRECT_3, TOPRNS_1, TOPS_2, NAT_1, JORDAN1, TOPREALA, BORSUK_1, FUNCT_2,
      RCOMP_3, RELAT_1, GOBOARD7, FUNCT_1, BORSUK_6, XBOOLE_1, ZFMISC_1,
      CONNSP_3, TOPALG_2, GOBOARD9, SPPOL_2, PSCOMP_1, TOPREAL4, TSEP_1,
      STRUCT_0, TOPALG_3, JGRAPH_8, COMPTS_1, SEQ_4, FUNCOP_1, WEIERSTR,
      REAL_1, JORDAN5C, JORDAN16, XREAL_0, XCMPLX_1, MEMBERED, SPRECT_1,
      JORDAN24, TOPGRP_1, TOPREAL2, RCOMP_1, BORSUK_4, BROUWER, ABIAN,
      ENUMSET1, METRIC_1, PCOMPS_1, TOPREALB, ABSVALUE, TEX_2, MCART_1,
      BORSUK_5, METRIC_6, COMPLEX1, JGRAPH_2, SEQ_2, TIETZE, CONNSP_2, GRCAT_1,
      FUNCT_4, TREAL_1, JGRAPH_1, PARTFUN3, RFUNCT_1, YELLOW12, XCMPLX_0,
      FUNCT_3, JORDAN5B, TOPALG_5, GOBOARD6, XXREAL_0, VALUED_1;
 schemes FUNCT_2;

begin :: Preliminaries

:: I would like to thank Professor Yatsuka Nakamura
:: for including me to the team working on the formalization
:: of the Jordan Curve Theorem. Especially, I am very grateful
:: to Professor Nakamura for inviting me to Shinshu University, Nagano
:: to work on the project together.
:: I am also thankful to Professor Andrzej Trybulec for his
:: continual help and fruitful discussions during the formalization.

reserve a, b, c, d, r, s for real number,
  n for Element of NAT,
  p, p1, p2 for Point of TOP-REAL 2,
  x, y for Point of TOP-REAL n,
  C for Simple_closed_curve,
  A, B, P for Subset of TOP-REAL 2,
  U, V for Subset of (TOP-REAL 2)|C`,
  D for compact with_the_max_arc Subset of TOP-REAL 2;
set T2 = TOP-REAL 2;

Lm1: for A, B, C, Z being set st A c= Z & B c= Z & C c= Z holds
A \/ B \/ C c= Z
proof
  let A, B, C, Z be set;
  assume A c= Z & B c= Z;
  then A \/ B c= Z by XBOOLE_1:8;
  hence thesis by XBOOLE_1:8;
end;

Lm2: for A, B, C, D, Z being set st A c= Z & B c= Z & C c= Z & D c= Z holds
A \/ B \/ C \/ D c= Z
proof
  let A, B, C, D, Z be set;
  assume A c= Z & B c= Z & C c= Z;
  then A \/ B \/ C c= Z by Lm1;
  hence thesis by XBOOLE_1:8;
end;

Lm3: for A, B, C, D, Z being set st A misses Z & B misses Z &
C misses Z & D misses Z holds A \/ B \/ C \/ D misses Z
proof
  let A, B, C, D, Z be set;
  assume A misses Z & B misses Z & C misses Z;
  then A \/ B \/ C misses Z by XBOOLE_1:114;
  hence thesis by XBOOLE_1:70;
end;

registration
  let M be symmetric triangle Reflexive MetrStruct, x, y be Point of M;
  cluster dist(x,y) -> non negative;
  coherence by METRIC_1:5;
end;

registration
  let n be Element of NAT, x, y be Point of TOP-REAL n;
  cluster dist(x,y) -> non negative;
  coherence
  proof
    ex p, q being Point of Euclid n st p = x & q = y & dist(x,y) = dist(p,q)
    by TOPREAL6:def 1;
    hence 0 <= dist(x,y);
  end;
end;

theorem Th1:
  for p1, p2 being Point of TOP-REAL n st p1 <> p2 holds 1/2*(p1+p2) <> p1
proof
  let p1, p2 be Point of TOP-REAL n;
  set r = 1/2;
  assume that
A1: p1 <> p2 and
A2: r*(p1+p2) = p1;
  r*(p1+p2) = r*p1+r*p2 by EUCLID:36;
  then 0.REAL n = p1-(r*p1+r*p2) by A2,EUCLID:46
    .= p1-r*p1-r*p2 by EUCLID:50
    .= 1 * p1-r*p1-r*p2 by EUCLID:33
    .= (1-r)*p1-r*p2 by EUCLID:54
    .= r*(p1-p2) by EUCLID:53;
  then p1-p2 = 0.REAL n by EUCLID:35;
  hence thesis by A1,EUCLID:47;
end;

theorem Th2:
  p1`2 < p2`2 implies p1`2 < (1/2*(p1+p2))`2
proof
  assume
A1: p1`2 < p2`2;
  (1/2*(p1+p2))`2 = 1/2*((p1+p2)`2) by TOPREAL3:9
    .= 1/2*(p1`2+p2`2) by TOPREAL3:7
    .= (p1`2+p2`2)/2;
  hence thesis by A1,XREAL_1:228;
end;

theorem Th3:
  p1`2 < p2`2 implies (1/2*(p1+p2))`2 < p2`2
proof
  assume
A1: p1`2 < p2`2;
  (1/2*(p1+p2))`2 = 1/2*((p1+p2)`2) by TOPREAL3:9
    .= 1/2*(p1`2+p2`2) by TOPREAL3:7
    .= (p1`2+p2`2)/2;
  hence thesis by A1,XREAL_1:228;
end;

theorem Th4:
  for A being vertical Subset of TOP-REAL 2 holds A /\ B is vertical
proof
  let A be vertical Subset of TOP-REAL 2;
  let p, q be Point of T2;
  assume p in A /\ B & q in A /\ B;
  then p in A & q in A by XBOOLE_0:def 3;
  hence thesis by SPPOL_1:def 3;
end;

theorem
  for A being horizontal Subset of TOP-REAL 2 holds A /\ B is horizontal
proof
  let A be horizontal Subset of TOP-REAL 2;
  let p, q be Point of T2;
  assume p in A /\ B & q in A /\ B;
  then p in A & q in A by XBOOLE_0:def 3;
  hence thesis by SPPOL_1:def 2;
end;

theorem
  p in LSeg(p1,p2) & LSeg(p1,p2) is vertical implies LSeg(p,p2) is vertical
proof
  assume
A1: p in LSeg(p1,p2);
  assume
A2: LSeg(p1,p2) is vertical;
  then
A3: p1`1 = p2`1 by SPPOL_1:37;
  p1 in LSeg(p1,p2) by TOPREAL1:6;
  then p`1 = p1`1 by A1,A2,SPPOL_1:def 3;
  hence thesis by A3,SPPOL_1:37;
end;

theorem
  p in LSeg(p1,p2) & LSeg(p1,p2) is horizontal implies
  LSeg(p,p2) is horizontal
proof
  assume
A1: p in LSeg(p1,p2);
  assume
A2: LSeg(p1,p2) is horizontal;
  then
A3: p1`2 = p2`2 by SPPOL_1:36;
  p1 in LSeg(p1,p2) by TOPREAL1:6;
  then p`2 = p1`2 by A1,A2,SPPOL_1:def 2;
  hence thesis by A3,SPPOL_1:36;
end;

registration

:: LSeg(NW-corner C,NE-corner C) is horizontal; ::SPRECT_3:29
  let P be Subset of TOP-REAL 2;
  cluster LSeg(SW-corner P,SE-corner P) -> horizontal;
  coherence
  proof
    (SW-corner P)`2 = S-bound P by EUCLID:56
      .= (SE-corner P)`2 by EUCLID:56;
    hence thesis by SPPOL_1:36;
  end;
  cluster LSeg(NW-corner P,SW-corner P) -> vertical;
  coherence
  proof
    (NW-corner P)`1 = W-bound P by EUCLID:56
      .= (SW-corner P)`1 by EUCLID:56;
    hence thesis by SPPOL_1:37;
  end;
  cluster LSeg(NE-corner P,SE-corner P) -> vertical;
  coherence
  proof
    (NE-corner P)`1 = E-bound P by EUCLID:56
      .= (SE-corner P)`1 by EUCLID:56;
    hence thesis by SPPOL_1:37;
  end;
end;

registration
  let P be Subset of TOP-REAL 2;
  cluster LSeg(SE-corner P,SW-corner P) -> horizontal;
  coherence;
  cluster LSeg(SW-corner P,NW-corner P) -> vertical;
  coherence;
  cluster LSeg(SE-corner P,NE-corner P) -> vertical;
  coherence;
end;

registration
  cluster vertical non empty compact -> with_the_max_arc Subset of TOP-REAL 2;
  coherence
  proof
    let A be Subset of TOP-REAL 2;
    assume
A1: A is vertical non empty compact;
    then
A2: W-bound A = E-bound A by SPRECT_1:17;
A3: E-min A in A by A1,SPRECT_1:16;
    (E-min A)`1 = E-bound A by EUCLID:56;
    then E-min A in Vertical_Line((W-bound A+E-bound A)/2) by A2,JORDAN6:34;
    hence A meets Vertical_Line((W-bound A+E-bound A)/2) by A3,XBOOLE_0:3;
  end;
end;

theorem Th8:
  p1`1 <= r & r <= p2`1 implies LSeg(p1,p2) meets Vertical_Line(r)
proof
  assume that
A1: p1`1 <= r and
A2: r <= p2`1;
  set a = p1`1, b = p2`1;
  set l = (r-a) / (b-a);
  set k = (1-l)*p1+l*p2;
A3: a-a <= r-a by A1,XREAL_1:11;
  a <= b by A1,A2,XXREAL_0:2;
  then b-b <= b-a by XREAL_1:12;
  then
A4: 0 <= l by A3,XREAL_1:138;
  r-a <= b-a by A2,XREAL_1:11;
  then
A5: k in LSeg(p1,p2) by A3,A4,SPPOL_1:22,XREAL_1:185;
  per cases;
  suppose a <> b;
    then
A6: b-a <> 0;
    k`1 = (1-l)*a+l*b by TOPREAL9:41
      .= a+l*(b-a)
      .= a+(r-a) by A6,XCMPLX_1:88;
    then k in Vertical_Line(r) by JORDAN6:34;
    hence thesis by A5,XBOOLE_0:3;
  end;
  suppose
A7: a = b;
A8: p1 in LSeg(p1,p2) by TOPREAL1:6;
    a = r by A1,A2,A7,XXREAL_0:1;
    then p1 in Vertical_Line(r) by JORDAN6:34;
    hence thesis by A8,XBOOLE_0:3;
  end;
end;

theorem
  p1`2 <= r & r <= p2`2 implies LSeg(p1,p2) meets Horizontal_Line(r)
proof
  assume that
A1: p1`2 <= r and
A2: r <= p2`2;
  set a = p1`2, b = p2`2;
  set l = (r-a) / (b-a);
  set k = (1-l)*p1+l*p2;
A3: a-a <= r-a by A1,XREAL_1:11;
  a <= b by A1,A2,XXREAL_0:2;
  then b-b <= b-a by XREAL_1:12;
  then
A4: 0 <= l by A3,XREAL_1:138;
  r-a <= b-a by A2,XREAL_1:11;
  then
A5: k in LSeg(p1,p2) by A3,A4,SPPOL_1:22,XREAL_1:185;
  per cases;
  suppose a <> b;
    then
A6: b-a <> 0;
    k`2 = (1-l)*a+l*b by TOPREAL9:42
      .= a+l*(b-a)
      .= a+(r-a) by A6,XCMPLX_1:88;
    then k in Horizontal_Line(r) by JORDAN6:35;
    hence thesis by A5,XBOOLE_0:3;
  end;
  suppose
A7: a = b;
A8: p1 in LSeg(p1,p2) by TOPREAL1:6;
    a = r by A1,A2,A7,XXREAL_0:1;
    then p1 in Horizontal_Line(r) by JORDAN6:35;
    hence thesis by A8,XBOOLE_0:3;
  end;
end;

Lm4: now
  let n;
  let A be Subset of TOP-REAL n such that
A1: A is empty;
  consider p being Point of TOP-REAL n;
  A c= {p} by A1,XBOOLE_1:2;
  hence A is Bounded by JORDAN2C:16;
end;

registration
  let n;
  cluster empty -> Bounded Subset of TOP-REAL n;
  coherence by Lm4;
  cluster non Bounded -> non empty Subset of TOP-REAL n;
  coherence by Lm4;
end;

registration
  let n be non empty Element of NAT;
  cluster open closed non Bounded convex Subset of TOP-REAL n;
  existence
  proof
    take [#]TOP-REAL n;
    1 <= n by NAT_1:14;
    hence thesis by JORDAN2C:21,41;
  end;
end;

theorem Th10:
  for C being compact Subset of TOP-REAL 2 holds
  north_halfline UMP C \ {UMP C} misses C
proof
  let C be compact Subset of TOP-REAL 2;
  set p = UMP C;
  set L = north_halfline p;
  set w = (W-bound C + E-bound C) / 2;
  assume L \ {p} meets C;
  then consider x being set such that
A1: x in L \ {p} and
A2: x in C by XBOOLE_0:3;
A3: x in L & x <> p by A1,ZFMISC_1:64;
  reconsider x as Point of T2 by A1;
A4: x`1 = p`1 & x`2 >= p`2 by A3,TOPREAL1:def 12;
  then x`2 <> p`2 by A3,TOPREAL3:11;
  then
A5: x`2 > p`2 by A4,REAL_1:def 5;
  x`1 = w by A4,EUCLID:56;
  then x in Vertical_Line w by JORDAN6:34;
  then x in C /\ Vertical_Line w by A2,XBOOLE_0:def 3;
  hence thesis by A5,JORDAN21:41;
end;

theorem Th11:
  for C being compact Subset of TOP-REAL 2 holds
  south_halfline LMP C \ {LMP C} misses C
proof
  let C be compact Subset of TOP-REAL 2;
  set p = LMP C;
  set L = south_halfline p;
  set w = (W-bound C + E-bound C) / 2;
  assume L \ {p} meets C;
  then consider x being set such that
A1: x in L \ {p} and
A2: x in C by XBOOLE_0:3;
A3: x in L & x <> p by A1,ZFMISC_1:64;
  reconsider x as Point of T2 by A1;
A4: x`1 = p`1 & x`2 <= p`2 by A3,TOPREAL1:def 14;
  then x`2 <> p`2 by A3,TOPREAL3:11;
  then
A5: x`2 < p`2 by A4,REAL_1:def 5;
  x`1 = w by A4,EUCLID:56;
  then x in Vertical_Line w by JORDAN6:34;
  then x in C /\ Vertical_Line w by A2,XBOOLE_0:def 3;
  hence thesis by A5,JORDAN21:42;
end;

Lm5: for A being non empty convex Subset of T2 holds A is connected;

theorem Th12:
  for C being compact Subset of TOP-REAL 2 holds
  north_halfline UMP C \ {UMP C} c= UBD C
proof
  let C be compact Subset of TOP-REAL 2;
  set A = north_halfline UMP C \ {UMP C};
  reconsider A as non Bounded Subset of T2 by JORDAN2C:130,TOPREAL6:99;
  A is convex by JORDAN21:6;
  then A is connected by Lm5;
  hence thesis by Th10,JORDAN2C:133;
end;

theorem Th13:
  for C being compact Subset of TOP-REAL 2 holds
  south_halfline LMP C \ {LMP C} c= UBD C
proof
  let C be compact Subset of TOP-REAL 2;
  set A = south_halfline LMP C \ {LMP C};
  reconsider A as non Bounded Subset of T2 by JORDAN2C:131,TOPREAL6:99;
  A is convex by JORDAN21:7;
  then A is connected by Lm5;
  hence thesis by Th11,JORDAN2C:133;
end;

theorem Th14:
  A is_inside_component_of B implies UBD B misses A
proof
  assume A is_inside_component_of B;
  then A c= BDD B by JORDAN2C:26;
  hence thesis by JORDAN2C:28,XBOOLE_1:63;
end;

theorem
  A is_outside_component_of B implies BDD B misses A
proof
  assume
A1: A is_outside_component_of B;
  BDD B misses UBD B by JORDAN2C:28;
  hence thesis by A1,JORDAN2C:27,XBOOLE_1:63;
end;

Lm6: p in C implies {p} misses U
proof
  assume
A1: p in C;
A2: U is Subset of T2 by PRE_TOPC:39;
  the carrier of T2|C` = C` by PRE_TOPC:29;
  then U misses C by A2,SUBSET_1:43;
  then not p in U by A1,XBOOLE_0:3;
  hence thesis by ZFMISC_1:56;
end;
set C0 = Closed-Interval-TSpace(0,1);
set C1 = Closed-Interval-TSpace(-1,1);
set l0 = (#)(-1,1);
set l1 = (-1,1)(#);
set h1 = L[01](l0,l1);

Lm7: the carrier of [:T2,T2:] = [:the carrier of T2, the carrier of T2:]
by BORSUK_1:def 5;

Lm8: now
  let T be non empty TopSpace;
  let a be Real;
  set c = the carrier of T;
  set f = c --> a;
  thus f is continuous
  proof
A1: dom f = c by FUNCT_2:def 1;
A2: rng f = {a} by FUNCOP_1:14;
    let Y be Subset of REAL;
    assume Y is closed;
    per cases;
    suppose a in Y;
      then
A3:   rng f c= Y by A2,ZFMISC_1:37;
      f"Y = f"(rng f /\ Y) by RELAT_1:168
        .= f"rng f by A3,XBOOLE_1:28
        .= [#]T by A1,RELAT_1:169;
      hence f"Y is closed;
    end;
    suppose not a in Y;
      then
A4:   rng f misses Y by A2,ZFMISC_1:56;
      f"Y = f"(rng f /\ Y) by RELAT_1:168
        .= f"{} by A4,XBOOLE_0:def 7
        .= {}T by RELAT_1:171;
      hence f"Y is closed;
    end;
  end;
end;

theorem Th16:
  for r being positive (real number)
  for a being Point of TOP-REAL n holds a in Ball(a,r)
proof
  let r be positive (real number);
  let a be Point of TOP-REAL n;
  |. a-a .| = 0 by TOPRNS_1:29;
  hence thesis by TOPREAL9:7;
end;

theorem Th17:
  for r being non negative (real number)
  for p being Point of TOP-REAL n holds p is Point of Tdisk(p,r)
proof
  let r be non negative (real number);
  let p be Point of TOP-REAL n;
A1: the carrier of Tdisk(p,r) = cl_Ball(p,r) by BROUWER:3;
  |. p-p .| = 0 by TOPRNS_1:29;
  hence thesis by A1,TOPREAL9:8;
end;

registration
  let r be positive (real number);
  let n be non empty Element of NAT;
  let p, q be Point of TOP-REAL n;
  cluster cl_Ball(p,r) \ {q} -> non empty;
  coherence
  proof
A1: the carrier of Tcircle(p,r) = Sphere(p,r) by TOPREALB:9;
A2: the carrier of Tdisk(p,r) = cl_Ball(p,r) by BROUWER:3;
A3: Sphere(p,r) c= cl_Ball(p,r) by TOPREAL9:17;
    consider a being Point of Tcircle(p,r);
A4: a in Sphere(p,r) by A1;
    per cases;
    suppose
A5:   a = q;
A6:   p is Point of Tdisk(p,r) by Th17;
      |. p-p .| <> r by TOPRNS_1:29;
      then p <> q by A1,A5,TOPREAL9:9;
      hence thesis by A2,A6,ZFMISC_1:64;
    end;
    suppose a <> q;
      hence thesis by A3,A4,ZFMISC_1:64;
    end;
  end;
end;

theorem Th18:
  r <= s implies Ball(x,r) c= Ball(x,s)
proof
  reconsider xe = x as Point of Euclid n by TOPREAL3:13;
  Ball(x,r) = Ball(xe,r) & Ball(x,s) = Ball(xe,s) by TOPREAL9:13;
  hence thesis by PCOMPS_1:1;
end;

theorem Th19:
  cl_Ball(x,r) \ Ball(x,r) = Sphere(x,r)
proof
  thus cl_Ball(x,r) \ Ball(x,r) c= Sphere(x,r)
  proof
    let a be set;
    assume
A1: a in cl_Ball(x,r) \ Ball(x,r);
    then reconsider a as Point of TOP-REAL n;
    a in cl_Ball(x,r) & not a in Ball(x,r) by A1,XBOOLE_0:def 4;
    then |. a-x .| <= r & |. a-x .| >= r by TOPREAL9:7,8;
    then |. a-x .| = r by XXREAL_0:1;
    hence thesis by TOPREAL9:9;
  end;
  let a be set;
  assume
A2: a in Sphere(x,r);
  then reconsider a as Point of TOP-REAL n;
  |. a-x .| = r by A2,TOPREAL9:9;
  then a in cl_Ball(x,r) & not a in Ball(x,r) by TOPREAL9:7,8;
  hence thesis by XBOOLE_0:def 4;
end;

theorem Th20:
  y in Sphere(x,r) implies LSeg(x,y) \ {x,y} c= Ball(x,r)
proof
  assume
A1: y in Sphere(x,r);
  per cases;
  suppose
A2: r = 0;
    reconsider xe = x as Point of Euclid n by TOPREAL3:13;
    Sphere(x,r) = Sphere(xe,r) by TOPREAL9:15;
    then Sphere(x,r) = {x} by A2,TOPREAL6:62;
    then
A3: x = y by A1,TARSKI:def 1;
A4: LSeg(x,x) = {x} by TOPREAL1:7;
A5: {x,x} = {x} by ENUMSET1:69;
    {x} \ {x} = {} by XBOOLE_1:37;
    hence thesis by A3,A4,A5,XBOOLE_1:2;
  end;
  suppose
A6: r <> 0;
    let k be set;
    assume
A7: k in LSeg(x,y) \ {x,y};
    then k in LSeg(y,x) by XBOOLE_0:def 4;
    then consider l being Real such that
A8: 0 <= l & l <= 1 and
A9: k = (1-l)*x + l*y by SPPOL_1:21;
    reconsider k as Point of TOP-REAL n by A9;
    not k in {x,y} by A7,XBOOLE_0:def 4;
    then k <> x & k <> y by TARSKI:def 2;
    then 0 <> l & l <> 1 by A9,TOPREAL9:4;
    then
A10: 0 < l & l < 1 by A8,REAL_1:def 5;
    k-x = (1-l)*x - x + l*y by A9,JORDAN2C:9
      .= 1 * x - l*x - x + l*y by EUCLID:54
      .= x - l*x - x + l*y by EUCLID:33
      .= x - x - l*x + l*y by TOPREAL9:1
      .= 0.REAL n - l*x + l*y by EUCLID:46
      .= l*y - l*x by JORDAN2C:13
      .= l*(y-x) by EUCLID:53;
    then
A11: |. k-x .| = abs(l) * |. y-x .| by TOPRNS_1:8
      .= l*|. y-x .| by A8,ABSVALUE:def 1
      .= l*r by A1,TOPREAL9:9;
    0 <= r by A1,TOPREAL9:39;
    then l*r < 1 * r by A6,A10,XREAL_1:70;
    hence thesis by A11,TOPREAL9:7;
  end;
end;

theorem Th21:
  r < s implies cl_Ball(x,r) c= Ball(x,s)
proof
  assume
A1: r < s;
  let a be set;
  assume
A2: a in cl_Ball(x,r);
  then reconsider a as Point of TOP-REAL n;
  |. a-x .| <= r by A2,TOPREAL9:8;
  then |. a-x .| < s by A1,XXREAL_0:2;
  hence thesis by TOPREAL9:7;
end;

theorem Th22:
  r < s implies Sphere(x,r) c= Ball(x,s)
proof
  assume r < s;
  then
A1: cl_Ball(x,r) c= Ball(x,s) by Th21;
  Sphere(x,r) c= cl_Ball(x,r) by TOPREAL9:17;
  hence thesis by A1,XBOOLE_1:1;
end;

theorem Th23:
  for r being non zero (real number) holds Cl Ball(x,r) = cl_Ball(x,r)
proof
  let r be non zero (real number);
  thus Cl Ball(x,r) c= cl_Ball(x,r) by TOPREAL9:16,TOPS_1:31;
  per cases;
  suppose
A1: Ball(x,r) is empty;
    then r < 0 by TOPREAL9:21;
    then
A2: cl_Ball(x,r) is empty by TOPREAL9:22;
    for A being empty Subset of TOP-REAL n holds Cl A is empty;
    hence thesis by A1,A2;
  end;
  suppose
A3: Ball(x,r) is non empty;
    let a be set;
    assume
A4: a in cl_Ball(x,r);
    then reconsider a as Point of TOP-REAL n;
    reconsider ae = a as Point of Euclid n by TOPREAL3:13;
A5: 0 < r by A3,TOPREAL9:20;
    for s being real number st 0 < s & s < r holds Ball(ae,s) meets Ball(x,r)
    proof
      let s be real number such that
A6:   0 < s and
A7:   s < r;
      now
A8:     Ball(x,r) \/ Sphere(x,r) = cl_Ball(x,r) by TOPREAL9:18;
        per cases by A4,A8,XBOOLE_0:def 2;
        suppose
A9:       a in Ball(x,r);
          |.a-a.| = 0 by TOPRNS_1:29;
          then a in Ball(a,s) by A6,TOPREAL9:7;
          hence Ball(a,s) meets Ball(x,r) by A9,XBOOLE_0:3;
        end;
        suppose
A10:      a in Sphere(x,r);
          then
A11:      |. a-x .| = r by TOPREAL9:9;
          |. x-x .| = 0 by TOPRNS_1:29;
          then
A12:      x in Ball(x,r) by A5,TOPREAL9:7;
          set z = s/(2*r);
          set q = (1-z)*a+z*x;
          1 * r < 2*r by A5,XREAL_1:70;
          then s < 2*r by A7,XXREAL_0:2;
          then
A13:      z < 1 by A6,XREAL_1:191;
          0 < 2*r by A5,XREAL_1:131;
          then
A14:      0 < z by A6,XREAL_1:141;
          then
A15:      q in LSeg(a,x) by A13,SPPOL_1:22;
          Ball(x,r) misses Sphere(x,r) by TOPREAL9:19;
          then a <> x by A10,A12,XBOOLE_0:3;
          then q <> a & q <> x by A13,A14,TOPREAL9:4;
          then not q in {a,x} by TARSKI:def 2;
          then
A16:      q in LSeg(a,x) \ {a,x} by A15,XBOOLE_0:def 4;
A17:      LSeg(a,x) \ {a,x} c= Ball(x,r) by A10,Th20;
          q-a = (1-z)*a - a + z*x by JORDAN2C:9
            .= 1 * a - z*a - a + z*x by EUCLID:54
            .= a - z*a - a + z*x by EUCLID:33
            .= a - a - z*a + z*x by TOPREAL9:1
            .= 0.REAL n - z*a + z*x by EUCLID:46
            .= z*x - z*a by JORDAN2C:13
            .= z*(x-a) by EUCLID:53;
          then |.q-a.| = abs(z) * |.x-a.| by TOPRNS_1:8
            .= z*|.x-a.| by A14,ABSVALUE:def 1
            .= z*|. a-x .| by TOPRNS_1:28
            .= s/2 by A11,XCMPLX_1:93;
          then
A18:      q in Sphere(a,s/2) by TOPREAL9:9;
          s/2 < s/1 by A6,XREAL_1:78;
          then Sphere(a,s/2) c= Ball(a,s) by Th22;
          hence Ball(a,s) meets Ball(x,r) by A16,A17,A18,XBOOLE_0:3;
        end;
      end;
      hence thesis by TOPREAL9:13;
    end;
    hence thesis by A5,GOBOARD6:96;
  end;
end;

theorem Th24:
  for r being non zero (real number) holds Fr Ball(x,r) = Sphere(x,r)
proof
  let r be non zero (real number);
  set P = Ball(x,r);
  thus Fr P = Cl P \ P by TOPS_1:76
    .= cl_Ball(x,r) \ P by Th23
    .= Sphere(x,r) by Th19;
end;

registration
  let n be non empty Element of NAT;
  cluster Bounded -> proper Subset of TOP-REAL n;
  coherence
  proof
    [#]TOP-REAL n is not Bounded by JORDAN2C:41,NAT_1:14;
    hence thesis by TEX_2:5;
  end;
end;

registration
  let n;
  cluster non empty closed convex Bounded Subset of TOP-REAL n;
  existence
  proof
    take cl_Ball(0.REAL n,1);
    thus thesis;
  end;
  cluster non empty open convex Bounded Subset of TOP-REAL n;
  existence
  proof
    take Ball(0.REAL n,1);
    thus thesis;
  end;
end;

registration
  let n be Element of NAT;
  let A be Bounded Subset of TOP-REAL n;
  cluster Cl A -> Bounded;
  coherence by TOPREAL6:71;
end;

registration
  let n be Element of NAT;
  let A be Bounded Subset of TOP-REAL n;
  cluster Fr A -> Bounded;
  coherence by TOPREAL6:98;
end;

theorem Th25:
  for A being closed Subset of TOP-REAL n, p being Point of TOP-REAL n st
  not p in A ex r being positive (real number) st Ball(p,r) misses A
proof
  let A be closed Subset of TOP-REAL n, p be Point of TOP-REAL n;
  assume not p in A;
  then
A1: p in A` by SUBSET_1:50;
  reconsider e = p as Point of Euclid n by TOPREAL3:13;
  consider r being real number such that
A2: r > 0 and
A3: Ball(e,r) c= A` by A1,TOPMETR:22;
  reconsider r as positive (real number) by A2;
  take r;
  Ball(p,r) = Ball(e,r) by TOPREAL9:13;
  hence thesis by A3,SUBSET_1:43;
end;

theorem Th26:
  for A being Bounded Subset of TOP-REAL n, a being Point of TOP-REAL n
  ex r being positive (real number) st A c= Ball(a,r)
proof
  let A be Bounded Subset of TOP-REAL n;
  let a be Point of TOP-REAL n;
  reconsider C = A as bounded Subset of Euclid n by JORDAN2C:def 2;
  consider r being Real, x being Element of Euclid n such that
A1: 0 < r and
A2: C c= Ball(x,r) by METRIC_6:def 10;
  reconsider r as positive (real number) by A1;
  reconsider x1 = x as Point of TOP-REAL n by TOPREAL3:13;
  take s = r+|.x1-a.|;
  let p be set;
  assume
A3: p in A;
  then reconsider p1 = p as Point of TOP-REAL n;
  p = p1;
  then reconsider p as Point of Euclid n by TOPREAL3:13;
A4: dist(p,x) < r by A2,A3,METRIC_1:12;
A5: |.p1-x1.| = dist(p,x) by SPPOL_1:62;
A6: |.p1-a.| <= |.p1-x1.| + |.x1-a.| by TOPRNS_1:35;
  |.p1-x1.| + |.x1-a.| < s by A4,A5,XREAL_1:8;
  then |.p1-a.| < s by A6,XXREAL_0:2;
  hence thesis by TOPREAL9:7;
end;

theorem Th27:
  for S, T being TopStruct, f being Function
  of S, T st f is_homeomorphism holds f is onto
proof
  let S, T be TopStruct;
  let f be Function of S, T;
  assume f is_homeomorphism;
  then rng f = [#]T by TOPS_2:def 5
    .= the carrier of T;
  hence f is onto by FUNCT_2:def 3;
end;

registration
  let T be non empty being_T2 TopSpace;
  cluster -> being_T2 (non empty SubSpace of T);
  coherence by TOPMETR:3;
end;

registration
  let p, r;
  cluster Tdisk(p,r) -> closed;
  coherence
  proof
    let A be Subset of T2;
    assume A = the carrier of Tdisk(p,r);
    then A = cl_Ball(p,r) by BROUWER:3;
    hence A is closed;
  end;
end;

registration
  let p, r;
  cluster Tdisk(p,r) -> compact;
  coherence
  proof
    set D = Tdisk(p,r);
    reconsider Q = [#]D as Subset of T2 by TSEP_1:1;
    [#]D = cl_Ball(p,r) by BROUWER:3;
    then Q is compact by TOPREAL6:88;
    then [#]D is compact by COMPTS_1:11;
    hence thesis by COMPTS_1:10;
  end;
end;

begin :: Paths

theorem
  for T being non empty TopSpace, a, b being Point of T
  for f being Path of a,b st a,b are_connected holds rng f is connected
proof
  let T be non empty TopSpace, a, b be Point of T;
  let f be Path of a,b such that
A1: a,b are_connected;
A2: dom f = the carrier of I[01] by FUNCT_2:def 1;
  reconsider A = [.0,1.] as Subset of R^1 by TOPMETR:24;
  reconsider B = A as Subset of I[01] by A2,BORSUK_1:83;
  A is connected by RCOMP_3:52;
  then
A3: B is connected by CONNSP_1:24;
A4: f is continuous by A1,BORSUK_2:def 2;
  f.:B = rng f by A2,BORSUK_1:83,RELAT_1:146;
  hence rng f is connected by A3,A4,TOPS_2:75;
end;

theorem Th29:
  for X being non empty TopSpace, Y being non empty SubSpace of X,
  x1, x2 being Point of X, y1, y2 being Point of Y,
  f being Path of x1,x2 st x1 = y1 & x2 = y2 & x1,x2 are_connected &
  rng f c= the carrier of Y holds y1,y2 are_connected & f is Path of y1,y2
proof
  let X be non empty TopSpace, Y be non empty SubSpace of X,
  x1, x2 be Point of X, y1, y2 be Point of Y, f be Path of x1,x2 such that
A1: x1 = y1 & x2 = y2 and
A2: x1, x2 are_connected;
  assume rng f c= the carrier of Y;
  then reconsider g = f as Function of I[01], Y by FUNCT_2:8;
A3: f is continuous by A2,BORSUK_2:def 2; thus
A4: ex f being Function of I[01], Y st f is continuous & f.0 = y1 & f.1 = y2
  proof
    take g;
    thus g is continuous by A3,TOPMETR:8;
    thus thesis by A1,A2,BORSUK_2:def 2;
  end;
  g is Path of y1,y2
  proof
    thus ex f being Function
    of I[01], Y st f is continuous & f.0 = y1 & f.1 = y2 by A4;
    thus g is continuous by A3,TOPMETR:8;
    thus g.0 = y1 & g.1 = y2 by A1,A2,BORSUK_2:def 2;
  end;
  hence f is Path of y1,y2;
end;

theorem Th30:
  for X being arcwise_connected non empty TopSpace,
  Y being non empty SubSpace of X,
  x1, x2 being Point of X, y1, y2 being Point of Y,
  f being Path of x1,x2 st x1 = y1 & x2 = y2 & rng f c= the carrier of Y holds
  y1,y2 are_connected & f is Path of y1,y2
proof
  let X be arcwise_connected non empty TopSpace, Y be non empty SubSpace of X,
  x1, x2 be Point of X, y1, y2 be Point of Y;
  x1,x2 are_connected by BORSUK_2:def 3;
  hence thesis by Th29;
end;

Lm9: for T being non empty TopSpace, a, b being Point of T
for f being Path of a,b st a,b are_connected holds rng f c= rng -f
proof
  let T be non empty TopSpace;
  let a, b be Point of T;
  let f be Path of a,b;
  assume
A1: a,b are_connected;
  let y be set;
  assume y in rng f;
  then consider x being set such that
A2: x in dom f and
A3: f.x = y by FUNCT_1:def 5;
  reconsider x as Point of I[01] by A2;
A4: dom -f = the carrier of I[01] by FUNCT_2:def 1;
A5: 1-x is Point of I[01] by JORDAN5B:4;
  then (-f).(1-x) = f.(1-(1-x)) by A1,BORSUK_2:def 6;
  hence thesis by A3,A4,A5,FUNCT_1:def 5;
end;

theorem Th31:
  for T being non empty TopSpace, a, b being Point of T
  for f being Path of a,b st a,b are_connected holds rng f = rng -f
proof
  let T be non empty TopSpace;
  let a, b be Point of T;
  let f be Path of a,b;
  assume
A1: a,b are_connected;
  hence rng f c= rng -f by Lm9;
  f = --f by A1,BORSUK_6:50;
  hence thesis by A1,Lm9;
end;

theorem Th32:
  for T being arcwise_connected non empty TopSpace, a, b being Point of T
  for f being Path of a,b holds rng f = rng -f
proof
  let T be arcwise_connected non empty TopSpace;
  let a, b be Point of T;
  let f be Path of a,b;
  a,b are_connected by BORSUK_2:def 3;
  hence thesis by Th31;
end;

theorem Th33:
  for T being non empty TopSpace, a, b, c being Point of T
  for f being Path of a,b, g being Path of b,c st
  a,b are_connected & b,c are_connected holds rng f c= rng(f+g)
proof
  let T be non empty TopSpace;
  let a, b, c be Point of T;
  let f be Path of a,b;
  let g be Path of b,c;
  assume that
A1: a,b are_connected and
A2: b,c are_connected;
  let y be set;
  assume y in rng f;
  then consider x being set such that
A3: x in dom f and
A4: f.x = y by FUNCT_1:def 5;
A5: dom(f+g) = the carrier of I[01] by FUNCT_2:def 1;
  reconsider x as Point of I[01] by A3;
  1/2*x = x/2;
  then
A6: x/2 is Point of I[01] by BORSUK_6:9;
  x <= 1 by BORSUK_1:86;
  then x/2 <= 1/2 by XREAL_1:74;
  then (f+g).(x/2) = f.(2*(x/2)) by A1,A2,A6,BORSUK_2:def 5;
  hence y in rng(f+g) by A4,A5,A6,FUNCT_1:def 5;
end;

theorem
  for T being arcwise_connected non empty TopSpace, a, b, c being Point of T
  for f being Path of a,b, g being Path of b,c holds rng f c= rng(f+g)
proof
  let T be arcwise_connected non empty TopSpace;
  let a, b, c be Point of T;
  let f be Path of a,b;
  let g be Path of b,c;
  a,b are_connected & b,c are_connected by BORSUK_2:def 3;
  hence thesis by Th33;
end;

theorem Th35:
  for T being non empty TopSpace, a, b, c being Point of T
  for f being Path of b,c, g being Path of a,b st
  a,b are_connected & b,c are_connected holds rng f c= rng(g+f)
proof
  let T be non empty TopSpace;
  let a, b, c be Point of T;
  let f be Path of b,c;
  let g be Path of a,b;
  assume that
A1: a,b are_connected and
A2: b,c are_connected;
  let y be set;
  assume y in rng f;
  then consider x being set such that
A3: x in dom f and
A4: f.x = y by FUNCT_1:def 5;
A5: dom(g+f) = the carrier of I[01] by FUNCT_2:def 1;
  reconsider x as Point of I[01] by A3;
  0 <= x by BORSUK_1:86;
  then 0/2 <= x/2 by XREAL_1:74;
  then
A6: 0+1/2 <= x/2+1/2 by XREAL_1:8;
  x <= 1 by BORSUK_1:86;
  then x+1 <= 1+1 by XREAL_1:8;
  then (x+1)/2 <= 2/2 by XREAL_1:74;
  then
A7: x/2+1/2 is Point of I[01] by A6,BORSUK_1:86;
  then (g+f).(x/2+1/2) = f.(2*(x/2+1/2)-1) by A1,A2,A6,BORSUK_2:def 5;
  hence y in rng(g+f) by A4,A5,A7,FUNCT_1:def 5;
end;

theorem
  for T being arcwise_connected non empty TopSpace, a, b, c being Point of T
  for f being Path of b,c, g being Path of a,b holds rng f c= rng(g+f)
proof
  let T be arcwise_connected non empty TopSpace;
  let a, b, c be Point of T;
  let f be Path of b,c;
  let g be Path of a,b;
  a,b are_connected & b,c are_connected by BORSUK_2:def 3;
  hence thesis by Th35;
end;

theorem Th37:
  for T being non empty TopSpace, a, b, c being Point of T
  for f being Path of a,b, g being Path of b,c st
  a,b are_connected & b,c are_connected holds rng(f+g) = rng f \/ rng g
proof
  let T be non empty TopSpace;
  let a, b, c be Point of T;
  let f be Path of a,b;
  let g be Path of b,c;
  assume that
A1: a,b are_connected and
A2: b,c are_connected;
  thus rng(f+g) c= rng f \/ rng g
  proof
    let y be set;
    assume y in rng(f+g);
    then consider x being set such that
A3: x in dom(f+g) and
A4: y = (f+g).x by FUNCT_1:def 5;
    reconsider x as Point of I[01] by A3;
    per cases;
    suppose
A5:   x <= 1/2;
      then
A6:   (f+g).x = f.(2*x) by A1,A2,BORSUK_2:def 5;
A7:   rng f c= rng f \/ rng g by XBOOLE_1:7;
A8:   dom f = the carrier of I[01] by FUNCT_2:def 1;
      2*x is Point of I[01] by A5,BORSUK_6:6;
      then y in rng f by A4,A6,A8,FUNCT_1:def 5;
      hence thesis by A7;
    end;
    suppose
A9:   1/2 <= x;
      then
A10:  (f+g).x = g.(2*x-1) by A1,A2,BORSUK_2:def 5;
A11:  rng g c= rng f \/ rng g by XBOOLE_1:7;
A12:  dom g = the carrier of I[01] by FUNCT_2:def 1;
      2*x-1 is Point of I[01] by A9,BORSUK_6:7;
      then y in rng g by A4,A10,A12,FUNCT_1:def 5;
      hence thesis by A11;
    end;
  end;
  rng f c= rng(f+g) & rng g c= rng(f+g) by A1,A2,Th33,Th35;
  hence thesis by XBOOLE_1:8;
end;

theorem
  for T being arcwise_connected non empty TopSpace, a, b, c being Point of T
  for f being Path of a,b, g being Path of b,c holds rng(f+g) = rng f \/ rng g
proof
  let T be arcwise_connected non empty TopSpace;
  let a, b, c be Point of T;
  let f be Path of a,b;
  let g be Path of b,c;
  a,b are_connected & b,c are_connected by BORSUK_2:def 3;
  hence thesis by Th37;
end;

theorem Th39:
  for T being non empty TopSpace, a, b, c, d being Point of T
  for f being Path of a,b, g being Path of b,c, h being Path of c,d st
  a,b are_connected & b,c are_connected & c,d are_connected holds
  rng(f+g+h) = rng f \/ rng g \/ rng h
proof
  let T be non empty TopSpace;
  let a, b, c, d be Point of T;
  let f be Path of a,b;
  let g be Path of b,c;
  let h be Path of c,d;
  assume that
A1: a,b are_connected and
A2: b,c are_connected and
A3: c,d are_connected;
  a,c are_connected by A1,A2,BORSUK_6:46;
  hence rng(f+g+h) = rng(f+g) \/ rng h by A3,Th37
    .= rng f \/ rng g \/ rng h by A1,A2,Th37;
end;

theorem Th40:
  for T being arcwise_connected non empty TopSpace, a, b, c, d being Point of T
  for f being Path of a,b, g being Path of b,c, h being Path of c,d
  holds rng(f+g+h) = rng f \/ rng g \/ rng h
proof
  let T be arcwise_connected non empty TopSpace;
  let a, b, c, d be Point of T;
  let f be Path of a,b;
  let g be Path of b,c;
  let h be Path of c,d;
  a,b are_connected & b,c are_connected & c,d are_connected by BORSUK_2:def 3;
  hence thesis by Th39;
end;

Lm10: for T being non empty TopSpace, a, b, c, d, e being Point of T
for f being Path of a,b, g being Path of b,c,
h being Path of c,d, i being Path of d,e st
a,b are_connected & b,c are_connected &
c,d are_connected & d,e are_connected holds
rng(f+g+h+i) = rng f \/ rng g \/ rng h \/ rng i
proof
  let T be non empty TopSpace;
  let a, b, c, d, e be Point of T;
  let f be Path of a,b;
  let g be Path of b,c;
  let h be Path of c,d;
  let i be Path of d,e;
  assume that
A1: a,b are_connected and
A2: b,c are_connected and
A3: c,d are_connected and
A4: d,e are_connected;
  a,c are_connected by A1,A2,BORSUK_6:46;
  then a,d are_connected by A3,BORSUK_6:46;
  hence rng(f+g+h+i) = rng(f+g+h) \/ rng i by A4,Th37
    .= rng f \/ rng g \/ rng h \/ rng i by A1,A2,A3,Th39;
end;

Lm11: for T being arcwise_connected non empty TopSpace,
a, b, c, d, e being Point of T for f being Path of a,b, g being Path of b,c,
h being Path of c,d, i being Path of d,e
holds rng(f+g+h+i) = rng f \/ rng g \/ rng h \/ rng i
proof
  let T be arcwise_connected non empty TopSpace;
  let a, b, c, d, e be Point of T;
  let f be Path of a,b;
  let g be Path of b,c;
  let h be Path of c,d;
  let i be Path of d,e;
  a,b are_connected & b,c are_connected &
  c,d are_connected & d,e are_connected by BORSUK_2:def 3;
  hence thesis by Lm10;
end;

Lm12: for T being non empty TopSpace, a, b, c, d, e, z being Point of T
for f being Path of a,b, g being Path of b,c,
h being Path of c,d, i being Path of d,e, j being Path of e,z st
a,b are_connected & b,c are_connected & c,d are_connected &
d,e are_connected & e,z are_connected holds
rng(f+g+h+i+j) = rng f \/ rng g \/ rng h \/ rng i \/ rng j
proof
  let T be non empty TopSpace;
  let a, b, c, d, e, z be Point of T;
  let f be Path of a,b;
  let g be Path of b,c;
  let h be Path of c,d;
  let i be Path of d,e;
  let j be Path of e,z;
  assume that
A1: a,b are_connected and
A2: b,c are_connected and
A3: c,d are_connected and
A4: d,e are_connected and
A5: e,z are_connected;
  a,c are_connected by A1,A2,BORSUK_6:46;
  then a,d are_connected by A3,BORSUK_6:46;
  then a,e are_connected by A4,BORSUK_6:46;
  hence rng(f+g+h+i+j) = rng(f+g+h+i) \/ rng j by A5,Th37
    .= rng f \/ rng g \/ rng h \/ rng i \/ rng j by A1,A2,A3,A4,Lm10;
end;

Lm13: for T being arcwise_connected non empty TopSpace,
a, b, c, d, e, z being Point of T
for f being Path of a,b, g being Path of b,c,
h being Path of c,d, i being Path of d,e, j being Path of e,z
holds rng(f+g+h+i+j) = rng f \/ rng g \/ rng h \/ rng i \/ rng j
proof
  let T be arcwise_connected non empty TopSpace;
  let a, b, c, d, e, z be Point of T;
  let f be Path of a,b;
  let g be Path of b,c;
  let h be Path of c,d;
  let i be Path of d,e;
  let j be Path of e,z;
  a,b are_connected & b,c are_connected & c,d are_connected &
  d,e are_connected & e,z are_connected by BORSUK_2:def 3;
  hence thesis by Lm12;
end;

theorem Th41:
  for T being non empty TopSpace, a being Point of T holds
  I[01] --> a is Path of a,a
proof
  let T be non empty TopSpace, a be Point of T;
  thus a,a are_connected;
  thus thesis by BORSUK_1:def 17,def 18,TOPALG_3:5;
end;

theorem Th42:
  for p1, p2 being Point of TOP-REAL n, P being Subset of TOP-REAL n
  holds P is_an_arc_of p1,p2 implies
  ex F being Path of p1,p2, f being Function of I[01], (TOP-REAL n)|P st
  rng f = P & F = f
proof
  let p1, p2 be Point of TOP-REAL n, P be Subset of TOP-REAL n;
  assume
A1: P is_an_arc_of p1,p2;
  then reconsider P1 = P as non empty Subset of TOP-REAL n by TOPREAL1:4;
  consider h being Function of I[01], (TOP-REAL n)|P such that
A2: h is_homeomorphism and
A3: h.0 = p1 & h.1 = p2 by A1,TOPREAL1:def 2;
  h is Function of I[01], (TOP-REAL n)|P1;
  then reconsider h1 = h as Function of I[01],TOP-REAL n by TOPREALA:28;
  h1 is Path of p1,p2
  proof
    h is continuous by A2,TOPS_2:def 5;
    hence h1 is continuous & h1.0 = p1 & h1.1 = p2 by A3,TOPMETR:7;
  end;
  then reconsider f = h as Path of p1,p2;
  take f, h;
  thus rng h = [#]((TOP-REAL n)|P) by A2,TOPS_2:def 5
    .= P by PRE_TOPC:29;
  thus thesis;
end;

theorem Th43:
  for p1, p2 being Point of TOP-REAL n
  ex F being Path of p1,p2, f being Function
  of I[01], (TOP-REAL n)|LSeg(p1,p2) st rng f = LSeg(p1,p2) & F = f
proof
  let p1, p2 be Point of TOP-REAL n;
  per cases;
  suppose
A1: p1 = p2;
    then reconsider g = I[01] --> p1 as Path of p1,p2 by Th41;
    take g;
A2: LSeg(p1,p2) = {p1} by A1,TOPREAL1:7;
A3: rng g = {p1} by FUNCOP_1:14;
    the carrier of (TOP-REAL n)|LSeg(p1,p2) = LSeg(p1,p2) by PRE_TOPC:29;
    then reconsider f = g as Function of I[01],(TOP-REAL n)|LSeg(p1,p2)
    by A2,A3,FUNCT_2:8;
    take f;
    thus thesis by A1,A3,TOPREAL1:7;
  end;
  suppose p1 <> p2;
    then LSeg(p1,p2) is_an_arc_of p1,p2 by TOPREAL1:15;
    hence thesis by Th42;
  end;
end;

theorem Th44:
  for p1,p2,q1,q2 being Point of TOP-REAL 2
  st P is_an_arc_of p1,p2 & q1 in P & q2 in P &
  q1 <> p1 & q1 <> p2 & q2 <> p1 & q2 <> p2
  ex f being Path of q1,q2 st rng f c= P & rng f misses {p1,p2}
proof
  let p1,p2,q1,q2 be Point of TOP-REAL 2 such that
A1: P is_an_arc_of p1,p2 and
A2: q1 in P & q2 in P and
A3: q1 <> p1 & q1 <> p2 & q2 <> p1 & q2 <> p2;
  per cases;
  suppose q1 = q2;
    then reconsider f = I[01] --> q1 as Path of q1,q2 by Th41;
    take f;
A4: rng f = {q1} by FUNCOP_1:14;
    thus rng f c= P
    proof
      let x be set;
      assume x in rng f;
      hence thesis by A2,A4,TARSKI:def 1;
    end;
    not p1 in {q1} & not p2 in {q1} by A3,TARSKI:def 1;
    hence thesis by A4,ZFMISC_1:57;
  end;
  suppose q1 <> q2;
    then consider Q being non empty Subset of T2 such that
A5: Q is_an_arc_of q1,q2 and
A6: Q c= P & Q misses {p1,p2} by A1,A2,A3,JORDAN16:38;
    consider g being Path of q1,q2, f being Function of I[01], T2|Q such that
A7: rng f = Q & g = f by A5,Th42;
    reconsider h = f as Function of I[01],T2 by TOPREALA:28;
    the carrier of T2|Q = Q by PRE_TOPC:29;
    then reconsider z1 = q1, z2 = q2 as Point of T2|Q by A5,TOPREAL1:4;
A8: z1,z2 are_connected
    proof
      take f;
      thus f is continuous by A7,TOPMETR:8;
      thus f.0 = z1 & f.1 = z2 by A7,BORSUK_2:def 4;
    end;
    f is Path of z1,z2
    proof
      thus z1,z2 are_connected by A8;
      thus f is continuous by A7,TOPMETR:8;
      thus f.0 = z1 & f.1 = z2 by A7,BORSUK_2:def 4;
    end;
    then reconsider h as Path of q1,q2 by A8,TOPALG_2:1;
    take h;
    thus thesis by A6,A7;
  end;
end;

begin :: Rectangles

theorem Th45:
  a <= b & c <= d implies
  rectangle(a,b,c,d) c= closed_inside_of_rectangle(a,b,c,d)
proof
  assume
A1: a <= b & c <= d;
  let x be set;
  assume x in rectangle(a,b,c,d);
  then x in {p: p`1 = a & p`2 <= d & p`2 >= c or
  p`1 <= b & p`1 >= a & p`2 = d or p`1 <= b & p`1 >= a & p`2 = c or
  p`1 = b & p`2 <= d & p`2 >= c} by A1,SPPOL_2:58;
  then ex p st x = p & (p`1 = a & p`2 <= d & p`2 >= c or
  p`1 <= b & p`1 >= a & p`2 = d or p`1 <= b & p`1 >= a & p`2 = c or
  p`1 = b & p`2 <= d & p`2 >= c);
  hence thesis by A1;
end;

theorem Th46:
  inside_of_rectangle(a,b,c,d) c= closed_inside_of_rectangle(a,b,c,d)
proof
  let x be set;
  assume x in inside_of_rectangle(a,b,c,d);
  then ex p st x = p & (a < p`1 & p`1 < b & c < p`2 & p`2 < d);
  hence thesis;
end;

theorem Th47:
  closed_inside_of_rectangle(a,b,c,d) = outside_of_rectangle(a,b,c,d)`
proof
  set R = closed_inside_of_rectangle(a,b,c,d);
  set O = outside_of_rectangle(a,b,c,d);
  thus R c= O`
  proof
    let x be set;
    assume x in R;
    then consider p such that
A1: x = p and
A2: a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d;
    now
      assume p in O;
      then ex p1 st p1 = p &
      not(a <= p1`1 & p1`1 <= b & c <= p1`2 & p1`2 <= d);
      hence contradiction by A2;
    end;
    hence thesis by A1,SUBSET_1:50;
  end;
  let x be set;
  assume
A3: x in O`;
  then
A4: not x in O by XBOOLE_0:def 4;
  reconsider x as Point of T2 by A3;
  a <= x`1 & x`1 <= b & c <= x`2 & x`2 <= d by A4;
  hence thesis;
end;

registration
  let a, b, c, d be real number;
  cluster closed_inside_of_rectangle(a,b,c,d) -> closed;
  coherence
  proof
    set P2 = outside_of_rectangle(a,b,c,d);
    a is Real & b is Real & c is Real & d is Real by XREAL_0:def 1;
    then reconsider P2 as open Subset of T2 by JORDAN1:39;
    P2` is closed;
    hence thesis by Th47;
  end;
end;

theorem Th48:
  closed_inside_of_rectangle(a,b,c,d) misses outside_of_rectangle(a,b,c,d)
proof
  set R = closed_inside_of_rectangle(a,b,c,d);
  set P2 = outside_of_rectangle(a,b,c,d);
  assume R meets P2;
  then consider x being set such that
A1: x in R and
A2: x in P2 by XBOOLE_0:3;
A3: ex p st x = p & a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d by A1;
  ex p st x = p & not (a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d) by A2;
  hence thesis by A3;
end;

theorem Th49:
  closed_inside_of_rectangle(a,b,c,d) /\ inside_of_rectangle(a,b,c,d)
  = inside_of_rectangle(a,b,c,d)
proof
  set R = closed_inside_of_rectangle(a,b,c,d);
  set P1 = inside_of_rectangle(a,b,c,d);
  thus R /\ P1 c= P1 by XBOOLE_1:17;
  P1 /\ P1 c= P1 /\ R by Th46,XBOOLE_1:26;
  hence thesis;
end;

theorem Th50:
  a < b & c < d implies
  Int closed_inside_of_rectangle(a,b,c,d) = inside_of_rectangle(a,b,c,d)
proof
  assume
A1: a < b & c < d;
  set P = rectangle(a,b,c,d);
  set R = closed_inside_of_rectangle(a,b,c,d);
  set P1 = inside_of_rectangle(a,b,c,d);
  set P2 = outside_of_rectangle(a,b,c,d);
A2: a is Real & b is Real & c is Real & d is Real by XREAL_0:def 1;
A3: P = {p where p is Point of T2: p`1 = a & p`2 <= d & p`2 >= c or
  p`1 <= b & p`1 >= a & p`2 = d or p`1 <= b & p`1 >= a & p`2 = c or
  p`1 = b & p`2 <= d & p`2 >= c} by A1,SPPOL_2:58;
A4: R misses P2 by Th48;
  thus Int R = (Cl P2``)` by Th47
    .= (P2 \/ P)` by A1,A2,A3,JORDAN1:49
    .= P2` /\ P` by XBOOLE_1:53
    .= R /\ P` by Th47
    .= R /\ (P1 \/ P2) by A1,A2,A3,JORDAN1:41
    .= R /\ P1 \/ R /\ P2 by XBOOLE_1:23
    .= R /\ P1 \/ {} by A4,XBOOLE_0:def 7
    .= P1 by Th49;
end;

theorem Th51:
  a <= b & c <= d implies
  closed_inside_of_rectangle(a,b,c,d) \ inside_of_rectangle(a,b,c,d)
  = rectangle(a,b,c,d)
proof
  assume
A1: a <= b & c <= d;
  set R = rectangle(a,b,c,d);
  set P = closed_inside_of_rectangle(a,b,c,d);
  set P1 = inside_of_rectangle(a,b,c,d);
A2: R = {p where p is Point of T2: p`1 = a & p`2 <= d & p`2 >= c or
  p`1 <= b & p`1 >= a & p`2 = d or p`1 <= b & p`1 >= a & p`2 = c or
  p`1 = b & p`2 <= d & p`2 >= c} by A1,SPPOL_2:58;
  thus P \ P1 c= R
  proof
    let x be set;
    assume
A3: x in P \ P1;
    then
A4: not x in P1 by XBOOLE_0:def 4;
    x in P by A3,XBOOLE_0:def 4;
    then consider p such that
A5: x = p and
A6: a <= p`1 & p`1 <= b & c <= p`2 & p`2 <= d;
    not (a < p`1 & p`1 < b & c < p`2 & p`2 < d) by A4,A5;
    then p`1 = a & p`2 <= d & p`2 >= c or p`1 <= b & p`1 >= a & p`2 = d or
    p`1 <= b & p`1 >= a & p`2 = c or
    p`1 = b & p`2 <= d & p`2 >= c by A6,XXREAL_0:1;
    hence thesis by A2,A5;
  end;
  let x be set;
  assume
A7: x in R;
  then
A8: ex p st p = x & (p`1 = a & p`2 <= d & p`2 >= c or
  p`1 <= b & p`1 >= a & p`2 = d or p`1 <= b & p`1 >= a & p`2 = c or
  p`1 = b & p`2 <= d & p`2 >= c) by A2;
A9: R c= P by A1,Th45;
  now
    assume x in P1;
    then ex p st x = p & a < p`1 & p`1 < b & c < p`2 & p`2 < d;
    hence contradiction by A8;
  end;
  hence thesis by A7,A9,XBOOLE_0:def 4;
end;

theorem Th52:
  a < b & c < d implies
  Fr closed_inside_of_rectangle(a,b,c,d) = rectangle(a,b,c,d)
proof
  assume
A1: a < b & c < d;
  set P = closed_inside_of_rectangle(a,b,c,d);
  thus Fr P = P \ Int P by TOPS_1:77
    .= P \ inside_of_rectangle(a,b,c,d) by A1,Th50
    .= rectangle(a,b,c,d) by A1,Th51;
end;

theorem
  a <= b & c <= d implies W-bound closed_inside_of_rectangle(a,b,c,d) = a
proof
  assume
A1: a <= b & c <= d;
  set X = closed_inside_of_rectangle(a,b,c,d);
  reconsider Z = (proj1||X).:the carrier of (T2|X) as Subset of REAL;
A2: X = the carrier of (T2|X) by PRE_TOPC:29;
A3: |[a,c]| in X by A1,TOPREALA:52;
A4: for p be real number st p in Z holds p >= a
  proof
    let p be real number;
    assume p in Z;
    then consider p0 being set such that
A5: p0 in the carrier of T2|X and p0 in the carrier of T2|X and
A6: p = (proj1||X).p0 by FUNCT_2:115;
    ex p1 st p0 = p1 & a <= p1`1 & p1`1 <= b & c <= p1`2 & p1`2 <= d by A2,A5;
    hence thesis by A2,A5,A6,PSCOMP_1:69;
  end;
A7: for q being real number st
  for p being real number st p in Z holds p >= q holds a >= q
  proof
    let q be real number such that
A8: for p being real number st p in Z holds p >= q;
A9: |[a,c]|`1 = a & |[a,c]|`2 = c by EUCLID:56;
    (proj1||X). |[a,c]| = |[a,c]|`1 by A1,PSCOMP_1:69,TOPREALA:52;
    hence thesis by A2,A3,A8,A9,FUNCT_2:43;
  end;
  dom(proj1||X) = X by A2,FUNCT_2:def 1;
  then Z is non empty by A2,A3,RELAT_1:152;
  hence W-bound X = a by A4,A7,SEQ_4:61;
end;

theorem
  a <= b & c <= d implies S-bound closed_inside_of_rectangle(a,b,c,d) = c
proof
  assume
A1: a <= b & c <= d;
  set X = closed_inside_of_rectangle(a,b,c,d);
  reconsider Z = (proj2||X).:the carrier of (T2|X) as Subset of REAL;
A2: X = the carrier of (T2|X) by PRE_TOPC:29;
A3: |[a,c]| in X by A1,TOPREALA:52;
A4: for p be real number st p in Z holds p >= c
  proof
    let p be real number;
    assume p in Z;
    then consider p0 being set such that
A5: p0 in the carrier of T2|X and p0 in the carrier of T2|X and
A6: p = (proj2||X).p0 by FUNCT_2:115;
    ex p1 st p0 = p1 & a <= p1`1 & p1`1 <= b & c <= p1`2 & p1`2 <= d by A2,A5;
    hence thesis by A2,A5,A6,PSCOMP_1:70;
  end;
A7: for q being real number st
  for p being real number st p in Z holds p >= q holds c >= q
  proof
    let q be real number such that
A8: for p being real number st p in Z holds p >= q;
A9: |[a,c]|`1 = a & |[a,c]|`2 = c by EUCLID:56;
    (proj2||X). |[a,c]| = |[a,c]|`2 by A1,PSCOMP_1:70,TOPREALA:52;
    hence thesis by A2,A3,A8,A9,FUNCT_2:43;
  end;
  dom(proj2||X) = X by A2,FUNCT_2:def 1;
  then Z is non empty by A2,A3,RELAT_1:152;
  hence S-bound X = c by A4,A7,SEQ_4:61;
end;

theorem
  a <= b & c <= d implies E-bound closed_inside_of_rectangle(a,b,c,d) = b
proof
  assume
A1: a <= b & c <= d;
  set X = closed_inside_of_rectangle(a,b,c,d);
  reconsider Z = (proj1||X).:the carrier of (T2|X) as Subset of REAL;
A2: X = the carrier of (T2|X) by PRE_TOPC:29;
A3: for p be real number st p in Z holds p <= b
  proof
    let p be real number;
    assume p in Z;
    then consider p0 being set such that
A4: p0 in the carrier of T2|X and p0 in the carrier of T2|X and
A5: p = (proj1||X).p0 by FUNCT_2:115;
    ex p1 st p0 = p1 & a <= p1`1 & p1`1 <= b & c <= p1`2 & p1`2 <= d by A2,A4;
    hence thesis by A2,A4,A5,PSCOMP_1:69;
  end;
A6: for q being real number st
  for p being real number st p in Z holds p <= q holds b <= q
  proof
    let q be real number such that
A7: for p being real number st p in Z holds p <= q;
A8: |[b,d]|`1 = b & |[b,d]|`2 = d by EUCLID:56;
    then
A9: |[b,d]| in X by A1;
    then (proj1||X). |[b,d]| = |[b,d]|`1 by PSCOMP_1:69;
    hence thesis by A2,A7,A8,A9,FUNCT_2:43;
  end;
A10: |[a,c]| in X by A1,TOPREALA:52;
  dom(proj1||X) = X by A2,FUNCT_2:def 1;
  then Z is non empty by A2,A10,RELAT_1:152;
  hence E-bound X = b by A3,A6,SEQ_4:63;
end;

theorem
  a <= b & c <= d implies N-bound closed_inside_of_rectangle(a,b,c,d) = d
proof
  assume
A1: a <= b & c <= d;
  set X = closed_inside_of_rectangle(a,b,c,d);
  reconsider Z = (proj2||X).:the carrier of (T2|X) as Subset of REAL;
A2: X = the carrier of (T2|X) by PRE_TOPC:29;
A3: for p be real number st p in Z holds p <= d
  proof
    let p be real number;
    assume p in Z;
    then consider p0 being set such that
A4: p0 in the carrier of T2|X and p0 in the carrier of T2|X and
A5: p = (proj2||X).p0 by FUNCT_2:115;
    ex p1 st p0 = p1 & a <= p1`1 & p1`1 <= b & c <= p1`2 & p1`2 <= d by A2,A4;
    hence thesis by A2,A4,A5,PSCOMP_1:70;
  end;
A6: for q being real number st
  for p being real number st p in Z holds p <= q holds d <= q
  proof
    let q be real number such that
A7: for p being real number st p in Z holds p <= q;
A8: |[b,d]|`1 = b & |[b,d]|`2 = d by EUCLID:56;
    then
A9: |[b,d]| in X by A1;
    then (proj2||X). |[b,d]| = |[b,d]|`2 by PSCOMP_1:70;
    hence thesis by A2,A7,A8,A9,FUNCT_2:43;
  end;
A10: |[a,c]| in X by A1,TOPREALA:52;
  dom(proj2||X) = X by A2,FUNCT_2:def 1;
  then Z is non empty by A2,A10,RELAT_1:152;
  hence N-bound X = d by A3,A6,SEQ_4:63;
end;

theorem Th57:
  a < b & c < d & p1 in closed_inside_of_rectangle(a,b,c,d) &
  not p2 in closed_inside_of_rectangle(a,b,c,d) & P is_an_arc_of p1,p2 implies
  Segment(P,p1,p2,p1,First_Point(P,p1,p2,rectangle(a,b,c,d))) c=
  closed_inside_of_rectangle(a,b,c,d)
proof
  set R = closed_inside_of_rectangle(a,b,c,d);
  set dR = rectangle(a,b,c,d);
  set n = First_Point(P,p1,p2,dR);
  assume that
A1: a < b & c < d and
A2: p1 in R and
A3: not p2 in R and
A4: P is_an_arc_of p1,p2;
  let x be set;
  assume that
A5: x in Segment(P,p1,p2,p1,n) and
A6: not x in R;
  reconsider x as Point of T2 by A5;
A7: Fr R = dR by A1,Th52;
  p1 in P by A4,TOPREAL1:4;
  then
A8: P meets R by A2,XBOOLE_0:3;
  p2 in P by A4,TOPREAL1:4;
  then P \ R <> {}T2 by A3,XBOOLE_0:def 4;
  then
A9: P meets dR by A4,A7,A8,CONNSP_1:23,JORDAN6:11;
  P is closed by A4,JORDAN6:12;
  then
A10: P /\ dR is closed by TOPS_1:35;
  then
A11: n in P /\ dR by A4,A9,JORDAN5C:def 1;
  per cases;
  suppose x = n;
    then
A12: x in dR by A11,XBOOLE_0:def 3;
    dR c= R by A1,Th45;
    hence thesis by A6,A12;
  end;
  suppose
A13: x <> n;
    reconsider P as non empty Subset of T2 by A4,TOPREAL1:4;
    consider f being Function of I[01], T2|P such that
A14: f is_homeomorphism and
A15: f.0 = p1 & f.1 = p2 by A4,TOPREAL1:def 2;
A16: rng f = [#](T2|P) by A14,TOPS_2:def 5
      .= P by PRE_TOPC:def 10;
    n in P by A11,XBOOLE_0:def 3;
    then consider na being set such that
A17: na in dom f and
A18: f.na = n by A16,FUNCT_1:def 5;
    na is real by A17,MEMBERED:def 3;
    then reconsider na as Real by XREAL_0:def 1;
A19: 0 <= na & na <= 1 by A17,BORSUK_1:86;
A20: Segment(P,p1,p2,p1,n) c= P by JORDAN16:5;
    then consider xa being set such that
A21: xa in dom f and
A22: f.xa = x by A5,A16,FUNCT_1:def 5;
    xa is real by A21,MEMBERED:def 3;
    then reconsider xa as Real by XREAL_0:def 1;
A23: 0 <= xa & xa <= 1 by A21,BORSUK_1:86;
A24: Segment(P,p1,p2,p1,x) is_an_arc_of p1,x by A2,A4,A5,A6,A20,JORDAN16:39;
    then p1 in Segment(P,p1,p2,p1,x) by TOPREAL1:4;
    then
A25: Segment(P,p1,p2,p1,x) meets R by A2,XBOOLE_0:3;
    x in Segment(P,p1,p2,p1,x) by A24,TOPREAL1:4;
    then Segment(P,p1,p2,p1,x) \ R <> {}T2 by A6,XBOOLE_0:def 4;
    then Segment(P,p1,p2,p1,x) meets Fr R by A24,A25,CONNSP_1:23,JORDAN6:11;
    then consider z being set such that
A26: z in Segment(P,p1,p2,p1,x) and
A27: z in dR by A7,XBOOLE_0:3;
    reconsider z as Point of T2 by A26;
    Segment(P,p1,p2,p1,x) = {p: LE p1,p,P,p1,p2 & LE p,x,P,p1,p2}
    by JORDAN6:29;
    then consider zz being Point of T2 such that
A28: zz = z and LE p1,zz,P,p1,p2 and
A29: LE zz,x,P,p1,p2 by A26;
    Segment(P,p1,p2,p1,x) c= P by JORDAN16:5;
    then consider za being set such that
A30: za in dom f and
A31: f.za = z by A16,A26,FUNCT_1:def 5;
    za is real by A30,MEMBERED:def 3;
    then reconsider za as Real by XREAL_0:def 1;
A32: 0 <= za & za <= 1 by A30,BORSUK_1:86;
    then
A33: na <= za by A4,A9,A10,A14,A15,A18,A19,A27,A31,JORDAN5C:def 1;
A34: za <= xa by A14,A15,A22,A23,A28,A29,A31,A32,JORDAN5C:def 3;
    Segment(P,p1,p2,p1,n) = {p: LE p1,p,P,p1,p2 & LE p,n,P,p1,p2}
    by JORDAN6:29;
    then consider xx being Point of T2 such that
A35: xx = x and LE p1,xx,P,p1,p2 and
A36: LE xx,n,P,p1,p2 by A5;
    xa <= na by A14,A15,A18,A19,A22,A23,A35,A36,JORDAN5C:def 3;
    then xa < na by A13,A18,A22,REAL_1:def 5;
    hence thesis by A33,A34,XXREAL_0:2;
  end;
end;

begin :: Some useful functions

definition
  let S, T be non empty TopSpace, x be Point of [:S,T:];
  redefine func x`1 -> Element of S;
  coherence
  proof
    the carrier of [:S,T:] = [:the carrier of S, the carrier of T:]
    by BORSUK_1:def 5;
    hence thesis by MCART_1:10;
  end;
  redefine func x`2 -> Element of T;
  coherence
  proof
    the carrier of [:S,T:] = [:the carrier of S, the carrier of T:]
    by BORSUK_1:def 5;
    hence thesis by MCART_1:10;
  end;
end;

definition
  let o be Point of TOP-REAL 2;
  func diffX2_1(o) -> RealMap of [:TOP-REAL 2,TOP-REAL 2:] means
  :Def1:
  for x being Point of [:TOP-REAL 2,TOP-REAL 2:] holds it.x = x`2`1 - o`1;
  existence
  proof
    deffunc F(Point of [:T2,T2:]) = $1`2`1 - o`1;
    consider xo being RealMap of [:T2,T2:] such that
A1: for x being Point of [:T2,T2:] holds xo.x = F(x) from FUNCT_2:sch 4;
    take xo;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let f, g be RealMap of [:T2,T2:] such that
A2: for x being Point of [:T2,T2:] holds f.x = x`2`1 - o`1 and
A3: for x being Point of [:T2,T2:] holds g.x = x`2`1 - o`1;
    now
      let x be Point of [:T2,T2:];
      thus f.x = x`2`1 - o`1 by A2
        .= g.x by A3;
    end;
    hence thesis by FUNCT_2:113;
  end;
  func diffX2_2(o) -> RealMap of [:TOP-REAL 2,TOP-REAL 2:] means
  :Def2:
  for x being Point of [:TOP-REAL 2,TOP-REAL 2:] holds it.x = x`2`2 - o`2;
  existence
  proof
    deffunc F(Point of [:T2,T2:]) = $1`2`2 - o`2;
    consider xo being RealMap of [:T2,T2:] such that
A4: for x being Point of [:T2,T2:] holds xo.x = F(x) from FUNCT_2:sch 4;
    take xo;
    thus thesis by A4;
  end;
  uniqueness
  proof
    let f, g be RealMap of [:T2,T2:] such that
A5: for x being Point of [:T2,T2:] holds f.x = x`2`2 - o`2 and
A6: for x being Point of [:T2,T2:] holds g.x = x`2`2 - o`2;
    now
      let x be Point of [:T2,T2:];
      thus f.x = x`2`2 - o`2 by A5
        .= g.x by A6;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

definition
  func diffX1_X2_1 -> RealMap of [:TOP-REAL 2, TOP-REAL 2:] means
  :Def3:
  for x being Point of [:TOP-REAL 2, TOP-REAL 2:] holds it.x = x`1`1 - x`2`1;
  existence
  proof
    deffunc F(Point of [:T2,T2:]) = $1`1`1 - $1`2`1;
    consider xo being RealMap of [:T2,T2:] such that
A1: for x being Point of [:T2,T2:] holds xo.x = F(x) from FUNCT_2:sch 4;
    take xo;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let f, g be RealMap of [:T2,T2:] such that
A2: for x being Point of [:T2,T2:] holds f.x = x`1`1 - x`2`1 and
A3: for x being Point of [:T2,T2:] holds g.x = x`1`1 - x`2`1;
    now
      let x be Point of [:T2,T2:];
      thus f.x = x`1`1 - x`2`1 by A2
        .= g.x by A3;
    end;
    hence thesis by FUNCT_2:113;
  end;
  func diffX1_X2_2 -> RealMap of [:TOP-REAL 2, TOP-REAL 2:] means
  :Def4:
  for x being Point of [:TOP-REAL 2, TOP-REAL 2:] holds it.x = x`1`2 - x`2`2;
  existence
  proof
    deffunc F(Point of [:T2,T2:]) = $1`1`2 - $1`2`2;
    consider xo being RealMap of [:T2,T2:] such that
A4: for x being Point of [:T2,T2:] holds xo.x = F(x) from FUNCT_2:sch 4;
    take xo;
    thus thesis by A4;
  end;
  uniqueness
  proof
    let f, g be RealMap of [:T2,T2:] such that
A5: for x being Point of [:T2,T2:] holds f.x = x`1`2 - x`2`2 and
A6: for x being Point of [:T2,T2:] holds g.x = x`1`2 - x`2`2;
    now
      let x be Point of [:T2,T2:];
      thus f.x = x`1`2 - x`2`2 by A5
        .= g.x by A6;
    end;
    hence thesis by FUNCT_2:113;
  end;
  func Proj2_1 -> RealMap of [:TOP-REAL 2, TOP-REAL 2:] means
  :Def5:
  for x being Point of [:TOP-REAL 2, TOP-REAL 2:] holds it.x = x`2`1;
  existence
  proof
    deffunc F(Point of [:T2,T2:]) = $1`2`1;
    consider xo being RealMap of [:T2,T2:] such that
A7: for x being Point of [:T2,T2:] holds xo.x = F(x) from FUNCT_2:sch 4;
    take xo;
    thus thesis by A7;
  end;
  uniqueness
  proof
    let f, g be RealMap of [:T2,T2:] such that
A8: for x being Point of [:T2,T2:] holds f.x = x`2`1 and
A9: for x being Point of [:T2,T2:] holds g.x = x`2`1;
    now
      let x be Point of [:T2,T2:];
      thus f.x = x`2`1 by A8
        .= g.x by A9;
    end;
    hence thesis by FUNCT_2:113;
  end;
  func Proj2_2 -> RealMap of [:TOP-REAL 2, TOP-REAL 2:] means
  :Def6:
  for x being Point of [:TOP-REAL 2, TOP-REAL 2:] holds it.x = x`2`2;
  existence
  proof
    deffunc F(Point of [:T2,T2:]) = $1`2`2;
    consider xo being RealMap of [:T2,T2:] such that
A10: for x being Point of [:T2,T2:] holds xo.x = F(x) from FUNCT_2:sch 4;
    take xo;
    thus thesis by A10;
  end;
  uniqueness
  proof
    let f, g be RealMap of [:T2,T2:] such that
A11: for x being Point of [:T2,T2:] holds f.x = x`2`2 and
A12: for x being Point of [:T2,T2:] holds g.x = x`2`2;
    now
      let x be Point of [:T2,T2:];
      thus f.x = x`2`2 by A11
        .= g.x by A12;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

theorem Th58:
  for o being Point of TOP-REAL 2 holds
  diffX2_1(o) is continuous Function of [:TOP-REAL 2, TOP-REAL 2:], R^1
proof
  let o be Point of TOP-REAL 2;
  reconsider Xo = diffX2_1(o) as Function of [:T2,T2:],R^1 by TOPMETR:24;
  for p being Point of [:T2,T2:], V being Subset of R^1
  st Xo.p in V & V is open holds
  ex W being Subset of [:T2,T2:] st p in W & W is open & Xo.:W c= V
  proof
    let p be Point of [:T2,T2:], V be Subset of R^1 such that
A1: Xo.p in V and
A2: V is open;
A3: Xo.p = p`2`1 - o`1 by Def1;
    set r = p`2`1 - o`1;
    reconsider V1 = V as open Subset of REAL by A2,BORSUK_5:62,TOPMETR:24;
    consider g being real number such that
A4: 0 < g and
A5: ].r-g,r+g.[ c= V1 by A1,A3,RCOMP_1:40;
    reconsider g as Element of REAL by XREAL_0:def 1;
    set W2 = {|[x,y]| where x, y is Element of REAL:
    p`2`1-g < x & x < p`2`1+g};
    W2 c= the carrier of T2
    proof
      let a be set;
      assume a in W2;
      then ex x, y being Element of REAL st
      a = |[x,y]| & p`2`1-g < x & x < p`2`1+g;
      hence thesis;
    end;
    then reconsider W2 as Subset of T2;
    take [:[#]T2,W2:];
A6: p`2 = |[p`2`1,p`2`2]| by EUCLID:57;
A7: p = [p`1,p`2] by Lm7,MCART_1:23;
    p`2`1-g < p`2`1-0 & p`2`1+0 < p`2`1+g by A4,REAL_1:92,XREAL_1:8;
    then p`2 in W2 by A6;
    hence p in [:[#]T2,W2:] by A7,ZFMISC_1:def 2;
    W2 is open by PSCOMP_1:66;
    hence [:[#]T2,W2:] is open by BORSUK_1:46;
    let b be set;
    assume b in Xo.:[:[#]T2,W2:];
    then consider a being Point of [:T2,T2:] such that
A8: a in [:[#]T2,W2:] and
A9: Xo.a = b by FUNCT_2:116;
A10: a = [a`1,a`2] by Lm7,MCART_1:23;
A11: (diffX2_1(o)).a = a`2`1 - o`1 by Def1;
    a`2 in W2 by A8,A10,ZFMISC_1:106;
    then consider x2, y2 being Element of REAL such that
A12: a`2 = |[x2,y2]| and
A13: p`2`1-g < x2 & x2 < p`2`1+g;
    a`2`1 = x2 by A12,EUCLID:56;
    then p`2`1 - g - o`1 < a`2`1 - o`1 & a`2`1 - o`1 < p`2`1 + g - o`1
    by A13,XREAL_1:11;
    then a`2`1 - o`1 in ].r-g,r+g.[ by RCOMP_1:47;
    hence b in V by A5,A9,A11;
  end;
  hence thesis by JGRAPH_2:20;
end;

theorem Th59:
  for o being Point of TOP-REAL 2 holds
  diffX2_2(o) is continuous Function of [:TOP-REAL 2, TOP-REAL 2:], R^1
proof
  let o be Point of TOP-REAL 2;
  reconsider Yo = diffX2_2(o) as Function of [:T2,T2:],R^1 by TOPMETR:24;
  for p being Point of [:T2,T2:], V being Subset of R^1
  st Yo.p in V & V is open holds
  ex W being Subset of [:T2,T2:] st p in W & W is open & Yo.:W c= V
  proof
    let p be Point of [:T2,T2:], V be Subset of R^1 such that
A1: Yo.p in V and
A2: V is open;
A3: p = [p`1,p`2] by Lm7,MCART_1:23;
A4: Yo.p = p`2`2 - o`2 by Def2;
    set r = p`2`2 - o`2;
    reconsider V1 = V as open Subset of REAL by A2,BORSUK_5:62,TOPMETR:24;
    consider g being real number such that
A5: 0 < g and
A6: ].r-g,r+g.[ c= V1 by A1,A4,RCOMP_1:40;
    reconsider g as Element of REAL by XREAL_0:def 1;
    set W2 = {|[x,y]| where x, y is Element of REAL:
    p`2`2-g < y & y < p`2`2+g};
    W2 c= the carrier of T2
    proof
      let a be set;
      assume a in W2;
      then ex x, y being Element of REAL st
      a = |[x,y]| & p`2`2-g < y & y < p`2`2+g;
      hence thesis;
    end;
    then reconsider W2 as Subset of T2;
    take [:[#]T2,W2:];
A7: p`2 = |[p`2`1,p`2`2]| by EUCLID:57;
    p`2`2-g < p`2`2-0 & p`2`2+0 < p`2`2+g by A5,REAL_1:92,XREAL_1:8;
    then p`2 in W2 by A7;
    hence p in [:[#]T2,W2:] by A3,ZFMISC_1:def 2;
    W2 is open by PSCOMP_1:68;
    hence [:[#]T2,W2:] is open by BORSUK_1:46;
    let b be set;
    assume b in Yo.:[:[#]T2,W2:];
    then consider a being Point of [:T2,T2:] such that
A8: a in [:[#]T2,W2:] and
A9: Yo.a = b by FUNCT_2:116;
A10: a = [a`1,a`2] by Lm7,MCART_1:23;
A11: (diffX2_2(o)).a = a`2`2 - o`2 by Def2;
    a`2 in W2 by A8,A10,ZFMISC_1:106;
    then consider x2, y2 being Element of REAL such that
A12: a`2 = |[x2,y2]| and
A13: p`2`2-g < y2 & y2 < p`2`2+g;
    a`2`2 = y2 by A12,EUCLID:56;
    then p`2`2 - g - o`2 < a`2`2 - o`2 & a`2`2 - o`2 < p`2`2 + g - o`2
    by A13,XREAL_1:11;
    then a`2`2 - o`2 in ].r-g,r+g.[ by RCOMP_1:47;
    hence b in V by A6,A9,A11;
  end;
  hence thesis by JGRAPH_2:20;
end;

theorem Th60:
  diffX1_X2_1 is continuous Function of [:TOP-REAL 2, TOP-REAL 2:], R^1
proof
  reconsider Dx = diffX1_X2_1 as Function of [:T2,T2:],R^1 by TOPMETR:24;
  for p being Point of [:T2,T2:], V being Subset of R^1
  st Dx.p in V & V is open holds
  ex W being Subset of [:T2,T2:] st p in W & W is open & Dx.:W c= V
  proof
    let p be Point of [:T2,T2:], V be Subset of R^1 such that
A1: Dx.p in V and
A2: V is open;
A3: p = [p`1,p`2] by Lm7,MCART_1:23;
A4: diffX1_X2_1.p = p`1`1 - p`2`1 by Def3;
    set r = p`1`1 - p`2`1;
    reconsider V1 = V as open Subset of REAL by A2,BORSUK_5:62,TOPMETR:24;
    consider g being real number such that
A5: 0 < g and
A6: ].r-g,r+g.[ c= V1 by A1,A4,RCOMP_1:40;
    reconsider g as Element of REAL by XREAL_0:def 1;
    set W1 = {|[x,y]| where x, y is Element of REAL:
    p`1`1-g/2 < x & x < p`1`1+g/2};
    set W2 = {|[x,y]| where x, y is Element of REAL:
    p`2`1-g/2 < x & x < p`2`1+g/2};
    W1 c= the carrier of T2
    proof
      let a be set;
      assume a in W1;
      then ex x, y being Element of REAL st
      a = |[x,y]| & p`1`1-g/2 < x & x < p`1`1+g/2;
      hence thesis;
    end;
    then reconsider W1 as Subset of T2;
    W2 c= the carrier of T2
    proof
      let a be set;
      assume a in W2;
      then ex x, y being Element of REAL st
      a = |[x,y]| & p`2`1-g/2 < x & x < p`2`1+g/2;
      hence thesis;
    end;
    then reconsider W2 as Subset of T2;
    take [:W1,W2:];
A7: p`1 = |[p`1`1,p`1`2]| by EUCLID:57;
A8: 0/2 < g/2 by A5,REAL_1:73;
    then p`1`1-g/2 < p`1`1-0 & p`1`1+0 < p`1`1+g/2 by REAL_1:92,XREAL_1:8;
    then
A9: p`1 in W1 by A7;
A10: p`2 = |[p`2`1,p`2`2]| by EUCLID:57;
    p`2`1-g/2 < p`2`1-0 & p`2`1+0 < p`2`1+g/2 by A8,REAL_1:92,XREAL_1:8;
    then p`2 in W2 by A10;
    hence p in [:W1,W2:] by A3,A9,ZFMISC_1:def 2;
    W1 is open & W2 is open by PSCOMP_1:66;
    hence [:W1,W2:] is open by BORSUK_1:46;
    let b be set;
    assume b in Dx.:[:W1,W2:];
    then consider a being Point of [:T2,T2:] such that
A11: a in [:W1,W2:] and
A12: Dx.a = b by FUNCT_2:116;
A13: a = [a`1,a`2] by Lm7,MCART_1:23;
A14: diffX1_X2_1.a = a`1`1 - a`2`1 by Def3;
    a`1 in W1 by A11,A13,ZFMISC_1:106;
    then consider x1, y1 being Element of REAL such that
A15: a`1 = |[x1,y1]| and
A16: p`1`1-g/2 < x1 and
A17: x1 < p`1`1+g/2;
A18: a`1`1 = x1 by A15,EUCLID:56;
    p`1`1-g/2+g/2 < x1+g/2 & p`1`1-x1 > p`1`1-(p`1`1+g/2)
    by A16,A17,REAL_1:92,XREAL_1:8;
    then p`1`1-x1 < x1+g/2-x1 & p`1`1-x1 > -g/2 by XREAL_1:11;
    then
A19: abs(p`1`1-x1) < g/2 by SEQ_2:9;
    a`2 in W2 by A11,A13,ZFMISC_1:106;
    then consider x2, y2 being Element of REAL such that
A20: a`2 = |[x2,y2]| and
A21: p`2`1-g/2 < x2 and
A22: x2 < p`2`1+g/2;
A23: a`2`1 = x2 by A20,EUCLID:56;
    p`2`1-g/2+g/2 < x2+g/2 & p`2`1-x2 > p`2`1-(p`2`1+g/2)
    by A21,A22,REAL_1:92,XREAL_1:8;
    then p`2`1-x2 < x2+g/2-x2 & p`2`1-x2 > -g/2 by XREAL_1:11;
    then abs(p`2`1-x2) < g/2 by SEQ_2:9;
    then
A24: abs(p`1`1-x1)+abs(p`2`1-x2) < g/2+g/2 by A19,XREAL_1:10;
    abs(p`1`1-x1-(p`2`1-x2)) <= abs(p`1`1-x1)+abs(p`2`1-x2) by COMPLEX1:143;
    then abs-(p`1`1-x1-(p`2`1-x2)) <= abs(p`1`1-x1)+abs(p`2`1-x2)
    by COMPLEX1:138;
    then abs(x1-x2-r) < g by A24,XXREAL_0:2;
    then a`1`1 - a`2`1 in ].r-g,r+g.[ by A18,A23,RCOMP_1:8;
    hence b in V by A6,A12,A14;
  end;
  hence thesis by JGRAPH_2:20;
end;

theorem Th61:
  diffX1_X2_2 is continuous Function of [:TOP-REAL 2, TOP-REAL 2:], R^1
proof
  reconsider Dy = diffX1_X2_2 as Function of [:T2,T2:],R^1 by TOPMETR:24;
  for p being Point of [:T2,T2:], V being Subset of R^1
  st Dy.p in V & V is open holds
  ex W being Subset of [:T2,T2:] st p in W & W is open & Dy.:W c= V
  proof
    let p be Point of [:T2,T2:], V be Subset of R^1 such that
A1: Dy.p in V and
A2: V is open;
A3: p = [p`1,p`2] by Lm7,MCART_1:23;
A4: diffX1_X2_2.p = p`1`2 - p`2`2 by Def4;
    set r = p`1`2 - p`2`2;
    reconsider V1 = V as open Subset of REAL by A2,BORSUK_5:62,TOPMETR:24;
    consider g being real number such that
A5: 0 < g and
A6: ].r-g,r+g.[ c= V1 by A1,A4,RCOMP_1:40;
    reconsider g as Element of REAL by XREAL_0:def 1;
    set W1 = {|[x,y]| where x, y is Element of REAL:
    p`1`2-g/2 < y & y < p`1`2+g/2};
    set W2 = {|[x,y]| where x, y is Element of REAL:
    p`2`2-g/2 < y & y < p`2`2+g/2};
    W1 c= the carrier of T2
    proof
      let a be set;
      assume a in W1;
      then ex x, y being Element of REAL st
      a = |[x,y]| & p`1`2-g/2 < y & y < p`1`2+g/2;
      hence thesis;
    end;
    then reconsider W1 as Subset of T2;
    W2 c= the carrier of T2
    proof
      let a be set;
      assume a in W2;
      then ex x, y being Element of REAL st
      a = |[x,y]| & p`2`2-g/2 < y & y < p`2`2+g/2;
      hence thesis;
    end;
    then reconsider W2 as Subset of T2;
    take [:W1,W2:];
A7: p`1 = |[p`1`1,p`1`2]| by EUCLID:57;
A8: 0/2 < g/2 by A5,REAL_1:73;
    then p`1`2-g/2 < p`1`2-0 & p`1`2+0 < p`1`2+g/2 by REAL_1:92,XREAL_1:8;
    then
A9: p`1 in W1 by A7;
A10: p`2 = |[p`2`1,p`2`2]| by EUCLID:57;
    p`2`2-g/2 < p`2`2-0 & p`2`2+0 < p`2`2+g/2 by A8,REAL_1:92,XREAL_1:8;
    then p`2 in W2 by A10;
    hence p in [:W1,W2:] by A3,A9,ZFMISC_1:def 2;
    W1 is open & W2 is open by PSCOMP_1:68;
    hence [:W1,W2:] is open by BORSUK_1:46;
    let b be set;
    assume b in Dy.:[:W1,W2:];
    then consider a being Point of [:T2,T2:] such that
A11: a in [:W1,W2:] and
A12: Dy.a = b by FUNCT_2:116;
A13: a = [a`1,a`2] by Lm7,MCART_1:23;
A14: diffX1_X2_2.a = a`1`2 - a`2`2 by Def4;
    a`1 in W1 by A11,A13,ZFMISC_1:106;
    then consider x1, y1 being Element of REAL such that
A15: a`1 = |[x1,y1]| and
A16: p`1`2-g/2 < y1 and
A17: y1 < p`1`2+g/2;
A18: a`1`2 = y1 by A15,EUCLID:56;
    p`1`2-g/2+g/2 < y1+g/2 & p`1`2-y1 > p`1`2-(p`1`2+g/2)
    by A16,A17,REAL_1:92,XREAL_1:8;
    then p`1`2-y1 < y1+g/2-y1 & p`1`2-y1 > -g/2 by XREAL_1:11;
    then
A19: abs(p`1`2-y1) < g/2 by SEQ_2:9;
    a`2 in W2 by A11,A13,ZFMISC_1:106;
    then consider x2, y2 being Element of REAL such that
A20: a`2 = |[x2,y2]| and
A21: p`2`2-g/2 < y2 and
A22: y2 < p`2`2+g/2;
A23: a`2`2 = y2 by A20,EUCLID:56;
    p`2`2-g/2+g/2 < y2+g/2 & p`2`2-y2 > p`2`2-(p`2`2+g/2)
    by A21,A22,REAL_1:92,XREAL_1:8;
    then p`2`2-y2 < y2+g/2-y2 & p`2`2-y2 > -g/2 by XREAL_1:11;
    then abs(p`2`2-y2) < g/2 by SEQ_2:9;
    then
A24: abs(p`1`2-y1)+abs(p`2`2-y2) < g/2+g/2 by A19,XREAL_1:10;
    abs(p`1`2-y1-(p`2`2-y2)) <= abs(p`1`2-y1)+abs(p`2`2-y2) by COMPLEX1:143;
    then abs-(p`1`2-y1-(p`2`2-y2)) <= abs(p`1`2-y1)+abs(p`2`2-y2)
    by COMPLEX1:138;
    then abs(y1-y2-r) < g by A24,XXREAL_0:2;
    then a`1`2 - a`2`2 in ].r-g,r+g.[ by A18,A23,RCOMP_1:8;
    hence b in V by A6,A12,A14;
  end;
  hence thesis by JGRAPH_2:20;
end;

theorem Th62:
  Proj2_1 is continuous Function of [:TOP-REAL 2, TOP-REAL 2:], R^1
proof
  reconsider fX2 = Proj2_1 as Function of [:T2,T2:],R^1 by TOPMETR:24;
  for p being Point of [:T2,T2:], V being Subset of R^1
  st fX2.p in V & V is open holds
  ex W being Subset of [:T2,T2:] st p in W & W is open & fX2.:W c= V
  proof
    let p be Point of [:T2,T2:], V be Subset of R^1 such that
A1: fX2.p in V and
A2: V is open;
A3: p = [p`1,p`2] by Lm7,MCART_1:23;
A4: fX2.p = p`2`1 by Def5;
    reconsider V1 = V as open Subset of REAL by A2,BORSUK_5:62,TOPMETR:24;
    consider g being real number such that
A5: 0 < g and
A6: ].p`2`1-g,p`2`1+g.[ c= V1 by A1,A4,RCOMP_1:40;
    reconsider g as Element of REAL by XREAL_0:def 1;
    set W1 = {|[x,y]| where x, y is Element of REAL:
    p`2`1-g < x & x < p`2`1+g};
    W1 c= the carrier of T2
    proof
      let a be set;
      assume a in W1;
      then ex x, y being Element of REAL st
      a = |[x,y]| & p`2`1-g < x & x < p`2`1+g;
      hence thesis;
    end;
    then reconsider W1 as Subset of T2;
    take [:[#]T2,W1:];
A7: p`2 = |[p`2`1,p`2`2]| by EUCLID:57;
    p`2`1-g < p`2`1-0 & p`2`1+0 < p`2`1+g by A5,REAL_1:92,XREAL_1:8;
    then p`2 in W1 by A7;
    hence p in [:[#]T2,W1:] by A3,ZFMISC_1:def 2;
    W1 is open by PSCOMP_1:66;
    hence [:[#]T2,W1:] is open by BORSUK_1:46;
    let b be set;
    assume b in fX2.:[:[#]T2,W1:];
    then consider a being Point of [:T2,T2:] such that
A8: a in [:[#]T2,W1:] and
A9: fX2.a = b by FUNCT_2:116;
A10: a = [a`1,a`2] by Lm7,MCART_1:23;
A11: fX2.a = a`2`1 by Def5;
    a`2 in W1 by A8,A10,ZFMISC_1:106;
    then consider x1, y1 being Element of REAL such that
A12: a`2 = |[x1,y1]| and
A13: p`2`1-g < x1 and
A14: x1 < p`2`1+g;
A15: a`2`1 = x1 by A12,EUCLID:56;
    p`2`1-g+g < x1+g & p`2`1-x1 > p`2`1-(p`2`1+g)
    by A13,A14,REAL_1:92,XREAL_1:8;
    then p`2`1-x1 < x1+g-x1 & p`2`1-x1 > -g by XREAL_1:11;
    then abs(p`2`1-x1) < g by SEQ_2:9;
    then abs-(p`2`1-x1) < g by COMPLEX1:138;
    then abs(x1-p`2`1) < g;
    then a`2`1 in ].p`2`1-g,p`2`1+g.[ by A15,RCOMP_1:8;
    hence b in V by A6,A9,A11;
  end;
  hence thesis by JGRAPH_2:20;
end;

theorem Th63:
  Proj2_2 is continuous Function of [:TOP-REAL 2, TOP-REAL 2:], R^1
proof
  reconsider fY2 = Proj2_2 as Function of [:T2,T2:],R^1 by TOPMETR:24;
  for p being Point of [:T2,T2:], V being Subset of R^1
  st fY2.p in V & V is open holds
  ex W being Subset of [:T2,T2:] st p in W & W is open & fY2.:W c= V
  proof
    let p be Point of [:T2,T2:], V be Subset of R^1 such that
A1: fY2.p in V and
A2: V is open;
A3: p = [p`1,p`2] by Lm7,MCART_1:23;
A4: fY2.p = p`2`2 by Def6;
    reconsider V1 = V as open Subset of REAL by A2,BORSUK_5:62,TOPMETR:24;
    consider g being real number such that
A5: 0 < g and
A6: ].p`2`2-g,p`2`2+g.[ c= V1 by A1,A4,RCOMP_1:40;
    reconsider g as Element of REAL by XREAL_0:def 1;
    set W1 = {|[x,y]| where x, y is Element of REAL:
    p`2`2-g < y & y < p`2`2+g};
    W1 c= the carrier of T2
    proof
      let a be set;
      assume a in W1;
      then ex x, y being Element of REAL st
      a = |[x,y]| & p`2`2-g < y & y < p`2`2+g;
      hence thesis;
    end;
    then reconsider W1 as Subset of T2;
    take [:[#]T2,W1:];
A7: p`2 = |[p`2`1,p`2`2]| by EUCLID:57;
    p`2`2-g < p`2`2-0 & p`2`2+0 < p`2`2+g by A5,REAL_1:92,XREAL_1:8;
    then p`2 in W1 by A7;
    hence p in [:[#]T2,W1:] by A3,ZFMISC_1:def 2;
    W1 is open by PSCOMP_1:68;
    hence [:[#]T2,W1:] is open by BORSUK_1:46;
    let b be set;
    assume b in fY2.:[:[#]T2,W1:];
    then consider a being Point of [:T2,T2:] such that
A8: a in [:[#]T2,W1:] and
A9: fY2.a = b by FUNCT_2:116;
A10: a = [a`1,a`2] by Lm7,MCART_1:23;
A11: fY2.a = a`2`2 by Def6;
    a`2 in W1 by A8,A10,ZFMISC_1:106;
    then consider x1, y1 being Element of REAL such that
A12: a`2 = |[x1,y1]| and
A13: p`2`2-g < y1 and
A14: y1 < p`2`2+g;
A15: a`2`2 = y1 by A12,EUCLID:56;
    p`2`2-g+g < y1+g & p`2`2-y1 > p`2`2-(p`2`2+g)
    by A13,A14,REAL_1:92,XREAL_1:8;
    then p`2`2-y1 < y1+g-y1 & p`2`2-y1 > -g by XREAL_1:11;
    then abs(p`2`2-y1) < g by SEQ_2:9;
    then abs-(p`2`2-y1) < g by COMPLEX1:138;
    then abs(y1-p`2`2) < g;
    then a`2`2 in ].p`2`2-g,p`2`2+g.[ by A15,RCOMP_1:8;
    hence b in V by A6,A9,A11;
  end;
  hence thesis by JGRAPH_2:20;
end;

registration
  let o be Point of TOP-REAL 2;
  cluster diffX2_1(o) -> continuous;
  coherence
  proof
    diffX2_1(o) is continuous Function of [:T2,T2:],R^1 by Th58;
    hence thesis by TOPREAL6:83;
  end;
  cluster diffX2_2(o) -> continuous;
  coherence
  proof
    diffX2_2(o) is continuous Function of [:T2,T2:],R^1 by Th59;
    hence thesis by TOPREAL6:83;
  end;
end;

registration
  cluster diffX1_X2_1 -> continuous;
  coherence by Th60,TOPREAL6:83;
  cluster diffX1_X2_2 -> continuous;
  coherence by Th61,TOPREAL6:83;
  cluster Proj2_1 -> continuous;
  coherence by Th62,TOPREAL6:83;
  cluster Proj2_2 -> continuous;
  coherence by Th63,TOPREAL6:83;
end;

definition
  let n be non empty Element of NAT, o, p be Point of TOP-REAL n,
  r be positive (real number) such that
A1: p is Point of Tdisk(o,r);
  set X = (TOP-REAL n)|(cl_Ball(o,r)\{p});
  func DiskProj(o,r,p) -> Function
  of (TOP-REAL n)|(cl_Ball(o,r)\{p}), Tcircle(o,r) means
  :Def7:
  for x being Point of (TOP-REAL n)|(cl_Ball(o,r)\{p})
  ex y being Point of TOP-REAL n st x = y & it.x = HC(p,y,o,r);
  existence
  proof
A2: the carrier of X = cl_Ball(o,r)\{p} by PRE_TOPC:29;
    defpred P[set,set] means
    ex z being Point of TOP-REAL n st $1 = z & $2 = HC(p,z,o,r);
A3: for x being set st x in the carrier of X
    ex y being set st y in the carrier of Tcircle(o,r) & P[x,y]
    proof
      let x be set such that
A4:   x in the carrier of X;
      reconsider z = x as Point of TOP-REAL n by A4,TOPMETR:2;
      z in cl_Ball(o,r) by A2,A4,XBOOLE_0:def 4;
      then
A5:   z is Point of Tdisk(o,r) by BROUWER:3;
      p <> z by A2,A4,ZFMISC_1:64;
      then HC(p,z,o,r) is Point of Tcircle(o,r) by A1,A5,BROUWER:6;
      hence thesis;
    end;
    consider f being Function of the carrier of X, the carrier of Tcircle(o,r)
    such that
A6: for x being set st x in the carrier of X holds P[x,f.x]
    from FUNCT_2:sch 1(A3);
    reconsider f as Function of X, Tcircle(o,r);
    take f;
    let x be Point of X;
    thus thesis by A6;
  end;
  uniqueness
  proof
    let f, g be Function of X, Tcircle(o,r) such that
A7: for x being Point of X
    ex y being Point of TOP-REAL n st x = y & f.x = HC(p,y,o,r) and
A8: for x being Point of X
    ex y being Point of TOP-REAL n st x = y & g.x = HC(p,y,o,r);
    now
      let x be set such that
A9:   x in the carrier of X;
A10:  ex y being Point of TOP-REAL n st x = y & f.x = HC(p,y,o,r) by A7,A9;
      ex y being Point of TOP-REAL n st x = y & g.x = HC(p,y,o,r) by A8,A9;
      hence f.x = g.x by A10;
    end;
    hence thesis by FUNCT_2:18;
  end;
end;

theorem Th64:
  for o, p being Point of TOP-REAL 2,
  r being positive (real number) st p is Point of Tdisk(o,r) holds
  DiskProj(o,r,p) is continuous
proof
  let o, p be Point of TOP-REAL 2;
  let r be positive (real number) such that
A1: p is Point of Tdisk(o,r);
  set D = Tdisk(o,r);
  set cB = cl_Ball(o,r);
  set Bp = cB \ {p};
  set OK = [:Bp,{p}:];
  set D1 = T2|Bp;
  set D2 = T2|{p};
  set S1 = Tcircle(o,r);
A2: p in {p} by TARSKI:def 1;
A3: the carrier of D = cl_Ball(o,r) by BROUWER:3;
A4: the carrier of D1 = Bp by PRE_TOPC:29;
A5: the carrier of D2 = {p} by PRE_TOPC:29;
  set TD = [:T2,T2:] | OK;
  set gg = DiskProj(o,r,p);
  set xo = diffX2_1(o);
  set yo = diffX2_2(o);
  set dx = diffX1_X2_1;
  set dy = diffX1_X2_2;
  set fx2 = Proj2_1;
  set fy2 = Proj2_2;
  reconsider rr = r^2 as Real by XREAL_0:def 1;
  set f1 = (the carrier of [:T2,T2:]) --> rr;
  reconsider f1 as continuous RealMap of [:T2,T2:] by Lm8;
  set Zf1 = f1 || OK;
  set Zfx2 = fx2 || OK;
  set Zfy2 = fy2 || OK;
  set Zdx = dx || OK;
  set Zdy = dy || OK;
  set Zxo = xo || OK;
  set Zyo = yo || OK;
  set xx = Zxo(#)Zdx;
  set yy = Zyo(#)Zdy;
  set m = Zdx(#)Zdx + Zdy(#)Zdy;
A6: the carrier of TD = OK by PRE_TOPC:29;
A7: for y being Point of D1, z being Point of D2 holds Zdx. [y,z] = dx. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
    [y,z] in OK by A4,A5,ZFMISC_1:def 2;
    hence Zdx. [y,z] = dx. [y,z] by FUNCT_1:72;
  end;
A8: for y being Point of D1, z being Point of D2 holds Zdy. [y,z] = dy. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
    [y,z] in OK by A4,A5,ZFMISC_1:def 2;
    hence Zdy. [y,z] = dy. [y,z] by FUNCT_1:72;
  end;
A9: for y being Point of D1, z being Point of D2 holds Zfx2. [y,z] = fx2. [y,z
  ]
  proof
    let y be Point of D1;
    let z be Point of D2;
    [y,z] in OK by A4,A5,ZFMISC_1:def 2;
    hence Zfx2. [y,z] = fx2. [y,z] by FUNCT_1:72;
  end;
A10: for y being Point of D1, z being Point of D2 holds Zfy2. [y,z] = fy2. [y,z
  ]
  proof
    let y be Point of D1;
    let z be Point of D2;
    [y,z] in OK by A4,A5,ZFMISC_1:def 2;
    hence Zfy2. [y,z] = fy2. [y,z] by FUNCT_1:72;
  end;
A11: for y being Point of D1, z being Point of D2 holds Zf1. [y,z] = f1. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
    [y,z] in OK by A4,A5,ZFMISC_1:def 2;
    hence Zf1. [y,z] = f1. [y,z] by FUNCT_1:72;
  end;
A12: for y being Point of D1, z being Point of D2 holds Zxo. [y,z] = xo. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
    [y,z] in OK by A4,A5,ZFMISC_1:def 2;
    hence Zxo. [y,z] = xo. [y,z] by FUNCT_1:72;
  end;
A13: for y being Point of D1, z being Point of D2 holds Zyo. [y,z] = yo. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
    [y,z] in OK by A4,A5,ZFMISC_1:def 2;
    hence Zyo. [y,z] = yo. [y,z] by FUNCT_1:72;
  end;
  now
    let b be real number;
    assume b in rng m;
    then consider a being set such that
A14: a in dom m and
A15: m.a = b by FUNCT_1:def 5;
    consider y, z being set such that
A16: y in Bp & z in {p} & a = [y,z] by A6,A14,ZFMISC_1:def 2;
A17: z = p by A16,TARSKI:def 1;
    reconsider y, z as Point of T2 by A16;
A18: y <> z by A16,A17,ZFMISC_1:64;
    [y,z] = [[y,z]`1,[y,z]`2] by Lm7,MCART_1:23;
    then
A19: y = [y,z]`1 & z = [y,z]`2 by ZFMISC_1:33;
A20: dx. [y,z] = [y,z]`1`1 - [y,z]`2`1 by Def3;
A21: dy. [y,z] = [y,z]`1`2 - [y,z]`2`2 by Def4;
    set r1 = y`1-z`1;
    set r2 = y`2-z`2;
A22: Zdx. [y,z] = dx. [y,z] by A4,A5,A7,A16;
A23: Zdy. [y,z] = dy. [y,z] by A4,A5,A8,A16;
A24: m. [y,z] = (Zdx(#)Zdx). [y,z] + (Zdy(#)Zdy). [y,z] by A14,A16,VALUED_1:1
      .= Zdx. [y,z] * Zdx. [y,z] + (Zdy(#)Zdy). [y,z] by VALUED_1:5
      .= r1^2+r2^2 by A19,A20,A21,A22,A23,VALUED_1:5;
A25: 0+0 <= r1^2+r2^2 by XREAL_1:9;
    now
      assume r1^2+r2^2 = 0;
      then r1 = 0 & r2 = 0 by COMPLEX1:2;
      hence contradiction by A18,TOPREAL3:11;
    end;
    hence 0 < b by A15,A16,A24,A25;
  end;
  then reconsider m as positive-yielding continuous RealMap of TD
  by PARTFUN3:def 1;
  set p1 = (xx+yy)(#)(xx+yy);
  set p2 = Zxo(#)Zxo + Zyo(#)Zyo - Zf1;
A26: dom p2 = the carrier of TD by FUNCT_2:def 1;
  now
    let b be real number;
    assume b in rng p2;
    then consider a being set such that
A27: a in dom p2 and
A28: p2.a = b by FUNCT_1:def 5;
    consider y, z being set such that
A29: y in Bp & z in {p} & a = [y,z] by A6,A27,ZFMISC_1:def 2;
    reconsider y, z as Point of T2 by A29;
    set r3 = z`1-o`1, r4 = z`2-o`2;
A30: Zf1. [y,z] = f1. [y,z] by A4,A5,A11,A29;
A31: Zxo. [y,z] = xo. [y,z] by A4,A5,A12,A29;
A32: Zyo. [y,z] = yo. [y,z] by A4,A5,A13,A29;
A33: [y,z] = [[y,z]`1,[y,z]`2] by Lm7,MCART_1:23;
A34: xo. [y,z] = [y,z]`2`1 - o`1 by Def1;
A35: y = [y,z]`1 & z = [y,z]`2 by A33,ZFMISC_1:33;
A36: yo. [y,z] = [y,z]`2`2 - o`2 by Def2;
A37: p2. [y,z] = (Zxo(#)Zxo + Zyo(#)Zyo). [y,z] - Zf1. [y,z]
    by A27,A29,VALUED_1:13
      .= (Zxo(#)Zxo + Zyo(#)Zyo). [y,z] - r^2 by A30,FUNCOP_1:13
      .= (Zxo(#)Zxo). [y,z] + (Zyo(#)Zyo). [y,z] - r^2 by A27,A29,VALUED_1:1
      .= Zxo. [y,z] * Zxo. [y,z] + (Zyo(#)Zyo). [y,z] - r^2 by VALUED_1:5
      .= r3^2+r4^2-r^2 by A31,A32,A34,A35,A36,VALUED_1:5;
    z = p by A29,TARSKI:def 1;
    then |. z-o .| <= r by A1,A3,TOPREAL9:8;
    then
A38: |. z-o .|^2 <= r^2 by SQUARE_1:77;
    |. z-o .|^2 = ((z-o)`1)^2+((z-o)`2)^2 by JGRAPH_1:46
      .= r3^2+((z-o)`2)^2 by TOPREAL3:8
      .= r3^2+r4^2 by TOPREAL3:8;
    then r3^2+r4^2-r^2 <= r^2-r^2 by A38,XREAL_1:11;
    hence 0 >= b by A28,A29,A37;
  end;
  then reconsider p2 as nonpositive-yielding continuous RealMap of TD
  by PARTFUN3:def 3;
  set pp = p1 - m(#)p2;
  set k = (-(xx+yy) + sqrt(pp)) / m;
  set x3 = Zfx2 + k(#)Zdx;
  set y3 = Zfy2 + k(#)Zdy;
  reconsider X3 = x3, Y3 = y3 as Function of TD,R^1 by TOPMETR:24;
  set F = <:X3,Y3:>;
  set R = R2Homeomorphism;
A39: for x being Point of D1 holds gg.x = (R*F). [x,p]
  proof
    let x be Point of D1;
    consider y being Point of T2 such that
A40: x = y & gg.x = HC(p,y,o,r) by A1,Def7;
A41: x <> p by A4,ZFMISC_1:64;
A42: [y,p] in OK by A2,A4,A40,ZFMISC_1:def 2;
    set r1 = y`1-p`1, r2 = y`2-p`2, r3 = p`1-o`1, r4 = p`2-o`2;
    set l = (-(r3*r1+r4*r2)+sqrt((r3*r1+r4*r2)^2-(r1^2+r2^2)*(r3^2+r4^2-r^2)))
    / (r1^2+r2^2);
    [y,p] = [[y,p]`1,[y,p]`2] by Lm7,MCART_1:23;
    then
A43: y = [y,p]`1 & p = [y,p]`2 by ZFMISC_1:33;
A44: fx2. [y,p] = [y,p]`2`1 by Def5;
A45: fy2. [y,p] = [y,p]`2`2 by Def6;
A46: dx. [y,p] = [y,p]`1`1 - [y,p]`2`1 by Def3;
A47: dy. [y,p] = [y,p]`1`2 - [y,p]`2`2 by Def4;
A48: xo. [y,p] = [y,p]`2`1 - o`1 by Def1;
A49: yo. [y,p] = [y,p]`2`2 - o`2 by Def2;
A50: dom X3 = the carrier of TD by FUNCT_2:def 1;
A51: dom Y3 = the carrier of TD by FUNCT_2:def 1;
A52: dom pp = the carrier of TD by FUNCT_2:def 1;
A53: y is Point of D1 & p is Point of D2 by A5,A40,TARSKI:def 1;
    then
A54: Zdx. [y,p] = dx. [y,p] by A7;
A55: Zdy. [y,p] = dy. [y,p] by A8,A53;
A56: Zf1. [y,p] = f1. [y,p] by A11,A53;
A57: Zxo. [y,p] = xo. [y,p] by A12,A53;
A58: Zyo. [y,p] = yo. [y,p] by A13,A53;
A59: m. [y,p] = (Zdx(#)Zdx). [y,p] + (Zdy(#)Zdy). [y,p] by A6,A42,VALUED_1:1
      .= Zdx. [y,p] * Zdx. [y,p] + (Zdy(#)Zdy). [y,p] by VALUED_1:5
      .= r1^2+r2^2 by A43,A46,A47,A54,A55,VALUED_1:5;
A60: xx. [y,p] = Zxo. [y,p] * Zdx. [y,p] by VALUED_1:5;
A61: yy. [y,p] = Zyo. [y,p] * Zdy. [y,p] by VALUED_1:5;
A62: (xx+yy). [y,p] = xx. [y,p] + yy. [y,p] by A6,A42,VALUED_1:1;
    then
A63: p1. [y,p] = (r3*r1+r4*r2)^2
    by A43,A46,A47,A48,A49,A54,A55,A57,A58,A60,A61,VALUED_1:5;
A64: p2. [y,p] = (Zxo(#)Zxo + Zyo(#)Zyo). [y,p] - Zf1. [y,p]
    by A6,A26,A42,VALUED_1:13
      .= (Zxo(#)Zxo + Zyo(#)Zyo). [y,p] - r^2 by A56,FUNCOP_1:13
      .= (Zxo(#)Zxo). [y,p] + (Zyo(#)Zyo). [y,p] - r^2 by A6,A42,VALUED_1:1
      .= Zxo. [y,p] * Zxo. [y,p] + (Zyo(#)Zyo). [y,p] - r^2 by VALUED_1:5
      .= r3^2+r4^2-r^2 by A43,A48,A49,A57,A58,VALUED_1:5;
    dom sqrt pp = the carrier of TD by FUNCT_2:def 1;
    then
A65: sqrt(pp). [y,p] = sqrt(pp. [y,p]) by A6,A42,PARTFUN3:def 5
      .= sqrt(p1. [y,p] - (m(#)p2). [y,p]) by A6,A42,A52,VALUED_1:13
      .= sqrt((r3*r1+r4*r2)^2-(r1^2+r2^2)*(r3^2+r4^2-r^2))
    by A59,A63,A64,VALUED_1:5;
    dom k = the carrier of TD by FUNCT_2:def 1;
    then
A66: k. [y,p] = (-(xx+yy) + sqrt(pp)). [y,p] * (m. [y,p])" by A6,A42,
    RFUNCT_1:def 4
      .= (-(xx+yy) + sqrt(pp)). [y,p] / m. [y,p] by XCMPLX_0:def 9
      .= ((-(xx+yy)). [y,p] + sqrt(pp). [y,p]) / (r1^2+r2^2)
    by A6,A42,A59,VALUED_1:1
      .= l by A43,A46,A47,A48,A49,A54,A55,A57,A58,A60,A61,A62,A65,VALUED_1:8;
A67: X3. [y,p] = Zfx2. [y,p] + (k(#)Zdx). [y,p] by A6,A42,VALUED_1:1
      .= p`1 + (k(#)Zdx). [y,p] by A9,A43,A44,A53
      .= p`1+l*r1 by A43,A46,A54,A66,VALUED_1:5;
A68: Y3. [y,p] = Zfy2. [y,p] + (k(#)Zdy). [y,p] by A6,A42,VALUED_1:1
      .= p`2 + (k(#)Zdy). [y,p] by A10,A43,A45,A53
      .= p`2+l*r2 by A43,A47,A55,A66,VALUED_1:5;
A69: y in Bp by A4,A40;
    Bp c= cB by XBOOLE_1:36;
    hence gg.x = |[ p`1+l*r1, p`2+l*r2 ]| by A1,A3,A40,A41,A69,BROUWER:8
      .= R. [X3. [y,p], Y3. [y,p]] by A67,A68,TOPREALA:def 2
      .= R.(F. [y,p]) by A6,A42,A50,A51,FUNCT_3:69
      .= (R*F). [x,p] by A6,A40,A42,FUNCT_2:21;
  end;
  X3 is continuous & Y3 is continuous by TOPREAL6:83;
  then reconsider F as continuous Function of TD,[:R^1,R^1:] by YELLOW12:41;
  for pp being Point of D1, V being Subset of S1
  st gg.pp in V & V is open holds
  ex W being Subset of D1 st pp in W & W is open & gg.:W c= V
  proof
    let pp be Point of D1, V be Subset of S1 such that
A70: gg.pp in V and
A71: V is open;
    reconsider p1 = pp, fp = p as Point of T2 by TOPMETR:2;
A72: [pp,p] in OK by A2,A4,ZFMISC_1:def 2;
    consider V1 being Subset of T2 such that
A73: V1 is open and
A74: V1 /\ [#]S1 = V by A71,TOPS_2:32;
A75: gg.pp = (R*F). [pp,p] by A39;
    R" is_homeomorphism by TOPREALA:56,TOPS_2:70;
    then
A76: R" .:V1 is open by A73,TOPGRP_1:25;
A77: dom F = the carrier of [:T2,T2:] | OK by FUNCT_2:def 1;
A78: dom R = the carrier of [:R^1,R^1:] by FUNCT_2:def 1;
    then
A79: rng F c= dom R;
    then
A80: dom (R*F) = dom F by RELAT_1:46;
A81: rng R = [#]T2 & R is one-to-one by TOPREALA:56,TOPS_2:def 5;
A82: R"*(R*F) = R"*R*F by RELAT_1:55
      .= id dom R*F by A81,TOPS_2:65;
    dom id dom R = dom R by RELAT_1:71;
    then
A83: dom (id dom R*F) = dom F by A79,RELAT_1:46;
    for x being set st x in dom F holds (id dom R*F).x = F.x
    proof
      let x be set such that
A84:  x in dom F;
A85:  F.x in rng F by A84,FUNCT_1:def 5;
      thus (id dom R*F).x = id dom R.(F.x) by A84,FUNCT_1:23
        .= F.x by A78,A85,FUNCT_1:35;
    end;
    then
A86: id dom R*F = F by A83,FUNCT_1:9;
    (R*F). [p1,fp] in V1 by A70,A74,A75,XBOOLE_0:def 3;
    then R" .((R*F). [p1,fp]) in R" .:V1 by FUNCT_2:43;
    then (R"*(R*F)). [p1,fp] in R" .:V1 by A6,A72,A77,A80,FUNCT_1:23;
    then consider W being Subset of TD such that
A87: [p1,fp] in W and
A88: W is open and
A89: F.:W c= R" .:V1 by A6,A72,A76,A82,A86,JGRAPH_2:20;
    consider WW being Subset of [:T2,T2:] such that
A90: WW is open and
A91: WW /\ [#]TD = W by A88,TOPS_2:32;
    consider SF being Subset-Family of [:T2,T2:] such that
A92: WW = union SF and
A93: for e being set st e in SF
    ex X1 being Subset of T2, Y1 being Subset of T2 st
    e = [:X1,Y1:] & X1 is open & Y1 is open by A90,BORSUK_1:45;
    [p1,fp] in WW by A87,A91,XBOOLE_0:def 3;
    then consider Z being set such that
A94: [p1,fp] in Z and
A95: Z in SF by A92,TARSKI:def 4;
    consider X1, Y1 being Subset of T2 such that
A96: Z = [:X1,Y1:] and
A97: X1 is open and Y1 is open by A93,A95;
    set ZZ = Z /\ [#]TD;
    reconsider XX = X1 /\ [#]D1 as open Subset of D1 by A97,TOPS_2:32;
    take XX;
    pp in X1 by A94,A96,ZFMISC_1:106;
    hence pp in XX by XBOOLE_0:def 3;
    thus XX is open;
    let b be set;
    assume b in gg.:XX;
    then consider a being Point of D1 such that
A98: a in XX and
A99: b = gg.a by FUNCT_2:116;
    reconsider a1 = a, fa = fp as Point of T2 by TOPMETR:2;
A100: a in X1 by A98,XBOOLE_0:def 3;
A101: [a,p] in OK by A2,A4,ZFMISC_1:def 2;
    fa in Y1 by A94,A96,ZFMISC_1:106;
    then [a,fa] in Z by A96,A100,ZFMISC_1:def 2;
    then [a,fa] in ZZ by A6,A101,XBOOLE_0:def 3;
    then
A102: F. [a1,fa] in F.:ZZ by FUNCT_2:43;
A103: R qua Function" = R" by A81,TOPS_2:def 4;
A104: dom(R") = [#]T2 by A81,TOPS_2:62;
    Z c= WW by A92,A95,ZFMISC_1:92;
    then ZZ c= WW /\ [#]TD by XBOOLE_1:27;
    then F.:ZZ c= F.:W by A91,RELAT_1:156;
    then F. [a1,fa] in F.:W by A102;
    then R.(F. [a1,fa]) in R.:(R" .:V1) by A89,FUNCT_2:43;
    then (R*F). [a1,fa] in R.:(R" .:V1) by A6,A101,FUNCT_2:21;
    then (R*F). [a1,fa] in V1 by A81,A103,A104,PARTFUN3:1;
    then gg.a in V1 by A39;
    hence b in V by A74,A99,XBOOLE_0:def 3;
  end;
  hence thesis by JGRAPH_2:20;
end;

theorem Th65:
  for n being non empty Element of NAT, o, p being Point of TOP-REAL n,
  r being positive (real number) st p in Ball(o,r) holds
  DiskProj(o,r,p)|Sphere(o,r) = id Sphere(o,r)
proof
  let n be non empty Element of NAT;
  let o, p be Point of TOP-REAL n;
  let r be positive (real number);
  assume
A1: p in Ball(o,r);
A2: the carrier of Tdisk(o,r) = cl_Ball(o,r) by BROUWER:3;
A3: the carrier of (TOP-REAL n)|(cl_Ball(o,r)\{p}) = cl_Ball(o,r)\{p}
  by PRE_TOPC:29;
A4: dom DiskProj(o,r,p) = the carrier of (TOP-REAL n)|(cl_Ball(o,r)\{p})
  by FUNCT_2:def 1;
A5: Sphere(o,r) misses Ball(o,r) by TOPREAL9:19;
A6: Sphere(o,r) c= cl_Ball(o,r) by TOPREAL9:17;
A7: Ball(o,r) c= cl_Ball(o,r) by TOPREAL9:16;
  Sphere(o,r) c= cl_Ball(o,r)\{p}
  proof
    let a be set;
    assume
A8: a in Sphere(o,r);
    then a <> p by A1,A5,XBOOLE_0:3;
    hence thesis by A6,A8,ZFMISC_1:64;
  end;
  then
A9: dom(DiskProj(o,r,p)|Sphere(o,r)) = Sphere(o,r) by A3,A4,RELAT_1:91;
A10: dom id Sphere(o,r) = Sphere(o,r) by RELAT_1:71;
  now
    let x be set;
    assume
A11: x in dom(DiskProj(o,r,p)|Sphere(o,r));
    then consider y being Point of TOP-REAL n such that
A12: x = y and
A13: (DiskProj(o,r,p)).x = HC(p,y,o,r) by A1,A2,A7,Def7;
    y in halfline(p,y) by TOPREAL9:28;
    then
A14: x in halfline(p,y) /\ Sphere(o,r) by A9,A11,A12,XBOOLE_0:def 3;
A15: x <> p by A1,A5,A9,A11,XBOOLE_0:3;
    thus (DiskProj(o,r,p)|Sphere(o,r)).x = (DiskProj(o,r,p)).x
    by A11,FUNCT_1:70
      .= x by A1,A2,A6,A7,A9,A11,A12,A13,A14,A15,BROUWER:def 3
      .= (id Sphere(o,r)).x by A9,A11,FUNCT_1:35;
  end;
  hence thesis by A9,A10,FUNCT_1:9;
end;

definition
  let n be non empty Element of NAT, o, p be Point of TOP-REAL n,
  r be positive (real number) such that
A1: p in Ball(o,r);
  set X = Tcircle(o,r);
  func RotateCircle(o,r,p) -> Function of Tcircle(o,r), Tcircle(o,r) means
  :Def8:
  for x being Point of Tcircle(o,r)
  ex y being Point of TOP-REAL n st x = y & it.x = HC(y,p,o,r);
  existence
  proof
A2: the carrier of X = Sphere(o,r) by TOPREALB:9;
    defpred P[set,set] means
    ex z being Point of TOP-REAL n st $1 = z & $2 = HC(z,p,o,r);
A3: for x being set st x in the carrier of X
    ex y being set st y in the carrier of X & P[x,y]
    proof
      let x be set such that
A4:   x in the carrier of X;
      reconsider z = x as Point of TOP-REAL n by A4,TOPMETR:2;
      Sphere(o,r) c= cl_Ball(o,r) by TOPREAL9:17;
      then
A5:   z is Point of Tdisk(o,r) by A2,A4,BROUWER:3;
      Ball(o,r) c= cl_Ball(o,r) by TOPREAL9:16;
      then
A6:   p is Point of Tdisk(o,r) by A1,BROUWER:3;
      Ball(o,r) misses Sphere(o,r) by TOPREAL9:19;
      then p <> z by A1,A2,A4,XBOOLE_0:3;
      then HC(z,p,o,r) is Point of X by A5,A6,BROUWER:6;
      hence thesis;
    end;
    consider f being Function of the carrier of X, the carrier of X such that
A7: for x being set st x in the carrier of X holds P[x,f.x]
    from FUNCT_2:sch 1(A3);
    reconsider f as Function of X, X;
    take f;
    let x be Point of X;
    thus thesis by A7;
  end;
  uniqueness
  proof
    let f, g be Function of X, X such that
A8: for x being Point of X
    ex y being Point of TOP-REAL n st x = y & f.x = HC(y,p,o,r) and
A9: for x being Point of X
    ex y being Point of TOP-REAL n st x = y & g.x = HC(y,p,o,r);
    now
      let x be set such that
A10:  x in the carrier of X;
A11:  ex y being Point of TOP-REAL n st x = y & f.x = HC(y,p,o,r) by A8,A10;
      ex y being Point of TOP-REAL n st x = y & g.x = HC(y,p,o,r) by A9,A10;
      hence f.x = g.x by A11;
    end;
    hence thesis by FUNCT_2:18;
  end;
end;

theorem Th66:
  for o, p being Point of TOP-REAL 2,
  r being positive (real number) st p in Ball(o,r) holds
  RotateCircle(o,r,p) is continuous
proof
  let o, p be Point of TOP-REAL 2;
  let r be positive (real number) such that
A1: p in Ball(o,r);
  set D = Tdisk(o,r);
  set cB = cl_Ball(o,r);
  set Bp = Sphere(o,r);
  set OK = [:{p},Bp:];
  set D1 = T2|{p};
  set D2 = T2|Bp;
  set S1 = Tcircle(o,r);
A2: D2 = S1 by TOPREALB:def 6;
A3: Ball(o,r) misses Sphere(o,r) by TOPREAL9:19;
A4: p in {p} by TARSKI:def 1;
A5: Bp c= cB by TOPREAL9:17;
A6: Ball(o,r) c= cB by TOPREAL9:16;
A7: the carrier of D = cB by BROUWER:3;
A8: the carrier of D1 = {p} by PRE_TOPC:29;
A9: the carrier of D2 = Bp by PRE_TOPC:29;
  set TD = [:T2,T2:] | OK;
  set gg = RotateCircle(o,r,p);
  set xo = diffX2_1(o);
  set yo = diffX2_2(o);
  set dx = diffX1_X2_1;
  set dy = diffX1_X2_2;
  set fx2 = Proj2_1;
  set fy2 = Proj2_2;
  reconsider rr = r^2 as Real by XREAL_0:def 1;
  set f1 = (the carrier of [:T2,T2:]) --> rr;
  reconsider f1 as continuous RealMap of [:T2,T2:] by Lm8;
  set Zf1 = f1 || OK;
  set Zfx2 = fx2 || OK;
  set Zfy2 = fy2 || OK;
  set Zdx = dx || OK;
  set Zdy = dy || OK;
  set Zxo = xo || OK;
  set Zyo = yo || OK;
  set xx = Zxo(#)Zdx;
  set yy = Zyo(#)Zdy;
  set m = Zdx(#)Zdx + Zdy(#)Zdy;
A10: the carrier of TD = OK by PRE_TOPC:29;
A11: for y being Point of D1, z being Point of D2 holds Zdx. [y,z] = dx. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
    [y,z] in OK by A8,A9,ZFMISC_1:def 2;
    hence Zdx. [y,z] = dx. [y,z] by FUNCT_1:72;
  end;
A12: for y being Point of D1, z being Point of D2 holds Zdy. [y,z] = dy. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
    [y,z] in OK by A8,A9,ZFMISC_1:def 2;
    hence Zdy. [y,z] = dy. [y,z] by FUNCT_1:72;
  end;
A13: for y being Point of D1, z being Point of D2 holds Zfx2. [y,z] = fx2. [y,z
  ]
  proof
    let y be Point of D1;
    let z be Point of D2;
    [y,z] in OK by A8,A9,ZFMISC_1:def 2;
    hence Zfx2. [y,z] = fx2. [y,z] by FUNCT_1:72;
  end;
A14: for y being Point of D1, z being Point of D2 holds Zfy2. [y,z] = fy2. [y,z
  ]
  proof
    let y be Point of D1;
    let z be Point of D2;
    [y,z] in OK by A8,A9,ZFMISC_1:def 2;
    hence Zfy2. [y,z] = fy2. [y,z] by FUNCT_1:72;
  end;
A15: for y being Point of D1, z being Point of D2 holds Zf1. [y,z] = f1. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
    [y,z] in OK by A8,A9,ZFMISC_1:def 2;
    hence Zf1. [y,z] = f1. [y,z] by FUNCT_1:72;
  end;
A16: for y being Point of D1, z being Point of D2 holds Zxo. [y,z] = xo. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
    [y,z] in OK by A8,A9,ZFMISC_1:def 2;
    hence Zxo. [y,z] = xo. [y,z] by FUNCT_1:72;
  end;
A17: for y being Point of D1, z being Point of D2 holds Zyo. [y,z] = yo. [y,z]
  proof
    let y be Point of D1;
    let z be Point of D2;
    [y,z] in OK by A8,A9,ZFMISC_1:def 2;
    hence Zyo. [y,z] = yo. [y,z] by FUNCT_1:72;
  end;
  now
    let b be real number;
    assume b in rng m;
    then consider a being set such that
A18: a in dom m and
A19: m.a = b by FUNCT_1:def 5;
    consider y, z being set such that
A20: y in {p} & z in Bp & a = [y,z] by A10,A18,ZFMISC_1:def 2;
A21: y = p by A20,TARSKI:def 1;
    reconsider y, z as Point of T2 by A20;
A22: y <> z by A1,A3,A20,A21,XBOOLE_0:3;
    [y,z] = [[y,z]`1,[y,z]`2] by Lm7,MCART_1:23;
    then
A23: y = [y,z]`1 & z = [y,z]`2 by ZFMISC_1:33;
A24: dx. [y,z] = [y,z]`1`1 - [y,z]`2`1 by Def3;
A25: dy. [y,z] = [y,z]`1`2 - [y,z]`2`2 by Def4;
    set r1 = y`1-z`1;
    set r2 = y`2-z`2;
A26: Zdx. [y,z] = dx. [y,z] by A8,A9,A11,A20;
A27: Zdy. [y,z] = dy. [y,z] by A8,A9,A12,A20;
A28: m. [y,z] = (Zdx(#)Zdx). [y,z] + (Zdy(#)Zdy). [y,z] by A18,A20,VALUED_1:1
      .= Zdx. [y,z] * Zdx. [y,z] + (Zdy(#)Zdy). [y,z] by VALUED_1:5
      .= r1^2+r2^2 by A23,A24,A25,A26,A27,VALUED_1:5;
A29: 0+0 <= r1^2+r2^2 by XREAL_1:9;
    now
      assume r1^2+r2^2 = 0;
      then r1 = 0 & r2 = 0 by COMPLEX1:2;
      hence contradiction by A22,TOPREAL3:11;
    end;
    hence 0 < b by A19,A20,A28,A29;
  end;
  then reconsider m as positive-yielding continuous RealMap of TD
  by PARTFUN3:def 1;
  set p1 = (xx+yy)(#)(xx+yy);
  set p2 = Zxo(#)Zxo + Zyo(#)Zyo - Zf1;
A30: dom p2 = the carrier of TD by FUNCT_2:def 1;
  now
    let b be real number;
    assume b in rng p2;
    then consider a being set such that
A31: a in dom p2 and
A32: p2.a = b by FUNCT_1:def 5;
    consider y, z being set such that
A33: y in {p} & z in Bp & a = [y,z] by A10,A31,ZFMISC_1:def 2;
    reconsider y, z as Point of T2 by A33;
    set r3 = z`1-o`1, r4 = z`2-o`2;
A34: Zf1. [y,z] = f1. [y,z] by A8,A9,A15,A33;
A35: Zxo. [y,z] = xo. [y,z] by A8,A9,A16,A33;
A36: Zyo. [y,z] = yo. [y,z] by A8,A9,A17,A33;
    [y,z] = [[y,z]`1,[y,z]`2] by Lm7,MCART_1:23;
    then
A37: y = [y,z]`1 & z = [y,z]`2 by ZFMISC_1:33;
A38: xo. [y,z] = [y,z]`2`1 - o`1 by Def1;
A39: yo. [y,z] = [y,z]`2`2 - o`2 by Def2;
A40: p2. [y,z] = (Zxo(#)Zxo + Zyo(#)Zyo). [y,z] - Zf1. [y,z]
    by A31,A33,VALUED_1:13
      .= (Zxo(#)Zxo + Zyo(#)Zyo). [y,z] - r^2 by A34,FUNCOP_1:13
      .= (Zxo(#)Zxo). [y,z] + (Zyo(#)Zyo). [y,z] - r^2 by A31,A33,VALUED_1:1
      .= Zxo. [y,z] * Zxo. [y,z] + (Zyo(#)Zyo). [y,z] - r^2 by VALUED_1:5
      .= r3^2+r4^2-r^2 by A35,A36,A37,A38,A39,VALUED_1:5;
    |. z-o .| <= r by A5,A33,TOPREAL9:8;
    then
A41: |. z-o .|^2 <= r^2 by SQUARE_1:77;
    |. z-o .|^2 = ((z-o)`1)^2+((z-o)`2)^2 by JGRAPH_1:46
      .= r3^2+((z-o)`2)^2 by TOPREAL3:8
      .= r3^2+r4^2 by TOPREAL3:8;
    then r3^2+r4^2-r^2 <= r^2-r^2 by A41,XREAL_1:11;
    hence 0 >= b by A32,A33,A40;
  end;
  then reconsider p2 as nonpositive-yielding continuous RealMap of TD
  by PARTFUN3:def 3;
  set pp = p1 - m(#)p2;
  set k = (-(xx+yy) + sqrt(pp)) / m;
  set x3 = Zfx2 + k(#)Zdx;
  set y3 = Zfy2 + k(#)Zdy;
  reconsider X3 = x3, Y3 = y3 as Function of TD,R^1 by TOPMETR:24;
  set F = <:X3,Y3:>;
  set R = R2Homeomorphism;
A42: for x being Point of D2 holds gg.x = (R*F). [p,x]
  proof
    let x be Point of D2;
    consider y being Point of T2 such that
A43: x = y & gg.x = HC(y,p,o,r) by A1,A2,Def8;
A44: x <> p by A1,A3,A9,XBOOLE_0:3;
A45: [p,y] in OK by A4,A9,A43,ZFMISC_1:def 2;
    set r1 = p`1-y`1, r2 = p`2-y`2, r3 = y`1-o`1, r4 = y`2-o`2;
    set l = (-(r3*r1+r4*r2)+sqrt((r3*r1+r4*r2)^2-(r1^2+r2^2)*(r3^2+r4^2-r^2)))
    / (r1^2+r2^2);
    [p,y] = [[p,y]`1,[p,y]`2] by Lm7,MCART_1:23;
    then
A46: p = [p,y]`1 & y = [p,y]`2 by ZFMISC_1:33;
A47: fx2. [p,y] = [p,y]`2`1 by Def5;
A48: fy2. [p,y] = [p,y]`2`2 by Def6;
A49: dx. [p,y] = [p,y]`1`1 - [p,y]`2`1 by Def3;
A50: dy. [p,y] = [p,y]`1`2 - [p,y]`2`2 by Def4;
A51: xo. [p,y] = [p,y]`2`1 - o`1 by Def1;
A52: yo. [p,y] = [p,y]`2`2 - o`2 by Def2;
A53: dom X3 = the carrier of TD by FUNCT_2:def 1;
A54: dom Y3 = the carrier of TD by FUNCT_2:def 1;
A55: dom pp = the carrier of TD by FUNCT_2:def 1;
A56: y is Point of D2 & p is Point of D1 by A8,A43,TARSKI:def 1;
    then
A57: Zdx. [p,y] = dx. [p,y] by A11;
A58: Zdy. [p,y] = dy. [p,y] by A12,A56;
A59: Zf1. [p,y] = f1. [p,y] by A15,A56;
A60: Zxo. [p,y] = xo. [p,y] by A16,A56;
A61: Zyo. [p,y] = yo. [p,y] by A17,A56;
A62: m. [p,y] = (Zdx(#)Zdx). [p,y] + (Zdy(#)Zdy). [p,y] by A10,A45,VALUED_1:1
      .= Zdx. [p,y] * Zdx. [p,y] + (Zdy(#)Zdy). [p,y] by VALUED_1:5
      .= r1^2+r2^2 by A46,A49,A50,A57,A58,VALUED_1:5;
A63: xx. [p,y] = Zxo. [p,y] * Zdx. [p,y] by VALUED_1:5;
A64: yy. [p,y] = Zyo. [p,y] * Zdy. [p,y] by VALUED_1:5;
A65: (xx+yy). [p,y] = xx. [p,y] + yy. [p,y] by A10,A45,VALUED_1:1;
    then
A66: p1. [p,y] = (r3*r1+r4*r2)^2
    by A46,A49,A50,A51,A52,A57,A58,A60,A61,A63,A64,VALUED_1:5;
A67: p2. [p,y] = (Zxo(#)Zxo + Zyo(#)Zyo). [p,y] - Zf1. [p,y]
    by A10,A30,A45,VALUED_1:13
      .= (Zxo(#)Zxo + Zyo(#)Zyo). [p,y] - r^2 by A59,FUNCOP_1:13
      .= (Zxo(#)Zxo). [p,y] + (Zyo(#)Zyo). [p,y] - r^2 by A10,A45,VALUED_1:1
      .= Zxo. [p,y] * Zxo. [p,y] + (Zyo(#)Zyo). [p,y] - r^2 by VALUED_1:5
      .= r3^2+r4^2-r^2 by A46,A51,A52,A60,A61,VALUED_1:5;
    dom sqrt pp = the carrier of TD by FUNCT_2:def 1;
    then
A68: sqrt(pp). [p,y] = sqrt(pp. [p,y]) by A10,A45,PARTFUN3:def 5
      .= sqrt(p1. [p,y] - (m(#)p2). [p,y]) by A10,A45,A55,VALUED_1:13
      .= sqrt((r3*r1+r4*r2)^2-(r1^2+r2^2)*(r3^2+r4^2-r^2))
    by A62,A66,A67,VALUED_1:5;
    dom k = the carrier of TD by FUNCT_2:def 1;
    then
A69: k. [p,y] = (-(xx+yy) + sqrt(pp)). [p,y] * (m. [p,y])" by A10,A45,
    RFUNCT_1:def 4
      .= (-(xx+yy) + sqrt(pp)). [p,y] / m. [p,y] by XCMPLX_0:def 9
      .= ((-(xx+yy)). [p,y] + sqrt(pp). [p,y]) / (r1^2+r2^2)
    by A10,A45,A62,VALUED_1:1
      .= l by A46,A49,A50,A51,A52,A57,A58,A60,A61,A63,A64,A65,A68,VALUED_1:8;
A70: X3. [p,y] = Zfx2. [p,y] + (k(#)Zdx). [p,y] by A10,A45,VALUED_1:1
      .= y`1 + (k(#)Zdx). [p,y] by A13,A46,A47,A56
      .= y`1+l*r1 by A46,A49,A57,A69,VALUED_1:5;
A71: Y3. [p,y] = Zfy2. [p,y] + (k(#)Zdy). [p,y] by A10,A45,VALUED_1:1
      .= y`2 + (k(#)Zdy). [p,y] by A14,A46,A48,A56
      .= y`2+l*r2 by A46,A50,A58,A69,VALUED_1:5;
    y in the carrier of D2 by A43;
    hence gg.x = |[ y`1+l*r1, y`2+l*r2 ]| by A1,A5,A6,A7,A9,A43,A44,BROUWER:8
      .= R. [X3. [p,y], Y3. [p,y]] by A70,A71,TOPREALA:def 2
      .= R.(F. [p,y]) by A10,A45,A53,A54,FUNCT_3:69
      .= (R*F). [p,x] by A10,A43,A45,FUNCT_2:21;
  end;
  X3 is continuous & Y3 is continuous by TOPREAL6:83;
  then reconsider F as continuous Function of TD,[:R^1,R^1:] by YELLOW12:41;
  for pp being Point of D2, V being Subset of S1
  st gg.pp in V & V is open holds
  ex W being Subset of D2 st pp in W & W is open & gg.:W c= V
  proof
    let pp be Point of D2, V be Subset of S1 such that
A72: gg.pp in V and
A73: V is open;
    reconsider p1 = pp, fp = p as Point of T2 by TOPMETR:2;
A74: [p,pp] in OK by A4,A9,ZFMISC_1:def 2;
    consider V1 being Subset of T2 such that
A75: V1 is open and
A76: V1 /\ [#]S1 = V by A73,TOPS_2:32;
A77: gg.pp = (R*F). [p,pp] by A42;
    R" is_homeomorphism by TOPREALA:56,TOPS_2:70;
    then
A78: R" .:V1 is open by A75,TOPGRP_1:25;
A79: dom F = the carrier of [:T2,T2:] | OK by FUNCT_2:def 1;
A80: dom R = the carrier of [:R^1,R^1:] by FUNCT_2:def 1;
    then
A81: rng F c= dom R;
    then
A82: dom (R*F) = dom F by RELAT_1:46;
A83: rng R = [#]T2 & R is one-to-one by TOPREALA:56,TOPS_2:def 5;
A84: R"*(R*F) = R"*R*F by RELAT_1:55
      .= id dom R*F by A83,TOPS_2:65;
    dom id dom R = dom R by RELAT_1:71;
    then
A85: dom (id dom R*F) = dom F by A81,RELAT_1:46;
    for x being set st x in dom F holds (id dom R*F).x = F.x
    proof
      let x be set such that
A86:  x in dom F;
A87:  F.x in rng F by A86,FUNCT_1:def 5;
      thus (id dom R*F).x = id dom R.(F.x) by A86,FUNCT_1:23
        .= F.x by A80,A87,FUNCT_1:35;
    end;
    then
A88: id dom R*F = F by A85,FUNCT_1:9;
    (R*F). [fp,p1] in V1 by A72,A76,A77,XBOOLE_0:def 3;
    then R" .((R*F). [fp,p1]) in R" .:V1 by FUNCT_2:43;
    then (R"*(R*F)). [fp,p1] in R" .:V1 by A10,A74,A79,A82,FUNCT_1:23;
    then consider W being Subset of TD such that
A89: [fp,p1] in W and
A90: W is open and
A91: F.:W c= R" .:V1 by A10,A74,A78,A84,A88,JGRAPH_2:20;
    consider WW being Subset of [:T2,T2:] such that
A92: WW is open and
A93: WW /\ [#]TD = W by A90,TOPS_2:32;
    consider SF being Subset-Family of [:T2,T2:] such that
A94: WW = union SF and
A95: for e being set st e in SF
    ex X1 being Subset of T2, Y1 being Subset of T2 st
    e = [:X1,Y1:] & X1 is open & Y1 is open by A92,BORSUK_1:45;
    [fp,p1] in WW by A89,A93,XBOOLE_0:def 3;
    then consider Z being set such that
A96: [fp,p1] in Z and
A97: Z in SF by A94,TARSKI:def 4;
    consider X1, Y1 being Subset of T2 such that
A98: Z = [:X1,Y1:] and X1 is open and
A99: Y1 is open by A95,A97;
    set ZZ = Z /\ [#]TD;
    reconsider XX = Y1 /\ [#]D2 as open Subset of D2 by A99,TOPS_2:32;
    take XX;
    pp in Y1 by A96,A98,ZFMISC_1:106;
    hence pp in XX by XBOOLE_0:def 3;
    thus XX is open;
    let b be set;
    assume b in gg.:XX;
    then consider a being Point of D2 such that
A100: a in XX and
A101: b = gg.a by A2,FUNCT_2:116;
    reconsider a1 = a, fa = fp as Point of T2 by TOPMETR:2;
A102: a in Y1 by A100,XBOOLE_0:def 3;
A103: [p,a] in OK by A4,A9,ZFMISC_1:def 2;
    fa in X1 by A96,A98,ZFMISC_1:106;
    then [fa,a] in Z by A98,A102,ZFMISC_1:def 2;
    then [fa,a] in ZZ by A10,A103,XBOOLE_0:def 3;
    then
A104: F. [fa,a1] in F.:ZZ by FUNCT_2:43;
A105: R qua Function" = R" by A83,TOPS_2:def 4;
A106: dom(R") = [#]T2 by A83,TOPS_2:62;
A107: gg.a1 in the carrier of S1 by A2,FUNCT_2:7;
    Z c= WW by A94,A97,ZFMISC_1:92;
    then ZZ c= WW /\ [#]TD by XBOOLE_1:27;
    then F.:ZZ c= F.:W by A93,RELAT_1:156;
    then F. [fa,a1] in F.:W by A104;
    then R.(F. [fa,a1]) in R.:(R" .:V1) by A91,FUNCT_2:43;
    then (R*F). [fa,a1] in R.:(R" .:V1) by A10,A103,FUNCT_2:21;
    then (R*F). [fa,a1] in V1 by A83,A105,A106,PARTFUN3:1;
    then gg.a in V1 by A42;
    hence b in V by A76,A101,A107,XBOOLE_0:def 3;
  end;
  hence thesis by A2,JGRAPH_2:20;
end;

theorem Th67:
  for n being non empty Element of NAT for o, p being Point of TOP-REAL n,
  r being positive (real number) st p in Ball(o,r)
  holds RotateCircle(o,r,p) has_no_fixpoint
proof
  let n be non empty Element of NAT;
  let o, p be Point of TOP-REAL n;
  let r be positive (real number);
  assume
A1: p in Ball(o,r);
  set f = RotateCircle(o,r,p);
  let x be set;
  assume
A2: x in dom f;
  set S = Tcircle(o,r);
A3: dom f = the carrier of S by FUNCT_2:def 1;
  consider y being Point of TOP-REAL n such that
A4: x = y and
A5: f.x = HC(y,p,o,r) by A1,A2,Def8;
A6: the carrier of S = Sphere(o,r) by TOPREALB:9;
  Sphere(o,r) c= cl_Ball(o,r) by TOPREAL9:17;
  then
A7: y is Point of Tdisk(o,r) by A2,A3,A4,A6,BROUWER:3;
  Ball(o,r) c= cl_Ball(o,r) by TOPREAL9:16;
  then
A8: p is Point of Tdisk(o,r) by A1,BROUWER:3;
  Ball(o,r) misses Sphere(o,r) by TOPREAL9:19;
  then y <> p by A1,A2,A4,A6,XBOOLE_0:3;
  hence f.x <> x by A4,A5,A7,A8,BROUWER:def 3;
end;

begin :: Jordan's Curve Theorem

theorem Th68:
  U = P & U is_a_component_of (TOP-REAL 2)|C` &
  V is_a_component_of (TOP-REAL 2)|C` & U <> V implies Cl P misses V
proof
  assume that
A1: U = P and
A2: U is_a_component_of T2|C` and
A3: V is_a_component_of T2|C` and
A4: U <> V;
  assume Cl P meets V;
  then
A5: ex x being set st x in Cl P & x in V by XBOOLE_0:3;
  the carrier of T2|C` = C` by PRE_TOPC:29;
  then reconsider V1 = V as Subset of T2 by XBOOLE_1:1;
  reconsider T2C = T2|C` as non empty SubSpace of T2;
  T2C is locally_connected by JORDAN2C:89;
  then V is open by A3,CONNSP_2:21;
  then V1 is open by TSEP_1:17;
  then P meets V1 by A5,PRE_TOPC:def 13;
  hence thesis by A1,A2,A3,A4,CONNSP_1:37;
end;

theorem Th69:
  U is_a_component_of (TOP-REAL 2)|C` implies
  (TOP-REAL 2)|C`|U is arcwise_connected
proof
  set T = T2|C`;
  assume
A1: U is_a_component_of T;
  let a, b be Point of T|U;
A2: the carrier of T|U = U by PRE_TOPC:29;
A3: U <> {}T by A1,CONNSP_1:34;
  then
A4: T|U is non empty by A2,STRUCT_0:def 1;
  per cases;
  suppose
A5: a = b;
    reconsider TU = T|U as non empty TopSpace by A2,A3,STRUCT_0:def 1;
    reconsider a as Point of TU;
    reconsider f = I[01] --> a as Function of I[01],T|U;
    take f;
    thus thesis by A5,BORSUK_1:def 17,def 18,TOPALG_3:5;
  end;
  suppose
A6: a <> b;
A7: T|U is SubSpace of T2 by TSEP_1:7;
    then reconsider a1 = a, b1 = b as Point of T2 by A4,TOPMETR:2;
    reconsider V = U as Subset of T2 by PRE_TOPC:39;
    V is_a_component_of C` by A1,CONNSP_1:def 6;
    then
A8: V is open by SPRECT_3:18;
    U is connected by A1,CONNSP_1:def 5;
    then V is connected by CONNSP_1:24;
    then V is being_Region by A8,TOPREAL4:def 3;
    then consider P being Subset of T2 such that
A9: P is_S-P_arc_joining a1,b1 and
A10: P c= V by A2,A3,A6,TOPREAL4:30;
A11: a1 in P by A9,TOPREAL4:4;
    P is_an_arc_of a1,b1 by A9,TOPREAL4:3;
    then consider g being Function of I[01], T2|P such that
A12: g is_homeomorphism and
A13: g.0 = a & g.1 = b by TOPREAL1:def 2;
A14: the carrier of T2|P = P by PRE_TOPC:29;
    then reconsider f = g as Function of I[01], T|U by A2,A10,A11,FUNCT_2:9;
    take f;
A15: g is continuous by A12,TOPS_2:def 5;
    T2|P is SubSpace of T|U by A2,A7,A10,A14,TSEP_1:4;
    hence f is continuous by A15,TOPMETR:7;
    thus f.0 = a & f.1 = b by A13;
  end;
end;

::Th5_6: ex r ...

Lm14: for r being non negative (real number) st A is_an_arc_of p1,p2 &
A is Subset of Tdisk(p,r) ex f being Function
of Tdisk(p,r), (TOP-REAL 2)|A st f is continuous & f|A = id A
proof
  let r be non negative (real number);
  set D = Tdisk(p,r);
  assume that
A1: A is_an_arc_of p1,p2 and
A2: A is Subset of D;
  reconsider A1 = A as non empty Subset of D by A1,A2,TOPREAL1:4;
  reconsider A2 = A as non empty Subset of T2 by A1,TOPREAL1:4;
  set TA = T2|A2;
  consider h being Function of I[01],TA such that
A3: h is_homeomorphism and h.0 = p1 and h.1 = p2 by A1,TOPREAL1:def 2;
A4: h1 is_homeomorphism by TREAL_1:20;
  then
A5: h1 is continuous by TOPS_2:def 5;
  reconsider hh = h as Function of C0,TA by TOPMETR:27;
A6: TA = D|A1 by TOPALG_5:4;
  then reconsider f = h1*hh" as Function of D|A1,C1;
  A is closed by A1,JORDAN6:12;
  then
A7: A1 is closed by TSEP_1:12;
  hh" is continuous by A3,TOPMETR:27,TOPS_2:def 5;
  then h1*hh" is continuous by A5,TOPS_2:58;
  then consider g being continuous Function of D,C1 such that
A8: g|A1 = f by A6,A7,TIETZE:25;
  reconsider R = hh*h1"*g as Function of D,(TOP-REAL 2)|A;
  take R;
A9: hh is continuous by A3,TOPMETR:27,TOPS_2:def 5;
  h1" is continuous by A4,TOPS_2:def 5;
  then hh*h1" is continuous by A9,TOPS_2:58;
  hence R is continuous by TOPS_2:58;
A10: the carrier of TA = A1 by PRE_TOPC:29;
  dom R = the carrier of D by FUNCT_2:def 1;
  then
A11: dom(R|A) = A by A2,RELAT_1:91;
A12: dom id A = A by RELAT_1:71;
  now
    let a be set;
    assume
A13: a in dom(R|A);
A14: dom g = the carrier of D by FUNCT_2:def 1;
A15: dom(h1*hh") = the carrier of TA by FUNCT_2:def 1;
A16: h1 is one-to-one by A4,TOPS_2:def 5;
A17: h1 is onto by A4,Th27;
A18: hh is one-to-one onto by A3,Th27,TOPMETR:27,TOPS_2:def 5;
A19: hh*h1"*(h1*hh") = hh*h1"*h1*hh" by RELAT_1:55
      .= hh*(h1"*h1)*hh" by RELAT_1:55
      .= hh*id C0*hh" by A16,A17,GRCAT_1:56
      .= hh*hh" by FUNCT_2:23
      .= id TA by A18,GRCAT_1:56;
    thus (R|A).a = R.a by A11,A13,FUNCT_1:72
      .= (hh*h1").(g.a) by A13,A14,FUNCT_1:23
      .= (hh*h1").((h1*hh").a) by A8,A11,A13,FUNCT_1:72
      .= (id A).a by A10,A11,A13,A15,A19,FUNCT_1:23;
  end;
  hence thesis by A11,A12,FUNCT_1:9;
end;

::Th5_6: ex f ...

Lm15: for r being positive (real number) st A is_an_arc_of p1,p2 & A c= C &
C c= Ball(p,r) & p in U & Cl P /\ P` c= A & P c= Ball(p,r)
for f being Function of Tdisk(p,r), (TOP-REAL 2)|A st f is continuous &
f|A = id A &

:: komponenty
U = P & U is_a_component_of (TOP-REAL 2)|C` & B = cl_Ball(p,r) \ {p}
ex g being Function of Tdisk(p,r), (TOP-REAL 2)|B st g is continuous &
for x being Point of Tdisk(p,r) holds (x in Cl P implies g.x = f.x) &
(x in P` implies g.x = x)
proof
  let r be positive (real number);
  set D = Tdisk(p,r);
  assume that
A1: A is_an_arc_of p1,p2 and
A2: A c= C and
A3: C c= Ball(p,r) and
A4: p in U and
A5: Cl P /\ P` c= A and
A6: P c= Ball(p,r);
  let f be Function of D, T2|A;
  assume that
A7: f is continuous and
A8: f|A = id A and
A9: U = P and
A10: U is_a_component_of T2|C` and
A11: B = cl_Ball(p,r) \ {p};
  reconsider B1 = B as non empty Subset of T2 by A11;
  reconsider T2B1 = T2|B1 as non empty SubSpace of T2;
A12: the carrier of T2|C` = C` by PRE_TOPC:29;
A13: the carrier of T2|A = A by PRE_TOPC:29;
A14: the carrier of D = cl_Ball(p,r) by BROUWER:3;
A15: Ball(p,r) c= cl_Ball(p,r) by TOPREAL9:16;
A16: A <> {} by A1,TOPREAL1:4;
  reconsider A1 = A as non empty Subset of T2 by A1,TOPREAL1:4;
A17: not p in C by A4,A12,XBOOLE_0:def 4;
  |. p-p .| = 0 by TOPRNS_1:29;
  then
A18: p in [#]D by A14,TOPREAL9:8;
A19: P c= Cl P by PRE_TOPC:48;
  then reconsider F1 = (Cl P) /\ [#]D as non empty Subset of D
  by A4,A9,A18,XBOOLE_0:def 3;
A20: Sphere(p,r) c= cl_Ball(p,r) by TOPREAL9:17;
A21: Ball(p,r) misses Sphere(p,r) by TOPREAL9:19;
  consider e being Point of T2 such that
A22: e in Sphere(p,r) by SUBSET_1:10;
  not e in P by A6,A21,A22,XBOOLE_0:3;
  then e in P` by SUBSET_1:50;
  then reconsider F3 = P` /\ [#]D as non empty Subset of D
  by A14,A20,A22,XBOOLE_0:def 3;
  reconsider T1 = D|F1 as non empty SubSpace of D;
  reconsider T3 = D|F3 as non empty SubSpace of D;
A23: the carrier of T1 = F1 by PRE_TOPC:29;
A24: the carrier of T3 = F3 by PRE_TOPC:29;
A25: the carrier of T2B1 = B1 by PRE_TOPC:29;
A26: A c= B
  proof
    let a be set;
    assume a in A;
    then
A27: a in C by A2;
    then a in Ball(p,r) by A3;
    hence thesis by A11,A15,A17,A27,ZFMISC_1:64;
  end;
A28: F3 c= B
  proof
    let a be set;
    assume
A29: a in F3;
    then a in P` by XBOOLE_0:def 3;
    then not a in P by XBOOLE_0:def 4;
    hence thesis by A4,A9,A11,A14,A29,ZFMISC_1:64;
  end;
  f|F1 is Function of F1,A by A13,A16,FUNCT_2:38;
  then reconsider f1 = f|F1 as Function
  of T1,T2B1 by A16,A23,A25,A26,FUNCT_2:9;
  reconsider g1 = id F3 as Function of T3,T2B1 by A24,A25,A28,FUNCT_2:9;
A30: F1 = [#]T1 & F3 = [#]T3 by PRE_TOPC:29;
A31: [#]T1 \/ [#]T3 = [#]D
  proof
    thus [#]T1 \/ [#]T3 c= [#]D by A30,XBOOLE_1:8;
    let p be set;
    assume
A32: p in [#]D;
    per cases;
    suppose p in P;
      then p in F1 by A19,A32,XBOOLE_0:def 3;
      hence thesis by A30,XBOOLE_0:def 2;
    end;
    suppose not p in P;
      then p in P` by A14,A32,SUBSET_1:50;
      then p in F3 by A32,XBOOLE_0:def 3;
      hence thesis by A30,XBOOLE_0:def 2;
    end;
  end;
  reconsider DT = [#]D as closed Subset of T2 by BORSUK_1:def 14,TSEP_1:1;
  DT /\ Cl P is closed by TOPS_1:35;
  then
A33: F1 is closed by TSEP_1:8;
  P is_a_component_of C`
  proof
    take U;
    thus thesis by A9,A10;
  end;
  then P is open by SPRECT_3:18;
  then P` is closed by TOPS_1:30;
  then DT /\ P` is closed by TOPS_1:35;
  then
A34: F3 is closed by TSEP_1:8;
  reconsider f2 = f|F1 as Function of T1,T2|A1 by A23,FUNCT_2:38;
A35: T2|A1 is SubSpace of T2B1 by A13,A25,A26,TSEP_1:4;
  T3 is SubSpace of T2 by TSEP_1:7;
  then
A36: T3 is SubSpace of T2B1 by A24,A25,A28,TSEP_1:4;
  f2 is continuous by A7,TOPMETR:10;
  then
A37: f1 is continuous by A35,TOPMETR:7;
  reconsider g2 = id F3 as Function of T3,T3 by A24;
  g2 = id T3 by PRE_TOPC:29;
  then
A38: g1 is continuous by A36,TOPMETR:7;
A39: for x being set st x in Cl P & x in P` holds f.x = x
  proof
    let x be set;
    assume x in Cl P & x in P`;
    then
A40: x in Cl P /\ P` by XBOOLE_0:def 3;
    then (id A).x = x by A5,FUNCT_1:35;
    hence f.x = x by A5,A8,A40,FUNCT_1:72;
  end;
  for x being set st x in [#]T1 /\ [#]T3 holds f1.x = g1.x
  proof
    let x be set;
    assume
A41: x in [#]T1 /\ [#]T3;
    then
A42: x in [#]T1 & x in [#]T3 by XBOOLE_0:def 3;
    then x in Cl P & x in P` by A30,XBOOLE_0:def 3;
    then
A43: f.x = x by A39;
    thus f1.x = f.x by A30,A42,FUNCT_1:72
      .= g1.x by A30,A41,A43,FUNCT_1:35;
  end;
  then consider g being Function of D,T2|B such that
A44: g = f1+*g1 and
A45: g is continuous by A30,A31,A33,A34,A37,A38,JGRAPH_2:9;
  take g;
  thus g is continuous by A45;
  let x be Point of D;
A46: dom g1 = the carrier of T3 by FUNCT_2:def 1;
  hereby
    assume
A47: x in Cl P;
    then
A48: x in F1 by XBOOLE_0:def 3;
    per cases;
    suppose not x in dom g1;
      hence g.x = f1.x by A44,FUNCT_4:12
        .= f.x by A48,FUNCT_1:72;
    end;
    suppose
A49:  x in dom g1;
      then
A50:  x in Cl P & x in P` by A24,A47,XBOOLE_0:def 3;
      thus g.x = g1.x by A44,A49,FUNCT_4:14
        .= x by A24,A49,FUNCT_1:35
        .= f.x by A39,A50;
    end;
  end;
  assume x in P`;
  then
A51: x in F3 by XBOOLE_0:def 3;
  hence g.x = g1.x by A24,A44,A46,FUNCT_4:14
    .= x by A51,FUNCT_1:35;
end;

Lm16: for A being non empty Subset of T2 st U <> V
for r being positive (real number) st A c= C &
C c= Ball(p,r) & p in V & Cl P /\ P` c= A & Ball(p,r) meets P
for f being Function of Tdisk(p,r), (TOP-REAL 2)|A st f is continuous &
f|A = id A &

:: komponenty
U = P & U is_a_component_of (TOP-REAL 2)|C` &
V is_a_component_of T2|C` & B = cl_Ball(p,r) \ {p}
ex g being Function of Tdisk(p,r), (TOP-REAL 2)|B st g is continuous &
for x being Point of Tdisk(p,r) holds (x in Cl P implies g.x = x) &
(x in P` implies g.x = f.x)
proof
  let A be non empty Subset of T2 such that
A1: U <> V;
  let r be positive (real number);
  set D = Tdisk(p,r);
  assume that
A2: A c= C and
A3: C c= Ball(p,r) and
A4: p in V and
A5: Cl P /\ P` c= A and
A6: Ball(p,r) meets P;
  let f be Function of D, T2|A;
  assume that
A7: f is continuous and
A8: f|A = id A and
A9: U = P and
A10: U is_a_component_of T2|C` and
A11: V is_a_component_of T2|C` and
A12: B = cl_Ball(p,r) \ {p};
  reconsider B1 = B as non empty Subset of T2 by A12;
  reconsider T2B1 = T2|B1 as non empty SubSpace of T2;
A13: the carrier of T2|C` = C` by PRE_TOPC:29;
A14: the carrier of T2|A = A by PRE_TOPC:29;
A15: the carrier of D = cl_Ball(p,r) by BROUWER:3;
A16: Ball(p,r) c= cl_Ball(p,r) by TOPREAL9:16;
A17: not p in C by A4,A13,XBOOLE_0:def 4;
  |. p-p .| = 0 by TOPRNS_1:29;
  then
A18: p in [#]D by A15,TOPREAL9:8;
A19: P c= Cl P by PRE_TOPC:48;
  ex j being set st j in Ball(p,r) & j in P by A6,XBOOLE_0:3;
  then reconsider F1 = (Cl P) /\ [#]D as non empty Subset of D
  by A15,A16,A19,XBOOLE_0:def 3;
  now
    assume
A20: p in P;
    U misses V by A1,A10,A11,CONNSP_1:37;
    hence contradiction by A4,A9,A20,XBOOLE_0:3;
  end;
  then p in P` by SUBSET_1:50;
  then reconsider F3 = P` /\ [#]D as non empty Subset of D
  by A18,XBOOLE_0:def 3;
  set T1 = D|F1;
  set T3 = D|F3;
A21: the carrier of T1 = F1 by PRE_TOPC:29;
A22: the carrier of T3 = F3 by PRE_TOPC:29;
A23: the carrier of T2|B1 = B1 by PRE_TOPC:29;
A24: A c= B
  proof
    let a be set;
    assume a in A;
    then
A25: a in C by A2;
    then a in Ball(p,r) by A3;
    hence thesis by A12,A16,A17,A25,ZFMISC_1:64;
  end;
A26: F1 c= B
  proof
    let a be set;
    assume
A27: a in F1;
    then
A28: a in Cl P by XBOOLE_0:def 3;
    now
      assume
A29:  p in Cl P;
      Cl P misses V by A1,A9,A10,A11,Th68;
      hence contradiction by A4,A29,XBOOLE_0:3;
    end;
    hence thesis by A12,A15,A27,A28,ZFMISC_1:64;
  end;
  then reconsider f1 = id F1 as Function of T1,T2B1 by A21,A23,FUNCT_2:9;
  f|F3 is Function of F3,A by A14,FUNCT_2:38;
  then reconsider g1 = f|F3 as Function of T3,T2B1 by A22,A23,A24,FUNCT_2:9;
A30: F1 = [#]T1 & F3 = [#]T3 by PRE_TOPC:29;
A31: [#]T1 \/ [#]T3 = [#]D
  proof
    thus [#]T1 \/ [#]T3 c= [#]D by A30,XBOOLE_1:8;
    let p be set;
    assume
A32: p in [#]D;
    per cases;
    suppose p in P;
      then p in F1 by A19,A32,XBOOLE_0:def 3;
      hence thesis by A30,XBOOLE_0:def 2;
    end;
    suppose not p in P;
      then p in P` by A15,A32,SUBSET_1:50;
      then p in F3 by A32,XBOOLE_0:def 3;
      hence thesis by A30,XBOOLE_0:def 2;
    end;
  end;
  reconsider DT = [#]D as closed Subset of T2 by BORSUK_1:def 14,TSEP_1:1;
  DT /\ Cl P is closed by TOPS_1:35;
  then
A33: F1 is closed by TSEP_1:8;
  P is_a_component_of C`
  proof
    take U;
    thus thesis by A9,A10;
  end;
  then P is open by SPRECT_3:18;
  then P` is closed by TOPS_1:30;
  then DT /\ P` is closed by TOPS_1:35;
  then
A34: F3 is closed by TSEP_1:8;
A35: id T1 = id F1 by PRE_TOPC:29;
  T1 is SubSpace of T2 by TSEP_1:7;
  then T1 is SubSpace of T2B1 by A21,A23,A26,TSEP_1:4;
  then
A36: f1 is continuous by A35,TOPMETR:7;
A37: T2|A is SubSpace of T2B1 by A14,A23,A24,TSEP_1:4;
  reconsider g2 = g1 as Function of T3,T2|A by A22,FUNCT_2:38;
  g2 is continuous by A7,TOPMETR:10;
  then
A38: g1 is continuous by A37,TOPMETR:7;
A39: for x being set st x in Cl P & x in P` holds f.x = x
  proof
    let x be set;
    assume x in Cl P & x in P`;
    then
A40: x in Cl P /\ P` by XBOOLE_0:def 3;
    then (id A).x = x by A5,FUNCT_1:35;
    hence f.x = x by A5,A8,A40,FUNCT_1:72;
  end;
  for x being set st x in [#]T1 /\ [#]T3 holds f1.x = g1.x
  proof
    let x be set;
    assume
A41: x in [#]T1 /\ [#]T3;
    then
A42: x in [#]T1 & x in [#]T3 by XBOOLE_0:def 3;
    then x in Cl P & x in P` by A30,XBOOLE_0:def 3;
    then
A43: f.x = x by A39;
    thus f1.x = x by A30,A42,FUNCT_1:35
      .= g1.x by A30,A41,A43,FUNCT_1:72;
  end;
  then consider g being Function of D,T2|B such that
A44: g = f1+*g1 and
A45: g is continuous by A30,A31,A33,A34,A36,A38,JGRAPH_2:9;
  take g;
  thus g is continuous by A45;
  let x be Point of D;
A46: dom g1 = the carrier of T3 by FUNCT_2:def 1;
  hereby
    assume
A47: x in Cl P;
    then
A48: x in F1 by XBOOLE_0:def 3;
    per cases;
    suppose not x in dom g1;
      hence g.x = f1.x by A44,FUNCT_4:12
        .= x by A48,FUNCT_1:35;
    end;
    suppose
A49:  x in dom g1;
      then
A50:  x in Cl P & x in P` by A22,A47,XBOOLE_0:def 3;
      thus g.x = g1.x by A44,A49,FUNCT_4:14
        .= f.x by A22,A49,FUNCT_1:72
        .= x by A39,A50;
    end;
  end;
  assume x in P`;
  then
A51: x in F3 by XBOOLE_0:def 3;
  hence g.x = g1.x by A22,A44,A46,FUNCT_4:14
    .= f.x by A51,FUNCT_1:72;
end;

Lm17: BDD C is non empty &
U = P & U is_a_component_of (TOP-REAL 2)|C` implies C = Fr P
proof
  assume that
A1: BDD C is non empty and
A2: U = P and
A3: U is_a_component_of T2|C` and
A4: C <> Fr P;
A5: the carrier of T2|C` = C` by PRE_TOPC:29;
  reconsider T2C = T2|C` as non empty SubSpace of T2;
A6: T2C is locally_connected by JORDAN2C:89;
  then U is open by A3,CONNSP_2:21;
  then reconsider P as open Subset of T2 by A2,TSEP_1:17;
A7: Fr P = Cl P /\ P` by PRE_TOPC:52;
  set Z = {X where X is Subset of T2|C`: X is_a_component_of T2|C` & X <> U};
  set V = union Z;
A8: V \/ U \/ C = the carrier of T2
  proof
A9: V c= the carrier of T2
    proof
      let a be set;
      assume a in V;
      then consider A being set such that
A10:  a in A and
A11:  A in Z by TARSKI:def 4;
      ex X being Subset of T2|C` st X = A & X is_a_component_of T2|C` &
      X <> U by A11;
      hence thesis by A5,A10,TARSKI:def 3;
    end;
    U c= the carrier of T2 by A5,XBOOLE_1:1;
    then V \/ U c= the carrier of T2 by A9,XBOOLE_1:8;
    hence V \/ U \/ C c= the carrier of T2 by XBOOLE_1:8;
    let a be set;
    assume
A12: a in the carrier of T2;
    per cases;
    suppose a in C;
      hence thesis by XBOOLE_0:def 2;
    end;
    suppose not a in C;
      then reconsider a as Point of T2|C` by A5,A12,SUBSET_1:50;
A13:  a in Component_of a by CONNSP_1:40;
      per cases;
      suppose Component_of a = U;
        then a in V \/ U by A13,XBOOLE_0:def 2;
        hence thesis by XBOOLE_0:def 2;
      end;
      suppose
A14:    Component_of a <> U;
        Component_of a is_a_component_of T2|C` by CONNSP_1:43;
        then Component_of a in Z by A14;
        then a in V by A13,TARSKI:def 4;
        then a in V \/ U by XBOOLE_0:def 2;
        hence thesis by XBOOLE_0:def 2;
      end;
    end;
  end;
A15: P misses P` by XBOOLE_1:79;
  Fr P c= C
  proof
    let a be set;
    assume
A16: a in Fr P;
    then
A17: a in Cl P & a in P` by A7,XBOOLE_0:def 3;
    assume not a in C;
    then a in V \/ U by A8,A16,XBOOLE_0:def 2;
    then a in V or a in U by XBOOLE_0:def 2;
    then consider O being set such that
A18: a in O and
A19: O in Z by A2,A15,A17,TARSKI:def 4,XBOOLE_0:3;
    consider X being Subset of T2|C` such that
A20: X = O and
A21: X is_a_component_of T2|C` and
A22: X <> U by A19;
    Cl P misses X by A2,A3,A21,A22,Th68;
    hence thesis by A17,A18,A20,XBOOLE_0:3;
  end;
  then Fr P c< C by A4,XBOOLE_0:def 8;
  then consider p1, p2, A such that
A23: A is_an_arc_of p1,p2 and
A24: Fr P c= A and
A25: A c= C by BORSUK_4:84;
A26: U <> {}(T2|C`) by A3,CONNSP_1:34;
  per cases;
  suppose P is Bounded;
    then reconsider P as Bounded Subset of T2;
    consider p being set such that
A27: p in U by A26,XBOOLE_0:def 1;
    reconsider p as Point of T2 by A2,A27;
A28: P \/ C is Bounded by TOPREAL6:76;
    then reconsider PC = P \/ C as bounded Subset of Euclid 2
    by JORDAN2C:def 2;
    consider r being positive (real number) such that
A29: PC c= Ball(p,r) by A28,Th26;
    C c= PC by XBOOLE_1:7;
    then
A30: C c= Ball(p,r) by A29,XBOOLE_1:1;
    set D = Tdisk(p,r);
    set S = Tcircle(p,r);
    set B = cl_Ball(p,r) \ {p};
A31: the carrier of S = Sphere(p,r) by TOPREALB:9;
A32: the carrier of D = cl_Ball(p,r) by BROUWER:3;
A33: Sphere(p,r) c= cl_Ball(p,r) by TOPREAL9:17;
A34: Ball(p,r) misses Sphere(p,r) by TOPREAL9:19;
A35: Ball(p,r) c= cl_Ball(p,r) by TOPREAL9:16;
    A c= Ball(p,r) by A25,A30,XBOOLE_1:1;
    then A is Subset of D by A32,A35,XBOOLE_1:1;
    then consider R being Function of D, T2|A such that
A36: R is continuous and
A37: R|A = id A by A23,Lm14;
    P c= PC by XBOOLE_1:7;
    then
A38: P c= Ball(p,r) by A29,XBOOLE_1:1;
    then consider f being Function of D, T2|B such that
A39: f is continuous and
A40: for x being Point of D holds (x in Cl P implies f.x = R.x) &
    (x in P` implies f.x = x) by A2,A3,A7,A23,A24,A25,A27,A30,A36,A37,Lm15;
    set g = DiskProj(p,r,p);
    set h = RotateCircle(p,r,p);
A41: S is SubSpace of D by A31,A32,A33,TSEP_1:4;
    reconsider F = h*(g*f) as Function of D,D by A31,A32,A33,FUNCT_2:9;
    p is Point of D by Th17;
    then g is continuous by Th64;
    then
A42: g*f is continuous by A39,TOPS_2:58;
    |. p-p .| = 0 by TOPRNS_1:29;
    then
A43: p in Ball(p,r) by TOPREAL9:7;
    then h is continuous by Th66;
    then h*(g*f) is continuous by A42,TOPS_2:58;
    then
A44: F is continuous by A41,TOPMETR:7;
    now
      let x be set;
      per cases;
      suppose
A45:    x in dom F;
A46:    Ball(p,r) \/ Sphere(p,r) = cl_Ball(p,r) by TOPREAL9:18;
        now per cases by A32,A45,A46,XBOOLE_0:def 2;
          suppose
A47:        x in Ball(p,r);
            F.x in the carrier of S by A45,FUNCT_2:7;
            hence F.x <> x by A31,A34,A47,XBOOLE_0:3;
          end;
          suppose
A48:        x in Sphere(p,r);
A49:        dom f = the carrier of D by FUNCT_2:def 1;
            not x in P by A34,A38,A48,XBOOLE_0:3;
            then
A50:        x in P` by A48,SUBSET_1:50;
A51:        g|Sphere(p,r) = id Sphere(p,r) by A43,Th65;
            h has_no_fixpoint by A43,Th67;
            then
A52:        not x is_a_fixpoint_of h by ABIAN:def 5;
A53:        dom h = the carrier of S by FUNCT_2:def 1;
            F.x = h.((g*f).x) by A45,FUNCT_1:22
              .= h.(g.(f.x)) by A32,A33,A48,A49,FUNCT_1:23
              .= h.(g.x) by A32,A33,A40,A48,A50
              .= h.((id Sphere(p,r)).x) by A48,A51,FUNCT_1:72
              .= h.x by A48,FUNCT_1:35;
            hence F.x <> x by A31,A48,A52,A53,ABIAN:def 3;
          end;
        end;
        hence not x is_a_fixpoint_of F by ABIAN:def 3;
      end;
      suppose not x in dom F;
        hence not x is_a_fixpoint_of F by ABIAN:def 3;
      end;
    end;
    then not F has_a_fixpoint by ABIAN:def 5;
    hence thesis by A44,BROUWER:14;
  end;
  suppose
A54: not P is Bounded;
    consider p being set such that
A55: p in BDD C by A1,XBOOLE_0:def 1;
    consider Z being set such that
A56: p in Z and
A57: Z in {B where B is Subset of T2: B is_inside_component_of C}
    by A55,TARSKI:def 4;
    consider P1 being Subset of T2 such that
A58: Z = P1 and
A59: P1 is_inside_component_of C by A57;
    consider U1 being Subset of T2|C` such that
A60: U1 = P1 & U1 is_a_component_of T2|C` and
    U1 is bounded Subset of Euclid 2 by A59,JORDAN2C:17;
    U1 is open by A6,A60,CONNSP_2:21;
    then reconsider P1 as non empty open Bounded Subset of T2
    by A56,A58,A59,A60,JORDAN2C:def 3,TSEP_1:17;
    reconsider p as Point of T2 by A56,A58;
A61: p in P1 by A56,A58;
A62: P1 \/ C is Bounded by TOPREAL6:76;
    then reconsider PC = P1 \/ C as bounded Subset of Euclid 2
    by JORDAN2C:def 2;
    consider rv being positive (real number) such that
A63: PC c= Ball(p,rv) by A62,Th26;
    not P c= Ball(p,rv) by A54,JORDAN2C:16;
    then consider u being set such that
A64: u in P and
A65: not u in Ball(p,rv) by TARSKI:def 3;
    reconsider u as Point of T2 by A64;
    set r = |.u-p.|;
    P misses P1 by A2,A3,A54,A60,CONNSP_1:37;
    then p <> u by A56,A58,A64,XBOOLE_0:3;
    then reconsider r as non empty non negative (real number) by TOPRNS_1:29;
A66: r >= rv by A65,TOPREAL9:7;
    then Ball(p,rv) c= Ball(p,r) by Th18;
    then
A67: PC c= Ball(p,r) by A63,XBOOLE_1:1;
A68: Fr Ball(p,r) = Sphere(p,r) by Th24;
    u in Sphere(p,r) by TOPREAL9:9;
    then
A69: P meets Ball(p,r) by A64,A68,TOPS_1:61;
A70: C c= PC by XBOOLE_1:7;
    then
A71: C c= Ball(p,r) by A67,XBOOLE_1:1;
    set D = Tdisk(p,r);
    set S = Tcircle(p,r);
    set B = cl_Ball(p,r) \ {p};
A72: the carrier of S = Sphere(p,r) by TOPREALB:9;
A73: the carrier of D = cl_Ball(p,r) by BROUWER:3;
A74: Sphere(p,r) c= cl_Ball(p,r) by TOPREAL9:17;
A75: Ball(p,r) misses Sphere(p,r) by TOPREAL9:19;
A76: Ball(p,r) c= cl_Ball(p,r) by TOPREAL9:16;
    A c= Ball(p,r) by A25,A71,XBOOLE_1:1;
    then A is Subset of D by A73,A76,XBOOLE_1:1;
    then consider R being Function of D, T2|A such that
A77: R is continuous and
A78: R|A = id A by A23,Lm14;
    p1 in A by A23,TOPREAL1:4;
    then consider f being Function of D, T2|B such that
A79: f is continuous and
A80: for x being Point of D holds (x in Cl P implies f.x = x) &
    (x in P` implies f.x = R.x)
    by A2,A3,A7,A24,A25,A54,A60,A61,A69,A71,A77,A78,Lm16;
    set g = DiskProj(p,r,p);
    set h = RotateCircle(p,r,p);
A81: S is SubSpace of D by A72,A73,A74,TSEP_1:4;
    reconsider F = h*(g*f) as Function of D,D by A72,A73,A74,FUNCT_2:9;
    p is Point of D by Th17;
    then g is continuous by Th64;
    then
A82: g*f is continuous by A79,TOPS_2:58;
    |. p-p .| = 0 by TOPRNS_1:29;
    then
A83: p in Ball(p,r) by TOPREAL9:7;
    then h is continuous by Th66;
    then h*(g*f) is continuous by A82,TOPS_2:58;
    then
A84: F is continuous by A81,TOPMETR:7;
    now
      let x be set;
      per cases;
      suppose
A85:    x in dom F;
A86:    Ball(p,r) \/ Sphere(p,r) = cl_Ball(p,r) by TOPREAL9:18;
        now per cases by A73,A85,A86,XBOOLE_0:def 2;
          suppose
A87:        x in Ball(p,r);
            F.x in the carrier of S by A85,FUNCT_2:7;
            hence F.x <> x by A72,A75,A87,XBOOLE_0:3;
          end;
          suppose
A88:        x in Sphere(p,r);
A89:        dom f = the carrier of D by FUNCT_2:def 1;
A90:        P c= Cl P by PRE_TOPC:48;
            set SS = Sphere(p,r);
            SS c= C`
            proof
              let a be set;
              assume
A91:          a in SS;
              assume not a in C`;
              then
A92:          a in C by A91,SUBSET_1:50;
              reconsider a as Point of T2 by A91;
              a in PC by A70,A92;
              then |.a-p.| < rv by A63,TOPREAL9:7;
              hence contradiction by A66,A91,TOPREAL9:9;
            end;
            then reconsider SS as Subset of T2|C` by PRE_TOPC:29;
A93:        u in SS by TOPREAL9:9;
            SS is connected by CONNSP_1:24;
            then SS misses U or SS c= U by A3,CONNSP_1:38;
            then
A94:        x in P by A2,A64,A88,A93,XBOOLE_0:3;
A95:        g|Sphere(p,r) = id Sphere(p,r) by A83,Th65;
            h has_no_fixpoint by A83,Th67;
            then
A96:        not x is_a_fixpoint_of h by ABIAN:def 5;
A97:        dom h = the carrier of S by FUNCT_2:def 1;
            F.x = h.((g*f).x) by A85,FUNCT_1:22
              .= h.(g.(f.x)) by A73,A74,A88,A89,FUNCT_1:23
              .= h.(g.x) by A73,A74,A80,A88,A90,A94
              .= h.((id Sphere(p,r)).x) by A88,A95,FUNCT_1:72
              .= h.x by A88,FUNCT_1:35;
            hence F.x <> x by A72,A88,A96,A97,ABIAN:def 3;
          end;
        end;
        hence not x is_a_fixpoint_of F by ABIAN:def 3;
      end;
      suppose not x in dom F;
        hence not x is_a_fixpoint_of F by ABIAN:def 3;
      end;
    end;
    then not F has_a_fixpoint by ABIAN:def 5;
    hence thesis by A84,BROUWER:14;
  end;
end;
set rp = 1;
set rl = -rp;
set rg = 3;
set rd = -rg;
set a = |[rl,0]|;
set b = |[rp,0]|;
set c = |[0,rg]|;
set d = |[0,rd]|;
set lg = |[rl,rg]|;
set pg = |[rp,rg]|;
set ld = |[rl,rd]|;
set pd = |[rp,rd]|;
set R = closed_inside_of_rectangle(rl,rp,rd,rg);
set dR = rectangle(rl,rp,rd,rg);
set TR = Trectangle(rl,rp,rd,rg);

Lm18: a`1 = rl by EUCLID:56;

Lm19: b`1 = rp by EUCLID:56;

Lm20: a`2 = 0 by EUCLID:56;

Lm21: b`2 = 0 by EUCLID:56;

Lm22: c`1 = 0 by EUCLID:56;

Lm23: c`2 = rg by EUCLID:56;

Lm24: d`1 = 0 by EUCLID:56;

Lm25: d`2 = rd by EUCLID:56;

Lm26: lg`1 = rl by EUCLID:56;

Lm27: lg`2 = rg by EUCLID:56;

Lm28: ld`1 = rl by EUCLID:56;

Lm29: ld`2 = rd by EUCLID:56;

Lm30: pg`1 = rp by EUCLID:56;

Lm31: pg`2 = rg by EUCLID:56;

Lm32: pd`1 = rp by EUCLID:56;

Lm33: pd`2 = rd by EUCLID:56;

Lm34: ld = |[ld`1,ld`2]| by EUCLID:57;

Lm35: lg = |[lg`1,lg`2]| by EUCLID:57;

Lm36: pd = |[pd`1,pd`2]| by EUCLID:57;

Lm37: pg = |[pg`1,pg`2]| by EUCLID:57;

Lm38: dR = (LSeg(ld,lg) \/ LSeg(lg,pg)) \/ (LSeg(pg,pd) \/ LSeg(pd,ld))
by SPPOL_2:def 3;
then LSeg(ld,lg) c= LSeg(ld,lg) \/ LSeg(lg,pg) &
LSeg(ld,lg) \/ LSeg(lg,pg) c= dR by XBOOLE_1:7;
then

Lm39: LSeg(ld,lg) c= dR by XBOOLE_1:1;
LSeg(lg,pg) c= LSeg(ld,lg) \/ LSeg(lg,pg) &
LSeg(ld,lg) \/ LSeg(lg,pg) c= dR by Lm38,XBOOLE_1:7;
then

Lm40: LSeg(lg,pg) c= dR by XBOOLE_1:1;
LSeg(pg,pd) c= LSeg(pg,pd) \/ LSeg(pd,ld) &
LSeg(pg,pd) \/ LSeg(pd,ld) c= dR by Lm38,XBOOLE_1:7;
then

Lm41: LSeg(pg,pd) c= dR by XBOOLE_1:1;
LSeg(pd,ld) c= LSeg(pg,pd) \/ LSeg(pd,ld) &
LSeg(pg,pd) \/ LSeg(pd,ld) c= dR by Lm38,XBOOLE_1:7;
then

Lm42: LSeg(pd,ld) c= dR by XBOOLE_1:1;

Lm43: LSeg(ld,lg) is vertical by Lm26,Lm28,SPPOL_1:37;

Lm44: LSeg(pd,pg) is vertical by Lm30,Lm32,SPPOL_1:37;

Lm45: LSeg(a,lg) is vertical by Lm18,Lm26,SPPOL_1:37;

Lm46: LSeg(a,ld) is vertical by Lm18,Lm28,SPPOL_1:37;

Lm47: LSeg(b,pg) is vertical by Lm19,Lm30,SPPOL_1:37;

Lm48: LSeg(b,pd) is vertical by Lm19,Lm32,SPPOL_1:37;

Lm49: LSeg(ld,d) is horizontal by Lm25,Lm29,SPPOL_1:36;

Lm50: LSeg(pd,d) is horizontal by Lm25,Lm33,SPPOL_1:36;

Lm51: LSeg(lg,c) is horizontal by Lm23,Lm27,SPPOL_1:36;

Lm52: LSeg(pg,c) is horizontal by Lm23,Lm31,SPPOL_1:36;

Lm53: LSeg(lg,pg) is horizontal by Lm27,Lm31,SPPOL_1:36;

Lm54: LSeg(ld,pd) is horizontal by Lm29,Lm33,SPPOL_1:36;

Lm55: LSeg(a,lg) c= LSeg(ld,lg)
by Lm18,Lm20,Lm27,Lm28,Lm29,Lm43,Lm45,GOBOARD7:65;

Lm56: LSeg(a,ld) c= LSeg(ld,lg) by Lm20,Lm27,Lm28,Lm29,Lm43,Lm46,GOBOARD7:65;

Lm57: LSeg(b,pg) c= LSeg(pd,pg)
by Lm19,Lm21,Lm31,Lm32,Lm33,Lm44,Lm47,GOBOARD7:65;

Lm58: LSeg(b,pd) c= LSeg(pd,pg) by Lm21,Lm31,Lm32,Lm33,Lm44,Lm48,GOBOARD7:65;

Lm59: dR = {p where p is Point of T2: p`1 = rl & p`2 <= rg & p`2 >= rd or
p`1 <= rp & p`1 >= rl & p`2 = rg or p`1 <= rp & p`1 >= rl & p`2 = rd or
p`1 = rp & p`2 <= rg & p`2 >= rd} by SPPOL_2:58;
then

Lm60: c in dR by Lm22,Lm23;

Lm61: d in dR by Lm24,Lm25,Lm59;

Lm62: (2+1)^2 = 4 + 4 + 1;
then

Lm63: sqrt(9) = 3 by SQUARE_1:def 4;

Lm64: dist(a,b) = sqrt ((a`1-b`1)^2 + (a`2-b`2)^2) by TOPREAL6:101
  .= --2 by Lm18,Lm19,Lm20,Lm21,SQUARE_1:90;

theorem Th70:
  for h being Homeomorphism of TOP-REAL 2 holds
  h.:C is being_simple_closed_curve
proof
  let h be Homeomorphism of T2;
  consider f being Function of T2|R^2-unit_square, T2|C such that
A1: f is_homeomorphism by TOPREAL2:def 1;
  reconsider g = h|C as Function of T2|C,T2|(h.:C) by JORDAN24:12;
  take g*f;
  g is_homeomorphism by JORDAN24:14;
  hence g*f is_homeomorphism by A1,TOPS_2:71;
end;

theorem Th71:
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies
  P c= closed_inside_of_rectangle(-1,1,-3,3)
proof
  assume that
A1: a in P and
A2: b in P and
A3: for x, y being Point of TOP-REAL 2 st x in P & y in P holds
  dist(a,b) >= dist(x,y);
  let p be set;
  assume
A4: p in P;
  then reconsider p as Point of TOP-REAL 2;
A5: dist(a,p) = sqrt((rl-p`1)^2 + (0-p`2)^2) by Lm18,Lm20,TOPREAL6:101
    .= sqrt((rl-p`1)^2 + p`2^2);
A6: now
    assume 9 < p`2^2;
    then 0+9 < (rl-p`1)^2+p`2^2 by XREAL_1:10;
    then 3 < sqrt((rl-p`1)^2+p`2^2) by Lm63,SQUARE_1:95;
    then 2 < sqrt((rl-p`1)^2+p`2^2) by XXREAL_0:2;
    hence contradiction by A1,A3,A4,A5,Lm64;
  end;
A7: now
    assume
A8: rl > p`1;
    then LSeg(p,b) meets Vertical_Line(rl) by Lm19,Th8;
    then consider x being set such that
A9: x in LSeg(p,b) and
A10: x in Vertical_Line(rl) by XBOOLE_0:3;
    reconsider x as Point of T2 by A9;
A11: x`1 = rl by A10,JORDAN6:34;
A12: dist(p,b) = dist(p,x)+dist(x,b) by A9,JORDAN1K:29;
A13: dist(x,b) = sqrt((x`1-b`1)^2 + (x`2-b`2)^2) by TOPREAL6:101
      .= sqrt((-2)^2 + (x`2-0)^2) by A11,Lm19,EUCLID:56
      .= sqrt(4 + x`2^2);
    now
      assume dist(x,b) < dist(a,b);
      then 4 + x`2^2 < 4 + 0 by A13,Lm64,SQUARE_1:85,94;
      hence contradiction by XREAL_1:8;
    end;
    then dist(p,b) + 0 > dist(a,b) + 0 by A8,A11,A12,JORDAN1K:22,XREAL_1:10;
    hence contradiction by A2,A3,A4;
  end;
A14: now
    assume
A15: p`1 > rp;
    then LSeg(p,a) meets Vertical_Line(rp) by Lm18,Th8;
    then consider x being set such that
A16: x in LSeg(p,a) and
A17: x in Vertical_Line(rp) by XBOOLE_0:3;
    reconsider x as Point of T2 by A16;
A18: x`1 = rp by A17,JORDAN6:34;
A19: dist(p,a) = dist(p,x)+dist(x,a) by A16,JORDAN1K:29;
A20: dist(x,a) = sqrt((x`1-a`1)^2 + (x`2-a`2)^2) by TOPREAL6:101
      .= sqrt(4 + x`2^2) by A18,Lm18,Lm20;
    now
      assume dist(x,a) < dist(a,b);
      then 4 + x`2^2 < 4 + 0 by A20,Lm64,SQUARE_1:85,94;
      hence contradiction by XREAL_1:8;
    end;
    then dist(p,a) + 0 > dist(a,b) + 0 by A15,A18,A19,JORDAN1K:22,XREAL_1:10;
    hence contradiction by A1,A3,A4;
  end;
A21: now
    assume rd > p`2;
    then p`2^2 > rd^2 by SQUARE_1:114;
    hence contradiction by A6;
  end;
  rg >= p`2 by A6,Lm62,SQUARE_1:78;
  hence thesis by A7,A14,A21;
end;

Lm65: dR c= R by Th45;
lg`2 = lg`2 & lg`1 <= c`1 & c`1 <= pg`1 by Lm26,Lm30,EUCLID:56;
then LSeg(lg,c) c= LSeg(lg,pg) by Lm51,Lm53,GOBOARD7:66;
then

Lm66: LSeg(lg,c) c= dR by Lm40,XBOOLE_1:1;
LSeg(pg,c) c= LSeg(lg,pg) by Lm22,Lm23,Lm26,Lm27,Lm30,Lm52,Lm53,GOBOARD7:66;
then

Lm67: LSeg(pg,c) c= dR by Lm40,XBOOLE_1:1;
ld`2 = ld`2 & ld`1 <= d`1 & d`1 <= pd`1 by Lm28,Lm32,EUCLID:56;
then LSeg(ld,d) c= LSeg(ld,pd) by Lm49,Lm54,GOBOARD7:66;
then

Lm68: LSeg(ld,d) c= dR by Lm42,XBOOLE_1:1;
LSeg(pd,d) c= LSeg(ld,pd) by Lm24,Lm25,Lm28,Lm29,Lm32,Lm50,Lm54,GOBOARD7:66;
then

Lm69: LSeg(pd,d) c= dR by Lm42,XBOOLE_1:1;

Lm70: 0 <= p`2 & p in dR implies p in LSeg(a,lg) or p in LSeg(lg,c) or
p in LSeg(c,pg) or p in LSeg(pg,b)
proof
  assume
A1: 0 <= p`2;
  assume p in dR;
  then consider p1 such that
A2: p1 = p and
A3: p1`1 = rl & p1`2 <= rg & p1`2 >= rd or
  p1`1 <= rp & p1`1 >= rl & p1`2 = rg or
  p1`1 <= rp & p1`1 >= rl & p1`2 = rd or
  p1`1 = rp & p1`2 <= rg & p1`2 >= rd by Lm59;
  per cases by A3;
  suppose p1`1 = rl & p1`2 <= rg & p1`2 >= rd;
    hence thesis by A1,A2,Lm18,Lm20,Lm26,Lm27,GOBOARD7:8;
  end;
  suppose
A4: p1`1 <= rp & p1`1 >= rl & p1`2 = rg;
    per cases;
    suppose p1`1 <= c`1;
      hence thesis by A2,A4,Lm23,Lm26,Lm27,GOBOARD7:9;
    end;
    suppose c`1 <= p1`1;
      hence thesis by A2,A4,Lm23,Lm30,Lm31,GOBOARD7:9;
    end;
  end;
  suppose p1`1 <= rp & p1`1 >= rl & p1`2 = rd;
    hence thesis by A1,A2;
  end;
  suppose p1`1 = rp & p1`2 <= rg & p1`2 >= rd;
    hence thesis by A1,A2,Lm19,Lm21,Lm30,Lm31,GOBOARD7:8;
  end;
end;

Lm71: p`2 <= 0 & p in dR implies p in LSeg(a,ld) or p in LSeg(ld,d) or
p in LSeg(d,pd) or p in LSeg(pd,b)
proof
  assume
A1: p`2 <= 0;
  assume p in dR;
  then consider p1 such that
A2: p1 = p and
A3: p1`1 = rl & p1`2 <= rg & p1`2 >= rd or
  p1`1 <= rp & p1`1 >= rl & p1`2 = rg or
  p1`1 <= rp & p1`1 >= rl & p1`2 = rd or
  p1`1 = rp & p1`2 <= rg & p1`2 >= rd by Lm59;
  per cases by A3;
  suppose p1`1 = rl & p1`2 <= rg & p1`2 >= rd;
    hence thesis by A1,A2,Lm18,Lm20,Lm28,Lm29,GOBOARD7:8;
  end;
  suppose p1`1 <= rp & p1`1 >= rl & p1`2 = rg;
    hence thesis by A1,A2;
  end;
  suppose
A4: p1`1 <= rp & p1`1 >= rl & p1`2 = rd;
    per cases;
    suppose p1`1 <= d`1;
      hence thesis by A2,A4,Lm25,Lm28,Lm29,GOBOARD7:9;
    end;
    suppose d`1 <= p1`1;
      hence thesis by A2,A4,Lm25,Lm32,Lm33,GOBOARD7:9;
    end;
  end;
  suppose p1`1 = rp & p1`2 <= rg & p1`2 >= rd;
    hence thesis by A1,A2,Lm19,Lm21,Lm32,Lm33,GOBOARD7:8;
  end;
end;

theorem Th72:
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies
  P misses LSeg(|[-1,3]|,|[1,3]|)
proof
  assume
A1: a,b realize-max-dist-in P;
  assume P meets LSeg(lg,pg);
  then consider x being set such that
A2: x in P and
A3: x in LSeg(lg,pg) by XBOOLE_0:3;
  reconsider x as Point of T2 by A2;
  lg in LSeg(lg,pg) by TOPREAL1:6;
  then
A4: x`2 = rg by A3,Lm27,Lm53,SPPOL_1:def 2;
A5: a in P by A1,JORDAN24:def 1;
A6: dist(a,x) = sqrt ((a`1-x`1)^2 + (a`2-x`2)^2) by TOPREAL6:101
    .= sqrt ((rl-x`1)^2 + rg^2) by A4,Lm20,EUCLID:56;
  0+4 < (rl-x`1)^2+9 by XREAL_1:10;
  then 2 < dist(a,x) by A6,SQUARE_1:85,95;
  hence thesis by A1,A2,A5,Lm64,JORDAN24:def 1;
end;

theorem Th73:
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies
  P misses LSeg(|[-1,-3]|,|[1,-3]|)
proof
  assume
A1: a,b realize-max-dist-in P;
  assume P meets LSeg(ld,pd);
  then consider x being set such that
A2: x in P and
A3: x in LSeg(ld,pd) by XBOOLE_0:3;
  reconsider x as Point of T2 by A2;
  ld in LSeg(ld,pd) by TOPREAL1:6;
  then
A4: x`2 = rd by A3,Lm29,Lm54,SPPOL_1:def 2;
A5: a in P by A1,JORDAN24:def 1;
A6: dist(a,x) = sqrt ((a`1-x`1)^2 + (a`2-x`2)^2) by TOPREAL6:101
    .= sqrt ((rl-x`1)^2 + (-rd)^2) by A4,Lm20,EUCLID:56;
  0+4 < (rl-x`1)^2+9 by XREAL_1:10;
  then 2 < dist(a,x) by A6,SQUARE_1:85,95;
  hence thesis by A1,A2,A5,Lm64,JORDAN24:def 1;
end;

theorem Th74:
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies
  P /\ rectangle(-1,1,-3,3) = {|[-1,0]|,|[1,0]|}
proof
  assume
A1: a,b realize-max-dist-in P;
  then
A2: a in P & b in P by JORDAN24:def 1;
  thus P /\ dR c= {a,b}
  proof
    let x be set;
    assume
A3: x in P /\ dR;
    then
A4: x in P by XBOOLE_0:def 3;
    x in dR by A3,XBOOLE_0:def 3;
    then
A5: x in LSeg(ld,lg) \/ LSeg(lg,pg) or
    x in LSeg(pg,pd) \/ LSeg(pd,ld) by Lm38,XBOOLE_0:def 2;
    reconsider x as Point of T2 by A3;
    per cases by A5,XBOOLE_0:def 2;
    suppose
A6:   x in LSeg(ld,lg);
      ld in LSeg(ld,lg) by TOPREAL1:6;
      then
A7:   x`1 = rl by A6,Lm28,Lm43,SPPOL_1:def 3;
      per cases;
      suppose x`2 = 0;
        then x = a by A7,Lm18,Lm20,TOPREAL3:11;
        hence thesis by TARSKI:def 2;
      end;
      suppose x`2 <> 0;
        then
A8:     x`2^2 > 0 by SQUARE_1:74;
A9:     dist(b,x) = sqrt ((rp-rl)^2 + (0-x`2)^2) by A7,Lm19,Lm21,TOPREAL6:101
          .= sqrt (4 + x`2^2);
        0+4 < x`2^2+4 by A8,XREAL_1:8;
        then 2 < sqrt(x`2^2+4) by SQUARE_1:85,95;
        hence thesis by A1,A2,A4,A9,Lm64,JORDAN24:def 1;
      end;
    end;
    suppose x in LSeg(lg,pg);
      then LSeg(lg,pg) meets P by A4,XBOOLE_0:3;
      hence thesis by A1,Th72;
    end;
    suppose
A10:  x in LSeg(pg,pd);
      pd in LSeg(pd,pg) by TOPREAL1:6;
      then
A11:  x`1 = rp by A10,Lm32,Lm44,SPPOL_1:def 3;
      per cases;
      suppose x`2 = 0;
        then x = b by A11,Lm19,Lm21,TOPREAL3:11;
        hence thesis by TARSKI:def 2;
      end;
      suppose x`2 <> 0;
        then
A12:    x`2^2 > 0 by SQUARE_1:74;
A13:    dist(x,a) = sqrt ((x`1-a`1)^2 + (x`2-a`2)^2) by TOPREAL6:101
          .= sqrt (4 + x`2^2) by A11,Lm18,Lm20;
        0+4 < x`2^2+4 by A12,XREAL_1:8;
        then 2 < sqrt(x`2^2+4) by SQUARE_1:85,95;
        hence thesis by A1,A2,A4,A13,Lm64,JORDAN24:def 1;
      end;
    end;
    suppose x in LSeg(pd,ld);
      then LSeg(pd,ld) meets P by A4,XBOOLE_0:3;
      hence thesis by A1,Th73;
    end;
  end;
  let x be set;
  assume x in {a,b};
  then
A14: x = a or x = b by TARSKI:def 2;
  a in dR & b in dR by Lm18,Lm19,Lm20,Lm21,Lm59;
  hence thesis by A2,A14,XBOOLE_0:def 3;
end;

Lm72: |[-1,0]|,|[1,0]| realize-max-dist-in C implies LSeg(lg,c) misses C
proof
  assume a,b realize-max-dist-in C;
  then
A1: C /\ dR = {a,b} by Th74;
  assume LSeg(lg,c) meets C;
  then consider q being set such that
A2: q in LSeg(lg,c) and
A3: q in C by XBOOLE_0:3;
  reconsider q as Point of T2 by A3;
  q in dR /\ C by A2,A3,Lm66,XBOOLE_0:def 3;
  then q = a or q = b by A1,TARSKI:def 2;
  hence contradiction by A2,Lm20,Lm21,TOPREAL3:18;
end;

Lm73: |[-1,0]|,|[1,0]| realize-max-dist-in C implies LSeg(pg,c) misses C
proof
  assume a,b realize-max-dist-in C;
  then
A1: C /\ dR = {a,b} by Th74;
  assume LSeg(pg,c) meets C;
  then consider q being set such that
A2: q in LSeg(pg,c) and
A3: q in C by XBOOLE_0:3;
  reconsider q as Point of T2 by A3;
  q in dR /\ C by A2,A3,Lm67,XBOOLE_0:def 3;
  then q = a or q = b by A1,TARSKI:def 2;
  hence contradiction by A2,Lm20,Lm21,TOPREAL3:18;
end;

Lm74: |[-1,0]|,|[1,0]| realize-max-dist-in C implies LSeg(ld,d) misses C
proof
  assume a,b realize-max-dist-in C;
  then
A1: C /\ dR = {a,b} by Th74;
  assume LSeg(ld,d) meets C;
  then consider q being set such that
A2: q in LSeg(ld,d) and
A3: q in C by XBOOLE_0:3;
  reconsider q as Point of T2 by A3;
  q in dR /\ C by A2,A3,Lm68,XBOOLE_0:def 3;
  then q = a or q = b by A1,TARSKI:def 2;
  hence contradiction by A2,Lm20,Lm21,TOPREAL3:18;
end;

Lm75: |[-1,0]|,|[1,0]| realize-max-dist-in C implies LSeg(pd,d) misses C
proof
  assume a,b realize-max-dist-in C;
  then
A1: C /\ dR = {a,b} by Th74;
  assume LSeg(pd,d) meets C;
  then consider q being set such that
A2: q in LSeg(pd,d) and
A3: q in C by XBOOLE_0:3;
  reconsider q as Point of T2 by A3;
  q in dR /\ C by A2,A3,Lm69,XBOOLE_0:def 3;
  then q = a or q = b by A1,TARSKI:def 2;
  hence contradiction by A2,Lm20,Lm21,TOPREAL3:18;
end;

Lm76: |[
-1,0]|,|[1,0]| realize-max-dist-in C & p in C` & p in LSeg(a,lg) implies
LSeg(p,lg) misses C
proof
  assume that
A1: a,b realize-max-dist-in C and
A2: p in C` and
A3: p in LSeg(a,lg);
A4: C /\ dR = {a,b} by A1,Th74;
  assume LSeg(p,lg) meets C;
  then consider q being set such that
A5: q in LSeg(p,lg) and
A6: q in C by XBOOLE_0:3;
  reconsider q as Point of T2 by A6;
  lg in LSeg(a,lg) by TOPREAL1:6;
  then
A7: p`1 = lg`1 by A3,Lm45,SPPOL_1:def 3;
A8: p`2 <= lg`2 by A3,Lm27,JGRAPH_6:9;
A9: LSeg(p,lg) is vertical by A7,SPPOL_1:37;
  a`2 <= p`2 by A3,Lm20,JGRAPH_6:9;
  then LSeg(p,lg) c= LSeg(ld,lg)
  by A7,A8,A9,Lm20,Lm26,Lm28,Lm29,Lm43,GOBOARD7:65;
  then LSeg(p,lg) c= dR by Lm39,XBOOLE_1:1;
  then q in dR /\ C by A5,A6,XBOOLE_0:def 3;
  then
A10: q = a or q = b by A4,TARSKI:def 2;
  a in LSeg(a,lg) by TOPREAL1:6;
  then
A11: a`1 = p`1 by A3,Lm45,SPPOL_1:def 3;
A12: a in C by A1,JORDAN24:def 1;
  not p in C by A2,XBOOLE_0:def 4;
  then a`2 <> p`2 by A11,A12,TOPREAL3:11;
  then
A13: a`2 < p`2 by A3,Lm20,JGRAPH_6:9;
  p = |[p`1,p`2]| by EUCLID:57;
  hence contradiction by A5,A7,A8,A10,A13,Lm19,Lm26,Lm35,JGRAPH_6:9;
end;

Lm77: |[
-1,0]|,|[1,0]| realize-max-dist-in C & p in C` & p in LSeg(b,pg) implies
LSeg(p,pg) misses C
proof
  assume that
A1: a,b realize-max-dist-in C and
A2: p in C` and
A3: p in LSeg(b,pg);
A4: C /\ dR = {a,b} by A1,Th74;
  assume LSeg(p,pg) meets C;
  then consider q being set such that
A5: q in LSeg(p,pg) and
A6: q in C by XBOOLE_0:3;
  reconsider q as Point of T2 by A6;
  pg in LSeg(b,pg) by TOPREAL1:6;
  then
A7: p`1 = pg`1 by A3,Lm47,SPPOL_1:def 3;
A8: p`2 <= pg`2 by A3,Lm31,JGRAPH_6:9;
A9: LSeg(p,pg) is vertical by A7,SPPOL_1:37;
  b`2 <= p`2 by A3,Lm21,JGRAPH_6:9;
  then LSeg(p,pg) c= LSeg(pd,pg)
  by A7,A8,A9,Lm21,Lm30,Lm32,Lm33,Lm44,GOBOARD7:65;
  then LSeg(p,pg) c= dR by Lm41,XBOOLE_1:1;
  then q in dR /\ C by A5,A6,XBOOLE_0:def 3;
  then
A10: q = a or q = b by A4,TARSKI:def 2;
  b in LSeg(b,pg) by TOPREAL1:6;
  then
A11: b`1 = p`1 by A3,Lm47,SPPOL_1:def 3;
A12: b in C by A1,JORDAN24:def 1;
  not p in C by A2,XBOOLE_0:def 4;
  then b`2 <> p`2 by A11,A12,TOPREAL3:11;
  then
A13: b`2 < p`2 by A3,Lm21,JGRAPH_6:9;
  p = |[p`1,p`2]| by EUCLID:57;
  hence contradiction by A5,A7,A8,A10,A13,Lm18,Lm30,Lm37,JGRAPH_6:9;
end;

Lm78: |[
-1,0]|,|[1,0]| realize-max-dist-in C & p in C` & p in LSeg(a,ld) implies
LSeg(p,ld) misses C
proof
  assume that
A1: a,b realize-max-dist-in C and
A2: p in C` and
A3: p in LSeg(a,ld);
A4: C /\ dR = {a,b} by A1,Th74;
  assume LSeg(p,ld) meets C;
  then consider q being set such that
A5: q in LSeg(p,ld) and
A6: q in C by XBOOLE_0:3;
  reconsider q as Point of T2 by A6;
  ld in LSeg(a,ld) by TOPREAL1:6;
  then
A7: p`1 = ld`1 by A3,Lm46,SPPOL_1:def 3;
A8: ld`2 <= p`2 by A3,Lm29,JGRAPH_6:9;
A9: LSeg(p,ld) is vertical by A7,SPPOL_1:37;
  p`2 <= a`2 by A3,Lm20,JGRAPH_6:9;
  then LSeg(p,ld) c= LSeg(ld,lg) by A7,A8,A9,Lm20,Lm27,Lm43,GOBOARD7:65;
  then LSeg(p,ld) c= dR by Lm39,XBOOLE_1:1;
  then q in dR /\ C by A5,A6,XBOOLE_0:def 3;
  then
A10: q = a or q = b by A4,TARSKI:def 2;
  a in LSeg(a,ld) by TOPREAL1:6;
  then
A11: a`1 = p`1 by A3,Lm46,SPPOL_1:def 3;
A12: a in C by A1,JORDAN24:def 1;
  not p in C by A2,XBOOLE_0:def 4;
  then a`2 <> p`2 by A11,A12,TOPREAL3:11;
  then
A13: p`2 < a`2 by A3,Lm20,JGRAPH_6:9;
  p = |[p`1,p`2]| by EUCLID:57;
  hence contradiction by A5,A7,A8,A10,A13,Lm19,Lm28,Lm34,JGRAPH_6:9;
end;

Lm79: |[
-1,0]|,|[1,0]| realize-max-dist-in C & p in C` & p in LSeg(b,pd) implies
LSeg(p,pd) misses C
proof
  assume that
A1: a,b realize-max-dist-in C and
A2: p in C` and
A3: p in LSeg(b,pd);
A4: C /\ dR = {a,b} by A1,Th74;
  assume LSeg(p,pd) meets C;
  then consider q being set such that
A5: q in LSeg(p,pd) and
A6: q in C by XBOOLE_0:3;
  reconsider q as Point of T2 by A6;
  pd in LSeg(b,pd) by TOPREAL1:6;
  then
A7: p`1 = pd`1 by A3,Lm48,SPPOL_1:def 3;
A8: pd`2 <= p`2 by A3,Lm33,JGRAPH_6:9;
A9: LSeg(p,pd) is vertical by A7,SPPOL_1:37;
  p`2 <= b`2 by A3,Lm21,JGRAPH_6:9;
  then LSeg(p,pd) c= LSeg(pd,pg) by A7,A8,A9,Lm21,Lm31,Lm44,GOBOARD7:65;
  then LSeg(p,pd) c= dR by Lm41,XBOOLE_1:1;
  then q in dR /\ C by A5,A6,XBOOLE_0:def 3;
  then
A10: q = a or q = b by A4,TARSKI:def 2;
  b in LSeg(b,pd) by TOPREAL1:6;
  then
A11: b`1 = p`1 by A3,Lm48,SPPOL_1:def 3;
A12: b in C by A1,JORDAN24:def 1;
  not p in C by A2,XBOOLE_0:def 4;
  then b`2 <> p`2 by A11,A12,TOPREAL3:11;
  then
A13: p`2 < b`2 by A3,Lm21,JGRAPH_6:9;
  p = |[p`1,p`2]| by EUCLID:57;
  hence contradiction by A5,A7,A8,A10,A13,Lm18,Lm32,Lm36,JGRAPH_6:9;
end;

Lm80: |[0,r]| in rectangle(rl,rp,rd,rg) implies r = rd or r = rg
proof
  assume |[0,r]| in dR;
  then ex p st p = |[0,r]| & (p`1 = rl & p`2 <= rg & p`2 >= rd or
  p`1 <= rp & p`1 >= rl & p`2 = rg or p`1 <= rp & p`1 >= rl & p`2 = rd or
  p`1 = rp & p`2 <= rg & p`2 >= rd) by Lm59;
  hence thesis by EUCLID:56;
end;

theorem Th75:
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies W-bound P = -1
proof
  assume
A1: a,b realize-max-dist-in P;
  then
A2: P c= R by Th71;
A3: P = the carrier of (T2|P) by PRE_TOPC:29;
A4: a in P by A1,JORDAN24:def 1;
  reconsider P as non empty Subset of T2 by A1,JORDAN24:def 1;
  reconsider Z = (proj1||P).:the carrier of (T2|P) as Subset of REAL;
A5: for p be real number st p in Z holds p >= rl
  proof
    let p be real number;
    assume p in Z;
    then consider p0 being set such that
A6: p0 in the carrier of T2|P and p0 in the carrier of T2|P and
A7: p = (proj1||P).p0 by FUNCT_2:115;
    p0 in R by A2,A3,A6;
    then ex p1 st p0 = p1 & rl <= p1`1 & p1`1 <= rp & rd <= p1`2 & p1`2 <= rg;
    hence thesis by A3,A6,A7,PSCOMP_1:69;
  end;
  for q being real number st
  for p being real number st p in Z holds p >= q holds rl >= q
  proof
    let q be real number such that
A8: for p being real number st p in Z holds p >= q;
    (proj1||P).a = a`1 by A4,PSCOMP_1:69;
    hence thesis by A3,A4,A8,Lm18,FUNCT_2:43;
  end;
  hence thesis by A5,SEQ_4:61;
end;

theorem Th76:
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies E-bound P = 1
proof
  assume
A1: a,b realize-max-dist-in P;
  then
A2: P c= R by Th71;
A3: b in P by A1,JORDAN24:def 1;
  reconsider P as non empty Subset of T2 by A1,JORDAN24:def 1;
  reconsider Z = (proj1||P).:the carrier of (T2|P) as Subset of REAL;
A4: P = the carrier of (T2|P) by PRE_TOPC:29;
A5: for p be real number st p in Z holds p <= rp
  proof
    let p be real number;
    assume p in Z;
    then consider p0 being set such that
A6: p0 in the carrier of T2|P and p0 in the carrier of T2|P and
A7: p = (proj1||P).p0 by FUNCT_2:115;
    p0 in R by A2,A4,A6;
    then ex p1 st p0 = p1 & rl <= p1`1 & p1`1 <= rp & rd <= p1`2 & p1`2 <= rg;
    hence thesis by A4,A6,A7,PSCOMP_1:69;
  end;
  for q being real number st
  for p being real number st p in Z holds p <= q holds rp <= q
  proof
    let q be real number such that
A8: for p being real number st p in Z holds p <= q;
    (proj1||P).b = b`1 by A3,PSCOMP_1:69;
    hence thesis by A3,A4,A8,Lm19,FUNCT_2:43;
  end;
  hence thesis by A5,SEQ_4:63;
end;

theorem Th77:
  for P being compact Subset of TOP-REAL 2 holds
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies W-most P = {|[-1,0]|}
proof
  let P be compact Subset of T2;
  assume
A1: a,b realize-max-dist-in P;
  then
A2: P c= R by Th71;
  set L = LSeg(SW-corner P, NW-corner P);
A3: a in P by A1,JORDAN24:def 1;
A4: (SW-corner P)`1 = |[rl,S-bound P]|`1 by A1,Th75
    .= rl by EUCLID:56;
A5: (NW-corner P)`1 = |[rl,N-bound P]|`1 by A1,Th75
    .= rl by EUCLID:56;
  thus W-most P c= {a}
  proof
    let x be set;
    assume
A6: x in W-most P;
    then
A7: x in P by XBOOLE_0:def 3;
    reconsider x as Point of T2 by A6;
A8: x in L by A6,XBOOLE_0:def 3;
    SW-corner P in L by TOPREAL1:6;
    then
A9: x`1 = rl by A4,A8,SPPOL_1:def 3;
    x in R by A2,A7;
    then ex p st x = p & rl <= p`1 & p`1 <= rp & rd <= p`2 & p`2 <= rg;
    then x in dR by A9,Lm59;
    then x in P /\ dR by A7,XBOOLE_0:def 3;
    then x in {a,b} by A1,Th74;
    then x = a or x = b by TARSKI:def 2;
    hence thesis by A9,EUCLID:56,TARSKI:def 1;
  end;
  let x be set;
  assume x in {a};
  then
A10: x = a by TARSKI:def 1;
A11: (SW-corner P)`2 = S-bound P by EUCLID:56;
  (NW-corner P)`2 = N-bound P by EUCLID:56;
  then (SW-corner P)`2 <= a`2 & a`2 <= (NW-corner P)`2 by A3,A11,PSCOMP_1:71;
  then a in L by A4,A5,Lm18,GOBOARD7:8;
  hence thesis by A3,A10,XBOOLE_0:def 3;
end;

theorem Th78:
  for P being compact Subset of TOP-REAL 2 holds
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies E-most P = {|[1,0]|}
proof
  let P be compact Subset of T2;
  assume
A1: a,b realize-max-dist-in P;
  then
A2: P c= R by Th71;
  set L = LSeg(SE-corner P, NE-corner P);
A3: b in P by A1,JORDAN24:def 1;
A4: (SE-corner P)`1 = |[rp,S-bound P]|`1 by A1,Th76
    .= rp by EUCLID:56;
A5: (NE-corner P)`1 = |[rp,N-bound P]|`1 by A1,Th76
    .= rp by EUCLID:56;
  thus E-most P c= {b}
  proof
    let x be set;
    assume
A6: x in E-most P;
    then
A7: x in P by XBOOLE_0:def 3;
    reconsider x as Point of T2 by A6;
A8: x in L by A6,XBOOLE_0:def 3;
    SE-corner P in L by TOPREAL1:6;
    then
A9: x`1 = rp by A4,A8,SPPOL_1:def 3;
    x in R by A2,A7;
    then ex p st x = p & rl <= p`1 & p`1 <= rp & rd <= p`2 & p`2 <= rg;
    then x in dR by A9,Lm59;
    then x in P /\ dR by A7,XBOOLE_0:def 3;
    then x in {a,b} by A1,Th74;
    then x = a or x = b by TARSKI:def 2;
    hence thesis by A9,EUCLID:56,TARSKI:def 1;
  end;
  let x be set;
  assume x in {b};
  then
A10: x = b by TARSKI:def 1;
A11: (SE-corner P)`2 = S-bound P by EUCLID:56;
  (NE-corner P)`2 = N-bound P by EUCLID:56;
  then (SE-corner P)`2 <= b`2 & b`2 <= (NE-corner P)`2 by A3,A11,PSCOMP_1:71;
  then b in L by A4,A5,Lm19,GOBOARD7:8;
  hence thesis by A3,A10,XBOOLE_0:def 3;
end;

theorem Th79:
  for P being compact Subset of TOP-REAL 2 holds
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies
  W-min P = |[-1,0]| & W-max P = |[-1,0]|
proof
  let P be compact Subset of T2;
  set M = W-most P;
  assume
A1: a,b realize-max-dist-in P;
  then
A2: M = {a} by Th77;
  set f = proj2||M;
A3: dom f = the carrier of (T2|M) by FUNCT_2:def 1;
A4: the carrier of (T2|M) = M by PRE_TOPC:29;
A5: a in {a} by TARSKI:def 1;
A6: f.:the carrier of (T2|M) = Im(f,a) by A1,A4,Th77
    .= {f.a} by A2,A3,A4,A5,FUNCT_1:117
    .= {proj2.a} by A2,A5,FUNCT_1:72
    .= {a`2} by PSCOMP_1:def 29;
  then
A7: inf (proj2||M) = a`2 by SEQ_4:22;
A8: sup (proj2||M) = a`2 by A6,SEQ_4:22;
  a = |[a`1,a`2]| by EUCLID:57;
  hence thesis by A1,A7,A8,Lm18,Th75;
end;

theorem Th80:
  for P being compact Subset of TOP-REAL 2 holds
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies
  E-min P = |[1,0]| & E-max P = |[1,0]|
proof
  let P be compact Subset of T2;
  set M = E-most P;
  assume
A1: a,b realize-max-dist-in P;
  then
A2: M = {b} by Th78;
  set f = proj2||M;
A3: dom f = the carrier of (T2|M) by FUNCT_2:def 1;
A4: the carrier of (T2|M) = M by PRE_TOPC:29;
A5: b in {b} by TARSKI:def 1;
A6: f.:the carrier of (T2|M) = Im(f,b) by A1,A4,Th78
    .= {f.b} by A2,A3,A4,A5,FUNCT_1:117
    .= {proj2.b} by A2,A5,FUNCT_1:72
    .= {b`2} by PSCOMP_1:def 29;
  then
A7: inf (proj2||M) = b`2 by SEQ_4:22;
A8: sup (proj2||M) = b`2 by A6,SEQ_4:22;
  b = |[b`1,b`2]| by EUCLID:57;
  hence thesis by A1,A7,A8,Lm19,Th76;
end;

Lm81: |[-1,0]|,|[1,0]| realize-max-dist-in P implies
c`1 = (W-bound P + E-bound P) / 2
proof
  assume a,b realize-max-dist-in P;
  then W-bound P = rl & E-bound P = rp by Th75,Th76;
  hence thesis by EUCLID:56;
end;

Lm82: |[-1,0]|,|[1,0]| realize-max-dist-in P implies
d`1 = (W-bound P + E-bound P) / 2
proof
  assume a,b realize-max-dist-in P;
  then W-bound P = rl & E-bound P = rp by Th75,Th76;
  hence thesis by EUCLID:56;
end;

theorem Th81:
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies
  LSeg(|[0,3]|,UMP P) is vertical
proof
  assume a,b realize-max-dist-in P;
  then c`1 = (W-bound P + E-bound P) / 2 by Lm81
    .= (UMP P)`1 by EUCLID:56;
  hence thesis by SPPOL_1:37;
end;

theorem Th82:
  |[-1,0]|,|[1,0]| realize-max-dist-in P implies
  LSeg(LMP P,|[0,-3]|) is vertical
proof
  assume a,b realize-max-dist-in P;
  then d`1 = (W-bound P + E-bound P) / 2 by Lm82
    .= (LMP P)`1 by EUCLID:56;
  hence thesis by SPPOL_1:37;
end;

theorem Th83:
  |[-1,0]|,|[1,0]| realize-max-dist-in P & p in P implies p`2 < 3
proof
  assume that
A1: a,b realize-max-dist-in P and
A2: p in P;
A3: P /\ dR = {a,b} by A1,Th74;
  P c= R by A1,Th71;
  then p in R by A2;
  then
A4: ex p1 st p1 = p & rl <= p1`1 & p1`1 <= rp & rd <= p1`2 & p1`2 <= rg;
  now
    assume
A5: p`2 = c`2;
    then p in LSeg(lg,pg) by A4,Lm23,Lm26,Lm27,Lm30,Lm31,GOBOARD7:9;
    then p in P /\ dR by A2,Lm40,XBOOLE_0:def 3;
    hence contradiction by A3,A5,Lm20,Lm21,Lm23,TARSKI:def 2;
  end;
  hence thesis by A4,Lm23,REAL_1:def 5;
end;

theorem Th84:
  |[-1,0]|,|[1,0]| realize-max-dist-in P & p in P implies -3 < p`2
proof
  assume that
A1: a,b realize-max-dist-in P and
A2: p in P;
A3: P /\ dR = {a,b} by A1,Th74;
  P c= R by A1,Th71;
  then p in R by A2;
  then
A4: ex p1 st p1 = p & rl <= p1`1 & p1`1 <= rp & rd <= p1`2 & p1`2 <= rg;
  now
    assume
A5: p`2 = d`2;
    then p in LSeg(ld,pd) by A4,Lm25,Lm28,Lm29,Lm32,Lm33,GOBOARD7:9;
    then p in P /\ dR by A2,Lm42,XBOOLE_0:def 3;
    then p = a or p = b by A3,TARSKI:def 2;
    hence contradiction by A5,Lm25,EUCLID:56;
  end;
  hence thesis by A4,Lm25,REAL_1:def 5;
end;

theorem Th85:
  |[-1,0]|,|[1,0]| realize-max-dist-in D & p in LSeg(|[0,3]|,UMP D) implies
  (UMP D)`2 <= p`2
proof
  set x = UMP D;
  assume that
A1: a,b realize-max-dist-in D and
A2: p in LSeg(c,x);
A3: x in LSeg(c,x) by TOPREAL1:6;
A4: LSeg(c,x) is vertical by A1,Th81;
A5: c = |[c`1,c`2]| & x = |[x`1,x`2]| by EUCLID:57;
  c in LSeg(c,x) by TOPREAL1:6;
  then
A6: c`1 = x`1 by A3,A4,SPPOL_1:def 3;
  x`2 <= c`2 by A1,Lm23,Th83,JORDAN21:43;
  hence thesis by A2,A5,A6,JGRAPH_6:9;
end;

theorem Th86:
  |[-1,0]|,|[1,0]| realize-max-dist-in D & p in LSeg(LMP D,|[0,-3]|) implies
  p`2 <= (LMP D)`2
proof
  set x = LMP D;
  assume that
A1: a,b realize-max-dist-in D and
A2: p in LSeg(x,d);
A3: x in LSeg(x,d) by TOPREAL1:6;
A4: LSeg(x,d) is vertical by A1,Th82;
A5: d = |[d`1,d`2]| & x = |[x`1,x`2]| by EUCLID:57;
  d in LSeg(x,d) by TOPREAL1:6;
  then
A6: d`1 = x`1 by A3,A4,SPPOL_1:def 3;
  d`2 <= x`2 by A1,Lm25,Th84,JORDAN21:44;
  hence thesis by A2,A5,A6,JGRAPH_6:9;
end;

theorem Th87:
  |[-1,0]|,|[1,0]| realize-max-dist-in D implies
  LSeg(|[0,3]|,UMP D) c= north_halfline UMP D
proof
  set p = UMP D;
  assume
A1: a,b realize-max-dist-in D;
  let x be set;
  assume
A2: x in LSeg(c,p);
  then reconsider x as Point of T2;
A3: p in LSeg(c,p) by TOPREAL1:6;
  LSeg(c,p) is vertical by A1,Th81;
  then
A4: x`1 = p`1 by A2,A3,SPPOL_1:def 3;
  p`2 <= x`2 by A1,A2,Th85;
  hence thesis by A4,TOPREAL1:def 12;
end;

theorem Th88:
  |[-1,0]|,|[1,0]| realize-max-dist-in D implies
  LSeg(LMP D,|[0,-3]|) c= south_halfline LMP D
proof
  set p = LMP D;
  assume
A1: a,b realize-max-dist-in D;
  let x be set;
  assume
A2: x in LSeg(p,d);
  then reconsider x as Point of T2;
A3: p in LSeg(p,d) by TOPREAL1:6;
A4: LSeg(p,d) is vertical by A1,Th82;
  then
A5: x`1 = p`1 by A2,A3,SPPOL_1:def 3;
A6: d = |[d`1,d`2]| & p = |[p`1,p`2]| by EUCLID:57;
  d in LSeg(p,d) by TOPREAL1:6;
  then
A7: d`1 = p`1 by A3,A4,SPPOL_1:def 3;
  d`2 <= p`2 by A1,Lm25,Th84,JORDAN21:44;
  then x`2 <= p`2 by A2,A6,A7,JGRAPH_6:9;
  hence thesis by A5,TOPREAL1:def 14;
end;

theorem Th89:
  |[-1,0]|,|[1,0]| realize-max-dist-in C & P is_inside_component_of C implies
  LSeg(|[0,3]|,UMP C) misses P
proof
  set m = UMP C;
  set L = LSeg(c,m);
  assume that
A1: a,b realize-max-dist-in C and
A2: P is_inside_component_of C;
  consider VP being Subset of T2|C` such that
A3: VP = P and VP is_a_component_of T2|C` and
  VP is bounded Subset of Euclid 2 by A2,JORDAN2C:17;
  m in L by TOPREAL1:6;
  then {m} c= L by ZFMISC_1:37;
  then
A4: L = L \ {m} \/ {m} by XBOOLE_1:45;
A5: L \ {m} c= north_halfline m \ {m} by A1,Th87,XBOOLE_1:33;
  north_halfline m \ {m} c= UBD C by Th12;
  then L \ {m} c= UBD C by A5,XBOOLE_1:1;
  then
A6: L \ {m} misses P by A2,Th14,XBOOLE_1:63;
  {m} misses P by A3,Lm6,JORDAN21:43;
  hence thesis by A4,A6,XBOOLE_1:70;
end;

theorem Th90:
  |[-1,0]|,|[1,0]| realize-max-dist-in C & P is_inside_component_of C implies
  LSeg(LMP C,|[0,-3]|) misses P
proof
  set m = LMP C;
  set L = LSeg(m,d);
  assume that
A1: a,b realize-max-dist-in C and
A2: P is_inside_component_of C;
  consider VP being Subset of T2|C` such that
A3: VP = P and VP is_a_component_of T2|C` and
  VP is bounded Subset of Euclid 2 by A2,JORDAN2C:17;
  m in L by TOPREAL1:6;
  then {m} c= L by ZFMISC_1:37;
  then
A4: L = L \ {m} \/ {m} by XBOOLE_1:45;
A5: L \ {m} c= south_halfline m \ {m} by A1,Th88,XBOOLE_1:33;
  south_halfline m \ {m} c= UBD C by Th13;
  then L \ {m} c= UBD C by A5,XBOOLE_1:1;
  then
A6: L \ {m} misses P by A2,Th14,XBOOLE_1:63;
  {m} misses P by A3,Lm6,JORDAN21:44;
  hence thesis by A4,A6,XBOOLE_1:70;
end;

theorem Th91:
  |[-1,0]|,|[1,0]| realize-max-dist-in D implies
  LSeg(|[0,3]|,UMP D) /\ D = {UMP D}
proof
  assume
A1: a,b realize-max-dist-in D;
  set m = UMP D;
  set w = (W-bound D + E-bound D) / 2;
A2: c`1 = w by A1,Lm81;
A3: m`1 = w by EUCLID:56;
A4: m in LSeg(c,m) by TOPREAL1:6;
A5: m in D by JORDAN21:43;
  thus LSeg(c,m) /\ D c= {m}
  proof
    let x be set;
    assume
A6: x in LSeg(c,m) /\ D;
    then
A7: x in LSeg(c,m) & x in D by XBOOLE_0:def 3;
    reconsider x as Point of T2 by A6;
    LSeg(c,m) is vertical by A2,A3,SPPOL_1:37;
    then
A8: x`1 = m`1 by A4,A7,SPPOL_1:def 3;
    then x in Vertical_Line w by A3,JORDAN6:34;
    then x in D /\ Vertical_Line w by A7,XBOOLE_0:def 3;
    then
A9: x`2 <= m`2 by JORDAN21:41;
    m`2 <= x`2 by A1,A7,Th85;
    then x`2 = m`2 by A9,XXREAL_0:1;
    then x = m by A8,TOPREAL3:11;
    hence thesis by TARSKI:def 1;
  end;
  let x be set;
  assume x in {m};
  then x = m by TARSKI:def 1;
  hence thesis by A4,A5,XBOOLE_0:def 3;
end;

theorem
  |[-1,0]|,|[1,0]| realize-max-dist-in D implies
  LSeg(|[0,-3]|,LMP D) /\ D = {LMP D}
proof
  assume
A1: a,b realize-max-dist-in D;
  set m = LMP D;
  set w = (W-bound D + E-bound D) / 2;
A2: d`1 = w by A1,Lm82;
A3: m`1 = w by EUCLID:56;
A4: m in LSeg(d,m) by TOPREAL1:6;
A5: m in D by JORDAN21:44;
  thus LSeg(d,m) /\ D c= {m}
  proof
    let x be set;
    assume
A6: x in LSeg(d,m) /\ D;
    then
A7: x in LSeg(d,m) & x in D by XBOOLE_0:def 3;
    reconsider x as Point of T2 by A6;
    LSeg(d,m) is vertical by A2,A3,SPPOL_1:37;
    then
A8: x`1 = m`1 by A4,A7,SPPOL_1:def 3;
    then x in Vertical_Line w by A3,JORDAN6:34;
    then x in D /\ Vertical_Line w by A7,XBOOLE_0:def 3;
    then
A9: m`2 <= x`2 by JORDAN21:42;
    x`2 <= m`2 by A1,A7,Th86;
    then x`2 = m`2 by A9,XXREAL_0:1;
    then x = m by A8,TOPREAL3:11;
    hence thesis by TARSKI:def 1;
  end;
  let x be set;
  assume x in {m};
  then x = m by TARSKI:def 1;
  hence thesis by A4,A5,XBOOLE_0:def 3;
end;

theorem Th93:
  P is compact &
  |[-1,0]|,|[1,0]| realize-max-dist-in P & A is_inside_component_of P implies
  A c= closed_inside_of_rectangle(-1,1,-3,3)
proof
  assume that
A1: P is compact and
A2: |[-1,0]|,|[1,0]| realize-max-dist-in P and
A3: A is_inside_component_of P;
  let x be set;
  assume that
A4: x in A and
A5: not x in R;
  P c= R by A2,Th71;
  then
A6: R` c= P` by SUBSET_1:31;
  reconsider x as Point of T2 by A4;
A7: not (rl <= x`1 & x`1 <= rp & rd <= x`2 & x`2 <= rg) by A5;
  per cases;
  suppose
A8: 0 <= x`1;
    set E = east_halfline(x);
    E c= R`
    proof
      let e be set;
      assume
A9:   e in E;
      then reconsider e as Point of T2;
A10:  e`1 >= x`1 & e`2 = x`2 by A9,TOPREAL1:def 13;
      now
        assume e in R;
        then ex p st e = p & rl <= p`1 & p`1 <= rp & rd <= p`2 & p`2 <= rg;
        hence contradiction by A7,A8,A10,XXREAL_0:2;
      end;
      hence thesis by SUBSET_1:50;
    end;
    then E c= P` by A6,XBOOLE_1:1;
    then E misses P by SUBSET_1:43;
    then
A11: E c= UBD P by A1,JORDAN2C:135;
    x in E by TOPREAL1:45;
    then A meets UBD P by A4,A11,XBOOLE_0:3;
    hence thesis by A3,Th14;
  end;
  suppose
A12: x`1 < 0;
    set E = west_halfline(x);
    E c= R`
    proof
      let e be set;
      assume
A13:  e in E;
      then reconsider e as Point of T2;
A14:  e`1 <= x`1 & e`2 = x`2 by A13,TOPREAL1:def 15;
      now
        assume e in R;
        then ex p st e = p & rl <= p`1 & p`1 <= rp & rd <= p`2 & p`2 <= rg;
        hence contradiction by A7,A12,A14,XXREAL_0:2;
      end;
      hence thesis by SUBSET_1:50;
    end;
    then E c= P` by A6,XBOOLE_1:1;
    then E misses P by SUBSET_1:43;
    then
A15: E c= UBD P by A1,JORDAN2C:134;
    x in E by TOPREAL1:45;
    then A meets UBD P by A4,A15,XBOOLE_0:3;
    hence thesis by A3,Th14;
  end;
end;

Lm83: p in R implies R c= Ball(p,10)
proof
  assume p in R;
  then consider p1 such that
A1: p1 = p and
A2: rl <= p1`1 & p1`1 <= rp & rd <= p1`2 & p1`2 <= rg;
  let x be set;
  assume
A3: x in R;
  then reconsider x as Point of T2;
  consider p2 such that
A4: p2 = x and
A5: rl <= p2`1 & p2`1 <= rp & rd <= p2`2 & p2`2 <= rg by A3;
A6: ex s, t being Point of Euclid 2 st
  s = p1 & t = p2 & dist(p1,p2) = dist(s,t) by TOPREAL6:def 1;
  dist(p1,p2) <= (rp-rl) + (rg-rd) by A2,A5,TOPREAL6:104;
  then dist(p1,p2) < 10 by XXREAL_0:2;
  then |.x-p.| < 10 by A1,A4,A6,SPPOL_1:62;
  hence thesis by TOPREAL9:7;
end;

theorem
  |[-1,0]|,|[1,0]| realize-max-dist-in C implies LSeg(|[0,3]|,|[0,-3]|) meets
  C
proof
  assume
A1: a,b realize-max-dist-in C;
  set Jc = Upper_Arc C;
  consider Pf being Path of c,d, f being Function
  of I[01], T2|LSeg(c,d) such that
A2: rng f = LSeg(c,d) & Pf = f by Th43;
  a = W-min C & b = E-max C by A1,Th79,Th80;
  then Jc is_an_arc_of a,b by JORDAN6:def 8;
  then consider Pg being Path of a,b, g being Function
  of I[01], T2|Jc such that
A3: rng g = Jc & Pg = g by Th42;
A4: Jc c= C by JORDAN6:76;
A5: C c= R by A1,Th71;
A6: a in C & b in C by A1,JORDAN24:def 1;
A7: the carrier of TR = R by PRE_TOPC:29;
  reconsider AR = a, BR = b, CR = c, DR = d
  as Point of TR by A5,A6,Lm60,Lm61,Lm65,PRE_TOPC:29;
  rng Pg c= the carrier of TR by A3,A4,A5,A7,XBOOLE_1:1;
  then reconsider h = Pg as Path of AR,BR by Th30;
  LSeg(c,d) c= R by Lm60,Lm61,Lm65,JORDAN1:def 1;
  then reconsider v = Pf as Path of CR,DR by A2,A7,Th30;
  consider s, t being Point of I[01] such that
A8: h.s = v.t by Lm18,Lm19,Lm23,Lm25,JGRAPH_8:6;
  dom h = the carrier of I[01] & dom v = the carrier of I[01]
  by FUNCT_2:def 1;
  then v.t in rng Pf & h.s in rng Pg by FUNCT_1:def 5;
  hence thesis by A2,A3,A4,A8,XBOOLE_0:3;
end;

Lm84: |[-1,0]|,|[1,0]| realize-max-dist-in C implies
ex Jc, Jd being compact with_the_max_arc Subset of T2 st
Jc is_an_arc_of |[-1,0]|,|[1,0]| & Jd is_an_arc_of |[-1,0]|,|[1,0]| &
C = Jc \/ Jd & Jc /\ Jd = {|[-1,0]|,|[1,0]|} & UMP C in Jc & LMP C in Jd &
W-bound C = W-bound Jc & E-bound C = E-bound Jc
proof
  assume
A1: a,b realize-max-dist-in C;
  set U = Upper_Arc C;
  set L = Lower_Arc C;
A2: U \/ L = C by JORDAN6:def 9;
A3: UMP C in C by JORDAN21:43;
  LMP C in C by JORDAN21:44;
  then
A4: LMP C in U or LMP C in L by A2,XBOOLE_0:def 2;
A5: W-min C = a & E-max C = b by A1,Th79,Th80;
  per cases by A2,A3,XBOOLE_0:def 2;
  suppose
A6: UMP C in U;
    take U, L;
    thus thesis by A4,A5,A6,JORDAN21:26,27,63,JORDAN6:65;
  end;
  suppose
A7: UMP C in L;
    take L, U;
    thus thesis by A4,A5,A7,JORDAN21:28,29,62,JORDAN6:65;
  end;
end;

theorem Th95:
  |[-1,0]|,|[1,0]| realize-max-dist-in C implies
  for Jc, Jd being compact with_the_max_arc Subset of TOP-REAL 2 st
  Jc is_an_arc_of |[-1,0]|,|[1,0]| & Jd is_an_arc_of |[-1,0]|,|[1,0]| &
  C = Jc \/ Jd & Jc /\ Jd = {|[-1,0]|,|[1,0]|} & UMP C in Jc & LMP C in Jd &
  W-bound C = W-bound Jc & E-bound C = E-bound Jc
  for Ux being Subset of TOP-REAL 2 st Ux = Component_of Down ((1/2) *
  ((UMP (LSeg(LMP Jc,|[0,-3]|) /\ Jd)) + LMP Jc), C`)
  holds Ux is_inside_component_of C & for V being Subset of TOP-REAL 2 st
  V is_inside_component_of C holds V = Ux
proof
  set m = UMP C;
  set j = LMP C;
  assume
A1: a,b realize-max-dist-in C;
  let Jc, Jd being compact with_the_max_arc Subset of T2 such that
A2: Jc is_an_arc_of a,b and
A3: Jd is_an_arc_of a,b and
A4: C = Jc \/ Jd and
A5: Jc /\ Jd = {a,b} and
A6: UMP C in Jc and
A7: LMP C in Jd and
A8: W-bound C = W-bound Jc and
A9: E-bound C = E-bound Jc;
  set l = LMP Jc;
  set LJ = LSeg(l,d) /\ Jd;
  set k = UMP LJ;
  set x = (1/2)*(k+l);
  set w = (W-bound C + E-bound C) / 2;
  let Ux be Subset of TOP-REAL 2 such that
A10: Ux = Component_of Down (x,C`);
A11: C c= R by A1,Th71;
A12: W-bound C = rl by A1,Th75;
A13: E-bound C = rp by A1,Th76;
A14: a in C & b in C by A1,JORDAN24:def 1;
A15: m in C by JORDAN21:43;
A16: l in Jc by JORDAN21:44;
A17: Jd c= C by A4,XBOOLE_1:7;
A18: Jc c= C by A4,XBOOLE_1:7;
  then
A19: l in C by A16;
A20: m`2 < c`2 by A1,Lm23,Th83,JORDAN21:43;
A21: l`1 = 0 by A8,A9,A12,A13,EUCLID:56;
A22: c`1 = w by A1,Lm81;
A23: m`1 = w by EUCLID:56;
A24: m <> a & m <> b & l <> a & l <> b by A8,A9,A12,A13,Lm18,Lm19,EUCLID:56;
  then consider Pml being Path of m,l such that
A25: rng Pml c= Jc and
A26: rng Pml misses {a,b} by A2,A6,A16,Th44;
  set ml = rng Pml;
A27: ml c= C by A18,A25,XBOOLE_1:1;
A28: j in C by A7,A17;
A29: LSeg(l,d) is vertical by A21,Lm24,SPPOL_1:37;
A30: d`2 <= j`2 by A1,A7,A17,Lm25,Th84;
A31: j`1 = 0 by A12,A13,EUCLID:56;
  l in Vertical_Line w by A12,A13,A21,JORDAN6:34;
  then
A32: l in C /\ Vertical_Line w by A16,A18,XBOOLE_0:def 3;
  then j`2 <= l`2 by JORDAN21:42;
  then j in LSeg(l,d) by A21,A30,A31,Lm24,GOBOARD7:8;
  then
A33: LJ is non empty by A7,XBOOLE_0:def 3;
A34: LJ is compact by COMPTS_1:20;
A35: for A being vertical non empty compact Subset of TOP-REAL 2 holds
  A is with_the_max_arc;
A36: LJ is vertical by A29,Th4;
  then LJ is with_the_max_arc by A33,A34,A35;
  then
A37: k in LJ by A34,JORDAN21:43;
  then
A38: k in LSeg(l,d) by XBOOLE_0:def 3;
A39: k in Jd by A37,XBOOLE_0:def 3;
  then
A40: k in C by A17;
A41: d in LSeg(l,d) by TOPREAL1:6;
  then
A42: k`1 = 0 by A29,A38,Lm24,SPPOL_1:def 3;
  then k <> a & k <> b & j <> a & j <> b by A31,EUCLID:56;
  then consider Pkj being Path of k,j such that
A43: rng Pkj c= Jd and
A44: rng Pkj misses {a,b} by A3,A7,A39,Th44;
  set kj = rng Pkj;
A45: kj c= C by A17,A43,XBOOLE_1:1;
A46: x in LSeg(k,l) by GOBOARD7:7;
A47: Component_of Down(x,C`) is_a_component_of T2|C` by CONNSP_1:43;
A48: the carrier of T2|C` = C` by PRE_TOPC:29;
A49: LSeg(l,k) is vertical by A21,A42,SPPOL_1:37;
A50: k in LSeg(l,k) by TOPREAL1:6;
A51: l = |[l`1,l`2]| & k = |[k`1,k`2]| by EUCLID:57;
A52: d = |[d`1,d`2]| by EUCLID:57;
  d`2 <= l`2 by A1,A16,A18,Lm25,Th84;
  then
A53: k`2 <= l`2 by A21,A38,A51,A52,Lm24,JGRAPH_6:9;
  a <> k & b <> k & a <> l & b <> l by A21,A42,EUCLID:56;
  then not k in {a,b} & not l in {a,b} by TARSKI:def 2;
  then
A54: k <> l by A5,A16,A39,XBOOLE_0:def 3;
  then k`2 <> l`2 by A21,A42,TOPREAL3:11;
  then
A55: k`2 < l`2 by A53,REAL_1:def 5;
  k in Vertical_Line w by A12,A13,A42,JORDAN6:34;
  then k in C /\ Vertical_Line w by A17,A39,XBOOLE_0:def 3;
  then j`2 <= k`2 by JORDAN21:42;
  then d`2 <= k`2 by A1,A7,A17,Lm25,Th84,XXREAL_0:2;
  then
A56: LSeg(l,k) c= LSeg(l,d) by A29,A42,A49,A53,Lm24,GOBOARD7:65;
A57: LSeg(l,k) \ {l,k} c= C`
  proof
    let q be set;
    assume that
A58: q in LSeg(l,k) \ {l,k} and
A59: not q in C`;
A60: q in LSeg(l,k) by A58,XBOOLE_0:def 4;
    reconsider q as Point of T2 by A58;
A61: q in C by A59,SUBSET_1:50;
A62: q`1 = w by A12,A13,A42,A49,A50,A60,SPPOL_1:def 3;
    then
A63: q in Vertical_Line w by JORDAN6:34;
    per cases by A4,A61,XBOOLE_0:def 2;
    suppose q in Jc;
      then q in Jc /\ Vertical_Line w by A63,XBOOLE_0:def 3;
      then
A64:  l`2 <= q`2 by A8,A9,JORDAN21:42;
      q`2 <= l`2 by A21,A42,A51,A53,A60,JGRAPH_6:9;
      then l`2 = q`2 by A64,XXREAL_0:1;
      then l = q by A12,A13,A21,A62,TOPREAL3:11;
      then q in {l,k} by TARSKI:def 2;
      hence contradiction by A58,XBOOLE_0:def 4;
    end;
    suppose q in Jd;
      then
A65:  q in LJ by A56,A60,XBOOLE_0:def 3;
A66:  q`1 = d`1 by A29,A41,A56,A60,SPPOL_1:def 3;
      LJ c= LSeg(l,d) by XBOOLE_1:17;
      then
A67:  W-bound LSeg(l,d) <= W-bound LJ & E-bound LJ <= E-bound LSeg(l,d)
      by A65,PSCOMP_1:130,132;
A68:  W-bound LJ = E-bound LJ by A33,A34,A36,SPRECT_1:17;
A69:  W-bound LSeg(l,d) = d`1 & E-bound LSeg(l,d) = d`1
      by A21,Lm24,SPRECT_1:62,65;
      then W-bound LSeg(l,d) = W-bound LJ & E-bound LSeg(l,d) = E-bound LJ
      by A67,A68,XXREAL_0:1;
      then q in Vertical_Line ((W-bound LJ + E-bound LJ) / 2)
      by A66,A69,JORDAN6:34;
      then q in LJ /\ Vertical_Line ((W-bound LJ + E-bound LJ) / 2)
      by A65,XBOOLE_0:def 3;
      then
A70:  q`2 <= k`2 by A34,JORDAN21:41;
      k`2 <= q`2 by A21,A42,A51,A53,A60,JGRAPH_6:9;
      then k`2 = q`2 by A70,XXREAL_0:1;
      then k = q by A12,A13,A42,A62,TOPREAL3:11;
      then q in {l,k} by TARSKI:def 2;
      hence contradiction by A58,XBOOLE_0:def 4;
    end;
  end;
  then reconsider X = LSeg(l,k) \ {l,k} as Subset of T2|C` by PRE_TOPC:29;
  now
    assume x in {l,k};
    then x = l or x = k by TARSKI:def 2;
    hence contradiction by A54,Th1;
  end;
  then
A71: x in LSeg(l,k) \ {l,k} by A46,XBOOLE_0:def 4;
  then Component_of(x,C`) = Component_of Down(x,C`) by A57,CONNSP_3:27;
  then
A72: x in Component_of Down(x,C`) by A57,A71,CONNSP_3:26;
  then
A73: X meets Ux by A10,A71,XBOOLE_0:3;
  LSeg(l,k) \ {l,k} is connected by JORDAN1:52;
  then X is connected by CONNSP_1:24;
  then
A74: X c= Component_of Down(x,C`) by A10,A47,A73,CONNSP_1:38;
A75: LSeg(l,k) c= R by A11,A19,A40,JORDAN1:def 1;
A76: the carrier of TR = R by PRE_TOPC:29;
  reconsider AR = a, BR = b, CR = c, DR = d
  as Point of TR by A11,A14,Lm60,Lm61,Lm65,PRE_TOPC:29;
  consider Pcm being Path of c,m, fcm being Function of I[01], T2|LSeg(c,m)
  such that
A77: rng fcm = LSeg(c,m) & Pcm = fcm by Th43;
A78: LSeg(c,m) c= R by A11,A15,Lm60,Lm65,JORDAN1:def 1;
A79: ml c= R by A11,A27,XBOOLE_1:1;

:: component containg x is bounded
  thus Ux is_inside_component_of C
  proof thus
A80: Ux is_a_component_of C` by A10,A47,CONNSP_1:def 6;
    assume not Ux is Bounded;
    then not Ux c= Ball(x,10) by JORDAN2C:16;
    then consider u being set such that
A81: u in Ux and
A82: not u in Ball(x,10) by TARSKI:def 3;
A83: R c= Ball(x,10) by A46,A75,Lm83;
    reconsider u as Point of T2 by A81;
A84: Ux is open by A80,SPRECT_3:18;
    Component_of Down(x,C`) is connected by A47,CONNSP_1:def 5;
    then Ux is connected by A10,CONNSP_1:24;
    then
A85: Ux is_Region by A84,TOPREAL4:def 3;
    x in Ball(x,10) by Th16;
    then consider P1 being Subset of T2 such that
A86: P1 is_S-P_arc_joining x,u and
A87: P1 c= Ux by A10,A72,A81,A82,A85,TOPREAL4:30;
A88: P1 is_an_arc_of x,u by A86,TOPREAL4:3;
    reconsider P2 = P1 as Subset of T2|C` by A10,A87,XBOOLE_1:1;
A89: P2 c= Component_of Down(x,C`) by A10,A87;
A90: P2 misses C by A48,SUBSET_1:43;
    then
A91: P2 misses Jc by A4,XBOOLE_1:7,63;
A92: P2 misses Jd by A4,A90,XBOOLE_1:7,63;
A93: x`1 = 1/2*((k+l)`1) by TOPREAL3:9
      .= 1/2*(k`1+l`1) by TOPREAL3:7
      .= 0 by A21,A42;
    then
A94: LSeg(d,x) is vertical by Lm24,SPPOL_1:37;
A95: x = |[x`1,x`2]| by EUCLID:57;
A96: x`2 < l`2 by A55,Th3;
A97: k`2 < x`2 by A55,Th2;
    then
A98: d`2 <= x`2 by A1,A17,A39,Lm25,Th84,XXREAL_0:2;
    d`1 = d`1;
    then
A99: LSeg(d,x) c= LSeg(d,l) by A29,A94,A96,A98,GOBOARD7:65;
A100: LSeg(d,x) misses Jc
    proof
      assume not thesis;
      then consider q being set such that
A101: q in LSeg(d,x) and
A102: q in Jc by XBOOLE_0:3;
      reconsider q as Point of T2 by A101;
      q`2 <= x`2 by A52,A93,A95,A98,A101,Lm24,JGRAPH_6:9;
      then
A103: q`2 < l`2 by A96,XXREAL_0:2;
      q`1 = 0 by A29,A41,A99,A101,Lm24,SPPOL_1:def 3;
      then q in Vertical_Line w by A12,A13,JORDAN6:34;
      then q in Jc /\ Vertical_Line w by A102,XBOOLE_0:def 3;
      hence contradiction by A8,A9,A103,JORDAN21:42;
    end;
    set n = First_Point(P1,x,u,dR);
A104: not u in R by A82,A83;
A105: Fr R = dR by Th52;
    u in P1 by A88,TOPREAL1:4;
    then
A106: P1 \ R <> {}T2 by A104,XBOOLE_0:def 4;
    x in P1 by A88,TOPREAL1:4;
    then P1 meets R by A46,A75,XBOOLE_0:3;
    then
A107: P1 meets dR by A88,A105,A106,CONNSP_1:23,JORDAN6:11;
    P1 is closed by A86,JORDAN6:12,TOPREAL4:3;
    then P1 /\ dR is closed by TOPS_1:35;
    then
A108: n in P1 /\ dR by A88,A107,JORDAN5C:def 1;
    then
A109: n in dR by XBOOLE_0:def 3;
A110: n in P1 by A108,XBOOLE_0:def 3;
    set alpha = Segment(P1,x,u,x,n);
A111: rd < k`2 by A1,A17,A39,Th84;
    l`2 <= m`2 by A32,JORDAN21:41;
    then x`2 < m`2 by A96,XXREAL_0:2;
    then not x in dR by A20,A93,A95,A97,A111,Lm80;
    then
A112: alpha is_an_arc_of x,n by A86,A109,A110,JORDAN16:39,TOPREAL4:3;
A113: alpha misses Jc by A91,JORDAN16:5,XBOOLE_1:63;
A114: alpha misses Jd by A92,JORDAN16:5,XBOOLE_1:63;
    consider Pdx being Path of d,x,
    fdx being Function of I[01], T2|LSeg(d,x) such that
A115: rng fdx = LSeg(d,x) & Pdx = fdx by Th43;
    consider PJc being Path of a,b, fJc being Function
    of I[01], T2|Jc such that
A116: rng fJc = Jc & PJc = fJc by A2,Th42;
    consider PJd being Path of a,b, fJd being Function
    of I[01], T2|Jd such that
A117: rng fJd = Jd & PJd = fJd by A3,Th42;
    consider Palpha being Path of x,n,
    falpha being Function of I[01], T2|alpha such that
A118: rng falpha = alpha & Palpha = falpha by A112,Th42;
    n in R by A109,Lm65;
    then
A119: ex p st p = n & rl <= p`1 & p`1 <= rp & rd <= p`2 & p`2 <= rg;
    rng PJc c= the carrier of TR by A11,A18,A76,A116,XBOOLE_1:1;
    then reconsider h = PJc as Path of AR,BR by Th30;
    rng PJd c= the carrier of TR by A11,A17,A76,A117,XBOOLE_1:1;
    then reconsider H = PJd as Path of AR,BR by Th30;
A120: LSeg(d,x) c= R by A46,A75,Lm61,Lm65,JORDAN1:def 1;
A121: alpha c= R by A46,A75,A86,A104,Th57,TOPREAL4:3;
A122: ld in LSeg(ld,lg) by TOPREAL1:6;
A123: pd in LSeg(pd,pg) by TOPREAL1:6;
    LSeg(lg,c) misses C by A1,Lm72;
    then
A124: LSeg(lg,c) misses Jc by A4,XBOOLE_1:7,63;
A125: LSeg(lg,c) c= R by Lm65,Lm66,XBOOLE_1:1;
A126: LSeg(pg,c) c= R by Lm65,Lm67,XBOOLE_1:1;
    LSeg(pg,c) misses C by A1,Lm73;
    then
A127: LSeg(pg,c) misses Jc by A4,XBOOLE_1:7,63;
    consider Plx being Path of l,x, flx being Function of I[01], T2|LSeg(l,x)
    such that
A128: rng flx = LSeg(l,x) & Plx = flx by Th43;
    set PCX = Pcm + Pml + Plx;
A129: rng PCX = rng Pcm \/ rng Pml \/ rng Plx by Th40;
A130: ml misses Jd
    proof
      assume ml meets Jd;
      then consider q being set such that
A131: q in ml and
A132: q in Jd by XBOOLE_0:3;
      q in {a,b} by A5,A25,A131,A132,XBOOLE_0:def 3;
      hence contradiction by A26,A131,XBOOLE_0:3;
    end;
A133: LSeg(c,m) /\ C = {m} by A1,Th91;
A134: LSeg(c,m) misses Jd
    proof
      assume LSeg(c,m) meets Jd;
      then consider q being set such that
A135: q in LSeg(c,m) and
A136: q in Jd by XBOOLE_0:3;
      q in {m} by A17,A133,A135,A136,XBOOLE_0:def 3;
      then q = m by TARSKI:def 1;
      then m in {a,b} by A5,A6,A136,XBOOLE_0:def 3;
      hence contradiction by A24,TARSKI:def 2;
    end;
    LSeg(l,x) is vertical by A21,A93,SPPOL_1:37;
    then
A137: LSeg(l,x) c= LSeg(l,k) by A42,A49,A93,A96,A97,GOBOARD7:65;
    l in LSeg(l,x) by TOPREAL1:6;
    then {l} c= LSeg(l,x) by ZFMISC_1:37;
    then
A138: LSeg(l,x) = LSeg(l,x) \ {l} \/ {l} by XBOOLE_1:45;
    LSeg(l,x) \ {l} c= LSeg(l,k) \ {l,k}
    proof
      let q be set;
      assume q in LSeg(l,x) \ {l};
      then
A139: q in LSeg(l,x) & q <> l by ZFMISC_1:64;
      then q <> k by A21,A51,A93,A95,A96,A97,JGRAPH_6:9;
      then not q in {l,k} by A139,TARSKI:def 2;
      hence thesis by A137,A139,XBOOLE_0:def 4;
    end;
    then LSeg(l,x) \ {l} c= C` by A57,XBOOLE_1:1;
    then LSeg(l,x) \ {l} misses C by SUBSET_1:43;
    then
A140: LSeg(l,x) \ {l} misses Jd by A4,XBOOLE_1:7,63;
    {l} misses Jd
    proof
      assume {l} meets Jd;
      then l in Jd by ZFMISC_1:56;
      then l in {a,b} by A5,A16,XBOOLE_0:def 3;
      hence thesis by A24,TARSKI:def 2;
    end;
    then LSeg(l,x) misses Jd by A138,A140,XBOOLE_1:70;
    then
A141: rng PCX misses Jd by A77,A128,A129,A130,A134,XBOOLE_1:114;
    LSeg(l,x) c= R by A75,A137,XBOOLE_1:1;
    then
A142: rng PCX c= R by A77,A78,A79,A128,A129,Lm1;
    LSeg(ld,d) misses C by A1,Lm74;
    then
A143: LSeg(ld,d) misses Jd by A4,XBOOLE_1:7,63;
    LSeg(pd,d) misses C by A1,Lm75;
    then
A144: LSeg(pd,d) misses Jd by A4,XBOOLE_1:7,63;
    per cases;
    suppose
A145: n`2 < 0;
      per cases by A109,A145,Lm71;
      suppose
A146:   n in LSeg(a,ld);
        consider Pnld being Path of n,ld,
        fnld being Function of I[01], T2|LSeg(n,ld) such that
A147:   rng fnld = LSeg(n,ld) & Pnld = fnld by Th43;
        consider Pldd being Path of ld,d,
        fldd being Function of I[01], T2|LSeg(ld,d) such that
A148:   rng fldd = LSeg(ld,d) & Pldd = fldd by Th43;
A149:   ld`1 = n`1 by A122,A146,Lm43,Lm56,SPPOL_1:def 3;
        then LSeg(n,ld) is vertical by SPPOL_1:37;
        then LSeg(n,ld) c= LSeg(ld,lg) by A119,A149,Lm27,Lm29,Lm43,GOBOARD7:65;
        then
A150:   LSeg(n,ld) c= dR by Lm39,XBOOLE_1:1;
        set K1 = PCX + Palpha + Pnld + Pldd;
        LSeg(n,ld) misses C by A1,A48,A89,A110,A146,Lm78;
        then
A151:   LSeg(n,ld) misses Jd by A4,XBOOLE_1:7,63;
A152:   rng K1 = rng PCX \/ rng Palpha \/ rng Pnld \/ rng Pldd by Lm11;
        then
A153:   rng PJd misses rng K1 by A114,A117,A118,A141,A143,A147,A148,A151,Lm3;
A154:   LSeg(ld,d) c= R by Lm65,Lm68,XBOOLE_1:1;
        LSeg(n,ld) c= R by A150,Lm65,XBOOLE_1:1;
        then rng K1 c= the carrier of TR
        by A76,A118,A121,A142,A147,A148,A152,A154,Lm2;
        then reconsider v = K1 as Path of CR,DR by Th30;
        consider s, t being Point of I[01] such that
A155:   H.s = v.t by Lm18,Lm19,Lm23,Lm25,JGRAPH_8:6;
        dom H = the carrier of I[01] & dom v = the carrier of I[01]
        by FUNCT_2:def 1;
        then H.s in rng PJd & v.t in rng K1 by FUNCT_1:def 5;
        hence contradiction by A153,A155,XBOOLE_0:3;
      end;
      suppose
A156:   n in LSeg(ld,d);
        consider Pnd being Path of n,d,
        fnd being Function of I[01], T2|LSeg(n,d) such that
A157:   rng fnd = LSeg(n,d) & Pnd = fnd by Th43;
        set K1 = PCX + Palpha + Pnd;
        ld in LSeg(ld,d) by TOPREAL1:6;
        then
A158:   ld`2 = n`2 by A156,Lm49,SPPOL_1:def 2;
        then
A159:   LSeg(n,d) is horizontal by Lm25,Lm29,SPPOL_1:36;
        ld`1 <= n`1 & n`1 <= d`1 by A156,Lm24,Lm28,JGRAPH_6:11;
        then
A160:   LSeg(n,d) c= LSeg(ld,d) by A158,A159,Lm49,GOBOARD7:66;
        then
A161:   LSeg(n,d) c= dR by Lm68,XBOOLE_1:1;
        LSeg(n,d) misses C by A1,A160,Lm74,XBOOLE_1:63;
        then
A162:   LSeg(n,d) misses Jd by A4,XBOOLE_1:7,63;
A163:   rng K1 = rng PCX \/ rng Palpha \/ rng Pnd by Th40;
        then
A164:   rng K1 misses Jd by A114,A118,A141,A157,A162,XBOOLE_1:114;
        LSeg(n,d) c= R by A161,Lm65,XBOOLE_1:1;
        then rng K1 c= the carrier of TR by A76,A118,A121,A142,A157,A163,Lm1;
        then reconsider v = K1 as Path of CR,DR by Th30;
        consider s, t being Point of I[01] such that
A165:   H.s = v.t by Lm18,Lm19,Lm23,Lm25,JGRAPH_8:6;
        dom H = the carrier of I[01] & dom v = the carrier of I[01]
        by FUNCT_2:def 1;
        then H.s in rng PJd & v.t in rng K1 by FUNCT_1:def 5;
        hence contradiction by A117,A164,A165,XBOOLE_0:3;
      end;
      suppose
A166:   n in LSeg(d,pd);
        consider Pnd being Path of n,d,
        fnd being Function of I[01], T2|LSeg(n,d) such that
A167:   rng fnd = LSeg(n,d) & Pnd = fnd by Th43;
        set K1 = PCX + Palpha + Pnd;
        pd in LSeg(pd,d) by TOPREAL1:6;
        then pd`2 = n`2 by A166,Lm50,SPPOL_1:def 2;
        then
A168:   LSeg(n,d) is horizontal by Lm25,Lm33,SPPOL_1:36;
A169:   d`2 = d`2;
        d`1 <= n`1 & n`1 <= pd`1 by A166,Lm24,Lm32,JGRAPH_6:11;
        then
A170:   LSeg(n,d) c= LSeg(pd,d) by A168,A169,Lm50,GOBOARD7:66;
        then
A171:   LSeg(n,d) c= dR by Lm69,XBOOLE_1:1;
        LSeg(n,d) misses C by A1,A170,Lm75,XBOOLE_1:63;
        then
A172:   LSeg(n,d) misses Jd by A4,XBOOLE_1:7,63;
A173:   rng K1 = rng PCX \/ rng Palpha \/ rng Pnd by Th40;
        then
A174:   rng K1 misses Jd by A114,A118,A141,A167,A172,XBOOLE_1:114;
        LSeg(n,d) c= R by A171,Lm65,XBOOLE_1:1;
        then rng K1 c= the carrier of TR by A76,A118,A121,A142,A167,A173,Lm1;
        then reconsider v = K1 as Path of CR,DR by Th30;
        consider s, t being Point of I[01] such that
A175:   H.s = v.t by Lm18,Lm19,Lm23,Lm25,JGRAPH_8:6;
        dom H = the carrier of I[01] & dom v = the carrier of I[01]
        by FUNCT_2:def 1;
        then H.s in rng PJd & v.t in rng K1 by FUNCT_1:def 5;
        hence contradiction by A117,A174,A175,XBOOLE_0:3;
      end;
      suppose
A176:   n in LSeg(pd,b);
        consider Pnpd being Path of n,pd,
        fnpd being Function of I[01], T2|LSeg(n,pd) such that
A177:   rng fnpd = LSeg(n,pd) & Pnpd = fnpd by Th43;
        consider Ppdd being Path of pd,d,
        fpdd being Function of I[01], T2|LSeg(pd,d) such that
A178:   rng fpdd = LSeg(pd,d) & Ppdd = fpdd by Th43;
A179:   pd`1 = n`1 by A123,A176,Lm44,Lm58,SPPOL_1:def 3;
        then LSeg(n,pd) is vertical by SPPOL_1:37;
        then LSeg(n,pd) c= LSeg(pd,pg) by A119,A179,Lm31,Lm33,Lm44,GOBOARD7:65;
        then
A180:   LSeg(n,pd) c= dR by Lm41,XBOOLE_1:1;
        set K1 = PCX + Palpha + Pnpd + Ppdd;
        LSeg(n,pd) misses C by A1,A48,A89,A110,A176,Lm79;
        then
A181:   LSeg(n,pd) misses Jd by A4,XBOOLE_1:7,63;
A182:   rng K1 = rng PCX \/ rng Palpha \/ rng Pnpd \/ rng Ppdd by Lm11;
        then
A183:   rng PJd misses rng K1 by A114,A117,A118,A141,A144,A177,A178,A181,Lm3;
A184:   LSeg(pd,d) c= R by Lm65,Lm69,XBOOLE_1:1;
        LSeg(n,pd) c= R by A180,Lm65,XBOOLE_1:1;
        then rng K1 c= the carrier of TR
        by A76,A118,A121,A142,A177,A178,A182,A184,Lm2;
        then reconsider v = K1 as Path of CR,DR by Th30;
        consider s, t being Point of I[01] such that
A185:   H.s = v.t by Lm18,Lm19,Lm23,Lm25,JGRAPH_8:6;
        dom H = the carrier of I[01] & dom v = the carrier of I[01]
        by FUNCT_2:def 1;
        then H.s in rng PJd & v.t in rng K1 by FUNCT_1:def 5;
        hence contradiction by A183,A185,XBOOLE_0:3;
      end;
    end;
    suppose
A186: n`2 >= 0;
      per cases by A109,A186,Lm70;
      suppose
A187:   n in LSeg(a,lg);
        consider Pnlg being Path of n,lg,
        fnlg being Function of I[01], T2|LSeg(n,lg) such that
A188:   rng fnlg = LSeg(n,lg) & Pnlg = fnlg by Th43;
        consider Plgc being Path of lg,c,
        flgc being Function of I[01], T2|LSeg(lg,c) such that
A189:   rng flgc = LSeg(lg,c) & Plgc = flgc by Th43;
A190:   ld`1 = n`1 by A122,A187,Lm43,Lm55,SPPOL_1:def 3;
        then LSeg(n,lg) is vertical by Lm26,Lm28,SPPOL_1:37;
        then LSeg(n,lg) c= LSeg(ld,lg) by A119,A190,Lm27,Lm29,Lm43,GOBOARD7:65;
        then
A191:   LSeg(n,lg) c= dR by Lm39,XBOOLE_1:1;
        set K1 = Pdx + Palpha + Pnlg + Plgc;
        LSeg(n,lg) misses C by A1,A48,A89,A110,A187,Lm76;
        then
A192:   LSeg(n,lg) misses Jc by A4,XBOOLE_1:7,63;
A193:   rng K1 = rng Pdx \/ rng Palpha \/ rng Pnlg \/ rng Plgc by Lm11;
        then
A194:   rng K1 misses Jc by A100,A113,A115,A118,A124,A188,A189,A192,Lm3;
A195:   rng K1 = rng -K1 by Th32;
        LSeg(n,lg) c= R by A191,Lm65,XBOOLE_1:1;
        then rng K1 c= the carrier of TR
        by A76,A115,A118,A120,A121,A125,A188,A189,A193,Lm2;
        then reconsider v = -K1 as Path of CR,DR by A195,Th30;
        consider s, t being Point of I[01] such that
A196:   h.s = v.t by Lm18,Lm19,Lm23,Lm25,JGRAPH_8:6;
        dom h = the carrier of I[01] & dom v = the carrier of I[01]
        by FUNCT_2:def 1;
        then h.s in rng PJc & v.t in rng -K1 by FUNCT_1:def 5;
        hence contradiction by A116,A194,A195,A196,XBOOLE_0:3;
      end;
      suppose
A197:   n in LSeg(lg,c);
        consider Pnc being Path of n,c,
        fnc being Function of I[01], T2|LSeg(n,c) such that
A198:   rng fnc = LSeg(n,c) & Pnc = fnc by Th43;
        set K1 = Pdx + Palpha + Pnc;
        lg in LSeg(lg,c) by TOPREAL1:6;
        then
A199:   lg`2 = n`2 by A197,Lm51,SPPOL_1:def 2;
        then
A200:   LSeg(n,c) is horizontal by Lm23,Lm27,SPPOL_1:36;
        lg`1 <= n`1 & n`1 <= c`1 by A197,Lm22,Lm26,JGRAPH_6:11;
        then
A201:   LSeg(n,c) c= LSeg(lg,c) by A199,A200,Lm51,GOBOARD7:66;
        then
A202:   LSeg(n,c) c= dR by Lm66,XBOOLE_1:1;
        LSeg(n,c) misses C by A1,A201,Lm72,XBOOLE_1:63;
        then
A203:   LSeg(n,c) misses Jc by A4,XBOOLE_1:7,63;
A204:   rng K1 = rng Pdx \/ rng Palpha \/ rng Pnc by Th40;
        then
A205:   rng K1 misses Jc by A100,A113,A115,A118,A198,A203,XBOOLE_1:114;
A206:   rng K1 = rng -K1 by Th32;
        LSeg(n,c) c= R by A202,Lm65,XBOOLE_1:1;
        then rng K1 c= the carrier of TR
        by A76,A115,A118,A120,A121,A198,A204,Lm1;
        then reconsider v = -K1 as Path of CR,DR by A206,Th30;
        consider s, t being Point of I[01] such that
A207:   h.s = v.t by Lm18,Lm19,Lm23,Lm25,JGRAPH_8:6;
        dom h = the carrier of I[01] & dom v = the carrier of I[01]
        by FUNCT_2:def 1;
        then h.s in rng PJc & v.t in rng -K1 by FUNCT_1:def 5;
        hence contradiction by A116,A205,A206,A207,XBOOLE_0:3;
      end;
      suppose
A208:   n in LSeg(c,pg);
        consider Pnc being Path of n,c,
        fnc being Function of I[01], T2|LSeg(n,c) such that
A209:   rng fnc = LSeg(n,c) & Pnc = fnc by Th43;
        set K1 = Pdx + Palpha + Pnc;
        pg in LSeg(pg,c) by TOPREAL1:6;
        then pg`2 = n`2 by A208,Lm52,SPPOL_1:def 2;
        then
A210:   LSeg(n,c) is horizontal by Lm23,Lm31,SPPOL_1:36;
A211:   c`2 = c`2;
        c`1 <= n`1 & n`1 <= pg`1 by A208,Lm22,Lm30,JGRAPH_6:11;
        then
A212:   LSeg(c,n) c= LSeg(c,pg) by A210,A211,Lm52,GOBOARD7:66;
        then
A213:   LSeg(n,c) c= dR by Lm67,XBOOLE_1:1;
        LSeg(n,c) misses C by A1,A212,Lm73,XBOOLE_1:63;
        then
A214:   LSeg(n,c) misses Jc by A4,XBOOLE_1:7,63;
A215:   rng K1 = rng Pdx \/ rng Palpha \/ rng Pnc by Th40;
        then
A216:   rng K1 misses Jc by A100,A113,A115,A118,A209,A214,XBOOLE_1:114;
A217:   rng K1 = rng -K1 by Th32;
        LSeg(n,c) c= R by A213,Lm65,XBOOLE_1:1;
        then rng K1 c= the carrier of TR
        by A76,A115,A118,A120,A121,A209,A215,Lm1;
        then reconsider v = -K1 as Path of CR,DR by A217,Th30;
        consider s, t being Point of I[01] such that
A218:   h.s = v.t by Lm18,Lm19,Lm23,Lm25,JGRAPH_8:6;
        dom h = the carrier of I[01] & dom v = the carrier of I[01]
        by FUNCT_2:def 1;
        then h.s in rng PJc & v.t in rng -K1 by FUNCT_1:def 5;
        hence contradiction by A116,A216,A217,A218,XBOOLE_0:3;
      end;
      suppose
A219:   n in LSeg(pg,b);
        consider Pnpg being Path of n,pg,
        fnpg being Function of I[01], T2|LSeg(n,pg) such that
A220:   rng fnpg = LSeg(n,pg) & Pnpg = fnpg by Th43;
        consider Ppgc being Path of pg,c,
        fpgc being Function of I[01], T2|LSeg(pg,c) such that
A221:   rng fpgc = LSeg(pg,c) & Ppgc = fpgc by Th43;
A222:   pd`1 = n`1 by A123,A219,Lm44,Lm57,SPPOL_1:def 3;
        then LSeg(n,pg) is vertical by Lm30,Lm32,SPPOL_1:37;
        then LSeg(n,pg) c= LSeg(pd,pg) by A119,A222,Lm31,Lm33,Lm44,GOBOARD7:65;
        then
A223:   LSeg(n,pg) c= dR by Lm41,XBOOLE_1:1;
        set K1 = Pdx + Palpha + Pnpg + Ppgc;
        LSeg(n,pg) misses C by A1,A48,A89,A110,A219,Lm77;
        then
A224:   LSeg(n,pg) misses Jc by A4,XBOOLE_1:7,63;
A225:   rng K1 = rng Pdx \/ rng Palpha \/ rng Pnpg \/ rng Ppgc by Lm11;
        then
A226:   rng K1 misses Jc by A100,A113,A115,A118,A127,A220,A221,A224,Lm3;
A227:   rng K1 = rng -K1 by Th32;
        LSeg(n,pg) c= R by A223,Lm65,XBOOLE_1:1;
        then rng K1 c= the carrier of TR
        by A76,A115,A118,A120,A121,A126,A220,A221,A225,Lm2;
        then reconsider v = -K1 as Path of CR,DR by A227,Th30;
        consider s, t being Point of I[01] such that
A228:   h.s = v.t by Lm18,Lm19,Lm23,Lm25,JGRAPH_8:6;
        dom h = the carrier of I[01] & dom v = the carrier of I[01]
        by FUNCT_2:def 1;
        then h.s in rng PJc & v.t in rng -K1 by FUNCT_1:def 5;
        hence contradiction by A116,A226,A227,A228,XBOOLE_0:3;
      end;
    end;
  end;

:: uniqueness
  let V be Subset of T2;
  assume
A229: V is_inside_component_of C;
  assume
A230: V <> Ux;
  consider VP being Subset of T2|C` such that
A231: VP = V and
A232: VP is_a_component_of T2|C` and
  VP is bounded Subset of Euclid 2 by A229,JORDAN2C:17;
  reconsider T2C = T2|C` as non empty SubSpace of T2;
  VP <> {}(T2|C`) by A232,CONNSP_1:34;
  then reconsider VP as non empty Subset of T2C;
A233: V misses C by A48,A231,SUBSET_1:43;
  consider Pjd being Path of j,d,
  fjd being Function of I[01], T2|LSeg(j,d) such that
A234: rng fjd = LSeg(j,d) & Pjd = fjd by Th43;
  consider Plk being Path of l,k,
  flk being Function of I[01], T2|LSeg(l,k) such that
A235: rng flk = LSeg(l,k) & Plk = flk by Th43;
  set beta = Pcm + Pml + Plk + Pkj + Pjd;
A236: rng beta = rng Pcm \/ rng Pml \/ rng Plk \/ rng Pkj \/ rng Pjd by Lm13;
  dom beta = [#]I[01] by FUNCT_2:def 1;
  then beta.:dom beta is compact by WEIERSTR:14;
  then rng beta is compact by RELAT_1:146;
  then
A237: rng beta is closed by COMPTS_1:16;
A238: ml misses V by A18,A25,A233,XBOOLE_1:1,63;
  {l,k} c= LSeg(l,k)
  proof
    let x be set;
    assume x in {l,k};
    then x = l or x = k by TARSKI:def 2;
    hence thesis by TOPREAL1:6;
  end;
  then
A239: LSeg(l,k) = LSeg(l,k) \ {l,k} \/ {l,k} by XBOOLE_1:45;
A240: LSeg(l,k) \ {l,k} misses V
  proof
    assume not thesis;
    then consider q being set such that
A241: q in LSeg(l,k) \ {l,k} and
A242: q in V by XBOOLE_0:3;
    V meets Ux by A10,A74,A241,A242,XBOOLE_0:3;
    hence contradiction by A10,A47,A230,A231,A232,CONNSP_1:37;
  end;
  not l in V & not k in V by A16,A17,A18,A39,A233,XBOOLE_0:3;
  then {l,k} misses V by ZFMISC_1:57;
  then
A243: LSeg(l,k) misses V by A239,A240,XBOOLE_1:70;
A244: kj misses V by A45,A233,XBOOLE_1:63;
A245: LSeg(j,d) misses V by A1,A229,Th90;
  LSeg(c,m) misses V by A1,A229,Th89;
  then LSeg(c,m) \/ ml \/ LSeg(l,k) misses V by A238,A243,XBOOLE_1:114;
  then
A246: rng beta misses V by A77,A234,A235,A236,A244,A245,XBOOLE_1:114;
A247: m = |[m`1,m`2]| by EUCLID:57;
A248: c = |[c`1,c`2]| by EUCLID:57;
A249: j = |[j`1,j`2]| by EUCLID:57;
A250: not a in LSeg(c,m) by A12,A13,A20,A22,A23,A247,A248,Lm18,JGRAPH_6:9;
  not a in ml by A26,ZFMISC_1:55;
  then
A251: not a in LSeg(c,m) \/ ml by A250,XBOOLE_0:def 2;
  not a in LSeg(l,k) by A21,A42,A51,A53,Lm18,JGRAPH_6:9;
  then
A252: not a in LSeg(c,m) \/ ml \/ LSeg(l,k) by A251,XBOOLE_0:def 2;
  not a in kj by A44,ZFMISC_1:55;
  then
A253: not a in LSeg(c,m) \/ ml \/ LSeg(l,k) \/ kj by A252,XBOOLE_0:def 2;
  not a in LSeg(j,d) by A30,A31,A52,A249,Lm18,Lm24,JGRAPH_6:9;
  then not a in rng beta by A77,A234,A235,A236,A253,XBOOLE_0:def 2;
  then consider ra being positive (real number) such that
A254: Ball(a,ra) misses rng beta by A237,Th25;
A255: not b in LSeg(c,m) by A12,A13,A20,A22,A23,A247,A248,Lm19,JGRAPH_6:9;
  not b in ml by A26,ZFMISC_1:55;
  then
A256: not b in LSeg(c,m) \/ ml by A255,XBOOLE_0:def 2;
  not b in LSeg(l,k) by A21,A42,A51,A53,Lm19,JGRAPH_6:9;
  then
A257: not b in LSeg(c,m) \/ ml \/ LSeg(l,k) by A256,XBOOLE_0:def 2;
  not b in kj by A44,ZFMISC_1:55;
  then
A258: not b in LSeg(c,m) \/ ml \/ LSeg(l,k) \/ kj by A257,XBOOLE_0:def 2;
  not b in LSeg(j,d) by A30,A31,A52,A249,Lm19,Lm24,JGRAPH_6:9;
  then not b in rng beta by A77,A234,A235,A236,A258,XBOOLE_0:def 2;
  then consider rb being positive (real number) such that
A259: Ball(b,rb) misses rng beta by A237,Th25;
  set A = Ball(a,ra), B = Ball(b,rb);
A260: a in A by Th16;
A261: b in B by Th16;
  VP is non empty;
  then consider t being set such that
A262: t in V by A231,XBOOLE_0:def 1;
  V in {W where W is Subset of T2: W is_inside_component_of C} by A229;
  then t in BDD C by A262,TARSKI:def 4;
  then
A263: C = Fr V by A231,A232,Lm17;
  then a in Cl V by A14,XBOOLE_0:def 3;
  then A meets V by A260,PRE_TOPC:def 13;
  then consider u being set such that
A264: u in A and
A265: u in V by XBOOLE_0:3;
  b in Cl V by A14,A263,XBOOLE_0:def 3;
  then B meets V by A261,PRE_TOPC:def 13;
  then consider v being set such that
A266: v in B and
A267: v in V by XBOOLE_0:3;
  reconsider u, v as Point of T2 by A264,A266;
A268: the carrier of T2C|VP = VP by PRE_TOPC:29;
  reconsider u1 = u, v1 = v as Point of T2C|VP by A231,A265,A267,PRE_TOPC:29;
  T2C|VP is arcwise_connected by A232,Th69;
  then
A269: u1,v1 are_connected by BORSUK_2:def 3;
  then consider fuv being Function of I[01], T2C|VP such that
A270: fuv is continuous & fuv.0 = u1 & fuv.1 = v1 by BORSUK_2:def 1;
A271: T2C|VP = T2|V by A231,GOBOARD9:4;
  fuv is Path of u1,v1 by A269,A270,BORSUK_2:def 2;
  then reconsider uv = fuv as Path of u,v by A269,A271,TOPALG_2:1;
A272: rng fuv c= the carrier of T2C|VP;
  then
A273: rng uv misses rng beta by A231,A246,A268,XBOOLE_1:63;
  consider au being Path of a,u,
  fau being Function of I[01], T2|LSeg(a,u) such that
A274: rng fau = LSeg(a,u) & au = fau by Th43;
  consider vb being Path of v,b,
  fvb being Function of I[01], T2|LSeg(v,b) such that
A275: rng fvb = LSeg(v,b) & vb = fvb by Th43;
  set AB = au + uv + vb;
A276: rng AB = rng au \/ rng uv \/ rng vb by Th40;
  a in A by Th16;
  then LSeg(a,u) c= A by A264,JORDAN1:def 1;
  then
A277: LSeg(a,u) misses rng beta by A254,XBOOLE_1:63;
  b in B by Th16;
  then LSeg(v,b) c= B by A266,JORDAN1:def 1;
  then LSeg(v,b) misses rng beta by A259,XBOOLE_1:63;
  then
A278: rng AB misses rng beta by A273,A274,A275,A276,A277,XBOOLE_1:114;
A279: a,b are_connected by BORSUK_2:def 3;
A280: V c= R by A1,A229,Th93;
  then
A281: LSeg(a,u) c= R by A11,A14,A265,JORDAN1:def 1;
A282: LSeg(v,b) c= R by A11,A14,A267,A280,JORDAN1:def 1;
  rng uv c= R by A231,A268,A272,A280,XBOOLE_1:1;
  then LSeg(a,u) \/ rng uv c= R by A281,XBOOLE_1:8;
  then rng AB c= the carrier of TR by A76,A274,A275,A276,A282,XBOOLE_1:8;
  then reconsider h = AB as Path of AR,BR by A279,Th29;
A283: c,d are_connected by BORSUK_2:def 3;
  LSeg(c,m) \/ ml c= R by A78,A79,XBOOLE_1:8;
  then
A284: LSeg(c,m) \/ ml \/ LSeg(l,k) c= R by A75,XBOOLE_1:8;
  kj c= R by A11,A45,XBOOLE_1:1;
  then
A285: LSeg(c,m) \/ ml \/ LSeg(l,k) \/ kj c= R by A284,XBOOLE_1:8;
  LSeg(j,d) c= R by A11,A28,Lm61,Lm65,JORDAN1:def 1;
  then rng beta c= the carrier of TR by A76,A77,A234,A235,A236,A285,XBOOLE_1:8;
  then reconsider v = beta as Path of CR,DR by A283,Th29;
  consider s, t being Point of I[01] such that
A286: h.s = v.t by Lm18,Lm19,Lm23,Lm25,JGRAPH_8:6;
  dom h = the carrier of I[01] & dom v = the carrier of I[01]
  by FUNCT_2:def 1;
  then h.s in rng AB & v.t in rng beta by FUNCT_1:def 5;
  hence contradiction by A278,A286,XBOOLE_0:3;
end;

theorem Th96:
  |[-1,0]|,|[1,0]| realize-max-dist-in C implies
  for Jc, Jd being compact with_the_max_arc Subset of TOP-REAL 2 st
  Jc is_an_arc_of |[-1,0]|,|[1,0]| & Jd is_an_arc_of |[-1,0]|,|[1,0]| &
  C = Jc \/ Jd & Jc /\ Jd = {|[-1,0]|,|[1,0]|} & UMP C in Jc & LMP C in Jd &
  W-bound C = W-bound Jc & E-bound C = E-bound Jc holds
  BDD C = Component_of Down ((1/2) *
  ((UMP (LSeg(LMP Jc,|[0,-3]|) /\ Jd)) + LMP Jc), C`)
proof
  assume
A1: a,b realize-max-dist-in C;
  let Jc, Jd being compact with_the_max_arc Subset of T2 such that
A2: Jc is_an_arc_of a,b and
A3: Jd is_an_arc_of a,b and
A4: C = Jc \/ Jd and
A5: Jc /\ Jd = {a,b} and
A6: UMP C in Jc and
A7: LMP C in Jd and
A8: W-bound C = W-bound Jc and
A9: E-bound C = E-bound Jc;
  reconsider
  Ux = Component_of Down((1/2) * ((UMP (LSeg(LMP Jc,d) /\ Jd)) + LMP Jc),C`)
  as Subset of T2 by PRE_TOPC:39;
  Ux = BDD C
  proof
    Ux is_inside_component_of C by A1,A2,A3,A4,A5,A6,A7,A8,A9,Th95;
    hence Ux c= BDD C by JORDAN2C:26;
    set F = {B where B is Subset of T2: B is_inside_component_of C};
    let q be set;
    assume q in BDD C;
    then consider Z being set such that
A10: q in Z and
A11: Z in F by TARSKI:def 4;
    ex B being Subset of T2 st Z = B & B is_inside_component_of C by A11;
    hence thesis by A1,A2,A3,A4,A5,A6,A7,A8,A9,A10,Th95;
  end;
  hence thesis;
end;

Lm85: |[-1,0]|,|[1,0]| realize-max-dist-in C implies C is Jordan
proof
  assume
A1: a,b realize-max-dist-in C;
  then consider Jc, Jd being compact with_the_max_arc Subset of T2 such that
A2: Jc is_an_arc_of a,b and
A3: Jd is_an_arc_of a,b and
A4: C = Jc \/ Jd and
A5: Jc /\ Jd = {a,b} and
A6: UMP C in Jc and
A7: LMP C in Jd and
A8: W-bound C = W-bound Jc and
A9: E-bound C = E-bound Jc by Lm84;
  set l = LMP Jc;
  set LJ = LSeg(l,d) /\ Jd;
  set k = UMP LJ;
  set x = (1/2)*(k+l);
A10: Component_of Down(x,C`) is_a_component_of T2|C` by CONNSP_1:43;
A11: Component_of Down(x,C`) = BDD C by A1,A2,A3,A4,A5,A6,A7,A8,A9,Th96;
  thus C` <> {};
  take A1 = UBD C, A2 = BDD C;
  thus C` = A1 \/ A2 by JORDAN2C:31;
  thus A1 misses A2 by JORDAN2C:28;
A12: Component_of Down(x,C`) <> {}(T2|C`) by A10,CONNSP_1:34;
  A1 is_a_component_of C` by JORDAN2C:132;
  then
A13: ex B1 being Subset of T2|C` st B1 = A1 & B1 is_a_component_of T2 |C `
  by CONNSP_1:def 6;
  then
A14: C = Fr A1 by A11,A12,Lm17
    .= Cl A1 /\ Cl A1`;
A15: C = Fr A2 by A10,A11,A12,Lm17
    .= Cl A2 /\ Cl A2`;
  A2 c= C` by JORDAN2C:29;
  then C misses A2 by SUBSET_1:43;
  then
A16: C c= Cl A2 \ A2 by A15,XBOOLE_1:17,86;
A17: A1 misses A2 by JORDAN2C:28;
  then A2 c= A1` by SUBSET_1:43;
  then
A18: Cl A2 c= A1` by TOPS_1:31;
  A1 \/ A2 = C` by JORDAN2C:31;
  then A1 \/ A2 misses C by SUBSET_1:43;
  then C misses A1 by XBOOLE_1:70;
  then
A19: A2 \/ C misses A1 by A17,XBOOLE_1:70;
  A2 \/ A1 = C` by JORDAN2C:31;
  then (A2 \/ A1)` misses C` by SUBSET_1:43;
  then (A2 \/ A1)` /\ C` = {} by XBOOLE_0:def 7;
  then (A2 \/ A1 \/ C)` = {} by XBOOLE_1:53;
  then ((A2 \/ C) \/ A1)` = {} by XBOOLE_1:4;
  then (A2 \/ C)` /\ A1` = {} by XBOOLE_1:53;
  then (A2 \/ C)` misses A1` by XBOOLE_0:def 7;
  then Cl A2 c= A2 \/ C by A18,A19,SUBSET_1:46;
  then
A20: Cl A2 \ A2 c= C by XBOOLE_1:43;
  A1 c= C` by JORDAN2C:30;
  then C misses A1 by SUBSET_1:43;
  then
A21: C c= Cl A1 \ A1 by A14,XBOOLE_1:17,86;
  A1 c= A2` by A17,SUBSET_1:43;
  then
A22: Cl A1 c= A2` by TOPS_1:31;
  A2 \/ A1 = C` by JORDAN2C:31;
  then A2 \/ A1 misses C by SUBSET_1:43;
  then C misses A2 by XBOOLE_1:70;
  then
A23: A1 \/ C misses A2 by A17,XBOOLE_1:70;
  A1 \/ A2 = C` by JORDAN2C:31;
  then (A1 \/ A2)` misses C` by SUBSET_1:43;
  then (A1 \/ A2)` /\ C` = {} by XBOOLE_0:def 7;
  then (A1 \/ A2 \/ C)` = {} by XBOOLE_1:53;
  then ((A1 \/ C) \/ A2)` = {} by XBOOLE_1:4;
  then (A1 \/ C)` /\ A2` = {} by XBOOLE_1:53;
  then (A1 \/ C)` misses A2` by XBOOLE_0:def 7;
  then Cl A1 c= A1 \/ C by A22,A23,SUBSET_1:46;
  then Cl A1 \ A1 c= C by XBOOLE_1:43;
  hence (Cl A1) \ A1 = C by A21,XBOOLE_0:def 10
    .= (Cl A2) \ A2 by A16,A20,XBOOLE_0:def 10;
  thus thesis by A11,A13,CONNSP_1:43;
end;

Lm86: C is Jordan
proof
  consider f being Homeomorphism of T2 such that
A1: a,b realize-max-dist-in f.:C by JORDAN24:7;
A2: f" is Homeomorphism of T2 by TOPGRP_1:30;
  f.:C is Simple_closed_curve by Th70;
  then f.:C is Jordan by A1,Lm85;
  then
A3: f".:(f.:C) is Jordan by A2,JORDAN24:16;
  rng f = [#]T2 by TOPS_2:def 5;
  then
A4: f" = f qua Function" by TOPS_2:def 4;
  dom f = the carrier of T2 by FUNCT_2:def 1;
  hence thesis by A3,A4,JGRAPH_6:22;
end;

registration :: do not remove it
  let C;
  cluster BDD C -> non empty;
  coherence
  proof
    C is Jordan by Lm86;
    then BDD C is_inside_component_of C by JORDAN2C:116;
    then BDD C is_a_component_of C` by JORDAN2C:def 3;
    then ex B1 being Subset of T2|C` st B1 = BDD C & B1 is_a_component_of T2|C
    ` by CONNSP_1:def 6;
    then BDD C <> {}(T2|C`) by CONNSP_1:34;
    hence thesis;
  end;
end;

theorem
  U = P & U is_a_component_of (TOP-REAL 2)|C` implies C = Fr P
proof
  BDD C is non empty;
  hence thesis by Lm17;
end;

theorem :: Jordan's Curve Theorem
  for C being Simple_closed_curve ex A1, A2 being Subset of TOP-REAL 2 st
  C` = A1 \/ A2 & A1 misses A2 & (Cl A1) \ A1 = (Cl A2) \ A2 &
  for C1, C2 being Subset of (TOP-REAL 2)|C` st C1 = A1 & C2 = A2 holds
  C1 is_a_component_of (TOP-REAL 2)|C` & C2 is_a_component_of (TOP-REAL 2)|C`
proof
  let C;
  C is Jordan by Lm86;
  hence thesis by JORDAN1:def 2;
end;

theorem
  for C being Simple_closed_curve holds C is Jordan by Lm86;

