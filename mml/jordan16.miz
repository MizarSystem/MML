:: On the Decomposition of a Simple Closed Curve into Two Arcs
::  by Andrzej Trybulec and Yatsuka Nakamura
::
:: Received September 16, 2002
:: Copyright (c) 2002 Association of Mizar Users
 
environ

 vocabularies NUMBERS, TOPREAL2, SUBSET_1, EUCLID, PRE_TOPC, ZFMISC_1, TARSKI,
      XBOOLE_0, JORDAN6, PSCOMP_1, TOPREAL1, JORDAN3, FUNCT_1, ORDINAL2,
      CONNSP_2, RCOMP_1, RELAT_1, TOPS_2, FUNCT_2, STRUCT_0, BORSUK_1, CARD_1,
      REAL_1, ARYTM_3, XXREAL_0, XXREAL_1, PCOMPS_1, XREAL_0, ORDINAL1,
      JGRAPH_2, PARTFUN1, ARYTM_1, TOPMETR;
 notations TARSKI, XBOOLE_0, ENUMSET1, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0,
      XXREAL_0, XREAL_0, REAL_1, RELAT_1, FUNCT_1, RELSET_1, PARTFUN1, FUNCT_2,
      FCONT_1, REALSET1, STRUCT_0, PRE_TOPC, TOPS_2, COMPTS_1, CONNSP_2,
      PCOMPS_1, RCOMP_1, TMAP_1, EUCLID, PSCOMP_1, TOPMETR, JORDAN6, TOPREAL1,
      JORDAN5C, TOPREAL2, JORDAN7;
 constructors REAL_1, RCOMP_1, FCONT_1, REALSET1, TOPS_2, TMAP_1, TOPMETR,
      PCOMPS_1, TOPREAL1, PSCOMP_1, JORDAN5C, JORDAN6, JORDAN7, SEQ_1;
 registrations XBOOLE_0, FUNCT_1, RELSET_1, FUNCT_2, NUMBERS, XXREAL_0,
      XREAL_0, MEMBERED, STRUCT_0, PRE_TOPC, BORSUK_1, EUCLID, TOPMETR,
      TOPREAL2, VALUED_0, FCONT_1, TMAP_1;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, TOPS_2, TOPREAL1, FUNCT_1, XBOOLE_0, BORSUK_1, STRUCT_0;
 theorems PRE_TOPC, TOPMETR, XREAL_0, XBOOLE_0, FUNCT_1, XBOOLE_1, FUNCT_2,
      TOPREAL5, TREAL_1, BORSUK_1, JORDAN6, TOPREAL1, TOPS_2, TARSKI, RELAT_1,
      TOPREAL3, JORDAN5B, TOPMETR3, TOPREAL2, ENUMSET1, ZFMISC_1, JORDAN5A,
      JORDAN5C, FCONT_1, FUNCT_3, CONNSP_2, TMAP_1, RFUNCT_2, RELSET_1,
      FUNCT_4, JORDAN7, SPRECT_1, XCMPLX_0, XCMPLX_1, XREAL_1, XXREAL_0,
      XXREAL_1, EUCLID;
 schemes FUNCT_2;

begin

reserve C for Simple_closed_curve,
  A,A1,A2 for Subset of TOP-REAL 2,
  p,p1,p2,q ,q1,q2 for Point of TOP-REAL 2,
  n for Element of NAT;

registration
  let n;
  cluster trivial Subset of TOP-REAL n;
  existence
  proof
    consider A being trivial Subset of TOP-REAL n;
    A is Subset of TOP-REAL n;
    hence thesis;
  end;
end;

canceled;

theorem
  for a,b,c,X being set st a in X & b in X & c in X holds {a,b,c} c= X
proof
  let a,b,c,X be set;
  assume a in X & b in X & c in X;
  then {a,b} c= X & {c} c= X by ZFMISC_1:37,38;
  then {a,b} \/ {c} c= X by XBOOLE_1:8;
  hence thesis by ENUMSET1:43;
end;

canceled;

theorem
  Lower_Arc C <> Upper_Arc C
proof
  assume Lower_Arc C = Upper_Arc C;
  then
A1: Lower_Arc C =(C\Lower_Arc C) \/ {W-min C, E-max C} by JORDAN6:66;
  Lower_Arc C is_an_arc_of W-min C, E-max C by JORDAN6:65;
  then
A2: ex p3 being Point of TOP-REAL 2 st p3 in Lower_Arc C & p3<>W-min C & p3
  <>E-max C by JORDAN6:55;
  Lower_Arc C misses C\Lower_Arc C by XBOOLE_1:79;
  then Lower_Arc C c= {W-min C, E-max C} by A1,XBOOLE_1:73;
  hence contradiction by A2,TARSKI:def 2;
end;

theorem Th5:
  Segment(A,p1,p2,q1,q2) c= A
proof
  Segment(A,p1,p2,q1,q2) = R_Segment(A,p1,p2,q1) /\ L_Segment(A,p1,p2,q2)
  by JORDAN6:def 5;
  then
  R_Segment(A,p1,p2,q1) c= A & Segment(A,p1,p2,q1,q2) c= R_Segment(A,p1,p2
  ,q1) by JORDAN6:21,XBOOLE_1:17;
  hence thesis by XBOOLE_1:1;
end;

canceled;

theorem Th7:
  q in A implies q in L_Segment(A,p1,p2,q)
proof
  assume q in A;
  then
A1: LE q,q,A,p1,p2 by JORDAN5C:9;
  L_Segment(A,p1,p2,q) = {q1 : LE q1,q,A,p1,p2} by JORDAN6:def 3;
  hence thesis by A1;
end;

theorem Th8:
  q in A implies q in R_Segment(A,p1,p2,q)
proof
  assume q in A;
  then
A1: LE q,q,A,p1,p2 by JORDAN5C:9;
  R_Segment(A,p1,p2,q) = {q1 : LE q,q1,A,p1,p2} by JORDAN6:def 4;
  hence thesis by A1;
end;

theorem Th9:
  LE q1, q2, A, p1, p2 implies q1 in Segment(A,p1,p2,q1,q2) & q2 in
  Segment(A,p1,p2,q1,q2)
proof
A1: Segment(A,p1,p2,q1,q2) = R_Segment(A,p1,p2,q1) /\ L_Segment(A,p1,p2,q2)
  by JORDAN6:def 5;
  assume
A2: LE q1, q2, A, p1, p2;
  L_Segment(A,p1,p2,q2) = {q : LE q,q2,A,p1,p2} by JORDAN6:def 3;
  then
A3: q1 in L_Segment(A,p1,p2,q2) by A2;
  q1 in A by A2,JORDAN5C:def 3;
  then q1 in R_Segment(A,p1,p2,q1) by Th8;
  hence q1 in Segment(A,p1,p2,q1,q2) by A1,A3,XBOOLE_0:def 4;
  R_Segment(A,p1,p2,q1) = {q : LE q1,q,A,p1,p2} by JORDAN6:def 4;
  then
A4: q2 in R_Segment(A,p1,p2,q1) by A2;
  q2 in A by A2,JORDAN5C:def 3;
  then q2 in L_Segment(A,p1,p2,q2) by Th7;
  hence thesis by A1,A4,XBOOLE_0:def 4;
end;

theorem
  Segment(p,q,C) c= C
proof
  set S =Segment(p,q,C);
  let e be set such that
A1: e in S;
  Upper_Arc C \/ Lower_Arc C = C by JORDAN6:65;
  then
A2: Upper_Arc C c= C & Lower_Arc C c= C by XBOOLE_1:7;
  per cases;
  suppose
    q = W-min C;
    then S = {p1: LE p,p1,C or p in C & p1=W-min C} by JORDAN7:def 1;
    then consider p1 such that
A3: e = p1 &( LE p,p1,C or p in C & p1=W-min C) by A1;
    now
      assume LE p,p1,C;
      then p1 in Upper_Arc C or p1 in Lower_Arc C by JORDAN6:def 10;
      hence p1 in C by A2;
    end;
    hence thesis by A3,SPRECT_1:15;
  end;
  suppose
    q <> W-min C;
    then S = {p1: LE p,p1,C & LE p1,q,C} by JORDAN7:def 1;
    then consider p1 such that
A4: e = p1 and
A5: LE p,p1,C and
    LE p1,q,C by A1;
    p1 in Upper_Arc C or p1 in Lower_Arc C by A5,JORDAN6:def 10;
    hence thesis by A2,A4;
  end;
end;

theorem
  p in C & q in C implies LE p,q,C or LE q,p,C
proof
  assume that
A1: p in C and
A2: q in C;
A3: C = Lower_Arc C \/ Upper_Arc C by JORDAN6:65;
  per cases by A1,A2,A3,JORDAN7:1,XBOOLE_0:def 3;
  suppose
    p = q;
    hence thesis by A1,JORDAN6:71;
  end;
  suppose that
A4: p in Lower_Arc C & p <> W-min C & q in Lower_Arc C & q <> W-min C and
A5: p <> q;
    Lower_Arc C is_an_arc_of E-max C,W-min C by JORDAN6:65;
    then LE p,q,Lower_Arc C, E-max C,W-min C or LE q,p,Lower_Arc C, E-max C,
    W-min C by A4,A5,JORDAN5C:14;
    hence thesis by A4,JORDAN6:def 10;
  end;
  suppose
    p in Lower_Arc C & p <> W-min C & q in Upper_Arc C;
    hence thesis by JORDAN6:def 10;
  end;
  suppose
    p in Upper_Arc C & q in Lower_Arc C & q <> W-min C;
    hence thesis by JORDAN6:def 10;
  end;
  suppose that
A6: p in Upper_Arc C & q in Upper_Arc C and
A7: p <> q;
    Upper_Arc C is_an_arc_of W-min C, E-max C by JORDAN6:65;
    then LE p,q,Upper_Arc C,W-min C, E-max C or LE q,p,Upper_Arc C,W-min C,
    E-max C by A6,A7,JORDAN5C:14;
    hence thesis by A6,JORDAN6:def 10;
  end;
end;

theorem Th12:
  for X,Y being non empty TopSpace, Y0 being non empty SubSpace of
Y, f being Function of X,Y, g being Function of X,Y0 st f = g & f is continuous
  holds g is continuous
proof
  let X,Y be non empty TopSpace, Y0 being non empty SubSpace of Y;
  let f be Function of X,Y, g be Function of X,Y0 such that
A1: f = g and
A2: f is continuous;
  let W being Point of X, G being a_neighborhood of g.W;
  consider V being Subset of Y0 such that
A3: V is open and
A4: V c= G and
A5: g.W in V by CONNSP_2:8;
  g.W in [#]Y0 & [#]Y0 c= [#]Y by PRE_TOPC:def 9;
  then reconsider p = g.W as Point of Y;
  consider C being Subset of Y such that
A6: C is open and
A7: C /\ [#]Y0 = V by A3,TOPS_2:32;
  p in C by A5,A7,XBOOLE_0:def 4;
  then C is a_neighborhood of p by A6,CONNSP_2:5;
  then consider H being a_neighborhood of W such that
A8: f.:H c= C by A1,A2,BORSUK_1:def 3;
  take H;
  g.:H c= V by A1,A7,A8,XBOOLE_1:19;
  hence thesis by A4,XBOOLE_1:1;
end;

theorem Th13:
  for S,T being non empty TopSpace, S0 being non empty SubSpace of
  S, T0 being non empty SubSpace of T, f being Function of S,T st f is
being_homeomorphism for g being Function of S0,T0 st g = f|S0 & g is onto holds
  g is being_homeomorphism
proof
  let S,T be non empty TopSpace, S0 be non empty SubSpace of S, T0 be non
  empty SubSpace of T, f be Function of S,T such that
A1: f is being_homeomorphism;
A2: rng f = [#]T by A1,TOPS_2:def 5;
A3: f" is continuous by A1,TOPS_2:def 5;
  let g be Function of S0,T0 such that
A4: g = f|S0 and
A5: g is onto;
A6: g = f|the carrier of S0 by A4,TMAP_1:def 4;
  then
A7: f.:the carrier of S0 = rng g by RELAT_1:148
    .= the carrier of T0 by A5,FUNCT_2:def 3;
  thus dom g = [#]S0 by FUNCT_2:def 1;
  thus
A8: rng g = [#]T0 by A5,FUNCT_2:def 3;
A9: f is one-to-one by A1,TOPS_2:def 5;
  hence
A10: g is one-to-one by A6,FUNCT_1:84;
  f is continuous by A1,TOPS_2:def 5;
  then g is continuous Function of S0,T by A4;
  hence g is continuous by Th12;
  g" = (f qua Function|the carrier of S0)" by A8,A6,A10,TOPS_2:def 4
    .= (f qua Function)"|(f.:the carrier of S0) by A9,RFUNCT_2:40
    .= f"|(the carrier of T0) by A9,A2,A7,TOPS_2:def 4
    .= f"|T0 by TMAP_1:def 4;
  then g" is continuous Function of T0,S by A3;
  hence thesis by Th12;
end;

theorem Th14:
  for P1,P2,P3 being Subset of TOP-REAL 2 for p1,p2 being Point of
  TOP-REAL 2 st P1 is_an_arc_of p1,p2 & P2 is_an_arc_of p1,p2 & P3 is_an_arc_of
  p1,p2 & P2 /\ P3={p1,p2} & P1 c= P2 \/ P3 holds P1=P2 or P1=P3
proof
  let P1,P2,P3 be Subset of TOP-REAL 2;
  set P = P2 \/ P3;
A1: the carrier of (TOP-REAL 2)|P=[#]((TOP-REAL 2)|P) .=P by PRE_TOPC:def 10;
  then reconsider U2=P2 as Subset of (TOP-REAL 2)|P by XBOOLE_1:7;
  reconsider U3=P3 as Subset of (TOP-REAL 2)|P by A1,XBOOLE_1:7;
  let p1,p2 be Point of TOP-REAL 2;
  assume that
A2: P1 is_an_arc_of p1,p2 and
A3: P2 is_an_arc_of p1,p2 and
A4: P3 is_an_arc_of p1,p2;
  consider f being Function of I[01], (TOP-REAL 2)|P1 such that
A5: f is being_homeomorphism and
A6: f.0 = p1 & f.1 = p2 by A2,TOPREAL1:def 2;
A7: f is one-to-one by A5,TOPS_2:def 5;
  U2=P2 /\ P by XBOOLE_1:7,28;
  then
A8: U2 is closed by A3,JORDAN6:3,12;
A9: rng f=[#]((TOP-REAL 2)|P1) by A5,TOPS_2:def 5
    .=P1 by PRE_TOPC:def 10;
  p1 in P2 by A3,TOPREAL1:4;
  then reconsider Q=P as non empty Subset of Euclid 2 by TOPREAL3:13;
  assume that
A10: P2 /\ P3={p1,p2} and
A11: P1 c= P2 \/ P3;
A12: p2 in P2 /\ P3 & p1 in P2 /\ P3 by A10,TARSKI:def 2;
  U3=P3 /\ P by XBOOLE_1:7,28;
  then
A13: U3 is closed by A4,JORDAN6:3,12;
A14: f is continuous by A5,TOPS_2:def 5;
A15: dom f=[#](I[01]) by A5,TOPS_2:def 5;
  per cases;
  suppose
A16: for r being Real st 0<r & r<1 holds f.r in P3;
    P1 c= P3
    proof
      let y be set;
      assume y in P1;
      then consider x being set such that
A17:  x in dom f and
A18:  y=f.x by A9,FUNCT_1:def 5;
      reconsider r=x as Real by A15,A17,BORSUK_1:83;
      r<=1 by A17,BORSUK_1:83,XXREAL_1:1;
      then r = 0 or r = 1 or 0<r & r <1 by A17,BORSUK_1:83,XXREAL_0:1 ,
      XXREAL_1:1;
      hence thesis by A12,A6,A16,A18,XBOOLE_0:def 4;
    end;
    hence thesis by A2,A4,JORDAN6:59;
  end;
  suppose
A19: ex r being Real st 0<r & r<1 & not f.r in P3;
    now
      per cases;
      case
A20:    for r being Real st 0<r & r<1 holds f.r in P2;
        P1 c= P2
        proof
          let y be set;
          assume y in P1;
          then consider x being set such that
A21:      x in dom f and
A22:      y=f.x by A9,FUNCT_1:def 5;
          reconsider r=x as Real by A15,A21,BORSUK_1:83;
          r<=1 by A21,BORSUK_1:83,XXREAL_1:1;
          then 0<r & r<1 or r=0 or r=1 by A21,BORSUK_1:83,XXREAL_0:1 ,
          XXREAL_1:1;
          hence thesis by A12,A6,A20,A22,XBOOLE_0:def 4;
        end;
        hence thesis by A2,A3,JORDAN6:59;
      end;
      case
        ex r being Real st 0<r & r<1 & not f.r in P2;
        then consider r2 being Real such that
A23:    0<r2 and
A24:    r2<1 and
A25:    not f.r2 in P2;
        r2 in [.0,1.] by A23,A24,XXREAL_1:1;
        then f.r2 in rng f by A15,BORSUK_1:83,FUNCT_1:def 5;
        then
A26:    f.r2 in P3 by A11,A9,A25,XBOOLE_0:def 3;
        consider r1 being Real such that
A27:    0<r1 and
A28:    r1<1 and
A29:    not f.r1 in P3 by A19;
        r1 in [.0,1.] by A27,A28,XXREAL_1:1;
        then
A30:    f.r1 in rng f by A15,BORSUK_1:83,FUNCT_1:def 5;
        then
A31:    f.r1 in P2 by A11,A9,A29,XBOOLE_0:def 3;
        now
          per cases;
          suppose
A32:        r1<=r2;
            now
              per cases by A32,XXREAL_0:1;
              suppose
                r1=r2;
                hence contradiction by A11,A9,A25,A29,A30,XBOOLE_0:def 3;
              end;
              suppose
A33:            r1<r2;
A34:            the carrier of (TOP-REAL 2)|P1=[#]((TOP-REAL 2)|P1)
                  .=P1 by PRE_TOPC:def 10;
                the carrier of (TOP-REAL 2)|P=[#]((TOP-REAL 2)|P)
                  .=P by PRE_TOPC:def 10;
                then rng f c= the carrier of (TOP-REAL 2)|P by A11,A34,
                XBOOLE_1:1;
                then reconsider
                g=f as Function of I[01],(TOP-REAL 2)|P by A15,
                FUNCT_2:4;
                P=P1 \/ P by A11,XBOOLE_1:12;
                then
A35:            (TOP-REAL 2)|P1 is SubSpace of (TOP-REAL 2)|P by
                TOPMETR:5;
                U2 \/ U3 =the carrier of ((Euclid 2)|Q) & (TOP-REAL 2)|P=
                TopSpaceMetr(( Euclid 2)|Q) by EUCLID:67,TOPMETR:def 2;
                then consider r0 being Real such that
A36:            r1<=r0 and
A37:            r0<=r2 and
A38:            g.r0 in U2 /\ U3 by A14,A8,A13,A24,A27,A26,A31,A33,
                A35,PRE_TOPC:56 ,TOPMETR3:17;
                r0<1 by A24,A37,XXREAL_0:2;
                then
A39:            r0 in dom f by A15,A27,A36,BORSUK_1:83 ,XXREAL_1:1;
A40:            0 in dom f & 1 in dom f by A15,BORSUK_1:83,XXREAL_1:1;
                g.r0=p1 or g.r0=p2 by A10,A38,TARSKI:def 2;
                hence contradiction by A6,A7,A24,A27,A36,A37,A39,A40,
                FUNCT_1:def 8;
              end;
            end;
            hence contradiction;
          end;
          suppose
A41:        r1>r2;
A42:        the carrier of (TOP-REAL 2)|P1=[#]((TOP-REAL 2)|P1)
              .=P1 by PRE_TOPC:def 10;
            the carrier of (TOP-REAL 2)|P=[#]((TOP-REAL 2)|P)
              .=P by PRE_TOPC:def 10;
            then rng f c= the carrier of (TOP-REAL 2)|P by A11,A42,
            XBOOLE_1:1;
            then reconsider g=f as Function of I[01],(TOP-REAL 2)|P by A15,
            FUNCT_2:4;
            P=P1 \/ P by A11,XBOOLE_1:12;
            then
A43:        (TOP-REAL 2)|P1 is SubSpace of (TOP-REAL 2)|P by
            TOPMETR:
            5;
            U2 \/ U3 =the carrier of ((Euclid 2)|Q) & (TOP-REAL 2)|P=
            TopSpaceMetr(( Euclid 2)|Q) by EUCLID:67,TOPMETR:def 2;
            then consider r0 being Real such that
A44:        r2<=r0 and
A45:        r0<=r1 and
A46:        g.r0 in U2 /\ U3 by A14,A8,A13,A23,A28,A26,A31,A41,A43,
            PRE_TOPC:56,TOPMETR3:17;
            r0<1 by A28,A45,XXREAL_0:2;
            then
A47:        r0 in dom f by A15,A23,A44,BORSUK_1:83,XXREAL_1:1;
A48:        0 in dom f & 1 in dom f by A15,BORSUK_1:83,XXREAL_1:1;
            g.r0=p1 or g.r0=p2 by A10,A46,TARSKI:def 2;
            hence contradiction by A6,A7,A23,A28,A44,A45,A47,A48, FUNCT_1:
            def 8;
          end;
        end;
        hence contradiction;
      end;
    end;
    hence thesis;
  end;
end;

theorem Th15:
  for C being Simple_closed_curve, A1,A2 being Subset of TOP-REAL
  2, p1,p2 being Point of TOP-REAL 2 st A1 is_an_arc_of p1,p2 & A2 is_an_arc_of
  p1,p2 & A1 c= C & A2 c= C & A1 <> A2 holds A1 \/ A2 = C & A1 /\ A2 = {p1,p2}
proof
  let C be Simple_closed_curve, A1,A2 be Subset of TOP-REAL 2, p1,p2 be Point
  of TOP-REAL 2 such that
A1: A1 is_an_arc_of p1,p2 and
A2: A2 is_an_arc_of p1,p2 and
A3: A1 c= C and
A4: A2 c= C & A1 <> A2;
A5: p2 in A1 by A1,TOPREAL1:4;
  p1 <> p2 & p1 in A1 by A1,JORDAN6:49,TOPREAL1:4;
  then consider P1,P2 being non empty Subset of TOP-REAL 2 such that
A6: P1 is_an_arc_of p1,p2 & P2 is_an_arc_of p1,p2 & C = P1 \/ P2 & P1 /\
  P2 = {p1,p2} by A3,A5,TOPREAL2:5;
  reconsider P1,P2 as non empty Subset of TOP-REAL 2;
  A1=P1 or A1=P2 by A1,A3,A6,Th14;
  hence thesis by A2,A4,A6,Th14;
end;

theorem Th16:
  for A1,A2 being Subset of TOP-REAL 2, p1,p2,q1,q2 being Point of
  TOP-REAL 2 st A1 is_an_arc_of p1,p2 & A1 /\ A2 = {q1,q2} holds A1 <> A2
proof
  let A1,A2 be Subset of TOP-REAL 2, p1,p2,q1,q2 be Point of TOP-REAL 2 such
  that
A1: A1 is_an_arc_of p1,p2 and
A2: A1 /\ A2 = {q1,q2} & A1 = A2;
  p1 in A1 by A1,TOPREAL1:4;
  then
A3: p1= q1 or p1 = q2 by A2,TARSKI:def 2;
  p2 in A1 by A1,TOPREAL1:4;
  then
A4: p2= q1 or p2 = q2 by A2,TARSKI:def 2;
  ex p3 being Point of TOP-REAL 2 st p3 in A1 & p3<>p1 & p3<>p2 by A1,
  JORDAN6:55;
  hence contradiction by A1,A2,A3,A4,JORDAN6:49,TARSKI:def 2;
end;

theorem
  for C being Simple_closed_curve, A1,A2 being Subset of TOP-REAL 2, p1,
p2 being Point of TOP-REAL 2 st A1 is_an_arc_of p1,p2 & A2 is_an_arc_of p1,p2 &
  A1 c= C & A2 c= C & A1 /\ A2 = {p1,p2} holds A1 \/ A2 = C
proof
  let C be Simple_closed_curve, A1,A2 be Subset of TOP-REAL 2, p1,p2 be Point
  of TOP-REAL 2 such that
A1: A1 is_an_arc_of p1,p2 and
A2: A2 is_an_arc_of p1,p2 and
A3: A1 c= C & A2 c= C and
A4: A1 /\ A2 = {p1,p2};
  A1 <> A2 by A2,A4,Th16;
  hence thesis by A1,A2,A3,Th15;
end;

theorem
  A1 c= C & A2 c= C & A1 <> A2 & A1 is_an_arc_of p1,p2 & A2 is_an_arc_of
  p1,p2 implies for A st A is_an_arc_of p1,p2 & A c= C holds A = A1 or A = A2
proof
  assume that
A1: A1 c= C & A2 c= C & A1 <> A2 and
A2: A1 is_an_arc_of p1,p2 & A2 is_an_arc_of p1,p2;
A3: A1 \/ A2 = C & A1 /\ A2 = {p1,p2} by A1,A2,Th15;
  let A;
  assume A is_an_arc_of p1,p2 & A c= C;
  hence thesis by A2,A3,Th14;
end;

theorem Th19:
  for C being Simple_closed_curve, A being non empty Subset of
TOP-REAL 2 st A is_an_arc_of W-min C, E-max C & A c= C holds A = Lower_Arc C or
  A = Upper_Arc C
proof
  let C be Simple_closed_curve, A be non empty Subset of TOP-REAL 2 such that
A1: A is_an_arc_of W-min C, E-max C and
A2: A c= C;
  A is compact by A1,JORDAN5A:1;
  hence thesis by A1,A2,TOPMETR3:19;
end;

theorem Th20:
  A is_an_arc_of p1,p2 & LE q1, q2, A, p1, p2 implies ex g being
  Function of I[01], (TOP-REAL 2)|A, s1, s2 being Real st g is
  being_homeomorphism & g.0 = p1 & g.1 = p2 & g.s1 = q1 & g.s2 = q2 & 0 <= s1 &
  s1 <= s2 & s2 <= 1
proof
  given f being Function of I[01], (TOP-REAL 2)|A such that
A1: f is being_homeomorphism and
A2: f.0 = p1 & f.1 = p2;
A3: rng f = [#]((TOP-REAL 2)|A) by A1,TOPS_2:def 5
    .= A by PRE_TOPC:def 10;
  assume
A4: LE q1, q2, A, p1, p2;
  then q1 in A by JORDAN5C:def 3;
  then consider u being set such that
A5: u in dom f and
A6: q1 = f.u by A3,FUNCT_1:def 5;
  take f;
A7: dom f = [#]I[01] by A1,TOPS_2:def 5
    .= [.0,1.] by BORSUK_1:83;
  then reconsider s1 = u as Element of REAL by A5;
A8: s1 <= 1 by A7,A5,XXREAL_1:1;
  q2 in A by A4,JORDAN5C:def 3;
  then consider u being set such that
A9: u in dom f and
A10: q2 = f.u by A3,FUNCT_1:def 5;
  reconsider s2 = u as Element of REAL by A7,A9;
  take s1,s2;
  thus f is being_homeomorphism & f.0 = p1 & f.1 = p2 by A1,A2;
  thus q1 = f.s1 & q2 = f.s2 by A6,A10;
  thus 0 <= s1 by A7,A5,XXREAL_1:1;
  0 <= s2 & s2 <= 1 by A7,A9,XXREAL_1:1;
  hence s1 <= s2 by A1,A2,A4,A6,A10,A8,JORDAN5C:def 3;
  thus thesis by A7,A9,XXREAL_1:1;
end;

theorem Th21:
  A is_an_arc_of p1,p2 & LE q1, q2, A, p1, p2 & q1 <> q2 implies
  ex g being Function of I[01], (TOP-REAL 2)|A, s1, s2 being Real st g is
  being_homeomorphism & g.0 = p1 & g.1 = p2 & g.s1 = q1 & g.s2 = q2 & 0 <= s1 &
  s1 < s2 & s2 <= 1
proof
  assume that
A1: A is_an_arc_of p1,p2 & LE q1, q2, A, p1, p2 and
A2: q1 <> q2;
  consider g being Function of I[01], (TOP-REAL 2)|A, s1, s2 being Real such
  that
A3: g is being_homeomorphism & g.0 = p1 & g.1 = p2 and
A4: g.s1 = q1 & g.s2 = q2 and
A5: 0 <= s1 and
A6: s1 <= s2 and
A7: s2 <= 1 by A1,Th20;
  take g,s1,s2;
  thus g is being_homeomorphism & g.0 = p1 & g.1 = p2 & g.s1 = q1 & g.s2 = q2
  & 0 <= s1 by A3,A4,A5;
  thus s1 < s2 by A2,A4,A6,XXREAL_0:1;
  thus thesis by A7;
end;

theorem
  LE q1, q2, A, p1, p2 implies Segment(A,p1,p2,q1,q2) is non empty
proof
A1: Segment(A,p1,p2,q1,q2)={q:LE q1,q,A,p1,p2 & LE q,q2,A,p1,p2} by JORDAN6:
  29;
  assume
A2: LE q1, q2, A, p1, p2;
  then q2 in A by JORDAN5C:def 3;
  then LE q2,q2,A,p1,p2 by JORDAN5C:9;
  then q2 in Segment(A,p1,p2,q1,q2) by A2,A1;
  hence thesis;
end;

theorem
  p in C implies p in Segment(p,W-min C,C) & W-min C in Segment(p,W-min C,C)
proof
A1: Segment(p,W-min C,C) = {p1: LE p,p1,C or p in C & p1=W-min C} by JORDAN7
  :def 1;
  assume
A2: p in C;
  then LE p,p,C by JORDAN6:71;
  hence p in Segment(p,W-min C, C) by A1;
  thus thesis by A2,A1;
end;

definition
  canceled 2;
  let a,b be real number;
  func AffineMap(a,b) -> Function of REAL, REAL means
  :Def3:
  for x being real number holds it.x = a*x + b;
  existence
  proof
    reconsider a9 = a, b9 = b as Element of REAL by XREAL_0:def 1;
    deffunc F(Real)=a9*$1+b9;
    consider f being Function of REAL, REAL such that
A1: for x being Element of REAL holds f.x =F(x) from FUNCT_2:sch 4;
    take f;
    let x be real number;
    reconsider x9 = x as Element of REAL by XREAL_0:def 1;
    f.x9 = a9*x+b9 by A1;
    hence thesis;
  end;
  uniqueness
  proof
    let f,f9 be Function of REAL, REAL such that
A2: for x being real number holds f.x = a*x + b and
A3: for x being real number holds f9.x = a*x + b;
    now
      let x be Element of REAL;
      thus f.x = a*x + b by A2
        .= f9.x by A3;
    end;
    hence f = f9 by FUNCT_2:113;
  end;
end;

registration
  let a,b be real number;
  cluster AffineMap(a,b) -> continuous;
  coherence
  proof
    set f = AffineMap(a,b);
    for x0 being real number st x0 in REAL holds f.x0 = a*x0+b by Def3;
    then REAL = dom f & AffineMap(a,b)|REAL is continuous by FCONT_1:48 ,
    FUNCT_2:def 1;
    hence thesis by RELAT_1:98;
  end;
end;

registration
  cluster continuous Function of REAL, REAL;
  existence
  proof
    take AffineMap(1,1);
    thus thesis;
  end;
end;

theorem
  for f,g being continuous PartFunc of REAL, REAL holds g*f is
  continuous PartFunc of REAL, REAL;

theorem Th25:
  for a,b being real number holds AffineMap(a,b).0 = b
proof
  let a,b be real number;
  thus AffineMap(a,b).0 = a*0 + b by Def3
    .= b;
end;

theorem Th26:
  for a,b being real number holds AffineMap(a,b).1 = a+b
proof
  let a,b be real number;
  thus AffineMap(a,b).1 = a*1 + b by Def3
    .= a + b;
end;

theorem Th27:
  for a,b being real number st a<> 0 holds AffineMap(a,b) is one-to-one
proof
  let a,b be real number such that
A1: a<> 0;
  let x1,x2 be set;
  set f = AffineMap(a,b);
  assume x1 in dom f;
  then reconsider r1 = x1 as real number;
  assume x2 in dom f;
  then reconsider r2 = x2 as real number;
  assume f.x1 = f.x2;
  then a*r1+b = f.x2 by Def3
    .= a*r2 +b by Def3;
  hence thesis by A1,XCMPLX_1:5;
end;

theorem
  for a,b,x,y being real number st a > 0 & x < y holds AffineMap(a,b).x
  < AffineMap(a,b).y
proof
  let a,b,x,y be real number;
  assume a > 0 & x < y;
  then
A1: a*x < a*y by XREAL_1:70;
  AffineMap(a,b).x = a*x + b & AffineMap(a,b).y = a*y + b by Def3;
  hence thesis by A1,XREAL_1:10;
end;

theorem
  for a,b,x,y being real number st a < 0 & x < y holds AffineMap(a,b).x
  > AffineMap(a,b).y
proof
  let a,b,x,y be real number;
  assume a < 0 & x < y;
  then
A1: a*x > a*y by XREAL_1:71;
  AffineMap(a,b).x = a*x + b & AffineMap(a,b).y = a*y + b by Def3;
  hence thesis by A1,XREAL_1:10;
end;

theorem Th30:
  for a,b,x,y being real number st a >= 0 & x <= y holds AffineMap
  (a,b).x <= AffineMap(a,b).y
proof
  let a,b,x,y be real number;
  assume a >= 0 & x <= y;
  then
A1: a*x <= a*y by XREAL_1:66;
  AffineMap(a,b).x = a*x + b & AffineMap(a,b).y = a*y + b by Def3;
  hence thesis by A1,XREAL_1:9;
end;

theorem
  for a,b,x,y being real number st a <= 0 & x <= y holds AffineMap(a,b).
  x >= AffineMap(a,b).y
proof
  let a,b,x,y be real number;
  assume a <= 0 & x <= y;
  then
A1: a*x >= a*y by XREAL_1:67;
  AffineMap(a,b).x = a*x + b & AffineMap(a,b).y = a*y + b by Def3;
  hence thesis by A1,XREAL_1:9;
end;

theorem Th32:
  for a,b being real number st a <> 0 holds rng AffineMap(a,b) = REAL
proof
  let a,b be real number such that
A1: a <> 0;
  thus rng AffineMap(a,b) c= REAL;
  let e be set;
  assume e in REAL;
  then reconsider r = e as Element of REAL;
  set s = (r - b)/a;
  AffineMap(a,b).s = a*s + b by Def3
    .= r - b + b by A1,XCMPLX_1:88
    .= r;
  hence thesis by FUNCT_2:6;
end;

theorem Th33:
  for a,b being real number st a <> 0 holds (AffineMap(a,b) qua
  Function)" = AffineMap(a",-b/a)
proof
  let a,b be real number such that
A1: a <> 0;
  for x being Element of REAL holds (AffineMap(a",-b/a)*AffineMap(a,b)).x
  = (id REAL).x
  proof
    let x being Element of REAL;
    thus (AffineMap(a",-b/a)*AffineMap(a,b)).x = AffineMap(a",-b/a).(AffineMap
    (a,b).x) by FUNCT_2:21
      .= AffineMap(a",-b/a).(a*x+b) by Def3
      .= a"*(a*x+b)+-b/a by Def3
      .= a"*a*x+a"*b +-b/a
      .= 1 *x+a"*b +-b/a by A1,XCMPLX_0:def 7
      .= x+a"*b -b/a
      .= x+ b/a -b/a by XCMPLX_0:def 9
      .= (id REAL).x by FUNCT_1:35;
  end;
  then
A2: AffineMap(a",-b/a)*AffineMap(a,b) = id REAL by FUNCT_2:113;
  rng AffineMap(a,b) = REAL by A1,Th32;
  hence thesis by A1,A2,Th27,FUNCT_2:36;
end;

theorem Th34:
  for a,b being real number st a > 0 holds AffineMap(a,b).:[.0,1.] = [.b,a+b.]
proof
  let a,b be real number such that
A1: a > 0;
  thus AffineMap(a,b).:[.0,1.] c= [.b,a+b.]
  proof
A2: AffineMap(a,b).1 = a+b by Th26;
    let u be set;
    assume u in AffineMap(a,b).:[.0,1.];
    then consider r being Element of REAL such that
A3: r in [.0,1.] and
A4: u = AffineMap(a,b).r by FUNCT_2:116;
    reconsider s = u as real number by A4;
    r <= 1 by A3,XXREAL_1:1;
    then
A5: s <= a+b by A1,A4,A2,Th30;
A6: AffineMap(a,b).0 = b by Th25;
    0 <= r by A3,XXREAL_1:1;
    then b <= s by A1,A4,A6,Th30;
    hence thesis by A5,XXREAL_1:1;
  end;
  let u be set;
  assume
A7: u in [.b,a+b.];
  then reconsider r = u as Element of REAL;
  set s = (r - b)/a;
A8: AffineMap(a,b).s = a*s + b by Def3
    .= r - b + b by A1,XCMPLX_1:88
    .= r;
  r <= a+b by A7,XXREAL_1:1;
  then r-b <= a by XREAL_1:22;
  then s <= a/a by A1,XREAL_1:74;
  then
A9: s <= 1 by A1,XCMPLX_1:60;
  b <= r by A7,XXREAL_1:1;
  then 0 <= r - b by XREAL_1:50;
  then s in [.0,1.] by A1,A9,XXREAL_1:1;
  hence thesis by A8,FUNCT_2:43;
end;

theorem Th35:
  for f being Function of R^1, R^1 for a,b being Real st a <> 0 &
  f = AffineMap(a,b) holds f is being_homeomorphism
proof
  let f be Function of R^1, R^1;
  let a,b be Real such that
A1: a <> 0 and
A2: f = AffineMap(a,b);
  thus dom f = [#]R^1 by FUNCT_2:def 1;
  thus
A3: rng f = [#]R^1 by A1,A2,Th32,TOPMETR:24;
  thus
A4: f is one-to-one by A1,A2,Th27;
  for x being Real holds f.x = a*x + b by A2,Def3;
  hence f is continuous by TOPMETR:28;
  f" = (f qua Function)" by A3,A4,TOPS_2:def 4
    .= AffineMap(a",-b/a) by A1,A2,Th33;
  then for x being Real holds f".x = a"*x + -b/a by Def3;
  hence thesis by TOPMETR:28;
end;

theorem Th36:
  A is_an_arc_of p1,p2 & LE q1, q2, A, p1, p2 & q1 <> q2 implies
  Segment(A,p1,p2,q1,q2) is_an_arc_of q1,q2
proof
  assume that
A1: A is_an_arc_of p1,p2 and
A2: LE q1, q2, A, p1, p2 & q1 <> q2;
  consider g being Function of I[01], (TOP-REAL 2)|A, s1, s2 being Real such
  that
A3: g is being_homeomorphism and
A4: g.0 = p1 & g.1 = p2 and
A5: g.s1 = q1 and
A6: g.s2 = q2 and
A7: 0 <= s1 and
A8: s1 < s2 and
A9: s2 <= 1 by A1,A2,Th21;
  reconsider A9 = A as non empty Subset of TOP-REAL 2 by A1,TOPREAL1:4;
  set S = Segment(A,p1,p2,q1,q2);
A10: S={q:LE q1,q,A,p1,p2 & LE q,q2,A,p1,p2} by JORDAN6:29;
A11: 0 < s2 - s1 by A8,XREAL_1:52;
  set f = (g*AffineMap(s2-s1,s1))| [.0,1.];
  reconsider g as Function of I[01], (TOP-REAL 2)|A9;
  reconsider m = AffineMap(s2-s1,s1) as Function of R^1,R^1 by TOPMETR:24;
  for x being Real holds m.x = (s2-s1)*x + s1 by Def3;
  then reconsider m as continuous Function of R^1, R^1 by TOPMETR:28;
  set h = m | I[01];
A12: h = m | [. 0,1 .] by BORSUK_1:83,TMAP_1:def 4;
  then
A13: rng h = m.:[. 0,1 .] by RELAT_1:148
    .= [.s1,s2-s1+s1.] by A8,Th34,XREAL_1:52
    .= [.s1,s2.];
  then
A14: rng h c= [. 0,1 .] by A7,A9,XXREAL_1:34;
A15: dom m = REAL by FUNCT_2:def 1;
  then
A16: dom h = [.0,1.] by A12,RELAT_1:91;
  then reconsider h as Function of I[01],I[01] by A14,BORSUK_1:83,RELSET_1:11;
A17: f = g*h by A12,RELAT_1:112;
A18: [.0,1.] = dom g by BORSUK_1:83,FUNCT_2:def 1;
  m.: [.0,1.] c= dom g
  proof
    let e be set;
    assume e in m.: [.0,1.];
    then
A19: e in [.s1,s2-s1+s1.] by A8,Th34,XREAL_1:52;
    [.s1,s2.] c= [.0,1.] by A7,A9,XXREAL_1:34;
    hence thesis by A18,A19;
  end;
  then
A20: [.0,1.] c= dom(g*m) by A15,FUNCT_3:3;
  then
A21: dom f = [#]I[01] by BORSUK_1:83,RELAT_1:91;
  reconsider CIT = Closed-Interval-TSpace(s1,s2) as non empty SubSpace of
  I[01] by A7,A8,A9,TOPMETR:27,TREAL_1:6;
  [.s1,s2.] c= [.0,1.] by A7,A9,XXREAL_1:34;
  then
A22: the carrier of CIT c= dom g by A8,A18,TOPMETR:25;
A23: rng h = the carrier of CIT by A8,A13,TOPMETR:25;
A24: dom f = the carrier of I[01] by A20,BORSUK_1:83,RELAT_1:91;
A25: s1 < 1 by A8,A9,XXREAL_0:2;
  for y being set holds y in [#]((TOP-REAL 2)|S) iff ex x being set st x
  in dom f & y = f.x
  proof
    let y be set;
    thus y in [#]((TOP-REAL 2)|S) implies ex x being set st x in dom f & y = f
    .x
    proof
      assume y in [#]((TOP-REAL 2)|S);
      then y in S by PRE_TOPC:def 10;
      then consider q0 being Point of TOP-REAL 2 such that
A26:  y = q0 and
A27:  LE q1,q0,A,p1,p2 and
A28:  LE q0,q2,A,p1,p2 by A10;
      q0 in A by A27,JORDAN5C:def 3;
      then q0 in [#]((TOP-REAL 2)|A) by PRE_TOPC:def 10;
      then q0 in rng g by A3,TOPS_2:def 5;
      then consider s being set such that
A29:  s in dom g and
A30:  q0 = g.s by FUNCT_1:def 5;
      reconsider s as Element of REAL by A18,A29;
      take x = (s-s1)/(s2-s1);
A31:  s <= 1 by A29,BORSUK_1:83,XXREAL_1:1;
      then s <= s2 by A3,A4,A6,A7,A8,A9,A28,A30,JORDAN5C:def 3;
      then s-s1 <= s2 - s1 by XREAL_1:11;
      then x <= (s2 - s1)/(s2 - s1) by A11,XREAL_1:74;
      then
A32:  x <= 1 by A11,XCMPLX_1:60;
      0 <= s by A29,BORSUK_1:83,XXREAL_1:1;
      then s1+0 <= s by A3,A4,A5,A25,A27,A30,A31,JORDAN5C:def 3;
      then 0 <= s - s1 by XREAL_1:21;
      hence
A33:  x in dom f by A11,A21,A32,BORSUK_1:83,XXREAL_1:1;
      m.x = (s2-s1)*x + s1 by Def3
        .= s - s1 + s1 by A11,XCMPLX_1:88
        .= s;
      hence y = (g*m).x by A15,A26,A30,FUNCT_1:23
        .= f.x by A33,FUNCT_1:70;
    end;
    given x be set such that
A34: x in dom f and
A35: y = f.x;
    reconsider x as Element of REAL by A34;
    AffineMap(s2-s1,s1).x in REAL;
    then reconsider s = m.x as Element of REAL;
    h.x = m.x by A12,A16,A21,A34,BORSUK_1:83,FUNCT_1:70;
    then
A36: s in rng h by A16,A21,A34,BORSUK_1:83,FUNCT_1:def 5;
    then
A37: s1 <= s by A13,XXREAL_1:1;
    y in rng f by A34,A35,FUNCT_1:def 5;
    then y in [#]((TOP-REAL 2)|A);
    then y in A by PRE_TOPC:def 10;
    then reconsider q = y as Point of TOP-REAL 2;
A38: s <= s2 by A13,A36,XXREAL_1:1;
    then
A39: s <= 1 by A9,XXREAL_0:2;
A40: q = (g*m).x by A34,A35,FUNCT_1:70
      .= g.s by A15,FUNCT_1:23;
    then
A41: LE q1,q,A,p1,p2 by A1,A3,A4,A5,A7,A25,A37,A39,JORDAN5C:8;
    LE q,q2,A,p1,p2 by A1,A3,A4,A6,A7,A9,A40,A37,A38,A39,JORDAN5C:8;
    then q in S by A10,A41;
    hence thesis by PRE_TOPC:def 10;
  end;
  then
A42: rng f = [#]((TOP-REAL 2)|S) by FUNCT_1:def 5;
  then
A43: [#]((TOP-REAL 2)|S) <> {} by A21,RELAT_1:65;
  then reconsider f as Function of I[01], (TOP-REAL 2)|S by A24,A42, FUNCT_2:
  def 1,RELSET_1:11;
  reconsider TS = (TOP-REAL 2)|S as non empty SubSpace of (TOP-REAL 2)|A9 by
  A43,Th5,TOPMETR:29;
  take f;
A44: AffineMap(s2-s1,s1).0 = s1 by Th25;
  set o = g | CIT;
A45: dom o = dom(g|the carrier of CIT) by TMAP_1:def 4
    .= dom g /\ the carrier of CIT by RELAT_1:90
    .= the carrier of CIT by A22,XBOOLE_1:28;
  reconsider h as Function of I[01], CIT by A16,A23,RELSET_1:11;
  h is onto by A23,FUNCT_2:def 3;
  then
A46: h is being_homeomorphism by A11,Th13,Th35;
A47: the carrier of CIT = [.s1,s2.] by A8,TOPMETR:25;
  then o = g|rng h by A13,TMAP_1:def 4;
  then
A48: f = o*h by A17,FUNCT_4:2;
  then
A49: rng o = rng f by A13,A45,A47,RELAT_1:47;
  then reconsider o as Function of CIT, TS by A45,RELSET_1:11;
  o is onto by A42,A49,FUNCT_2:def 3;
  then o is being_homeomorphism by A3,Th13;
  hence f is being_homeomorphism by A48,A46,TOPS_2:71;
A50: dom AffineMap(s2-s1,s1) = REAL by FUNCT_2:def 1;
  0 in [.0,1.] by XXREAL_1:1;
  hence f.0 = (g*m).0 by FUNCT_1:72
    .=q1 by A5,A44,A50,FUNCT_1:23;
A51: AffineMap(s2-s1,s1).1 = s2-s1+s1 by Th26
    .= s2;
  1 in [.0,1.] by XXREAL_1:1;
  hence f.1 = (g*m).1 by FUNCT_1:72
    .= q2 by A6,A51,A50,FUNCT_1:23;
end;

theorem
  for p1,p2 being Point of TOP-REAL 2 for P being Subset of TOP-REAL 2
st P c= C & P is_an_arc_of p1,p2 & W-min C in P & E-max C in P holds Upper_Arc
  C c= P or Lower_Arc C c= P
proof
  let p1,p2 be Point of TOP-REAL 2;
  let P being Subset of TOP-REAL 2 such that
A1: P c= C and
A2: P is_an_arc_of p1,p2 and
A3: W-min C in P and
A4: E-max C in P;
  reconsider P9 = P as non empty Subset of TOP-REAL 2 by A3;
A5: W-min C <> E-max C by TOPREAL5:25;
  per cases by A2,A3,A4,A5,JORDAN5C:14;
  suppose
A6: LE W-min C, E-max C, P,p1,p2;
    set S = Segment(P9,p1,p2,W-min C, E-max C);
    reconsider S9 = S as non empty Subset of TOP-REAL 2 by A6,Th9;
    S c= P by Th5;
    then S c= C by A1,XBOOLE_1:1;
    then S9 = Lower_Arc C or S9 = Upper_Arc C by A2,A5,A6,Th19,Th36;
    hence thesis by Th5;
  end;
  suppose
A7: LE E-max C, W-min C, P,p1,p2;
    set S = Segment(P9,p1,p2,E-max C, W-min C);
    reconsider S9 = S as non empty Subset of TOP-REAL 2 by A7,Th9;
    S is_an_arc_of E-max C, W-min C by A2,A5,A7,Th36;
    then
A8: S9 is_an_arc_of W-min C, E-max C by JORDAN5B:14;
    S c= P by Th5;
    hence thesis by A1,A8,Th19,XBOOLE_1:1;
  end;
end;
 
:: Moved from JORDAN18, AK, 23.02.2006

theorem
  for P being Subset of TOP-REAL 2, p1,p2,q1,q2 being Point of TOP-REAL
2 st P is_an_arc_of p1,p2 & q1 in P & q2 in P & q1 <> p1 & q1 <> p2 & q2 <> p1
  & q2 <> p2 & q1 <> q2 ex Q being non empty Subset of TOP-REAL 2 st Q
  is_an_arc_of q1,q2 & Q c= P & Q misses {p1,p2}
proof
  let P be Subset of TOP-REAL 2, p1,p2,q1,q2 be Point of TOP-REAL 2 such that
A1: P is_an_arc_of p1,p2 and
A2: q1 in P & q2 in P and
A3: q1 <> p1 and
A4: q1 <> p2 and
A5: q2 <> p1 and
A6: q2 <> p2 and
A7: q1 <> q2;
  per cases by A1,A2,A7,JORDAN5C:14;
  suppose
A8: LE q1,q2,P,p1,p2;
    set S = Segment(P,p1,p2,q1,q2);
    S is_an_arc_of q1,q2 by A1,A7,A8,Th36;
    then reconsider S as non empty Subset of TOP-REAL 2 by TOPREAL1:4;
    take S;
    thus S is_an_arc_of q1,q2 by A1,A7,A8,Th36;
    thus S c= P by Th5;
    now
A9:   S = {q where q is Point of TOP-REAL 2 : LE q1,q,P,p1,p2 & LE q,q2,P
      ,p1,p2} by JORDAN6:29;
      assume
A10:  S meets {p1,p2};
      per cases by A10,ZFMISC_1:57;
      suppose
        p1 in S;
        then
        ex q being Point of TOP-REAL 2 st q = p1 & LE q1,q,P,p1,p2 & LE q
        ,q2,P,p1,p2 by A9;
        hence contradiction by A1,A3,JORDAN6:69;
      end;
      suppose
        p2 in S;
        then
        ex q being Point of TOP-REAL 2 st q = p2 & LE q1,q,P,p1,p2 & LE q
        ,q2,P,p1,p2 by A9;
        hence contradiction by A1,A6,JORDAN6:70;
      end;
    end;
    hence thesis;
  end;
  suppose
A11: LE q2,q1,P,p1,p2;
    set S = Segment(P,p1,p2,q2,q1);
    S is_an_arc_of q2,q1 by A1,A7,A11,Th36;
    then reconsider S as non empty Subset of TOP-REAL 2 by TOPREAL1:4;
    take S;
    S is_an_arc_of q2,q1 by A1,A7,A11,Th36;
    hence S is_an_arc_of q1,q2 by JORDAN5B:14;
    thus S c= P by Th5;
    now
A12:  S = {q where q is Point of TOP-REAL 2 : LE q2,q,P,p1,p2 & LE q,q1,P
      ,p1,p2} by JORDAN6:29;
      assume
A13:  S meets {p1,p2};
      per cases by A13,ZFMISC_1:57;
      suppose
        p1 in S;
        then
        ex q being Point of TOP-REAL 2 st q = p1 & LE q2,q,P,p1,p2 & LE q
        ,q1,P,p1,p2 by A12;
        hence contradiction by A1,A5,JORDAN6:69;
      end;
      suppose
        p2 in S;
        then
        ex q being Point of TOP-REAL 2 st q = p2 & LE q2,q,P,p1,p2 & LE q
        ,q1,P,p1,p2 by A12;
        hence contradiction by A1,A4,JORDAN6:70;
      end;
    end;
    hence thesis;
  end;
end;
 
:: moved from JORDAN20, AG 1.04.2006

theorem
  for P being non empty Subset of TOP-REAL 2, p1,p2,q1 being Point of
TOP-REAL 2 st P is_an_arc_of p1,p2 & q1 in P & p1<>q1 holds Segment(P,p1,p2,p1,
  q1) is_an_arc_of p1,q1
proof
  let P be non empty Subset of TOP-REAL 2, p1,p2,q1 be Point of TOP-REAL 2;
  assume that
A1: P is_an_arc_of p1,p2 and
A2: q1 in P and
A3: p1<>q1;
  LE p1,q1,P,p1,p2 by A1,A2,JORDAN5C:10;
  hence thesis by A1,A3,Th36;
end;
