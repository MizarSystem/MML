:: Inverse Trigonometric Functions Arcsin and Arccos
::  by Artur Korni{\l}owicz and Yasunari Shidama
::
:: Received September 25, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabulary SIN_COS6, PARTFUN1, RELAT_1, RCOMP_1, ARYTM_1, ARYTM, SIN_COS,
      FUNCT_1, ARYTM_3, INT_1, FDIFF_1, ASYMPT_0, BOOLE, FCONT_1, SQUARE_1,
      RFUNCT_2;
 notation TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0,
      REAL_1, NAT_1, INT_1, SQUARE_1, RCOMP_1, RELAT_1, FUNCT_1, PARTFUN1,
      RELSET_1, SEQ_1, PARTFUN2, RFUNCT_2, FCONT_1, FDIFF_1, SIN_COS;
 constructors XCMPLX_0, SIN_COS, RCOMP_1, PARTFUN2, INT_1, COMSEQ_3, COMPLEX1,
      SEQ_1, ARYTM_0, REAL_1, PARTFUN1, FDIFF_1, MEMBERED, RFUNCT_1, FCONT_1,
      SQUARE_1, RFUNCT_2, BINOP_2;
 clusters RELSET_1, XREAL_0, ORDINAL2, NUMBERS, NAT_1, REAL_1, FUNCT_1, INT_1,
      PARTFUN2, RFUNCT_2, FCONT_3, XCMPLX_0, SQUARE_1, SIN_COS, ASYMPT_0;
 requirements BOOLE, SUBSET, NUMERALS, ARITHM, REAL;
 definitions TARSKI, XBOOLE_0, XREAL_0, FUNCT_1, RFUNCT_2;
 theorems RELAT_1, SIN_COS, FUNCT_1, COMPTRIG, FDIFF_1, FDIFF_2, RFUNCT_2,
      RCOMP_1, EXTENS_1, JORDAN6, TOPREAL5, REAL_1, AXIOMS, XCMPLX_1, INT_1,
      XBOOLE_0, JGRAPH_3, SQUARE_1, COMPLEX2, XREAL_0, REAL_2, FCONT_3,
      FCONT_1, TREAL_1, UNIROOTS, JGRAPH_2;
 schemes INT_1;

begin :: Preliminaries

 reserve r, s for real number,
         i for integer number;

theorem Tcal:
  0 <= r & r < s implies [\ r/s /] = 0
proof
  assume that
a1: 0 <= r and
a2: r < s;
  0 < s by a1,a2;
  then
a3: 0 <= r/s by a1,SQUARE_1:27;
  r/s < s/s by a1,a2,REAL_1:73;
  then r/s-1 < s/s-1 by REAL_1:54;
  then r/s-1 < 1-1 by a1,a2,XCMPLX_1:60;
  hence [\ r/s /] = 0 by a3,INT_1:def 4;
end;

theorem Tzaw11:
  for f being Function, X, Y being set st f|X is one-to-one & Y c= X holds
  f|Y is one-to-one
proof
  let f be Function,
      X, Y be set such that
a1: f|X is one-to-one and
a2: Y c= X;
a3: dom (f|Y) = dom f /\ Y by RELAT_1:90;
  let x, y be set;
  assume
b1: x in dom (f|Y);
  then
a5: x in Y by a3,XBOOLE_0:def 3;
  assume
b2: y in dom (f|Y);
  then
a6: y in Y by a3,XBOOLE_0:def 3;
  assume
a7: (f|Y).x = (f|Y).y;
a4: (f|X).x = f.x by a2,a5,FUNCT_1:72
    .= (f|Y).x by a5,FUNCT_1:72
    .= f.y by a6,a7,FUNCT_1:72
    .= (f|X).y by a2,a6,FUNCT_1:72;
  x in dom f & y in dom f by a3,b1,b2,XBOOLE_0:def 3;
  then x in dom f /\ X & y in dom f /\ X by a2,a5,a6,XBOOLE_0:def 3;
  then x in dom (f|X) & y in dom (f|X) by RELAT_1:90;
  hence x = y by a1,a4,FUNCT_1:def 8;
end;

begin :: sine and cosine

theorem Tsm1:
  -1 <= sin r
proof
  sin.r in [.-1,1.] by COMPTRIG:45;
  then sin r in [.-1,1.] by SIN_COS:def 21;
  hence thesis by TOPREAL5:1;
end;

theorem Ts1:
  sin r <= 1
proof
  sin.r in [.-1,1.] by COMPTRIG:45;
  then sin r in [.-1,1.] by SIN_COS:def 21;
  hence thesis by TOPREAL5:1;
end;

theorem Tcm1:
  -1 <= cos r
proof
  cos.r in [.-1,1.] by COMPTRIG:45;
  then cos r in [.-1,1.] by SIN_COS:def 23;
  hence thesis by TOPREAL5:1;
end;

theorem Tc1:
  cos r <= 1
proof
  cos.r in [.-1,1.] by COMPTRIG:45;
  then cos r in [.-1,1.] by SIN_COS:def 23;
  hence thesis by TOPREAL5:1;
end;

deffunc T(Integer) = 2*PI*$1;

Lmd1: dom sin = REAL by SIN_COS:def 20;
Lmd2: dom cos = REAL by SIN_COS:def 22;
Lm0: [.-PI/2+T(0),PI/2+T(0).] = [.-PI/2,PI/2.];
Lm0a: [.PI/2+T(0),3/2*PI+T(0).] = [.PI/2,3/2*PI.];
Lm3: [.T(0),PI+T(0).] = [.0,PI.];
Lm3a: [.PI+T(0),2*PI+T(0).] = [.PI,2*PI.];

Lm5:
r^2 + s^2 = 1 implies -1 <= r & r <= 1
proof
  s^2 >= 0 by SQUARE_1:72;
  then -s^2 <= -0 by REAL_1:50;
  then r^2 - (r^2 + s^2) <= 0;
  hence thesis by JGRAPH_3:5;
end;

registration
  cluster PI -> positive;
coherence
proof
  PI in  ]. 0, 4 .[ by SIN_COS:def 30;
  hence PI > 0 by JORDAN6:45;
end;
end;

Lm7: -PI/2 < -0 by REAL_1:50;
  then
Lm4: -PI/2 < PI/2;
Lm8: PI/2 < PI/1 by REAL_2:200;
Lm61: 1*PI < 3/2*PI by REAL_1:70;
Lm21: 0+T(1) < PI/2+T(1) by REAL_1:53;
Lm60: 3/2*PI < 2*PI by REAL_1:70;
Lmp8: 1*PI < 2*PI by REAL_1:70;

Lm52: ].-1,1.[ c= [.-1,1.] by RCOMP_1:15;
Lm33: ].-PI/2,0 .[ c= [.-PI/2,0 .] by RCOMP_1:15;
Lm31: ].-PI/2,PI/2.[ c= [.-PI/2,PI/2.] by RCOMP_1:15;
Lm34: ].0,PI/2.[ c= [.0,PI/2.] by RCOMP_1:15;
Lm50: ].0,PI.[ c= [.0,PI.] by RCOMP_1:15;
Lm36: ].PI/2,PI.[ c= [.PI/2,PI.] by RCOMP_1:15;
Lm32: ].PI/2,3/2*PI.[ c= [.PI/2,3/2*PI.] by RCOMP_1:15;
Lm35: ].PI,3/2*PI.[ c= [.PI,3/2*PI.] by RCOMP_1:15;
Lm51: ].PI,2*PI.[ c= [.PI,2*PI.] by RCOMP_1:15;
Lm30: ].3/2*PI,2*PI.[ c= [.3/2*PI,2*PI.] by RCOMP_1:15;

Lm40: [.-PI/2,0 .] c= [.-PI/2,PI/2.] by TREAL_1:1;
Lm41: [.0,PI/2.] c= [.-PI/2,PI/2.] by Lm7,TREAL_1:1;
Lm53: [.0,PI/2.] c= [.0,PI.] by Lm8,TREAL_1:1;
Lm42: [.PI/2,PI.] c= [.PI/2,3/2*PI.] by Lm61,TREAL_1:1;
Lm54: [.PI/2,PI.] c= [.0,PI.] by TREAL_1:1;
Lm43: [.PI,3/2*PI.] c= [.PI/2,3/2*PI.] by Lm8,TREAL_1:1;
Lm55: [.PI,3/2*PI.] c= [.PI,2*PI.] by Lm60,TREAL_1:1;
Lm44: [.3/2*PI,2*PI.] c= [.-PI/2+T(1),PI/2+T(1).] by Lm21,TREAL_1:1;
Lm56: [.3/2*PI,2*PI.] c= [.PI,2*PI.] by Lm61,TREAL_1:1;

theorem T7:
  sin(-PI/2) = -1 & sin.-PI/2 = -1
proof
  sin.(-PI/2) = sin.(-PI/2+2*PI) by SIN_COS:83
    .= -1 by SIN_COS:81;
  hence thesis by SIN_COS:def 21;
end;

theorem T30:
  sin.r = sin.(r+2*PI*i)
proof
  thus sin.r = sin r by SIN_COS:def 21
    .= sin(r+2*PI*i) by COMPLEX2:9
    .= sin.(r+2*PI*i) by SIN_COS:def 21;
end;

theorem T7a:
  cos(-PI/2) = 0 & cos.-PI/2 = 0
proof
  cos.(-PI/2) = cos.(-PI/2+2*PI) by SIN_COS:83
    .= 0 by SIN_COS:81;
  hence thesis by SIN_COS:def 23;
end;

theorem T31:
  cos.r = cos.(r+2*PI*i)
proof
  thus cos.r = cos r by SIN_COS:def 23
    .= cos(r+2*PI*i) by COMPLEX2:10
    .= cos.(r+2*PI*i) by SIN_COS:def 23;
end;

theorem Th23:
  2*PI*i < r & r < PI+2*PI*i implies sin r > 0
proof
  assume T(i) < r & r < PI+T(i);
  then T(i)-T(i) < r-T(i) & r-T(i) < PI+T(i)-T(i) by REAL_1:54;
  then r-T(i) in ].0,PI.[ by JORDAN6:45;
  then sin.(r+T(-i)) > 0 by COMPTRIG:23;
  then sin.r > 0 by T30;
  hence thesis by SIN_COS:def 21;
end;

theorem Th25:
  PI+2*PI*i < r & r < 2*PI+2*PI*i implies sin r < 0
proof
  assume PI+T(i) < r & r < 2*PI+T(i);
  then PI+T(i)-T(i) < r-T(i) & r-T(i) < 2*PI+T(i)-T(i) by REAL_1:54;
  then r-T(i) in ].PI,2*PI.[ by JORDAN6:45;
  then sin.(r+T(-i)) < 0 by COMPTRIG:25;
  then sin.r < 0 by T30;
  hence thesis by SIN_COS:def 21;
end;

theorem Th27:
  -PI/2+2*PI*i < r & r < PI/2+2*PI*i implies cos r > 0
proof
  assume -PI/2+T(i) < r & r < PI/2+T(i);
  then -PI/2+T(i)+PI/2 < r+PI/2 & r+PI/2 < PI/2+T(i)+PI/2 by REAL_1:53;
  then
A1: T(i) < r+PI/2 & r+PI/2 < PI+T(i);
  sin(r+PI/2) = cos r by SIN_COS:84;
  hence cos r > 0 by A1,Th23;
end;

theorem Th29:
  PI/2+2*PI*i < r & r < 3/2*PI+2*PI*i implies cos r < 0
proof
  assume PI/2+T(i) < r & r < 3/2*PI+T(i);
  then PI/2+T(i)+PI/2 < r+PI/2 & r+PI/2 < 3/2*PI+T(i)+PI/2 by REAL_1:53;
  then
A1: PI+T(i) < r+PI/2 & r+PI/2 < 2*PI+T(i);
  sin(r+PI/2) = cos r by SIN_COS:84;
  hence cos r < 0 by A1,Th25;
end;

theorem Th31:
  3/2*PI+2*PI*i < r & r < 2*PI+2*PI*i implies cos r > 0
proof
a0: PI+T(i) < 3/2*PI+T(i) by COMPTRIG:21,REAL_1:53;
  assume 3/2*PI+T(i) < r & r < 2*PI+T(i);
  then 3/2*PI+T(i)-PI < r-PI & r-PI < 2*PI+T(i)-PI by REAL_1:54;
  then
A1: PI/2+T(i) < r-PI & r-PI < 3/2*PI+T(i) by a0,AXIOMS:22;
  cos(r-PI) = cos(-(PI-r))
    .= cos(PI+-r) by SIN_COS:34
    .= -cos(-r) by SIN_COS:84
    .= -cos r by SIN_COS:34;
  then -cos r < -0 by A1,Th29;
  hence cos r > 0 by REAL_1:50;
end;

theorem
  2*PI*i <= r & r <= PI+2*PI*i implies sin r >= 0
proof
  assume
a1: 2*PI*i <= r & r <= PI+2*PI*i;
  per cases by a1,REAL_1:def 5;
  suppose 2*PI*i < r & r < PI+2*PI*i;
  hence thesis by Th23;
  end;
  suppose
a2: 2*PI*i = r;
  sin(0+2*PI*i) = sin 0 by COMPLEX2:9;
  hence thesis by a2,SIN_COS:34;
  end;
  suppose r = PI+2*PI*i;
  hence thesis by SIN_COS:82,COMPLEX2:9;
  end;
end;

theorem
  PI+2*PI*i <= r & r <= 2*PI+2*PI*i implies sin r <= 0
proof
  assume PI+2*PI*i <= r & r <= 2*PI+2*PI*i;
  then PI+2*PI*i < r & r < 2*PI+2*PI*i or
    PI+2*PI*i = r or r = 2*PI+2*PI*i by REAL_1:def 5;
  hence thesis by Th25,SIN_COS:82,COMPLEX2:9;
end;

theorem
  -PI/2+2*PI*i <= r & r <= PI/2+2*PI*i implies cos r >= 0
proof
  assume -PI/2+2*PI*i <= r & r <= PI/2+2*PI*i;
  then -PI/2+2*PI*i < r & r < PI/2+2*PI*i or
    -PI/2+2*PI*i = r or r = PI/2+2*PI*i by REAL_1:def 5;
  hence thesis by T7a,Th27,SIN_COS:82,COMPLEX2:10;
end;

theorem
  PI/2+2*PI*i <= r & r <= 3/2*PI+2*PI*i implies cos r <= 0
proof
  assume PI/2+2*PI*i <= r & r <= 3/2*PI+2*PI*i;
  then PI/2+2*PI*i < r & r < 3/2*PI+2*PI*i or
    PI/2+2*PI*i = r or r = 3/2*PI+2*PI*i by REAL_1:def 5;
  hence thesis by Th29,SIN_COS:82,COMPLEX2:10;
end;

theorem
  3/2*PI+2*PI*i <= r & r <= 2*PI+2*PI*i implies cos r >= 0
proof
  assume
a1: 3/2*PI+2*PI*i <= r & r <= 2*PI+2*PI*i;
  per cases by a1,REAL_1:def 5;
  suppose 3/2*PI+2*PI*i < r & r < 2*PI+2*PI*i;
  hence thesis by Th31;
  end;
  suppose 3/2*PI+2*PI*i = r;
  hence thesis by SIN_COS:82,COMPLEX2:10;
  end;
  suppose
a3: r = 2*PI+2*PI*i;
  cos(2*PI+2*PI*i) = cos(2*PI) by COMPLEX2:10;
  hence thesis by a3,SIN_COS:82;
  end;
end;

theorem Tsin1:
  2*PI*i <= r & r < 2*PI+2*PI*i & sin r = 0 implies
  r = 2*PI*i or r = PI+2*PI*i
proof
  assume that
A1:  T(i) <= r and
A2:  r < 2*PI+T(i);
  assume sin r = 0;
  then (r <= T(i) or r >= PI+T(i)) &
  (r <= PI+T(i) or 2*PI+T(i) <= r) by Th23,Th25;
  hence r = T(i) or r = PI+T(i) by A1,A2,AXIOMS:21;
end;

theorem Tcos1:
  2*PI*i <= r & r < 2*PI+2*PI*i & cos r = 0 implies
  r = PI/2+2*PI*i or r = 3/2*PI+2*PI*i
proof
  assume that
A1:  T(i) <= r and
A2:  r < 2*PI+T(i);
  assume
A3: cos r = 0;
  then
A5: (-PI/2+T(i) >= r or r >= PI/2+T(i)) &
  (PI/2+T(i) >= r or r >= 3/2*PI+T(i)) by Th27,Th29;
  -PI/2+T(i) < 0+T(i) by COMPTRIG:21,REAL_1:53;
  then r = PI/2+T(i) or r = 3/2*PI+T(i) or r > 3/2*PI+T(i)
    by A5,A1,AXIOMS:21,22;
  hence r = PI/2+T(i) or r = 3/2*PI+T(i) by A2,A3,Th31;
end;

theorem T4:
  sin r = -1 implies r = 3/2*PI + 2*PI*[\r/(2*PI)/]
proof
  set i = [\r/(2*PI)/];
  assume
a1: sin r = -1;
  consider w being real number such that
d1:   w = (2*PI)*-i+r and
d2:   0 <= w and
d3:   w < 2*PI by COMPLEX2:2;
A1: 0+T(i) <= w+T(i) by d2,AXIOMS:24;
A3: w+T(i) < 2*PI+T(i) by d3,REAL_1:53;
  (cos r)*(cos r)+(-1)*(-1) = 1 by a1,SIN_COS:32;
  then (cos r)*(cos r) = 0 by XCMPLX_1:3;
  then cos r = 0 by XCMPLX_1:6;
  then r = PI/2+T(i) or r = 3/2*PI+T(i) by d1,A1,A3,Tcos1;
  hence r = 3/2*PI+T(i) by a1,SIN_COS:82,COMPLEX2:9;
end;

theorem T5:
  sin r = 1 implies r = PI/2 + 2*PI*[\r/(2*PI)/]
proof
  set i = [\r/(2*PI)/];
  assume
a1: sin r = 1;
  consider w being real number such that
d1:   w = (2*PI)*-i+r and
d2:   0 <= w and
d3:   w < 2*PI by COMPLEX2:2;
A1: 0+T(i) <= w+T(i) by d2,AXIOMS:24;
A3: w+T(i) < 2*PI+T(i) by d3,REAL_1:53;
  (cos r)*(cos r)+1*1 = 1 by a1,SIN_COS:32;
  then (cos r)*(cos r) = 0 by XCMPLX_1:3;
  then cos r = 0 by XCMPLX_1:6;
  then r = PI/2+T(i) or r = 3/2*PI+T(i) by d1,A1,A3,Tcos1;
  hence r = PI/2+T(i) by a1,SIN_COS:82,COMPLEX2:9;
end;

theorem T12:
  cos r = -1 implies r = PI + 2*PI*[\r/(2*PI)/]
proof
  set i = [\r/(2*PI)/];
  assume
a1: cos r = -1;
  consider w being real number such that
d1:   w = (2*PI)*-i+r and
d2:   0 <= w and
d3:   w < 2*PI by COMPLEX2:2;
A1: 0+T(i) <= w+T(i) by d2,AXIOMS:24;
A3: w+T(i) < 2*PI+T(i) by d3,REAL_1:53;
  (sin r)*(sin r)+(-1)*(-1) = 1 by a1,SIN_COS:32;
  then (sin r)*(sin r) = 0 by XCMPLX_1:3;
  then sin r = 0 by XCMPLX_1:6;
  then r = 0+T(i) or r = PI+T(i) by d1,A1,A3,Tsin1;
  hence r = PI+T(i) by a1,SIN_COS:34,COMPLEX2:10;
end;

theorem T13:
  cos r = 1 implies r = 2*PI*[\r/(2*PI)/]
proof
  set i = [\r/(2*PI)/];
  assume
a1: cos r = 1;
  consider w being real number such that
d1:   w = (2*PI)*-i+r and
d2:   0 <= w and
d3:   w < 2*PI by COMPLEX2:2;
A1: 0+T(i) <= w+T(i) by d2,AXIOMS:24;
A3: w+T(i) < 2*PI+T(i) by d3,REAL_1:53;
  (sin r)*(sin r)+1*1 = 1 by a1,SIN_COS:32;
  then (sin r)*(sin r) = 0 by XCMPLX_1:3;
  then sin r = 0 by XCMPLX_1:6;
  then r = 0+T(i) or r = PI+T(i) by d1,A1,A3,Tsin1;
  hence r = T(i) by a1,SIN_COS:82,COMPLEX2:10;
end;

theorem Th37:
  0 <= r & r <= 2*PI & sin r = -1 implies r = 3/2*PI
proof
  assume that
Z0: 0 <= r and
Z1: r <= 2*PI and
Z2: sin r = -1;
Z3: r = 2*PI or r < 2*PI by Z1,REAL_1:def 5;
  thus r = 3/2*PI + 2*PI*[\r/(2*PI)/] by Z2,T4
    .= 3/2*PI + 2*PI*0 by Z0,Z2,Z3,Tcal,SIN_COS:82
    .= 3/2*PI;
end;

theorem Th38:
  0 <= r & r <= 2*PI & sin r = 1 implies r = PI/2
proof
  assume that
Z0: 0 <= r and
Z1: r <= 2*PI and
Z2: sin r = 1;
Z3: r = 2*PI or r < 2*PI by Z1,REAL_1:def 5;
  thus r = PI/2 + 2*PI*[\r/(2*PI)/] by Z2,T5
    .= PI/2 + 2*PI*0 by Z0,Z2,Z3,Tcal,SIN_COS:82
    .= PI/2;
end;

:: case cos(r) = 1 has been proved as UNIROOTS:17
theorem Th39:
  0 <= r & r <= 2*PI & cos r = -1 implies r = PI
proof
  assume that
Z0: 0 <= r and
Z1: r <= 2*PI and
Z2: cos r = -1;
Z3: r = 2*PI or r < 2*PI by Z1,REAL_1:def 5;
  thus r = PI + 2*PI*[\r/(2*PI)/] by Z2,T12
    .= PI + 2*PI*0 by Z0,Z3,Z2,Tcal,SIN_COS:82
    .= PI;
end;

theorem Tb1:
  0 <= r & r < PI/2 implies sin r < 1
proof
  assume that
a1: 0 <= r and
a3: r < PI/2 and
b1: sin r >= 1;
  sin r <= 1 by Ts1;
  then
a2: sin r = 1 by b1,AXIOMS:21;
  1/2*PI <= 2*PI by AXIOMS:25;
  then r < 2*PI by a3,AXIOMS:22;
  hence thesis by a3,a1,a2,Th38;
end;

theorem Tb2:
  0 <= r & r < 3/2*PI implies sin r > -1
proof
  assume that
a1: 0 <= r and
a3: r < 3/2*PI and
b1: sin r <= -1;
  sin r >= -1 by Tsm1;
  then
a2: sin r = -1 by b1,AXIOMS:21;
  r <= 2*PI by a3,Lm60,AXIOMS:22;
  hence thesis by a3,a1,a2,Th37;
end;

theorem Tb3:
  3/2*PI < r & r <= 2*PI implies sin r > -1
proof
  assume that
a1: 3/2*PI < r and
a3: r <= 2*PI and
b1: sin r <= -1;
  sin r >= -1 by Tsm1;
  then
a2: sin r = -1 by b1,AXIOMS:21;
  thus thesis by a3,a1,a2,Th37;
end;

theorem Tb4:
  PI/2 < r & r <= 2*PI implies sin r < 1
proof
  assume that
a1: PI/2 < r and
a3: r <= 2*PI and
b1: sin r >= 1;
  sin r <= 1 by Ts1;
  then
a2: sin r = 1 by b1,AXIOMS:21;
  thus thesis by a3,a1,a2,Th38;
end;

theorem Tcos:
  0 < r & r < 2*PI implies cos r < 1
proof
  assume that
a1: 0 < r and
a2: r < 2*PI;
a3: cos r <= 1 by Tc1;
  cos r <> 1 by a1,a2,UNIROOTS:17;
  hence cos(r) < 1 by a3,REAL_1:def 5;
end;

theorem Tb7:
  0 <= r & r < PI implies cos r > -1
proof
  assume that
a1: 0 <= r and
a2: r < PI;
a3: cos r >= -1 by Tcm1;
  r <= 2*PI by a2,Lmp8,AXIOMS:22;
  then cos r <> -1 by a1,a2,Th39;
  hence thesis by a3,REAL_1:def 5;
end;

theorem Tb8:
  PI < r & r <= 2*PI implies cos r > -1
proof
  assume that
a1: PI < r and
a2: r <= 2*PI;
a3: cos r >= -1 by Tcm1;
  cos r <> -1 by a1,a2,Th39;
  hence thesis by a3,REAL_1:def 5;
end;

theorem
  2*PI*i <= r & r < PI/2+2*PI*i implies sin r < 1
proof
  assume that
a1: T(i) <= r and
a3: r < PI/2+T(i) and
b1: sin r >= 1;
a5: T(i)-T(i) <= r-T(i) & r-T(i) < PI/2+T(i)-T(i) by a1,a3,REAL_1:49,54;
a2: sin r <= 1 by Ts1;
  sin(r-T(i)) = sin(r+T(-i))
    .= sin r by COMPLEX2:9
    .= 1 by a2,b1,AXIOMS:21;
  hence thesis by a5,Tb1;
end;

theorem
  2*PI*i <= r & r < 3/2*PI+2*PI*i implies sin r > -1
proof
  assume that
a1: T(i) <= r and
a3: r < 3/2*PI+T(i) and
b1: sin r <= -1;
a5: T(i)-T(i) <= r-T(i) & r-T(i) < 3/2*PI+T(i)-T(i) by a1,a3,REAL_1:49,54;
a2: sin r >= -1 by Tsm1;
  sin(r-T(i)) = sin(r+T(-i))
    .= sin r by COMPLEX2:9
    .= -1 by a2,b1,AXIOMS:21;
  hence thesis by a5,Tb2;
end;

theorem
  3/2*PI+2*PI*i < r & r <= 2*PI+2*PI*i implies sin r > -1
proof
  assume that
a1: 3/2*PI+T(i) < r and
a3: r <= 2*PI+T(i) and
b1: sin r <= -1;
a5: 3/2*PI+T(i)-T(i) < r-T(i) & r-T(i) <= 2*PI+T(i)-T(i) by a1,a3,REAL_1:49,54;
a2: sin r >= -1 by Tsm1;
  sin(r-T(i)) = sin(r+T(-i))
    .= sin r by COMPLEX2:9
    .= -1 by a2,b1,AXIOMS:21;
  hence thesis by a5,Tb3;
end;

theorem
  PI/2+2*PI*i < r & r <= 2*PI+2*PI*i implies sin r < 1
proof
  assume that
a1: PI/2+T(i) < r and
a3: r <= 2*PI+T(i) and
b1: sin r >= 1;
a5: PI/2+T(i)-T(i) < r-T(i) & r-T(i) <= 2*PI+T(i)-T(i) by a1,a3,REAL_1:49,54;
a2: sin r <= 1 by Ts1;
  sin(r-T(i)) = sin(r+T(-i))
    .= sin r by COMPLEX2:9
    .= 1 by a2,b1,AXIOMS:21;
  hence thesis by a5,Tb4;
end;

theorem
  2*PI*i < r & r < 2*PI+2*PI*i implies cos r < 1
proof
  assume that
a1: T(i) < r and
a3: r < 2*PI+T(i) and
b1: cos r >= 1;
a5: T(i)-T(i) < r-T(i) & r-T(i) < 2*PI+T(i)-T(i) by a1,a3,REAL_1:54;
a2: cos r <= 1 by Tc1;
  cos(r-T(i)) = cos(r+T(-i))
    .= cos r by COMPLEX2:10
    .= 1 by a2,b1,AXIOMS:21;
  hence thesis by a5,Tcos;
end;

theorem
  2*PI*i <= r & r < PI+2*PI*i implies cos r > -1
proof
  assume that
a1: T(i) <= r and
a3: r < PI+T(i) and
b1: cos r <= -1;
a5: T(i)-T(i) <= r-T(i) & r-T(i) < PI+T(i)-T(i) by a1,a3,REAL_1:49,54;
a2: cos r >= -1 by Tcm1;
  cos(r-T(i)) = cos(r+T(-i))
    .= cos r by COMPLEX2:10
    .= -1 by a2,b1,AXIOMS:21;
  hence thesis by a5,Tb7;
end;

theorem
  PI+2*PI*i < r & r <= 2*PI+2*PI*i implies cos r > -1
proof
  assume that
a1: PI+T(i) < r and
a3: r <= 2*PI+T(i) and
b1: cos r <= -1;
a5: PI+T(i)-T(i) < r-T(i) & r-T(i) <= 2*PI+T(i)-T(i) by a1,a3,REAL_1:49,54;
a2: cos r >= -1 by Tcm1;
  cos(r-T(i)) = cos(r+T(-i))
    .= cos r by COMPLEX2:10
    .= -1 by a2,b1,AXIOMS:21;
  hence thesis by a5,Tb8;
end;

theorem
  cos(2*PI*r) = 1 implies r in INT
proof
  assume
Z0: cos(2*PI*r) = 1;
  set s = [\r/];
  assume not r in INT;
  then r is not integer by INT_1:def 2;
  then reconsider t = frac r as positive Real by INT_1:72;
a3: r = s+t by INT_1:68;
a4: cos(2*PI*(s+t)) = cos(2*PI*s+2*PI*t)
    .= cos(2*PI*t) by COMPLEX2:10;
  reconsider d = 2 as positive (real number);
  t < 1 by INT_1:69;
  then d*PI*t < d*PI*1 by REAL_1:70;
  hence contradiction by a3,a4,Z0,Tcos;
end;

theorem T22:
  sin.:[.-PI/2,PI/2.] = [.-1,1.] by COMPTRIG:48,RELAT_1:148;

theorem T0:
  sin.:].-PI/2,PI/2.[ = ].-1,1.[
proof
  sin|].-PI/2,PI/2.[ c= sin|[.-PI/2,PI/2.] by Lm31,RELAT_1:104;
  then
a4: rng (sin|].-PI/2,PI/2.[) c= rng (sin|[.-PI/2,PI/2.]) by RELAT_1:25;
a5: rng (sin|].-PI/2,PI/2.[) = sin.:].-PI/2,PI/2.[ by RELAT_1:148;
  thus sin.:].-PI/2,PI/2.[ c= ].-1,1.[
  proof
    let x be set;
    assume
a7:   x in sin.:].-PI/2,PI/2.[;
    then consider a being set such that
a1:   a in dom sin and
a2:   a in ].-PI/2,PI/2.[ and
a3:   sin.a = x by FUNCT_1:def 12;
    reconsider a, x as Real by a1,a3;
w1: sin.a = sin a by SIN_COS:def 21;
a8: -1 <= x & x <= 1 by a4,a5,a7,TOPREAL5:1,COMPTRIG:48;
    set i = [\a/(2*PI)/];
b1: T(i)/(2*PI*1) = i/1 by XCMPLX_1:92;
b2: now
      assume x = -1;
      then a = 3/2*PI+T(i) by w1,a3,T4;
      then -PI/2 < 3/2*PI+T(i) & 3/2*PI+T(i) < PI/2 by a2,JORDAN6:45;
      then -PI/2-3/2*PI < 3/2*PI+T(i)-3/2*PI &
      3/2*PI+T(i)-3/2*PI < PI/2-3/2*PI by REAL_1:54;
      then (-2*PI)/(2*PI) < i & i < (-PI)/(2*PI) by b1,REAL_1:73;
      then -((2*PI)/(2*PI)) < i & i < -(PI/(2*PI)) by XCMPLX_1:188;
      then -1 < i & i < -((1*PI)/(2*PI)) by XCMPLX_1:60;
      then -1+1 <= i & i < -1/2 by INT_1:20,XCMPLX_1:92;
      hence contradiction by AXIOMS:22;
    end;
    now
      assume x = 1;
      then a = PI/2+T(i) by w1,a3,T5;
      then -PI/2 < PI/2+T(i) & PI/2+T(i) < PI/2 by a2,JORDAN6:45;
      then -PI/2-PI/2 < PI/2+T(i)-PI/2 & PI/2+T(i)-PI/2 < PI/2-PI/2
        by REAL_1:54;
      then (-PI)/(2*PI) < T(i)/(2*PI) & T(i)/(2*PI) < 0/(2*PI)
        by REAL_1:73;
      then ((-1)*PI)/(2*PI) < i & i < 0 by b1;
      then (-1)/2 < i & i < 0 by XCMPLX_1:92;
      then -1/2 < i & i <= -1 by INT_1:21;
      hence contradiction by AXIOMS:22;
    end;
    then -1 < x & x < 1 by a8,b2,REAL_1:def 5;
    hence thesis by JORDAN6:45;
  end;
  let a be set;
  assume
c1: a in ].-1,1.[;
  then reconsider a as Real;
  -1 < a & a < 1 by c1,JORDAN6:45;
  then a in rng (sin|[.-PI/2,PI/2.]) by COMPTRIG:48,TOPREAL5:1;
  then consider x being set such that
c2: x in dom (sin|[.-PI/2,PI/2.]) and
c3: (sin|[.-PI/2,PI/2.]).x = a by FUNCT_1:def 5;
c4: dom (sin|[.-PI/2,PI/2.]) = [.-PI/2,PI/2.] by RELAT_1:91,Lmd1;
  reconsider x as Real by c2;
c7: sin.x = a by c2,c3,FUNCT_1:70;
  -PI/2 <= x & x <= PI/2 by c2,c4,TOPREAL5:1;
  then -PI/2 < x & x < PI/2 or -PI/2 = x or PI/2 = x by REAL_1:def 5;
  then x in ].-PI/2,PI/2.[ by c7,c1,T7,JORDAN6:45,SIN_COS:81;
  hence thesis by c7,Lmd1,FUNCT_1:def 12;
end;

theorem
  sin.:[.PI/2,3/2*PI.] = [.-1,1.] by COMPTRIG:49,RELAT_1:148;

theorem
  sin.:].PI/2,3/2*PI.[ = ].-1,1.[
proof
  sin|].PI/2,3/2*PI.[ c= sin|[.PI/2,3/2*PI.] by Lm32,RELAT_1:104;
  then
a4: rng (sin|].PI/2,3/2*PI.[) c= rng (sin|[.PI/2,3/2*PI.]) by RELAT_1:25;
a5: rng (sin|].PI/2,3/2*PI.[) = sin.:].PI/2,3/2*PI.[ by RELAT_1:148;
  thus sin.:].PI/2,3/2*PI.[ c= ].-1,1.[
  proof
    let x be set;
    assume
a7:   x in sin.:].PI/2,3/2*PI.[;
    then consider a being set such that
a1:   a in dom sin and
a2:   a in ].PI/2,3/2*PI.[ and
a3:   sin.a = x by FUNCT_1:def 12;
    reconsider a, x as Real by a1,a3;
w1: sin.a = sin a by SIN_COS:def 21;
a8: -1 <= x & x <= 1 by a4,a5,a7,TOPREAL5:1,COMPTRIG:49;
    set i = [\a/(2*PI)/];
b1: T(i)/(2*PI*1) = i/1 by XCMPLX_1:92;
b2: now
      assume x = 1;
      then a = PI/2+T(i) by w1,a3,T5;
      then PI/2 < PI/2+T(i) & PI/2+T(i) < 3/2*PI by a2,JORDAN6:45;
      then PI/2-PI/2 < PI/2+T(i)-PI/2 &
      PI/2+T(i)-PI/2 < 3/2*PI-PI/2 by REAL_1:54;
      then 0/(2*PI) < i & T(i) < PI by b1,REAL_1:73;
      then 0 < i & i < (1*PI)/(2*PI) by b1,REAL_1:73;
      then 0+1 <= i & i < 1/2 by INT_1:20,XCMPLX_1:92;
      hence contradiction by AXIOMS:22;
    end;
    now
      assume x = -1;
      then a = 3/2*PI+T(i) by w1,a3,T4;
      then PI/2 < 3/2*PI+T(i) & 3/2*PI+T(i) < 3/2*PI by a2,JORDAN6:45;
      then PI/2-3/2*PI < 3/2*PI+T(i)-3/2*PI &
      3/2*PI+T(i)-3/2*PI < 3/2*PI-3/2*PI by REAL_1:54;
      then (-PI)/(2*PI) < T(i)/(2*PI) & T(i)/(2*PI) < 0/(2*PI)
        by REAL_1:73;
      then ((-1)*PI)/(2*PI) < i & i < 0 by b1;
      then (-1)/2 < i & i < 0 by XCMPLX_1:92;
      then -1/2 < i & i <= -1 by INT_1:21;
      hence contradiction by AXIOMS:22;
    end;
    then -1 < x & x < 1 by a8,b2,REAL_1:def 5;
    hence thesis by JORDAN6:45;
  end;
  let a be set;
  assume
c1: a in ].-1,1.[;
  then reconsider a as Real;
  -1 < a & a < 1 by c1,JORDAN6:45;
  then a in rng (sin|[.PI/2,3/2*PI.]) by COMPTRIG:49,TOPREAL5:1;
  then consider x being set such that
c2: x in dom (sin|[.PI/2,3/2*PI.]) and
c3: (sin|[.PI/2,3/2*PI.]).x = a by FUNCT_1:def 5;
c4: dom (sin|[.PI/2,3/2*PI.]) = [.PI/2,3/2*PI.] by RELAT_1:91,Lmd1;
  reconsider x as Real by c2;
c7: sin.x = a by c2,c3,FUNCT_1:70;
  PI/2 <= x & x <= 3/2*PI by c2,c4,TOPREAL5:1;
  then PI/2 < x & x < 3/2*PI or PI/2 = x or 3/2*PI = x by REAL_1:def 5;
  then x in ].PI/2,3/2*PI.[ by c7,c1,JORDAN6:45,SIN_COS:81;
  hence thesis by c7,Lmd1,FUNCT_1:def 12;
end;

theorem T23:
  cos.:[.0,PI.] = [.-1,1.] by COMPTRIG:50,RELAT_1:148;

theorem T0a:
  cos.:].0,PI.[ = ].-1,1.[
proof
  cos|].0,PI.[ c= cos|[.0,PI.] by Lm50,RELAT_1:104;
  then
a4: rng (cos|].0,PI.[) c= rng (cos|[.0,PI.]) by RELAT_1:25;
a5: rng (cos|].0,PI.[) = cos.:].0,PI.[ by RELAT_1:148;
  thus cos.:].0,PI.[ c= ].-1,1.[
  proof
    let x be set;
    assume
a7:   x in cos.:].0,PI.[;
    then consider a being set such that
a1:   a in dom cos and
a2:   a in ].0,PI.[ and
a3:   cos.a = x by FUNCT_1:def 12;
    reconsider a, x as Real by a1,a3;
w1: cos.a = cos a by SIN_COS:def 23;
a8: -1 <= x & x <= 1 by a4,a5,a7,TOPREAL5:1,COMPTRIG:50;
    set i = [\a/(2*PI)/];
b1: T(i)/(2*PI*1) = i/1 by XCMPLX_1:92;
b2: now
      assume x = -1;
      then a = PI+T(i) by w1,a3,T12;
      then 0 < PI+T(i) & PI+T(i) < PI by a2,JORDAN6:45;
      then 0-PI < PI+T(i)-PI & PI+T(i)-PI < PI-PI by REAL_1:54;
      then (-PI)/(2*PI) < T(i)/(2*PI) & T(i)/(2*PI) < 0/(2*PI)
        by REAL_1:73;
      then -(1*PI)/(2*PI) < i & i < 0 by b1,XCMPLX_1:188;
      then -1/2 < i & i <= -1 by INT_1:21,XCMPLX_1:92;
      hence contradiction by AXIOMS:22;
    end;
    now
      assume x = 1;
      then a = T(i) by w1,a3,T13;
      then 0 < T(i) & T(i) < PI by a2,JORDAN6:45;
      then 0/(2*PI) < T(i)/(2*PI) & T(i)/(2*PI) < PI/(2*PI) by REAL_1:73;
      then 0 < i & i < (1*PI)/(2*PI) by b1;
      then 0+1 <= i & i < 1/2 by INT_1:20,XCMPLX_1:92;
      hence contradiction by AXIOMS:22;
    end;
    then -1 < x & x < 1 by a8,b2,REAL_1:def 5;
    hence thesis by JORDAN6:45;
  end;
  let a be set;
  assume
c1: a in ].-1,1.[;
  then reconsider a as Real;
  -1 < a & a < 1 by c1,JORDAN6:45;
  then a in rng (cos|[.0,PI.]) by COMPTRIG:50,TOPREAL5:1;
  then consider x being set such that
c2: x in dom (cos|[.0,PI.]) and
c3: (cos|[.0,PI.]).x = a by FUNCT_1:def 5;
c4: dom (cos|[.0,PI.]) = [.0,PI.] by RELAT_1:91,Lmd2;
  reconsider x as Real by c2;
c7: cos.x = a by c2,c3,FUNCT_1:70;
  0 <= x & x <= PI by c2,c4,TOPREAL5:1;
  then 0 < x & x < PI or 0 = x or PI = x by REAL_1:def 5;
  then x in ].0,PI.[ by c7,c1,JORDAN6:45,SIN_COS:33,81;
  hence thesis by c7,Lmd2,FUNCT_1:def 12;
end;

theorem
  cos.:[.PI,2*PI.] = [.-1,1.] by COMPTRIG:51,RELAT_1:148;

theorem
  cos.:].PI,2*PI.[ = ].-1,1.[
proof
  cos|].PI,2*PI.[ c= cos|[.PI,2*PI.] by Lm51,RELAT_1:104;
  then
a4: rng (cos|].PI,2*PI.[) c= rng (cos|[.PI,2*PI.]) by RELAT_1:25;
a5: rng (cos|].PI,2*PI.[) = cos.:].PI,2*PI.[ by RELAT_1:148;
  thus cos.:].PI,2*PI.[ c= ].-1,1.[
  proof
    let x be set;
    assume
a7:   x in cos.:].PI,2*PI.[;
    then consider a being set such that
a1:   a in dom cos and
a2:   a in ].PI,2*PI.[ and
a3:   cos.a = x by FUNCT_1:def 12;
    reconsider a, x as Real by a1,a3;
w1: cos.a = cos a by SIN_COS:def 23;
a8: -1 <= x & x <= 1 by a4,a5,a7,TOPREAL5:1,COMPTRIG:51;
    set i = [\a/(2*PI)/];
b1: T(i)/(2*PI*1) = i/1 by XCMPLX_1:92;
b2: now
      assume x = 1;
      then a = T(i) by w1,a3,T13;
      then PI < T(i) & T(i) < 2*PI by a2,JORDAN6:45;
      then (1*PI)/(2*PI) < i & i < 2*PI/(2*PI) by b1,REAL_1:73;
      then 1/2 < i & i < 1+0 by XCMPLX_1:60,92;
      then 1/2 < i & i <= 0 by INT_1:20;
      hence contradiction;
    end;
    now
      assume x = -1;
      then a = PI+T(i) by w1,a3,T12;
      then PI < PI+T(i) & PI+T(i) < 2*PI by a2,JORDAN6:45;
      then PI-PI < PI+T(i)-PI & PI+T(i)-PI < 2*PI-PI by REAL_1:54;
      then 0/(2*PI) < i & i < (1*PI)/(2*PI) by b1,REAL_1:73;
      then 0+1 <= i & i <= 1/2 by INT_1:20,XCMPLX_1:92;
      hence contradiction by AXIOMS:22;
    end;
    then -1 < x & x < 1 by a8,b2,REAL_1:def 5;
    hence thesis by JORDAN6:45;
  end;
  let a be set;
  assume
c1: a in ].-1,1.[;
  then reconsider a as Real;
  -1 < a & a < 1 by c1,JORDAN6:45;
  then a in rng (cos|[.PI,2*PI.]) by COMPTRIG:51,TOPREAL5:1;
  then consider x being set such that
c2: x in dom (cos|[.PI,2*PI.]) and
c3: (cos|[.PI,2*PI.]).x = a by FUNCT_1:def 5;
c4: dom (cos|[.PI,2*PI.]) = [.PI,2*PI.] by RELAT_1:91,Lmd2;
  reconsider x as Real by c2;
c7: cos.x = a by c2,c3,FUNCT_1:70;
  PI <= x & x <= 2*PI by c2,c4,TOPREAL5:1;
  then PI < x & x < 2*PI or PI = x or 2*PI = x by REAL_1:def 5;
  then x in ].PI,2*PI.[ by c7,c1,JORDAN6:45,SIN_COS:81;
  hence thesis by c7,Lmd2,FUNCT_1:def 12;
end;

Lm10:
now
  let i, r;
  let p1, p2 be real number;
  assume r in [.p1+T(i),p2+T(i).];
  then p1+T(i) <= r & r <= p2+T(i) by TOPREAL5:1;
  then p1+T(i)+2*PI <= r+2*PI & r+2*PI <= p2+T(i)+2*PI by AXIOMS:24;
  hence r+2*PI in [.p1+T(i+1),p2+T(i+1).] by TOPREAL5:1;
end;

Lm11:
now
  let i, r;
  let p1, p2 be real number;
  assume r in [.p1+T(i),p2+T(i).] /\ REAL;
  then r in [.p1+T(i),p2+T(i).] & r in REAL by XBOOLE_0:def 3;
  then r+2*PI in [.p1+T(i+1),p2+T(i+1).] & r+2*PI in REAL
    by Lm10,XREAL_0:def 1;
  hence r+2*PI in [.p1+T(i+1),p2+T(i+1).] /\ REAL by XBOOLE_0:def 3;
end;

Lm13:
now
  let i, r;
  let p1, p2 be real number;
  assume r in [.p1+T(i),p2+T(i).];
  then p1+T(i) <= r & r <= p2+T(i) by TOPREAL5:1;
  then p1+T(i)-2*PI <= r-2*PI & r-2*PI <= p2+T(i)-2*PI by REAL_1:49;
  hence r-2*PI in [.p1+T(i-1),p2+T(i-1).] by TOPREAL5:1;
end;

Lm12:
now
  let i, r;
  let p1, p2 be real number;
  assume r in [.p1+T(i),p2+T(i).] /\ REAL;
  then r in [.p1+T(i),p2+T(i).] & r in REAL by XBOOLE_0:def 3;
  then r-2*PI in [.p1+T(i-1),p2+T(i-1).] & r-2*PI in REAL
    by Lm13,XREAL_0:def 1;
  hence r-2*PI in [.p1+T(i-1),p2+T(i-1).] /\ REAL by XBOOLE_0:def 3;
end;

theorem T32:
  sin is_increasing_on [.-PI/2+2*PI*i,PI/2+2*PI*i.]
proof
  defpred P[Integer] means sin is_increasing_on [.-PI/2+T($1),PI/2+T($1).];
a1: P[0] by COMPTRIG:39;
a2: for i holds P[i] implies P[i-1] & P[i+1]
  proof
    let i such that
a3:   P[i];
    set Z = [.-PI/2+T(i-1+1),PI/2+T(i-1+1).];
    thus P[i-1]
    proof
      set Y = [.-PI/2+T(i-1),PI/2+T(i-1).];
      let r1, r2 be Element of REAL;
      assume r1 in Y /\ dom sin & r2 in Y /\ dom sin;
      then
a4:   r1+2*PI in Z /\ dom sin & r2+2*PI in Z /\ dom sin by Lm11,Lmd1;
      assume r1 < r2;
      then r1+2*PI < r2+2*PI by REAL_1:53;
      then sin.(r1+2*PI) < sin.(r2+2*PI*1) by a3,a4,RFUNCT_2:def 2;
      then sin.(r1+2*PI*1) < sin.r2 by T30;
      hence sin.r1 < sin.r2 by T30;
    end;
    set Y = [.-PI/2+T(i+1),PI/2+T(i+1).];
a5: Z = [.-PI/2+T(i+1-1),PI/2+T(i+1-1).];
    let r1, r2 be Element of REAL;
    assume r1 in Y /\ dom sin & r2 in Y /\ dom sin;
    then
a4: r1-2*PI in Z /\ dom sin & r2-2*PI in Z /\ dom sin by a5,Lm12,Lmd1;
    assume r1 < r2;
    then r1-2*PI < r2-2*PI by REAL_1:54;
    then sin.(r1-2*PI) < sin.(r2+2*PI*(-1)) by a3,a4,RFUNCT_2:def 2;
    then sin.(r1+2*PI*(-1)) < sin.r2 by T30;
    hence sin.r1 < sin.r2 by T30;
  end;
  for i holds P[i] from INT_1:sch 4(a1,a2);
  hence thesis;
end;

theorem T33:
  sin is_decreasing_on [.PI/2+2*PI*i,3/2*PI+2*PI*i.]
proof
  defpred P[Integer] means sin is_decreasing_on [.PI/2+T($1),3/2*PI+T($1).];
a1: P[0] by COMPTRIG:40;
a2: for i holds P[i] implies P[i-1] & P[i+1]
  proof
    let i such that
a3:   P[i];
    set Z = [.PI/2+T(i-1+1),3/2*PI+T(i-1+1).];
    thus P[i-1]
    proof
      set Y = [.PI/2+T(i-1),3/2*PI+T(i-1).];
      let r1, r2 be Element of REAL;
      assume r1 in Y /\ dom sin & r2 in Y /\ dom sin;
      then
a4:   r1+2*PI in Z /\ dom sin & r2+2*PI in Z /\ dom sin by Lm11,Lmd1;
      assume r1 < r2;
      then r1+2*PI < r2+2*PI by REAL_1:53;
      then sin.(r1+2*PI) > sin.(r2+2*PI*1) by a3,a4,RFUNCT_2:def 3;
      then sin.(r1+2*PI*1) > sin.r2 by T30;
      hence sin.r1 > sin.r2 by T30;
    end;
    set Y = [.PI/2+T(i+1),3/2*PI+T(i+1).];
a5: Z = [.PI/2+T(i+1-1),3/2*PI+T(i+1-1).];
    let r1, r2 be Element of REAL;
    assume r1 in Y /\ dom sin & r2 in Y /\ dom sin;
    then
a4: r1-2*PI in Z /\ dom sin & r2-2*PI in Z /\ dom sin by a5,Lm12,Lmd1;
    assume r1 < r2;
    then r1-2*PI < r2-2*PI by REAL_1:54;
    then sin.(r1-2*PI) > sin.(r2+2*PI*(-1)) by a3,a4,RFUNCT_2:def 3;
    then sin.(r1+2*PI*(-1)) > sin.r2 by T30;
    hence sin.r1 > sin.r2 by T30;
  end;
  for i holds P[i] from INT_1:sch 4(a1,a2);
  hence thesis;
end;

theorem T34:
  cos is_decreasing_on [.2*PI*i,PI+2*PI*i.]
proof
  defpred P[Integer] means cos is_decreasing_on [.T($1),PI+T($1).];
a1: P[0] by COMPTRIG:41;
a2: for i holds P[i] implies P[i-1] & P[i+1]
  proof
    let i such that
a3:   P[i];
    set Z = [.0+T(i-1+1),PI+T(i-1+1).];
    thus P[i-1]
    proof
      set Y = [.T(i-1),PI+T(i-1).];
a5:   Y = [.0+T(i-1),PI+T(i-1).];
      let r1, r2 be Element of REAL;
      assume r1 in Y /\ dom cos & r2 in Y /\ dom cos;
      then
a4:   r1+2*PI in Z /\ dom cos & r2+2*PI in Z /\ dom cos by a5,Lm11,Lmd2;
      assume r1 < r2;
      then r1+2*PI < r2+2*PI by REAL_1:53;
      then cos.(r1+2*PI) > cos.(r2+2*PI*1) by a3,a4,RFUNCT_2:def 3;
      then cos.(r1+2*PI*1) > cos.r2 by T31;
      hence cos.r1 > cos.r2 by T31;
    end;
    set Y = [.T(i+1),PI+T(i+1).];
a6: Y = [.0+T(i+1),PI+T(i+1).];
a5: Z = [.T(i+1-1),PI+T(i+1-1).];
    let r1, r2 be Element of REAL;
    assume r1 in Y /\ dom cos & r2 in Y /\ dom cos;
    then
a4: r1-2*PI in Z /\ dom cos & r2-2*PI in Z /\ dom cos by a5,a6,Lm12,Lmd2;
    assume r1 < r2;
    then r1-2*PI < r2-2*PI by REAL_1:54;
    then cos.(r1-2*PI) > cos.(r2+2*PI*(-1)) by a3,a4,RFUNCT_2:def 3;
    then cos.(r1+2*PI*(-1)) > cos.r2 by T31;
    hence cos.r1 > cos.r2 by T31;
  end;
  for i holds P[i] from INT_1:sch 4(a1,a2);
  hence thesis;
end;

theorem T35:
  cos is_increasing_on [.PI+2*PI*i,2*PI+2*PI*i.]
proof
  defpred P[Integer] means cos is_increasing_on [.PI+T($1),2*PI+T($1).];
a1: P[0] by COMPTRIG:42;
a2: for i holds P[i] implies P[i-1] & P[i+1]
  proof
    let i such that
a3:   P[i];
    set Z = [.PI+T(i-1+1),2*PI+T(i-1+1).];
    thus P[i-1]
    proof
      set Y = [.PI+T(i-1),2*PI+T(i-1).];
      let r1, r2 be Element of REAL;
      assume r1 in Y /\ dom cos & r2 in Y /\ dom cos;
      then
a4:   r1+2*PI in Z /\ dom cos & r2+2*PI in Z /\ dom cos by Lm11,Lmd2;
      assume r1 < r2;
      then r1+2*PI < r2+2*PI by REAL_1:53;
      then cos.(r1+2*PI) < cos.(r2+2*PI*1) by a3,a4,RFUNCT_2:def 2;
      then cos.(r1+2*PI*1) < cos.r2 by T31;
      hence cos.r1 < cos.r2 by T31;
    end;
    set Y = [.PI+T(i+1),2*PI+T(i+1).];
a5: Z = [.PI+T(i+1-1),2*PI+T(i+1-1).];
    let r1, r2 be Element of REAL;
    assume r1 in Y /\ dom cos & r2 in Y /\ dom cos;
    then
a4: r1-2*PI in Z /\ dom cos & r2-2*PI in Z /\ dom cos by a5,Lm12,Lmd2;
    assume r1 < r2;
    then r1-2*PI < r2-2*PI by REAL_1:54;
    then cos.(r1-2*PI) < cos.(r2+2*PI*(-1)) by a3,a4,RFUNCT_2:def 2;
    then cos.(r1+2*PI*(-1)) < cos.r2 by T31;
    hence cos.r1 < cos.r2 by T31;
  end;
  for i holds P[i] from INT_1:sch 4(a1,a2);
  hence thesis;
end;

theorem T1:
  sin | [.-PI/2+2*PI*i,PI/2+2*PI*i.] is one-to-one
proof
  set Q = [.-PI/2+2*PI*i,PI/2+2*PI*i.];
  sin is_increasing_on Q by T32;
  then sin|Q is_increasing_on Q by RFUNCT_2:50;
  then sin|Q|Q is one-to-one by FCONT_3:16;
  hence thesis by RELAT_1:101;
end;

theorem T1a:
  sin | [.PI/2+2*PI*i,3/2*PI+2*PI*i.] is one-to-one
proof
  set Q = [.PI/2+2*PI*i,3/2*PI+2*PI*i.];
  sin is_decreasing_on Q by T33;
  then sin|Q is_decreasing_on Q by RFUNCT_2:51;
  then sin|Q|Q is one-to-one by FCONT_3:16;
  hence thesis by RELAT_1:101;
end;

registration
  cluster sin | [.-PI/2,PI/2.] -> one-to-one;
coherence by Lm0,T1;
  cluster sin | [.PI/2,3/2*PI.] -> one-to-one;
coherence by Lm0a,T1a;
end;

registration
  cluster sin | [.-PI/2,0 .] -> one-to-one;
coherence
proof
  sin | [.-PI/2,PI/2.] is one-to-one;
  hence thesis by Lm40,Tzaw11;
end;
  cluster sin | [.0,PI/2.] -> one-to-one;
coherence
proof
  sin | [.-PI/2,PI/2.] is one-to-one;
  hence thesis by Lm41,Tzaw11;
end;
  cluster sin | [.PI/2,PI.] -> one-to-one;
coherence
proof
  sin | [.PI/2,3/2*PI.] is one-to-one;
  hence thesis by Lm42,Tzaw11;
end;
  cluster sin | [.PI,3/2*PI.] -> one-to-one;
coherence
proof
  sin | [.PI/2,3/2*PI.] is one-to-one;
  hence thesis by Lm43,Tzaw11;
end;
  cluster sin | [.3/2*PI,2*PI.] -> one-to-one;
coherence
proof
  sin | [.-PI/2+T(1),PI/2+T(1).] is one-to-one by T1;
  hence thesis by Lm44,Tzaw11;
end;
end;

registration
  cluster sin | ].-PI/2,PI/2.[ -> one-to-one;
coherence
proof
  sin | [.-PI/2,PI/2.] is one-to-one;
  hence thesis by Lm31,Tzaw11;
end;
  cluster sin | ].PI/2,3/2*PI.[ -> one-to-one;
coherence
proof
  sin | [.PI/2,3/2*PI.] is one-to-one;
  hence thesis by Lm32,Tzaw11;
end;
  cluster sin | ].-PI/2,0 .[ -> one-to-one;
coherence
proof
  sin | [.-PI/2,0 .] is one-to-one;
  hence thesis by Lm33,Tzaw11;
end;
  cluster sin | ].0,PI/2.[ -> one-to-one;
coherence
proof
  sin | [.0,PI/2.] is one-to-one;
  hence thesis by Lm34,Tzaw11;
end;
  cluster sin | ].PI/2,PI.[ -> one-to-one;
coherence
proof
  sin | [.PI/2,PI.] is one-to-one;
  hence thesis by Lm36,Tzaw11;
end;
  cluster sin | ].PI,3/2*PI.[ -> one-to-one;
coherence
proof
  sin | [.PI,3/2*PI.] is one-to-one;
  hence thesis by Lm35,Tzaw11;
end;
  cluster sin | ].3/2*PI,2*PI.[ -> one-to-one;
coherence
proof
  sin | [.3/2*PI,2*PI.] is one-to-one;
  hence thesis by Lm30,Tzaw11;
end;
end;

theorem T11:
  cos | [.2*PI*i,PI+2*PI*i.] is one-to-one
proof
  set Q1 = [.2*PI*i,PI+2*PI*i.];
  cos is_decreasing_on Q1 by T34;
  then cos|Q1 is_decreasing_on Q1 by RFUNCT_2:51;
  then cos|Q1|Q1 is one-to-one by FCONT_3:16;
  hence thesis by RELAT_1:101;
end;

theorem T11a:
  cos | [.PI+2*PI*i,2*PI+2*PI*i.] is one-to-one
proof
  set Q1 = [.PI+2*PI*i,2*PI+2*PI*i.];
  cos is_increasing_on Q1 by T35;
  then cos|Q1 is_increasing_on Q1 by RFUNCT_2:50;
  then cos|Q1|Q1 is one-to-one by FCONT_3:16;
  hence thesis by RELAT_1:101;
end;

registration
  cluster cos | [.0,PI.] -> one-to-one;
coherence by Lm3,T11;
  cluster cos | [.PI,2*PI.] -> one-to-one;
coherence by Lm3a,T11a;
end;

registration
  cluster cos | [.0,PI/2.] -> one-to-one;
coherence
proof
  cos | [.0,PI.] is one-to-one;
  hence thesis by Lm53,Tzaw11;
end;
  cluster cos | [.PI/2,PI.] -> one-to-one;
coherence
proof
  cos | [.0,PI.] is one-to-one;
  hence thesis by Lm54,Tzaw11;
end;
  cluster cos | [.PI,3/2*PI.] -> one-to-one;
coherence
proof
  cos | [.PI,2*PI.] is one-to-one;
  hence thesis by Lm55,Tzaw11;
end;
  cluster cos | [.3/2*PI,2*PI.] -> one-to-one;
coherence
proof
  cos | [.PI,2*PI.] is one-to-one;
  hence thesis by Lm56,Tzaw11;
end;
end;

registration
  cluster cos | ].0,PI.[ -> one-to-one;
coherence
proof
  cos | [.0,PI.] is one-to-one;
  hence thesis by Lm50,Tzaw11;
end;
  cluster cos | ].PI,2*PI.[ -> one-to-one;
coherence
proof
  cos | [.PI,2*PI.] is one-to-one;
  hence thesis by Lm51,Tzaw11;
end;
  cluster cos | ].0,PI/2.[ -> one-to-one;
coherence
proof
  cos | [.0,PI/2.] is one-to-one;
  hence thesis by Lm34,Tzaw11;
end;
  cluster cos | ].PI/2,PI.[ -> one-to-one;
coherence
proof
  cos | [.PI/2,PI.] is one-to-one;
  hence thesis by Lm36,Tzaw11;
end;
  cluster cos | ].PI,3/2*PI.[ -> one-to-one;
coherence
proof
  cos | [.PI,3/2*PI.] is one-to-one;
  hence thesis by Lm35,Tzaw11;
end;
  cluster cos | ].3/2*PI,2*PI.[ -> one-to-one;
coherence
proof
  cos | [.3/2*PI,2*PI.] is one-to-one;
  hence thesis by Lm30,Tzaw11;
end;
end;

theorem
  2*PI*i <= r & r < 2*PI+2*PI*i & 2*PI*i <= s & s < 2*PI+2*PI*i &
  sin r = sin s & cos r = cos s implies r = s
proof
  assume that
A1: 2*PI*i <= r and
A2: r < 2*PI+2*PI*i and
A3: 2*PI*i <= s and
A4: s < 2*PI+2*PI*i and
A5: sin r = sin s and
A6: cos r = cos s;
A7: cos(r-s) = (cos r)*(cos s)+(sin r)*(sin s) by COMPLEX2:4
      .= 1 by A5,A6,SIN_COS:32;
A8: cos (s-r) = (cos r)*(cos s)+(sin r)*(sin s) by COMPLEX2:4
      .= 1 by A5,A6,SIN_COS:32;
A9: sin(r-s) = (sin r)*(cos s)-(cos r)*(sin s) by COMPLEX2:4
      .= 0 by A5,A6;
A10: sin (s-r) = (sin s)*(cos r)-(cos s)*(sin r) by COMPLEX2:4
      .= 0 by A5,A6;
  per cases by REAL_1:def 5;
  suppose r > s;
  then r > s+0;
  then
A11: 0 <= r-s by REAL_1:86;
  r+2*PI*i < 2*PI+2*PI*i+s by A2,A3,REAL_1:67;
  then r+2*PI*i-2*PI*i < 2*PI+2*PI*i+s-2*PI*i by REAL_1:54;
  then r < 2*PI+s;
  then r-s < 2*PI by REAL_1:84;
  then r-s = 0 or r-s = PI by A9,A11,COMPTRIG:33;
  hence r = s by A7,SIN_COS:82,XCMPLX_1:15;
  end;
  suppose r < s;
  then s > r+0;
  then
A12: 0 <= s-r by REAL_1:86;
  s+2*PI*i < 2*PI+2*PI*i+r by A1,A4,REAL_1:67;
  then s+2*PI*i-2*PI*i < 2*PI+2*PI*i+r-2*PI*i by REAL_1:54;
  then s < 2*PI+r;
  then s-r < 2*PI by REAL_1:84;
  then s-r = 0 or s-r = PI by A10,A12,COMPTRIG:33;
  hence r = s by A8,SIN_COS:82,XCMPLX_1:15;
  end;
  suppose r = s; hence thesis;
  end;
end;

begin :: arcsin

definition
  func arcsin -> PartFunc of REAL, REAL equals :darcsin:
  (sin | [.-PI/2,PI/2.])";
coherence;
end;

definition
  let r be set;
  func arcsin r equals :darcsin1:
  arcsin.r;
coherence;
end;

definition
  let r be set;
  redefine func arcsin r -> Real;
coherence
proof
  arcsin r = arcsin.r by darcsin1;
  hence thesis;
end;
end;

theorem T3:
  arcsin" = sin | [.-PI/2,PI/2.] by darcsin,FUNCT_1:65;

theorem T2:
  rng arcsin = [.-PI/2,PI/2.]
proof
  dom (sin|[.-PI/2,PI/2.]) = [.-PI/2,PI/2.] by Lmd1,RELAT_1:91;
  hence thesis by darcsin,FUNCT_1:55;
end;

registration
  cluster arcsin -> one-to-one;
coherence by darcsin,FUNCT_1:62;
end;

theorem T6:
  dom arcsin = [.-1,1.] by darcsin,FUNCT_1:55,COMPTRIG:48;

theorem T9:
  (sin | [.-PI/2,PI/2.]) qua Function * arcsin = id [.-1,1.]
  by darcsin,COMPTRIG:48,FUNCT_1:61;

theorem
  arcsin * (sin | [.-PI/2,PI/2.]) = id [.-1,1.]
  by darcsin,COMPTRIG:48,FUNCT_1:61;

theorem T10:
  (sin | [.-PI/2,PI/2.]) * arcsin = id [.-PI/2,PI/2.] by T2,T3,FUNCT_1:61;

theorem
  arcsin qua Function * (sin | [.-PI/2,PI/2.]) = id [.-PI/2,PI/2.]
  by T2,T3,FUNCT_1:61;

theorem T18:
  -1 <= r & r <= 1 implies sin arcsin r = r
proof
  assume -1 <= r & r <= 1;
  then
Z0: r in [.-1,1.] by TOPREAL5:1;
  then
Z1: (arcsin.r) in [.-PI/2,PI/2.] by T2,T6,FUNCT_1:def 5;
  thus sin arcsin r = sin.(arcsin r) by SIN_COS:def 21
    .= sin.(arcsin.r) by darcsin1
    .= ((sin|[.-PI/2,PI/2.]) qua Function).(arcsin.r) by Z1,FUNCT_1:72
    .= (id [.-1,1.]).r by T9,Z0,T6,FUNCT_1:23
    .= r by Z0,FUNCT_1:35;
end;

theorem T20:
  -PI/2 <= r & r <= PI/2 implies arcsin sin r = r
proof
  assume -PI/2 <= r & r <= PI/2;
  then
Z0: r in [.-PI/2,PI/2.] by TOPREAL5:1;
a1: dom (sin|[.-PI/2,PI/2.]) = [.-PI/2,PI/2.] by Lmd1,RELAT_1:91;
  thus arcsin sin r = arcsin(sin.r) by SIN_COS:def 21
    .= arcsin.(sin.r) by darcsin1
    .= arcsin.((sin|[.-PI/2,PI/2.]).r) by Z0,FUNCT_1:72
    .= (id [.-PI/2,PI/2.]).r by a1,T10,Z0,FUNCT_1:23
    .= r by Z0,FUNCT_1:35;
end;

theorem
  arcsin (-1) = -PI/2 by T7,Lm4,T20;

theorem
  arcsin 0 = 0 by Lm7,T20,SIN_COS:34;

theorem
  arcsin 1 = PI/2 by Lm4,T20,SIN_COS:82;

theorem
  -1 <= r & r <= 1 & arcsin r = -PI/2 implies r = -1 by T7,T18;

theorem
  -1 <= r & r <= 1 & arcsin r = 0 implies r = 0 by T18,SIN_COS:34;

theorem
  -1 <= r & r <= 1 & arcsin r = PI/2 implies r = 1 by T18,SIN_COS:82;

theorem T16:
  -1 <= r & r <= 1 implies -PI/2 <= arcsin r & arcsin r <= PI/2
proof
  assume -1 <= r & r <= 1;
  then r in [.-1,1.] by TOPREAL5:1;
  then arcsin.r in rng arcsin by T6,FUNCT_1:def 5;
  then arcsin r in rng arcsin by darcsin1;
  hence thesis by T2,TOPREAL5:1;
end;

theorem T16c:
  -1 < r & r < 1 implies -PI/2 < arcsin r & arcsin r < PI/2
proof
  assume
a1: -1 < r & r < 1;
  then -PI/2 <= arcsin r & arcsin r <= PI/2 by T16;
  then -PI/2 < arcsin r & arcsin r < PI/2 or
    -PI/2 = arcsin r or arcsin r = PI/2 by REAL_1:def 5;
  hence thesis by a1,T7,T18,SIN_COS:82;
end;

theorem Ta3:
  -1 <= r & r <= 1 implies arcsin r = -arcsin(-r)
proof
  assume -1 <= r & r <= 1;
  then
a1: --1 >= -r & -r >= -1 by REAL_1:50;
a2: r = 0-1*(-r)
     .= sin 0 * cos arcsin(-r) - cos 0 * sin arcsin(-r) by a1,T18,SIN_COS:34
     .= sin(0-arcsin(-r)) by COMPLEX2:4;
  arcsin(-r) <= PI/2 by a1,T16;
  then
a3: -PI/2 <= -arcsin(-r) by REAL_1:50;
  -PI/2 <= arcsin(-r) by a1,T16;
  then -arcsin(-r) <= --PI/2 by REAL_1:50;
  hence thesis by a2,a3,T20;
end;

theorem Tw1:
  0 <= s & r^2 + s^2 = 1 implies cos arcsin r = s
proof
  set x = arcsin r;
  assume that
a3: 0 <= s and
Z0: r^2 + s^2 = 1;
a1: -1 <= r & r <= 1 by Z0,Lm5;
  (sin.x)^2 + (cos.x)^2 = 1 by SIN_COS:31;
  then (cos.x)^2 = 1 - (sin.x)^2
    .= 1 - (sin x)^2 by SIN_COS:def 21
    .= 1 - r^2 by a1,T18;
  then
a2: cos.x = s or cos.x = -s by Z0,JGRAPH_3:1;
  -PI/2 <= x & x <= PI/2 by a1,T16;
  then
a4: x in [.-PI/2,PI/2.] by TOPREAL5:1;
  ---s < 0 or s = 0 by a3,REAL_1:66;
  hence cos x = s by a2,a4,COMPTRIG:28,SIN_COS:def 23;
end;

theorem
  s <= 0 & r^2 + s^2 = 1 implies cos arcsin r = -s
proof
  set x = arcsin r;
  assume that
a3: s <= 0 and
Z0: r^2 + s^2 = 1;
a1: -1 <= r & r <= 1 by Z0,Lm5;
  (sin.x)^2 + (cos.x)^2 = 1 by SIN_COS:31;
  then (cos.x)^2 = 1 - (sin.x)^2
    .= 1 - (sin x)^2 by SIN_COS:def 21
    .= 1 - r^2 by a1,T18;
  then
a2: cos.x = s or cos.x = -s by Z0,JGRAPH_3:1;
  -PI/2 <= x & x <= PI/2 by a1,T16;
  then
a4: x in [.-PI/2,PI/2.] by TOPREAL5:1;
  0 > s or s = 0 by a3;
  hence cos x = -s by a2,a4,COMPTRIG:28,SIN_COS:def 23;
end;

theorem Te1:
  -1 <= r & r <= 1 implies cos arcsin r = sqrt(1-r^2)
proof
  set s = sqrt(1-r^2);
  assume -1 <= r & r <= 1;
  then r^2+0 <= 1 by JGRAPH_2:7,SQUARE_1:59;
  then 0 <= 1-r^2 by REAL_1:84;
  then 0 <= s & r^2 + s^2 = r^2 + (1-r^2) by SQUARE_1:def 4;
  hence thesis by Tw1;
end;

theorem
  arcsin is_increasing_on [.-1,1.]
proof
  set f = sin | [.-PI/2,PI/2.];
a1: f.:[.-PI/2,PI/2.] = [.-1,1.] by T22,RFUNCT_2:5;
  f is_increasing_on [.-PI/2,PI/2.] by COMPTRIG:39,RFUNCT_2:50;
  then (f|[.-PI/2,PI/2.])" is_increasing_on f.:[.-PI/2,PI/2.] by FCONT_3:17;
  hence thesis by a1,darcsin,RELAT_1:101;
end;

theorem
  arcsin is_differentiable_on ].-1,1.[ &
  (-1 < r & r < 1 implies diff(arcsin,r) = 1 / sqrt(1-r^2))
proof
  set f = sin|].-PI/2,PI/2.[;
  set h = sin|[.-PI/2,PI/2.];
  set g = arcsin|].-1,1.[;
a3: f|].-PI/2,PI/2.[ = f by RELAT_1:101;
a5: f" = (h|].-PI/2,PI/2.[)" by Lm31,RELAT_1:103
      .= h"|(h.:].-PI/2,PI/2.[) by RFUNCT_2:40
      .= g by Lm31,T0,darcsin,EXTENS_1:1;
a4: dom (f") = ].-1,1.[ by a5,Lm52,T6,RELAT_1:91;
a0: sin is_differentiable_on ].-PI/2,PI/2.[ by FDIFF_1:34,SIN_COS:73;
  then
a1: f is_differentiable_on ].-PI/2,PI/2.[ by FDIFF_2:16;
a6: now
    let x0 be Real such that
a2:   x0 in ].-PI/2,PI/2.[;
    diff(f,x0) = (f`|].-PI/2,PI/2.[).x0 by a1,a2,FDIFF_1:def 8
      .= (sin`|].-PI/2,PI/2.[).x0 by a0,FDIFF_2:16
      .= diff(sin,x0) by a0,a2,FDIFF_1:def 8
      .= cos.x0 by SIN_COS:73;
    hence 0 < diff(f,x0) by a2,COMPTRIG:27;
  end;
  then
A1: g is_differentiable_on ].-1,1.[ by a4,a5,a3,a1,FDIFF_2:48;
k1: ].-1,1.[ c= dom arcsin by T6,RCOMP_1:15;
  now
    let x be Real such that
k2:   x in ].-1,1.[;
    thus g is_differentiable_in x by k2,A1,FDIFF_1:16;
  end;
  hence
A0: arcsin is_differentiable_on ].-1,1.[ by k1,FDIFF_1:def 7;
  set s = sqrt(1-r^2);
  set x = arcsin.r;
  assume
w1: -1 < r & r < 1;
  then
a7: r in ].-1,1.[ by JORDAN6:45;
a8: g.r = x by a7,FUNCT_1:72;
w2: x = arcsin r by darcsin1;
  then -PI/2 < x & x < PI/2 by w1,T16c;
  then
a2: x in ].-PI/2,PI/2.[ by JORDAN6:45;
Z1: diff(f,x) = (f`|].-PI/2,PI/2.[).x by a1,a2,FDIFF_1:def 8
    .= (sin`|].-PI/2,PI/2.[).x by a0,FDIFF_2:16
    .= diff(sin,x) by a0,a2,FDIFF_1:def 8
    .= cos.x by SIN_COS:73
    .= cos arcsin r by w2,SIN_COS:def 23
    .= s by w1,Te1;
  thus diff(arcsin,r) = (arcsin`|].-1,1.[).r by A0,a7,FDIFF_1:def 8
    .= (g`|].-1,1.[).r by A0,FDIFF_2:16
    .= diff(g,r) by A1,a7,FDIFF_1:def 8
    .= 1 / s by Z1,a8,a7,a6,a4,a5,a3,a1,FDIFF_2:48;
end;

theorem
  arcsin is_continuous_on [.-1,1.]
proof
  set f = sin | [.-PI/2,PI/2.];
a1: dom f = [.-PI/2,PI/2.] by Lmd1,RELAT_1:91;
a2: f|[.-PI/2,PI/2.] = f by RELAT_1:102;
  f is_increasing_on [.-PI/2,PI/2.] by COMPTRIG:39,RFUNCT_2:50;
  then (f|[.-PI/2,PI/2.])" is_continuous_on f.:[.-PI/2,PI/2.]
    by a1,Lm4,FCONT_1:54;
  hence thesis by a2,darcsin,COMPTRIG:48,RELAT_1:148;
end;

begin :: arccos

definition
  func arccos -> PartFunc of REAL, REAL equals :darccos:
  (cos | [.0,PI.])";
coherence;
end;

definition
  let r be set;
  func arccos r equals :darccos1:
  arccos.r;
coherence;
end;

definition
  let r be set;
  redefine func arccos r -> Real;
coherence
proof
  arccos r = arccos.r by darccos1;
  hence thesis;
end;
end;

theorem T3:
  arccos" = cos | [.0,PI.] by darccos,FUNCT_1:65;

theorem T2:
  rng arccos = [.0,PI.]
proof
  dom (cos|[.0,PI.]) = [.0,PI.] by Lmd2,RELAT_1:91;
  hence thesis by darccos,FUNCT_1:55;
end;

registration
  cluster arccos -> one-to-one;
coherence by darccos,FUNCT_1:62;
end;

theorem T6:
  dom arccos = [.-1,1.] by darccos,FUNCT_1:55,COMPTRIG:50;

theorem T9:
  (cos | [.0,PI.]) qua Function * arccos = id [.-1,1.]
  by darccos,COMPTRIG:50,FUNCT_1:61;

theorem
  arccos * (cos | [.0,PI.]) = id [.-1,1.]
  by darccos,COMPTRIG:50,FUNCT_1:61;

theorem T10:
  (cos | [.0,PI.]) * arccos = id [.0,PI.] by T2,T3,FUNCT_1:61;

theorem
  arccos qua Function * (cos | [.0,PI.]) = id [.0,PI.]
  by T2,T3,FUNCT_1:61;

theorem T15:
  -1 <= r & r <= 1 implies cos arccos r = r
proof
  assume -1 <= r & r <= 1;
  then
Z0: r in [.-1,1.] by TOPREAL5:1;
  then
Z1: arccos.r in [.0,PI.] by T2,T6,FUNCT_1:def 5;
  thus cos arccos r = cos.(arccos r) by SIN_COS:def 23
    .= cos.(arccos.r) by darccos1
    .= ((cos|[.0,PI.]) qua Function).(arccos.r) by Z1,FUNCT_1:72
    .= (id [.-1,1.]).r by T9,Z0,T6,FUNCT_1:23
    .= r by Z0,FUNCT_1:35;
end;

theorem T20a:
  0 <= r & r <= PI implies arccos cos r = r
proof
  assume 0 <= r & r <= PI;
  then
Z0: r in [.0,PI.] by TOPREAL5:1;
a1: dom (cos|[.0,PI.]) = [.0,PI.] by Lmd2,RELAT_1:91;
  thus arccos cos r = arccos(cos.r) by SIN_COS:def 23
    .= arccos.(cos.r) by darccos1
    .= arccos.((cos|[.0,PI.]).r) by Z0,FUNCT_1:72
    .= (id [.0,PI.]).r by a1,T10,Z0,FUNCT_1:23
    .= r by Z0,FUNCT_1:35;
end;

theorem
  arccos (-1) = PI by T20a,SIN_COS:82;

theorem
  arccos 0 = PI/2 by Lm8,T20a,SIN_COS:82;

theorem
  arccos 1 = 0 by T20a,SIN_COS:34;

theorem
  -1 <= r & r <= 1 & arccos r = 0 implies r = 1 by T15,SIN_COS:34;

theorem
  -1 <= r & r <= 1 & arccos r = PI/2 implies r = 0 by T15,SIN_COS:82;

theorem
  -1 <= r & r <= 1 & arccos r = PI implies r = -1 by T15,SIN_COS:82;

theorem T17:
  -1 <= r & r <= 1 implies 0 <= arccos r & arccos r <= PI
proof 
  assume -1 <= r & r <= 1;
  then r in [.-1,1.] by TOPREAL5:1;
  then arccos.r in rng arccos by T6,FUNCT_1:def 5;
  then arccos r in rng arccos by darccos1;
  hence thesis by T2,TOPREAL5:1;
end;

theorem T17c:
  -1 < r & r < 1 implies 0 < arccos r & arccos r < PI
proof 
  assume
a1: -1 < r & r < 1;
  then 0 <= arccos r & arccos r <= PI by T17;
  then 0 < arccos r & arccos r < PI or
    0 = arccos r or arccos r = PI by REAL_1:def 5;
  hence thesis by a1,T15,SIN_COS:34,82;
end;

theorem Ta1:
  -1 <= r & r <= 1 implies arccos r = PI - arccos(-r)
proof 
  assume -1 <= r & r <= 1;
  then
a1: --1 >= -r & -r >= -1 by REAL_1:50;
a2: r = (-1)*(-r)
     .= cos PI * cos arccos(-r) + sin PI * sin arccos(-r) by a1,T15,SIN_COS:82
     .= cos(PI-arccos(-r)) by COMPLEX2:4;
  0+arccos(-r) <= PI by a1,T17;
  then
a3: 0 <= PI-arccos(-r) by REAL_1:84;
  0 <= arccos(-r) by a1,T17;
  then PI+0 <= PI+arccos(-r) by AXIOMS:24;
  then PI-arccos(-r) <= PI by REAL_1:86;
  hence thesis by a2,a3,T20a;
end;

theorem Tw2:
  0 <= s & r^2 + s^2 = 1 implies sin arccos r = s
proof 
  set x = arccos r;
  assume that
a3: 0 <= s and
Z0: r^2 + s^2 = 1;
a1: -1 <= r & r <= 1 by Z0,Lm5;
  (sin.x)^2 + (cos.x)^2 = 1 by SIN_COS:31;
  then (sin.x)^2 = 1 - (cos.x)^2
    .= 1 - (cos x)^2 by SIN_COS:def 23
    .= 1 - r^2 by a1,T15;
  then
a2: sin.x = s or sin.x = -s by Z0,JGRAPH_3:1;
  0 <= x & x <= PI by a1,T17;
  then
a4: x in [.0,PI.] by TOPREAL5:1;
  ---s < 0 or s = 0 by a3,REAL_1:66;
  hence sin x = s by a2,a4,COMPTRIG:24,SIN_COS:def 21;
end;

theorem
  s <= 0 & r^2 + s^2 = 1 implies sin arccos r = -s
proof 
  set x = arccos r;
  assume that
a3: s <= 0 and
Z0: r^2 + s^2 = 1;
a1: -1 <= r & r <= 1 by Z0,Lm5;
  (sin.x)^2 + (cos.x)^2 = 1 by SIN_COS:31;
  then (sin.x)^2 = 1 - (cos.x)^2
    .= 1 - (cos x)^2 by SIN_COS:def 23
    .= 1 - r^2 by a1,T15;
  then
a2: sin.x = s or sin.x = -s by Z0,JGRAPH_3:1;
  0 <= x & x <= PI by a1,T17;
  then
a4: x in [.0,PI.] by TOPREAL5:1;
  0 > s or s = 0 by a3;
  hence sin x = -s by a2,a4,COMPTRIG:24,SIN_COS:def 21;
end;

theorem Te2:
  -1 <= r & r <= 1 implies sin arccos r = sqrt(1-r^2)
proof 
  set s = sqrt(1-r^2);
  assume -1 <= r & r <= 1;
  then r^2+0 <= 1 by JGRAPH_2:7,SQUARE_1:59;
  then 0 <= 1-r^2 by REAL_1:84;
  then 0 <= s & r^2 + s^2 = r^2 + (1-r^2) by SQUARE_1:def 4;
  hence thesis by Tw2;
end;

theorem
  arccos is_decreasing_on [.-1,1.]
proof 
  set f = cos | [.0,PI.];
a1: f.:[.0,PI.] = [.-1,1.] by T23,RFUNCT_2:5;
  f is_decreasing_on [.0,PI.] by COMPTRIG:41,RFUNCT_2:51;
  then (f|[.0,PI.])" is_decreasing_on f.:[.0,PI.] by FCONT_3:18;
  hence thesis by a1,darccos,RELAT_1:101;
end;

theorem
  arccos is_differentiable_on ].-1,1.[ &
  (-1 < r & r < 1 implies diff(arccos,r) = -1 / sqrt(1-r^2))
proof 
  set f = cos|].0,PI.[;
  set h = cos|[.0,PI.];
  set g = arccos|].-1,1.[;
a3: f|].0,PI.[ = f by RELAT_1:101;
a5: f" = (h|].0,PI.[)" by Lm50,RELAT_1:103
      .= h"|(h.:].0,PI.[) by RFUNCT_2:40
      .= g by Lm50,T0a,darccos,EXTENS_1:1;
a4: dom (f") = ].-1,1.[ by a5,Lm52,T6,RELAT_1:91;
a0: cos is_differentiable_on ].0,PI.[ by FDIFF_1:34,SIN_COS:72;
  then
a1: f is_differentiable_on ].0,PI.[ by FDIFF_2:16;
a6: now
    let x0 be Real such that
a2:   x0 in ].0,PI.[;
w1: diff(f,x0) = (f`|].0,PI.[).x0 by a1,a2,FDIFF_1:def 8
      .= (cos`|].0,PI.[).x0 by a0,FDIFF_2:16
      .= diff(cos,x0) by a0,a2,FDIFF_1:def 8
      .= -sin.x0 by SIN_COS:72;
    --sin.x0 > 0 by a2,COMPTRIG:23;
    hence 0 > diff(f,x0) by w1,REAL_1:66;
  end;
  then
A1: g is_differentiable_on ].-1,1.[ by a4,a5,a3,a1,FDIFF_2:48;
k1: ].-1,1.[ c= dom arccos by T6,RCOMP_1:15;
  now
    let x be Real such that
k2:   x in ].-1,1.[;
    thus g is_differentiable_in x by k2,A1,FDIFF_1:16;
  end;
  hence
A0: arccos is_differentiable_on ].-1,1.[ by k1,FDIFF_1:def 7;
  set s = sqrt(1-r^2);
  set x = arccos.r;
  assume
w1: -1 < r & r < 1;
  then
a7: r in ].-1,1.[ by JORDAN6:45;
a8: g.r = x by a7,FUNCT_1:72;
w2: x = arccos r by darccos1;
  then 0 < x & x < PI by w1,T17c;
  then
a2: x in ].0,PI.[ by JORDAN6:45;
Z1: diff(f,x) = (f`|].0,PI.[).x by a1,a2,FDIFF_1:def 8
    .= (cos`|].0,PI.[).x by a0,FDIFF_2:16
    .= diff(cos,x) by a0,a2,FDIFF_1:def 8
    .= -sin.x by SIN_COS:72
    .= -sin arccos r by w2,SIN_COS:def 21
    .= -s by w1,Te2;
  thus diff(arccos,r) = (arccos`|].-1,1.[).r by A0,a7,FDIFF_1:def 8
    .= (g`|].-1,1.[).r by A0,FDIFF_2:16
    .= diff(g,r) by A1,a7,FDIFF_1:def 8
    .= 1 / -s by Z1,a8,a7,a6,a4,a5,a3,a1,FDIFF_2:48
    .= (-1)/s by XCMPLX_1:193
    .= -1/s by XCMPLX_1:188;
end;

theorem
  arccos is_continuous_on [.-1,1.]
proof 
  set f = cos | [.0,PI.];
a1: dom f = [.0,PI.] by Lmd2,RELAT_1:91;
a2: f|[.0,PI.] = f by RELAT_1:102;
  f is_decreasing_on [.0,PI.] by COMPTRIG:41,RFUNCT_2:51;
  then (f|[.0,PI.])" is_continuous_on f.:[.0,PI.] by a1,FCONT_1:54;
  hence thesis by a2,darccos,COMPTRIG:50,RELAT_1:148;
end;

:: Correspondence between arcsin and arccos

theorem Ta2:
  -1 <= r & r <= 1 implies arcsin r + arccos r = PI/2
proof 
  assume
a1: -1 <= r & r <= 1;
  then -PI/2+PI/2 <= arccos r & arccos r <= PI/2+PI/2 by T17;
  then -PI/2 <= arccos r-PI/2 & arccos r-PI/2 <= PI/2 by REAL_1:84,86;
  then
a2: --PI/2 >= -(arccos r-PI/2) & -(arccos r-PI/2) >= -PI/2 by REAL_1:50;
  r = sin(PI/2)*cos arccos r - cos(PI/2)*sin arccos r by a1,T15,SIN_COS:82
   .= sin(PI/2-arccos r) by COMPLEX2:4;
  then arcsin r = PI/2 - arccos r by a2,T20;
  hence thesis;
end;

theorem
  -1 <= r & r <= 1 implies arccos(-r) - arcsin r = PI/2
proof 
  assume
a0: -1 <= r & r <= 1;
  then arcsin r + arccos r = PI/2 + 0 by Ta2;
  then
a2: arccos r - 0 = PI/2 - arcsin r by XCMPLX_1:33;
  --1 >= -r & -r >= -1 by a0,REAL_1:50;
  then arccos(-r) = PI - arccos(--r) by Ta1
    .= PI - (PI/2 - arcsin r) by a2
    .= arcsin r + PI/2;
  hence thesis;
end;

theorem
  -1 <= r & r <= 1 implies arccos r - arcsin(-r) = PI/2
proof 
  assume
a0: -1 <= r & r <= 1;
  then arcsin r + arccos r = PI/2 + 0 by Ta2;
  then
a2: arcsin r - 0 = PI/2 - arccos r by XCMPLX_1:33;
  --1 >= -r & -r >= -1 by a0,REAL_1:50;
  then arcsin(-r) = -arcsin(--r) by Ta3
    .= -(PI/2 - arccos r) by a2
    .= arccos r - PI/2;
  hence thesis;
end;
  
