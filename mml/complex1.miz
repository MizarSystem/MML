:: The Complex Numbers
::  by Czes{\l}aw Byli\'nski
::
:: Received March 1, 1990
:: Copyright (c) 1990 Association of Mizar Users
 
environ

 vocabularies NUMBERS, SUBSET_1, XREAL_0, ORDINAL1, SQUARE_1, ARYTM_3, CARD_1,
      XXREAL_0, XCMPLX_0, FUNCT_1, FUNCT_2, XBOOLE_0, RELAT_1, REAL_1,
      FUNCOP_1, ARYTM_0, ARYTM_1, COMPLEX1;
 notations TARSKI, SUBSET_1, ORDINAL1, ARYTM_0, NUMBERS, XCMPLX_0, XREAL_0,
      REAL_1, SQUARE_1, RELAT_1, FUNCT_1, FUNCT_2, FUNCT_4, XXREAL_0;
 constructors FUNCT_4, ARYTM_0, REAL_1, SQUARE_1, MEMBERED, RELSET_1;
 registrations XBOOLE_0, RELSET_1, NUMBERS, XCMPLX_0, XXREAL_0, XREAL_0,
      SQUARE_1, MEMBERED;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions SQUARE_1;
 theorems SQUARE_1, XREAL_0, FUNCT_2, XBOOLE_0, TARSKI, NUMBERS, FUNCT_4,
      XCMPLX_0, XCMPLX_1, ARYTM_0, XREAL_1, XXREAL_0, CARD_1;

begin

reserve a,b,c,d for real number;
 
:: Auxiliary theorems

canceled;

theorem Th2:
  a^2 + b^2 = 0 implies a = 0
proof
  0 <= a^2 & 0 <= b^2 by XREAL_1:65;
  hence thesis;
end;

Lm1: 0 <= a^2 + b^2
proof
  0<=a^2 & 0<=b^2 by XREAL_1:65;
  hence thesis;
end;
 
:: Complex Numbers

definition
  let z be complex number;
  canceled;
  func Re z means
  :Def2:
  it = z if z in REAL
  otherwise ex f being Function of 2,REAL st z = f & it = f.0;
  existence
  proof
    thus z in REAL implies ex IT being set st IT = z;
A1: z in COMPLEX by XCMPLX_0:def 2;
    assume not z in REAL;
    then
    z in Funcs(2,REAL) \ { x where x is Element of Funcs(2,REAL): x.1 = 0
    } by A1,CARD_1:88,NUMBERS:def 2,XBOOLE_0:def 3;
    then reconsider f = z as Function of 2, REAL by FUNCT_2:121;
    take f.0, f;
    thus thesis;
  end;
  uniqueness;
  consistency;
  func Im z means
  :Def3:
  it = 0 if z in REAL otherwise ex f being Function of
  2,REAL st z = f & it = f.1;
  existence
  proof
    thus z in REAL implies ex IT being set st IT = 0;
A2: z in COMPLEX by XCMPLX_0:def 2;
    assume not z in REAL;
    then
    z in Funcs(2,REAL) \ { x where x is Element of Funcs(2,REAL): x.1 = 0
    } by A2,CARD_1:88,NUMBERS:def 2,XBOOLE_0:def 3;
    then reconsider f = z as Function of 2, REAL by FUNCT_2:121;
    take f.1, f;
    thus thesis;
  end;
  uniqueness;
  consistency;
end;

registration
  let z be complex number;
  cluster Re z -> real;
  coherence
  proof
    per cases;
    suppose
      z in REAL;
      hence thesis by Def2;
    end;
    suppose
      not z in REAL;
      then consider f being Function of 2,REAL such that
      z = f and
A1:   Re z = f.0 by Def2;
      0 in 2 by CARD_1:88,TARSKI:def 2;
      then f.0 in REAL by FUNCT_2:7;
      hence thesis by A1;
    end;
  end;
  cluster Im z -> real;
  coherence
  proof
    per cases;
    suppose
      z in REAL;
      hence thesis by Def3;
    end;
    suppose
      not z in REAL;
      then consider f being Function of 2,REAL such that
      z = f and
A2:   Im z = f.1 by Def3;
      1 in 2 by CARD_1:88,TARSKI:def 2;
      then f.1 in REAL by FUNCT_2:7;
      hence thesis by A2;
    end;
  end;
end;

definition
  let z be complex number;
  redefine func Re z -> Real;
  coherence by XREAL_0:def 1;
  redefine func Im z -> Real;
  coherence by XREAL_0:def 1;
end;

canceled 2;

theorem Th5:
  for f being Function of 2,REAL
  ex a,b being Element of REAL st f = (0,1)-->(a,b)
proof
  let f be Function of 2,REAL;
  0 in 2 & 1 in 2 by CARD_1:88,TARSKI:def 2;
  then reconsider a = f.0, b = f.1 as Element of REAL by FUNCT_2:7;
  take a,b;
  dom f = {0,1} by CARD_1:88,FUNCT_2:def 1;
  hence thesis by FUNCT_4:69;
end;

Lm2:
for a, b being Element of REAL holds Re [*a,b*] = a & Im [*a,b*] = b
proof
  let a, b be Element of REAL;
  reconsider a9 =a, b9 = b as Element of REAL;
  thus Re [*a,b*] = a
  proof
    per cases;
    suppose
      b = 0;
      then [*a,b*] = a by ARYTM_0:def 7;
      hence thesis by Def2;
    end;
    suppose
      b <> 0;
      then
A1:   [*a,b*] = (0,1)-->(a9,b9) by ARYTM_0:def 7;
      then reconsider f = [*a,b*] as Function of 2, REAL by CARD_1:88;
      ( not [*a,b*] in REAL)& f.0 = a by A1,ARYTM_0:10,FUNCT_4:66;
      hence thesis by Def2;
    end;
  end;
  per cases;
  suppose
A2: b = 0;
    then [*a,b*] = a by ARYTM_0:def 7;
    hence thesis by A2,Def3;
  end;
  suppose
    b <> 0;
    then
A3: [*a,b*] = (0,1)-->(a9,b9) by ARYTM_0:def 7;
    then reconsider f = [*a,b*] as Function of 2, REAL by CARD_1:88;
    ( not [*a,b*] in REAL)& f.1 = b by A3,ARYTM_0:10,FUNCT_4:66;
    hence thesis by Def3;
  end;
end;

reserve z,z1,z2 for complex number;

Lm3: [*Re z, Im z*] = z
proof
A1: z in COMPLEX by XCMPLX_0:def 2;
  per cases;
  suppose
A2: z in REAL;
    then Im z = 0 by Def3;
    hence [*Re z, Im z*] = Re z by ARYTM_0:def 7
      .= z by A2,Def2;
  end;
  suppose
A3: not z in REAL;
    then
A4: ex f being Function of 2,REAL st z = f & Im z = f.1 by Def3;
    then consider a,b being Element of REAL such that
A5: z = (0,1)-->(a,b) by Th5;
    reconsider f = z as Element of Funcs(2,REAL) by A5,CARD_1:88,FUNCT_2:11;
    z in Funcs(2,REAL) \ { x where x is Element of Funcs(2,REAL): x.1 = 0
    } by A1,A3,CARD_1:88,NUMBERS:def 2,XBOOLE_0:def 3;
    then not z in { x where x is Element of Funcs(2,REAL): x.1 = 0} by
    XBOOLE_0:def 5;
    then f.1 <> 0;
    then
A6: b <> 0 by A5,FUNCT_4:66;
    ex f being Function of 2,REAL st z = f & Re z = f.0 by A3,Def2;
    then
A7: Re z = a by A5,FUNCT_4:66;
    Im z = b by A4,A5,FUNCT_4:66;
    hence thesis by A5,A7,A6,ARYTM_0:def 7;
  end;
end;

canceled 3;

theorem Th9:
  Re z1 = Re z2 & Im z1 = Im z2 implies z1 = z2
proof
  assume Re z1 = Re z2 & Im z1 = Im z2;
  hence z1 = [*Re z2,Im z2*] by Lm3
    .= z2 by Lm3;
end;

definition
  let z1,z2 be complex number;
  canceled;
  redefine pred z1 = z2 means
  Re z1 = Re z2 & Im z1 = Im z2;
  compatibility by Th9;
end;

notation
  synonym 0c for 0;
end;

definition
  redefine func 0c -> Element of COMPLEX;
  correctness by XCMPLX_0:def 2;
end;

definition
  canceled;
  func 1r -> Element of COMPLEX equals
  1;
  correctness by XCMPLX_0:def 2;
  redefine func <i> -> Element of COMPLEX;
  coherence by XCMPLX_0:def 2;
end;

Lm4: 0 = [*0,0*] by ARYTM_0:def 7;

Lm5: 1r = [*1,0*] by ARYTM_0:def 7;

canceled 2;

theorem Th12:
  Re 0 = 0 & Im 0 = 0 by Lm2,Lm4;

theorem Th13:
  z = 0 iff (Re z)^2 + (Im z)^2 = 0
proof
  set r = Re z, i = Im z;
  thus z=0 implies r^2 + i^2 = 0 by Th12;
  assume 0 = r^2+i^2;
  then r = 0 & i = 0 by Th2;
  hence thesis by Th9,Th12;
end;

canceled;

theorem Th15:
  Re(1r) = 1 & Im(1r) = 0 by Lm2,Lm5;

canceled;

Lm6: <i> = [*0,1*] by ARYTM_0:def 7,XCMPLX_0:def 1;

theorem Th17:
  Re(<i>) = 0 & Im(<i>) = 1 by Lm2,Lm6;

Lm7: for x being real number, x1,x2 being Element of REAL st x = [*x1,x2*]
holds x2 = 0 & x = x1
proof
  let x be real number, x1,x2 being Element of REAL;
  assume
A1: x = [*x1,x2*];
A2: x in REAL by XREAL_0:def 1;
  hereby
    assume x2 <> 0;
    then x = (0,1) --> (x1,x2) by A1,ARYTM_0:def 7;
    hence contradiction by A2,ARYTM_0:10;
  end;
  hence thesis by A1,ARYTM_0:def 7;
end;

Lm8: for x9,y9 being Element of REAL, x,y being real number st x9 = x & y9 = y
holds +(x9,y9) = x + y
proof
  let x9,y9 be Element of REAL, x,y be real number such that
A1: x9 = x & y9 = y;
  consider x1,x2,y1,y2 being Element of REAL such that
A2: x = [* x1,x2 *] & y = [*y1,y2*] and
A3: x+y = [*+(x1,y1),+(x2,y2)*] by XCMPLX_0:def 4;
  x2 = 0 & y2 = 0 by A2,Lm7;
  then
A4: +(x2,y2) = 0 by ARYTM_0:13;
  x = x1 & y = y1 by A2,Lm7;
  hence thesis by A1,A3,A4,ARYTM_0:def 7;
end;

Lm9: for x being Element of REAL holds opp x = -x
proof
  let x be Element of REAL;
  +(x,opp x) = 0 by ARYTM_0:def 4;
  then x + opp x = 0 by Lm8;
  hence thesis;
end;

Lm10: for x9,y9 being Element of REAL, x,y being real number st x9 = x & y9 =
y holds *(x9,y9) = x * y
proof
  let x9,y9 be Element of REAL, x,y be real number such that
A1: x9 = x & y9 = y;
  consider x1,x2,y1,y2 being Element of REAL such that
A2: x = [* x1,x2 *] and
A3: y = [*y1,y2*] and
A4: x*y = [* +(*(x1,y1),opp*(x2,y2)), +(*(x1,y2),*(x2,y1)) *] by
  XCMPLX_0:def 5;
  x2 = 0 by A2,Lm7;
  then
A5: *(x2,y1) = 0 by ARYTM_0:14;
A6: y2 = 0 by A3,Lm7;
  then *(x1,y2) = 0 by ARYTM_0:14;
  then
A7: +(*(x1,y2),*(x2,y1)) = 0 by A5,ARYTM_0:13;
  x = x1 & y = y1 by A2,A3,Lm7;
  hence *(x9,y9) = +(*(x1,y1),*(opp x2,y2)) by A1,A6,ARYTM_0:13,14
    .= +(*(x1,y1),opp*(x2,y2)) by ARYTM_0:17
    .= x * y by A4,A7,ARYTM_0:def 7;
end;

Lm11: for x,y,z being complex number st z = x + y holds Re z = Re x + Re y
proof
  let x,y,z be complex number such that
A1: z = x + y;
  consider x1,x2,y1,y2 being Element of REAL such that
A2: x = [*x1,x2*] & y = [*y1,y2*] and
A3: x + y = [*+(x1,y1),+(x2,y2)*] by XCMPLX_0:def 4;
A4: Re x = x1 & Re y = y1 by A2,Lm2;
  thus Re z = +(x1,y1) by A1,A3,Lm2
    .= Re x + Re y by A4,Lm8;
end;

Lm12: for x,y,z being complex number st z = x+y holds Im z = Im x + Im y
proof
  let x,y,z be complex number such that
A1: z = x+y;
  consider x1,x2,y1,y2 being Element of REAL such that
A2: x = [*x1,x2*] & y = [*y1,y2*] and
A3: x + y = [*+(x1,y1),+(x2,y2)*] by XCMPLX_0:def 4;
A4: Im x = x2 & Im y = y2 by A2,Lm2;
  thus Im z = +(x2,y2) by A1,A3,Lm2
    .= Im x + Im y by A4,Lm8;
end;

Lm13: for x,y,z being complex number st z = x * y holds Re z = Re x * Re y -
Im x * Im y
proof
  let x,y,z be complex number such that
A1: z = x * y;
  consider x1,x2,y1,y2 being Element of REAL such that
A2: x = [*x1,x2*] & y = [*y1,y2*] and
A3: x * y = [* +(*(x1,y1),opp*(x2,y2)), +(*(x1,y2),*(x2,y1)) *] by
  XCMPLX_0:def 5;
A4: Re x = x1 & Re y = y1 by A2,Lm2;
A5: Im x = x2 & Im y = y2 by A2,Lm2;
  thus Re z = +(*(x1,y1),opp*(x2,y2)) by A1,A3,Lm2
    .= *(x1,y1) + opp*(x2,y2) by Lm8
    .= x1*y1 + opp*(x2,y2) by Lm10
    .= x1*y1 + -*(x2,y2) by Lm9
    .= x1*y1 - *(x2,y2)
    .= Re x * Re y - Im x * Im y by A4,A5,Lm10;
end;

Lm14: for x,y,z being complex number st z = x*y holds Im z = Re x * Im y + Im
x * Re y
proof
  let x,y,z be complex number such that
A1: z = x*y;
  consider x1,x2,y1,y2 being Element of REAL such that
A2: x = [*x1,x2*] & y = [*y1,y2*] and
A3: x * y = [* +(*(x1,y1),opp*(x2,y2)), +(*(x1,y2),*(x2,y1)) *] by
  XCMPLX_0:def 5;
A4: Im x = x2 & Im y = y2 by A2,Lm2;
A5: Re x = x1 & Re y = y1 by A2,Lm2;
  thus Im z = +(*(x1,y2),*(x2,y1)) by A1,A3,Lm2
    .= *(x1,y2) + *(x2,y1) by Lm8
    .= x1*y2 + *(x2,y1) by Lm10
    .= Re x * Im y + Im x * Re y by A4,A5,Lm10;
end;

Lm15: z1+z2 = [* Re z1 + Re z2, Im z1 + Im z2 *]
proof
  set z = [* Re z1 + Re z2, Im z1 + Im z2 *];
  reconsider z9 = z1 + z2 as Element of COMPLEX by XCMPLX_0:def 2;
A1: Im z = Im z1 + Im z2 by Lm2
    .= Im z9 by Lm12;
  Re z = Re z1 + Re z2 by Lm2
    .= Re z9 by Lm11;
  hence thesis by A1,Th9;
end;

Lm16: z1 * z2 = [* Re z1 * Re z2 - Im z1 * Im z2, Re z1 * Im z2 + Re z2 * Im
z1 *]
proof
  set z = [* Re z1 * Re z2 - Im z1 * Im z2, Re z1 * Im z2 + Re z2 * Im z1 *];
  reconsider z9 = z1 * z2 as Element of COMPLEX by XCMPLX_0:def 2;
A1: Im z = Re z1 * Im z2 + Re z2 * Im z1 by Lm2
    .= Im z9 by Lm14;
  Re z = Re z1 * Re z2 - Im z1 * Im z2 by Lm2
    .= Re z9 by Lm13;
  hence thesis by A1,Th9;
end;

Lm17: Re(z1 * z2) = Re z1 * Re z2 - Im
z1 * Im z2 & Im(z1 * z2) = Re z1 * Im z2 + Re z2 * Im z1
proof
  z1 * z2 = [*Re z1 * Re z2 - Im z1 * Im z2, Re z1 * Im z2 + Re z2 * Im z1
  *] by Lm16;
  hence thesis by Lm2;
end;

Lm18: Re(z1 + z2) = Re z1 + Re z2 & Im(z1 + z2) = Im z1 + Im z2
proof
  (z1 + z2) = [* Re z1 + Re z2, Im z1 + Im z2 *] by Lm15;
  hence thesis by Lm2;
end;

Lm19: for x being Real holds Re (x*<i>) = 0
proof
  let x be Real;
  thus Re (x*<i>) = Re x * Re (<i>) - Im x * Im (<i>) by Lm17
    .= Re x * 0 - 0 * Im (<i>) by Def3,Th17
    .= 0;
end;

Lm20: for x being Real holds Im (x*<i>) = x
proof
  let x be Real;
  thus Im (x*<i>) = Re x * Im (<i>) + Im x * Re (<i>) by Lm17
    .= x by Def2,Th17;
end;

Lm21: for x, y being Real holds [*x,y*] = x + y * <i>
proof
  let x, y be Real;
A1: Im (x + y*<i>) = Im x + Im (y*<i>) by Lm18
    .= 0 + Im (y*<i>) by Def3
    .= y by Lm20;
  Re (x + y*<i>) = Re x + Re (y*<i>) by Lm18
    .= Re x + 0 by Lm19
    .= x by Def2;
  hence thesis by A1,Lm3;
end;

definition
  canceled;
  let z1,z2 be Element of COMPLEX;
  redefine func z1 + z2 -> Element of COMPLEX equals
  Re z1 + Re z2 + (Im z1 + Im z2)*<i>;
  coherence by XCMPLX_0:def 2;
  compatibility
  proof
    z1 + z2 = [* Re z1 + Re z2, Im z1 + Im z2 *] by Lm15;
    hence thesis by Lm21;
  end;
end;

canceled;

theorem Th19:
  Re(z1 + z2) = Re z1 + Re z2 & Im(z1 + z2) = Im z1 + Im z2
proof
  (z1 + z2) = [* Re z1 + Re z2, Im z1 + Im z2 *] by Lm15;
  hence thesis by Lm2;
end;

definition
  let z1,z2 be Element of COMPLEX;
  redefine func z1 * z2 -> Element of COMPLEX equals
  Re z1 * Re z2 - Im z1 *
  Im z2+(Re z1 * Im z2 + Re z2 * Im z1)*<i>;
  coherence by XCMPLX_0:def 2;
  compatibility
  proof
    z1 * z2 = [* Re z1 * Re z2 - Im z1 * Im z2, Re z1 * Im z2 + Re z2 * Im
    z1 *] by Lm16;
    hence thesis by Lm21;
  end;
end;

canceled 4;

theorem Th24:
  Re(z1 * z2) = Re z1 * Re z2 - Im z1 * Im z2 &
  Im(z1 * z2) = Re z1 * Im z2 + Re z2 * Im z1
proof
  z1 * z2 = [*Re z1 * Re z2 - Im z1 * Im z2, Re z1 * Im z2 + Re z2 * Im z1
  *] by Lm16;
  hence thesis by Lm2;
end;

theorem
  Re (a*<i>) = 0
proof
a1: a in REAL by XREAL_0:def 1;
  thus Re (a*<i>) = Re a * Re (<i>) - Im a * Im (<i>) by Th24
  .= Re a * 0 - 0 * Im (<i>) by Def3,Th17,a1
  .= 0;
end;

theorem
  Im (a*<i>) = a
proof
a1: a in REAL by XREAL_0:def 1;
  thus Im (a*<i>) = Re a * Im (<i>) + Im a * Re (<i>) by Th24
  .= a by Def2,Th17,a1;
end;

canceled;

theorem Th28:
  Re(a+b*<i>) = a & Im(a+b*<i>) = b
proof
  reconsider a,b as Real by XREAL_0:def 1;
  (a+b*<i>) = [*a,b*] by Lm21;
  hence thesis by Lm2;
end;

theorem Th29:
  Re z+(Im z)*<i> = z
proof
  [*Re z, Im z*] = z by Lm3;
  hence thesis by Lm21;
end;

theorem Th30:
  Im z1 = 0 & Im z2 = 0 implies Re(z1*z2) = Re z1 * Re z2 & Im(z1*z2) = 0
proof
  assume that
A1: Im z1 = 0 and
A2: Im z2 = 0;
  thus Re(z1*z2) = Re z1 * Re z2 - Im z1 * Im z2 by Th24
    .= Re z1 * Re z2 by A1;
  thus Im(z1*z2) = Re z1 * Im z2 + Re z2 * Im z1 by Th24
    .= 0 by A1,A2;
end;

theorem Th31:
  Re z1 = 0 & Re z2 = 0 implies Re(z1*z2) = - Im z1 * Im z2 & Im(z1*z2) = 0
proof
  assume that
A1: Re z1 = 0 and
A2: Re z2 = 0;
  thus Re(z1*z2) = Re z1 * Re z2 - Im z1 * Im z2 by Th24
    .= - Im z1 * Im z2 by A1;
  thus Im(z1*z2) = Re z1 * Im z2 + Re z2 * Im z1 by Th24
    .= 0 by A1,A2;
end;

theorem
  Re(z*z) = (Re z)^2 - (Im z)^2 & Im(z*z) = 2*(Re z *Im z)
proof
  thus Re(z*z) = (Re z)^2 - (Im z)^2 by Th24;
  thus Im(z*z) = Re z *Im z + Re z *Im z by Th24
    .= 2*(Re z *Im z);
end;

definition
  let z be Element of COMPLEX;
  redefine func -z -> Element of COMPLEX equals
  :Def11:
  -Re z+(-Im z)*<i>;
  coherence by XCMPLX_0:def 2;
  compatibility
  proof
    set z9 = [* -Re z, -Im z *];
    z9 + z = [* Re z9 + Re z, Im z9 + Im z *] by Lm15
      .= [* -Re z + Re z, Im z9 + Im z *] by Lm2
      .= [* 0, -Im z + Im z *] by Lm2
      .= 0 by ARYTM_0:def 7;
    hence thesis by Lm21;
  end;
end;

canceled;

theorem Th34:
  Re(-z) = -(Re z) & Im(-z) = -(Im z)
proof
  z in COMPLEX by XCMPLX_0:def 2;
  then -z = -Re z+(-Im z)*<i> by Def11;
  hence thesis by Th28;
end;

canceled 2;

theorem
  <i>*<i> = -1r;

definition
  let z1,z2 be Element of COMPLEX;
  redefine func z1 - z2 -> Element of COMPLEX equals
  :Def12:
  Re z1 - Re z2 + (Im z1 - Im z2)*<i>;
  coherence by XCMPLX_0:def 2;
  compatibility
  proof
A1: z1 = Re z1 + (Im z1)*<i> by Th29;
    z1 - z2 = z1 + -z2 .= z1 + (-Re z2+(-Im z2)*<i>) by Def11
      .= Re z1 - Re z2 + (Im z1 - Im z2)*<i> by A1;
    hence thesis;
  end;
end;

canceled 10;

theorem Th48:
  Re(z1 - z2) = Re z1 - Re z2 & Im(z1 - z2) = Im z1 - Im z2
proof
a1: z1 in COMPLEX & z2 in COMPLEX by XCMPLX_0:def 2;
    hence Re(z1 - z2) = Re(Re z1 - Re z2 + (Im z1 - Im z2)*<i>) by Def12
    .= Re z1 - Re z2 by Th28;
  thus Im(z1 - z2) = Im(Re z1 - Re z2 + (Im z1 - Im z2)*<i>) by a1,Def12
    .= Im z1 - Im z2 by Th28;
end;

definition
  let z be Element of COMPLEX;
  redefine func z" -> Element of COMPLEX equals
  :Def13:
  Re z / ((Re z)^2+(Im z
  )^2)+((- Im z) / ((Re z)^2+(Im z)^2))*<i>;
  coherence by XCMPLX_0:def 2;
  compatibility
  proof
    reconsider z9 =Re z / ((Re z)^2+(Im z)^2)+((- Im z) / ((Re z)^2+(Im z)^2))
    *<i> as Element of COMPLEX by XCMPLX_0:def 2;
    per cases;
    suppose
      z = 0;
      hence thesis by Th12;
    end;
    suppose
A1:   z <> 0;
      then
A2:   (Re z)^2+(Im z)^2 <> 0 by Th13;
A3:   Im z9 = (-Im z) / ((Re z)^2+(Im z)^2) by Th28;
      then
A4:   Re z * Im z9 + Re z9 * Im z = (Re z)/1 * ((-Im z) / ((Re z)^2+(Im z)
      ^2)) + Re z / ((Re z)^2+(Im z)^2) * Im z by Th28
        .= Re z * (-Im z) / (1*((Re z)^2+(Im z)^2)) + Re z / ((Re z)^2+(Im z
      )^2) * (Im z)/1 by XCMPLX_1:77
        .= Re z * (-Im z) / (1*((Re z)^2+(Im z)^2)) + (Im z)/1 * Re z / ((Re
      z)^2+(Im z)^2) by XCMPLX_1:77
        .= (- Re z * Im z + Re z * Im z) / ((Re z)^2+(Im z)^2) by XCMPLX_1:
      63
        .= 0;
A5:   Re z * Re z9 - Im z * Im z9 = (Re z)/1 * (Re z / ((Re z)^2+(Im z)^2)
      ) - Im z *((-Im z)/((Re z)^2+(Im z)^2)) by A3,Th28
        .= Re z * Re z / (1*((Re z)^2+(Im z)^2)) - (Im z)/1 *((-Im z)/((Re z
      )^2+(Im z)^2)) by XCMPLX_1:77
        .= (Re z)^2 / ((Re z)^2+(Im z)^2) - Im z *(-Im z)/(1*((Re z)^2+(Im z
      )^2)) by XCMPLX_1:77
        .= (Re z)^2 / ((Re z)^2+(Im z)^2) - (-((Im z)^2))/(1*((Re z)^2+(Im z
      )^2))
        .= (Re z)^2 / ((Re z)^2+(Im z)^2) - -((Im z)^2)/((Re z)^2+(Im z)^2)
      by XCMPLX_1:188
        .= (Re z)^2 / ((Re z)^2+(Im z)^2) + (Im z)^2/(1*((Re z)^2+(Im z)^2))
        .= ((Re z)^2 + (Im z)^2)/((Re z)^2+(Im z)^2) by XCMPLX_1:63
        .= 1 by A2,XCMPLX_1:60;
      z * z9= [*Re z * Re z9 - Im z * Im z9,Re z * Im z9 + Re z9 * Im z*]
      by Lm16
        .= 1 by A5,A4,ARYTM_0:def 7;
      hence thesis by A1,XCMPLX_0:def 7;
    end;
  end;
end;

canceled 15;

theorem Th64:
  Re(z") = Re z / ((Re z)^2+(Im z)^2) & Im(z") = (- Im z) / ((Re z)^2+(Im z)^2)
proof
  z in COMPLEX by XCMPLX_0:def 2;
  then
  z" = Re z / ((Re z)^2+(Im z)^2)+((- Im z) / ((Re z)^2+(Im z)^2))*<i>
  by Def13;
  hence thesis by Th28;
end;

canceled 7;

theorem
  <i>" = -<i>;

canceled 6;

theorem Th79:
  Re z <> 0 & Im z = 0 implies Re(z") = (Re z)" & Im(z") = 0
proof
  assume that
A1: Re z <> 0 and
A2: Im z = 0;
  Re(z") = Re z / ((Re z)^2+(Im z)^2) by Th64;
  hence Re(z") = (1*Re z) / (Re z * Re z) by A2
    .= 1/Re z by A1,XCMPLX_1:92
    .= (Re z)" by XCMPLX_1:217;
  Im(z") = (- Im z) / ((Re z)^2+(Im z)^2) by Th64;
  hence thesis by A2;
end;

theorem Th80:
  Re z = 0 & Im z <> 0 implies Re(z") = 0 & Im(z") = -(Im z)"
proof
  assume that
A1: Re z = 0 and
A2: Im z <> 0;
  Re(z") = Re z / ((Re z)^2+(Im z)^2) by Th64;
  hence Re(z") = 0 by A1;
  Im(z") = (- Im z) / ((Re z)^2+(Im z)^2) by Th64;
  hence Im(z") = -(1*Im z / (Im z * Im z)) by A1,XCMPLX_1:188
    .= -(1 / Im z) by A2,XCMPLX_1:92
    .= - (Im z)" by XCMPLX_1:217;
end;

definition
  let z1,z2 be complex number;
  redefine func z1 / z2 -> Element of COMPLEX equals
  :Def14:
  (Re z1 * Re z2 + Im z1 * Im z2) / ((Re z2)^2 + (Im z2)^2) +
  ((Re z2 * Im z1 - Re z1 * Im z2) / ((Re z2)^2 + (Im z2)^2))*<i>;
  coherence by XCMPLX_0:def 2;
  compatibility
  proof
    reconsider z1,z2 as Element of COMPLEX by XCMPLX_0:def 2;
    set k = (Re z2)^2 + (Im z2)^2;
A1: Re[*Re z2/k,(-Im z2)/k*] = Re(Re z2/k+(-Im z2)/k*<i>) by Lm21
      .= Re z2/k by Th28;
A2: Im[*Re z2/k,(-Im z2)/k*] = Im(Re z2/k+(-Im z2)/k*<i>) by Lm21
      .= (-Im z2)/k by Th28;
    z1 / z2 = z1 * z2" by XCMPLX_0:def 9
      .= z1*[* Re z2/k, (-Im z2)/k *] by Lm21
      .= [* (Re z1)/1 * (Re z2/k) - Im z1 * ((-Im z2)/k), Re z1 * ((-Im z2)/
    k) + (Re z2/k) * Im z1 *] by A1,A2,Lm16
      .= (Re z1)/1 * (Re z2/k) - Im z1 * ((-Im z2)/k)+ (Re z1 * ((-Im z2)/k)
    + (Re z2/k) * Im z1)*<i> by Lm21
      .= Re z1 * Re z2/(1*k) - (Im z1)/1 * ((-Im z2)/k)+ (Re z1 * ((-Im z2)/
    k) + (Re z2/k) * Im z1)*<i> by XCMPLX_1:77
      .= Re z1 * Re z2/k - Im z1 * (-Im z2)/(1*k)+ ((Re z1)/1 * ((-Im z2)/k)
    + (Re z2/k) * Im z1)*<i> by XCMPLX_1:77
      .= Re z1 * Re z2/k - Im z1 * (-Im z2)/k+ (Re z1 * (-Im z2)/(1*k) + (Re
    z2/k) * ((Im z1)/1))*<i> by XCMPLX_1:77
      .= Re z1 * Re z2/k - Im z1 * (-Im z2)/k+ (Re z1 * (-Im z2)/k + Im z1 *
    Re z2/(1*k))*<i> by XCMPLX_1:77
      .= (Re z1 * Re z2 - Im z1 * (-Im z2))/k+ (Re z1 * (-Im z2)/k + Im z1 *
    Re z2/(1*k))*<i> by XCMPLX_1:121
      .= (Re z1 * Re z2 + Im z1 * Im z2)/k+ ((-Re z1 * Im z2 + Im z1 * Re z2
    )/k)*<i> by XCMPLX_1:63
      .= (Re z1 * Re z2 + Im z1 * Im z2) / ((Re z2)^2 + (Im z2)^2) + ((Re z2
    * Im z1 - Re z1 * Im z2) / ((Re z2)^2 + (Im z2)^2))*<i>;
    hence thesis;
  end;
end;

canceled;

theorem
  Re(z1 / z2) = (Re z1 * Re z2 + Im z1 * Im z2) / ((Re z2)^2 + (Im z2)^2) &
  Im(z1 / z2) = (Re z2 * Im z1 - Re z1 * Im z2) / ((Re z2)^2 + (Im z2)^2)
proof
  thus Re(z1 / z2) = Re((Re z1 * Re z2 + Im z1 * Im z2) / ((Re z2)^2 + (Im z2)
^2) + ((Re z2 * Im z1 - Re z1 * Im z2) / ((Re z2)^2 + (Im z2)^2))*<i>) by Def14
    .= (Re z1 * Re z2 + Im z1 * Im z2) / ((Re z2)^2 + (Im z2)^2) by Th28;
  thus Im(z1 / z2) = Im((Re z1 * Re z2 + Im z1 * Im z2) / ((Re z2)^2 + (Im z2)
^2) + ((Re z2 * Im z1 - Re z1 * Im z2) / ((Re z2)^2 + (Im z2)^2))*<i>) by Def14
    .= (Re z2 * Im z1 - Re z1 * Im z2) / ((Re z2)^2 + (Im z2)^2) by Th28;
end;

canceled 26;

theorem
  Im z1 = 0 & Im z2 = 0 & Re z2 <> 0 implies Re(z1/z2) = (Re z1)/(Re z2)
  & Im(z1/z2) = 0
proof
  assume that
A1: Im z1 = 0 and
A2: Im z2 = 0 & Re z2 <> 0;
A3: z1/z2 = z1*z2" & Im(z2") = 0 by A2,Th79,XCMPLX_0:def 9;
  hence Re(z1/z2) = (Re z1)*Re(z2") by A1,Th30
    .= (Re z1)*(Re z2)" by A2,Th79
    .= (Re z1)/(Re z2) by XCMPLX_0:def 9;
  thus thesis by A1,A3,Th30;
end;

theorem
  Re z1 = 0 & Re z2 = 0 & Im z2 <> 0 implies Re(z1/z2) = (Im z1)/(Im z2)
  & Im(z1/z2) = 0
proof
  assume that
A1: Re z1 = 0 and
A2: Re z2 = 0 & Im z2 <> 0;
A3: z1/z2 = z1*z2" & Re(z2") = 0 by A2,Th80,XCMPLX_0:def 9;
  hence Re(z1/z2) = -(Im z1)*Im(z2") by A1,Th31
    .= -(Im z1)*-(Im z2)" by A2,Th80
    .= --(Im z1)*(Im z2)"
    .= (Im z1)/(Im z2) by XCMPLX_0:def 9;
  thus thesis by A1,A3,Th31;
end;

definition
  let z be complex number;
  func z*' -> complex number equals
  Re z-(Im z)*<i>;
  correctness;
  involutiveness
  proof
    let z,z9 be complex number;
    assume z = Re z9-(Im z9)*<i>;
    then z = Re z9+(-Im z9)*<i>;
    then Re z = Re z9 & -Im z = --Im z9 by Th28;
    hence z9= Re z+(-Im z)*<i> by Th29
      .= Re z-(Im z)*<i>;
  end;
end;

definition
  let z be complex number;
  redefine func z*' -> Element of COMPLEX;
  coherence by XCMPLX_0:def 2;
end;

canceled;

theorem Th112:
  Re (z*') = Re z & Im (z*') = -Im z
proof
  z*' = Re z+(-Im z)*<i>;
  hence thesis by Th28;
end;

theorem
  0*' = 0 by Th12;

theorem
  z*' = 0 implies z = 0
proof
  assume z*' = 0;
  then 0 = Re z+(-Im z)*<i>;
  hence Re z = Re 0 & Im z = Im 0 by Th12,Th28;
end;

theorem
  1r*' = 1r by Th15;

theorem
  <i>*' = -<i> by Th17;

canceled;

theorem Th118:
  (z1 + z2)*' = z1*' + z2*'
proof
  thus Re ((z1 + z2)*') = Re(z1 + z2) by Th112
    .= Re z1 + Re z2 by Th19
    .= Re (z1*') + Re z2 by Th112
    .= Re (z1*') + Re (z2*') by Th112
    .= Re (z1*'+ z2*') by Th19;
  thus Im ((z1 + z2)*') = -Im(z1 + z2) by Th112
    .= -(Im z1 + --Im z2) by Th19
    .= -Im z1 + -Im z2
    .= Im (z1*') + -Im z2 by Th112
    .= Im (z1*') + Im (z2*') by Th112
    .= Im (z1*' + z2*') by Th19;
end;

theorem Th119:
  (-z)*' = -(z*')
proof
  thus Re ((-z)*') = Re -z by Th112
    .= - Re z by Th34
    .= - Re (z*') by Th112
    .= Re -(z*') by Th34;
  thus Im ((-z)*') = -Im -z by Th112
    .= - -Im z by Th34
    .= - Im (z*') by Th112
    .= Im -(z*') by Th34;
end;

theorem
  (z1 - z2)*' = z1*' - z2*'
proof
  thus (z1 - z2)*' = (z1 + -z2)*' .= z1*' + (-z2)*' by Th118
    .= z1*' + -(z2*') by Th119
    .= z1*' - z2*';
end;

theorem Th121:
  (z1*z2)*' = z1*'*z2*'
proof
A1: Re(z1*') = Re z1 & Re(z2*') = Re z2 by Th112;
A2: Im(z1*') = -Im z1 & Im(z2*') = -Im z2 by Th112;
  thus Re((z1*z2)*') = Re(z1*z2) by Th112
    .= (Re (z1*') * Re (z2*')) - (-Im (z1*')) * -Im (z2*') by A1,A2,Th24
    .= (Re (z1*') * Re (z2*')) - --(Im (z1*') * Im (z2*'))
    .= Re(z1*'*z2*') by Th24;
  thus Im((z1*z2)*') = -Im(z1*z2) by Th112
    .= -((Re (z1*') * -Im (z2*')) + (Re (z2*') * -Im (z1*'))) by A1,A2,Th24
    .= (Re (z2*') * Im (z1*'))+--(Re (z1*') * Im (z2*'))
    .= Im(z1*'*z2*') by Th24;
end;

theorem Th122:
  z"*' = z*'"
proof
A1: Re z = Re (z*') & -Im z = Im (z*') by Th112;
  thus Re(z"*') = Re(z") by Th112
    .= Re z / ((Re z)^2+(Im z)^2) by Th64
    .= Re(z*'") by A1,Th64;
  thus Im(z"*') = -Im(z") by Th112
    .= -((- Im z) / ((Re z)^2+(Im z)^2)) by Th64
    .= (-Im (z*'))/((Re (z*'))^2+(Im (z*'))^2) by A1,XCMPLX_1:188
    .= Im(z*'") by Th64;
end;

theorem
  (z1/z2)*' = (z1*')/(z2*')
proof
  thus (z1/z2)*' = (z1*z2")*' by XCMPLX_0:def 9
    .= (z1*'*z2"*') by Th121
    .= (z1*'*z2*'") by Th122
    .= (z1*')/(z2*') by XCMPLX_0:def 9;
end;

theorem
  Im z = 0 implies z*' = z
proof
  Re (z*') = Re z & Im (z*') = -Im z by Th112;
  hence thesis by Th9;
end;

theorem
  Re z = 0 implies z*' = -z
proof
a1: z in COMPLEX by XCMPLX_0:def 2;
  assume
A1: Re z = 0;
  hence z*' = -0+(-Im z)*<i> .= -z by a1,A1,Def11;
end;

theorem
  Re(z*z*') = (Re z)^2 + (Im z)^2 & Im(z*z*') = 0
proof
  thus Re(z*(z*')) = Re z * Re (z*') - Im z * Im (z*') by Th24
    .= Re z * Re z - Im z * Im (z*') by Th112
    .= Re z * Re z - Im z * -Im z by Th112
    .= (Re z)^2 + (Im z)^2;
  thus Im(z*(z*')) = Re z * Im (z*') + Re (z*') * Im z by Th24
    .= Re z * -Im z + Re (z*') * Im z by Th112
    .= -Re z * Im z + Re z * Im z by Th112
    .= 0;
end;

theorem
  Re(z + z*') = 2*Re z & Im(z + z*') = 0
proof
  thus Re(z + z*') = Re z + Re (z*') by Th19
    .= Re z + Re z by Th112
    .= 2*Re z;
  thus Im(z + (z*')) = Im z + Im (z*') by Th19
    .= Im z + -Im z by Th112
    .= 0;
end;

theorem
  Re(z - z*') = 0 & Im(z - z*') = 2*Im z
proof
  thus Re(z - z*') = Re z - Re (z*') by Th48
    .= Re z - Re z by Th112
    .= 0;
  thus Im(z - z*') = Im z - Im (z*') by Th48
    .= Im z - -Im z by Th112
    .= 2*Im z;
end;

definition
  let z be complex number;
  func |.z.| -> complex number equals
  sqrt ((Re z)^2 + (Im z)^2);
  coherence;
  projectivity
  proof
    let r, z be complex number;
    assume
A1: r = sqrt ((Re z)^2 + (Im z)^2);
    then reconsider r9 = r as real number;
A2: Im r = 0 by A1,Def3;
    (Re z)^2 >= 0 & (Im z)^2 >= 0 by XREAL_1:65;
    then r9 >= 0 by A1,SQUARE_1:def 4;
    then
A3: Re r >= 0 by A1,Def2;
    thus r = Re r by A1,Def2
      .= sqrt ((Re r)^2 + (Im r)^2) by A3,A2,SQUARE_1:89;
  end;
end;

registration
  let z;
  cluster |.z.| -> real;
  coherence;
end;

definition
  let z;
  redefine func |.z.| -> Real;
  coherence;
end;

theorem Th129:
  a >= 0 implies |.a.| = a
proof
A1: a in REAL by XREAL_0:def 1;
  then
A2: Im a = 0 by Def3;
  assume a >= 0;
  then Re a >= 0 by A1,Def2;
  hence |.a.| = Re a by A2,SQUARE_1:89
  .= a by A1,Def2;
end;

registration
  cluster |.0.| -> zero;
  coherence by Th12,SQUARE_1:82;
end;

theorem
  |.0.| = 0;

registration
  let z be non zero complex number;
  cluster |.z.| -> non zero;
  coherence
  proof
    assume |.z.| is zero;
    then (Re z)^2 + (Im z)^2 = 0 by Lm1,SQUARE_1:92;
    hence thesis by Th13;
  end;
end;

theorem
  |.z.| = 0 implies z = 0;

registration
  let z;
  cluster |.z.| -> non negative;
  coherence
  proof
    0 <= (Re z)^2 + (Im z)^2 by Lm1;
    hence thesis by SQUARE_1:def 4;
  end;
end;

theorem
  0 <= |.z.|;

theorem
  z <> 0 iff 0 < |.z.|;

theorem Th134:
  |.1r.| = 1 by Th15,SQUARE_1:83;

theorem
  |.<i>.| = 1 by Th17,SQUARE_1:83;

Lm22: |.-z.| = |.z.|
proof
  thus |.-z.| = sqrt ((-Re z)^2 + (Im -z)^2) by Th34
    .= sqrt ((-Re z)^2 + (-Im z)^2) by Th34
    .= |.z.|;
end;

Lm23: a <= 0 implies |.a.| = -a
proof
  assume a <= 0;
  then |.-a.| = -a by Th129;
  hence thesis by Lm22;
end;

Lm24: sqrt a^2 = |.a.|
proof
  per cases;
  suppose
A1: 0 <= a;
    then sqrt a^2 = a by SQUARE_1:89;
    hence thesis by A1,Th129;
  end;
  suppose
A2: not 0 <= a;
    then |.a.| = -a by Lm23;
    hence thesis by A2,SQUARE_1:90;
  end;
end;

theorem
  Im z = 0 implies |.z.| = |.Re z.| by Lm24;

theorem
  Re z = 0 implies |.z.| = |.Im z.| by Lm24;

theorem
  |.-z.| = |.z.| by Lm22;

theorem Th139:
  |.z*'.| = |.z.|
proof
  thus |.z*'.| = sqrt ((Re z)^2 + (Im (z*'))^2) by Th112
    .= sqrt ((Re z)^2 + (-Im z)^2) by Th112
    .= |.z.|;
end;

Lm25: -|.a.| <= a & a <= |.a.|
proof
  a < 0 implies -|.a.| <= a & a <= |.a.|
  proof
    assume a < 0;
    then |.a.| = -a by Lm23;
    hence thesis;
  end;
  hence thesis by Th129;
end;

theorem
  Re z <= |.z.|
proof
  0<=(Im z)^2 by XREAL_1:65;
  then
A1: (Re z)^2+0 <= ((Re z)^2 + (Im z)^2) by XREAL_1:9;
  0<=(Re z)^2 by XREAL_1:65;
  then sqrt (Re z)^2 <= |.z.| by A1,SQUARE_1:94;
  then
A2: |.Re z.| <= |.z.| by Lm24;
  Re z <= |.Re z.| by Lm25;
  hence thesis by A2,XXREAL_0:2;
end;

theorem
  Im z <= |.z.|
proof
  0<=(Re z)^2 by XREAL_1:65;
  then
A1: (Im z)^2+0 <= ((Re z)^2 + (Im z)^2) by XREAL_1:9;
  0<=(Im z)^2 by XREAL_1:65;
  then sqrt (Im z)^2 <= |.z.| by A1,SQUARE_1:94;
  then
A2: |.Im z.| <= |.z.| by Lm24;
  Im z <= |.Im z.| by Lm25;
  hence thesis by A2,XXREAL_0:2;
end;

theorem Th142:
  |.z1 + z2.| <= |.z1.| + |.z2.|
proof
  set r1 = Re z1, r2 = Re z2, i1 = Im z1, i2 = Im z2;
A1: (Im(z1 + z2))^2 = (i1 + i2)^2 by Th19
    .= i1^2 + 2*i1*i2 + i2^2;
A2: 0 <= r1^2+i1^2 by Lm1;
  (r1^2+i1^2)*(r2^2+i2^2)-(r1*r2+i1*i2)^2 = (r1*i2-i1*r2)^2;
  then 0 <= (r1^2+i1^2)*(r2^2+i2^2)-(r1*r2+i1*i2)^2 by XREAL_1:65;
  then
A4: (r1*r2+i1*i2)^2+0 <= (r1^2+i1^2)*(r2^2+i2^2) by XREAL_1:21;
  r1*r2+i1*i2 <= |.r1*r2+i1*i2.| by Lm25;
  then
A5: r1*r2+i1*i2 <= sqrt (r1*r2+i1*i2)^2 by Lm24;
A6: 0 <= r2^2+i2^2 by Lm1;
  then
A7: (sqrt (r2^2+i2^2))^2 = r2^2+i2^2 by SQUARE_1:def 4;
  0<=(r1*r2+i1*i2)^2 by XREAL_1:65;
  then sqrt (r1*r2+i1*i2)^2 <= sqrt ((r1^2+i1^2)*(r2^2+i2^2)) by A4,
  SQUARE_1:94;
  then sqrt (r1*r2+i1*i2)^2 <= sqrt (r1^2+i1^2)*sqrt (r2^2+i2^2) by A2,A6,
  SQUARE_1:97;
  then
A8: r1*r2 + i1*i2 <= sqrt (r1^2+i1^2)*sqrt (r2^2+i2^2) by A5,
  XXREAL_0:
  2;
  (2*r1*r2 + 2*i1*i2) = 2*(r1*r2 + i1*i2);
  then (2*r1*r2 + 2*i1*i2) <= 2*(sqrt (r1^2+i1^2)*sqrt (r2^2+i2^2)) by A8,
  XREAL_1:66;
  then
A9: (r1^2 + i1^2)+(2*r1*r2+2*i1*i2) <= (r1^2+i1^2)+2*sqrt (r1^2+i1^2)*sqrt
  (r2^2+i2^2) by XREAL_1:9;
  (Re(z1 + z2))^2 = (r1+ r2)^2 by Th19
    .= r1^2 + 2*r1*r2 + r2^2;
  then
  (Re(z1+z2))^2+(Im(z1+z2))^2 = r1^2 + i1^2 + (2*r1*r2 + 2*i1*i2) + (r2^2
  + i2^2) by A1;
  then
A10: (Re(z1+z2))^2+(Im(z1+z2))^2 <= (r1^2+i1^2)+2*sqrt (r1^2+i1^2)*sqrt (r2
  ^2+i2^2)+(r2^2+i2^2) by A9, XREAL_1:9;
A11: 0 <= (Re(z1 + z2))^2 + (Im(z1 + z2))^2 by Lm1;
  (sqrt (r1^2+i1^2))^2 = r1^2+i1^2 by A2,SQUARE_1:def 4;
  then sqrt ((Re(z1 + z2))^2 + (Im(z1 + z2))^2) <= sqrt ((sqrt (r1^2+i1^2) +
  sqrt (r2^2+i2^2))^2) by A7,A10,A11,SQUARE_1:94;
  hence thesis by SQUARE_1:89;
end;

theorem Th143:
  |.z1 - z2.| <= |.z1.| + |.z2.|
proof
  |.z1 - z2.| = |.z1 + - z2.|;
  then |.z1 - z2.| <= |.z1.| + |.-z2.| by Th142;
  hence thesis by Lm22;
end;

theorem
  |.z1.| - |.z2.| <= |.z1 + z2.|
proof
  z1 = z1 + z2 - z2;
  then |.z1.| <= |.z1 + z2.| + |.z2.| by Th143;
  hence thesis by XREAL_1:22;
end;

theorem Th145:
  |.z1.| - |.z2.| <= |.z1 - z2.|
proof
  z1 = z1 - z2 + z2;
  then |.z1.| <= |.z1 - z2.| + |.z2.| by Th142;
  hence thesis by XREAL_1:22;
end;

theorem Th146:
  |.z1 - z2.| = |.z2 - z1.|
proof
  thus |.z1 - z2.| = |.-(z2 - z1).| .= |.z2 - z1.| by Lm22;
end;

theorem Th147:
  |.z1 - z2.| = 0 iff z1 = z2
proof
  thus |.z1 - z2.| = 0 implies z1 = z2
  proof
    assume |.z1 - z2.| = 0;
    then z1 - z2 = 0;
    hence thesis;
  end;
  thus thesis;
end;

theorem
  z1 <> z2 iff 0 < |.z1 - z2.|
proof
  thus z1 <> z2 implies 0 < |.z1 - z2.|
  proof
    assume z1 <> z2;
    then |.z1 - z2.| <> 0 by Th147;
    hence thesis;
  end;
  thus thesis;
end;

theorem
  |.z1 - z2.| <= |.z1 - z.| + |.z - z2.|
proof
  |.z1 - z2.| = |.(z1 - z) + (z - z2).|;
  hence thesis by Th142;
end;

Lm26: -b <= a & a <= b iff |.a.| <= b
proof
A1: |.a.| <= b implies -b <= a & a <= b
  proof
    assume
A2: |.a.| <= b;
 a < -0 implies -b <= a & a <= b
    proof
      assume
A4:   a < -0;
      then -a <= b by A2,Lm23;
      then -b <= -(-a) by XREAL_1:26;
      hence thesis by A4;
    end;
    hence thesis by A2,Th129;
  end;
  -b <= a & a <= b implies |.a.| <= b
  proof
    assume that
A5: -b <= a and
A6: a <= b;
    -a <= -(-b) by A5,XREAL_1:26;
    then a < 0 implies |.a.| <= b by Lm23;
    hence thesis by A6,Th129;
  end;
  hence thesis by A1;
end;

theorem
  |.|.z1.| - |.z2.|.| <= |.z1 - z2.|
proof
  |.z2.| - |.z1.| <= |.z2 - z1.| by Th145;
  then -(|.z1.| - |.z2.|) <= |.z1 - z2.| by Th146;
  then
A1: -|.z1 - z2.| <= --(|.z1.| - |.z2.|) by XREAL_1:26;
  |.z1.| - |.z2.| <= |.z1 - z2.| by Th145;
  hence thesis by A1,Lm26;
end;

theorem Th151:
  |.z1*z2.| = |.z1.|*|.z2.|
proof
  set r1 = Re z1, r2 = Re z2, i1 = Im z1, i2 = Im z2;
A1: 0<=r1^2 + i1^2 & 0<=r2^2 + i2^2 by Lm1;
A2: (Im(z1*z2))^2 = (r1*i2 + r2*i1)^2 by Th24
    .= 2*(r1*r2)*(i1*i2) + ((r1*i2)^2 + (r2*i1)^2);
  (Re(z1*z2))^2 = (r1*r2 - i1*i2)^2 by Th24
    .= (r1*r2)^2 + (i1*i2)^2 + - 2*(r1*r2)*(i1*i2);
  then (Re(z1*z2))^2+(Im(z1*z2))^2 = (r1^2 + i1^2)*(r2^2 + i2^2) by A2;
  hence thesis by A1,SQUARE_1:97;
end;

theorem Th152:
  |.z".| = |.z.|"
proof
  per cases;
  suppose
A1: z <> 0;
    set r2i2 = (Re z)^2+(Im z)^2;
A2: r2i2 <> 0 by A1,Th13;
A3: 0 <= r2i2 by Lm1;
    thus |.z".| = sqrt ((Re z / r2i2)^2 + (Im(z"))^2) by Th64
      .= sqrt ((Re z / r2i2)^2 + ((-Im z) / r2i2)^2) by Th64
      .= sqrt ((Re z)^2 / r2i2^2 + ((-Im z) / r2i2)^2) by XCMPLX_1:77
      .= sqrt ((Re z)^2 / r2i2^2 + (-Im z)^2 / r2i2^2) by XCMPLX_1:77
      .= sqrt ((1*r2i2) / (r2i2*r2i2)) by XCMPLX_1:63
      .= sqrt (1 / r2i2) by A2,XCMPLX_1:92
      .= 1 / |.z.| by A3,SQUARE_1:83,99
      .= |.z.|" by XCMPLX_1:217;
  end;
  suppose
A4: z = 0;
    hence |.z".| = 0" .= |.z.|" by A4;
  end;
end;

theorem Th153:
  |.z1.| / |.z2.| = |.z1/z2.|
proof
  thus |.z1.|/|.z2.| = |.z1.|*|.z2.|" by XCMPLX_0:def 9
    .= |.z1.|*|.z2".| by Th152
    .= |.z1*z2".| by Th151
    .= |.z1/z2.| by XCMPLX_0:def 9;
end;

theorem
  |.z*z.| = (Re z)^2 + (Im z)^2
proof
  0<=(Re z)^2 + (Im z)^2 & |.z*z.| = |.z.|*|.z.| by Lm1,Th151;
  then |.z*z.| = sqrt (((Re z)^2 + (Im z)^2)^2) by SQUARE_1:97;
  hence thesis by Lm1,SQUARE_1:89;
end;

theorem
  |.z*z.| = |.z*z*'.|
proof
  thus |.z*z.| = |.z.|*|.z.| by Th151
    .= |.z.|*|.z*'.| by Th139
    .= |.z*z*'.| by Th151;
end;
 
:: Originally from SQUARE_1

theorem
  a <= 0 implies |.a.| = -a by Lm23;

theorem Th157:
  |.a.| = a or |.a.| = -a
proof
  a >= 0 or a < 0;
  hence thesis by Lm23,Th129;
end;

theorem :: SQUARE_1'91
  sqrt a^2 = |.a.| by Lm24;

theorem :: SQUARE_1'34
  min(a,b) = (a + b - |.a - b.|) / 2
proof
  per cases;
  suppose
A1: a <= b;
    |.a - b.| = |.-(b - a).| .= |.b - a.| by Lm22
      .= b - a by A1,Th129,XREAL_1:50;
    hence thesis by A1,XXREAL_0:def 9;
  end;
  suppose
A2: b <= a;
    hence min(a,b) = ((a+b)- (a - b))/2 by XXREAL_0:def 9
      .= ((a+b)-|.a-b.|)/2 by A2,Th129,XREAL_1:50;
  end;
end;

theorem :: SQUARE_1'45
  max(a,b) = (a + b + |.a - b.|) / 2
proof
  per cases;
  suppose
A1: b <= a;
    hence max(a,b) = ((a+b)+ (a - b))/2 by XXREAL_0:def 10
      .= ((a+b)+|.a-b.|)/2 by A1,Th129,XREAL_1:50;
  end;
  suppose
A2: a <= b;
    then
A3: 0 <= b - a by XREAL_1:50;
    thus max(a,b) = ((a+b)+ -(a - b))/2 by A2,XXREAL_0:def 10
      .= ((a+b)+|.-(a-b).|)/2 by A3,Th129
      .= ((a+b)+|.a-b.|)/2 by Lm22;
  end;
end;

theorem Th161: :: SQUARE_1'62
  |.a.|^2 = a^2
proof
  |.a.| = a or |.a.| = -a by Th157;
  hence thesis;
end;

theorem
  -|.a.| <= a & a <= |.a.| by Lm25;

notation
  let z be complex number;
  synonym abs z for |.z.|;
end;

definition
  let z be complex number;
  redefine func abs z -> Real;
  coherence;
end;

theorem
  a+b*<i> = c+d*<i> implies a = c & b = d
proof
  assume
A1: a+b*<i> = c+d*<i>;
  then a-c+(b-d)*<i> = 0;
  then a-c = 0 by Th12,Th28;
  hence thesis by A1;
end;
 
:: from JGRAPH_1, 29.12.2006, AK

theorem
  sqrt(a^2+b^2) <= abs(a)+abs(b)
proof
A1: (sqrt(a^2+b^2))^2>=0 by XREAL_1:65;
  a^2>=0 & b^2>=0 by XREAL_1:65;
  then
A2: (sqrt(a^2+b^2))^2=a^2+b^2 by SQUARE_1:def 4;
  (abs(a)+abs(b))^2=(abs(a))^2+2*(abs(a))*(abs(b))+(abs(b))^2 & (abs
  (a) )^2=a^2 by Th161;
  then
  (abs(a)+abs(b))^2 - (sqrt(a^2+b^2))^2= a^2+2*(abs(a))*(abs(b))+b
  ^2-(a^2+b^2) by A2,Th161
    .=2*(abs(a))*(abs(b));
  then (abs(a)+abs(b))^2>= (sqrt(a^2+b^2))^2 by XREAL_1:51;
  then sqrt((abs(a)+abs(b))^2)>=sqrt((sqrt(a^2+b^2))^2) by A1,
  SQUARE_1:94;
  hence thesis by A2,SQUARE_1:89;
end;

theorem
  abs(a) <= sqrt(a^2+b^2)
proof
  b^2>=0 by XREAL_1:65;
  then a^2>=0 & a^2+0<= a^2+b^2 by XREAL_1:8,65;
  then sqrt(a^2)<= sqrt(a^2+b^2) by SQUARE_1:94;
  hence thesis by Lm24;
end;

theorem
  |. 1/z1 .| = 1 / |.z1.| by Th134,Th153;
