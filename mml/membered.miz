:: On the Sets Inhabited by Numbers
::  by Andrzej Trybulec
::
:: Received August 23, 2003
:: Copyright (c) 2003 Association of Mizar Users

environ

 vocabularies XCMPLX_0, XXREAL_0, XREAL_0, RAT_1, INT_1, ORDINAL1, NUMBERS,
      XBOOLE_0, SUBSET_1, TARSKI, SETFAM_1, MEMBERED, IDEAL_1, ARYTM_3;
 notations TARSKI, XBOOLE_0, ENUMSET1, SUBSET_1, SETFAM_1, NUMBERS, XCMPLX_0,
      XXREAL_0, XREAL_0, RAT_1, INT_1, ORDINAL1;
 constructors ENUMSET1, SETFAM_1, XCMPLX_0, XXREAL_0, RAT_1;
 registrations XREAL_0, INT_1, RAT_1, ORDINAL1, XCMPLX_0, NUMBERS, NAT_1;
 requirements BOOLE, SUBSET;
 definitions XCMPLX_0, XREAL_0, INT_1, TARSKI, RAT_1, XBOOLE_0, XXREAL_0;
 theorems INT_1, XBOOLE_0, XREAL_0, XCMPLX_0, RAT_1, SUBSET_1, ORDINAL1,
      TARSKI, ENUMSET1, XBOOLE_1, SETFAM_1, NUMBERS, CARD_1, XXREAL_0, AXIOMS;
 schemes XBOOLE_0;

begin

reserve x, X, F for set;

definition
  let X be set;
  attr X is complex-membered means
  :Def1:
  x in X implies x is complex;
  attr X is ext-real-membered means
  :Def2:
  x in X implies x is ext-real;
  attr X is real-membered means
  :Def3:
  x in X implies x is real;
  attr X is rational-membered means
  :Def4:
  x in X implies x is rational;
  attr X is integer-membered means
  :Def5:
  x in X implies x is integer;
  attr X is natural-membered means
  :Def6:
  x in X implies x is natural;
end;

registration
  cluster natural-membered -> integer-membered set;
  coherence
  proof
    let X;
    assume
A1: X is natural-membered;
    let x;
    assume x in X;
    then x is natural by A1,Def6;
    then x in omega by ORDINAL1:def 13;
    hence x in INT by NUMBERS:17;
  end;
  cluster integer-membered -> rational-membered set;
  coherence
  proof
    let X;
    assume
A2: X is integer-membered;
    let x;
    assume x in X;
    then x is integer by A2,Def5;
    then x in INT by INT_1:def 2;
    hence x in RAT by NUMBERS:14;
  end;
  cluster rational-membered -> real-membered set;
  coherence
  proof
    let X;
    assume
A3: X is rational-membered;
    let x;
    assume x in X;
    then x is rational by A3,Def4;
    then x in RAT by RAT_1:def 2;
    hence x in REAL by NUMBERS:12;
  end;
  cluster real-membered -> ext-real-membered set;
  coherence
  proof
    let X;
    assume
A4: X is real-membered;
    let x;
    assume x in X;
    then x is real by A4,Def3;
    then x in REAL by XREAL_0:def 1;
    hence x in ExtREAL by NUMBERS:31;
  end;
  cluster real-membered -> complex-membered set;
  coherence
  proof
    let X;
    assume
A5: X is real-membered;
    let x;
    assume x in X;
    then x is real by A5,Def3;
    then x in REAL by XREAL_0:def 1;
    hence x in COMPLEX by NUMBERS:11;
  end;
end;

registration
  cluster non empty natural-membered set;
  existence
  proof
    take 1;
    thus 1 is non empty;
    let x;
    assume x in 1;
    hence thesis by CARD_1:87,TARSKI:def 1;
  end;
end;

registration
  cluster -> complex-membered Subset of COMPLEX;
  coherence
  proof
    let S be Subset of COMPLEX;
    let x;
    thus thesis;
  end;
  cluster -> ext-real-membered Subset of ExtREAL;
  coherence
  proof
    let S be Subset of ExtREAL;
    let x;
    thus thesis by XXREAL_0:def 1;
  end;
  cluster -> real-membered Subset of REAL;
  coherence
  proof
    let S be Subset of REAL;
    let x;
    thus thesis by XREAL_0:def 1;
  end;
  cluster -> rational-membered Subset of RAT;
  coherence
  proof
    let S be Subset of RAT;
    let x;
    thus thesis by RAT_1:def 2;
  end;
  cluster -> integer-membered Subset of INT;
  coherence
  proof
    let S be Subset of INT;
    let x;
    thus thesis;
  end;
  cluster -> natural-membered Subset of NAT;
  coherence
  proof
    let S be Subset of NAT;
    let x;
    thus thesis;
  end;
end;

registration
  cluster COMPLEX -> complex-membered;
  coherence
  proof
    let x;
    thus thesis;
  end;
  cluster ExtREAL -> ext-real-membered;
  coherence
  proof
    let x;
    thus thesis by XXREAL_0:def 1;
  end;
  cluster REAL -> real-membered;
  coherence
  proof
    let x;
    thus thesis by XREAL_0:def 1;
  end;
  cluster RAT -> rational-membered;
  coherence
  proof
    let x;
    thus thesis by RAT_1:def 2;
  end;
  cluster INT -> integer-membered;
  coherence
  proof
    let x;
    thus thesis;
  end;
  cluster NAT -> natural-membered;
  coherence
  proof
    let x;
    thus thesis;
  end;
end;

theorem Th1:
  X is complex-membered implies X c= COMPLEX
proof
  assume
A1: X is complex-membered;
  let x;
  assume x in X;
  then x is complex by A1,Def1;
  hence thesis by XCMPLX_0:def 2;
end;

theorem Th2:
  X is ext-real-membered implies X c= ExtREAL
proof
  assume
A1: X is ext-real-membered;
  let x;
  assume x in X;
  then x is ext-real by A1,Def2;
  hence thesis by XXREAL_0:def 1;
end;

theorem Th3:
  X is real-membered implies X c= REAL
proof
  assume
A1: X is real-membered;
  let x;
  assume x in X;
  then x is real by A1,Def3;
  hence thesis by XREAL_0:def 1;
end;

theorem Th4:
  X is rational-membered implies X c= RAT
proof
  assume
A1: X is rational-membered;
  let x;
  assume x in X;
  then x is rational by A1,Def4;
  hence thesis by RAT_1:def 2;
end;

theorem Th5:
  X is integer-membered implies X c= INT
proof
  assume
A1: X is integer-membered;
  let x;
  assume x in X;
  then x is integer by A1,Def5;
  hence thesis by INT_1:def 2;
end;

theorem Th6:
  X is natural-membered implies X c= NAT
proof
  assume
A1: X is natural-membered;
  let x;
  assume x in X;
  then x is natural by A1,Def6;
  hence thesis by ORDINAL1:def 13;
end;

registration
  let X be complex-membered set;
  cluster -> complex Element of X;
  coherence
  proof
    let e be Element of X;
    per cases;
    suppose
      X is empty;
      hence thesis by SUBSET_1:def 2;
    end;
    suppose
      X is non empty;
      hence thesis by Def1;
    end;
  end;
end;

registration
  let X be ext-real-membered set;
  cluster -> ext-real Element of X;
  coherence
  proof
    let e be Element of X;
    per cases;
    suppose
      X is empty;
      hence thesis by SUBSET_1:def 2;
    end;
    suppose
      X is non empty;
      hence thesis by Def2;
    end;
  end;
end;

registration
  let X be real-membered set;
  cluster -> real Element of X;
  coherence
  proof
    let e be Element of X;
    per cases;
    suppose
      X is empty;
      hence thesis by SUBSET_1:def 2;
    end;
    suppose
      X is non empty;
      hence thesis by Def3;
    end;
  end;
end;

registration
  let X be rational-membered set;
  cluster -> rational Element of X;
  coherence
  proof
    let e be Element of X;
    per cases;
    suppose
      X is empty;
      hence thesis by SUBSET_1:def 2;
    end;
    suppose
      X is non empty;
      hence thesis by Def4;
    end;
  end;
end;

registration
  let X be integer-membered set;
  cluster -> integer Element of X;
  coherence
  proof
    let e be Element of X;
    per cases;
    suppose
      X is empty;
      hence thesis by SUBSET_1:def 2;
    end;
    suppose
      X is non empty;
      hence thesis by Def5;
    end;
  end;
end;

registration
  let X be natural-membered set;
  cluster -> natural Element of X;
  coherence
  proof
    let e be Element of X;
    per cases;
    suppose
      X is empty;
      hence thesis by SUBSET_1:def 2;
    end;
    suppose
      X is non empty;
      hence thesis by Def6;
    end;
  end;
end;

reserve c, c1, c2, c3 for complex number,
  e, e1, e2, e3 for ext-real number,
  r , r1, r2, r3 for real number,
  w, w1, w2, w3 for rational number,
  i, i1, i2, i3 for integer number,
  n, n1, n2, n3 for natural number;

theorem
  for X being non empty complex-membered set ex c st c in X
proof
  let X be non empty complex-membered set;
  ex x being set st x in X by XBOOLE_0:def 1;
  hence thesis;
end;

theorem
  for X being non empty ext-real-membered set ex e st e in X
proof
  let X be non empty ext-real-membered set;
  ex x being set st x in X by XBOOLE_0:def 1;
  hence thesis;
end;

theorem
  for X being non empty real-membered set ex r st r in X
proof
  let X be non empty real-membered set;
  ex x being set st x in X by XBOOLE_0:def 1;
  hence thesis;
end;

theorem
  for X being non empty rational-membered set ex w st w in X
proof
  let X be non empty rational-membered set;
  ex x being set st x in X by XBOOLE_0:def 1;
  hence thesis;
end;

theorem
  for X being non empty integer-membered set ex i st i in X
proof
  let X be non empty integer-membered set;
  ex x being set st x in X by XBOOLE_0:def 1;
  hence thesis;
end;

theorem
  for X being non empty natural-membered set ex n st n in X
proof
  let X be non empty natural-membered set;
  ex x being set st x in X by XBOOLE_0:def 1;
  hence thesis;
end;

theorem
  for X being complex-membered set st for c holds c in X holds X = COMPLEX
proof
  let X be complex-membered set such that
A1: for c holds c in X;
  thus X c= COMPLEX by Th1;
  let e be set;
  assume e in COMPLEX;
  hence thesis by A1;
end;

theorem
  for X being ext-real-membered set st for e holds e in X holds X = ExtREAL
proof
  let X be ext-real-membered set such that
A1: for e holds e in X;
  thus X c= ExtREAL by Th2;
  let e be set;
  assume e in ExtREAL;
  hence thesis by A1;
end;

theorem
  for X being real-membered set st for r holds r in X holds X = REAL
proof
  let X be real-membered set such that
A1: for r holds r in X;
  thus X c= REAL by Th3;
  let e be set;
  assume e in REAL;
  hence thesis by A1;
end;

theorem
  for X being rational-membered set st for w holds w in X holds X = RAT
proof
  let X be rational-membered set such that
A1: for w holds w in X;
  thus X c= RAT by Th4;
  let e be set;
  assume e in RAT;
  hence thesis by A1;
end;

theorem
  for X being integer-membered set st for i holds i in X holds X = INT
proof
  let X be integer-membered set such that
A1: for i holds i in X;
  thus X c= INT by Th5;
  let e be set;
  assume e in INT;
  hence thesis by A1;
end;

theorem
  for X being natural-membered set st for n holds n in X holds X = NAT
proof
  let X be natural-membered set such that
A1: for n holds n in X;
  thus X c= NAT by Th6;
  let e be set;
  assume e in NAT;
  hence thesis by A1;
end;

theorem Th19:
  for Y being complex-membered set st X c= Y holds X is complex-membered
proof
  let Y be complex-membered set such that
A1: X c= Y;
  let x;
  thus thesis by A1;
end;

theorem Th20:
  for Y being ext-real-membered set st X c= Y holds X is ext-real-membered
proof
  let Y be ext-real-membered set such that
A1: X c= Y;
  let x;
  thus thesis by A1;
end;

theorem Th21:
  for Y being real-membered set st X c= Y holds X is real-membered
proof
  let Y be real-membered set such that
A1: X c= Y;
  let x;
  thus thesis by A1;
end;

theorem Th22:
  for Y being rational-membered set st X c= Y holds X is rational-membered
proof
  let Y be rational-membered set such that
A1: X c= Y;
  let x;
  thus thesis by A1;
end;

theorem Th23:
  for Y being integer-membered set st X c= Y holds X is integer-membered
proof
  let Y be integer-membered set such that
A1: X c= Y;
  let x;
  thus thesis by A1;
end;

theorem Th24:
  for Y being natural-membered set st X c= Y holds X is natural-membered
proof
  let Y be natural-membered set such that
A1: X c= Y;
  let x;
  thus thesis by A1;
end;

registration
  cluster empty -> natural-membered set;
  coherence
  proof
    let X be set such that
A1: X is empty;
    let x;
    thus thesis by A1;
  end;
end;

registration
  let c;
  cluster {c} -> complex-membered;
  coherence
  proof
    let e be set;
    thus thesis by TARSKI:def 1;
  end;
end;

registration
  let e;
  cluster {e} -> ext-real-membered;
  coherence
  proof
    let e be set;
    thus thesis by TARSKI:def 1;
  end;
end;

registration
  let r;
  cluster {r} -> real-membered;
  coherence
  proof
    let e be set;
    thus thesis by TARSKI:def 1;
  end;
end;

registration
  let w;
  cluster {w} -> rational-membered;
  coherence
  proof
    let e be set;
    thus thesis by TARSKI:def 1;
  end;
end;

registration
  let i;
  cluster {i} -> integer-membered;
  coherence
  proof
    let e be set;
    thus thesis by TARSKI:def 1;
  end;
end;

registration
  let n;
  cluster {n} -> natural-membered;
  coherence
  proof
    let e be set;
    thus thesis by TARSKI:def 1;
  end;
end;

registration
  let c1,c2;
  cluster {c1,c2} -> complex-membered;
  coherence
  proof
    let e be set;
    thus thesis by TARSKI:def 2;
  end;
end;

registration
  let e1,e2;
  cluster {e1,e2} -> ext-real-membered;
  coherence
  proof
    let e be set;
    thus thesis by TARSKI:def 2;
  end;
end;

registration
  let r1,r2;
  cluster {r1,r2} -> real-membered;
  coherence
  proof
    let e be set;
    thus thesis by TARSKI:def 2;
  end;
end;

registration
  let w1,w2;
  cluster {w1,w2} -> rational-membered;
  coherence
  proof
    let e be set;
    thus thesis by TARSKI:def 2;
  end;
end;

registration
  let i1,i2;
  cluster {i1,i2} -> integer-membered;
  coherence
  proof
    let e be set;
    thus thesis by TARSKI:def 2;
  end;
end;

registration
  let n1,n2;
  cluster {n1,n2} -> natural-membered;
  coherence
  proof
    let e be set;
    thus thesis by TARSKI:def 2;
  end;
end;

registration
  let c1,c2,c3;
  cluster {c1,c2,c3} -> complex-membered;
  coherence
  proof
    let e be set;
    thus thesis by ENUMSET1:def 1;
  end;
end;

registration
  let e1,e2,e3;
  cluster {e1,e2,e3} -> ext-real-membered;
  coherence
  proof
    let e be set;
    thus thesis by ENUMSET1:def 1;
  end;
end;

registration
  let r1,r2,r3;
  cluster {r1,r2,r3} -> real-membered;
  coherence
  proof
    let e be set;
    thus thesis by ENUMSET1:def 1;
  end;
end;

registration
  let w1,w2,w3;
  cluster {w1,w2,w3} -> rational-membered;
  coherence
  proof
    let e be set;
    thus thesis by ENUMSET1:def 1;
  end;
end;

registration
  let i1,i2,i3;
  cluster {i1,i2,i3} -> integer-membered;
  coherence
  proof
    let e be set;
    thus thesis by ENUMSET1:def 1;
  end;
end;

registration
  let n1,n2,n3;
  cluster {n1,n2,n3} -> natural-membered;
  coherence
  proof
    let e be set;
    thus thesis by ENUMSET1:def 1;
  end;
end;

registration
  let X be complex-membered set;
  cluster -> complex-membered Subset of X;
  coherence
  proof
    let S be Subset of X, x;
    thus thesis;
  end;
end;

registration
  let X be ext-real-membered set;
  cluster -> ext-real-membered Subset of X;
  coherence
  proof
    let S be Subset of X, x;
    thus thesis;
  end;
end;

registration
  let X be real-membered set;
  cluster -> real-membered Subset of X;
  coherence
  proof
    let S be Subset of X, x;
    thus thesis;
  end;
end;

registration
  let X be rational-membered set;
  cluster -> rational-membered Subset of X;
  coherence
  proof
    let S be Subset of X, x;
    thus thesis;
  end;
end;

registration
  let X be integer-membered set;
  cluster -> integer-membered Subset of X;
  coherence
  proof
    let S be Subset of X, x;
    thus thesis;
  end;
end;

registration
  let X be natural-membered set;
  cluster -> natural-membered Subset of X;
  coherence
  proof
    let S be Subset of X, x;
    thus thesis;
  end;
end;

registration
  let X,Y be complex-membered set;
  cluster X \/ Y -> complex-membered;
  coherence
  proof
    let x;
    assume x in X \/ Y;
    then x in X or x in Y by XBOOLE_0:def 3;
    hence thesis;
  end;
end;

registration
  let X,Y be ext-real-membered set;
  cluster X \/ Y -> ext-real-membered;
  coherence
  proof
    let x;
    assume x in X \/ Y;
    then x in X or x in Y by XBOOLE_0:def 3;
    hence thesis;
  end;
end;

registration
  let X,Y be real-membered set;
  cluster X \/ Y -> real-membered;
  coherence
  proof
    let x;
    assume x in X \/ Y;
    then x in X or x in Y by XBOOLE_0:def 3;
    hence thesis;
  end;
end;

registration
  let X,Y be rational-membered set;
  cluster X \/ Y -> rational-membered;
  coherence
  proof
    let x;
    assume x in X \/ Y;
    then x in X or x in Y by XBOOLE_0:def 3;
    hence thesis;
  end;
end;

registration
  let X,Y be integer-membered set;
  cluster X \/ Y -> integer-membered;
  coherence
  proof
    let x;
    assume x in X \/ Y;
    then x in X or x in Y by XBOOLE_0:def 3;
    hence thesis;
  end;
end;

registration
  let X,Y be natural-membered set;
  cluster X \/ Y -> natural-membered;
  coherence
  proof
    let x;
    assume x in X \/ Y;
    then x in X or x in Y by XBOOLE_0:def 3;
    hence thesis;
  end;
end;

registration
  let X be complex-membered set, Y be set;
  cluster X /\ Y -> complex-membered;
  coherence by Th19,XBOOLE_1:17;
  cluster Y /\ X -> complex-membered;
  coherence;
end;

registration
  let X be ext-real-membered set, Y be set;
  cluster X /\ Y -> ext-real-membered;
  coherence by Th20,XBOOLE_1:17;
  cluster Y /\ X -> ext-real-membered;
  coherence;
end;

registration
  let X be real-membered set, Y be set;
  cluster X /\ Y -> real-membered;
  coherence by Th21,XBOOLE_1:17;
  cluster Y /\ X -> real-membered;
  coherence;
end;

registration
  let X be rational-membered set, Y be set;
  cluster X /\ Y -> rational-membered;
  coherence by Th22,XBOOLE_1:17;
  cluster Y /\ X -> rational-membered;
  coherence;
end;

registration
  let X be integer-membered set, Y be set;
  cluster X /\ Y -> integer-membered;
  coherence by Th23,XBOOLE_1:17;
  cluster Y /\ X -> integer-membered;
  coherence;
end;

registration
  let X be natural-membered set, Y be set;
  cluster X /\ Y -> natural-membered;
  coherence by Th24,XBOOLE_1:17;
  cluster Y /\ X -> natural-membered;
  coherence;
end;

registration
  let X be complex-membered set, Y be set;
  cluster X \ Y -> complex-membered;
  coherence;
end;

registration
  let X be ext-real-membered set, Y be set;
  cluster X \ Y -> ext-real-membered;
  coherence;
end;

registration
  let X be real-membered set, Y be set;
  cluster X \ Y -> real-membered;
  coherence;
end;

registration
  let X be rational-membered set, Y be set;
  cluster X \ Y -> rational-membered;
  coherence;
end;

registration
  let X be integer-membered set, Y be set;
  cluster X \ Y -> integer-membered;
  coherence;
end;

registration
  let X be natural-membered set, Y be set;
  cluster X \ Y -> natural-membered;
  coherence;
end;

registration
  let X,Y be complex-membered set;
  cluster X \+\ Y -> complex-membered;
  coherence;
end;

registration
  let X,Y be ext-real-membered set;
  cluster X \+\ Y -> ext-real-membered;
  coherence;
end;

registration
  let X,Y be real-membered set;
  cluster X \+\ Y -> real-membered;
  coherence;
end;

registration
  let X,Y be rational-membered set;
  cluster X \+\ Y -> rational-membered;
  coherence;
end;

registration
  let X,Y be integer-membered set;
  cluster X \+\ Y -> integer-membered;
  coherence;
end;

registration
  let X,Y be natural-membered set;
  cluster X \+\ Y -> natural-membered;
  coherence;
end;

definition
  let X be complex-membered set, Y be set;
  redefine pred X c= Y means
  :Def7:
  c in X implies c in Y;
  compatibility
  proof
    thus X c= Y implies for c st c in X holds c in Y;
    assume
A1: c in X implies c in Y;
    let x;
    assume x in X;
    hence thesis by A1;
  end;
end;

definition
  let X be ext-real-membered set, Y be set;
  redefine pred X c= Y means
  :Def8:
  e in X implies e in Y;
  compatibility
  proof
    thus X c= Y implies for e st e in X holds e in Y;
    assume
A1: e in X implies e in Y;
    let x;
    assume x in X;
    hence thesis by A1;
  end;
end;

definition
  let X be real-membered set, Y be set;
  redefine pred X c= Y means
  :Def9:
  r in X implies r in Y;
  compatibility
  proof
    thus X c= Y implies for r st r in X holds r in Y;
    assume
A1: r in X implies r in Y;
    let e;
    assume e in X;
    hence thesis by A1;
  end;
end;

definition
  let X be rational-membered set, Y be set;
  redefine pred X c= Y means
  :Def10:
  w in X implies w in Y;
  compatibility
  proof
    thus X c= Y implies for w st w in X holds w in Y;
    assume
A1: w in X implies w in Y;
    let r;
    assume r in X;
    hence thesis by A1;
  end;
end;

definition
  let X be integer-membered set, Y be set;
  redefine pred X c= Y means
  :Def11:
  i in X implies i in Y;
  compatibility
  proof
    thus X c= Y implies for i st i in X holds i in Y;
    assume
A1: i in X implies i in Y;
    let w;
    assume w in X;
    hence thesis by A1;
  end;
end;

definition
  let X be natural-membered set, Y be set;
  redefine pred X c= Y means
  :Def12:
  n in X implies n in Y;
  compatibility
  proof
    thus X c= Y implies for n st n in X holds n in Y;
    assume
A1: n in X implies n in Y;
    let i;
    assume i in X;
    hence thesis by A1;
  end;
end;

definition
  let X,Y be complex-membered set;
  redefine pred X = Y means
  c in X iff c in Y;
  compatibility
  proof
    thus X = Y implies for c holds c in X iff c in Y;
    assume c in X iff c in Y;
    hence X c= Y & Y c= X by Def7;
  end;
end;

definition
  let X,Y be ext-real-membered set;
  redefine pred X = Y means
  e in X iff e in Y;
  compatibility
  proof
    thus X = Y implies for e holds e in X iff e in Y;
    assume e in X iff e in Y;
    then X c= Y & Y c= X by Def8;
    hence thesis by XBOOLE_0:def 10;
  end;
end;

definition
  let X,Y be real-membered set;
  redefine pred X = Y means
  r in X iff r in Y;
  compatibility
  proof
    thus X = Y implies for r holds r in X iff r in Y;
    assume r in X iff r in Y;
    then X c= Y & Y c= X by Def9;
    hence thesis by XBOOLE_0:def 10;
  end;
end;

definition
  let X,Y be rational-membered set;
  redefine pred X = Y means
  w in X iff w in Y;
  compatibility
  proof
    thus X = Y implies for w holds w in X iff w in Y;
    assume w in X iff w in Y;
    then X c= Y & Y c= X by Def10;
    hence thesis by XBOOLE_0:def 10;
  end;
end;

definition
  let X,Y be integer-membered set;
  redefine pred X = Y means
  i in X iff i in Y;
  compatibility
  proof
    thus X = Y implies for i holds i in X iff i in Y;
    assume i in X iff i in Y;
    then X c= Y & Y c= X by Def11;
    hence thesis by XBOOLE_0:def 10;
  end;
end;

definition
  let X,Y be natural-membered set;
  redefine pred X = Y means
  n in X iff n in Y;
  compatibility
  proof
    thus X = Y implies for n holds n in X iff n in Y;
    assume n in X iff n in Y;
    then X c= Y & Y c= X by Def12;
    hence thesis by XBOOLE_0:def 10;
  end;
end;

definition
  let X,Y be complex-membered set;
  redefine pred X meets Y means
  ex c st c in X & c in Y;
  compatibility
  proof
    thus X misses Y implies not ex c st c in X & c in Y by XBOOLE_0:3;
    assume
A1: not ex c st c in X & c in Y;
    assume X meets Y;
    then ex x st x in X & x in Y by XBOOLE_0:3;
    hence thesis by A1;
  end;
end;

definition
  let X,Y be ext-real-membered set;
  redefine pred X meets Y means
  ex e st e in X & e in Y;
  compatibility
  proof
    thus X misses Y implies not ex e st e in X & e in Y by XBOOLE_0:3;
    assume
A1: not ex e st e in X & e in Y;
    assume X meets Y;
    then ex x st x in X & x in Y by XBOOLE_0:3;
    hence thesis by A1;
  end;
end;

definition
  let X,Y be real-membered set;
  redefine pred X meets Y means
  ex r st r in X & r in Y;
  compatibility
  proof
    thus X misses Y implies not ex r st r in X & r in Y by XBOOLE_0:3;
    assume
A1: not ex r st r in X & r in Y;
    assume X meets Y;
    then ex x st x in X & x in Y by XBOOLE_0:3;
    hence thesis by A1;
  end;
end;

definition
  let X,Y be rational-membered set;
  redefine pred X meets Y means
  ex w st w in X & w in Y;
  compatibility
  proof
    thus X misses Y implies not ex w st w in X & w in Y by XBOOLE_0:3;
    assume
A1: not ex w st w in X & w in Y;
    assume X meets Y;
    then ex x st x in X & x in Y by XBOOLE_0:3;
    hence thesis by A1;
  end;
end;

definition
  let X,Y be integer-membered set;
  redefine pred X meets Y means
  ex i st i in X & i in Y;
  compatibility
  proof
    thus X misses Y implies not ex i st i in X & i in Y by XBOOLE_0:3;
    assume
A1: not ex i st i in X & i in Y;
    assume X meets Y;
    then ex x st x in X & x in Y by XBOOLE_0:3;
    hence thesis by A1;
  end;
end;

definition
  let X,Y be natural-membered set;
  redefine pred X meets Y means
  ex n st n in X & n in Y;
  compatibility
  proof
    thus X misses Y implies not ex n st n in X & n in Y by XBOOLE_0:3;
    assume
A1: not ex n st n in X & n in Y;
    assume X meets Y;
    then ex x st x in X & x in Y by XBOOLE_0:3;
    hence thesis by A1;
  end;
end;

theorem
  (for X st X in F holds X is complex-membered) implies union F is
  complex-membered
proof
  assume
A1: for X st X in F holds X is complex-membered;
  let x;
  assume x in union F;
  then consider X such that
A2: x in X and
A3: X in F by TARSKI:def 4;
  X is complex-membered by A1,A3;
  hence thesis by A2;
end;

theorem
  (for X st X in F holds X is ext-real-membered) implies union F is
  ext-real-membered
proof
  assume
A1: for X st X in F holds X is ext-real-membered;
  let x;
  assume x in union F;
  then consider X such that
A2: x in X and
A3: X in F by TARSKI:def 4;
  X is ext-real-membered by A1,A3;
  hence thesis by A2;
end;

theorem
  (for X st X in F holds X is real-membered) implies union F is real-membered
proof
  assume
A1: for X st X in F holds X is real-membered;
  let x;
  assume x in union F;
  then consider X such that
A2: x in X and
A3: X in F by TARSKI:def 4;
  X is real-membered by A1,A3;
  hence thesis by A2;
end;

theorem
  (for X st X in F holds X is rational-membered) implies union F is
  rational-membered
proof
  assume
A1: for X st X in F holds X is rational-membered;
  let x;
  assume x in union F;
  then consider X such that
A2: x in X and
A3: X in F by TARSKI:def 4;
  X is rational-membered by A1,A3;
  hence thesis by A2;
end;

theorem
  (for X st X in F holds X is integer-membered) implies union F is
  integer-membered
proof
  assume
A1: for X st X in F holds X is integer-membered;
  let x;
  assume x in union F;
  then consider X such that
A2: x in X and
A3: X in F by TARSKI:def 4;
  X is integer-membered by A1,A3;
  hence thesis by A2;
end;

theorem
  (for X st X in F holds X is natural-membered) implies union F is
  natural-membered
proof
  assume
A1: for X st X in F holds X is natural-membered;
  let x;
  assume x in union F;
  then consider X such that
A2: x in X and
A3: X in F by TARSKI:def 4;
  X is natural-membered by A1,A3;
  hence thesis by A2;
end;

theorem
  for X st X in F & X is complex-membered holds meet F is
  complex-membered by Th19,SETFAM_1:4;

theorem
  for X st X in F & X is ext-real-membered holds meet F is
  ext-real-membered by Th20,SETFAM_1:4;

theorem
  for X st X in F & X is real-membered holds meet F is real-membered by
  Th21,SETFAM_1:4;

theorem
  for X st X in F & X is rational-membered holds meet F is
  rational-membered by Th22,SETFAM_1:4;

theorem
  for X st X in F & X is integer-membered holds meet F is
  integer-membered by Th23,SETFAM_1:4;

theorem
  for X st X in F & X is natural-membered holds meet F is
  natural-membered by Th24,SETFAM_1:4;

scheme
  CMSeparation {P[set]}: ex X being complex-membered set st for c holds c in X
  iff P[c] proof
  consider X being set such that
A1: x in X iff x in COMPLEX & P[x] from XBOOLE_0:sch 1;
  X is complex-membered
  proof
    let x;
    assume x in X;
    then x in COMPLEX by A1;
    hence thesis;
  end;
  then reconsider X as complex-membered set;
  take X;
  let c;
  c in COMPLEX by XCMPLX_0:def 2;
  hence thesis by A1;
end;

scheme
  EMSeparation {P[set]}: ex X being ext-real-membered set st for e holds e in
  X iff P[e] proof
  consider X being set such that
A1: x in X iff x in ExtREAL & P[x] from XBOOLE_0:sch 1;
  X is ext-real-membered
  proof
    let x;
    assume x in X;
    then x in ExtREAL by A1;
    hence thesis;
  end;
  then reconsider X as ext-real-membered set;
  take X;
  let e;
  e in ExtREAL by XXREAL_0:def 1;
  hence thesis by A1;
end;

scheme
  RMSeparation {P[set]}: ex X being real-membered set st for r holds r in X
  iff P[r] proof
  consider X being set such that
A1: x in X iff x in REAL & P[x] from XBOOLE_0:sch 1;
  X is real-membered
  proof
    let x;
    assume x in X;
    then x in REAL by A1;
    hence thesis;
  end;
  then reconsider X as real-membered set;
  take X;
  let r;
  r in REAL by XREAL_0:def 1;
  hence thesis by A1;
end;

scheme
  WMSeparation {P[set]}: ex X being rational-membered set st for w holds w in
  X iff P[w] proof
  consider X being set such that
A1: x in X iff x in RAT & P[x] from XBOOLE_0:sch 1;
  X is rational-membered
  proof
    let x;
    assume x in X;
    then x in RAT by A1;
    hence thesis;
  end;
  then reconsider X as rational-membered set;
  take X;
  let w;
  w in RAT by RAT_1:def 2;
  hence thesis by A1;
end;

scheme
  IMSeparation {P[set]}: ex X being integer-membered set st for i holds i in X
  iff P[i] proof
  consider X being set such that
A1: x in X iff x in INT & P[x] from XBOOLE_0:sch 1;
  X is integer-membered
  proof
    let x;
    assume x in X;
    then x in INT by A1;
    hence thesis;
  end;
  then reconsider X as integer-membered set;
  take X;
  let i;
  i in INT by INT_1:def 2;
  hence thesis by A1;
end;

scheme
  NMSeparation {P[set]}: ex X being natural-membered set st for n holds n in X
  iff P[n] proof
  consider X being set such that
A1: x in X iff x in NAT & P[x] from XBOOLE_0:sch 1;
  X is natural-membered
  proof
    let x;
    assume x in X;
    then x in NAT by A1;
    hence thesis;
  end;
  then reconsider X as natural-membered set;
  take X;
  let n;
  n in NAT by ORDINAL1:def 13;
  hence thesis by A1;
end;

registration
  cluster non empty natural-membered set;
  existence
  proof
    consider X being non empty natural-membered set;
    take X;
    thus thesis;
  end;
end;

:: Added by AK, 2008.02.02

registration
  cluster -> natural-membered Element of NAT;
  coherence
  proof
    let n be Element of NAT;
    let x be set;
    n c= omega by ORDINAL1:def 2;
    hence thesis;
  end;
end;

:: From REAL_2, AG, 19.12.2008

reserve a,b,d for real number;

theorem
  for X,Y being real-membered set st X<>{} & Y<>{} & for a,b st a in X &
b in Y holds a<=b holds ex d st (for a st a in X holds a<=d) & for b st b in Y
  holds d<=b
proof
  let X,Y be real-membered set;
  consider x being Element of X;
  reconsider a=x as real number;
  consider y being Element of Y;
  reconsider b=y as real number;
  assume X<>{} & Y<>{};
  then
A1: a in X & b in Y;
A2: X c= REAL & Y c= REAL by Th3;
  assume for a,b st a in X & b in Y holds a<=b;
  then consider d being real number such that
A3: for a,b being real number st a in X & b in Y holds a<=d & d<=b by
  A2,AXIOMS:26;
  reconsider d as real number;
  take d;
  thus thesis by A1,A3;
end;
 
:: Added, AK, 2010.04.23

definition
  let X be set;
  attr X is add-closed means
  for x, y being complex number st x in X & y in X holds x+y in X;
end;

registration
  cluster empty -> add-closed set;
  coherence
  proof
    let X be set such that
a1: X is empty;
    let x be complex number;
    thus thesis by a1;
  end;
  cluster COMPLEX -> add-closed;
  coherence
  proof
    let x be complex number;
    thus thesis by XCMPLX_0:def 2;
  end;
  cluster REAL -> add-closed;
  coherence
  proof
    let x be complex number;
    thus thesis by XREAL_0:def 1;
  end;
  cluster RAT -> add-closed;
  coherence
  proof
    let x be complex number;
    thus thesis by RAT_1:def 2;
  end;
  cluster INT -> add-closed;
  coherence
  proof
    let x be complex number;
    thus thesis by INT_1:def 2;
  end;
  cluster NAT -> add-closed;
  coherence
  proof
    let x be complex number;
    thus thesis by ORDINAL1:def 13;
  end;
  cluster non empty add-closed natural-membered set;
  existence
  proof
    take NAT;
    thus thesis;
  end;
end;
