:: Bounded Domains and Unbounded Domains
::  by Yatsuka Nakamura , Andrzej Trybulec and Czeslaw Bylinski
::
:: Received January 7, 1999
:: Copyright (c) 1999 Association of Mizar Users

environ

 vocabularies NUMBERS, SUBSET_1, REAL_1, XXREAL_0, CARD_1, COMPLEX1, ARYTM_1,
      ARYTM_3, FINSEQ_1, RELAT_1, FUNCT_1, ORDINAL2, PRE_TOPC, EUCLID, RVSUM_1,
      SQUARE_1, XBOOLE_0, RELAT_2, TARSKI, STRUCT_0, NAT_1, XXREAL_2, CONNSP_1,
      METRIC_1, CONVEX1, CONNSP_3, ZFMISC_1, SETFAM_1, RLTOPSP1, FINSEQ_2,
      CARD_3, SUPINF_2, BORSUK_1, XXREAL_1, BORSUK_2, GRAPH_1, TOPMETR,
      TREAL_1, VALUED_1, FUNCT_4, RCOMP_1, TOPREAL1, TOPS_2, PCOMPS_1,
      WEIERSTR, SEQ_4, XREAL_0, ORDINAL1, PARTFUN1, FUNCOP_1, BINOP_2,
      ORDINAL4, JORDAN3, TBSP_1, CONNSP_2, RUSUB_4, SPPOL_1, GOBOARD2,
      SPRECT_1, TREES_1, PSCOMP_1, GOBOARD1, MCART_1, MATRIX_1, GOBOARD9,
      GOBOARD5, TOPS_1, JORDAN1, SPRECT_2, JORDAN2C;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, SETFAM_1, RELAT_1, FUNCT_1,
      RVSUM_1, 
      RELSET_1, PARTFUN1, FUNCT_2, ORDINAL1, CARD_1, NUMBERS, XREAL_0,
      XXREAL_2, COMPLEX1, SEQ_1, SEQM_3, PRE_TOPC, TOPS_1, TOPS_2, COMPTS_1,
      METRIC_1, BINOP_2, PCOMPS_1, CONNSP_1, CONNSP_2, TBSP_1, CONNSP_3,
      TOPMETR, RCOMP_1, FINSEQ_1, FINSEQ_2, DOMAIN_1, STRUCT_0, SQUARE_1,
      BORSUK_2, XXREAL_0, SEQ_4, FINSEQ_6, JORDAN3, BINARITH, FUNCOP_1,
      FUNCT_3, TREAL_1, FUNCT_4, RLVECT_1, RLTOPSP1, EUCLID, SPPOL_1,
      PSCOMP_1, SPRECT_1, SPRECT_2, TOPREAL1, MATRIX_1, GOBOARD1, GOBOARD2,
      GOBOARD5, GOBOARD9, JORDAN2B, REAL_1, NAT_1, NAT_D, WEIERSTR;
 constructors SETFAM_1, REAL_1, SQUARE_1, NAT_1, COMPLEX1, FINSEQOP, RCOMP_1,
      FINSOP_1, REALSET1, BINARITH, TOPS_1, CONNSP_1, TOPS_2, COMPTS_1, TBSP_1,
      MONOID_0, TREAL_1, GOBOARD2, SPPOL_1, JORDAN1, PSCOMP_1, WEIERSTR,
      BORSUK_2, GOBOARD9, CONNSP_3, JORDAN3, JORDAN2B, SPRECT_1, SPRECT_2,
      SEQ_1, BINOP_2, XXREAL_2, GOBOARD1, NAT_D, SEQ_4, FUNCSDOM, CONVEX1;
 registrations XBOOLE_0, SUBSET_1, FUNCT_1, RELSET_1, FUNCT_2, FINSET_1,
      NUMBERS, XXREAL_0, XREAL_0, MEMBERED, FINSEQ_1, REALSET1, STRUCT_0,
      PRE_TOPC, TOPS_1, METRIC_1, BORSUK_1, TBSP_1, EUCLID, TOPMETR, TOPREAL1,
      GOBOARD2, JORDAN1, BORSUK_2, SPRECT_1, SPRECT_3, VALUED_0, FINSEQ_2,
      RLTOPSP1, JORDAN2B, MONOID_0, SEQ_4, SPPOL_1;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, XBOOLE_0, TOPS_2, TBSP_1, JORDAN1, EUCLID, BINOP_1,
      SQUARE_1, SUBSET_1, PSCOMP_1, FINSEQ_2, CONNSP_3, RVSUM_1, STRUCT_0,
      VALUED_1, ALGSTR_0, RLTOPSP1, XXREAL_2;
 theorems PRE_TOPC, CONNSP_1, EUCLID, TBSP_1, TOPREAL3, NAT_1, JGRAPH_1,
      JORDAN1, TOPS_2, FUNCT_2, BORSUK_1, TOPMETR, TOPREAL1, FINSEQ_2,
      FINSEQOP, ABSVALUE, RVSUM_1, SQUARE_1, BORSUK_2, TOPREAL5, WEIERSTR,
      SPPOL_1, TARSKI, SEQ_4, FUNCT_1, METRIC_1, SUBSET_1, FUNCOP_1, ZFMISC_1,
      FINSEQ_1, FINSEQ_4, RELAT_1, FUNCT_3, RCOMP_1, FUNCT_4, HEINE, TREAL_1,
      CONNSP_3, JORDAN6, COMPTS_1, TSEP_1, CONNSP_2, TOPS_1, JORDAN5D,
      JORDAN2B, GOBOARD1, GOBOARD2, GOBOARD5, GOBOARD6, UNIFORM1, FINSEQ_6,
      MATRIX_1, GOBOARD9, GOBRD12, SPRECT_1, CARD_1, CARD_2, ENUMSET1,
      SPRECT_2, SPRECT_3, SEQ_2, RELSET_1, XBOOLE_0, XBOOLE_1, XREAL_0,
      TOPRNS_1, XCMPLX_0, XCMPLX_1, BINOP_2, COMPLEX1, XREAL_1, XXREAL_0,
      FINSOP_1, XXREAL_1, SEQM_3, NAT_D, VALUED_1, RLTOPSP1, RLVECT_1,
      XXREAL_2;
 schemes NAT_1, CLASSES1;

begin :: Definitions of Bounded Domain and Unbounded Domain

reserve m,n,i,i2,j for Element of NAT,
  r,r1,r2,s for Real,
  x,y,z for set;

theorem Th1:
  r<=0 implies abs(r)=-r
proof
  assume
A1: r<=0;
  per cases by A1;
  suppose r<0;
    hence thesis by ABSVALUE:def 1;
  end;
  suppose r=0;
    hence thesis by ABSVALUE:7;
  end;
end;

theorem Th2:
  for n,m st n<=m & m<=n+2 holds m=n or m=n+1 or m=n+2
proof
  let n,m;
  assume that
A1: n<=m and
A2: m<=n+2;
  per cases;
  suppose
    m<=n+1;
    hence thesis by A1,NAT_1:9;
  end;
  suppose
    m>n+1;
    then m=n+1 or m=n+1+1 by A2,NAT_1:9;
    hence thesis;
  end;
end;

theorem Th3:
  for n,m st n<=m & m<=n+3 holds m=n or m=n+1 or m=n+2 or m=n+3
proof
  let n,m;
  assume that
A1: n<=m and
A2: m<=n+3;
  per cases;
  suppose
    m<=n+2;
    hence thesis by A1,Th2;
  end;
  suppose
    m>n+2;
    then m=n+2 or m=n+2+1 by A2,NAT_1:9;
    hence thesis;
  end;
end;

theorem Th4:
  for n,m st n<=m & m<=n+4 holds m=n or m=n+1 or m=n+2 or m=n+3 or m=n+4
proof
  let n,m;
  assume that
A1: n<=m and
A2: m<=n+4;
  per cases;
  suppose
    m<=n+3;
    hence thesis by A1,Th3;
  end;
  suppose
    m>n+3;
    then m=n+3 or m=n+3+1 by A2,NAT_1:9;
    hence thesis;
  end;
end;

canceled 2;

theorem Th7:
  for f being FinSequence st rng f={x,y} & len f=2 holds f.1=x & f.
  2=y or f.1=y & f.2=x
proof
  let f be FinSequence;
  assume that
A1: rng f={x,y} and
A2: len f=2;
  2 in Seg len f by A2,FINSEQ_1:3;
  then 2 in dom f by FINSEQ_1:def 3;
  then
A3: f.2 in rng f by FUNCT_1:def 5;
A4: now
    x in rng f by A1,TARSKI:def 2;
    then consider z such that
A5: z in dom f and
A6: x=f.z by FUNCT_1:def 5;
    reconsider nz=z as Element of NAT by A5;
    z in Seg len f by A5,FINSEQ_1:def 3;
    then
A7: 1<=nz & nz<=len f by FINSEQ_1:3;
    assume that
A8: f.1=y and
A9: f.2=y;
    per cases by A2,A7,NAT_1:9;
    suppose
      nz=1;
      hence f.1=x & f.2=y by A9,A6;
    end;
    suppose
      nz=1+1;
      hence f.1=x & f.2=y by A8,A9,A6;
    end;
  end;
A10: now
    y in rng f by A1,TARSKI:def 2;
    then consider z such that
A11: z in dom f and
A12: y=f.z by FUNCT_1:def 5;
    reconsider nz=z as Element of NAT by A11;
    z in Seg len f by A11,FINSEQ_1:def 3;
    then
A13: 1<=nz & nz<=len f by FINSEQ_1:3;
    assume that
A14: f.1=x and
A15: f.2=x;
    per cases by A2,A13,NAT_1:9;
    suppose
      nz=1;
      hence f.1=x & f.2=y by A14,A15,A12;
    end;
    suppose
      nz=1+1;
      hence f.1=x & f.2=y by A14,A12;
    end;
  end;
  1 in Seg len f by A2,FINSEQ_1:3;
  then 1 in dom f by FINSEQ_1:def 3;
  then f.1 in rng f by FUNCT_1:def 5;
  hence thesis by A1,A3,A10,A4,TARSKI:def 2;
end;

theorem Th8:
  for f being increasing FinSequence of REAL st rng f={r,s} & len f
  =2 & r<=s holds f.1=r & f.2=s
proof
  let f be increasing FinSequence of REAL;
  assume that
A1: rng f={r,s} and
A2: len f=2 and
A3: r<=s;
  now
    2 in Seg len f by A2,FINSEQ_1:3;
    then
A4: 2 in dom f by FINSEQ_1:def 3;
    1 in Seg len f by A2,FINSEQ_1:3;
    then
A5: 1 in dom f by FINSEQ_1:def 3;
    assume f.1=s & f.2=r;
    hence thesis by A3,A5,A4,SEQM_3:def 1;
  end;
  hence thesis by A1,A2,Th7;
end;

reserve p,p1,p2,p3,q,q1,q2,q3,q4 for Point of TOP-REAL n;

theorem
  p1+p2-p3=p1-p3+p2 by EUCLID:30;

theorem
  abs(|.q.|)=|.q.| by ABSVALUE:def 1;

theorem Th11:
  abs(|.q1.|- |.q2.|)<=|.q1-q2.|
proof
  per cases;
  suppose
    |.q1.|>=|.q2.|;
    then |.q1.|- |.q2.|>=0 by XREAL_1:50;
    then |.q1.|- |.q2.|=abs(|.q1.|- |.q2.|) by ABSVALUE:def 1;
    hence thesis by TOPRNS_1:33;
  end;
  suppose
A1: |.q1.|<|.q2.|;
A2: |.q2.|- |.q1.|<= |.q2-q1.| by TOPRNS_1:33;
    |.q2.|- |.q1.|>0 by A1,XREAL_1:52;
    then abs(|.q2.|- |.q1.|)<= |.q2-q1.| by A2,ABSVALUE:def 1;
    then abs(|.q2.|- |.q1.|)<= |.q1-q2.| by TOPRNS_1:28;
    hence thesis by UNIFORM1:13;
  end;
end;

theorem Th12:
  |.|[r]|.|=abs(r)
proof
  set p=|[r]|;
  reconsider w=|[r]| as Element of REAL 1 by EUCLID:25;
  sqr w=<*r^2*> by RVSUM_1:81;
  then (|.p.|) =sqrt (r^2) by FINSOP_1:12
    .=abs r by COMPLEX1:158;
  hence thesis;
end;

canceled 2;

theorem Th15:
  for G being non empty TopSpace, P being Subset of G,A being
Subset of G, Q being Subset of G|A st P=Q & P is connected holds Q is connected
proof
  let G be non empty TopSpace,P be Subset of G, A be Subset of G, Q be Subset
  of G|A;
  assume that
A1: P=Q and
A2: P is connected;
A3: G|P is connected by A2,CONNSP_1:def 3;
  Q c= the carrier of G|A;
  then Q c= A by PRE_TOPC:29;
  then G|P=(G|A) |Q by A1,PRE_TOPC:28;
  hence thesis by A3,CONNSP_1:def 3;
end;

definition
  let n be Nat;
  let A be Subset of TOP-REAL n;
  canceled;
  attr A is Bounded means
  :Def2:
  A is bounded Subset of Euclid n;
  correctness;
end;

theorem Th16:
  for A,B being Subset of TOP-REAL n st B is Bounded & A c= B
  holds A is Bounded
proof
  let A,B be Subset of TOP-REAL n;
  assume that
A1: B is Bounded and
A2: A c= B;
  reconsider C2=A as Subset of Euclid n by TOPREAL3:13;
  reconsider C=B as bounded Subset of Euclid n by A1,Def2;
  A c= C by A2;
  then C2 is bounded by TBSP_1:21;
  hence thesis by Def2;
end;

definition
  let n be Nat;
  let A, B be Subset of TOP-REAL n;
  pred B is_inside_component_of A means
  :Def3:
  B is_a_component_of A` & B is Bounded;
end;

registration
  let M be non empty MetrStruct;
  cluster bounded Subset of M;
  existence
  proof
    take {}M, 1;
    thus thesis;
  end;
end;

theorem Th17:
  for A being Subset of TOP-REAL n, B being Subset of TOP-REAL n
  holds B is_inside_component_of A iff ex C being Subset of
  ((TOP-REAL n) | (A`))
  st C=B & C is a_component & C is bounded Subset of Euclid n
proof
  let A be Subset of TOP-REAL n, B be Subset of TOP-REAL n;
A1: B is_a_component_of A` iff ex C being Subset of (TOP-REAL n) | (A`) st C=B
  & C is a_component by CONNSP_1:def 6;
A2: B is_inside_component_of A iff B is_a_component_of A` & B is Bounded by
Def3;
  thus B is_inside_component_of A implies ex C being Subset of ((TOP-REAL n) |
(A`)) st C=B & C is a_component & C is bounded Subset of Euclid n
  proof
    assume
A3: B is_inside_component_of A;
    then B is bounded Subset of Euclid n by A2,Def2;
    hence thesis by A1,A3,Def3;
  end;
  given C being Subset of ((TOP-REAL n) | (A`)) such that
A4: C=B & C is a_component & C is bounded Subset
  of Euclid n;
  B is Bounded & B is_a_component_of A` by A4,Def2,CONNSP_1:def 6;
  hence thesis by Def3;
end;

definition
  let n be Nat;
  let A, B be Subset of TOP-REAL n;
  pred B is_outside_component_of A means
  :Def4:
  B is_a_component_of A` & B is not Bounded;
end;

theorem Th18:
  for A being Subset of TOP-REAL n, B being Subset of TOP-REAL n
  holds B is_outside_component_of A iff
  ex C being Subset of ((TOP-REAL n) | (A`))
  st C=B & C is a_component &
  C is not bounded Subset of Euclid n
proof
  let A be Subset of TOP-REAL n, B be Subset of TOP-REAL n;
A1: B is_a_component_of A` iff ex C being Subset of (TOP-REAL n) | (A`) st C=B
  & C is a_component by CONNSP_1:def 6;
A2: B is_outside_component_of A iff B is_a_component_of A` & not B is
  Bounded by Def4;
  thus B is_outside_component_of A implies ex C being Subset of ((TOP-REAL n) |
  (A`)) st C=B & C is a_component & C is not bounded
  Subset of Euclid n
  proof
    reconsider D2=B as Subset of Euclid n by TOPREAL3:13;
    assume
A3: B is_outside_component_of A;
    then consider C being Subset of (TOP-REAL n) | (A`) such that
A4: C=B and
A5: C is a_component by A1,Def4;
    now
      assume for D being Subset of Euclid n st D=C holds D is bounded;
      then D2 is bounded by A4;
      hence contradiction by A2,A3,Def2;
    end;
    hence thesis by A4,A5;
  end;
  given C being Subset of ((TOP-REAL n) | (A`)) such that
A6: C=B & C is a_component & C is not bounded
  Subset of Euclid n;
  ( not B is Bounded)& B is_a_component_of A` by A6,Def2,CONNSP_1:def 6;
  hence thesis by Def4;
end;

theorem
  for A,B being Subset of TOP-REAL n st B is_inside_component_of A holds
  B c= A`
proof
  let A,B be Subset of TOP-REAL n;
  assume B is_inside_component_of A;
  then B is_a_component_of A` by Def3;
  hence thesis by SPRECT_1:7;
end;

theorem
  for A,B being Subset of TOP-REAL n st B is_outside_component_of A
  holds B c= A`
proof
  let A,B be Subset of TOP-REAL n;
  assume B is_outside_component_of A;
  then B is_a_component_of A` by Def4;
  hence thesis by SPRECT_1:7;
end;

definition
  let n be Nat;
  let A be Subset of TOP-REAL n;
  func BDD A -> Subset of TOP-REAL n equals
  union{B where B is Subset of
  TOP-REAL n: B is_inside_component_of A};
  correctness
  proof
    union{B where B is Subset of TOP-REAL n: B is_inside_component_of A}
    c= the carrier of TOP-REAL n
    proof
      let x;
      assume x in union{B where B is Subset of TOP-REAL n: B
      is_inside_component_of A};
      then consider y being set such that
A1:   x in y and
A2:   y in {B where B is Subset of TOP-REAL n: B
      is_inside_component_of A} by TARSKI:def 4;
      ex B being Subset of TOP-REAL n st y=B & B is_inside_component_of A
      by A2;
      hence thesis by A1;
    end;
    hence thesis;
  end;
end;

definition
  let n be Nat;
  let A be Subset of TOP-REAL n;
  func UBD A -> Subset of TOP-REAL n equals
  union{B where B is Subset of
  TOP-REAL n: B is_outside_component_of A};
  correctness
  proof
    union{B where B is Subset of TOP-REAL n: B is_outside_component_of A}
    c= the carrier of TOP-REAL n
    proof
      let x;
      assume x in union{B where B is Subset of TOP-REAL n: B
      is_outside_component_of A};
      then consider y being set such that
A1:   x in y and
A2:   y in {B where B is Subset of TOP-REAL n: B
      is_outside_component_of A } by TARSKI:def 4;
      ex B being Subset of TOP-REAL n st y=B & B is_outside_component_of A
      by A2;
      hence thesis by A1;
    end;
    hence thesis;
  end;
end;

theorem Th21:
  [#](TOP-REAL n) is convex
proof
  let w1,w2 be Point of TOP-REAL n;
  thus thesis;
end;

theorem
  [#](TOP-REAL n) is connected by Th21,JORDAN1:9;

registration
  let n;
  cluster [#](TOP-REAL n) -> connected;
  coherence by Th21,JORDAN1:9;
end;

registration
  let n;
  cluster [#](TOP-REAL n) -> a_component;
coherence
proof
  set A=[#](TOP-REAL n);
  for B being Subset of TOP-REAL n st B is connected holds A c= B implies
  A = B by XBOOLE_0:def 10;
  hence thesis by CONNSP_1:def 5;
end;
end;

theorem Th23:
  [#](TOP-REAL n) is a_component;

theorem Th24:
  for A being Subset of TOP-REAL n holds BDD A is
  a_union_of_components of (TOP-REAL n) | A`
proof
  let A be Subset of TOP-REAL n;
  {B where B is Subset of TOP-REAL n: B is_inside_component_of A} c= bool
  (the carrier of ((TOP-REAL n) | A`))
  proof
    let x;
    assume
    x in {B where B is Subset of TOP-REAL n: B is_inside_component_of A};
    then consider B being Subset of TOP-REAL n such that
A1: x=B and
A2: B is_inside_component_of A;
    ex C being Subset of ((TOP-REAL n) | (A`)) st C=B & C is a_component &
    C is bounded Subset of Euclid n by A2,Th17;
    hence thesis by A1;
  end;
  then reconsider
  F0={B where B is Subset of TOP-REAL n: B is_inside_component_of A
  } as Subset-Family of the carrier of ((TOP-REAL n) | A`);
  reconsider F0 as Subset-Family of (TOP-REAL n) | A`;
A3: for B0 being Subset of ((TOP-REAL n) | A`) st B0 in F0 holds B0
  is a_component
  proof
    let B0 be Subset of ((TOP-REAL n) | A`);
    assume B0 in F0;
    then consider B being Subset of TOP-REAL n such that
A4: B=B0 and
A5: B is_inside_component_of A;
    ex C being Subset of ((TOP-REAL n) | (A`)) st C=B & C is a_component
    & C is bounded Subset of Euclid n by A5,Th17;
    hence thesis by A4;
  end;
  BDD A=union F0;
  hence thesis by A3,CONNSP_3:def 2;
end;

theorem Th25:
  for A being Subset of TOP-REAL n holds UBD A is
  a_union_of_components of (TOP-REAL n) | A`
proof
  let A be Subset of TOP-REAL n;
  {B where B is Subset of TOP-REAL n: B is_outside_component_of A} c= bool
  (the carrier of ((TOP-REAL n) | A`))
  proof
    let x;
    assume
    x in {B where B is Subset of TOP-REAL n: B is_outside_component_of A};
    then consider B being Subset of TOP-REAL n such that
A1: x=B and
A2: B is_outside_component_of A;
    ex C being Subset of ((TOP-REAL n) | (A`)) st C=B & C is a_component
    & C is not bounded Subset of Euclid n by A2,Th18;
    hence thesis by A1;
  end;
  then reconsider
  F0={B where B is Subset of TOP-REAL n: B is_outside_component_of
  A} as Subset-Family of the carrier of ((TOP-REAL n) | A`);
  reconsider F0 as Subset-Family of ((TOP-REAL n) | A`);
A3: for B0 being Subset of ((TOP-REAL n) | A`) st B0 in F0 holds B0
  is a_component
  proof
    let B0 be Subset of ((TOP-REAL n) | A`);
    assume B0 in F0;
    then consider B being Subset of TOP-REAL n such that
A4: B=B0 and
A5: B is_outside_component_of A;
    ex C being Subset of ((TOP-REAL n) | (A`)) st C=B & C is a_component
    & C is not bounded Subset of Euclid n by A5,Th18;
    hence thesis by A4;
  end;
  UBD A=union F0;
  hence thesis by A3,CONNSP_3:def 2;
end;

theorem Th26:
  for A being Subset of TOP-REAL n, B being Subset of TOP-REAL n
  st B is_inside_component_of A holds B c= BDD A
proof
  let A be Subset of TOP-REAL n, B be Subset of TOP-REAL n;
  assume B is_inside_component_of A;
  then
A1: B in {B2 where B2 is Subset of TOP-REAL n: B2 is_inside_component_of A};
  let x;
  assume x in B;
  hence thesis by A1,TARSKI:def 4;
end;

theorem Th27:
  for A being Subset of TOP-REAL n, B being Subset of TOP-REAL n
  st B is_outside_component_of A holds B c= UBD A
proof
  let A be Subset of TOP-REAL n, B be Subset of TOP-REAL n;
  assume B is_outside_component_of A;
  then
A1: B in {B2 where B2 is Subset of TOP-REAL n: B2 is_outside_component_of A};
  let x;
  assume x in B;
  hence thesis by A1,TARSKI:def 4;
end;

theorem Th28:
  for A being Subset of TOP-REAL n holds BDD A misses UBD A
proof
  let A be Subset of TOP-REAL n;
  consider x being Element of (BDD A) /\ (UBD A);
  assume
A1: (BDD A) /\ (UBD A) <>{};
  then
  x in union{B where B is Subset of TOP-REAL n: B is_inside_component_of A
  } by XBOOLE_0:def 4;
  then consider y being set such that
A2: x in y and
A3: y in {B where B is Subset of TOP-REAL n: B is_inside_component_of A}
  by TARSKI:def 4;
  x in union{B2 where B2 is Subset of TOP-REAL n: B2
  is_outside_component_of A} by A1,XBOOLE_0:def 4;
  then consider y2 being set such that
A4: x in y2 and
A5: y2 in {B2 where B2 is Subset of TOP-REAL n: B2
  is_outside_component_of A} by TARSKI:def 4;
  consider B being Subset of TOP-REAL n such that
A6: y=B and
A7: B is_inside_component_of A by A3;
  consider B2 being Subset of TOP-REAL n such that
A8: y2=B2 and
A9: B2 is_outside_component_of A by A5;
  consider C being Subset of ((TOP-REAL n) | (A`)) such that
A10: C=B and
A11: C is a_component & C is bounded Subset of
  Euclid n by A7,Th17;
  consider C2 being Subset of ((TOP-REAL n) | (A`)) such that
A12: C2=B2 and
A13: C2 is a_component & C2 is not bounded Subset
  of Euclid n by A9,Th18;
  C /\ C2<>{}((TOP-REAL n) | (A`)) by A2,A6,A10,A4,A8,A12,XBOOLE_0:def 4;
  then C meets C2 by XBOOLE_0:def 7;
  hence contradiction by A11,A13,CONNSP_1:37;
end;

theorem Th29:
  for A being Subset of TOP-REAL n holds BDD A c= A`
proof
  let A be Subset of TOP-REAL n;
  reconsider D=BDD A as Subset of (TOP-REAL n) | A` by Th24;
  D c= the carrier of ((TOP-REAL n) | A`);
  hence thesis by PRE_TOPC:29;
end;

theorem Th30:
  for A being Subset of TOP-REAL n holds UBD A c= A`
proof
  let A be Subset of TOP-REAL n;
  reconsider D=UBD A as Subset of (TOP-REAL n) | A` by Th25;
  D c= the carrier of ((TOP-REAL n) | A`);
  hence thesis by PRE_TOPC:29;
end;

theorem Th31:
  for A being Subset of TOP-REAL n holds (BDD A) \/ (UBD A) = A`
proof
  let A be Subset of TOP-REAL n;
A1: A` c= (BDD A) \/ (UBD A)
  proof
    let z be set;
    assume
A2: z in A`;
    then reconsider p=z as Element of A`;
    reconsider B=A` as non empty Subset of TOP-REAL n by A2;
    reconsider q=p as Point of (TOP-REAL n) | A` by PRE_TOPC:29;
    Component_of q is Subset of [#]((TOP-REAL n) | A`);
    then Component_of q is Subset of A` by PRE_TOPC:def 10;
    then reconsider G=Component_of q as Subset of TOP-REAL n by XBOOLE_1:1;
A3: (TOP-REAL n) | B is non empty;
    then
A4: q in G by CONNSP_1:40;
    Component_of q is a_component by A3,CONNSP_1:43;
    then
A5: G is_a_component_of A` by CONNSP_1:def 6;
    per cases;
    suppose
      G is Bounded;
      then G is_inside_component_of A by A5,Def3;
      then G c= BDD A by Th26;
      hence thesis by A4,XBOOLE_0:def 3;
    end;
    suppose
      not G is Bounded;
      then G is_outside_component_of A by A5,Def4;
      then G c= UBD A by Th27;
      hence thesis by A4,XBOOLE_0:def 3;
    end;
  end;
  (BDD A) c= A` & (UBD A) c= A` by Th29,Th30;
  then (BDD A) \/ (UBD A) c= A` by XBOOLE_1:8;
  hence thesis by A1,XBOOLE_0:def 10;
end;

reserve u for Point of Euclid n;

canceled;

theorem Th33:
  for P being Subset of TOP-REAL n st P=REAL n holds P is connected
proof
  let P be Subset of TOP-REAL n;
  assume
A1: P=(REAL n);
  for p1,p2 being Point of TOP-REAL n st p1 in P & p2 in P holds LSeg(p1,
  p2) c= P
  proof
    let p1,p2 be Point of TOP-REAL n;
    assume that
    p1 in P and
    p2 in P;
    the carrier of TOP-REAL n=REAL n by EUCLID:25;
    hence thesis by A1;
  end;
  then P is convex by JORDAN1:def 1;
  hence thesis;
end;

definition
  let n be Nat;
  func 1*n -> FinSequence of REAL equals
  n |-> (1 qua Real);
  coherence;
end;

definition
  let n be Nat;
  redefine func 1*n -> Element of REAL n;
  coherence
  proof
    reconsider f=1*n as FinSequence;
    len f =n by FINSEQ_1:def 18;
    hence thesis by FINSEQ_2:110;
  end;
end;

definition
  let n be Nat;
  func 1.REAL n -> Point of TOP-REAL n equals
  1*n;
  coherence by EUCLID:25;
end;

theorem
  abs 1*n = n |-> (1 qua Real)
proof
  thus abs 1*n = n |-> absreal.(1 qua Real) by FINSEQOP:17
    .= n |-> abs(1 qua Real) by EUCLID:def 2
    .= n |-> 1 by ABSVALUE:def 1;
end;

theorem Th35:
  |.1*n.| = sqrt n
proof
  reconsider f= n |-> 1^2 as FinSequence of REAL;
  thus |.1*n .| = sqrt Sum f by RVSUM_1:82
    .= sqrt (n*1) by RVSUM_1:110
    .= sqrt n;
end;

canceled;

theorem
  |. (1.REAL n) .| = sqrt n by Th35;

theorem Th38:
  1<=n implies 1<=|. (1.REAL n) .|
proof
  assume
A1: 1<=n;
  |.1.REAL n.|=sqrt n by Th35;
  hence thesis by A1,SQUARE_1:83,94;
end;

theorem Th39:
  for W being Subset of Euclid n st n>=1 & W=REAL n holds W is not bounded
proof
  let W be Subset of Euclid n;
  assume that
A1: n>=1 and
A2: W=(REAL n);
  reconsider y0=0.TOP-REAL n as Point of Euclid n by EUCLID:71;
  assume W is bounded;
  then consider r being Real such that
A3: 0<r and
A4: for x,y being Point of Euclid n st x in W & y in W holds dist(x,y)<=
  r by TBSP_1:def 9;
  reconsider x0=(r+1)*(1.REAL n) as Point of Euclid n by TOPREAL3:13;
  dist(x0,y0)<=r by A2,A4;
  then |.(r+1)*(1.REAL n) -0.TOP-REAL n.|<=r by JGRAPH_1:45;
  then |.(r+1)*(1.REAL n).|<=r by RLVECT_1:26;
  then abs(r+1)*|.(1.REAL n).|<=r by TOPRNS_1:8;
  then abs(r+1)*(sqrt n)<=r by Th35;
  then
A5: (r+1)*(sqrt n)<=r by A3,ABSVALUE:def 1;
  (sqrt 1)<=(sqrt n) by A1,SQUARE_1:94;
  then (r+1)*1<=(r+1)*(sqrt n) by A3,SQUARE_1:83,XREAL_1:66;
  then (r+1)*1<=r by A5,XXREAL_0:2;
  then (r+1)-r<=r-r by XREAL_1:11;
  then 1<=0;
  hence contradiction;
end;

theorem Th40:
  for A being Subset of TOP-REAL n holds A is Bounded iff ex r
  being Real st for q being Point of TOP-REAL n st q in A holds |.q.|<r
proof
  let A be Subset of TOP-REAL n;
  reconsider C=A as Subset of Euclid n by TOPREAL3:13;
  hereby
    assume A is Bounded;
    then reconsider C=A as bounded Subset of Euclid n by Def2;
    per cases;
    suppose
A1:   C<>{};
      reconsider o=0.TOP-REAL n as Point of Euclid n by EUCLID:71;
      consider x0 being Element of C;
      x0 in C by A1;
      then reconsider x0 as Point of Euclid n;
      consider r being Real such that
      0<r and
A2:   for x,y being Point of (Euclid n) st x in C & y in C holds dist(
      x,y) <= r by TBSP_1:def 9;
      set R0=r+dist(o,x0)+1;
      for q being Point of TOP-REAL n st q in A holds |.q.|<R0
      proof
        let q1 be Point of TOP-REAL n;
        reconsider z=q1 as Point of Euclid n by TOPREAL3:13;
        |.q1-(0.TOP-REAL n).|=dist(o,z) by JGRAPH_1:45;
        then
A3:     |.q1.|=dist(o,z) by RLVECT_1:26;
        assume q1 in A;
        then dist(x0,z)<=r by A2;
        then
        dist(o,z)<=dist(o,x0)+dist(x0,z) & dist(o,x0)+dist(x0,z)<=dist(o,
        x0)+r by METRIC_1:4,XREAL_1:8;
        then
A4:     dist(o,z)<=dist(o,x0)+r by XXREAL_0:2;
        r+dist(o,x0)<r+dist(o,x0)+1 by XREAL_1:31;
        hence thesis by A3,A4,XXREAL_0:2;
      end;
      hence
      ex r2 being Real st for q being Point of TOP-REAL n st q in A holds
      |.q.|<r2;
    end;
    suppose
      C={};
      then for q being Point of TOP-REAL n st q in A holds |.q.|<1;
      hence
      ex r2 being Real st for q being Point of TOP-REAL n st q in A holds
      |.q.|<r2;
    end;
  end;
  given r being Real such that
A5: for q being Point of TOP-REAL n st q in A holds |.q.|<r;
  now
    per cases;
    suppose
A6:   C<>{};
      consider x0 being Element of C;
      x0 in C by A6;
      then reconsider x0 as Point of Euclid n;
      reconsider q0=x0 as Point of TOP-REAL n by TOPREAL3:13;
      reconsider o=0.TOP-REAL n as Point of Euclid n by EUCLID:71;
      set R0=r+r;
A7:   for x,y being Point of (Euclid n) st x in C & y in C holds dist(x,y
      ) <= R0
      proof
        let x,y be Point of (Euclid n);
        assume that
A8:     x in C and
A9:     y in C;
        reconsider q2=y as Point of TOP-REAL n by A9;
        dist(o,y)=|.q2-(0.TOP-REAL n) .| by JGRAPH_1:45
          .=|.q2.| by RLVECT_1:26;
        then
A10:    dist(o,y) <r by A5,A9;
        reconsider q1=x as Point of TOP-REAL n by A8;
        dist(x,o)=|.q1-0.TOP-REAL n.| by JGRAPH_1:45
          .=|.q1.| by RLVECT_1:26;
        then dist(x,o) <r by A5,A8;
        then dist(x,y)<=dist(x,o)+dist(o,y) & dist(x,o)+dist(o,y)<=r+r by A10,
METRIC_1:4,XREAL_1:9;
        hence thesis by XXREAL_0:2;
      end;
      |.q0.|<r by A5,A6;
      hence C is bounded by A7,TBSP_1:def 9;
    end;
    suppose
      C={};
      hence C is bounded;
    end;
  end;
  hence thesis by Def2;
end;

theorem Th41:
  n>=1 implies not [#](TOP-REAL n) is Bounded
proof
  assume
A1: n>=1;
  assume [#](TOP-REAL n) is Bounded;
  then reconsider C=[#](TOP-REAL n) as bounded Subset of Euclid n by Def2;
  C=REAL n by EUCLID:25;
  hence contradiction by A1,Th39;
end;

theorem Th42:
  n>=1 implies UBD {}(TOP-REAL n)=REAL n
proof
  set A={}(TOP-REAL n);
A1: (TOP-REAL n) | [#](TOP-REAL n)=the TopStruct of TOP-REAL n by TSEP_1:100;
  assume
A2: n>=1;
A3: now
    reconsider D1=[#]((TOP-REAL n) | A`) as Subset of Euclid n
    by A1,TOPREAL3:13;
    assume for D being Subset of Euclid n st D=[#]((TOP-REAL n) | A`) holds D
    is bounded;
    then D1 is bounded;
    then [#](TOP-REAL n) is Bounded by A1,Def2;
    hence contradiction by A2,Th41;
  end;
  [#]((TOP-REAL n) | A`) is a_component by A1,CONNSP_1:48;
  then [#](TOP-REAL n) is_outside_component_of {}(TOP-REAL n) by A1,A3,Th18;
  then
A4: [#](TOP-REAL n) in {B2 where B2 is Subset of TOP-REAL n: B2
  is_outside_component_of {}(TOP-REAL n)};
  UBD {}(TOP-REAL n) c= the carrier of TOP-REAL n;
  hence UBD {}(TOP-REAL n) c= REAL n by EUCLID:25;
  let x be set;
  assume x in REAL n;
  then x in [#](TOP-REAL n) by EUCLID:25;
  hence thesis by A4,TARSKI:def 4;
end;

theorem Th43:
  for w1,w2,w3 being Point of TOP-REAL n, P being non empty Subset
of TOP-REAL n, h1,h2 being Function of I[01],(TOP-REAL n) |P st
h1 is continuous
  & w1=h1.0 & w2=h1.1 & h2 is continuous & w2=h2.0 & w3=h2.1 holds ex h3 being
  Function of I[01],(TOP-REAL n) |P st h3 is continuous & w1=h3.0 & w3=h3.1
proof
  let w1,w2,w3 be Point of TOP-REAL n, P be non empty Subset of TOP-REAL n, h1
  ,h2 be Function of I[01],(TOP-REAL n) |P;
  assume that
A1: h1 is continuous and
A2: w1=h1.0 and
A3: w2=h1.1 and
A4: h2 is continuous and
A5: w2=h2.0 and
A6: w3=h2.1;
  0 in [.0 qua Real,1 qua Real.] & 1 in [.0 qua Real,1 qua Real.] by XXREAL_1:1
;
  then reconsider p1=w1,p2=w2,p3=w3 as Point of (TOP-REAL n) |P by A2,A3,A6,
BORSUK_1:83,FUNCT_2:7;
  p2,p3 are_connected by A4,A5,A6,BORSUK_2:def 1;
  then reconsider P2=h2 as Path of p2,p3 by A4,A5,A6,BORSUK_2:def 2;
  p1,p2 are_connected by A1,A2,A3,BORSUK_2:def 1;
  then reconsider P1=h1 as Path of p1,p2 by A1,A2,A3,BORSUK_2:def 2;
  ex P0 being Path of p1,p3 st P0 is continuous & P0.0=p1 & P0.1=p3 & for
  t being Point of I[01], t9 being Real st t = t9 holds ( 0 <= t9 & t9 <= 1/2
implies P0.t = P1.(2*t9) ) & ( 1/2 <= t9 & t9 <= 1 implies P0.t = P2.(2*t9-1) )
  proof
    1/2 in { r : 0 <= r & r <= 1 };
    then reconsider pol = 1/2 as Point of I[01] by BORSUK_1:83,RCOMP_1:def 1;
    reconsider T1 = Closed-Interval-TSpace (0, 1/2), T2 =
Closed-Interval-TSpace (1/2, 1) as SubSpace of I[01] by TOPMETR:27,TREAL_1:6;
    set e2 = P[01](1/2, 1, (#)(0,1), (0,1)(#));
    set e1 = P[01](0, 1/2, (#)(0,1), (0,1)(#));
    set E1 = P1 * e1;
    set E2 = P2 * e2;
    set f = E1 +* E2;
A7: dom e1 = the carrier of Closed-Interval-TSpace(0,1/2) by FUNCT_2:def 1
      .= [.0,1/2.] by TOPMETR:25;
A8: dom e2 = the carrier of Closed-Interval-TSpace(1/2,1) by FUNCT_2:def 1
      .= [.1/2,1 qua Real.] by TOPMETR:25;
    reconsider gg = E2 as Function of T2, ((TOP-REAL n) |P) by TOPMETR:27;
    reconsider ff = E1 as Function of T1, ((TOP-REAL n) |P) by TOPMETR:27;
A9: for t9 being Real st 1/2 <= t9 & t9 <= 1 holds E2.t9 = P2.(2*t9-1)
    proof
      reconsider r1 = (#)(0,1), r2 = (0,1)(#) as Real by BORSUK_1:def 17,def 18
,TREAL_1:8;
      dom e2 = the carrier of Closed-Interval-TSpace(1/2,1) by FUNCT_2:def 1;
      then
A10:  dom e2 = [.1/2,1 qua Real.] by TOPMETR:25
        .= {r : 1/2 <= r & r <= 1 } by RCOMP_1:def 1;
      let t9 be Real;
      assume 1/2 <= t9 & t9 <= 1;
      then
A11:  t9 in dom e2 by A10;
      then reconsider s = t9 as Point of Closed-Interval-TSpace (1/2,1);
      e2.s = ((r2 - r1)/(1 - 1/2))*t9 + (1 * r1 - (1/2)*r2)/(1 - 1/2) by
TREAL_1:14
        .= 2*t9 - 1 by BORSUK_1:def 17,def 18,TREAL_1:8;
      hence thesis by A11,FUNCT_1:23;
    end;
A12: for t9 being Real st 0 <= t9 & t9 <= 1/2 holds E1.t9 = P1.(2*t9)
    proof
      reconsider r1 = (#)(0,1), r2 = (0,1)(#) as Real by BORSUK_1:def 17,def 18
,TREAL_1:8;
      dom e1 = the carrier of Closed-Interval-TSpace(0,1/2) by FUNCT_2:def 1;
      then
A13:  dom e1 = [.0, 1/2.] by TOPMETR:25
        .= {r : 0 <= r & r <= 1/2 } by RCOMP_1:def 1;
      let t9 be Real;
      assume 0 <= t9 & t9 <= 1/2;
      then
A14:  t9 in dom e1 by A13;
      then reconsider s = t9 as Point of Closed-Interval-TSpace (0, 1/2);
      e1.s = ((r2 - r1)/(1/2 - 0))*t9 + ((1/2)*r1 - 0 * r2)/(1/2 - 0) by
TREAL_1:14
        .= 2*t9 by BORSUK_1:def 17,def 18,TREAL_1:8;
      hence thesis by A14,FUNCT_1:23;
    end;
    then
A15: ff.(1/2) = P2.(2*(1/2)-1) by A3,A5
      .= gg.pol by A9;
    [#] T1 = [.0,1/2.] & [#] T2 = [.1/2,1 qua Real.] by TOPMETR:25;
    then
A16: ([#] T1) \/ ([#] T2) = [#] I[01] & ([#] T1) /\ ([#] T2) = {pol} by
BORSUK_1:83,XXREAL_1:174,418;
    rng f c= rng E1 \/ rng E2 by FUNCT_4:18;
    then
A17: rng f c= the carrier of ((TOP-REAL n) |P) by XBOOLE_1:1;
A18: T1 is compact & T2 is compact by HEINE:11;
    dom P1 = the carrier of I[01] by FUNCT_2:def 1;
    then
A19: rng e1 c= dom P1 by TOPMETR:27;
    dom P2 = the carrier of I[01] &
    rng e2 c= the carrier of Closed-Interval-TSpace(0,1) by FUNCT_2:def 1;
    then
A20: dom E2 = dom e2 by RELAT_1:46,TOPMETR:27;
    not 0 in { r : 1/2 <= r & r <= 1 }
    proof
      assume 0 in { r : 1/2 <= r & r <= 1 };
      then ex rr being Real st rr = 0 & 1/2 <= rr & rr <= 1;
      hence thesis;
    end;
    then not 0 in dom E2 by A8,A20,RCOMP_1:def 1;
    then
A21: f.0 = E1.0 by FUNCT_4:12
      .= P1.(2*0) by A12
      .= p1 by A2;
    dom f = dom E1 \/ dom E2 by FUNCT_4:def 1
      .= [.0,1/2.] \/ [.1/2,1 qua Real.] by A7,A8,A19,A20,RELAT_1:46
      .= the carrier of I[01] by BORSUK_1:83,XXREAL_1:174;
    then reconsider f as Function of I[01], ((TOP-REAL n) |P) by A17,
FUNCT_2:def 1,RELSET_1:11;
    e1 is continuous & e2 is continuous by TREAL_1:15;
    then reconsider
    f as continuous Function of I[01], ((TOP-REAL n) |P) by A1,A4,A15,A16,A18,
COMPTS_1:29,TOPMETR:27;
    1 in { r : 1/2 <= r & r <= 1 };
    then 1 in dom E2 by A8,A20,RCOMP_1:def 1;
    then
A22: f.1 = E2.1 by FUNCT_4:14
      .= P2.(2*1-1) by A9
      .= p3 by A6;
    then p1,p3 are_connected by A21,BORSUK_2:def 1;
    then reconsider f as Path of p1, p3 by A21,A22,BORSUK_2:def 2;
    for t being Point of I[01], t9 being Real st t = t9 holds ( 0 <= t9 &
t9 <= 1/2 implies f.t = P1.(2*t9) ) & ( 1/2 <= t9 & t9 <= 1 implies f.t = P2.(2
    *t9-1) )
    proof
      let t be Point of I[01], t9 be Real;
      assume
A23:  t = t9;
      thus 0 <= t9 & t9 <= 1/2 implies f.t = P1.(2*t9)
      proof
        assume
A24:    0 <= t9 & t9 <= 1/2;
        then t9 in { r : 0 <= r & r <= 1/2 };
        then
A25:    t9 in [.0,1/2.] by RCOMP_1:def 1;
        per cases;
        suppose
A26:      t9 <> 1/2;
          not t9 in dom E2
          proof
            assume t9 in dom E2;
            then t9 in [.0,1/2.] /\ [.1/2,1 qua Real.] by A8,A20,A25,
XBOOLE_0:def 4;
            then t9 in {1/2} by XXREAL_1:418;
            hence thesis by A26,TARSKI:def 1;
          end;
          then f.t = E1.t by A23,FUNCT_4:12
            .= P1.(2*t9) by A12,A23,A24;
          hence thesis;
        end;
        suppose
A27:      t9 = 1/2;
          1/2 in { r : 1/2 <= r & r <= 1 };
          then 1/2 in [.1/2, 1 qua Real.] by RCOMP_1:def 1;
          then 1/2 in the carrier of Closed-Interval-TSpace(1/2,1) by
TOPMETR:25;
          then t in dom E2 by A23,A27,FUNCT_2:def 1,TOPMETR:27;
          then f.t = E2.(1/2) by A23,A27,FUNCT_4:14
            .= P1.(2*t9) by A12,A15,A27;
          hence thesis;
        end;
      end;
      thus 1/2 <= t9 & t9 <= 1 implies f.t = P2.(2*t9-1)
      proof
        assume
A28:    1/2 <= t9 & t9 <= 1;
        then t9 in { r : 1/2 <= r & r <= 1 };
        then t9 in [.1/2,1 qua Real.] by RCOMP_1:def 1;
        then f.t = E2.t by A8,A20,A23,FUNCT_4:14
          .= P2.(2*t9-1) by A9,A23,A28;
        hence thesis;
      end;
    end;
    hence thesis by A21,A22;
  end;
  hence thesis;
end;

theorem Th44:
  for P being Subset of TOP-REAL n, w1,w2,w3 being Point of
TOP-REAL n st w1 in P & w2 in P & w3 in P & LSeg(w1,w2) c= P & LSeg(w2,w3) c= P
ex h being Function of I[01],(TOP-REAL n) |P st h is continuous & w1=h.0 &
w3=h.1
proof
  let P be Subset of TOP-REAL n, w1,w2,w3 be Point of TOP-REAL n;
  assume that
A1: w1 in P and
A2: w2 in P and
A3: w3 in P and
A4: LSeg(w1,w2) c= P and
A5: LSeg(w2,w3) c= P;
  reconsider Y = P as non empty Subset of TOP-REAL n by A1;
  per cases;
  suppose
A6: w1<>w2;
    then LSeg(w1,w2) is_an_arc_of w1,w2 by TOPREAL1:15;
    then consider
    f being Function of I[01], (TOP-REAL n) | LSeg(w1,w2) such that
A7: f is being_homeomorphism and
A8: f.0 = w1 and
A9: f.1 = w2 by TOPREAL1:def 2;
A10: rng f = [#]((TOP-REAL n) | LSeg(w1,w2)) by A7,TOPS_2:def 5;
    then
A11: rng f c= P by A4,PRE_TOPC:def 10;
    then [#]((TOP-REAL n) | LSeg(w1,w2)) c= [#]((TOP-REAL n) |P) by A10,
PRE_TOPC:def 10;
    then
A12: (TOP-REAL n) | LSeg(w1,w2) is SubSpace of (TOP-REAL n) |P by TOPMETR:4;
    dom f= ([.0 qua Real ,1 qua Real.]) by BORSUK_1:83,FUNCT_2:def 1;
    then reconsider g=f as Function of ([.0 qua Real,1 qua Real.]),P by A11,
FUNCT_2:4;
    reconsider gt=g as Function of I[01],(TOP-REAL n) | Y by BORSUK_1:83
,PRE_TOPC:29;
A13: f is continuous by A7,TOPS_2:def 5;
    now
      per cases;
      suppose
        w2<>w3;
        then LSeg(w2,w3) is_an_arc_of w2,w3 by TOPREAL1:15;
        then consider
        f2 being Function of I[01], (TOP-REAL n) | LSeg(w2,w3) such
        that
A14:    f2 is being_homeomorphism and
A15:    f2.0 = w2 & f2.1 = w3 by TOPREAL1:def 2;
A16:    rng f2 = [#]((TOP-REAL n) | LSeg(w2,w3)) by A14,TOPS_2:def 5;
        then
A17:    rng f2 c= P by A5,PRE_TOPC:def 10;
        then [#]((TOP-REAL n) | LSeg(w2,w3)) c= [#]((TOP-REAL n) |P) by A16,
PRE_TOPC:def 10;
        then
A18:    (TOP-REAL n) | LSeg(w2,w3) is SubSpace of (TOP-REAL n) |P by TOPMETR:4;
        [#]((TOP-REAL n) |P)=P by PRE_TOPC:def 10;
        then reconsider
        w19=w1,w29=w2,w39=w3 as Point of (TOP-REAL n) |P by A1,A2,A3;
A19:    gt is continuous & w29=gt.1 by A9,A13,A12,PRE_TOPC:56;
        dom f2=[.0 qua Real,1 qua Real.] by BORSUK_1:83,FUNCT_2:def 1;
        then reconsider
        g2=f2 as Function of ([.0 qua Real,1 qua Real.]),P by A17,FUNCT_2:4;
        reconsider gt2=g2 as Function of I[01],(TOP-REAL n) | Y by BORSUK_1:83
,PRE_TOPC:29;
        f2 is continuous by A14,TOPS_2:def 5;
        then gt2 is continuous by A18,PRE_TOPC:56;
        then ex h being Function of I[01],(TOP-REAL n) | Y st h is continuous &
w19=h.0 & w39=h.1 & rng h c= (rng gt) \/ (rng gt2) by A8,A15,A19,BORSUK_2:16;
        hence thesis;
      end;
      suppose
A20:    w2=w3;
        then LSeg(w1,w3) is_an_arc_of w1,w3 by A6,TOPREAL1:15;
        then consider
        f being Function of I[01], (TOP-REAL n) | LSeg(w1,w3) such that
A21:    f is being_homeomorphism and
A22:    f.0 = w1 & f.1 = w3 by TOPREAL1:def 2;
A23:    rng f = [#]((TOP-REAL n) | LSeg(w1,w3)) by A21,TOPS_2:def 5;
        then
A24:    rng f c= P by A4,A20,PRE_TOPC:def 10;
        then [#]((TOP-REAL n) | LSeg(w1,w3)) c= [#]((TOP-REAL n) |P) by A23,
PRE_TOPC:def 10;
        then
A25:    (TOP-REAL n) | LSeg(w1,w3) is SubSpace of (TOP-REAL n) |P by TOPMETR:4;
        dom f=[.0 qua Real ,1 qua Real.] by BORSUK_1:83,FUNCT_2:def 1;
        then reconsider
        g=f as Function of ([.0 qua Real,1 qua Real.]),P by A24,FUNCT_2:4;
        reconsider gt=g as Function of I[01],(TOP-REAL n) | Y by BORSUK_1:83
,PRE_TOPC:29;
        f is continuous by A21,TOPS_2:def 5;
        then gt is continuous by A25,PRE_TOPC:56;
        hence thesis by A22;
      end;
    end;
    hence thesis;
  end;
  suppose
A26: w1=w2;
    now
      per cases;
      case
        w2<>w3;
        then LSeg(w1,w3) is_an_arc_of w1,w3 by A26,TOPREAL1:15;
        then consider
        f being Function of I[01], (TOP-REAL n) | LSeg(w1,w3) such that
A27:    f is being_homeomorphism and
A28:    f.0 = w1 & f.1 = w3 by TOPREAL1:def 2;
A29:    rng f = [#]((TOP-REAL n) | LSeg(w1,w3)) by A27,TOPS_2:def 5;
        then
A30:    rng f c= P by A5,A26,PRE_TOPC:def 10;
        then [#]((TOP-REAL n) | LSeg(w1,w3)) c= [#]((TOP-REAL n) |P) by A29,
PRE_TOPC:def 10;
        then
A31:    (TOP-REAL n) | LSeg(w1,w3) is SubSpace of (TOP-REAL n) |P by TOPMETR:4;
        dom f=[.0 qua Real,1 qua Real.] by BORSUK_1:83,FUNCT_2:def 1;
        then reconsider g=f as Function of [.0 qua Real,1 qua Real.],P by A30,
FUNCT_2:4;
        reconsider gt=g as Function of I[01],(TOP-REAL n) | Y by BORSUK_1:83
,PRE_TOPC:29;
        f is continuous by A27,TOPS_2:def 5;
        then gt is continuous by A31,PRE_TOPC:56;
        hence thesis by A28;
      end;
      case
A32:    w2=w3;
        [#]((TOP-REAL n) |P)=P by PRE_TOPC:def 10;
        then reconsider w19=w1,w39=w3 as Point of (TOP-REAL n) |P by A1,A3;
        ex f be Function of I[01], (TOP-REAL n) | Y st f is continuous & f.
        0 = w19 & f.1 = w39 by A26,A32,BORSUK_2:4;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
end;

theorem Th45:
  for P being Subset of TOP-REAL n, w1,w2,w3,w4 being Point of
TOP-REAL n st w1 in P & w2 in P & w3 in P & w4 in P & LSeg(w1,w2) c= P & LSeg(
w2,w3) c= P & LSeg(w3,w4) c= P ex h being Function of I[01],(TOP-REAL n) |P
st h is continuous & w1=h.0 & w4=h.1
proof
  let P be Subset of TOP-REAL n, w1,w2,w3,w4 be Point of TOP-REAL n;
  assume that
A1: w1 in P and
A2: w2 in P and
A3: w3 in P and
A4: w4 in P and
A5: LSeg(w1,w2) c= P & LSeg(w2,w3) c= P and
A6: LSeg(w3,w4) c= P;
  reconsider Y = P as non empty Subset of TOP-REAL n by A1;
  consider h2 being Function of I[01],(TOP-REAL n) |P such that
A7: h2 is continuous & w1=h2.0 and
A8: w3=h2.1 by A1,A2,A3,A5,Th44;
  per cases;
  suppose
    w3<>w4;
    then LSeg(w3,w4) is_an_arc_of w3,w4 by TOPREAL1:15;
    then consider
    f being Function of I[01], (TOP-REAL n) | LSeg(w3,w4) such that
A9: f is being_homeomorphism and
A10: f.0 = w3 & f.1 = w4 by TOPREAL1:def 2;
A11: rng f = [#]((TOP-REAL n) | LSeg(w3,w4)) by A9,TOPS_2:def 5;
    then
A12: rng f c= P by A6,PRE_TOPC:def 10;
    then [#]((TOP-REAL n) | LSeg(w3,w4)) c= [#]((TOP-REAL n) |P) by A11,
PRE_TOPC:def 10;
    then
A13: (TOP-REAL n) | LSeg(w3,w4) is SubSpace of (TOP-REAL n) |P by TOPMETR:4;
    [#]((TOP-REAL n) |P)=P by PRE_TOPC:def 10;
    then reconsider
    w19=w1,w39=w3,w49=w4 as Point of (TOP-REAL n) |P by A1,A3,A4;
A14: w39=h2.1 by A8;
    dom f=[.0 qua Real,1 qua Real.] by BORSUK_1:83,FUNCT_2:def 1;
    then reconsider g=f as Function of [.0 qua Real,1 qua Real.],P by A12,
FUNCT_2:4;
    reconsider gt=g as Function of I[01],(TOP-REAL n) | Y by BORSUK_1:83
,PRE_TOPC:29;
    f is continuous by A9,TOPS_2:def 5;
    then gt is continuous by A13,PRE_TOPC:56;
    then
    ex h being Function of I[01],(TOP-REAL n) | Y st h is continuous & w19=
    h.0 & w49=h.1 & rng h c= (rng h2) \/ (rng gt) by A7,A10,A14,BORSUK_2:16;
    hence thesis;
  end;
  suppose
    w3=w4;
    hence thesis by A7,A8;
  end;
end;

theorem Th46:
  for P being Subset of TOP-REAL n, w1,w2,w3,w4,w5,w6,w7 being
Point of TOP-REAL n st w1 in P & w2 in P & w3 in P & w4 in P & w5 in P & w6 in
P & w7 in P & LSeg(w1,w2) c= P & LSeg(w2,w3) c= P & LSeg(w3,w4) c= P & LSeg(w4,
  w5) c= P & LSeg(w5,w6) c= P & LSeg(w6,w7) c= P ex h being Function of I[01],(
  TOP-REAL n) |P st h is continuous & w1=h.0 & w7=h.1
proof
  let P be Subset of TOP-REAL n, w1,w2,w3,w4,w5,w6,w7 be Point of TOP-REAL n;
  assume that
A1: w1 in P and
A2: w2 in P & w3 in P & w4 in P & w5 in P & w6 in P & w7 in P & LSeg(w1,
w2) c= P & LSeg(w2,w3) c= P & LSeg(w3,w4) c= P & LSeg(w4,w5) c= P & LSeg(w5, w6
  ) c= P & LSeg(w6,w7) c= P;
  ( ex h2 being Function of I[01],(TOP-REAL n) |P st h2 is continuous & w1=
  h2.0 & w4=h2.1)& ex h4 being Function of I[01],(TOP-REAL n) |P st h4 is
  continuous & w4=h4.0 & w7=h4.1 by A1,A2,Th45;
  hence thesis by A1,Th43;
end;



canceled;

theorem Th48:
  for w1,w2 being Point of TOP-REAL n,P being Subset of
  TopSpaceMetr(Euclid n) st P=LSeg(w1,w2)& not (0.TOP-REAL n) in LSeg(w1,w2)
holds ex w0 being Point of TOP-REAL n st w0 in LSeg(w1,w2) & |.w0.|>0 & |.w0.|=
  (dist_min(P)).(0.TOP-REAL n)
proof
  let w1,w2 be Point of TOP-REAL n,P be Subset of TopSpaceMetr(Euclid n);
  assume that
A1: P=LSeg(w1,w2) and
A2: not 0.TOP-REAL n in LSeg(w1,w2);
  set M=Euclid n;
  reconsider P0=P as Subset of TopSpaceMetr(M);
A3: the TopStruct of TOP-REAL n = TopSpaceMetr M by EUCLID:def 8;
  then reconsider Q={0.TOP-REAL n} as Subset of TopSpaceMetr(M);
  P0 is compact by A1,A3,COMPTS_1:33;
  then consider x1,x2 being Point of M such that
A4: x1 in P0 and
A5: x2 in Q and
A6: dist(x1,x2) = min_dist_min(P0,Q) by A1,A3,WEIERSTR:36;
  reconsider w01=x1 as Point of TOP-REAL n by EUCLID:71;
A7: x2=0.TOP-REAL n by A5,TARSKI:def 1;
  reconsider o=0.TOP-REAL n as Point of M by EUCLID:71;
  reconsider o2=0.TOP-REAL n as Point of TopSpaceMetr(M) by A3;
  for x being set holds x in (dist_min(P0)).:(Q) iff x=(dist_min(P0)).o
  proof
    let x be set;
    hereby
      assume x in (dist_min(P0)).:(Q);
      then
      ex y being set st y in dom(dist_min(P0)) & y in Q & x=( dist_min(P0)
      ).y by FUNCT_1:def 12;
      hence x=(dist_min(P0)).o by TARSKI:def 1;
    end;
    o2 in the carrier of TopSpaceMetr(M) by A3;
    then
A8: o in Q & o in dom (dist_min(P0)) by FUNCT_2:def 1,TARSKI:def 1;
    assume x=(dist_min(P0)).o;
    hence thesis by A8,FUNCT_1:def 12;
  end;
  then
A9: (dist_min(P0)).:(Q)={(dist_min(P0)).o} by TARSKI:def 1;
  [#] ((dist_min(P0)).:(Q))=(dist_min(P0)).:(Q) & lower_bound([#] ((
dist_min( P0)).:(Q)))=lower_bound((dist_min(P0)).:(Q)) by WEIERSTR:def 3,def 5;
  then
A10: lower_bound((dist_min(P0)).:(Q))=(dist_min(P0)).o by A9,SEQ_4:22;
A11: |.w01.|=|.w01-0.TOP-REAL n.| by RLVECT_1:26
    .=dist(x1,x2) by A7,JGRAPH_1:45;
  |.w01.| <> 0 by A1,A2,A4,TOPRNS_1:25;
  hence thesis by A1,A4,A6,A10,A11,WEIERSTR:def 9;
end;

theorem Th49:
  for a being Real, Q being Subset of TOP-REAL n, w1,w4 being
  Point of TOP-REAL n st Q={q : (|.q.|) > a } & w1 in Q & w4 in Q & not (ex r
  being Real st w1=r*w4 or w4=r*w1) holds ex w2,w3 being Point of TOP-REAL n st
  w2 in Q & w3 in Q & LSeg(w1,w2) c=Q & LSeg(w2,w3) c= Q & LSeg(w3,w4) c= Q
proof
  let a be Real, Q be Subset of TOP-REAL n, w1,w4 be Point of TOP-REAL n;
  the TopStruct of TOP-REAL n = TopSpaceMetr Euclid n by EUCLID:def 8;
  then reconsider P=LSeg(w1,w4) as Subset of TopSpaceMetr(Euclid n);
  assume
A1: Q={q : (|.q.|) > a } & w1 in Q & w4 in Q & not (ex r being Real st
  w1=r*w4 or w4=r*w1);
  then not (0.TOP-REAL n) in LSeg(w1,w4) by RLTOPSP1:72;
  then consider w0 being Point of TOP-REAL n such that
  w0 in LSeg(w1,w4) and
A2: |.w0.|>0 and
A3: |.w0.|=(dist_min(P)).(0.TOP-REAL n) by Th48;
  set l9=(a+1)/|.w0.|;
  set w2= l9*w1,w3=l9*w4;
A4: LSeg(w2,w3)c=Q
  proof
    the TopStruct of TOP-REAL n = TopSpaceMetr Euclid n by EUCLID:def 8;
    then reconsider P=LSeg(w1,w4) as Subset of TopSpaceMetr(Euclid n);
    reconsider o=0.TOP-REAL n as Point of Euclid n by EUCLID:71;
    let x be set;
A5: abs l9 = abs(a+1)/abs |.w0.| by COMPLEX1:153
      .=abs(a+1)/|.w0.| by ABSVALUE:def 1;
    (dist(o)).:(P) c= REAL
    proof
      let x be set;
      assume x in ((dist(o)).:(P));
      then consider z being set such that
      z in dom (dist(o)) and
A6:   z in P and
A7:   x=(dist(o)).z by FUNCT_1:def 12;
      thus thesis by A7;
    end;
    then reconsider F=((dist(o)).:(P)) as Subset of REAL;
    assume x in LSeg(w2,w3);
    then consider r such that
A8: x=(1-r)*w2 + r*w3 and
A9: 0 <= r & r <= 1;
    reconsider w5=(1-r)*w1 + r*w4 as Point of TOP-REAL n;
    reconsider w59=w5 as Point of Euclid n by TOPREAL3:13;
A10: dist(w59,o)=(dist(o)).w59 by WEIERSTR:def 6;
    0 is LowerBound of (dist o).:P
    proof
      let r be ext-real number;
      assume r in ((dist(o)).:(P));
      then consider x being set such that
      x in dom (dist(o)) and
A11:  x in P and
A12:  r=(dist(o)).x by FUNCT_1:def 12;
      reconsider w0=x as Point of Euclid n by A11,TOPREAL3:13;
      r=dist(w0,o) by A12,WEIERSTR:def 6;
      hence thesis by METRIC_1:5;
    end;
    then
A13: F is bounded_below by XXREAL_2:def 9;
    the TopStruct of TOP-REAL n = TopSpaceMetr Euclid n by EUCLID:def 8;
    then w59 in the carrier of TopSpaceMetr(Euclid n);
    then
A14: w59 in dom (dist(o)) by FUNCT_2:def 1;
    w5 in LSeg(w1,w4) by A9;
    then dist(w59,o) in ((dist(o)).:(P)) by A14,A10,FUNCT_1:def 12;
    then lower_bound F <=dist(w59,o) by A13,SEQ_4:def 5;
    then dist(w59,o)>= lower_bound([#]((dist(o)).:(P))) by WEIERSTR:def 3;
    then dist(w59,o)>= lower_bound((dist(o)).:(P)) by WEIERSTR:def 5;
    then dist(w59,o)>=|.w0.| by A3,WEIERSTR:def 8;
    then |.w5-0.TOP-REAL n.| >=|.w0.| by JGRAPH_1:45;
    then |.w5.| >=|.w0.| by RLVECT_1:26;
    then abs(a+1)>=0 & |.w5.|/|.w0.|>=1 by A2,COMPLEX1:132,XREAL_1:183;
    then abs(a+1)*(|.w5.|/|.w0.|)>=abs(a+1)*1 by XREAL_1:68;
    then abs(a+1)*(|.w0.|"*|.w5.|)>=abs(a+1) by XCMPLX_0:def 9;
    then abs(a+1)*|.w0.|"*|.w5.|>=abs(a+1);
    then
A15: abs(a+1)/|.w0.|*|.w5.|>=abs(a+1) by XCMPLX_0:def 9;
    a+1>a & abs(a+1)>=a+1 by ABSVALUE:11,XREAL_1:31;
    then abs(a+1)>a by XXREAL_0:2;
    then abs(a+1)/|.w0.|*|.w5.|>a by A15,XXREAL_0:2;
    then |.l9*((1-r)*w1 + r*w4).|>a by A5,TOPRNS_1:8;
    then |.l9*((1-r)*w1) + l9*(r*w4).|>a by EUCLID:36;
    then |.l9*((1-r)*w1) + (l9*r)*w4.|>a by EUCLID:34;
    then |.(l9*(1-r))*w1 + (l9*r)*w4.|>a by EUCLID:34;
    then |.((1-r)*l9)*w1 + r*(l9*w4).|>a by EUCLID:34;
    then |.(1-r)*w2 + r*w3.|>a by EUCLID:34;
    hence thesis by A1,A8;
  end;
A16: w3 in LSeg(w2,w3) by RLTOPSP1:69;
  then
A17: w3 in Q by A4;
A18: LSeg(w4,w3) c=Q
  proof
    let x be set;
    assume x in LSeg(w4,w3);
    then consider r such that
A19: x=(1-r)*w4 + r*w3 and
A20: 0 <= r and
A21: r <= 1;
    now
      per cases;
      case
A22:    a>=0;
        the TopStruct of TOP-REAL n = TopSpaceMetr Euclid n by EUCLID:def 8;
        then reconsider P=LSeg(w4,w1) as Subset of TopSpaceMetr(Euclid n);
        reconsider o=0.TOP-REAL n as Point of Euclid n by EUCLID:71;
        reconsider w5=(1-0)*w4 + 0 * w1 as Point of TOP-REAL n;
A23:    (1-0)*w4+0 * w1=(1-0)*w4+0.TOP-REAL n by EUCLID:33
          .=(1-0)*w4 by EUCLID:31
          .=w4 by EUCLID:33;
        (dist(o)).:(P) c= REAL
        proof
          let x be set;
          assume x in ((dist(o)).:(P));
          then consider z being set such that
          z in dom (dist(o)) and
A24:      z in P and
A25:      x=(dist(o)).z by FUNCT_1:def 12;
          thus thesis by A25;
        end;
        then reconsider F=((dist(o)).:(P)) as Subset of REAL;
        reconsider w59=w5 as Point of Euclid n by TOPREAL3:13;
A26:    dist(w59,o)=(dist(o)).w59 by WEIERSTR:def 6;
        0 is LowerBound of (dist o).:P
        proof
          let r be ext-real number;
          assume r in ((dist(o)).:(P));
          then consider x being set such that
          x in dom (dist(o)) and
A27:      x in P and
A28:      r=(dist(o)).x by FUNCT_1:def 12;
          reconsider w0=x as Point of Euclid n by A27,TOPREAL3:13;
          r=dist(w0,o) by A28,WEIERSTR:def 6;
          hence thesis by METRIC_1:5;
        end;
        then
A29:    F is bounded_below by XXREAL_2:def 9;
        the TopStruct of TOP-REAL n = TopSpaceMetr Euclid n by EUCLID:def 8;
        then w59 in the carrier of TopSpaceMetr(Euclid n);
        then
A30:    w59 in dom (dist(o)) by FUNCT_2:def 1;
        w5 in {(1-r1)*w4 + r1 * w1:0 <= r1 & r1 <= 1};
        then dist(w59,o) in ((dist(o)).:(P)) by A30,A26,FUNCT_1:def 12;
        then lower_bound F <=dist(w59,o) by A29,SEQ_4:def 5;
        then dist(w59,o)>= lower_bound([#]((dist(o)).:(P))) by WEIERSTR:def 3;
        then dist(w59,o)>= lower_bound((dist(o)).:(P)) by WEIERSTR:def 5;
        then dist(w59,o)>=|.w0.| by A3,WEIERSTR:def 8;
        then |.w5-0.TOP-REAL n.| >=|.w0.| by JGRAPH_1:45;
        then
A31:    |.w5.| >=|.w0.| by RLVECT_1:26;
        r*l9*|.w0.| =r*(a+1)/|.w0.|*|.w0.| by XCMPLX_1:75
          .=r*(a+1) by A2,XCMPLX_1:88;
        then
A32:    r*l9*|.w4.|>= r*(a+1) by A20,A22,A23,A31,XREAL_1:66;
A33:    1-r>=0 by A21,XREAL_1:50;
A34:    a+r>=a+0 by A20,XREAL_1:8;
A35:    ex q1 being Point of TOP-REAL n st q1=w4 & |.q1.| > a by A1;
        now
          per cases;
          case
            1-r>0;
            then
A36:        (1-r)*|.w4.|>(1-r)*a by A35,XREAL_1:70;
            abs((1-r)+ r*l9)*|.w4.|=((1-r)+ r*l9)*|.w4.| by A20,A22,A33,
ABSVALUE:def 1
              .= (1-r)*|.w4.|+r*l9*|.w4.|;
            then abs((1-r)+r*l9)*|.w4.|>r*(a+1)+(1-r)*a by A32,A36,XREAL_1:10;
            then abs((1-r)+r*l9)*|.w4.|>a by A34,XXREAL_0:2;
            then |.((1-r)+ r*l9)*w4.|>a by TOPRNS_1:8;
            then |.(1-r)*w4 + r*l9*w4.|>a by EUCLID:37;
            hence |.(1-r)*w4 + r*w3.|>a by EUCLID:34;
          end;
          case
            1-r<=0;
            then 1-r+r<=0+r by XREAL_1:8;
            then r=1 by A21,XXREAL_0:1;
            then
A37:        (1-r)*w4+r*w3=0.TOP-REAL n +1 * w3 by EUCLID:33
              .=0.TOP-REAL n +w3 by EUCLID:33
              .=w3 by EUCLID:31;
            ex q3 being Point of TOP-REAL n st q3=w3 & |.q3.| > a by A1,A17;
            hence |.(1-r)*w4 + r*w3.|>a by A37;
          end;
        end;
        hence |.(1-r)*w4 + r*w3.|>a;
      end;
      case
        a<0;
        hence |.(1-r)*w4 + r*w3.|>a;
      end;
    end;
    hence thesis by A1,A19;
  end;
A38: w2 in LSeg(w2,w3) by RLTOPSP1:69;
  then
A39: w2 in Q by A4;
  LSeg(w1,w2) c=Q
  proof
    let x be set;
    assume x in LSeg(w1,w2);
    then consider r such that
A40: x=(1-r)*w1 + r*w2 and
A41: 0 <= r and
A42: r <= 1;
    now
      per cases;
      case
A43:    a>=0;
        the TopStruct of TOP-REAL n = TopSpaceMetr Euclid n by EUCLID:def 8;
        then reconsider P=LSeg(w1,w4) as Subset of TopSpaceMetr(Euclid n);
        reconsider o=0.TOP-REAL n as Point of Euclid n by EUCLID:71;
        reconsider w5=(1-0)*w1 + 0 * w4 as Point of TOP-REAL n;
A44:    (1-0)*w1+0 * w4=(1-0)*w1+0.TOP-REAL n by EUCLID:33
          .=(1-0)*w1 by EUCLID:31
          .=w1 by EUCLID:33;
        ((dist(o)).:(P)) c= REAL
        proof
          let x be set;
          assume x in ((dist(o)).:(P));
          then consider z being set such that
          z in dom (dist(o)) and
A45:      z in P and
A46:      x=(dist(o)).z by FUNCT_1:def 12;
          thus thesis by A46;
        end;
        then reconsider F=((dist(o)).:(P)) as Subset of REAL;
        reconsider w59=w5 as Point of Euclid n by TOPREAL3:13;
    0 is LowerBound of (dist o).:P
        proof
          let r be ext-real number;
          assume r in ((dist(o)).:(P));
          then consider x being set such that
          x in dom (dist(o)) and
A47:      x in P and
A48:      r=(dist(o)).x by FUNCT_1:def 12;
          reconsider w0=x as Point of Euclid n by A47,TOPREAL3:13;
          r=dist(w0,o) by A48,WEIERSTR:def 6;
          hence thesis by METRIC_1:5;
        end;
        then
A49:    F is bounded_below by XXREAL_2:def 9;
        the TopStruct of TOP-REAL n = TopSpaceMetr Euclid n by EUCLID:def 8;
        then w59 in the carrier of TopSpaceMetr(Euclid n);
        then
A50:    w59 in dom (dist(o)) by FUNCT_2:def 1;
        w5 in LSeg(w1,w4) & dist(w59,o)=(dist(o)).w59 by WEIERSTR:def 6;
        then dist(w59,o) in ((dist(o)).:(P)) by A50,FUNCT_1:def 12;
        then lower_bound F <=dist(w59,o) by A49,SEQ_4:def 5;
        then dist(w59,o)>= lower_bound([#]((dist(o)).:(P))) by WEIERSTR:def 3;
        then dist(w59,o)>= lower_bound((dist(o)).:(P)) by WEIERSTR:def 5;
        then dist(w59,o)>=|.w0.| by A3,WEIERSTR:def 8;
        then |.w5-0.TOP-REAL n.| >=|.w0.| by JGRAPH_1:45;
        then
A51:    |.w5.| >=|.w0.| by RLVECT_1:26;
        r*l9*|.w0.| =r*(a+1)/|.w0.|*|.w0.| by XCMPLX_1:75
          .=r*(a+1) by A2,XCMPLX_1:88;
        then
A52:    r*l9*|.w1.|>= r*(a+1) by A41,A43,A44,A51,XREAL_1:66;
A53:    ex q1 being Point of TOP-REAL n st q1=w1 & |.q1.| > a by A1;
A54:    a+r>=a+0 by A41,XREAL_1:8;
A55:    1-r>=0 by A42,XREAL_1:50;
A56:    ex q2 being Point of TOP-REAL n st q2=w2 & |.q2.| > a by A1,A39;
        now
          per cases;
          case
            1-r>0;
            then
A57:        (1-r)*|.w1.|>(1-r)*a by A53,XREAL_1:70;
            abs((1-r)+ r*l9)*|.w1.|=((1-r)+ r*l9)*|.w1.| by A41,A43,A55,
ABSVALUE:def 1
              .= (1-r)*|.w1.|+r*l9*|.w1.|;
            then abs((1-r)+ r*l9)*|.w1.|>r*(a+1)+(1-r)*a by A52,A57,XREAL_1:10;
            then abs((1-r)+ r*l9)*|.w1.|>a by A54,XXREAL_0:2;
            then |.((1-r)+ r*l9)*w1.|>a by TOPRNS_1:8;
            then |.(1-r)*w1 + r*l9*w1.|>a by EUCLID:37;
            hence |.(1-r)*w1 + r*w2.|>a by EUCLID:34;
          end;
          case
            1-r<=0;
            then 1-r+r<=0+r by XREAL_1:8;
            then r=1 by A42,XXREAL_0:1;
            then (1-r)*w1+r*w2=0.TOP-REAL n +1 * w2 by EUCLID:33
              .=0.TOP-REAL n +w2 by EUCLID:33
              .=w2 by EUCLID:31;
            hence |.(1-r)*w1 + r*w2.|>a by A56;
          end;
        end;
        hence |.(1-r)*w1 + r*w2.|>a;
      end;
      case
        a<0;
        hence |.(1-r)*w1 + r*w2.|>a;
      end;
    end;
    hence thesis by A1,A40;
  end;
  hence thesis by A4,A38,A16,A18;
end;

theorem Th50:
  for a being Real, Q being Subset of TOP-REAL n, w1,w4 being
Point of TOP-REAL n st Q=(REAL n)\ {q : (|.q.|) < a } & w1 in Q & w4 in Q & not
(ex r being Real st w1=r*w4 or w4=r*w1) holds ex w2,w3 being Point of TOP-REAL
n st w2 in Q & w3 in Q & LSeg(w1,w2) c=Q & LSeg(w2,w3) c= Q & LSeg(w3,w4) c= Q
proof
  let a be Real, Q be Subset of TOP-REAL n, w1,w4 be Point of TOP-REAL n;
  the TopStruct of TOP-REAL n = TopSpaceMetr Euclid n by EUCLID:def 8;
  then reconsider P=LSeg(w1,w4) as Subset of TopSpaceMetr(Euclid n);
  assume
A1: Q=(REAL n)\ {q : (|.q.|) < a } & w1 in Q & w4 in Q & not (ex r being
  Real st w1=r*w4 or w4=r*w1);
  then not (0.TOP-REAL n) in LSeg(w1,w4) by RLTOPSP1:72;
  then consider w0 being Point of TOP-REAL n such that
  w0 in LSeg(w1,w4) and
A2: |.w0.|>0 and
A3: |.w0.|=(dist_min(P)).(0.TOP-REAL n) by Th48;
  set l9=a/|.w0.|;
  set w2= l9*w1,w3=l9*w4;
A4: (REAL n)\ {q : (|.q.|) < a } = {q1 : (|.q1.|) >= a }
  proof
    thus (REAL n)\ {q : (|.q.|) < a } c= {q1 : (|.q1.|) >= a }
    proof
      let z be set;
      assume
A5:   z in (REAL n)\ {q : (|.q.|) < a };
      then reconsider q2=z as Point of TOP-REAL n by EUCLID:25;
      not z in {q : (|.q.|) < a } by A5,XBOOLE_0:def 5;
      then |.q2.| >= a;
      hence thesis;
    end;
    let z be set;
    assume z in {q1 : (|.q1.|) >= a };
    then consider q1 such that
A6: z=q1 and
A7: (|.q1.|) >= a;
    q1 in the carrier of TOP-REAL n;
    then
A8: z in REAL n by A6,EUCLID:25;
    for q st q=z holds (|.q.|) >= a by A6,A7;
    then not z in {q : (|.q.|) < a };
    hence thesis by A8,XBOOLE_0:def 5;
  end;
A9: LSeg(w1,w2) c=Q
  proof
    let x be set;
    assume x in LSeg(w1,w2);
    then consider r such that
A10: x=(1-r)*w1 + r*w2 and
A11: 0 <= r and
A12: r <= 1;
    now
      per cases;
      case
A13:    a>0;
        the TopStruct of TOP-REAL n = TopSpaceMetr Euclid n by EUCLID:def 8;
        then reconsider P=LSeg(w1,w4) as Subset of TopSpaceMetr(Euclid n);
        reconsider o=0.TOP-REAL n as Point of Euclid n by EUCLID:71;
        reconsider w5=(1-0)*w1 + 0 * w4 as Point of TOP-REAL n;
A14:    (1-0)*w1+0 * w4=(1-0)*w1+0.TOP-REAL n by EUCLID:33
          .=(1-0)*w1 by EUCLID:31
          .=w1 by EUCLID:33;
        ((dist(o)).:(P)) c= REAL
        proof
          let x be set;
          assume x in ((dist(o)).:(P));
          then consider z being set such that
          z in dom (dist(o)) and
A15:      z in P and
A16:      x=(dist(o)).z by FUNCT_1:def 12;
          thus thesis by A16;
        end;
        then reconsider F=((dist(o)).:(P)) as Subset of REAL;
        reconsider w59=w5 as Point of Euclid n by TOPREAL3:13;
    0 is LowerBound of (dist o).:P
        proof
          let r be ext-real number;
          assume r in ((dist(o)).:(P));
          then consider x being set such that
          x in dom (dist(o)) and
A17:      x in P and
A18:      r=(dist(o)).x by FUNCT_1:def 12;
          reconsider w0=x as Point of Euclid n by A17,TOPREAL3:13;
          r=dist(w0,o) by A18,WEIERSTR:def 6;
          hence thesis by METRIC_1:5;
        end;
        then
A19:    F is bounded_below by XXREAL_2:def 9;
        the TopStruct of TOP-REAL n = TopSpaceMetr Euclid n by EUCLID:def 8;
        then w59 in the carrier of TopSpaceMetr(Euclid n);
        then
A20:    w59 in dom (dist(o)) by FUNCT_2:def 1;
        w5 in LSeg(w1,w4) & dist(w59,o)=(dist(o)).w59 by WEIERSTR:def 6;
        then dist(w59,o) in ((dist(o)).:(P)) by A20,FUNCT_1:def 12;
        then lower_bound F <=dist(w59,o) by A19,SEQ_4:def 5;
        then dist(w59,o)>= lower_bound([#]((dist(o)).:(P))) by WEIERSTR:def 3;
        then dist(w59,o)>= lower_bound((dist(o)).:(P)) by WEIERSTR:def 5;
        then dist(w59,o)>=|.w0.| by A3,WEIERSTR:def 8;
        then |.w5-0.TOP-REAL n.| >=|.w0.| by JGRAPH_1:45;
        then
A21:    |.w5.| >=|.w0.| by RLVECT_1:26;
A22:    1-r>=0 by A12,XREAL_1:50;
        then
A23:    abs((1-r)+r*l9)*|.w1.|=((1-r)+ r*l9)*|.w1.| by A11,A13,ABSVALUE:def 1
          .= (1-r)*|.w1.|+r*l9*|.w1.|;
        ex q1 being Point of TOP-REAL n st q1=w1 & |.q1.| >= a by A1,A4;
        then
A24:    (1-r)*|.w1.|>=(1-r)*a by A22,XREAL_1:66;
        r*l9*|.w0.| =r*a/|.w0.|*|.w0.| by XCMPLX_1:75
          .=r*a by A2,XCMPLX_1:88;
        then r*l9*|.w1.|>= r*a by A11,A13,A14,A21,XREAL_1:66;
        then abs((1-r)+r*l9)*|.w1.|>=r*a+(1-r)*a by A24,A23,XREAL_1:9;
        then |.((1-r)+ r*l9)*w1.|>=a by TOPRNS_1:8;
        then |.(1-r)*w1 + r*l9*w1.|>=a by EUCLID:37;
        hence |.(1-r)*w1 + r*w2.|>=a by EUCLID:34;
      end;
      case
        a<=0;
        hence |.(1-r)*w1 + r*w2.|>=a;
      end;
    end;
    hence thesis by A1,A4,A10;
  end;
A25: LSeg(w4,w3) c=Q
  proof
    let x be set;
    assume x in LSeg(w4,w3);
    then consider r such that
A26: x=(1-r)*w4 + r*w3 and
A27: 0 <= r and
A28: r <= 1;
    now
      per cases;
      case
A29:    a>0;
        the TopStruct of TOP-REAL n = TopSpaceMetr Euclid n by EUCLID:def 8;
        then reconsider P=LSeg(w4,w1) as Subset of TopSpaceMetr(Euclid n);
        reconsider o=0.TOP-REAL n as Point of Euclid n by EUCLID:71;
        reconsider w5=(1-0)*w4 + 0 * w1 as Point of TOP-REAL n;
A30:    (1-0)*w4+0 * w1=(1-0)*w4+0.TOP-REAL n by EUCLID:33
          .=(1-0)*w4 by EUCLID:31
          .=w4 by EUCLID:33;
        ((dist(o)).:(P)) c= REAL
        proof
          let x be set;
          assume x in ((dist(o)).:(P));
          then consider z being set such that
          z in dom (dist(o)) and
A31:      z in P and
A32:      x=(dist(o)).z by FUNCT_1:def 12;
          thus thesis by A32;
        end;
        then reconsider F=((dist(o)).:(P)) as Subset of REAL;
        reconsider w59=w5 as Point of Euclid n by TOPREAL3:13;
A33:    dist(w59,o)=(dist(o)).w59 by WEIERSTR:def 6;
    0 is LowerBound of (dist o).:P
        proof
          let r be ext-real number;
          assume r in ((dist(o)).:(P));
          then consider x being set such that
          x in dom (dist(o)) and
A34:      x in P and
A35:      r=(dist(o)).x by FUNCT_1:def 12;
          reconsider w0=x as Point of Euclid n by A34,TOPREAL3:13;
          r=dist(w0,o) by A35,WEIERSTR:def 6;
          hence thesis by METRIC_1:5;
        end;
        then
A36:    F is bounded_below by XXREAL_2:def 9;
        the TopStruct of TOP-REAL n = TopSpaceMetr Euclid n by EUCLID:def 8;
        then w59 in the carrier of TopSpaceMetr(Euclid n);
        then
A37:    w59 in dom (dist(o)) by FUNCT_2:def 1;
        w5 in {(1-r1)*w4 + r1 * w1:0 <= r1 & r1 <= 1};
        then dist(w59,o) in ((dist(o)).:(P)) by A37,A33,FUNCT_1:def 12;
        then lower_bound F <=dist(w59,o) by A36,SEQ_4:def 5;
        then dist(w59,o)>= lower_bound([#]((dist(o)).:(P))) by WEIERSTR:def 3;
        then dist(w59,o)>= lower_bound((dist(o)).:(P)) by WEIERSTR:def 5;
        then dist(w59,o)>=|.w0.| by A3,WEIERSTR:def 8;
        then |.w5-0.TOP-REAL n.| >=|.w0.| by JGRAPH_1:45;
        then
A38:    |.w5.| >=|.w0.| by RLVECT_1:26;
A39:    1-r>=0 by A28,XREAL_1:50;
        then
A40:    abs((1-r)+r*l9)*|.w4.|=((1-r)+ r*l9)*|.w4.| by A27,A29,ABSVALUE:def 1
          .= (1-r)*|.w4.|+r*l9*|.w4.|;
        ex q1 being Point of TOP-REAL n st q1=w4 & |.q1.| >= a by A1,A4;
        then
A41:    (1-r)*|.w4.|>=(1-r)*a by A39,XREAL_1:66;
        r*l9*|.w0.| =r*a/|.w0.|*|.w0.| by XCMPLX_1:75
          .=r*a by A2,XCMPLX_1:88;
        then r*l9*|.w4.|>= r*a by A27,A29,A30,A38,XREAL_1:66;
        then abs((1-r)+r*l9)*|.w4.|>=r*a+(1-r)*a by A41,A40,XREAL_1:9;
        then |.((1-r)+ r*l9)*w4.|>=a by TOPRNS_1:8;
        then |.(1-r)*w4 + r*l9*w4.|>=a by EUCLID:37;
        hence |.(1-r)*w4 + r*w3.|>=a by EUCLID:34;
      end;
      case
        a<=0;
        hence |.(1-r)*w4 + r*w3.|>=a;
      end;
    end;
    hence thesis by A1,A4,A26;
  end;
A42: LSeg(w2,w3)c=Q
  proof
    the TopStruct of TOP-REAL n = TopSpaceMetr Euclid n by EUCLID:def 8;
    then reconsider P=LSeg(w1,w4) as Subset of TopSpaceMetr(Euclid n);
    reconsider o=0.TOP-REAL n as Point of Euclid n by EUCLID:71;
    let x be set;
A43: abs l9=abs(a)/(abs|.w0.|) by COMPLEX1:153
      .=abs(a)/|.w0.| by ABSVALUE:def 1;
    ((dist(o)).:(P)) c= REAL
    proof
      let x be set;
      assume x in ((dist(o)).:(P));
      then consider z being set such that
      z in dom (dist(o)) and
A44:  z in P and
A45:  x=(dist(o)).z by FUNCT_1:def 12;
      thus thesis by A45;
    end;
    then reconsider F=(dist(o)).:(P) as Subset of REAL;
    assume x in LSeg(w2,w3);
    then consider r such that
A46: x=(1-r)*w2 + r*w3 and
A47: 0 <= r & r <= 1;
    reconsider w5=(1-r)*w1 + r*w4 as Point of TOP-REAL n;
    reconsider w59=w5 as Point of Euclid n by TOPREAL3:13;
A48: dist(w59,o)=(dist(o)).w59 by WEIERSTR:def 6;
    0 is LowerBound of (dist o).:P
    proof
      let r be ext-real number;
      assume r in ((dist(o)).:(P));
      then consider x being set such that
      x in dom (dist(o)) and
A49:  x in P and
A50:  r=(dist(o)).x by FUNCT_1:def 12;
      reconsider w0=x as Point of Euclid n by A49,TOPREAL3:13;
      r=dist(w0,o) by A50,WEIERSTR:def 6;
      hence thesis by METRIC_1:5;
    end;
    then
A51: F is bounded_below by XXREAL_2:def 9;
    the TopStruct of TOP-REAL n = TopSpaceMetr Euclid n by EUCLID:def 8;
    then w59 in the carrier of TopSpaceMetr(Euclid n);
    then
A52: w59 in dom (dist(o)) by FUNCT_2:def 1;
    w5 in LSeg(w1,w4) by A47;
    then dist(w59,o) in ((dist(o)).:(P)) by A52,A48,FUNCT_1:def 12;
    then lower_bound F <=dist(w59,o) by A51,SEQ_4:def 5;
    then dist(w59,o)>= lower_bound([#]((dist(o)).:(P))) by WEIERSTR:def 3;
    then dist(w59,o)>= lower_bound((dist(o)).:(P)) by WEIERSTR:def 5;
    then dist(w59,o)>=|.w0.| by A3,WEIERSTR:def 8;
    then |.w5-0.TOP-REAL n.| >=|.w0.| by JGRAPH_1:45;
    then |.w5.| >=|.w0.| by RLVECT_1:26;
    then abs(a)>=0 & |.w5.|/|.w0.|>=1 by A2,COMPLEX1:132,XREAL_1:183;
    then abs(a)*(|.w5.|/|.w0.|)>=abs(a)*1 by XREAL_1:68;
    then abs(a)*(|.w5.|*|.w0.|")>=abs(a) by XCMPLX_0:def 9;
    then abs(a)*|.w0.|"*|.w5.|>=abs(a);
    then
A53: abs(a)/|.w0.|*|.w5.|>=abs(a) by XCMPLX_0:def 9;
    abs(a)>=a by ABSVALUE:11;
    then abs(a)/|.w0.|*|.w5.|>=a by A53,XXREAL_0:2;
    then |.l9*((1-r)*w1 + r*w4).|>=a by A43,TOPRNS_1:8;
    then |.l9*((1-r)*w1) + l9*(r*w4).|>=a by EUCLID:36;
    then |.l9*((1-r)*w1) + (l9*r)*w4.|>=a by EUCLID:34;
    then |.(l9*(1-r))*w1 + (l9*r)*w4.|>=a by EUCLID:34;
    then |.((1-r)*l9)*w1 + r*(l9*w4).|>=a by EUCLID:34;
    then |.(1-r)*w2 + r*w3.|>=a by EUCLID:34;
    hence thesis by A1,A4,A46;
  end;
  w2 in LSeg(w2,w3) & w3 in LSeg(w2,w3) by RLTOPSP1:69;
  hence thesis by A42,A9,A25;
end;

canceled;

theorem Th52:
  for f being FinSequence of REAL holds f is Element of REAL (len
  f) & f is Point of TOP-REAL (len f)
proof
  let f be FinSequence of REAL;
  f is Element of REAL* by FINSEQ_1:def 11;
  then
  the carrier of TOP-REAL len f = the carrier of Euclid len f & f in ((len
  f) -tuples_on REAL) by EUCLID:71;
  hence thesis;
end;

theorem Th53:
  for x being Element of REAL n,f,g being FinSequence of REAL, r
being Real st f=x & g=r*x holds len f=len g & for i being Element of NAT st 1<=
  i & i<=len f holds g/.i=r*f/.i
proof
  reconsider h2= (id REAL) as Function;
  let x be Element of REAL n,f,g be FinSequence of REAL,r be Real;
  assume that
A1: f=x and
A2: g=r*x;
A3: len f=n by A1,FINSEQ_1:def 18;
  set h1= (dom (id REAL)) --> r;
A4: dom (<:h1, h2:>)=dom (h1) /\ dom ((id REAL)) by FUNCT_3:def 8;
A5: len g=n by A2,FINSEQ_1:def 18;
A6: g= (multreal * (<:h1, h2:>))*x by A2,FUNCOP_1:def 5;
  for i being Element of NAT st 1<=i & i<=len f holds g/.i=r*f/.i
  proof
    let i be Element of NAT;
A7: dom h1=dom (id REAL) by FUNCOP_1:19
      .=REAL by FUNCT_1:34;
    dom h2=REAL by FUNCT_1:34;
    then
A8: h1.(x.i) = r by FUNCOP_1:13;
A9: h2.(x.i)=x.i by FUNCT_1:34;
    assume
A10: 1<=i & i<=len f;
    then
A11: f.i=f/.i by FINSEQ_4:24;
    i in Seg len f by A10,FINSEQ_1:3;
    then i in dom g by A3,A5,FINSEQ_1:def 3;
    then
A12: g.i=(multreal * (<:h1, h2:>)).(x.i) by A6,FUNCT_1:22;
A13: dom (<:h1, h2:>)=dom h1 /\ REAL by A4,FUNCT_1:34;
    then (<:h1, h2:>).(x.i)=[h1.(x.i),h2.(x.i)] by A7,FUNCT_3:def 8;
    then g.i=multreal.(r,f.i) by A1,A12,A13,A7,A8,A9,FUNCT_1:23;
    then g.i=r*(f/.i) by A11,BINOP_2:def 11;
    hence thesis by A3,A5,A10,FINSEQ_4:24;
  end;
  hence thesis by A2,A3,FINSEQ_1:def 18;
end;

theorem Th54:
  for x being Element of REAL n,f being FinSequence st x<> 0*n & x
  =f holds ex i being Element of NAT st 1<=i & i<=n & f.i<>0
proof
  let x be Element of REAL n,f be FinSequence;
  assume that
A1: x<> 0*n and
A2: x=f;
A3: len f=n by A2,FINSEQ_1:def 18;
  assume
A4: not ex i being Element of NAT st 1<=i & i<=n & f.i<>0;
  for z being set holds z in f iff ex x,y st x in Seg n & y in {0} & z = [ x,y]
  proof
    let z be set;
    hereby
      assume
A5:   z in f;
      then consider x0,y0 being set such that
A6:   z = [x0,y0] by RELAT_1:def 1;
A7:   y0=f.x0 by A5,A6,FUNCT_1:8;
A8:   x0 in dom f by A5,A6,RELAT_1:def 4;
      then reconsider n1=x0 as Element of NAT;
A9:   x0 in Seg len f by A8,FINSEQ_1:def 3;
      then 1<=n1 & n1<=len f by FINSEQ_1:3;
      then f.n1=0 by A3,A4;
      then y0 in {0} by A7,TARSKI:def 1;
      hence ex x,y st x in Seg n & y in {0} & z = [x,y] by A3,A6,A9;
    end;
    given x,y such that
A10: x in Seg n and
A11: y in {0} and
A12: z = [x,y];
    reconsider n1=x as Element of NAT by A10;
A13: n1<=n by A10,FINSEQ_1:3;
A14: x in dom f by A3,A10,FINSEQ_1:def 3;
    y=0 & 1<=n1 by A10,A11,FINSEQ_1:3,TARSKI:def 1;
    then y=f.x by A4,A13;
    hence thesis by A12,A14,FUNCT_1:8;
  end;
  then f=[:Seg n,{0}:] by ZFMISC_1:def 2;
  hence contradiction by A1,A2,FUNCOP_1:def 2;
end;

theorem Th55:
  for x being Element of REAL n st n>=2 & x<> 0*n holds ex y being
  Element of REAL n st not ex r being Real st y=r*x or x=r*y
proof
  let x be Element of REAL n;
  assume that
A1: n>=2 and
A2: x<> 0*n;
  reconsider f=x as FinSequence of REAL;
  consider i2 being Element of NAT such that
A3: 1<=i2 and
A4: i2<=n and
A5: f.i2<>0 by A2,Th54;
A6: len f=n by FINSEQ_1:def 18;
  then
A7: 1<=len f by A1,XXREAL_0:2;
  per cases;
  suppose
A8: i2>1;
    reconsider g=(<*((f/.1)+1)*>)^mid(f,2,len f) as FinSequence of REAL;
A9: len (mid(f,2,len f))=len f-'2+1 by A1,A6,A7,FINSEQ_6:124
      .=len f-2+1 by A1,A6,XREAL_1:235;
    len g=len (<*(f/.1+1)*>) + len (mid(f,2,len f)) by FINSEQ_1:35;
    then
A10: len g=1+(len f-2+1) by A9,FINSEQ_1:56
      .=len f;
    then reconsider y2=g as Element of REAL n by A6,Th52;
A11: len (<*(f/.1+1)*>)=1 by FINSEQ_1:56;
    now
      given r being Real such that
A12:  y2=r*x or x=r*y2;
      per cases by A12;
      suppose
A13:    y2=r*x;
        i2<=len f-(1+1)+(1+1) by A4,FINSEQ_1:def 18;
        then
A14:    i2-1<=len f-(1+1)+1+1-1 by XREAL_1:11;
A15:    i2-'1=i2-1 & 1<=i2-'1 by A8,NAT_D:49,XREAL_1:235;
A16:    1<=len f by A1,A6,XXREAL_0:2;
        then
A17:    g/.1=g.1 by A10,FINSEQ_4:24;
A18:    g/.i2=g.i2 by A3,A4,A6,A10,FINSEQ_4:24;
A19:    i2-'1+2-'1=i2-'1+1+1-'1 .=i2-'1+1 by NAT_D:34
          .=i2-1+1 by A3,XREAL_1:235
          .=i2;
A20:    f/.i2=f.i2 by A3,A4,A6,FINSEQ_4:24;
        1+1<=i2 & i2<=1+len (mid(f,2,len f)) by A4,A8,A9,FINSEQ_1:def 18
,NAT_1:13;
        then g.i2= (mid(f,2,len f)).(i2-1) by A11,FINSEQ_1:36
          .=f.i2 by A1,A6,A9,A16,A15,A14,A19,FINSEQ_6:124;
        then 1 * f/.i2=r*f/.i2 by A3,A4,A6,A13,A18,A20,Th53;
        then
A21:    1=r by A5,A20,XCMPLX_1:5;
        g/.1=r*f/.1 by A13,A16,Th53;
        then f/.1+1=1 * f/.1 by A21,A17,FINSEQ_1:58;
        hence contradiction;
      end;
      suppose
A22:    x=r*y2;
        i2<=len f-(1+1)+(1+1) by A4,FINSEQ_1:def 18;
        then
A23:    i2-1<=len f-(1+1)+1+1-1 by XREAL_1:11;
A24:    i2-'1=i2-1 & 1<=i2-'1 by A8,NAT_D:49,XREAL_1:235;
A25:    1<=len f by A1,A6,XXREAL_0:2;
        then
A26:    g/.1=g.1 by A10,FINSEQ_4:24;
A27:    g/.i2=g.i2 by A3,A4,A6,A10,FINSEQ_4:24;
A28:    i2-'1+2-'1=i2-'1+1+1-'1 .=i2-'1+1 by NAT_D:34
          .=i2-1+1 by A3,XREAL_1:235
          .=i2;
A29:    f/.i2=f.i2 by A3,A4,A6,FINSEQ_4:24;
        1+1<=i2 & i2<=1+len (mid(f,2,len f)) by A4,A8,A9,FINSEQ_1:def 18
,NAT_1:13;
        then g.i2= (mid(f,2,len f)).(i2-1) by A11,FINSEQ_1:36
          .=f.i2 by A1,A6,A9,A25,A24,A23,A28,FINSEQ_6:124;
        then 1 * f/.i2=r*f/.i2 by A3,A4,A6,A10,A22,A27,A29,Th53;
        then
A30:    1=r by A5,A29,XCMPLX_1:5;
        f/.1=r*g/.1 by A10,A22,A25,Th53;
        then f/.1+1=1 * f/.1 by A30,A26,FINSEQ_1:58;
        hence contradiction;
      end;
    end;
    hence thesis;
  end;
  suppose
A31: i2<=1;
    reconsider g=mid(f,1,len f-'1)^<*(f/.len f+1)*> as FinSequence of REAL;
A32: len f-'1<=len f by NAT_D:44;
A33: 1+1-1<=len f-1 by A1,A6,XREAL_1:11;
A34: len f-'1=len f-1 by A1,A6,XREAL_1:235,XXREAL_0:2;
    then
A35: len f-'1-'1+1=len f-1-1+1 by A33,XREAL_1:235
      .=len f-(1+1)+1;
    then
A36: len (mid(f,1,len f-'1)) =len f-1 by A7,A34,A32,A33,FINSEQ_6:124;
    len (<*(f/.len f+1)*>)=1 & len (mid(f,1,len f-'1))=len f-2+1 by A7,A34,A32
,A33,A35,FINSEQ_1:56,FINSEQ_6:124;
    then
A37: len g=(len f-2+1)+1 by FINSEQ_1:35
      .=len f;
    then reconsider y2=g as Element of REAL n by A6,Th52;
A38: i2=1 by A3,A31,XXREAL_0:1;
    now
      given r being Real such that
A39:  y2=r*x or x=r*y2;
      per cases by A39;
      suppose
A40:    y2=r*x;
A41:    g/.i2=g.i2 by A3,A4,A6,A37,FINSEQ_4:24;
A42:    f/.i2=f.i2 by A3,A4,A6,FINSEQ_4:24;
        g.i2= (mid(f,1,len f-'1)).i2 by A38,A33,A36,FINSEQ_6:115
          .=f.i2 by A38,A34,A32,A33,FINSEQ_6:129;
        then 1 * f/.i2=r*f/.i2 by A3,A4,A6,A40,A41,A42,Th53;
        then
A43:    1=r by A5,A42,XCMPLX_1:5;
A44:    g.len f= g.(len f -1+1) .=f/.len f+1 by A36,FINSEQ_1:59;
A45:    1<=len f by A1,A6,XXREAL_0:2;
        then
A46:    g/.len f=g.len f by A37,FINSEQ_4:24;
        g/.len f=r*f/.len f by A40,A45,Th53;
        hence contradiction by A43,A46,A44;
      end;
      suppose
A47:    x=r*y2;
A48:    g/.i2=g.i2 by A3,A4,A6,A37,FINSEQ_4:24;
A49:    f/.i2=f.i2 by A3,A4,A6,FINSEQ_4:24;
        g.i2= (mid(f,1,len f-'1)).i2 by A38,A33,A36,FINSEQ_6:115
          .=f.(i2) by A38,A34,A32,A33,FINSEQ_6:129;
        then 1 * f/.i2=r*f/.i2 by A3,A4,A6,A37,A47,A48,A49,Th53;
        then
A50:    1=r by A5,A49,XCMPLX_1:5;
A51:    g.len f=g.(len f-1+1) .=f/.len f+1 by A36,FINSEQ_1:59;
A52:    1<=len f by A1,A6,XXREAL_0:2;
        then
A53:    g/.len f=g.len f by A37,FINSEQ_4:24;
        f/.len f=r*g/.len f by A37,A47,A52,Th53;
        hence contradiction by A50,A53,A51;
      end;
    end;
    hence thesis;
  end;
end;

theorem Th56:
  for a being Real, Q being Subset of TOP-REAL n, w1,w7 being
Point of TOP-REAL n st n>=2 & Q={q : (|.q.|) > a } & w1 in Q & w7 in Q & (ex r
  being Real st w1=r*w7 or w7=r*w1) holds ex w2,w3,w4,w5,w6 being Point of
TOP-REAL n st w2 in Q & w3 in Q & w4 in Q & w5 in Q & w6 in Q & LSeg(w1,w2) c=Q
  & LSeg(w2,w3) c= Q & LSeg(w3,w4) c= Q & LSeg(w4,w5) c= Q & LSeg(w5,w6) c=Q &
  LSeg(w6,w7) c= Q
proof
  let a be Real, Q be Subset of TOP-REAL n, w1,w7 be Point of TOP-REAL n;
  assume
A1: n>=2 & Q={q : (|.q.|) > a } & w1 in Q & w7 in Q;
  reconsider y1=w1 as Element of REAL n by EUCLID:25;
  given r8 being Real such that
A2: w1=r8*w7 or w7=r8*w1;
  per cases;
  suppose
A3: a>=0;
    now
      assume
A4:   w1=0.TOP-REAL n;
      ex q st q=w1 & (|.q.|)>a by A1;
      hence contradiction by A3,A4,TOPRNS_1:24;
    end;
    then w1 <> 0*n by EUCLID:74;
    then consider y being Element of REAL n such that
A5: not ex r being Real st y=r*y1 or y1=r*y by A1,Th55;
    set y4=((a+1)/|.y.|)*y;
    reconsider w4=y4 as Point of TOP-REAL n by EUCLID:25;
A6: now
A7:   0 *y1 = 0 * w1 by EUCLID:69
        .= 0.TOP-REAL n by EUCLID:33
        .=0*n by EUCLID:74;
      assume |.y.|=0;
      hence contradiction by A5,A7,EUCLID:11;
    end;
    then
A8: (a+1)/|.y.|>0 by A3,XREAL_1:141;
A9: now
      reconsider y9=y,y19=y1 as Element of n-tuples_on REAL;
      given r being Real such that
A10:  w1=r*w4 or w4=r*w1;
      per cases by A10;
      suppose
        w1=r*w4;
        then y1 = r*y4 by EUCLID:69;
        then y1=(r*((a+1)/|.y.|))*y by RVSUM_1:71;
        hence contradiction by A5;
      end;
      suppose
        w4=r*w1;
        then y4 = r*y1 by EUCLID:69;
        then ((a+1)/|.y.|)"*((a+1)/|.y.|)*y9=((a+1)/|.y.|)"*(r*y1) by
RVSUM_1:71;
        then ((a+1)/|.y.|)"*((a+1)/|.y.|)*y=((a+1)/|.y.|)"*r*y19 by RVSUM_1:71;
        then 1 *y=((a+1)/|.y.|)"*r*y1 by A8,XCMPLX_0:def 7;
        then y=((a+1)/|.y.|)"*r*y1 by RVSUM_1:74;
        hence contradiction by A5;
      end;
    end;
A11: |.w4.|=abs((a+1)/|.y.|)*|.y.| by EUCLID:14
      .= (a+1)/|.y.|*|.y.| by A3,ABSVALUE:def 1
      .=a+1 by A6,XCMPLX_1:88;
    then |.w4.|>a by XREAL_1:31;
    then
A12: w4 in Q by A1;
    now
      given r1 being Real such that
A13:  w4=r1*w7 or w7=r1*w4;
A14:  now
        assume r1=0;
        then
A15:    w4=0.TOP-REAL n or w7=0.TOP-REAL n by A13,EUCLID:33;
        ex q7 being Point of TOP-REAL n st q7=w7 & |.q7.|>a by A1;
        hence contradiction by A3,A11,A15,TOPRNS_1:24;
      end;
      per cases by A2;
      suppose
A16:    w1=r8*w7;
        now
          per cases by A13;
          case
            w4=r1*w7;
            then r1"*w4=r1"*r1*w7 by EUCLID:34;
            then r1"*w4=1 *w7 by A14,XCMPLX_0:def 7;
            then r1"*w4=w7 by EUCLID:33;
            then w1=r8*r1"*w4 by A16,EUCLID:34;
            hence contradiction by A9;
          end;
          case
            w7=r1*w4;
            then r1"*w7=r1"*r1*w4 by EUCLID:34;
            then r1"*w7=1 *w4 by A14,XCMPLX_0:def 7;
            then r1"*w7=w4 by EUCLID:33;
            then r1""*w4=r1""*r1"*w7 by EUCLID:34;
            then r1""*w4=1 *w7 by A14,XCMPLX_0:def 7;
            then r1""*w4=w7 by EUCLID:33;
            then w1=r8*r1""*w4 by A16,EUCLID:34;
            hence contradiction by A9;
          end;
        end;
        hence contradiction;
      end;
      suppose
A17:    w7=r8*w1;
A18:    now
          assume r8=0;
          then
A19:      w7=0.TOP-REAL n by A17,EUCLID:33;
          ex q7 being Point of TOP-REAL n st q7=w7 & |.q7.|>a by A1;
          hence contradiction by A3,A19,TOPRNS_1:24;
        end;
        r8"*w7=r8"*r8*w1 by A17,EUCLID:34;
        then r8"*w7=1 *w1 by A18,XCMPLX_0:def 7;
        then
A20:    r8"*w7=w1 by EUCLID:33;
        now
          per cases by A13;
          case
            w4=r1*w7;
            then r1"*w4=r1"*r1*w7 by EUCLID:34;
            then r1"*w4=1 *w7 by A14,XCMPLX_0:def 7;
            then r1"*w4=w7 by EUCLID:33;
            then w1=r8"*r1"*w4 by A20,EUCLID:34;
            hence contradiction by A9;
          end;
          case
            w7=r1*w4;
            then r1"*w7=r1"*r1*w4 by EUCLID:34;
            then r1"*w7=1 *w4 by A14,XCMPLX_0:def 7;
            then r1"*w7=w4 by EUCLID:33;
            then r1""*w4=r1""*r1"*w7 by EUCLID:34;
            then r1""*w4=1 *w7 by A14,XCMPLX_0:def 7;
            then r1""*w4=w7 by EUCLID:33;
            then w1=r8"*r1""*w4 by A20,EUCLID:34;
            hence contradiction by A9;
          end;
        end;
        hence contradiction;
      end;
    end;
    then
A21: ex w29,w39 being Point of TOP-REAL n st w29 in Q & w39 in Q & LSeg(w4,
    w29) c=Q & LSeg(w29,w39) c= Q & LSeg(w39,w7) c= Q by A1,A12,Th49;
    ex w2,w3 being Point of TOP-REAL n st w2 in Q & w3 in Q & LSeg(w1,w2)
    c=Q & LSeg(w2,w3) c= Q & LSeg(w3,w4) c= Q by A1,A12,A9,Th49;
    hence thesis by A12,A21;
  end;
  suppose
A22: a<0;
    set w2=0.TOP-REAL n;
A23: REAL n c= Q
    proof
      let x be set;
      assume x in REAL n;
      then reconsider w=x as Point of TOP-REAL n by EUCLID:25;
      |.w.|>=0;
      hence thesis by A1,A22;
    end;
    the carrier of TOP-REAL n=REAL n by EUCLID:25;
    then
A24: Q=the carrier of TOP-REAL n by A23,XBOOLE_0:def 10;
    then LSeg(w1,w2) c=Q & LSeg(w2,w7) c=Q;
    hence thesis by A24;
  end;
end;

theorem Th57:
  for a being Real, Q being Subset of TOP-REAL n, w1,w7 being
Point of TOP-REAL n st n>=2 & Q=(REAL n)\ {q : (|.q.|) < a } & w1 in Q & w7 in
Q & (ex r being Real st w1=r*w7 or w7=r*w1) holds ex w2,w3,w4,w5,w6 being Point
of TOP-REAL n st w2 in Q & w3 in Q & w4 in Q & w5 in Q & w6 in Q & LSeg(w1,w2)
c=Q & LSeg(w2,w3) c= Q & LSeg(w3,w4) c= Q & LSeg(w4,w5) c= Q & LSeg(w5,w6) c=Q
  & LSeg(w6,w7) c= Q
proof
  let a be Real, Q be Subset of TOP-REAL n, w1,w7 be Point of TOP-REAL n;
  reconsider y1=w1 as Element of REAL n by EUCLID:25;
  assume
A1: n>=2 & Q=(REAL n)\ {q : (|.q.|) < a } & w1 in Q & w7 in Q & ex r
  being Real st w1=r*w7 or w7=r*w1;
  then consider r8 being Real such that
A2: w1=r8*w7 or w7=r8*w1;
  per cases;
  suppose
A3: a>0;
    now
      assume w1=0.TOP-REAL n;
      then |.w1.|=0 by TOPRNS_1:24;
      then w1 in {q : (|.q.|) < a } by A3;
      hence contradiction by A1,XBOOLE_0:def 5;
    end;
    then w1 <> 0*n by EUCLID:74;
    then consider y being Element of REAL n such that
A4: not ex r being Real st y=r*y1 or y1=r*y by A1,Th55;
    set y4=(a/|.y.|)*y;
    reconsider w4=y4 as Point of TOP-REAL n by EUCLID:25;
A5: now
A6:   0 *y1 = 0 * w1 by EUCLID:69
        .= 0.TOP-REAL n by EUCLID:33
        .=0*n by EUCLID:74;
      assume |.y.|=0;
      hence contradiction by A4,A6,EUCLID:11;
    end;
    then
A7: a/|.y.|>0 by A3,XREAL_1:141;
A8: now
      reconsider y9=y,y19=y1 as Element of n-tuples_on REAL;
      given r being Real such that
A9:   w1=r*w4 or w4=r*w1;
      y1=r*y4 or y4=r*y1 by A9,EUCLID:69;
      then y1=(r*(a/|.y.|))*y or (a/|.y.|)"*(a/|.y.|)*y9=(a/|.y.|)"*(r*y1) by
RVSUM_1:71;
      then y1=(r*(a/|.y.|))*y or (a/|.y.|)"*(a/|.y.|)*y=(a/|.y.|)"*r*y19 by
RVSUM_1:71;
      then
A10:  y1=(r*(a/|.y.|))*y9 or 1 *y=(a/|.y.|)"*r*y1 by A7,XCMPLX_0:def 7;
      per cases by A10,RVSUM_1:74;
      suppose
        y1=(r*(a/|.y.|))*y;
        hence contradiction by A4;
      end;
      suppose
        y=(a/|.y.|)"*r*y1;
        hence contradiction by A4;
      end;
    end;
A11: |.w4.|=abs(a/|.y.|)*|.y.| by EUCLID:14
      .= a/|.y.|*|.y.| by A3,ABSVALUE:def 1
      .=a by A5,XCMPLX_1:88;
A12: now
      assume w4 in {q : (|.q.|) < a };
      then ex q st q=w4 & (|.q.|) < a;
      hence contradiction by A11;
    end;
    then
A13: w4 in Q by A1,XBOOLE_0:def 5;
    now
      given r1 being Real such that
A14:  w4=r1*w7 or w7=r1*w4;
A15:  now
        assume r1=0;
        then w4=0.TOP-REAL n or w7=0.TOP-REAL n by A14,EUCLID:33;
        then |.w4.|=0 or |.w7.|=0 by TOPRNS_1:24;
        then w4 in {q : (|.q.|) < a } or w7 in {q2 where q2 is Point of
        TOP-REAL n: (|.q2.|) < a } by A3;
        hence contradiction by A1,A12,XBOOLE_0:def 5;
      end;
      now
        per cases by A2;
        case
A16:      w1=r8*w7;
          now
            per cases by A14;
            case
              w4=r1*w7;
              then r1"*w4=r1"*r1*w7 by EUCLID:34;
              then r1"*w4=1 *w7 by A15,XCMPLX_0:def 7;
              then r1"*w4=w7 by EUCLID:33;
              then w1=r8*r1"*w4 by A16,EUCLID:34;
              hence contradiction by A8;
            end;
            case
              w7=r1*w4;
              then r1"*w7=r1"*r1*w4 by EUCLID:34;
              then r1"*w7=1 *w4 by A15,XCMPLX_0:def 7;
              then r1"*w7=w4 by EUCLID:33;
              then r1""*w4=r1""*r1"*w7 by EUCLID:34;
              then r1""*w4=1 *w7 by A15,XCMPLX_0:def 7;
              then r1""*w4=w7 by EUCLID:33;
              then w1=r8*r1""*w4 by A16,EUCLID:34;
              hence contradiction by A8;
            end;
          end;
          hence contradiction;
        end;
        case
A17:      w7=r8*w1;
A18:      now
            assume r8=0;
            then w7=0.TOP-REAL n by A17,EUCLID:33;
            then |.w7.|=0 by TOPRNS_1:24;
            then w7 in {q : (|.q.|) < a } by A3;
            hence contradiction by A1,XBOOLE_0:def 5;
          end;
          r8"*w7=r8"*r8*w1 by A17,EUCLID:34;
          then r8"*w7=1 *w1 by A18,XCMPLX_0:def 7;
          then
A19:      r8"*w7=w1 by EUCLID:33;
          now
            per cases by A14;
            case
              w4=r1*w7;
              then r1"*w4=r1"*r1*w7 by EUCLID:34;
              then r1"*w4=1 *w7 by A15,XCMPLX_0:def 7;
              then r1"*w4=w7 by EUCLID:33;
              then w1=r8"*r1"*w4 by A19,EUCLID:34;
              hence contradiction by A8;
            end;
            case
              w7=r1*w4;
              then r1"*w7=r1"*r1*w4 by EUCLID:34;
              then r1"*w7=1 *w4 by A15,XCMPLX_0:def 7;
              then r1"*w7=w4 by EUCLID:33;
              then r1""*w4=r1""*r1"*w7 by EUCLID:34;
              then r1""*w4=1 *w7 by A15,XCMPLX_0:def 7;
              then r1""*w4=w7 by EUCLID:33;
              then w1=r8"*r1""*w4 by A19,EUCLID:34;
              hence contradiction by A8;
            end;
          end;
          hence contradiction;
        end;
      end;
      hence contradiction;
    end;
    then
A20: ex w29,w39 being Point of TOP-REAL n st w29 in Q & w39 in Q & LSeg(w4,
    w29) c=Q & LSeg(w29,w39) c= Q & LSeg(w39,w7) c= Q by A1,A13,Th50;
    ex w2,w3 being Point of TOP-REAL n st w2 in Q & w3 in Q & LSeg(w1,w2)
    c=Q & LSeg(w2,w3) c= Q & LSeg(w3,w4) c= Q by A1,A13,A8,Th50;
    hence thesis by A13,A20;
  end;
  suppose
A21: a<=0;
    set w2=0.TOP-REAL n;
A22: REAL n c= Q
    proof
      let x be set;
A23:  now
        assume x in {q : (|.q.|) < a };
        then ex q being Point of TOP-REAL n st q=x & (|.q.|) < a;
        hence contradiction by A21;
      end;
      assume x in REAL n;
      hence thesis by A1,A23,XBOOLE_0:def 5;
    end;
    the carrier of TOP-REAL n=REAL n by EUCLID:25;
    then
A24: Q=the carrier of TOP-REAL n by A22,XBOOLE_0:def 10;
    then LSeg(w1,w2) c=Q & LSeg(w2,w7) c=Q;
    hence thesis by A24;
  end;
end;

theorem Th58:
  for a being Real st n>=1 holds {q: |.q.| >a} <>{}
proof
  let a be Real;
  assume
A1: n>=1;
  now
    abs(a+1)>=0 & sqrt 1<=sqrt n by A1,COMPLEX1:132,SQUARE_1:94;
    then
A2: abs(a+1)*1<=abs(a+1)*sqrt n by SQUARE_1:83,XREAL_1:66;
A3: a+1<=abs(a+1) by ABSVALUE:11;
    assume not (a+1)*(1.REAL n) in {q : |.q.| > a };
    then
A4: |.(a+1)*(1.REAL n).|<=a;
A5: a<a+1 by XREAL_1:31;
    |.(a+1)*(1.REAL n).|=abs(a+1)*|.(1.REAL n).| by TOPRNS_1:8
      .=abs(a+1)*(sqrt n) by Th35;
    then a+1<= |.(a+1)*(1.REAL n).| by A2,A3,XXREAL_0:2;
    hence contradiction by A4,A5,XXREAL_0:2;
  end;
  hence thesis;
end;

theorem Th59:
  for a being Real, P being Subset of TOP-REAL n st n>=2 & P={q :
  |.q.| > a } holds P is connected
proof
  let a be Real, P be Subset of TOP-REAL n;
  assume
A1: n>=2 & P={q : |.q.| > a };
  then reconsider Q=P as non empty Subset of TOP-REAL n by Th58,XXREAL_0:2;
  for w1,w7 being Point of TOP-REAL n st w1 in Q & w7 in Q & w1<>w7 ex f
  being Function of I[01],((TOP-REAL n) | Q) st f is continuous & w1=f.0 &
  w7=f.1
  proof
    let w1,w7 be Point of TOP-REAL n;
    assume that
A2: w1 in Q & w7 in Q and
    w1<>w7;
    per cases;
    suppose
      not (ex r being Real st w1=r*w7 or w7=r*w1);
      then
      ex w2,w3 being Point of TOP-REAL n st w2 in Q & w3 in Q & LSeg(w1,w2)
      c=Q & LSeg(w2,w3) c= Q & LSeg(w3,w7) c= Q by A1,A2,Th49;
      hence thesis by A2,Th45;
    end;
    suppose
      ex r being Real st w1=r*w7 or w7=r*w1;
      then
      ex w2,w3,w4,w5,w6 being Point of TOP-REAL n st w2 in Q & w3 in Q & w4
in Q & w5 in Q & w6 in Q & LSeg(w1,w2) c=Q & LSeg(w2,w3) c= Q & LSeg(w3,w4) c=
      Q & LSeg(w4,w5) c= Q & LSeg(w5,w6) c=Q & LSeg(w6,w7) c= Q by A1,A2,Th56;
      hence thesis by A2,Th46;
    end;
  end;
  hence thesis by JORDAN1:5;
end;

theorem Th60:
  for a being Real st n>=1 holds (REAL n) \ {q: |.q.| < a} <> {}
proof
  let a be Real;
A1: {q:(|.q.|)>a} c= (REAL n)\{q2:(|.q2.|)<a}
  proof
    let x be set;
    assume x in {q:(|.q.|)>a};
    then consider q such that
A2: q=x and
A3: (|.q.|)>a;
A4: now
      assume x in {q2:(|.q2.|)<a};
      then ex q2 st q2=x & (|.q2.|)<a;
      hence contradiction by A2,A3;
    end;
    q in the carrier of TOP-REAL n;
    then q in REAL n by EUCLID:25;
    hence thesis by A2,A4,XBOOLE_0:def 5;
  end;
  assume n>=1;
  hence thesis by A1,Th58,XBOOLE_1:3;
end;

theorem Th61:
  for a being Real,P being Subset of TOP-REAL n st n>=2 & P=(REAL
  n)\ {q : |.q.| < a } holds P is connected
proof
  let a be Real, P be Subset of TOP-REAL n;
  assume
A1: n>=2 & P=(REAL n)\ {q : |.q.| < a };
  then reconsider Q=P as non empty Subset of TOP-REAL n by Th60,XXREAL_0:2;
  for w1,w7 being Point of TOP-REAL n st w1 in Q & w7 in Q & w1<>w7 ex f
  being Function of I[01],((TOP-REAL n) | Q) st f is continuous & w1=f.0 &
  w7=f.1
  proof
    let w1,w7 be Point of TOP-REAL n;
    assume that
A2: w1 in Q & w7 in Q and
    w1<>w7;
    per cases;
    suppose
      not (ex r being Real st w1=r*w7 or w7=r*w1);
      then
      ex w2,w3 being Point of TOP-REAL n st w2 in Q & w3 in Q & LSeg(w1,w2)
      c=Q & LSeg(w2,w3) c= Q & LSeg(w3,w7) c= Q by A1,A2,Th50;
      hence thesis by A2,Th45;
    end;
    suppose
      ex r being Real st w1=r*w7 or w7=r*w1;
      then
      ex w2,w3,w4,w5,w6 being Point of TOP-REAL n st w2 in Q & w3 in Q & w4
in Q & w5 in Q & w6 in Q & LSeg(w1,w2) c=Q & LSeg(w2,w3) c= Q & LSeg(w3,w4) c=
      Q & LSeg(w4,w5) c= Q & LSeg(w5,w6) c=Q & LSeg(w6,w7) c= Q by A1,A2,Th57;
      hence thesis by A2,Th46;
    end;
  end;
  hence thesis by JORDAN1:5;
end;

theorem Th62:
  for a being Real,n being Element of NAT, P being Subset of
TOP-REAL n st n>=1 & P=(REAL n)\ {q where q is Point of TOP-REAL n: |.q.| < a }
  holds not P is Bounded
proof
  let a be Real,n be Element of NAT,P be Subset of TOP-REAL n;
  assume that
A1: n>=1 and
A2: P=(REAL n)\ {q where q is Point of TOP-REAL n : |.q.| < a };
  per cases;
  suppose
A3: a>0;
    now
      consider p being Element of P;
      assume P is Bounded;
      then consider r being Real such that
A4:   for q being Point of TOP-REAL n st q in P holds |.q.|<r by Th40;
A5:   P<>{} by A1,A2,Th60;
      then p in P;
      then reconsider p as Point of TOP-REAL n;
A6:   |.p.|<r by A4,A5;
A7:   now
        assume not (a+r+1)*(1.REAL n) in (REAL n) \{q where q is Point of
        TOP-REAL n: (|.q.|) < a };
        then
A8:     not ((a+r+1)*(1.REAL n) in (REAL n) & not (a+r+1)*(1.REAL n) in {
        q where q is Point of TOP-REAL n : (|.q.|) < a }) by XBOOLE_0:def 5;
        (a+r+1)*(1.REAL n) in the carrier of TOP-REAL n;
        then
A9:     ex q being Point of TOP-REAL n st q= (a+r+1)*(1.REAL n) & (|.q.|)
        < a by A8,EUCLID:25;
A10:    a+r+1<=abs(a+r+1) by ABSVALUE:11;
        a+r<a+r+1 & a<a+r by A6,XREAL_1:31;
        then
A11:    a<a+r+1 by XXREAL_0:2;
        abs(a+r+1)>=0 & sqrt 1<=sqrt n by A1,COMPLEX1:132,SQUARE_1:94;
        then
A12:    abs(a+r+1)*1<=abs(a+r+1)*sqrt n by SQUARE_1:83,XREAL_1:66;
        |.(a+r+1)*(1.REAL n).|=abs(a+r+1)*|.(1.REAL n).| by TOPRNS_1:8
          .=abs(a+r+1)*(sqrt n) by Th35;
        then a+r+1<= |.(a+r+1)*(1.REAL n).| by A12,A10,XXREAL_0:2;
        hence contradiction by A9,A11,XXREAL_0:2;
      end;
A13:  a+r+1<=abs(a+r+1) by ABSVALUE:11;
      abs(a+r+1)>=0 & sqrt 1<=sqrt n by A1,COMPLEX1:132,SQUARE_1:94;
      then
A14:  abs(a+r+1)*1<=abs(a+r+1)*sqrt n by SQUARE_1:83,XREAL_1:66;
A15:  a+r<a+r+1 by XREAL_1:31;
      |.(a+r+1)*(1.REAL n).| =abs(a+r+1)*|.(1.REAL n).| by TOPRNS_1:8
        .=abs(a+r+1)*(sqrt n) by Th35;
      then a+r+1<= |.(a+r+1)*(1.REAL n).| by A14,A13,XXREAL_0:2;
      then
A16:  a+r<|.((a+r+1)*(1.REAL n)).| by A15,XXREAL_0:2;
      r<r+a by A3,XREAL_1:31;
      hence contradiction by A2,A4,A7,A16,XXREAL_0:2;
    end;
    hence thesis;
  end;
  suppose
A17: a<=0;
    now
      {q where q is Point of TOP-REAL n: (|.q.|) < a } c= the carrier of
      TOP-REAL n
      proof
        let z;
        assume z in {q where q is Point of TOP-REAL n: (|.q.|) < a };
        then ex q being Point of TOP-REAL n st q=z & (|.q.|) < a;
        hence thesis;
      end;
      then reconsider
      Q={q where q is Point of TOP-REAL n: (|.q.|) < a } as Subset
      of TOP-REAL n;
      consider d being Element of Q;
      assume {q where q is Point of TOP-REAL n: (|.q.|) < a }<>{};
      then d in {q where q is Point of TOP-REAL n: (|.q.|) < a };
      then ex q being Point of TOP-REAL n st q=d & (|.q.|) < a;
      hence contradiction by A17;
    end;
    then P=[#](TOP-REAL n) by A2,EUCLID:25;
    hence thesis by A1,Th41;
  end;
end;

theorem Th63:
  for a being Real,P being Subset of TOP-REAL 1 st P={q where q is
  Point of TOP-REAL 1: ex r st q=<*r*> & r > a } holds P is convex
proof
  let a be Real,P be Subset of TOP-REAL 1;
  assume
A1: P={q where q is Point of TOP-REAL 1: ex r st q=<*r*> & r > a };
  for w1,w2 being Point of TOP-REAL 1 st w1 in P & w2 in P holds LSeg(w1,
  w2) c= P
  proof
    let w1,w2 be Point of TOP-REAL 1;
    assume that
A2: w1 in P and
A3: w2 in P;
    consider q2 being Point of TOP-REAL 1 such that
A4: q2=w2 and
A5: ex r st q2=<*r*> & r > a by A1,A3;
    consider q1 being Point of TOP-REAL 1 such that
A6: q1=w1 and
A7: ex r st q1=<*r*> & r > a by A1,A2;
    consider r2 such that
A8: q2=<*r2*> and
A9: r2 > a by A5;
    consider r1 such that
A10: q1=<*r1*> and
A11: r1 > a by A7;
    thus LSeg(w1,w2) c= P
    proof
      let x be set;
      assume x in LSeg(w1,w2);
      then consider r3 being Real such that
A12:  x=(1-r3)*w1+r3*w2 and
A13:  0<=r3 and
A14:  r3<=1;
A15:  1-r3>=0 by A14,XREAL_1:50;
      per cases;
      suppose
A16:    r3>0;
A17:    (1-r3)*r1>=(1-r3)*a & (1-r3)*a+r3*a=a by A11,A15,XREAL_1:66;
        r3*r2>r3*a by A9,A16,XREAL_1:70;
        then
A18:    (1-r3)*r1+r3*r2>a by A17,XREAL_1:10;
        <*(1-r3)*r1+r3*r2*>=|[(1-r3)*r1]|+|[r3*r2]| by JORDAN2B:27
          .=(1-r3)*|[r1]|+|[r3*r2]| by JORDAN2B:26
          .=(1-r3)*|[r1]|+r3*|[r2]| by JORDAN2B:26;
        hence thesis by A1,A6,A10,A4,A8,A12,A18;
      end;
      suppose
        r3<=0;
        then r3=0 by A13;
        then x=w1+0 *w2 by A12,EUCLID:33
          .=w1+0.TOP-REAL 1 by EUCLID:33
          .=w1 by EUCLID:31;
        hence thesis by A2;
      end;
    end;
  end;
  hence thesis by JORDAN1:def 1;
end;

theorem Th64:
  for a being Real,P being Subset of TOP-REAL 1 st P={q where q is
  Point of TOP-REAL 1 : ex r st q=<*r*> & r < -a } holds P is convex
proof
  let a be Real,P be Subset of TOP-REAL 1;
  assume
A1: P={q where q is Point of TOP-REAL 1 : ex r st q=<*r*> & r < -a };
  for w1,w2 being Point of TOP-REAL 1 st w1 in P & w2 in P holds LSeg(w1,
  w2) c= P
  proof
    let w1,w2 be Point of TOP-REAL 1;
    assume that
A2: w1 in P and
A3: w2 in P;
    consider q2 being Point of TOP-REAL 1 such that
A4: q2=w2 and
A5: ex r st q2=<*r*> & r < -a by A1,A3;
    consider q1 being Point of TOP-REAL 1 such that
A6: q1=w1 and
A7: ex r st q1=<*r*> & r < -a by A1,A2;
    consider r2 such that
A8: q2=<*r2*> and
A9: r2 < -a by A5;
    consider r1 such that
A10: q1=<*r1*> and
A11: r1 < -a by A7;
    thus LSeg(w1,w2) c= P
    proof
      let x be set;
      assume x in LSeg(w1,w2);
      then consider r3 being Real such that
A12:  x=(1-r3)*w1+r3*w2 and
A13:  0<=r3 and
A14:  r3<=1;
A15:  1-r3>=0 by A14,XREAL_1:50;
      per cases;
      suppose
A16:    r3>0;
A17:    (1-r3)*r1<=(1-r3)*(-a) & (1-r3)*(-a)+r3*(-a)=-a by A11,A15,XREAL_1:66;
        r3*r2<r3*(-a) by A9,A16,XREAL_1:70;
        then
A18:    (1-r3)*r1+r3*r2< -a by A17,XREAL_1:10;
        <*(1-r3)*r1+r3*r2*>=|[(1-r3)*r1]|+|[r3*r2]| by JORDAN2B:27
          .=(1-r3)*|[r1]|+|[r3*r2]| by JORDAN2B:26
          .=(1-r3)*|[r1]|+r3*|[r2]| by JORDAN2B:26;
        hence thesis by A1,A6,A10,A4,A8,A12,A18;
      end;
      suppose
        r3<=0;
        then r3=0 by A13;
        then x=w1+0 *w2 by A12,EUCLID:33
          .=w1+0.TOP-REAL 1 by EUCLID:33
          .=w1 by EUCLID:31;
        hence thesis by A2;
      end;
    end;
  end;
  hence thesis by JORDAN1:def 1;
end;

theorem
  for a being Real,P being Subset of TOP-REAL 1 st P={q where q is Point
of TOP-REAL 1 : ex r st q=<*r*> & r > a } holds P is connected by Th63,
JORDAN1:9;

theorem
  for a being Real,P being Subset of TOP-REAL 1 st P={q where q is Point
of TOP-REAL 1 : ex r st q=<*r*> & r < -a } holds P is connected by Th64,
JORDAN1:9;

theorem Th67:
  for W being Subset of Euclid 1,a being Real, P being Subset of
TOP-REAL 1 st W={q where q is Point of TOP-REAL 1 : ex r st q=<*r*> & r > a } &
  P=W holds P is connected & W is not bounded
proof
  let W be Subset of Euclid 1,a be Real, P be Subset of TOP-REAL 1;
  assume
A1: W={q where q is Point of TOP-REAL 1 : ex r st q=<*r*> & r > a } & P= W;
  hence P is connected by Th63,JORDAN1:9;
  now
    abs(a)>=0 by COMPLEX1:132;
    then
A2: abs(a)+abs(a)+abs(a)>=0+abs(a) by XREAL_1:8;
    assume W is bounded;
    then consider r such that
A3: 0<r and
A4: for x,y being Point of Euclid 1 st x in W & y in W holds dist(x,y)
    <= r by TBSP_1:def 9;
A5: (r+abs(a))*(1.REAL 1) = (r+abs(a))*<*1 qua Real *> by EUCLID:69,FINSEQ_2:73
      .=<*((r+abs(a))*1)*> by RVSUM_1:69;
    then reconsider z2=(r+abs(a))*(1.REAL 1) as Point of Euclid 1
     by FINSEQ_2:151;
    a<=abs(a) & 0+abs(a)<r+abs(a) by A3,ABSVALUE:11,XREAL_1:8;
    then a<r+abs(a) by XXREAL_0:2;
    then
A6: (r+abs(a))*(1.REAL 1) in W by A1,A5;
A7: (3*(r+abs(a)))*(1.REAL 1) = (3*(r+abs(a)))*<*1 qua Real *> by EUCLID:69
,FINSEQ_2:73
      .=<*((3*(r+abs(a)))*1)*> by RVSUM_1:69;
    then reconsider z1=(3*(r+abs(a)))*(1.REAL 1) as Point of Euclid 1
     by FINSEQ_2:151;
    dist(z1,z2)=|.(3*(r+abs(a)))*(1.REAL 1)-((r+abs(a))*(1.REAL 1)).| by
JGRAPH_1:45
      .=|.((r+abs(a))+(r+abs(a))+(r+abs(a))-(r+abs(a)))*(1.REAL 1).| by
EUCLID:54
      .=abs((r+abs(a))+(r+abs(a)))*|.(1.REAL 1).| by TOPRNS_1:8
      .=abs((r+abs(a))+(r+abs(a)))*(sqrt 1) by Th35;
    then
A8: (r+abs(a))+(r+abs(a))<= dist(z1,z2) by ABSVALUE:11,SQUARE_1:83;
A9: 0<=abs(a) by COMPLEX1:132;
    then (r+abs(a))+0<(r+abs(a))+(r+abs(a)) by A3,XREAL_1:8;
    then
A10: (r+abs(a))<dist(z1,z2) by A8,XXREAL_0:2;
    r+0<=r+abs(a) by A9,XREAL_1:8;
    then
A11: r<dist(z1,z2) by A10,XXREAL_0:2;
    3*r>0 by A3,XREAL_1:131;
    then a<=abs(a) & 0+abs(a)<3*r+3*abs(a) by A2,ABSVALUE:11,XREAL_1:10;
    then a<3*(r+abs(a)) by XXREAL_0:2;
    then (3*(r+abs(a)))*(1.REAL 1) in W by A1,A7;
    hence contradiction by A4,A6,A11;
  end;
  hence thesis;
end;

theorem Th68:
  for W being Subset of Euclid 1,a being Real, P being Subset of
TOP-REAL 1 st W={q where q is Point of TOP-REAL 1 : ex r st q=<*r*> & r < -a }
  & P=W holds P is connected & W is not bounded
proof
  let W be Subset of Euclid 1,a be Real, P be Subset of TOP-REAL 1;
  abs(a)>=0 by COMPLEX1:132;
  then
A1: abs(a)+abs(a)+abs(a)>=0+abs(a) by XREAL_1:8;
  assume
A2: W={q where q is Point of TOP-REAL 1 : ex r st q=<*r*> & r < -a } & P =W;
  hence P is connected by Th64,JORDAN1:9;
  assume W is bounded;
  then consider r such that
A3: 0<r and
A4: for x,y being Point of Euclid 1 st x in W & y in W holds dist(x,y)<=
  r by TBSP_1:def 9;
A5: (-3*(r+abs(a)))*(1.REAL 1) = (-3*(r+abs(a)))*<* 1 qua Real *> by EUCLID:69
,FINSEQ_2:73
    .=<*((-3*(r+abs(a)))*1)*> by RVSUM_1:69;
  then reconsider z1=(-3*(r+abs(a)))*(1.REAL 1) as Point of Euclid 1
   by FINSEQ_2:151;
  3*r>0 by A3,XREAL_1:131;
  then a<=abs(a) & 0+abs(a)<3*r+3*abs(a) by A1,ABSVALUE:11,XREAL_1:10;
  then a<3*(r+abs(a)) by XXREAL_0:2;
  then -a> -(3*(r+abs(a))) by XREAL_1:26;
  then
A6: (-3*(r+abs(a)))*(1.REAL 1) in {q where q is Point of TOP-REAL 1: ex r
  st q=<*r*> & r< -a } by A5;
A7: (-(r+abs(a)))*(1.REAL 1) = (-(r+abs(a)))*<* 1 qua Real *> by EUCLID:69
,FINSEQ_2:73
    .=<* (-(r+abs(a)))*1 *> by RVSUM_1:69;
  then reconsider z2=(-(r+abs(a)))*(1.REAL 1) as Point of Euclid 1
   by FINSEQ_2:151;
  dist(z1,z2)=|.(-3*(r+abs(a)))*(1.REAL 1)-(-(r+abs(a)))*(1.REAL 1).| by
JGRAPH_1:45
    .=|.(-3*(r+abs(a))--(r+abs(a)))*(1.REAL 1).| by EUCLID:54
    .=|.-((-3*(r+abs(a))--(r+abs(a)))*(1.REAL 1)).| by TOPRNS_1:27
    .=|.(-(-3*(r+abs(a))+--(r+abs(a))))*(1.REAL 1).| by EUCLID:44
    .=abs((r+abs(a))+(r+abs(a)))*|.(1.REAL 1).| by TOPRNS_1:8
    .=abs((r+abs(a))+(r+abs(a)))*(sqrt 1) by Th35;
  then
A8: (r+abs(a))+(r+abs(a))<= dist(z1,z2) by ABSVALUE:11,SQUARE_1:83;
A9: 0<=abs(a) by COMPLEX1:132;
  then (r+abs(a))+0<(r+abs(a))+(r+abs(a)) by A3,XREAL_1:8;
  then
A10: (r+abs(a))<dist(z1,z2) by A8,XXREAL_0:2;
  r+0<=r+abs(a) by A9,XREAL_1:8;
  then
A11: r<dist(z1,z2) by A10,XXREAL_0:2;
  a<=abs(a) & 0+abs(a)<r+abs(a) by A3,ABSVALUE:11,XREAL_1:8;
  then a<r+abs(a) by XXREAL_0:2;
  then -a> -(r+abs(a)) by XREAL_1:26;
  then (-(r+abs(a)))*(1.REAL 1) in W by A2,A7;
  hence contradiction by A2,A4,A6,A11;
end;

theorem Th69:
  for W being Subset of Euclid n,a being Real, P being Subset of
  TOP-REAL n st n>=2 & W={q : |.q.| > a } & P=W holds P is connected & W is not
  bounded
proof
  let W be Subset of Euclid n,a be Real, P be Subset of TOP-REAL n;
  assume
A1: n>=2 & W={q : |.q.| > a } & P=W;
  hence P is connected by Th59;
A2: 1<=n by A1,XXREAL_0:2;
  then
A3: 1<=sqrt n by SQUARE_1:83,94;
  assume W is bounded;
  then consider r such that
A4: 0<r and
A5: for x,y being Point of Euclid n st x in W & y in W holds dist(x,y)<=
  r by TBSP_1:def 9;
A6: (r+abs(a))<=abs(r+abs a) by ABSVALUE:11;
  abs(r+abs a)>=0 & 1<=sqrt n by A2,COMPLEX1:132,SQUARE_1:83,94;
  then
A7: abs(r+abs a)*1<=abs(r+abs a)*sqrt n by XREAL_1:66;
  a<=abs(a) & abs(a)<r+abs(a) by A4,ABSVALUE:11,XREAL_1:31;
  then
A8: a<r+abs(a) by XXREAL_0:2;
  |.-((r+abs(a))*(1.REAL n)).| = |.((r+abs(a))*(1.REAL n)).| by TOPRNS_1:27
    .=abs(r+abs a)*|.(1.REAL n).| by TOPRNS_1:8
    .=abs(r+abs a)*(sqrt n) by Th35;
  then (r+abs(a))<= |.-((r+abs(a))*(1.REAL n)).| by A7,A6,XXREAL_0:2;
  then a<|.-((r+abs(a))*(1.REAL n)).| by A8,XXREAL_0:2;
  then
A9: -((r+abs(a))*(1.REAL n)) in W by A1;
  then reconsider z2=-((r+abs(a))*(1.REAL n)) as Point of Euclid n;
A10: (r+abs(a))<=abs(r+abs(a)) by ABSVALUE:11;
  abs(r+abs(a))>=0 by COMPLEX1:132;
  then
A11: abs(r+abs(a))*1<=abs(r+abs(a))*sqrt n by A3,XREAL_1:66;
  |.(r+abs(a))*(1.REAL n).|=abs(r+abs(a))*|.(1.REAL n).| by TOPRNS_1:8
    .=abs(r+abs(a))*(sqrt n) by Th35;
  then (r+abs(a))<= |.(r+abs(a))*(1.REAL n).| by A11,A10,XXREAL_0:2;
  then a<|.(r+abs(a))*(1.REAL n).| by A8,XXREAL_0:2;
  then
A12: (r+abs(a))*(1.REAL n) in W by A1;
  then reconsider z1=(r+abs(a))*(1.REAL n) as Point of Euclid n;
A13: (r+abs(a))+(r+abs(a))<=abs((r+abs(a))+(r+abs(a))) by ABSVALUE:11;
  abs((r+abs(a))+(r+abs(a)))>=0 by COMPLEX1:132;
  then
A14: abs((r+abs(a))+(r+abs(a)))*1<=abs((r+abs(a))+(r+abs(a)))*sqrt n by A3,
XREAL_1:66;
A15: 0<=abs(a) by COMPLEX1:132;
  then
A16: r+0<=r+abs(a) by XREAL_1:8;
A17: (r+abs(a))+0<(r+abs(a))+(r+abs(a)) by A4,A15,XREAL_1:8;
  dist(z1,z2)=|.(r+abs(a))*(1.REAL n)--((r+abs(a))*(1.REAL n)).| by JGRAPH_1:45
    .=|.(r+abs(a))*(1.REAL n)+((r+abs(a))*(1.REAL n)).| by EUCLID:39
    .=|.((r+abs(a))+(r+abs(a)))*(1.REAL n).| by EUCLID:37
    .=abs(((r+abs(a))+(r+abs(a))))*|.(1.REAL n).| by TOPRNS_1:8
    .=abs((r+abs(a))+(r+abs(a)))*(sqrt n) by Th35;
  then (r+abs(a))+(r+abs(a))<= dist(z1,z2) by A14,A13,XXREAL_0:2;
  then (r+abs(a))<dist(z1,z2) by A17,XXREAL_0:2;
  then r<dist(z1,z2) by A16,XXREAL_0:2;
  hence contradiction by A5,A12,A9;
end;

theorem Th70:
  for W being Subset of Euclid n,a being Real, P being Subset of
TOP-REAL n st n>=2 & W=(REAL n)\ {q : (|.q.|) < a } & P=W holds P is connected
  & W is not bounded
proof
  let W be Subset of Euclid n,a be Real, P be Subset of TOP-REAL n;
  reconsider 1R = 1.REAL n as Point of TOP-REAL n;
  assume
A1: n>=2 & W=(REAL n)\ {q : (|.q.|) < a } & P=W;
  hence P is connected by Th61;
  assume W is bounded;
  then consider r such that
A2: 0<r and
A3: for x,y being Point of Euclid n st x in W & y in W holds dist(x,y)<=
  r by TBSP_1:def 9;
A4: 0<=abs(a) by COMPLEX1:132;
  then
A5: (r+abs(a))+0<(r+abs(a))+(r+abs(a)) by A2,XREAL_1:8;
  n>=1 by A1,XXREAL_0:2;
  then
A6: 1<=sqrt n by SQUARE_1:83,94;
A7: now
    a<=abs(a) & abs(a)<r+abs(a) by A2,ABSVALUE:11,XREAL_1:31;
    then
A8: a<r+abs(a) by XXREAL_0:2;
    assume -((r+abs(a))*(1.REAL n)) in {q : (|.q.|) < a };
    then
A9: ex q being Point of TOP-REAL n st q=-((r+abs(a))*(1.REAL n)) & (|.q.|) < a;
    abs(r+abs a)>=0 by COMPLEX1:132;
    then
A10: abs(r+abs a)*1<=abs(r+abs a)*sqrt n by A6,XREAL_1:66;
A11: (r+abs(a))<=abs(r+abs a) by ABSVALUE:11;
    |.-((r+abs(a))*(1.REAL n)).| = |.((r+abs(a))*(1.REAL n)).| by TOPRNS_1:27
      .=abs(r+abs a)*|.(1.REAL n).| by TOPRNS_1:8
      .=abs(r+abs a)*(sqrt n) by Th35;
    then (r+abs(a))<= |.-((r+abs(a))*(1.REAL n)).| by A10,A11,XXREAL_0:2;
    hence contradiction by A9,A8,XXREAL_0:2;
  end;
A12: now
    a<=abs(a) & abs(a)<r+abs(a) by A2,ABSVALUE:11,XREAL_1:31;
    then
A13: a<r+abs(a) by XXREAL_0:2;
    assume (r+abs(a))*(1.REAL n) in {q : (|.q.|) < a };
    then
A14: ex q being Point of TOP-REAL n st q=(r+abs(a))*(1.REAL n) & (|.q.|) < a;
    abs(r+abs a)>=0 by COMPLEX1:132;
    then
A15: abs(r+abs a)*1<=abs(r+abs a)*sqrt n by A6,XREAL_1:66;
A16: (r+abs(a))<=abs(r+abs a) by ABSVALUE:11;
    |.(r+abs(a))*(1.REAL n).|=abs(r+abs a)*|.(1.REAL n).| by TOPRNS_1:8
      .=abs(r+abs a)*(sqrt n) by Th35;
    then (r+abs(a))<= |.(r+abs(a))*(1.REAL n).| by A15,A16,XXREAL_0:2;
    hence contradiction by A14,A13,XXREAL_0:2;
  end;
  reconsider z2=-((r+abs(a))*(1.REAL n)) as Point of Euclid n by EUCLID:25;
  reconsider z1=(r+abs(a))*(1.REAL n) as Point of Euclid n by EUCLID:25;
A17: (r+abs(a))+(r+abs(a))<=abs((r+abs(a))+(r+abs(a))) by ABSVALUE:11;
  abs(((r+abs(a))+(r+abs(a))))>=0 by COMPLEX1:132;
  then
A18: abs((r+abs(a))+(r+abs(a)))*1<=abs((r+abs(a))+(r+abs(a)))*sqrt n by A6,
XREAL_1:66;
  dist(z1,z2)=|.(r+abs(a))*(1.REAL n)--((r+abs(a))*(1.REAL n)).| by JGRAPH_1:45
    .=|.(r+abs a)*1R + --((r+abs a)*1R).|
    .=|.(r+abs a)*1R+((r+abs a)*1R).| by EUCLID:39
    .=|.((r+abs(a))+(r+abs(a)))*1R .| by EUCLID:37
    .=|.((r+abs(a))+(r+abs(a)))*(1.REAL n).|
    .=abs(((r+abs(a))+(r+abs(a))))*|.(1.REAL n).| by TOPRNS_1:8
    .=abs(((r+abs(a))+(r+abs(a))))*(sqrt n) by Th35;
  then (r+abs(a))+(r+abs(a))<= dist(z1,z2) by A18,A17,XXREAL_0:2;
  then
A19: (r+abs(a))<dist(z1,z2) by A5,XXREAL_0:2;
  r+0<=r+abs(a) by A4,XREAL_1:8;
  then
A20: r<dist(z1,z2) by A19,XXREAL_0:2;
  -((r+abs(a))*(1.REAL n)) in the carrier of TOP-REAL n;
  then -((r+abs(a))*(1.REAL n)) in REAL n by EUCLID:25;
  then
A21: -((r+abs(a))*(1.REAL n)) in W by A1,A7,XBOOLE_0:def 5;
  (r+abs(a))*(1.REAL n) in the carrier of TOP-REAL n;
  then (r+abs(a))*(1.REAL n) in REAL n by EUCLID:25;
  then (r+abs(a))*(1.REAL n) in W by A1,A12,XBOOLE_0:def 5;
  hence contradiction by A3,A21,A20;
end;

theorem Th71:
  for P, P1 being Subset of TOP-REAL n, Q being Subset of TOP-REAL
n, W being Subset of Euclid n st P=W & P is connected & W is not bounded & P1=
  Component_of (Down(P,Q`)) & W misses Q holds P1 is_outside_component_of Q
proof
  let P,P1 be Subset of TOP-REAL n, Q be Subset of TOP-REAL n, W be Subset of
  Euclid n;
  assume that
A1: P=W and
A2: P is connected and
A3: W is not bounded and
A4: P1=Component_of (Down(P,Q`)) and
A5: W misses Q;
A6: (TOP-REAL n) |P is connected by A2,CONNSP_1:def 3;
A7: (Down(P,Q`))=P \ Q by SUBSET_1:32
    .=P by A1,A5,XBOOLE_1:83;
  then reconsider P0=P as Subset of (TOP-REAL n) | Q`;
  reconsider W0=Component_of P0 as Subset of Euclid n by A4,A7,TOPREAL3:13;
  P0 c= Q` by A1,A5,SUBSET_1:43;
  then ((TOP-REAL n) | Q`) |P0=(TOP-REAL n) |P by PRE_TOPC:28;
  then
A8: P0 is connected by A6,CONNSP_1:def 3;
A9: now
    assume for D being Subset of Euclid n st D=P1 holds D is bounded;
    then W0 is bounded by A4,A7;
    hence contradiction by A1,A3,A8,CONNSP_3:1,TBSP_1:21;
  end;
A10: W <> {}Euclid n by A3;
A11: W /\ Q = {} by A5,XBOOLE_0:def 7;
  now
    assume Q`={};
    then Q=({}(the carrier of TOP-REAL n))`;
    hence contradiction by A1,A10,A11,XBOOLE_1:28;
  end;
  then reconsider Q1=Q` as non empty Subset of TOP-REAL n;
  (TOP-REAL n) | Q1 is non empty;
  then Component_of P0 is a_component by A1,A10,A8,CONNSP_3:9;
  hence thesis by A4,A7,A9,Th18;
end;

theorem Th72:
  for A being Subset of Euclid n, B being non empty Subset of
  Euclid n, C being Subset of (Euclid n) | B st A=C & C is bounded holds A is
  bounded
proof
  let A be Subset of Euclid n, B be non empty Subset of Euclid n, C be Subset
  of (Euclid n) | B;
  assume that
A1: A=C and
A2: C is bounded;
  consider r0 being Real such that
A3: 0<r0 and
A4: for x,y being Point of (Euclid n) | B st x in C & y in C holds dist(x,
  y)<=r0 by A2,TBSP_1:def 9;
  for x,y being Point of (Euclid n) st x in A & y in A holds dist(x,y) <= r0
  proof
    let x,y be Point of (Euclid n);
    assume
A5: x in A & y in A;
    then reconsider x0=x,y0=y as Point of (Euclid n) | B by A1;
A6: (the distance of ((Euclid n) | B)).(x0,y0)= (the distance of (Euclid n))
.(x,y) & (the distance of ((Euclid n) | B)).(x0,y0)=dist(x0,y0)
by METRIC_1:def 1,TOPMETR:def 1;
    dist(x0,y0)<=r0 by A1,A4,A5;
    hence thesis by A6,METRIC_1:def 1;
  end;
  hence thesis by A3,TBSP_1:def 9;
end;

theorem Th73:
  for A being Subset of TOP-REAL n st A is compact holds A is Bounded
proof
  let A be Subset of TOP-REAL n;
  assume
A1: A is compact;
  A c= the carrier of ((TOP-REAL n) | A) by PRE_TOPC:29;
  then reconsider A2=A as Subset of ((TOP-REAL n) | A);
  per cases;
  suppose
    A<>{};
    then reconsider A1=A as non empty Subset of Euclid n by TOPREAL3:13;
    [#]((TOP-REAL n) | A)=A2 by PRE_TOPC:def 10;
    then [#]((TOP-REAL n) | A) is compact by A1,COMPTS_1:11;
    then
A2: (TOP-REAL n) | A is compact by COMPTS_1:10;
    TopSpaceMetr((Euclid n) | A1)=(TOP-REAL n) | A by EUCLID:67;
    then (Euclid n) | A1 is totally_bounded by A2,TBSP_1:12;
    then
A3: (Euclid n) | A1 is bounded by TBSP_1:26;
    [#]((Euclid n) | A1) =A1 by TOPMETR:def 2;
    then A1 is bounded by A3,Th72;
    hence thesis by Def2;
  end;
  suppose
    A={};
    then A = {}(Euclid n);
    hence thesis by Def2;
  end;
end;

registration
  let n be Element of NAT;
  cluster compact -> Bounded Subset of TOP-REAL n;
  coherence by Th73;
end;

theorem Th74:
  for A being Subset of TOP-REAL n st 1<=n & A is Bounded holds A` <> {}
proof
  let A be Subset of TOP-REAL n;
  assume that
A1: 1<=n and
A2: A is Bounded;
  consider r being Real such that
A3: for q being Point of TOP-REAL n st q in A holds |.q.|<r by A2,Th40;
  abs(r)>=0 by COMPLEX1:132;
  then
A4: abs(r)*|.1.REAL n.|>=abs(r)*1 by A1,Th38,XREAL_1:66;
  |.r*(1.REAL n).|=abs(r)*|.1.REAL n.| & r<=abs(r) by ABSVALUE:11,TOPRNS_1:8;
  then not r*(1.REAL n) in A by A3,A4,XXREAL_0:2;
  hence thesis by XBOOLE_0:def 5;
end;

theorem Th75:
  for r being Real holds (ex B being Subset of Euclid n st B={q :
(|.q.|) < r }) & for A being Subset of Euclid n st A={q1 : (|.q1.|) < r } holds
  A is bounded
proof
  let r be Real;
A1: {q : (|.q.|) < r } c= the carrier of Euclid n
  proof
    let x be set;
    assume x in {q : (|.q.|) < r };
    then ex q being Point of TOP-REAL n st q=x & (|.q.|) < r;
    then x in the carrier of TOP-REAL n;
    hence thesis by TOPREAL3:13;
  end;
  hence ex B being Subset of Euclid n st B={q : (|.q.|) < r };
  reconsider C={q1 : (|.q1.|) < r } as Subset of TOP-REAL n by A1,TOPREAL3:13;
  let A be Subset of Euclid n;
  for q being Point of TOP-REAL n st q in C holds |.q.|<r
  proof
    let q be Point of TOP-REAL n;
    assume q in C;
    then ex q1 being Point of TOP-REAL n st q1=q & (|.q1.|) < r;
    hence thesis;
  end;
  then
A2: C is Bounded by Th40;
  assume A={q1 : (|.q1.|) < r };
  hence thesis by A2,Def2;
end;

theorem Th76:
  for A being Subset of TOP-REAL n st n>=2 & A is Bounded holds
  UBD A is_outside_component_of A
proof
  let A be Subset of TOP-REAL n;
  assume that
A1: n>=2 and
A2: A is Bounded;
  reconsider C=A as bounded Subset of Euclid n by A2,Def2;
  per cases;
  suppose
A3: C<>{};
    consider x0 being Element of C;
A4: x0 in C by A3;
    then reconsider q1=x0 as Point of TOP-REAL n;
    reconsider o=0.TOP-REAL n as Point of Euclid n by EUCLID:71;
    reconsider x0 as Point of Euclid n by A4;
    consider r being Real such that
    0<r and
A5: for x,y being Point of (Euclid n) st x in C & y in C holds dist(x,
    y) <= r by TBSP_1:def 9;
    set R0=r+dist(o,x0)+1;
    reconsider W=(REAL n)\{q where q is Point of TOP-REAL n: (|.q.|) < R0 } as
    Subset of Euclid n;
A6: now
      assume W meets A;
      then consider z being set such that
A7:   z in W and
A8:   z in A by XBOOLE_0:3;
A9:   not z in {q where q is Point of TOP-REAL n:(|.q.|) < R0 } by A7,
XBOOLE_0:def 5;
      reconsider z as Point of Euclid n by A7;
      dist(x0,z)<=r by A5,A8;
      then
      dist(o,z)<=dist(o,x0)+dist(x0,z) & dist(o,x0)+dist(x0,z)<=dist(o,x0
      )+r by METRIC_1:4,XREAL_1:8;
      then
A10:  dist(o,z)<=dist(o,x0)+r by XXREAL_0:2;
      reconsider q1=z as Point of TOP-REAL n by TOPREAL3:13;
A11:  |.q1-(0.TOP-REAL n).|=dist(o,z) by JGRAPH_1:45;
      (|.q1.|) >= r+dist(o,x0)+1 by A9;
      then dist(o,z)>=r+dist(o,x0)+1 by A11,RLVECT_1:26;
      then r+(dist(o,x0)+1)<=r+dist(o,x0) by A10,XXREAL_0:2;
      then dist(o,x0)+1<=dist(o,x0)+0 by XREAL_1:8;
      hence contradiction by XREAL_1:8;
    end;
    reconsider P=W as Subset of TOP-REAL n by TOPREAL3:13;
    reconsider P as Subset of TOP-REAL n;
    the carrier of (TOP-REAL n) | A`=A` by PRE_TOPC:29;
    then reconsider P1=Component_of (Down(P,A`)) as Subset of TOP-REAL n by
XBOOLE_1:1;
A12: P is connected by A1,Th70;
A13: UBD A c= P1
    proof
A14:  (TOP-REAL n) |P is connected by A12,CONNSP_1:def 3;
A15:  P c= A` by A6,SUBSET_1:43;
      then Down(P,A`)=P by XBOOLE_1:28;
      then ((TOP-REAL n) | A`) | Down(P,A`) is connected by A15,A14,PRE_TOPC:28
;
      then
A16:  Down(P,A`) is connected by CONNSP_1:def 3;
      reconsider G=A` as non empty Subset of TOP-REAL n by A1,A2,Th74,
XXREAL_0:2;
      let z be set;
A17:  (TOP-REAL n) | G is non empty TopSpace;
      assume z in UBD A;
      then consider y being set such that
A18:  z in y and
A19:  y in {B where B is Subset of TOP-REAL n: B
      is_outside_component_of A} by TARSKI:def 4;
      consider B being Subset of TOP-REAL n such that
A20:  y=B and
A21:  B is_outside_component_of A by A19;
      consider C2 being Subset of ((TOP-REAL n) | (A`)) such that
A22:  C2=B and
A23:  C2 is a_component and
A24:  C2 is not bounded Subset of Euclid n by A21,Th18;
      consider D2 being Subset of Euclid n such that
A25:  D2={q : |.q.| < R0 } by Th75;
      reconsider D2 as Subset of Euclid n;
A26:  A c= D2
      proof
        let z be set;
A27:    |.q1.|=|.q1-0.TOP-REAL n.| by RLVECT_1:26
          .=dist(x0,o) by JGRAPH_1:45;
        assume
A28:    z in A;
        then reconsider q2=z as Point of TOP-REAL n;
        reconsider x1=q2 as Point of Euclid n by TOPREAL3:13;
        |.q2-q1.|=dist(x1,x0) & dist(x1,x0)<=r by A5,A28,JGRAPH_1:45;
        then
A29:    |.q2-q1.|+|.q1.|<=r+dist(o,x0) by A27,XREAL_1:8;
A30:    r+dist(o,x0)<r+dist(o,x0)+1 by XREAL_1:31;
        |.q2.|=|.q2-q1+q1.| & |.q2-q1+q1.|<=|.q2-q1.|+|.q1.| by EUCLID:52
,TOPRNS_1:30;
        then |.q2.|<=r+dist(o,x0) by A29,XXREAL_0:2;
        then |.q2.|<r+dist(o,x0)+1 by A30,XXREAL_0:2;
        hence thesis by A25;
      end;
      the carrier of Euclid n=the carrier of TOP-REAL n by TOPREAL3:13;
      then D2` c= (the carrier of TOP-REAL n)\A by A26,XBOOLE_1:34;
      then
A31:  P /\ D2` c= (P /\ A`) by XBOOLE_1:26;
      now
        reconsider D=C2 as Subset of Euclid n by A22,TOPREAL3:13;
        assume
A32:    W /\ C2={};
A33:    C2 c= {q : (|.q.|) < R0 }
        proof
          let x8 be set;
          assume
A34:      x8 in C2;
          assume not x8 in {q : (|.q.|) < R0 };
          then x8 in W by A22,A24,A34,EUCLID:25,XBOOLE_0:def 5;
          hence contradiction by A32,A34,XBOOLE_0:def 4;
        end;
        not D is bounded by A24;
        hence contradiction by A25,A33,Th75,TBSP_1:21;
      end;
      then Down(P,A`)/\ C2 <>{} by A25,A31,XBOOLE_1:3,26;
      then
A35:  Down(P,A`) meets C2 by XBOOLE_0:def 7;
      C2 is connected by A23,CONNSP_1:def 5;
      then C2 c= Component_of (Down(P,A`)) by A16,A35,A17,CONNSP_3:16;
      hence thesis by A18,A20,A22;
    end;
    P=W;
    then W is not bounded by A1,Th70;
    then P1 is_outside_component_of A & P1 c= UBD A by A12,A6,Th27,Th71;
    hence thesis by A13,XBOOLE_0:def 10;
  end;
  suppose
A36: C={};
    REAL n c= the carrier of Euclid n;
    then reconsider W=REAL n as Subset of Euclid n;
    W /\ A={} by A36;
    then
A37: W misses A by XBOOLE_0:def 7;
    reconsider P=W as Subset of TOP-REAL n by TOPREAL3:13;
    reconsider P as Subset of TOP-REAL n;
    the carrier of (TOP-REAL n) | A`=A` by PRE_TOPC:29;
    then reconsider P1=Component_of Down(P,A`) as Subset of TOP-REAL n by
XBOOLE_1:1;
    [#](TOP-REAL n) is a_component;
    then
A38: [#](the TopStruct of TOP-REAL n) is a_component by CONNSP_1:48;
    W is not bounded by A1,Th39,XXREAL_0:2;
    then
A39: P1 is_outside_component_of A by A37,Th33,Th71;
    A={}(TOP-REAL n) by A36;
    then
A40: UBD A=REAL n by A1,Th42,XXREAL_0:2;
    [#](TOP-REAL n)=REAL n & (TOP-REAL n) | [#](TOP-REAL n)=the TopStruct
    of TOP-REAL n by EUCLID:25,TSEP_1:100;
    hence UBD A is_outside_component_of A by A36,A39,A40,A38,CONNSP_3:7;
  end;
end;

theorem Th77:
  for a being Real, P being Subset of TOP-REAL n st P={q : (|.q.|)
  < a } holds P is convex
proof
  let a be Real, P be Subset of TOP-REAL n;
  assume
A1: P={q : (|.q.|) < a };
  for p1,p2 being Point of TOP-REAL n st p1 in P & p2 in P holds LSeg(p1,
  p2) c= P
  proof
    let p1,p2 be Point of TOP-REAL n;
    assume that
A2: p1 in P and
A3: p2 in P;
A4: ex q2 st q2=p2 & (|.q2.|) < a by A1,A3;
A5: ex q1 st q1=p1 & (|.q1.|) < a by A1,A2;
    LSeg(p1,p2) c= P
    proof
      let x;
      assume
A6:   x in LSeg(p1,p2);
      then consider r such that
A7:   x=(1-r)*p1+r*p2 and
A8:   0<=r and
A9:   r<=1;
A10:  |.(1-r)*p1.|=abs(1-r)*|.p1.| by TOPRNS_1:8;
      reconsider q=x as Point of TOP-REAL n by A6;
A11:  |.(1-r)*p1+r*p2.|<=|.(1-r)*p1.|+|.r*p2.| by TOPRNS_1:30;
A12:  1-r>=0 by A9,XREAL_1:50;
      then
A13:  abs(1-r)=1-r by ABSVALUE:def 1;
      per cases;
      suppose
A14:    1-r>0;
A15:    |.r*p2.|=abs(r)*|.p2.| & r=abs(r) by A8,ABSVALUE:def 1,TOPRNS_1:8;
        0<=abs(r) by COMPLEX1:132;
        then
A16:    abs(r)*|.p2.|<=abs(r)*a by A4,XREAL_1:66;
        abs(1-r)*|.p1.|<abs(1-r)*a by A5,A13,A14,XREAL_1:70;
        then |.(1-r)*p1.|+|.r*p2.|<(1-r)*a+r*a by A10,A13,A16,A15,XREAL_1:10;
        then (|.q.|)<a by A7,A11,XXREAL_0:2;
        hence thesis by A1;
      end;
      suppose
        1-r<=0;
        then 1-r+r=0+r by A12;
        then 0<abs(r) by ABSVALUE:def 1;
        then
A17:    abs(r)*|.p2.|<abs(r)*a by A4,XREAL_1:70;
A18:    r=abs(r) by A8,ABSVALUE:def 1;
        abs(1-r)*|.p1.|<=abs(1-r)*a & |.r*p2.|=abs(r)*|.p2.| by A5,A12,A13,
TOPRNS_1:8,XREAL_1:66;
        then |.(1-r)*p1.|+|.r*p2.|<(1-r)*a+r*a by A10,A13,A17,A18,XREAL_1:10;
        then (|.q.|)<a by A7,A11,XXREAL_0:2;
        hence thesis by A1;
      end;
    end;
    hence thesis;
  end;
  hence thesis by JORDAN1:def 1;
end;

theorem Th78:
  for a being Real,P being Subset of TOP-REAL n st P=Ball(u,a)
  holds P is convex
proof
  let a be Real, P be Subset of TOP-REAL n;
  assume
A1: P=Ball(u,a);
  for p1,p2 being Point of TOP-REAL n st p1 in P & p2 in P holds LSeg(p1,
  p2) c= P
  proof
    reconsider p=u as Point of TOP-REAL n by TOPREAL3:13;
    let p1,p2 be Point of TOP-REAL n;
    assume that
A2: p1 in P and
A3: p2 in P;
A4: P={q where q is Element of Euclid n : dist(u,q) < a} by A1,METRIC_1:18;
    then ex q2 being Point of Euclid n st q2=p2 & dist(u,q2) < a by A3;
    then
A5: |.p-p2.|<a by JGRAPH_1:45;
A6: for p3 being Point of TOP-REAL n st |.p-p3.|<a holds p3 in P
    proof
      let p3 be Point of TOP-REAL n;
      reconsider u3=p3 as Point of Euclid n by TOPREAL3:13;
      assume |.p-p3.|<a;
      then dist(u,u3)<a by JGRAPH_1:45;
      hence thesis by A4;
    end;
    ex q1 being Point of Euclid n st q1=p1 & dist(u,q1) < a by A2,A4;
    then
A7: |.p-p1.|<a by JGRAPH_1:45;
    LSeg(p1,p2) c= P
    proof
      let x;
      assume
A8:   x in LSeg(p1,p2);
      then consider r such that
A9:   x=(1-r)*p1+r*p2 and
A10:  0<=r and
A11:  r<=1;
      reconsider q=x as Point of TOP-REAL n by A8;
A12:  |.(1-r)*(p-p1).|=abs(1-r)*|.(p-p1).| by TOPRNS_1:8;
      (1-r)*p+r*p=((1-r)+r)*p by EUCLID:37
        .=p by EUCLID:33;
      then |.p-((1-r)*p1+r*p2).|=|.(1-r)*p+r*p-(1-r)*p1-r*p2.| by EUCLID:50
        .=|.(1-r)*p+-(1-r)*p1+r*p+-r*p2.| by EUCLID:30
        .=|.(1-r)*p+-(1-r)*p1+(r*p+-r*p2).| by EUCLID:30
        .=|.(1-r)*p+(1-r)*(-p1)+(r*p+-r*p2).| by EUCLID:44
        .=|.(1-r)*(p-p1)+(r*p+-r*p2).| by EUCLID:36
        .=|.(1-r)*(p-p1)+(r*p+r*(-p2)).| by EUCLID:44
        .=|.(1-r)*(p-p1)+r*(p-p2).| by EUCLID:36;
      then
A13:  |.p-((1-r)*p1+r*p2).|<=|.(1-r)*(p-p1).|+|.r*(p-p2).| by TOPRNS_1:30;
A14:  1-r>=0 by A11,XREAL_1:50;
      then
A15:  abs(1-r)=1-r by ABSVALUE:def 1;
      per cases;
      suppose
A16:    1-r>0;
A17:    |.r*(p-p2).|=abs(r)*|.p-p2.| & r=abs(r) by A10,ABSVALUE:def 1
,TOPRNS_1:8;
        0<=abs(r) by COMPLEX1:132;
        then
A18:    abs(r)*|.p-p2.|<=abs(r)*a by A5,XREAL_1:66;
        abs(1-r)*|.p-p1.|<abs(1-r)*a by A7,A15,A16,XREAL_1:70;
        then |.(1-r)*(p-p1).|+|.r*(p-p2).|<(1-r)*a+r*a by A12,A15,A18,A17,
XREAL_1:10;
        then (|.p-q.|)<a by A9,A13,XXREAL_0:2;
        hence thesis by A6;
      end;
      suppose
        1-r<=0;
        then 1-r+r=0+r by A14;
        then 0<abs(r) by ABSVALUE:def 1;
        then
A19:    abs(r)*|.p-p2.|<abs(r)*a by A5,XREAL_1:70;
A20:    r=abs(r) by A10,ABSVALUE:def 1;
        abs(1-r)*|.p-p1.|<=abs(1-r)*a & |.r*(p-p2).|=abs(r)*|.p-p2.| by A7,A14
,A15,TOPRNS_1:8,XREAL_1:66;
        then |.(1-r)*(p-p1).|+|.r*(p-p2).| <(1-r)*a+r*a by A12,A15,A19,A20,
XREAL_1:10;
        then (|.p-q.|)<a by A9,A13,XXREAL_0:2;
        hence thesis by A6;
      end;
    end;
    hence thesis;
  end;
  hence thesis by JORDAN1:def 1;
end;

theorem
  for a being Real,P being Subset of TOP-REAL n st P={q : |.q.| < a }
  holds P is connected by Th77,JORDAN1:9;

reserve R for Subset of TOP-REAL n;
reserve P,Q for Subset of TOP-REAL n;

theorem Th80:
  p <> q & p in Ball(u,r) & q in Ball(u,r) implies ex h being
Function of I[01],TOP-REAL n st h is continuous & h.0=p & h.1=q & rng h c= Ball
  (u,r)
proof
  assume that
A1: p<>q and
A2: p in Ball(u,r) & q in Ball(u,r);
  reconsider Q=Ball(u,r) as Subset of TOP-REAL n by TOPREAL3:13;
  Q is convex by Th78;
  then
A3: LSeg(p,q) c= Ball(u,r) by A2,JORDAN1:def 1;
  reconsider P=LSeg(p,q) as Subset of TOP-REAL n;
  LSeg(p,q) is_an_arc_of p,q by A1,TOPREAL1:15;
  then consider f being Function of I[01], (TOP-REAL n) |P such that
A4: f is being_homeomorphism and
A5: f.0 = p & f.1 = q by TOPREAL1:def 2;
  reconsider h=f as Function of I[01],TOP-REAL n by JORDAN6:4;
  take h;
  rng f = [#]((TOP-REAL n) |P) & f is continuous by A4,TOPS_2:def 5;
  hence thesis by A3,A5,JORDAN6:4,PRE_TOPC:def 10;
end;

theorem Th81:
  for f being Function of I[01],TOP-REAL n st f is continuous & f.
0=p1 & f.1=p2 & p in Ball(u,r) & p2 in Ball(u,r) ex h being Function of I[01],
  TOP-REAL n st h is continuous & h.0=p1 & h.1=p & rng h c= rng f \/ Ball(u,r)
proof
  let f be Function of I[01],TOP-REAL n;
  assume that
A1: f is continuous & f.0=p1 & f.1=p2 and
A2: p in Ball(u,r) & p2 in Ball(u,r);
  per cases;
  suppose
    p2<>p;
    then LSeg(p2,p) is_an_arc_of p2,p by TOPREAL1:15;
    then consider
    f1 being Function of I[01], (TOP-REAL n) | LSeg(p2,p) such that
A3: f1 is being_homeomorphism and
A4: f1.0 = p2 & f1.1 = p by TOPREAL1:def 2;
    reconsider f2=f1 as Function of I[01],TOP-REAL n by JORDAN6:4;
    rng f1 = [#]((TOP-REAL n) | LSeg(p2,p)) by A3,TOPS_2:def 5;
    then rng f2=LSeg(p2,p) by PRE_TOPC:def 10;
    then
A5: rng f \/ rng f2 c= rng f \/ Ball(u,r) by A2,TOPREAL3:28,XBOOLE_1:9;
    f1 is continuous by A3,TOPS_2:def 5;
    then f2 is continuous by JORDAN6:4;
    then
    ex h3 being Function of I[01],(TOP-REAL n) st h3 is continuous & p1=h3
    .0 & p=h3.1 & rng h3 c= rng f \/ rng f2 by A1,A4,BORSUK_2:16;
    hence thesis by A5,XBOOLE_1:1;
  end;
  suppose
    p2=p;
    hence thesis by A1,XBOOLE_1:7;
  end;
end;

theorem Th82:
  for f being Function of I[01],TOP-REAL n st f is continuous &
rng f c=P & f.0=p1 & f.1=p2 & p in Ball(u,r) & p2 in Ball(u,r) & Ball(u,r) c= P
ex f1 being Function of I[01],TOP-REAL n st f1 is continuous & rng f1 c= P & f1
  .0=p1 & f1.1=p
proof
  let f be Function of I[01],TOP-REAL n;
  assume f is continuous & rng f c= P & f.0=p1 & f.1=p2 & p in Ball(u,r) & p2
  in Ball(u,r) & Ball(u,r) c= P;
  then
  (ex f3 being Function of I[01],TOP-REAL n st f3 is continuous & f3.0=p1
  & f3.1=p & rng f3 c= rng f \/ Ball(u,r) )& rng f \/ Ball(u,r) c= P by Th81,
XBOOLE_1:8;
  hence thesis by XBOOLE_1:1;
end;

theorem Th83:
  for p for P being Subset of TOP-REAL n st R is connected & R is
open & P = {q: q<>p & q in R & not ex f being Function of I[01],TOP-REAL n st f
  is continuous & rng f c= R & f.0=p & f.1=q} holds P is open
proof
  let p;
  let P be Subset of TOP-REAL n;
  assume that
A1: R is connected & R is open and
A2: P = {q: q<>p & q in R & not ex f being Function of I[01],TOP-REAL n
  st f is continuous & rng f c= R & f.0=p & f.1=q};
A3: the TopStruct of TOP-REAL n = TopSpaceMetr(Euclid n) by EUCLID:def 8;
  then reconsider P9=P as Subset of TopSpaceMetr(Euclid n);
A4: P c= R
  proof
    let x;
    assume x in P;
    then
    ex q st q=x & q<>p & q in R & not ex f being Function of I[01],TOP-REAL
    n st f is continuous & rng f c= R & f.0=p & f.1=q by A2;
    hence thesis;
  end;
  now
A5: the TopStruct of TOP-REAL n = TopSpaceMetr Euclid n by EUCLID:def 8;
    then reconsider R9=R as Subset of TopSpaceMetr(Euclid n);
    let u;
    reconsider p2=u as Point of TOP-REAL n by TOPREAL3:13;
    assume
A6: u in P;
    R9 is open by A1,A5,PRE_TOPC:60;
    then consider r be real number such that
A7: r>0 and
A8: Ball(u,r) c= R9 by A4,A6,TOPMETR:22;
    take r;
    thus r>0 by A7;
    reconsider r9=r as Real by XREAL_0:def 1;
A9: p2 in Ball(u,r9) by A7,TBSP_1:16;
    Ball(u,r) c= P9
    proof
      assume not thesis;
      then consider x such that
A10:  x in Ball(u,r) and
A11:  not x in P by TARSKI:def 3;
      x in R by A8,A10;
      then reconsider q=x as Point of TOP-REAL n;
      per cases by A2,A8,A10,A11;
      suppose
A12:    q=p;
A13:    now
          assume
A14:      q=p2;
          ex p3 st p3=p2 & p3<>p & p3 in R & not ex f1 being Function of
I[01],TOP-REAL n st f1 is continuous & rng f1 c= R & f1.0=p & f1.1=p3 by A2,A6;
          hence contradiction by A12,A14;
        end;
        u in Ball(u,r9) by A7,TBSP_1:16;
        then
A15:    ex f2 being Function of I[01],TOP-REAL n st f2 is continuous & f2.
        0=q & f2.1=p2 & rng f2 c= Ball(u,r9) by A10,A13,Th80;
        not p2 in P
        proof
          assume p2 in P;
          then ex q4 st q4=p2 & q4<>p & q4 in R & not ex f1 being Function of
I[01],TOP-REAL n st f1 is continuous & rng f1 c= R & f1.0=p & f1.1=q4 by A2;
          hence contradiction by A8,A12,A15,XBOOLE_1:1;
        end;
        hence contradiction by A6;
      end;
      suppose
A16:    ex f1 being Function of I[01],TOP-REAL n st f1 is continuous
        & rng f1 c= R & f1.0=p & f1.1=q;
        not p2 in P
        proof
          assume p2 in P;
          then ex q4 st q4=p2 & q4<>p & q4 in R & not ex f1 being Function of
I[01],TOP-REAL n st f1 is continuous & rng f1 c= R & f1.0=p & f1.1=q4 by A2;
          hence contradiction by A8,A9,A10,A16,Th82;
        end;
        hence contradiction by A6;
      end;
    end;
    hence Ball(u,r) c= P9;
  end;
  then P9 is open by TOPMETR:22;
  hence thesis by A3,PRE_TOPC:60;
end;

theorem Th84:
  for P being Subset of TOP-REAL n st R is connected & R is open &
  p in R & P = {q: q=p or ex f being Function of I[01],TOP-REAL n st f is
  continuous & rng f c= R & f.0=p & f.1=q} holds P is open
proof
  let P be Subset of TOP-REAL n;
  assume that
A1: R is connected & R is open and
A2: p in R and
A3: P = {q: q=p or ex f being Function of I[01],TOP-REAL n st f is
  continuous & rng f c= R & f.0=p & f.1=q};
A4: the TopStruct of TOP-REAL n = TopSpaceMetr(Euclid n) by EUCLID:def 8;
  then reconsider P9=P as Subset of TopSpaceMetr(Euclid n);
  reconsider RR=R as Subset of TopSpaceMetr(Euclid n) by A4;
  now
    let u;
    reconsider p2=u as Point of TOP-REAL n by TOPREAL3:13;
    assume u in P9;
    then consider q1 such that
A5: q1=u and
A6: q1=p or ex f being Function of I[01],TOP-REAL n st f is continuous
    & rng f c= R & f.0=p & f.1=q1 by A3;
A7: now
      per cases by A6;
      suppose
        q1=p;
        hence p2 in R by A2,A5;
      end;
      suppose
        q1<>p & ex f being Function of I[01],TOP-REAL n st f is
        continuous & rng f c= R & f.0=p & f.1=q1;
        then consider f2 being Function of I[01],TOP-REAL n such that
        f2 is continuous and
A8:     rng f2 c= R and
        f2.0=p and
A9:     f2.1=q1;
        dom f2=[.0 qua Real,1 qua Real.] by BORSUK_1:83,FUNCT_2:def 1;
        then 1 in dom f2 by XXREAL_1:1;
        then u in rng f2 by A5,A9,FUNCT_1:def 5;
        hence p2 in R by A8;
      end;
    end;
    RR is open by A1,A4,PRE_TOPC:60;
    then consider r be real number such that
A10: r>0 and
A11: Ball(u,r) c= R by A7,TOPMETR:22;
    take r;
    thus r>0 by A10;
    reconsider r9=r as Real by XREAL_0:def 1;
A12: p2 in Ball(u,r9) by A10,TBSP_1:16;
    thus Ball(u,r) c= P
    proof
      let x;
      assume
A13:  x in Ball(u,r);
      then reconsider q=x as Point of TOP-REAL n by A11,TARSKI:def 3;
      per cases;
      suppose
        q=p;
        hence thesis by A3;
      end;
      suppose
A14:    q<>p;
A15:    now
          assume q1=p;
          then p in Ball(u,r9) by A5,A10,TBSP_1:16;
          then consider f2 being Function of I[01],TOP-REAL n such that
A16:      f2 is continuous & f2.0=p & f2.1=q and
A17:      rng f2 c= Ball(u,r9) by A13,A14,Th80;
          rng f2 c= R by A11,A17,XBOOLE_1:1;
          hence thesis by A3,A16;
        end;
        now
          assume q1<>p;
          then ex f being Function of I[01],TOP-REAL n st f is continuous &
          rng f c= R & f.0=p & f.1=q by A5,A6,A11,A12,A13,Th82;
          hence thesis by A3;
        end;
        hence thesis by A15;
      end;
    end;
  end;
  then P9 is open by TOPMETR:22;
  hence thesis by A4,PRE_TOPC:60;
end;

theorem Th85:
  for R being Subset of TOP-REAL n holds p in R & P={q: q=p or ex
f being Function of I[01],TOP-REAL n st f is continuous & rng f c= R & f.0=p &
  f.1=q} implies P c= R
proof
  let R be Subset of TOP-REAL n;
  assume that
A1: p in R and
A2: P = {q: q=p or ex f being Function of I[01],TOP-REAL n st f is
  continuous & rng f c= R & f.0=p & f.1=q};
  let x;
  assume x in P;
  then consider q such that
A3: q=x and
A4: q=p or ex f being Function of I[01],TOP-REAL n st f is continuous &
  rng f c= R & f.0=p & f.1=q by A2;
  per cases by A4;
  suppose
    q=p;
    hence thesis by A1,A3;
  end;
  suppose
    ex f being Function of I[01],TOP-REAL n st f is continuous & rng f
    c= R & f.0=p & f.1=q;
    then consider f1 being Function of I[01],TOP-REAL n such that
    f1 is continuous and
A5: rng f1 c= R and
    f1.0=p and
A6: f1.1=q;
    reconsider P1=rng f1 as Subset of TOP-REAL n;
    dom f1=[.0 qua Real,1 qua Real.] by BORSUK_1:83,FUNCT_2:def 1;
    then 1 in dom f1 by XXREAL_1:1;
    then q in P1 by A6,FUNCT_1:def 5;
    hence thesis by A3,A5;
  end;
end;

theorem Th86:
  for R being Subset of TOP-REAL n, p being Point of TOP-REAL n st
R is connected & R is open & p in R & P={q: q=p or ex f being Function of I[01]
  ,TOP-REAL n st f is continuous & rng f c= R & f.0=p & f.1=q} holds R c= P
proof
  let R be Subset of TOP-REAL n, p be Point of TOP-REAL n;
  assume that
A1: R is connected & R is open and
A2: p in R and
A3: P = {q: q=p or ex f being Function of I[01],TOP-REAL n st f is
  continuous & rng f c= R & f.0=p & f.1=q};
  reconsider R9 = R as non empty Subset of TOP-REAL n by A2;
A4: p in P by A3;
  set P2 = R \ P;
  reconsider P22=P2 as Subset of TOP-REAL n;
A5: [#]((TOP-REAL n) | R) = R by PRE_TOPC:def 10;
  then reconsider P11 = P, P12 = P22 as Subset of (TOP-REAL n) | R by A2,A3
,Th85,XBOOLE_1:36;
  reconsider P11, P12 as Subset of (TOP-REAL n) | R;
  P \/ (R \ P) = P \/ R by XBOOLE_1:39;
  then
A6: P11 misses P12 & [#]((TOP-REAL n) | R) = P11 \/ P12 by A5,XBOOLE_1:12,79;
  now
    let x;
A7: now
      assume
A8:   x in P2;
      then reconsider q2=x as Point of TOP-REAL n;
      not x in P by A8,XBOOLE_0:def 5;
      then
A9:   q2<>p & not ex f being Function of I[01],TOP-REAL n st f is
      continuous & rng f c= R & f.0=p & f.1=q2 by A3;
      q2 in R by A8,XBOOLE_0:def 5;
      hence x in {q: q<>p & q in R & not ex f being Function of I[01],TOP-REAL
      n st f is continuous & rng f c= R & f.0=p & f.1=q} by A9;
    end;
    now
      assume x in {q: q<>p & q in R & not ex f being Function of I[01],
      TOP-REAL n st f is continuous & rng f c= R & f.0=p & f.1=q};
      then
A10:  ex q3 st q3=x & q3<>p & q3 in R & not ex f being Function of I[01],
      TOP-REAL n st f is continuous & rng f c= R & f.0=p & f.1=q3;
      then not ex q st q=x & (q=p or ex f being Function of I[01],TOP-REAL n
      st f is continuous & rng f c= R & f.0=p & f.1=q);
      then not x in P by A3;
      hence x in P2 by A10,XBOOLE_0:def 5;
    end;
    hence
    x in P2 iff x in {q: q<>p & q in R & not ex f being Function of I[01]
    ,TOP-REAL n st f is continuous & rng f c= R & f.0=p & f.1=q} by A7;
  end;
  then
  P2={q: q<>p & q in R & not ex f being Function of I[01],TOP-REAL n st f
  is continuous & rng f c= R & f.0=p & f.1=q} by TARSKI:2;
  then P22 is open by A1,Th83;
  then
A11: P22 in the topology of TOP-REAL n by PRE_TOPC:def 5;
  reconsider PPP=P as Subset of TOP-REAL n;
  PPP is open by A1,A2,A3,Th84;
  then
A12: P in the topology of TOP-REAL n by PRE_TOPC:def 5;
  P11 = P /\ [#]((TOP-REAL n) | R) by XBOOLE_1:28;
  then P11 in the topology of (TOP-REAL n) | R by A12,PRE_TOPC:def 9;
  then
A13: P11 is open by PRE_TOPC:def 5;
  P12 = P22 /\ [#]((TOP-REAL n) | R) by XBOOLE_1:28;
  then P12 in the topology of ( TOP-REAL n) | R by A11,PRE_TOPC:def 9;
  then
A14: P12 is open by PRE_TOPC:def 5;
  (TOP-REAL n) | R9 is connected by A1,CONNSP_1:def 3;
  then P11 = {}((TOP-REAL n) | R) or P12 = {}((TOP-REAL n) | R) by A6,A13,A14,
CONNSP_1:12;
  hence thesis by A4,XBOOLE_1:37;
end;

theorem Th87:
  for R being Subset of TOP-REAL n, p,q being Point of TOP-REAL n
  st R is connected & R is open & p in R & q in R & p<>q holds ex f being
  Function of I[01],TOP-REAL n st f is continuous & rng f c= R & f.0=p & f.1=q
proof
  let R be Subset of TOP-REAL n, p,q be Point of TOP-REAL n;
  assume that
A1: R is connected & R is open & p in R and
A2: q in R and
A3: p<>q;
  set RR={q2: q2=p or ex f being Function of I[01],TOP-REAL n st f is
  continuous & rng f c= R & f.0=p & f.1=q2};
  RR c= the carrier of TOP-REAL n
  proof
    let x;
    assume x in RR;
    then
    ex q2 st q2=x & (q2=p or ex f being Function of I[01],TOP-REAL n st f
    is continuous & rng f c= R & f.0=p & f.1=q2);
    hence thesis;
  end;
  then R c= RR by A1,Th86;
  then q in RR by A2;
  then
  ex q2 st q=q2 &(q2=p or ex f being Function of I[01],TOP-REAL n st f is
  continuous & rng f c= R & f.0=p & f.1=q2);
  hence thesis by A3;
end;

theorem Th88:
  for A being Subset of TOP-REAL n, a being real number st A={q:
  |.q.|=a} holds A` is open & A is closed
proof
  let A be Subset of TOP-REAL n, a be real number;
  assume
A1: A={q: |.q.|=a};
  reconsider a as Real by XREAL_0:def 1;
A2: the TopStruct of TOP-REAL n = TopSpaceMetr(Euclid n) by EUCLID:def 8;
  then reconsider P1=A` as Subset of TopSpaceMetr(Euclid n);
  for p being Point of Euclid n st p in P1 ex r be real number st r>0 &
  Ball(p,r) c= P1
  proof
    let p be Point of Euclid n;
    reconsider q1=p as Point of TOP-REAL n by TOPREAL3:13;
    assume p in P1;
    then not p in A by XBOOLE_0:def 5;
    then
A3: |.q1.|<>a by A1;
    now
      per cases;
      case
A4:     |.q1.|<=a;
        set r1=(a- |.q1.|)/2;
        |.q1.|<a by A3,A4,XXREAL_0:1;
        then
A5:     a- |.q1.|>0 by XREAL_1:52;
        Ball(p,r1) c= P1
        proof
          let x be set;
          assume
A6:       x in Ball(p,r1);
          then reconsider p2=x as Point of Euclid n;
          reconsider q2=p2 as Point of TOP-REAL n by TOPREAL3:13;
          dist(p,p2)<r1 by A6,METRIC_1:12;
          then
A7:       |.q2-q1.|<r1 by JGRAPH_1:45;
          now
            assume q2 in A;
            then
A8:         ex q st q=q2 & |.q.|=a by A1;
            |.q2-q1.| >=|.q2.|- |.q1.| by TOPRNS_1:33;
            then r1>r1+r1 by A7,A8,XXREAL_0:2;
            then r1-r1>r1 by XREAL_1:22;
            hence contradiction by A5;
          end;
          hence thesis by XBOOLE_0:def 5;
        end;
        hence thesis by A5,XREAL_1:141;
      end;
      case
A9:     |.q1.|>a;
        set r1=(|.q1.|-a)/2;
A10:    |.q1.|-a>0 by A9,XREAL_1:52;
        Ball(p,r1) c= P1
        proof
          let x be set;
          assume
A11:      x in Ball(p,r1);
          then reconsider p2=x as Point of Euclid n;
          reconsider q2=p2 as Point of TOP-REAL n by TOPREAL3:13;
          dist(p,p2)<r1 by A11,METRIC_1:12;
          then
A12:      |.q1-q2.|<r1 by JGRAPH_1:45;
          now
            assume q2 in A;
            then
A13:        ex q st q=q2 & |.q.|=a by A1;
            |.q1-q2.| >=|.q1.|- |.q2.| by TOPRNS_1:33;
            then r1>r1+r1 by A12,A13,XXREAL_0:2;
            then r1-r1>r1 by XREAL_1:22;
            hence contradiction by A10;
          end;
          hence thesis by XBOOLE_0:def 5;
        end;
        hence thesis by A10,XREAL_1:141;
      end;
    end;
    hence thesis;
  end;
  then P1 is open by TOPMETR:22;
  hence A` is open by A2,PRE_TOPC:60;
  hence thesis by TOPS_1:29;
end;

theorem Th89:
  for B being non empty Subset of TOP-REAL n st B is open holds
  (TOP-REAL n) | B is locally_connected
proof
  let B be non empty Subset of TOP-REAL n;
A1: the TopStruct of TOP-REAL n = TopSpaceMetr Euclid n by EUCLID:def 8;
  assume
A2: B is open;
  for A being non empty Subset of ((TOP-REAL n) | B), C being Subset of ((
  TOP-REAL n) | B) st A is open & C is_a_component_of A holds C is open
  proof
    let A be non empty Subset of (TOP-REAL n) | B,
    C be Subset of (TOP-REAL n) | B;
    assume that
A3: A is open and
A4: C is_a_component_of A;
    consider C1 being Subset of ((TOP-REAL n) | B) | A such that
A5: C1 = C and
A6: C1 is a_component by A4,CONNSP_1:def 6;
    C1 c= [#](((TOP-REAL n) | B) |A);
    then
A7: C1 c= A by PRE_TOPC:def 10;
    A c= the carrier of (TOP-REAL n) | B;
    then A c= B by PRE_TOPC:29;
    then C c= B by A5,A7,XBOOLE_1:1;
    then reconsider C0=C as Subset of TOP-REAL n by XBOOLE_1:1;
    reconsider CC = C0 as Subset of TopSpaceMetr Euclid n by A1;
    for p being Point of Euclid n st p in C0 ex r be real number st r>0 &
    Ball(p,r) c= C0
    proof
      consider A0 being Subset of TOP-REAL n such that
A8:   A0 is open and
A9:   A0 /\ [#]((TOP-REAL n) | B) = A by A3,TOPS_2:32;
A10:  A0 /\ B=A by A9,PRE_TOPC:def 10;
A11:  the TopStruct of TOP-REAL n = TopSpaceMetr Euclid n by EUCLID:def 8;
      then reconsider AA=A0 /\ B as Subset of TopSpaceMetr Euclid n;
      let p be Point of Euclid n;
      assume
A12:  p in C0;
      AA is open by A2,A8,A11,PRE_TOPC:60;
      then consider r1 being real number such that
A13:  r1>0 and
A14:  Ball(p,r1) c= AA by A5,A7,A12,A10,TOPMETR:22;
      reconsider r1 as Real by XREAL_0:def 1;
A15:  Ball(p,r1) c= A by A9,A14,PRE_TOPC:def 10;
      then reconsider BL2=Ball(p,r1) as Subset of (TOP-REAL n) | B by
XBOOLE_1:1;
      Ball(p,r1) c= [#](((TOP-REAL n) | B) |A) by A15,PRE_TOPC:def 10;
      then reconsider BL=Ball(p,r1) as Subset of ((TOP-REAL n) | B) |A;
      reconsider BL as Subset of ((TOP-REAL n) | B) |A;
      reconsider BL2 as Subset of (TOP-REAL n) | B;
      reconsider BL1=Ball(p,r1) as Subset of TOP-REAL n by TOPREAL3:13;
      reconsider BL1 as Subset of TOP-REAL n;
      now
        p in BL by A13,GOBOARD6:4;
        then BL /\ C <>{}(((TOP-REAL n) | B) |A) by A12,XBOOLE_0:def 4;
        then
A16:    BL meets C by XBOOLE_0:def 7;
        BL1 is convex by Th78;
        then
A17:    BL2 is connected by Th15;
        assume not Ball(p,r1) c= C0;
        hence contradiction by A5,A6,A17,A16,Th15,CONNSP_1:38;
      end;
      hence thesis by A13;
    end;
    then CC is open by TOPMETR:22;
    then
A18: [#]((TOP-REAL n) | B)=B & C0 is open by A1,PRE_TOPC:60,def 10;
    C c= the carrier of ((TOP-REAL n) | B);
    then C c= B by PRE_TOPC:29;
    then C0 /\ B=C by XBOOLE_1:28;
    hence thesis by A18,TOPS_2:32;
  end;
  hence thesis by CONNSP_2:24;
end;

theorem Th90:
  for B being non empty Subset of TOP-REAL n, A being Subset of
  TOP-REAL n,a being real number st A={q: |.q.|=a} & A`=B holds
  (TOP-REAL n) | B is locally_connected
proof
  let B be non empty Subset of TOP-REAL n, A be Subset of TOP-REAL n,a be real
  number;
  assume
A1: A={q: |.q.|=a} & A`=B;
  then A` is open by Th88;
  hence thesis by A1,Th89;
end;

theorem Th91:
  for f being Function of TOP-REAL n,R^1 st (for q holds f.q=|.q.|
  ) holds f is continuous
proof
  let f be Function of TOP-REAL n,R^1;
A1: the TopStruct of TOP-REAL n = TopSpaceMetr(Euclid n) by EUCLID:def 8;
  then reconsider
  f1=f as Function of TopSpaceMetr(Euclid n),TopSpaceMetr(RealSpace
  ) by TOPMETR:def 7;
  assume
A2: for q holds f.q=|.q.|;
  now
    let r be real number,u be Element of Euclid n,u1 be Element
    of RealSpace;
    assume that
A3: r>0 and
A4: u1=f1.u;
    set s1=r;
    for w being Element of Euclid n, w1 being Element of RealSpace st w1=
    f1.w & dist(u,w)<s1 holds dist(u1,w1)<r
    proof
      let w be Element of Euclid n, w1 be Element of RealSpace;
      assume that
A5:   w1=f1.w and
A6:   dist(u,w)<s1;
      reconsider tu=u1,tw=w1 as Real by METRIC_1:def 14;
      reconsider qw=w,qu=u as Point of TOP-REAL n by TOPREAL3:13;
A7:   dist(u1,w1)=(the distance of RealSpace).(u1,w1) by METRIC_1:def 1
        .=abs(tu-tw) by METRIC_1:def 13,def 14;
A8:   tu=|.qu.| by A2,A4;
      w1=|.qw.| by A2,A5;
      then dist(u,w)=|.qu-qw.| & dist(u1,w1)<=|.qu-qw.| by A7,A8,Th11,
JGRAPH_1:45;
      hence thesis by A6,XXREAL_0:2;
    end;
    hence ex s being real number st s>0 & for w being Element of Euclid n, w1
being Element of RealSpace st w1=f1.w & dist(u,w)<s holds dist(u1,w1)<r by A3;
  end;
  then f1 is continuous by UNIFORM1:4;
  hence thesis by A1,PRE_TOPC:62,TOPMETR:def 7;
end;

theorem Th92:
  ex f being Function of TOP-REAL n,R^1 st (for q holds f.q=|.q.|)
  & f is continuous
proof
  defpred P[set,set] means (ex q st q=$1 & $2=|.q.|);
A1: for x st x in (the carrier of TOP-REAL n) ex y st P[x,y]
  proof
    let x;
    assume x in the carrier of TOP-REAL n;
    then reconsider q3=x as Point of TOP-REAL n;
    take |.q3.|;
    thus thesis;
  end;
  consider f1 being Function such that
A2: dom f1 = (the carrier of TOP-REAL n) & for x st x in (the carrier of
  TOP-REAL n) holds P[x,f1.x] from CLASSES1:sch 1(A1);
  rng f1 c= the carrier of R^1
  proof
    let z be set;
    assume z in rng f1;
    then consider xz being set such that
A3: xz in dom f1 and
A4: z=f1.xz by FUNCT_1:def 5;
    ex q4 being Point of TOP-REAL n st q4=xz & f1.xz=|.q4.| by A2,A3;
    hence thesis by A4,TOPMETR:24;
  end;
  then reconsider f2=f1 as Function of TOP-REAL n,R^1 by A2,FUNCT_2:def 1
,RELSET_1:11;
A5: for q holds f1.q=|.q.|
  proof
    let q;
    ex q2 st q2=q & f1.q=|.q2.| by A2;
    hence thesis;
  end;
  then f2 is continuous by Th91;
  hence thesis by A5;
end;

theorem Th93:
  for g being Function of I[01],TOP-REAL n st g is continuous
holds ex f being Function of I[01],R^1 st (for t being Point of I[01] holds f.t
  =|.g.t.|) & f is continuous
proof
  let g be Function of I[01],TOP-REAL n;
  consider h being Function of TOP-REAL n,R^1 such that
A1: for q holds h.q=|.q.| and
A2: h is continuous by Th92;
  set f1=h*g;
A3: for t being Point of I[01] holds f1.t=|.g.t.|
  proof
    let t be Point of I[01];
    reconsider q=g.t as Point of TOP-REAL n;
    dom g = the carrier of I[01] by FUNCT_2:def 1;
    then f1.t=h.(g.t) by FUNCT_1:23
      .= |.q.| by A1;
    hence thesis;
  end;
  assume g is continuous;
  hence thesis by A2,A3;
end;

theorem Th94:
  for g being Function of I[01],TOP-REAL n,a being Real st g is
continuous & |.g/.0 .|<=a & a<=|.g/.1 .| holds ex s being Point of I[01] st
  |.g/.s.|=a
proof
  reconsider I=1 as Point of I[01] by BORSUK_1:83,XXREAL_1:1;
A1: 0 in [.0 qua Real,1 qua Real .] by XXREAL_1:1;
  reconsider o=0 as Point of I[01] by BORSUK_1:83,XXREAL_1:1;
  let g be Function of I[01],TOP-REAL n,a be Real;
  assume that
A2: g is continuous and
A3: |.g/.0 .|<=a & a<=|.g/.1.|;
  consider f being Function of I[01],R^1 such that
A4: for t being Point of I[01] holds f.t=|.g.t.| and
A5: f is continuous by A2,Th93;
A6: f.0=|.g.o.| by A4
    .=|.g/.0 .| by FUNCT_2:def 14;
  set c = |.g/.0 .|, b=|.g/.1.|;
A7: 1 in the carrier of I[01] by BORSUK_1:83,XXREAL_1:1;
A8: f.1=|.g.I.| by A4
    .=|.g/.1.| by FUNCT_2:def 14;
  per cases by A3,XXREAL_0:1;
  suppose
    c < a & a<b;
    then consider rc being Real such that
A9: f.rc =a and
A10: 0<rc & rc<1 by A5,A6,A8,TOPMETR:27,TOPREAL5:12;
    reconsider rc1=rc as Point of I[01] by A10,BORSUK_1:83,XXREAL_1:1;
A11: rc in the carrier of I[01] by A10,BORSUK_1:83,XXREAL_1:1;
    |.g/.rc.|= |. g.rc1 .| by FUNCT_2:def 14
      .=a by A4,A9;
    hence thesis by A11;
  end;
  suppose
    c =a;
    hence thesis by A1,BORSUK_1:83;
  end;
  suppose
    a=b;
    hence thesis by A7;
  end;
end;

theorem Th95:
  q=<*r*> implies |.q.|=abs(r)
proof
  assume
A1: q=<*r*>;
  reconsider xr=<*r*> as Element of REAL 1 by FINSEQ_2:151;
  len xr=1 by FINSEQ_1:56;
  then xr.1=xr/.1 by FINSEQ_4:24;
  then
A2: q/.1=xr.1 by A1;
  then len (sqr xr) =1 & (sqr xr).1=(q/.1)^2 by FINSEQ_1:def 18,VALUED_1:11;
  then
A3: sqr xr=<*(q/.1)^2*> by FINSEQ_1:57;
  sqrt (q/.1)^2 =abs(q/.1) by COMPLEX1:158
    .=abs(r) by A2,FINSEQ_1:57;
  hence thesis by A1,A3,FINSOP_1:12;
end;

theorem
  for A being Subset of TOP-REAL n,a being Real st n>=1 & a>0 & A={q: |.
  q.|=a} holds BDD A is_inside_component_of A
proof
  let A be Subset of TOP-REAL n,a be Real;
  {q where q is Point of TOP-REAL n: (|.q.|) <a } c= the carrier of TOP-REAL n
  proof
    let x;
    assume x in {q where q is Point of TOP-REAL n: (|.q.|) <a };
    then ex q st q=x & (|.q.|) <a;
    hence thesis;
  end;
  then reconsider
  W={q where q is Point of TOP-REAL n: (|.q.|) <a } as Subset of
  Euclid n by TOPREAL3:13;
  reconsider P=W as Subset of TOP-REAL n by TOPREAL3:13;
  reconsider P as Subset of TOP-REAL n;
A1: the carrier of (TOP-REAL n) | A`=A` by PRE_TOPC:29;
  then reconsider P1=Component_of (Down(P,A`)) as Subset of TOP-REAL n by
XBOOLE_1:1;
  assume
A2: n>=1 & a>0 & A={q: |.q.|=a};
A3: P c= A`
  proof
    let x;
    assume
A4: x in P;
    then reconsider q=x as Point of TOP-REAL n;
A5: ex q1 st q1=q & |.q1.|<a by A4;
    now
      assume q in A;
      then ex q2 st q2=q & |.q2.|=a by A2;
      hence contradiction by A5;
    end;
    hence thesis by XBOOLE_0:def 5;
  end;
  then
A6: Down(P,A`)=P by XBOOLE_1:28;
  P is connected by Th77,JORDAN1:9;
  then (TOP-REAL n) |P is connected by CONNSP_1:def 3;
  then ((TOP-REAL n) | A`) | Down(P,A`) is connected by A3,A6,PRE_TOPC:28;
  then
A7: Down(P,A`) is connected by CONNSP_1:def 3;
  |.0.TOP-REAL n.|=0 by TOPRNS_1:24;
  then
A8: 0.TOP-REAL n in P by A2;
  then reconsider G=A` as non empty Subset of TOP-REAL n by A3;
A9: (TOP-REAL n) | G is non empty;
A10: P c= Component_of (Down(P,A`)) by A8,A6,A7,CONNSP_3:13;
A11: Down(P,A`) <>{} by A3,A8,XBOOLE_0:def 4;
  then
A12: Component_of (Down(P,A`)) is a_component by A9,A7,CONNSP_3:9;
  then
A13: Component_of (Down(P,A`)) is connected by CONNSP_1:def 5;
  Component_of (Down(P,A`)) is bounded Subset of Euclid n
  proof
    reconsider D2=Component_of (Down(P,A`)) as Subset of TOP-REAL n by A1,
XBOOLE_1:1;
    reconsider D=D2 as Subset of Euclid n by TOPREAL3:13;
    reconsider D as Subset of Euclid n;
    now
      reconsider B=A` as non empty Subset of TOP-REAL n by A3,A8;
      set p=0.TOP-REAL n;
      reconsider RR=(TOP-REAL n) | B as non empty TopSpace;
      assume not D2 is Bounded;
      then consider q such that
A14:  q in D2 and
A15:  |.q.|>=a by Th40;
A16:  A` is open & D2 is connected by A2,A13,Th88,CONNSP_1:24;
      D c= the carrier of (TOP-REAL n) | A`;
      then
A17:  D2 c= A` by PRE_TOPC:29;
      then
A18:  D2=Down(D2,A`) by XBOOLE_1:28;
      RR is locally_connected by A2,Th90;
      then Component_of (Down(P,A`)) is open by A11,A7,CONNSP_2:21,CONNSP_3:9;
      then consider G being Subset of TOP-REAL n such that
A19:  G is open and
A20:  Down(D2,A`)=G /\ [#]((TOP-REAL n) | A`) by A18,TOPS_2:32;
A21:  G /\ A` = D2 by A18,A20,PRE_TOPC:def 10;
      p <> q by A2,A15,TOPRNS_1:24;
      then consider f1 being Function of I[01],TOP-REAL n such that
A22:  f1 is continuous and
A23:  rng f1 c= D2 and
A24:  f1.0=p and
A25:  f1.1=q by A8,A10,A14,A19,A21,A16,Th87;
A26:  |.f1/.1.|>=a by A15,A25,BORSUK_1:def 18,FUNCT_2:def 14;
      |.p.|<a by A2,TOPRNS_1:24;
      then |.f1/.0 .|<a by A24,BORSUK_1:def 17,FUNCT_2:def 14;
      then consider t0 being Point of I[01] such that
A27:  |.f1/.t0.|=a by A22,A26,Th94;
      reconsider q2=f1.t0 as Point of TOP-REAL n;
      t0 in [#](I[01]);
      then t0 in dom f1 by FUNCT_2:def 1;
      then q2 in rng f1 by FUNCT_1:def 5;
      then
A28:  q2 in D2 by A23;
      q2 in A by A2,A27;
      then A /\ A`<>{}(the carrier of TOP-REAL n) by A17,A28,XBOOLE_0:def 4;
      then A meets A` by XBOOLE_0:def 7;
      hence contradiction by XBOOLE_1:79;
    end;
    hence thesis by Def2;
  end;
  then
A29: P1 is_inside_component_of A by A12,Th17;
A30: P1 c= union{B where B is Subset of TOP-REAL n: B is_inside_component_of A}
  proof
    let x be set;
    assume
A31: x in P1;
    P1 in {B where B is Subset of TOP-REAL n: B is_inside_component_of A}
    by A29;
    hence thesis by A31,TARSKI:def 4;
  end;
  now
    per cases;
    case
A32:  n>=2;
      union{B where B is Subset of TOP-REAL n: B is_inside_component_of A
      } c= P1
      proof
        reconsider E=A` as non empty Subset of TOP-REAL n by A3,A8;
        let x be set;
        assume x in union{B where B is Subset of TOP-REAL n: B
        is_inside_component_of A};
        then consider y being set such that
A33:    x in y and
A34:    y in {B where B is Subset of TOP-REAL n: B
        is_inside_component_of A } by TARSKI:def 4;
        consider B being Subset of TOP-REAL n such that
A35:    B=y and
A36:    B is_inside_component_of A by A34;
        ex C being Subset of ((TOP-REAL n) | (A`)) st C=B & C
is a_component & C is bounded Subset of Euclid n
by A36,Th17;
        then reconsider p=x as Point of (TOP-REAL n) | A` by A33,A35;
A37:    the carrier of (TOP-REAL n) | A`=A` & p in the carrier of ((
        TOP-REAL n) |E) by PRE_TOPC:29;
        then reconsider q2=p as Point of TOP-REAL n;
        not p in A by A37,XBOOLE_0:def 5;
        then |.q2.|<>a by A2;
        then
A38:    |.q2.|<a or |.q2.|>a by XXREAL_0:1;
        now
          per cases by A38;
          case
A39:        p in {q: |.q.|>a};
            {q: |.q.|>a} c= A`
            proof
              let z be set;
              assume z in {q: |.q.|>a};
              then consider q such that
A40:          q=z and
A41:          |.q.|>a;
              now
                assume q in A;
                then ex q2 st q2=q & |.q2.|=a by A2;
                hence contradiction by A41;
              end;
              hence thesis by A40,XBOOLE_0:def 5;
            end;
            then reconsider Q={q: |.q.|>a} as Subset of (TOP-REAL n) | A` by
PRE_TOPC:29;
            reconsider Q as Subset of (TOP-REAL n) | A`;
            {q: |.q.|>a} c= the carrier of TOP-REAL n
            proof
              let z be set;
              assume z in {q: |.q.|>a};
              then ex q st q=z & |.q.|>a;
              hence thesis;
            end;
            then reconsider P2={q: |.q.|>a} as Subset of TOP-REAL n;
            P2 is Subset of Euclid n by TOPREAL3:13;
            then reconsider W2={q: |.q.|>a} as Subset of Euclid n;
            P2 is connected by A32,Th59;
            then
A42:        (TOP-REAL n) |P2 is connected by CONNSP_1:def 3;
            P2=W2;
            then
A43:        not W2 is bounded by A32,Th69;
A44:        now
              assume W2 meets A;
              then consider z being set such that
A45:          z in W2 & z in A by XBOOLE_0:3;
              ( ex q st q=z & |.q.|>a)& ex q2 st q2=z & |.q2.|=a by A2,A45;
              hence contradiction;
            end;
            then W2 /\ A``={} by XBOOLE_0:def 7;
            then P2\A`={} by SUBSET_1:32;
            then
A46:        W2 c= A` by XBOOLE_1:37;
            then Q=Down(P2,A`) by XBOOLE_1:28;
            then Up(Component_of Q) is_outside_component_of A by A32,A43,A44
,Th59,Th71;
            then
A47:        Component_of Q c= UBD A by Th27;
            (TOP-REAL n) |P2=((TOP-REAL n) | A`) |Q by A46,PRE_TOPC:28;
            then Q is connected by A42,CONNSP_1:def 3;
            then Q c= Component_of Q by CONNSP_3:1;
            then
A48:        p in Component_of Q by A39;
            B c= BDD A by A36,Th26;
            then p in (BDD A) /\ (UBD A) by A33,A35,A47,A48,XBOOLE_0:def 4;
            then (BDD A) meets (UBD A) by XBOOLE_0:4;
            hence thesis by Th28;
          end;
          case
A49:        p in {q1: |.q1.|<a};
            Down(P,A`) c= Component_of (Down(P,A`)) by A7,CONNSP_3:1;
            hence thesis by A6,A49;
          end;
        end;
        hence thesis;
      end;
      then P1=union{B where B is Subset of TOP-REAL n: B
      is_inside_component_of A} by A30,XBOOLE_0:def 10;
      hence ex B being Subset of TOP-REAL n st B is_inside_component_of A & B=
      BDD A by A29;
    end;
    case
      n<2;
      then n<1+1;
      then
A50:  n<=1 by NAT_1:13;
      then
A51:  n=1 by A2,XXREAL_0:1;
      union{B where B is Subset of TOP-REAL n: B is_inside_component_of
      A} c= P1
      proof
        reconsider E=A` as non empty Subset of TOP-REAL n by A3,A8;
        let x be set;
        assume x in union{B where B is Subset of TOP-REAL n: B
        is_inside_component_of A};
        then consider y being set such that
A52:    x in y and
A53:    y in {B where B is Subset of TOP-REAL n: B
        is_inside_component_of A} by TARSKI:def 4;
        consider B being Subset of TOP-REAL n such that
A54:    B=y and
A55:    B is_inside_component_of A by A53;
        ex C being Subset of ((TOP-REAL n) | (A`)) st C=B & C
is a_component & C is bounded Subset of Euclid n
by A55,Th17;
        then reconsider p=x as Point of (TOP-REAL n) | A` by A52,A54;
A56:    the carrier of (TOP-REAL n) | A`=A` & p in the carrier of ((
        TOP-REAL n) |E) by PRE_TOPC:29;
        then reconsider q2=p as Point of TOP-REAL n;
        not p in A by A56,XBOOLE_0:def 5;
        then |.q2.|<>a by A2;
        then
A57:    |.q2.|<a or |.q2.|>a by XXREAL_0:1;
        now
          per cases by A57;
          case
            p in {q: |.q.|>a};
            then consider q0 being Point of TOP-REAL n such that
A58:        q0=p and
A59:        |.q0.|>a;
            q0 is Element of REAL n by EUCLID:25;
            then consider r0 being Real such that
A60:        q0=<*r0*> by A51,FINSEQ_2:117;
A61:        |.q0.|=abs r0 by A60,Th95;
A62:        now
              per cases;
              case
                r0>=0;
                then r0=abs r0 by ABSVALUE:def 1;
                hence
                p in {q:ex r st q=<*r*> & r>a} or p in {q1:ex r1 st q1=<*
                r1*> & r1< -a} by A58,A59,A60,A61;
              end;
              case
                r0<0;
                then -r0>a by A59,A61,ABSVALUE:def 1;
                then --r0< -a by XREAL_1:26;
                hence
                p in {q:ex r st q=<*r*> & r>a} or p in {q1:ex r1 st q1=<*
                r1*> & r1< -a} by A58,A60;
              end;
            end;
            now
              per cases by A62;
              case
A63:            p in {q:ex r st q=<*r*> & r>a};
                {q:ex r st q=<*r*> & r>a} c= A`
                proof
                  let z be set;
                  assume z in {q:ex r st q=<*r*> & r>a};
                  then consider q such that
A64:              q=z and
A65:              ex r st q=<*r*> & r>a;
                  consider r such that
A66:              q=<*r*> and
A67:              r>a by A65;
                  n=1 by A2,A50,XXREAL_0:1;
                  then reconsider xr=<*r*> as Element of REAL n
                  by FINSEQ_2:151;
                  len xr=1 by FINSEQ_1:56;
                  then xr.1=xr/.1 by FINSEQ_4:24;
                  then
A68:              q/.1=xr.1 by A66;
                  then
A69:              (sqr xr).1=(q/.1)^2 by VALUED_1:11;
A70:              sqrt ((q/.1)^2) =abs(q/.1) by COMPLEX1:158
                    .=abs(r) by A68,FINSEQ_1:57;
                  len sqr xr =1 by A51,FINSEQ_1:def 18;
                  then sqr xr=<*(q/.1)^2*> by A69,FINSEQ_1:57;
                  then
A71:              |.q.|=abs(r) by A66,A70,FINSOP_1:12
                    .=r by A2,A67,ABSVALUE:def 1;
                  now
                    assume q in A;
                    then ex q2 st q2=q & |.q2.|=a by A2;
                    hence contradiction by A67,A71;
                  end;
                  hence thesis by A64,XBOOLE_0:def 5;
                end;
                then reconsider
                Q={q:ex r st q=<*r*> & r>a} as Subset of (TOP-REAL
                n) |A` by PRE_TOPC:29;
                {q:ex r st q=<*r*> & r>a} c= the carrier of TOP-REAL n
                proof
                  let z be set;
                  assume z in {q:ex r st q=<*r*> & r>a};
                  then ex q st q=z & ex r st q=<*r*> & r>a;
                  hence thesis;
                end;
                then reconsider
                P3={q: ex r st q=<*r*> & r>a} as Subset of TOP-REAL
                n;
                reconsider W3=P3 as Subset of Euclid n by TOPREAL3:13;
                reconsider Q as Subset of (TOP-REAL n) | A`;
                {q: |.q.|>a} c= the carrier of TOP-REAL n
                proof
                  let z be set;
                  assume z in {q: |.q.|>a};
                  then ex q st q=z & |.q.|>a;
                  hence thesis;
                end;
                then reconsider P2={q: |.q.|>a} as Subset of TOP-REAL n;
                P2 is Subset of Euclid n by TOPREAL3:13;
                then reconsider W2={q: |.q.|>a} as Subset of Euclid n;
A72:            W3 c= W2
                proof
                  let z be set;
                  assume z in W3;
                  then consider q such that
A73:              q=z and
A74:              ex r st q=<*r*> & r>a;
                  consider r such that
A75:              q=<*r*> and
A76:              r>a by A74;
A77:              r=abs(r) by A2,A76,ABSVALUE:def 1;
                  n=1 by A2,A50,XXREAL_0:1;
                  then reconsider xr=<*r*> as Element of REAL n
                  by FINSEQ_2:151;
                  len xr=1 by FINSEQ_1:56;
                  then xr.1=xr/.1 by FINSEQ_4:24;
                  then
A78:              q/.1=xr.1 by A75;
                  then
A79:              (sqr xr).1=(q/.1)^2 by VALUED_1:11;
                  len sqr xr =1 by A51,FINSEQ_1:def 18;
                  then
A80:              sqr xr=<*(q/.1)^2*> by A79,FINSEQ_1:57;
                  sqrt (q/.1)^2 =abs(q/.1) by COMPLEX1:158
                    .=abs(r) by A78,FINSEQ_1:57;
                  then |.q.|=abs(r) by A75,A80,FINSOP_1:12;
                  hence thesis by A73,A76,A77;
                end;
A81:            now
                  consider z being Element of W2 /\ A;
                  assume
A82:              not W2 /\ A={};
                  then z in W2 by XBOOLE_0:def 4;
                  then
A83:              ex q st q=z & |.q.|>a;
                  z in A by A82,XBOOLE_0:def 4;
                  then ex q2 st q2=z & |.q2.|=a by A2;
                  hence contradiction by A83;
                end;
                then W3 /\ A={} by A72,XBOOLE_1:3,26;
                then
A84:            W3 misses A by XBOOLE_0:def 7;
                W3 /\ A``={} by A81,A72,XBOOLE_1:3,26;
                then W3\A`={} by SUBSET_1:32;
                then
A85:            W3 c= A` by XBOOLE_1:37;
                then
A86:            (TOP-REAL n) |P3=((TOP-REAL n) | A`) |Q by PRE_TOPC:28;
                W3=P3;
                then
A87:            P3 is connected by A51,Th67;
                then (TOP-REAL n) |P3 is connected by CONNSP_1:def 3;
                then Q is connected by A86,CONNSP_1:def 3;
                then Q c= Component_of Q by CONNSP_3:1;
                then
A88:            p in Component_of Q by A63;
A89:            Q=Down(P3,A`) by A85,XBOOLE_1:28;
                not W3 is bounded by A51,Th67;
                then Up(Component_of Q) is_outside_component_of A by A87,A84
,A89,Th71;
                then
A90:            Component_of Q c= UBD A by Th27;
                B c= BDD A by A55,Th26;
                then (BDD A) /\ (UBD A)<>{} by A52,A54,A90,A88,XBOOLE_0:def 4;
                then (BDD A) meets (UBD A) by XBOOLE_0:def 7;
                hence thesis by Th28;
              end;
              case
A91:            p in {q1:ex r1 st q1=<*r1*> & r1< -a};
                {q:ex r st q=<*r*> & r< -a} c= A`
                proof
                  let z be set;
                  assume z in {q:ex r st q=<*r*> & r< -a};
                  then consider q such that
A92:              q=z and
A93:              ex r st q=<*r*> & r< -a;
                  consider r such that
A94:              q=<*r*> and
A95:              r< -a by A93;
A96:              r< -0 by A2,A95;
                  n=1 by A2,A50,XXREAL_0:1;
                  then reconsider xr=<*r*> as Element of REAL n
                  by FINSEQ_2:151;
                  len xr=1 by FINSEQ_1:56;
                  then xr.1=xr/.1 by FINSEQ_4:24;
                  then
A97:              q/.1=xr.1 by A94;
                  then
A98:              (sqr xr).1=(q/.1)^2 by VALUED_1:11;
                  len (sqr xr) =1 by A51,FINSEQ_1:def 18;
                  then
A99:              sqr xr=<*(q/.1)^2*> by A98,FINSEQ_1:57;
                  sqrt (q/.1)^2 =abs(q/.1) by COMPLEX1:158
                    .=abs(r) by A97,FINSEQ_1:57;
                  then
A100:             |.q.|=abs(r) by A94,A99,FINSOP_1:12
                    .=-r by A96,ABSVALUE:def 1;
                  now
                    assume q in A;
                    then ex q2 st q2=q & |.q2.|=a by A2;
                    hence contradiction by A95,A100;
                  end;
                  hence thesis by A92,XBOOLE_0:def 5;
                end;
                then reconsider Q={q:ex r st q=<*r*> & r< -a} as Subset of (
                TOP-REAL n) |A` by PRE_TOPC:29;
                {q:ex r st q=<*r*> & r< -a} c= the carrier of TOP-REAL n
                proof
                  let z be set;
                  assume z in {q:ex r st q=<*r*> & r< -a};
                  then ex q st q=z & ex r st q=<*r*> & r< -a;
                  hence thesis;
                end;
                then reconsider P3={q: ex r st q=<*r*> & r< -a} as Subset of
                TOP-REAL n;
                reconsider W3=P3 as Subset of Euclid n by TOPREAL3:13;
                reconsider Q as Subset of (TOP-REAL n) | A`;
                {q: |.q.|>a} c= the carrier of TOP-REAL n
                proof
                  let z be set;
                  assume z in {q: |.q.|>a};
                  then ex q st q=z & |.q.|>a;
                  hence thesis;
                end;
                then reconsider P2={q: |.q.|>a} as Subset of TOP-REAL n;
                P2 is Subset of Euclid n by TOPREAL3:13;
                then reconsider W2={q: |.q.|>a} as Subset of Euclid n;
A101:           W3 c= W2
                proof
                  let z be set;
                  assume z in W3;
                  then consider q such that
A102:             q=z and
A103:             ex r st q=<*r*> & r< -a;
                  consider r such that
A104:             q=<*r*> and
A105:             r< -a by A103;
A106:             r< -0 & -r>--a by A2,A105,XREAL_1:26;
                  n=1 by A2,A50,XXREAL_0:1;
                  then reconsider xr=<*r*> as Element of REAL n
                  by FINSEQ_2:151;
                  len xr=1 by FINSEQ_1:56;
                  then xr.1=xr/.1 by FINSEQ_4:24;
                  then
A107:             q/.1=xr.1 by A104;
                  then
A108:             (sqr xr).1=(q/.1)^2 by VALUED_1:11;
                  len sqr xr =1 by A51,FINSEQ_1:def 18;
                  then
A109:             sqr xr=<*(q/.1)^2*> by A108,FINSEQ_1:57;
                  sqrt (q/.1)^2 =abs(q/.1) by COMPLEX1:158
                    .=abs(r) by A107,FINSEQ_1:57;
                  then |.q.|=abs(r) by A104,A109,FINSOP_1:12;
                  then |.q.|>a by A106,ABSVALUE:def 1;
                  hence thesis by A102;
                end;
A110:           now
                  consider z being Element of W2 /\ A;
                  assume
A111:             not W2 /\ A={};
                  then z in W2 by XBOOLE_0:def 4;
                  then
A112:             ex q st q=z & |.q.|>a;
                  z in A by A111,XBOOLE_0:def 4;
                  then ex q2 st q2=z & |.q2.|=a by A2;
                  hence contradiction by A112;
                end;
                then W3 /\ A={} by A101,XBOOLE_1:3,26;
                then
A113:           W3 misses A by XBOOLE_0:def 7;
                W3 /\ A``={} by A110,A101,XBOOLE_1:3,26;
                then W3\A`={} by SUBSET_1:32;
                then
A114:           W3 c= A` by XBOOLE_1:37;
                then
A115:           (TOP-REAL n) |P3=((TOP-REAL n) | A`) |Q by PRE_TOPC:28;
                W3=P3;
                then
A116:           P3 is connected by A51,Th68;
                then (TOP-REAL n) |P3 is connected by CONNSP_1:def 3;
                then Q is connected by A115,CONNSP_1:def 3;
                then Q c= Component_of Q by CONNSP_3:1;
                then
A117:           p in Component_of Q by A91;
A118:           Q=Down(P3,A`) by A114,XBOOLE_1:28;
                not W3 is bounded by A51,Th68;
                then Up(Component_of Q) is_outside_component_of A by A116,A113
,A118,Th71;
                then
A119:           Component_of Q c= UBD A by Th27;
                B c= BDD A by A55,Th26;
                then p in (BDD A) /\ (UBD A) by A52,A54,A119,A117,
XBOOLE_0:def 4;
                then (BDD A) meets (UBD A) by XBOOLE_0:def 7;
                hence thesis by Th28;
              end;
            end;
            hence thesis;
          end;
          case
A120:       p in {q1: |.q1.|<a};
            Down(P,A`) c= Component_of (Down(P,A`)) by A7,CONNSP_3:1;
            hence thesis by A6,A120;
          end;
        end;
        hence thesis;
      end;
      then P1=union{B where B is Subset of TOP-REAL n: B
      is_inside_component_of A} by A30,XBOOLE_0:def 10;
      hence ex B being Subset of TOP-REAL n st B is_inside_component_of A & B=
      BDD A by A29;
    end;
  end;
  hence thesis;
end;

begin ::Bounded and Unbounded Domains of Rectangles

reserve D for non vertical non horizontal non empty compact Subset of TOP-REAL
  2;

theorem Th97:
  len (GoB SpStSeq D)=2 & width (GoB SpStSeq D)=2 & (SpStSeq D)/.1
=(GoB SpStSeq D)*(1,2) & (SpStSeq D)/.2=(GoB SpStSeq D)*(2,2) & (SpStSeq D)/.3=
(GoB SpStSeq D)*(2,1) & (SpStSeq D)/.4=(GoB SpStSeq D)*(1,1) & (SpStSeq D)/.5=(
  GoB SpStSeq D)*(1,2)
proof
  set f=SpStSeq D;
A1: S-bound L~f < N-bound L~f by SPRECT_1:34;
A2: len f=5 by SPRECT_1:90;
  then
A3: f/.5=f/.1 by FINSEQ_6:def 1;
  4 in Seg len f by A2,FINSEQ_1:3;
  then
A4: 4 in dom f by FINSEQ_1:def 3;
  then 4 in dom (X_axis(f)) by SPRECT_2:19;
  then f/.4 = W-min L~f & (X_axis(f)).4=(f/.4)`1 by GOBOARD1:def 3,SPRECT_1:94;
  then
A5: (X_axis(f)).4=W-bound L~f by EUCLID:56;
A6: f/.3 = S-max L~f by SPRECT_1:93;
  3 in Seg len f by A2,FINSEQ_1:3;
  then
A7: 3 in dom f by FINSEQ_1:def 3;
  then 3 in dom (X_axis(f)) by SPRECT_2:19;
  then f/.3 = E-min L~f & (X_axis(f)).3=(f/.3)`1 by GOBOARD1:def 3,SPRECT_1:93;
  then
A8: (X_axis(f)).3=E-bound L~f by EUCLID:56;
A9: f/.(1+1) = N-max L~f by SPRECT_1:92;
  3 in dom (Y_axis(f)) by A7,SPRECT_2:20;
  then (Y_axis(f)).3=(f/.3)`2 by GOBOARD1:def 4;
  then
A10: (Y_axis(f)).3=S-bound L~f by A6,EUCLID:56;
A11: f/.1 = N-min L~f by SPRECT_1:91;
  1 in Seg len f by A2,FINSEQ_1:3;
  then
A12: 1 in dom f by FINSEQ_1:def 3;
  then 1 in dom (Y_axis(f)) by SPRECT_2:20;
  then (Y_axis(f)).1=(f/.1)`2 by GOBOARD1:def 4;
  then
A13: (Y_axis(f)).1=N-bound L~f by A11,EUCLID:56;
A14: f/.4 = S-min L~f by SPRECT_1:94;
  2 in Seg len f by A2,FINSEQ_1:3;
  then
A15: 2 in dom f by FINSEQ_1:def 3;
  then 2 in dom (X_axis(f)) by SPRECT_2:19;
  then f/.(1+1) = E-max L~f & (X_axis(f)).2=(f/.2)`1 by GOBOARD1:def 3
,SPRECT_1:92;
  then
A16: (X_axis(f)).2=E-bound L~f by EUCLID:56;
  4 in dom (Y_axis(f)) by A4,SPRECT_2:20;
  then (Y_axis(f)).4=(f/.4)`2 by GOBOARD1:def 4;
  then
A17: (Y_axis(f)).4=S-bound L~f by A14,EUCLID:56;
  2 in dom (Y_axis(f)) by A15,SPRECT_2:20;
  then (Y_axis(f)).2=(f/.2)`2 by GOBOARD1:def 4;
  then
A18: (Y_axis(f)).2=N-bound L~f by A9,EUCLID:56;
A19: {S-bound L~f,N-bound L~f} c= rng (Y_axis(f))
  proof
    let z be set;
    assume
A20: z in {S-bound L~f,N-bound L~f};
    now
      per cases by A20,TARSKI:def 2;
      case
A21:    z=S-bound L~f;
        4 in dom (Y_axis(f)) by A4,SPRECT_2:20;
        hence thesis by A17,A21,FUNCT_1:def 5;
      end;
      case
A22:    z=N-bound L~f;
        2 in dom (Y_axis(f)) by A15,SPRECT_2:20;
        hence thesis by A18,A22,FUNCT_1:def 5;
      end;
    end;
    hence thesis;
  end;
A23: f/.1 = W-max L~f by SPRECT_1:91;
  1 in dom (X_axis(f)) by A12,SPRECT_2:19;
  then (X_axis(f)).1=(f/.1)`1 by GOBOARD1:def 3;
  then
A24: (X_axis(f)).1=W-bound L~f by A23,EUCLID:56;
A25: {W-bound L~f,E-bound L~f} c= rng (X_axis(f))
  proof
    let z be set;
    assume
A26: z in {W-bound L~f,E-bound L~f};
    now
      per cases by A26,TARSKI:def 2;
      case
A27:    z=W-bound L~f;
        1 in dom (X_axis(f)) by A12,SPRECT_2:19;
        hence thesis by A24,A27,FUNCT_1:def 5;
      end;
      case
A28:    z=E-bound L~f;
        2 in dom (X_axis(f)) by A15,SPRECT_2:19;
        hence thesis by A16,A28,FUNCT_1:def 5;
      end;
    end;
    hence thesis;
  end;
A29: GoB f = GoB(Incr(X_axis(f)),Incr(Y_axis(f))) by GOBOARD2:def 3;
  5 in Seg len f by A2,FINSEQ_1:3;
  then
A30: 5 in dom f by FINSEQ_1:def 3;
  then 5 in dom (X_axis(f)) by SPRECT_2:19;
  then (X_axis(f)).5=(f/.5)`1 by GOBOARD1:def 3;
  then
A31: (X_axis(f)).5=W-bound L~f by A23,A3,EUCLID:56;
  rng (X_axis(f)) c= {W-bound L~f,E-bound L~f}
  proof
    let z be set;
    assume z in rng (X_axis(f));
    then consider u being set such that
A32: u in dom (X_axis(f)) and
A33: z=(X_axis(f)).u by FUNCT_1:def 5;
    reconsider mu=u as Element of NAT by A32;
    u in dom f by A32,SPRECT_2:19;
    then u in Seg len f by FINSEQ_1:def 3;
    then 1<=mu & mu<=5 by A2,FINSEQ_1:3;
    then
A34: mu=1 or mu=1+1 or mu=1+2 or mu=1+3 or mu=1+4 by Th4;
    per cases by A34;
    suppose
      mu=1;
      hence thesis by A24,A33,TARSKI:def 2;
    end;
    suppose
      mu=2;
      hence thesis by A16,A33,TARSKI:def 2;
    end;
    suppose
      mu=3;
      hence thesis by A8,A33,TARSKI:def 2;
    end;
    suppose
      mu=4;
      hence thesis by A5,A33,TARSKI:def 2;
    end;
    suppose
      mu=5;
      hence thesis by A31,A33,TARSKI:def 2;
    end;
  end;
  then
A35: rng (X_axis(f))={W-bound L~f,E-bound L~f} by A25,XBOOLE_0:def 10;
  then
A36: rng (Incr (X_axis(f)))={W-bound L~f,E-bound L~f} by SEQ_4:def 25;
  5 in dom (Y_axis(f)) by A30,SPRECT_2:20;
  then (Y_axis(f)).5=(f/.5)`2 by GOBOARD1:def 4;
  then
A37: (Y_axis(f)).5=N-bound L~f by A11,A3,EUCLID:56;
  rng (Y_axis(f)) c= {S-bound L~f,N-bound L~f}
  proof
    let z be set;
    assume z in rng (Y_axis(f));
    then consider u being set such that
A38: u in dom (Y_axis(f)) and
A39: z=(Y_axis(f)).u by FUNCT_1:def 5;
    reconsider mu=u as Element of NAT by A38;
    u in dom f by A38,SPRECT_2:20;
    then u in Seg len f by FINSEQ_1:def 3;
    then 1<=mu & mu<=5 by A2,FINSEQ_1:3;
    then
A40: mu=1 or mu=1+1 or mu=1+2 or mu=1+3 or mu=1+4 by Th4;
    per cases by A40;
    suppose
      mu=1;
      hence thesis by A13,A39,TARSKI:def 2;
    end;
    suppose
      mu=2;
      hence thesis by A18,A39,TARSKI:def 2;
    end;
    suppose
      mu=3;
      hence thesis by A10,A39,TARSKI:def 2;
    end;
    suppose
      mu=4;
      hence thesis by A17,A39,TARSKI:def 2;
    end;
    suppose
      mu=5;
      hence thesis by A37,A39,TARSKI:def 2;
    end;
  end;
  then
A41: rng (Y_axis(f))={S-bound L~f,N-bound L~f} by A19,XBOOLE_0:def 10;
  then card rng (Y_axis(f))=2 by A1,CARD_2:76;
  then
A42: len (Incr (Y_axis(f)))=2 by SEQ_4:def 25;
A43: W-bound L~f < E-bound L~f by SPRECT_1:33;
  then
A44: card rng (X_axis(f))=2 by A35,CARD_2:76;
  then
A45: len (Incr (X_axis(f)))=2 by SEQ_4:def 25;
A46: len GoB(f) = card rng X_axis(f) by GOBOARD2:24
    .=1+1 by A43,A35,CARD_2:76;
  then
A47: 1 in Seg len GoB f by FINSEQ_1:3;
A48: width GoB(f) = card rng Y_axis(f) by GOBOARD2:24
    .=1+1 by A1,A41,CARD_2:76;
  for p being FinSequence of the carrier of TOP-REAL 2 st p in rng GoB f
  holds len p = 2
  proof
    len GoB(Incr(X_axis(f)),Incr(Y_axis(f)) ) =len (Incr(X_axis(f))) by
GOBOARD2:def 1
      .=2 by A44,SEQ_4:def 25;
    then consider s1 being FinSequence such that
A49: s1 in rng GoB(Incr(X_axis(f)),Incr(Y_axis(f)) ) and
A50: len s1 = width GoB(Incr(X_axis(f)),Incr(Y_axis(f)) ) by MATRIX_1:def 4;
    let p be FinSequence of the carrier of TOP-REAL 2;
    consider n being Nat such that
A51: for x st x in rng GoB f ex s being FinSequence st s=x & len s =
    n by MATRIX_1:def 1;
    assume p in rng GoB f;
    then
A52: ex s2 being FinSequence st s2=p & len s2 = n by A51;
    s1 in rng GoB f by A49,GOBOARD2:def 3;
    then ex s being FinSequence st s=s1 & len s = n by A51;
    hence thesis by A48,A50,A52,GOBOARD2:def 3;
  end;
  then
A53: GoB f is Matrix of 2,2,the carrier of TOP-REAL 2 by A46,MATRIX_1:def 3;
A54: 1 in Seg (width GoB f) by A48,FINSEQ_1:3;
  then [1,1] in [:Seg (len GoB f),Seg (width GoB f):] by A47,ZFMISC_1:106;
  then
A55: [1,1] in Indices GoB f by A46,A48,A53,MATRIX_1:25;
A56: width GoB f in Seg (width GoB f) by A48,FINSEQ_1:3;
  then [1,width GoB f] in [:Seg (len GoB f),Seg (width GoB f):] by A47,
ZFMISC_1:106;
  then
A57: [1,width GoB f] in Indices GoB f by A46,A48,A53,MATRIX_1:25;
A58: len GoB f in Seg len GoB f by A46,FINSEQ_1:3;
  then [len GoB f,1] in [:Seg (len GoB f),Seg (width GoB f):] by A54,
ZFMISC_1:106;
  then
A59: [len GoB f,1] in Indices GoB f by A46,A48,A53,MATRIX_1:25;
  (S-max L~f)`1 =(SE-corner D)`1 by SPRECT_1:89
    .=E-bound D by EUCLID:56
    .=E-bound L~f by SPRECT_1:69
    .=(Incr(X_axis(f))).2 by A43,A36,A45,Th8;
  then
  (S-max L~f)`1 =(|[(Incr(X_axis(f))).2,(Incr(Y_axis(f))).1]|)`1 by EUCLID:56;
  then
A60: (S-max L~f)`1 =((GoB f)*(len (GoB f),1))`1 by A29,A46,A59,GOBOARD2:def 1;
  (S-min L~f)`1 =(SW-corner D)`1 by SPRECT_1:88
    .=W-bound D by EUCLID:56
    .=W-bound L~f by SPRECT_1:66
    .=(Incr(X_axis(f))).1 by A43,A36,A45,Th8;
  then
  (S-min L~f)`1 =(|[(Incr(X_axis(f))).1,(Incr(Y_axis(f))).1]|)`1 by EUCLID:56;
  then
A61: (S-min L~f)`1 =((GoB f)*(1,1))`1 by A29,A55,GOBOARD2:def 1;
  [len GoB f,width GoB f] in [:Seg (len GoB f),Seg (width GoB f):] by A58,A56,
ZFMISC_1:106;
  then
A62: [len GoB f,width GoB f] in Indices GoB f by A46,A48,A53,MATRIX_1:25;
  W-bound L~f =(Incr(X_axis(f))).1 by A43,A36,A45,Th8;
  then (W-max L~f)`1 =(Incr(X_axis(f))).1 by EUCLID:56;
  then (W-max L~f)`1 =(|[(Incr(X_axis(f))).1,(Incr(Y_axis(f))).(1+1)]|)`1 by
EUCLID:56;
  then
A63: (W-max L~f)`1 =((GoB f)*(1,width (GoB f)))`1 by A29,A48,A57,GOBOARD2:def 1
;
A64: f/.3=|[(f/.3)`1,(f/.3)`2]| & f/.4=|[(f/.4)`1,(f/.4)`2]| by EUCLID:57;
A65: f/.1=|[(f/.1)`1,(f/.1)`2]| & f/.(1+1)=|[(f/.(1+1))`1,(f/.(1+1))`2]| by
EUCLID:57;
A66: rng (Incr (Y_axis(f)))={S-bound L~f,N-bound L~f} by A41,SEQ_4:def 25;
  then
A67: N-bound L~f =(Incr(Y_axis(f))).2 by A1,A42,Th8;
  then (N-min L~f)`2 =(Incr(Y_axis(f))).2 by EUCLID:56;
  then
  (N-min L~f)`2 =(|[(Incr(X_axis(f))).1,(Incr(Y_axis(f))).2]|)`2 by EUCLID:56;
  then
A68: (N-min L~f)`2 =((GoB f)*(1,width (GoB f)))`2 by A29,A48,A57,GOBOARD2:def 1
;
A69: S-bound L~f =(Incr(Y_axis(f))).1 by A1,A66,A42,Th8;
  then (S-min L~f)`2 =(Incr(Y_axis(f))).1 by EUCLID:56;
  then
  (S-min L~f)`2 =(|[(Incr(X_axis(f))).1,(Incr(Y_axis(f))).1]|)`2 by EUCLID:56;
  then
A70: (S-min L~f)`2 =((GoB f)*(1,1))`2 by A29,A55,GOBOARD2:def 1;
  (N-max L~f)`2 = N-bound (L~f) by EUCLID:56;
  then (N-max L~f)`2 =(|[(Incr(X_axis(f))).2,(Incr(Y_axis(f))).2]|)`2 by A67,
EUCLID:56;
  then
A71: (N-max L~f)`2 =((GoB f)*(len (GoB f),width (GoB f)))`2 by A29,A46,A48,A62,
GOBOARD2:def 1;
  (S-max L~f)`2 =(Incr(Y_axis(f))).1 by A69,EUCLID:56;
  then
  (S-max L~f)`2 =(|[(Incr(X_axis(f))).2,(Incr(Y_axis(f))).1]|)`2 by EUCLID:56;
  then
A72: (S-max L~f)`2 =((GoB f)*(len (GoB f),1))`2 by A29,A46,A59,GOBOARD2:def 1;
  (N-max L~f)`1 =(NE-corner D)`1 by SPRECT_1:85
    .=E-bound D by EUCLID:56
    .=E-bound L~f by SPRECT_1:69
    .=(Incr(X_axis(f))).2 by A43,A36,A45,Th8;
  then (N-max L~f)`1 =(|[(Incr(X_axis(f))).(1+1),(Incr(Y_axis(f))).(1+1)]|)`1
  by EUCLID:56;
  then (N-max L~f)`1 =((GoB f)*(len (GoB f),width (GoB f)))`1 by A29,A46,A48
,A62,GOBOARD2:def 1;
  hence thesis by A11,A23,A9,A6,A14,A3,A46,A48,A63,A60,A61,A68,A71,A72,A70,A65
,A64,EUCLID:57;
end;

theorem Th98:
  LeftComp SpStSeq D is non Bounded
proof
  set f=SpStSeq D;
  consider q3 being Element of LeftComp f;
  reconsider q4=q3 as Point of TOP-REAL 2;
  set r1=|. (1/2)*(f/.1+f/.2).|;
  reconsider f1=f as non constant standard special_circular_sequence;
A1: W-bound L~f1 < E-bound L~f1 by SPRECT_1:33;
A2: (N-min L~f1)`2=N-bound L~f1 by EUCLID:56;
  then (N-min L~f1)`2+(N-max L~f1)`2=N-bound L~f1 +N-bound L~f1 by EUCLID:56
    .=2*(N-bound L~f);
  then
A3: (1/2)*((N-min L~f)`2+(N-max L~f)`2)=N-bound L~f;
A4: len f1=5 by SPRECT_1:90;
  then 5 in Seg len f1 by FINSEQ_1:3;
  then
A5: 5 in dom f1 by FINSEQ_1:def 3;
  then 5 in dom (Y_axis(f1)) by SPRECT_2:20;
  then
A6: (Y_axis(f1)).5=(f1/.5)`2 by GOBOARD1:def 4;
  4 in Seg len f1 by A4,FINSEQ_1:3;
  then
A7: 4 in dom f1 by FINSEQ_1:def 3;
  then 4 in dom (Y_axis(f1)) by SPRECT_2:20;
  then f1/.4 = S-min L~f1 & (Y_axis(f1)).4=(f1/.4)`2 by GOBOARD1:def 4
,SPRECT_1:94;
  then
A8: (Y_axis(f1)).4=S-bound L~f1 by EUCLID:56;
  3 in Seg len f1 by A4,FINSEQ_1:3;
  then
A9: 3 in dom f1 by FINSEQ_1:def 3;
  then 3 in dom (Y_axis(f1)) by SPRECT_2:20;
  then f1/.3 = S-max L~f1 & (Y_axis(f1)).3=(f1/.3)`2 by GOBOARD1:def 4
,SPRECT_1:93;
  then
A10: (Y_axis(f1)).3=S-bound L~f1 by EUCLID:56;
  3 in dom (X_axis(f1)) by A9,SPRECT_2:19;
  then f1/.3=E-min L~f1 & (X_axis(f1)).3=(f1/.3)`1 by GOBOARD1:def 3
,SPRECT_1:93;
  then
A11: (X_axis(f1)).3=E-bound L~f1 by EUCLID:56;
  5 in dom (X_axis(f1)) by A5,SPRECT_2:19;
  then
A12: (X_axis(f1)).5=(f1/.5)`1 by GOBOARD1:def 3;
  assume LeftComp f is Bounded;
  then consider r being Real such that
A13: for q being Point of TOP-REAL 2 st q in (LeftComp f) holds |.q.|<r
  by Th40;
  set q1=|[0,r1+r+1]|+(1/2)*(f/.1+f/.2);
A14: f1/.1 = N-min L~f1 by SPRECT_1:91;
  4 in dom (X_axis(f1)) by A7,SPRECT_2:19;
  then f1/.4=W-min L~f1 & (X_axis(f1)).4=(f1/.4)`1 by GOBOARD1:def 3
,SPRECT_1:94;
  then
A15: (X_axis(f1)).4=W-bound L~f1 by EUCLID:56;
A16: GoB f1 = GoB(Incr(X_axis(f1)),Incr(Y_axis(f1))) by GOBOARD2:def 3;
A17: f1/.2=E-max L~f1 by SPRECT_1:92;
  2 in Seg len f1 by A4,FINSEQ_1:3;
  then
A18: 2 in dom f1 by FINSEQ_1:def 3;
  then
A19: 2 in dom (X_axis(f1)) by SPRECT_2:19;
  then (X_axis(f1)).2=(f1/.2)`1 by GOBOARD1:def 3;
  then
A20: (X_axis(f1)).2=E-bound L~f1 by A17,EUCLID:56;
A21: 1 in Seg len f1 by A4,FINSEQ_1:3;
  then
A22: 1 in dom f1 by FINSEQ_1:def 3;
  then 1 in dom (Y_axis(f1)) by SPRECT_2:20;
  then (Y_axis(f1)).1=(f1/.1)`2 by GOBOARD1:def 4;
  then
A23: (Y_axis(f1)).1=N-bound L~f1 by A14,EUCLID:56;
  (X_axis(f1)).2=(f1/.2)`1 by A19,GOBOARD1:def 3;
  then
A24: (f1/.2)`1 in rng X_axis(f1) by A19,FUNCT_1:def 5;
  len (X_axis(f1))=len f1 by GOBOARD1:def 3;
  then
A25: dom (X_axis(f1))=Seg len f1 by FINSEQ_1:def 3;
  then (X_axis(f1)).1=(f1/.1)`1 by A21,GOBOARD1:def 3;
  then
A26: (f1/.1)`1 in rng X_axis(f1) by A21,A25,FUNCT_1:def 5;
  {(f1/.1)`1,(f1/.2)`1} c= rng X_axis(f1)
  proof
    let z be set;
    assume z in {(f1/.1)`1,(f1/.2)`1};
    hence thesis by A26,A24,TARSKI:def 2;
  end;
  then {(f1/.1)`1} \/ {(f1/.2)`1} c= rng X_axis(f1) by ENUMSET1:41;
  then
A27: card ({(f1/.1)`1} \/ {(f1/.2)`1}) <= card rng X_axis(f1) by NAT_1:44;
A28: f1/.(1+1) = N-max L~f1 by SPRECT_1:92;
  then (f1/.1)`1 < (f1/.2)`1 by A14,SPRECT_2:55;
  then not (f1/.2)`1 in {(f1/.1)`1} by TARSKI:def 1;
  then
A29: card ({(f1/.1)`1} \/ {(f1/.2)`1})=card ({(f1/.1)`1})+1 by CARD_2:54
    .=1+1 by CARD_1:50
    .=2;
  then
A30: 1<>len (GoB f1) +1 by A27,GOBOARD2:24,XREAL_1:31;
  2 in dom (Y_axis(f1)) by A18,SPRECT_2:20;
  then (Y_axis(f1)).2=(f1/.2)`2 by GOBOARD1:def 4;
  then
A31: (Y_axis(f1)).2=N-bound L~f1 by A28,EUCLID:56;
  f1/.5=f1/.1 by A4,FINSEQ_6:def 1;
  then
A32: (Y_axis(f1)).5=N-bound L~f1 by A14,A6,EUCLID:56;
A33: rng (Y_axis(f1)) c= {S-bound L~f1,N-bound L~f1}
  proof
    let z be set;
    assume z in rng (Y_axis(f1));
    then consider u being set such that
A34: u in dom (Y_axis(f1)) and
A35: z=(Y_axis(f1)).u by FUNCT_1:def 5;
    reconsider mu=u as Element of NAT by A34;
    u in dom f1 by A34,SPRECT_2:20;
    then u in Seg len f1 by FINSEQ_1:def 3;
    then 1<=mu & mu<=5 by A4,FINSEQ_1:3;
    then
A36: mu=1 or mu=1+1 or mu=1+2 or mu=1+3 or mu=1+4 by Th4;
    per cases by A36;
    suppose
      mu=1;
      hence thesis by A23,A35,TARSKI:def 2;
    end;
    suppose
      mu=2;
      hence thesis by A31,A35,TARSKI:def 2;
    end;
    suppose
      mu=3;
      hence thesis by A10,A35,TARSKI:def 2;
    end;
    suppose
      mu=4;
      hence thesis by A8,A35,TARSKI:def 2;
    end;
    suppose
      mu=5;
      hence thesis by A32,A35,TARSKI:def 2;
    end;
  end;
  {S-bound L~f1,N-bound L~f1} c= rng (Y_axis(f1))
  proof
    let z be set;
    assume
A37: z in {S-bound L~f1,N-bound L~f1};
    per cases by A37,TARSKI:def 2;
    suppose
A38:  z=S-bound L~f1;
      4 in dom (Y_axis(f1)) by A7,SPRECT_2:20;
      hence thesis by A8,A38,FUNCT_1:def 5;
    end;
    suppose
A39:  z=N-bound L~f1;
      2 in dom (Y_axis(f1)) by A18,SPRECT_2:20;
      hence thesis by A31,A39,FUNCT_1:def 5;
    end;
  end;
  then
A40: S-bound L~f1 < N-bound L~f1 & rng (Y_axis(f1))={S-bound L~f1,N-bound L~
  f1} by A33,SPRECT_1:34,XBOOLE_0:def 10;
A41: width GoB(f1) = card rng Y_axis(f1) by GOBOARD2:24
    .=1+1 by A40,CARD_2:76;
  then
A42: width GoB f1 in Seg (width GoB f1) by FINSEQ_1:3;
  f1/.(1+1) = E-max L~f1 by SPRECT_1:92;
  then
A43: f/.2=|[(E-max L~f)`1,(N-max L~f)`2]| by A28,EUCLID:57;
A44: f1/.1 = W-max L~f1 by SPRECT_1:91;
  then f/.1=|[(W-max L~f)`1,(N-min L~f)`2]| by A14,EUCLID:57;
  then f/.1+f/.2=|[(W-max L~f)`1 +(E-max L~f)`1, (N-min L~f)`2+(N-max L~f)`2
  ]| by A43,EUCLID:60;
  then (1/2)*(f/.1+f/.2)= |[1/2*((W-max L~f)`1 +(E-max L~f)`1),N-bound L~f]|
  by A3,EUCLID:62;
  then
A45: q1=|[0+1/2*((W-max L~f)`1 +(E-max L~f)`1),r1+r+1+(N-bound L~f)]| by
EUCLID:60
    .=|[1/2*((W-max L~f)`1 +(E-max L~f)`1),r1+r+1+(N-bound L~f)]|;
  (W-max L~f)`1=W-bound L~f by EUCLID:56;
  then
A46: (W-max L~f)`1<(E-max L~f)`1 by A1,EUCLID:56;
A47: f1/.1=W-max L~f1 by SPRECT_1:91;
  then
A48: (GoB f1)*(1,1)`1<=(W-max L~f)`1 by A4,A41,JORDAN5D:7;
  then (GoB f1)*(1,1)`1<(E-max L~f)`1 by A46,XXREAL_0:2;
  then (GoB f1)*(1,1)`1 +(GoB f1)*(1,1)`1<(W-max L~f)`1+(E-max L~f)`1 by A48,
XREAL_1:10;
  then
A49: 1/2*(2*((GoB f1)*(1,1)`1))<1/2*((W-max L~f)`1+(E-max L~f)`1) by XREAL_1:70
;
  1 in dom (X_axis(f1)) by A22,SPRECT_2:19;
  then (X_axis(f1)).1=(f1/.1)`1 by GOBOARD1:def 3;
  then
A50: (X_axis(f1)).1=W-bound L~f1 by A47,EUCLID:56;
  f1/.5=W-max L~f1 by A4,A44,FINSEQ_6:def 1;
  then
A51: (X_axis(f1)).5=W-bound L~f1 by A12,EUCLID:56;
A52: rng (X_axis(f1)) c= {W-bound L~f1,E-bound L~f1}
  proof
    let z be set;
    assume z in rng (X_axis(f1));
    then consider u being set such that
A53: u in dom (X_axis(f1)) and
A54: z=(X_axis(f1)).u by FUNCT_1:def 5;
    reconsider mu=u as Element of NAT by A53;
    u in dom f1 by A53,SPRECT_2:19;
    then u in Seg len f1 by FINSEQ_1:def 3;
    then 1<=mu & mu<=5 by A4,FINSEQ_1:3;
    then
A55: mu=1 or mu=1+1 or mu=1+2 or mu=1+3 or mu=1+4 by Th4;
    per cases by A55;
    suppose
      mu=1;
      hence thesis by A50,A54,TARSKI:def 2;
    end;
    suppose
      mu=2;
      hence thesis by A20,A54,TARSKI:def 2;
    end;
    suppose
      mu=3;
      hence thesis by A11,A54,TARSKI:def 2;
    end;
    suppose
      mu=4;
      hence thesis by A15,A54,TARSKI:def 2;
    end;
    suppose
      mu=5;
      hence thesis by A51,A54,TARSKI:def 2;
    end;
  end;
  {W-bound L~f1,E-bound L~f1} c= rng (X_axis(f1))
  proof
    let z be set;
    assume
A56: z in {W-bound L~f1,E-bound L~f1};
    per cases by A56,TARSKI:def 2;
    suppose
A57:  z=W-bound L~f1;
      1 in dom (X_axis(f1)) by A22,SPRECT_2:19;
      hence thesis by A50,A57,FUNCT_1:def 5;
    end;
    suppose
A58:  z=E-bound L~f1;
      2 in dom (X_axis(f1)) by A18,SPRECT_2:19;
      hence thesis by A20,A58,FUNCT_1:def 5;
    end;
  end;
  then
A59: rng (X_axis(f1))={W-bound L~f1,E-bound L~f1} by A52,XBOOLE_0:def 10;
A60: len GoB(f1) = card rng X_axis(f1) by GOBOARD2:24
    .=1+1 by A1,A59,CARD_2:76;
  then
A61: (GoB f1)*(1+1,1)`1>=(E-max L~f)`1 by A4,A17,A41,JORDAN5D:7;
  then (W-max L~f)`1 <(GoB f1)*(1+1,1)`1 by A46,XXREAL_0:2;
  then (W-max L~f)`1+(E-max L~f)`1< (GoB f1)*(1+1,1)`1 +(GoB f1)*(1+1,1)`1 by
A61,XREAL_1:10;
  then
A62: 1/2*((W-max L~f)`1+(E-max L~f)`1)< 1/2*(2*((GoB f1)*(1+1,1)`1)) by
XREAL_1:70;
A63: card rng (X_axis(f1))=2 by A1,A59,CARD_2:76;
  for p being FinSequence of the carrier of TOP-REAL 2 st p in rng GoB
  f1 holds len p = 2
  proof
    len GoB(Incr(X_axis(f1)),Incr(Y_axis(f1)) ) =len (Incr(X_axis(f1)))
    by GOBOARD2:def 1
      .=2 by A63,SEQ_4:def 25;
    then consider s1 being FinSequence such that
A64: s1 in rng GoB(Incr(X_axis(f1)),Incr(Y_axis(f1)) ) and
A65: len s1 = width GoB(Incr(X_axis(f1)),Incr(Y_axis(f1))) by MATRIX_1:def 4;
    let p be FinSequence of the carrier of TOP-REAL 2;
    consider n being Nat such that
A66: for x st x in rng GoB f1 ex s being FinSequence st s=x & len s =
    n by MATRIX_1:def 1;
    assume p in rng GoB f1;
    then
A67: ex s2 being FinSequence st s2=p & len s2 = n by A66;
    ex s being FinSequence st s=s1 & len s = n by A16,A64,A66;
    hence thesis by A41,A65,A67,GOBOARD2:def 3;
  end;
  then
A68: GoB f1 is Matrix of 2,2,the carrier of TOP-REAL 2 by A60,MATRIX_1:def 3;
  len GoB f1 in Seg len GoB f1 by A60,FINSEQ_1:3;
  then [len GoB f1,width GoB f1] in [:Seg (len GoB f1),Seg (width GoB f1):]
  by A42,ZFMISC_1:106;
  then
A69: [len GoB f1,width GoB f1] in Indices GoB f1 by A60,A41,A68,MATRIX_1:25;
  1 in Seg len GoB f1 by A60,FINSEQ_1:3;
  then [1,width GoB f1] in [:Seg (len GoB f1),Seg (width GoB f1):] by A42,
ZFMISC_1:106;
  then
A70: [1,width GoB f1] in Indices GoB f1 by A60,A41,A68,MATRIX_1:25;
  card rng X_axis(f1) >1 by A27,A29,XXREAL_0:2;
  then
A71: 1<len (GoB f1) by GOBOARD2:24;
A72: f1/.1=(GoB f1)*(1,width (GoB f1)) by A41,Th97;
  set p=|[0,r1+r+1]|;
A73: p`1=0 & p`2=r1+r+1 by EUCLID:56;
A74: |.q1.|>=|.(|[0,r1+r+1]|).|-r1 & r<r+1 by TOPRNS_1:32,XREAL_1:31;
A75: Int (left_cell(f1,1)) c= (LeftComp f) by GOBOARD9:def 1;
A76: width GoB f1 <> width (GoB f1)+1;
  f1/.(1+1)=(GoB f1)*(len (GoB f1),width (GoB f1)) by A60,A41,Th97;
  then left_cell(f1,1) = cell(GoB(f1),1,width GoB(f1)) by A4,A70,A69,A72,A30
,A76,GOBOARD5:def 7;
  then
A77: Int (left_cell(f1,1))= { |[r2,s]| : (GoB f1)*(1,1)`1 < r2 & r2 < (GoB
  f1)*(1+1,1)`1 & (GoB f1)*(1,width (GoB f1))`2 < s } by A71,GOBOARD6:28;
A78: |.q4.|<r by A13;
A79: |.(|[0,r1+r+1]|).| = sqrt ((p`1)^2+(p`2)^2) by JGRAPH_1:47
    .=r1+r+1 by A78,A73,SQUARE_1:89;
  (GoB f1)*(1,width (GoB f1))`2< (N-bound L~f)+(r1+r+1) by A78,A14,A72,A2,
XREAL_1:31;
  then q1 in Int (left_cell(f1,1)) by A77,A45,A49,A62;
  hence contradiction by A13,A79,A74,A75,XXREAL_0:2;
end;

theorem Th99:
  LeftComp SpStSeq D c= UBD (L~SpStSeq D)
proof
  set f=SpStSeq D;
  set A=L~SpStSeq D;
  LeftComp f is_a_component_of A` & not LeftComp f is Bounded by Th98,
GOBOARD9:def 1;
  then
A1: LeftComp f is_outside_component_of A by Def4;
  LeftComp f c= union{B where B is Subset of TOP-REAL 2: B
  is_outside_component_of A}
  proof
    let x;
    assume
A2: x in LeftComp f;
    LeftComp f in {B where B is Subset of TOP-REAL 2: B
    is_outside_component_of A} by A1;
    hence thesis by A2,TARSKI:def 4;
  end;
  hence thesis;
end;

theorem Th100:
  for G being TopSpace,A,B,C being Subset of G st
  A is a_component & B is a_component & C is connected & A meets C & B
  meets C holds A=B
proof
  let G be TopSpace,A,B,C be Subset of G;
  assume that
A1: A is a_component and
A2: B is a_component and
A3: C is connected and
A4: A meets C and
A5: B meets C;
A6: C /\ A={}G or C c= A by A1,A3,A4,CONNSP_1:38;
A7: C misses B or C c= B by A2,A3,CONNSP_1:38;
  per cases by A1,A2,CONNSP_1:2,36;
  suppose
    A=B;
    hence thesis;
  end;
  suppose
    A misses B;
    then
A8: A /\ B = {} by XBOOLE_0:def 7;
    C c= A /\ B by A4,A5,A6,A7,XBOOLE_0:def 7,XBOOLE_1:19;
    then C ={} by A8;
    then C /\ A = {};
    hence thesis by A4,XBOOLE_0:def 7;
  end;
end;

theorem Th101:
  for B being Subset of TOP-REAL 2 st B is_a_component_of (L~
  SpStSeq D)` & not B is Bounded holds B=LeftComp SpStSeq D
proof
  let B be Subset of TOP-REAL 2;
  set f = SpStSeq D;
  assume that
A1: B is_a_component_of (L~f)` and
A2: not B is Bounded;
A3: ex B1 being Subset of (TOP-REAL 2) | (L~f)` st B1 = B & B1
  is a_component by A1,CONNSP_1:def 6;
  consider r1 being Real such that
A4: for q being Point of TOP-REAL 2 st q in L~f holds |.q.|<r1 by Th40;
  consider q4 being Point of TOP-REAL 2 such that
A5: q4 in B and
A6: |.q4.|>=r1 by A2,Th40;
A7: now
    assume q4 in {q where q is Point of TOP-REAL 2: (|.q.|) <r1};
    then ex q being Point of TOP-REAL 2 st q=q4 & (|.q.|) <r1;
    hence contradiction by A6;
  end;
  reconsider P=(REAL 2)\ {q where q is Point of TOP-REAL 2: (|.q.|) < r1 } as
  Subset of TOP-REAL 2 by EUCLID:25;
  P c= (the carrier of TOP-REAL 2)\(L~f)
  proof
    let z be set;
    assume
A8: z in P;
    now
      assume
A9:   z in L~f;
      then reconsider q3=z as Point of TOP-REAL 2;
A10:  not q3 in {q where q is Point of TOP-REAL 2: (|.q.|) < r1 } by A8,
XBOOLE_0:def 5;
      |.q3.|<r1 by A4,A9;
      hence contradiction by A10;
    end;
    hence thesis by A8,XBOOLE_0:def 5;
  end;
  then
A11: P /\ ((the carrier of TOP-REAL 2)\(L~f))=P by XBOOLE_1:28;
  then
A12: Down(P,(L~f)`) is connected by Th15,Th61;
  not LeftComp f is Bounded by Th98;
  then consider q3 being Point of TOP-REAL 2 such that
A13: q3 in LeftComp f and
A14: |.q3.|>=r1 by Th40;
A15: now
    assume q3 in {q where q is Point of TOP-REAL 2: (|.q.|) <r1};
    then ex q being Point of TOP-REAL 2 st q=q3 & (|.q.|) <r1;
    hence contradiction by A14;
  end;
  q4 in the carrier of TOP-REAL 2;
  then q4 in REAL 2 by EUCLID:25;
  then q4 in P by A7,XBOOLE_0:def 5;
  then
A16: B meets P by A5,XBOOLE_0:3;
  LeftComp f is_a_component_of (L~f)` by GOBOARD9:def 1;
  then consider L1 being Subset of (TOP-REAL 2) | (L~f)` such that
A17: L1 = LeftComp f and
A18: L1 is a_component by CONNSP_1:def 6;
  q3 in the carrier of TOP-REAL 2;
  then q3 in REAL 2 by EUCLID:25;
  then q3 in P by A15,XBOOLE_0:def 5;
  then L1 meets P by A17,A13,XBOOLE_0:3;
  hence thesis by A3,A17,A18,A11,A12,A16,Th100;
end;

theorem Th102:
  RightComp SpStSeq D c= BDD (L~SpStSeq D) & RightComp SpStSeq D is Bounded
proof
  set f=SpStSeq D;
  set A=L~SpStSeq D;
A1: RightComp f is_a_component_of A`by GOBOARD9:def 2;
A2: now
A3: LeftComp f misses RightComp f by SPRECT_1:96;
    assume not (RightComp f) is Bounded;
    hence contradiction by A1,A3,Th101;
  end;
  then
A4: RightComp f is_inside_component_of A by A1,Def3;
  RightComp f c= union{B where B is Subset of TOP-REAL 2: B
  is_inside_component_of A}
  proof
    let x;
    assume
A5: x in RightComp f;
    RightComp f in {B where B is Subset of TOP-REAL 2: B
    is_inside_component_of A} by A4;
    hence thesis by A5,TARSKI:def 4;
  end;
  hence RightComp f c= BDD (L~SpStSeq D);
  thus thesis by A2;
end;

theorem Th103:
  LeftComp SpStSeq D = UBD (L~SpStSeq D) & RightComp SpStSeq D =
  BDD (L~SpStSeq D)
proof
  set f=SpStSeq D;
A1: (L~f)`=LeftComp f \/ RightComp f by GOBRD12:11;
A2: LeftComp f c= UBD (L~SpStSeq D) by Th99;
A3: RightComp f c= BDD (L~SpStSeq D) by Th102;
A4: now
    assume not LeftComp f = UBD (L~SpStSeq D);
    then not UBD (L~SpStSeq D) c= LeftComp f by A2,XBOOLE_0:def 10;
    then consider z being set such that
A5: z in UBD (L~SpStSeq D) and
A6: not z in LeftComp f by TARSKI:def 3;
    UBD (L~f) c= (L~f)` by Th30;
    then z in LeftComp f or z in RightComp f by A1,A5,XBOOLE_0:def 3;
    then (BDD L~f) meets (UBD L~f) by A3,A5,A6,XBOOLE_0:3;
    hence contradiction by Th28;
  end;
  now
    assume not RightComp f = BDD (L~SpStSeq D);
    then not BDD (L~SpStSeq D) c= RightComp f by A3,XBOOLE_0:def 10;
    then consider z being set such that
A7: z in BDD (L~SpStSeq D) and
A8: not z in RightComp f by TARSKI:def 3;
    BDD (L~f) c= (L~f)` by Th29;
    then z in LeftComp f or z in RightComp f by A1,A7,XBOOLE_0:def 3;
    then (BDD L~f) meets (UBD L~f) by A2,A7,A8,XBOOLE_0:3;
    hence contradiction by Th28;
  end;
  hence thesis by A4;
end;

theorem Th104:
  UBD (L~SpStSeq D)<>{} & UBD (L~SpStSeq D)
is_outside_component_of (L~SpStSeq D) & BDD (L~SpStSeq D)<>{} & BDD (L~SpStSeq
  D) is_inside_component_of (L~SpStSeq D)
proof
  set f=SpStSeq D;
A1: UBD (L~SpStSeq D)=LeftComp (SpStSeq D) by Th103;
  hence UBD (L~SpStSeq D)<>{};
  LeftComp f is_a_component_of (L~f)` & not LeftComp f is Bounded by Th98,
GOBOARD9:def 1;
  hence UBD (L~SpStSeq D) is_outside_component_of (L~SpStSeq D) by A1,Def4;
A2: BDD (L~SpStSeq D)=RightComp (SpStSeq D) by Th103;
  hence BDD (L~SpStSeq D)<>{};
  RightComp (SpStSeq D) is_a_component_of (L~f)` & RightComp (SpStSeq D)
  is Bounded by Th102,GOBOARD9:def 2;
  hence thesis by A2,Def3;
end;

begin :: Jordan property and boundary property

theorem Th105:
  for G being non empty TopSpace, A being Subset of G st A`<>{}
holds A is boundary iff for x being set,V being Subset of G st x in A & x in V
  & V is open ex B being Subset of G st B is_a_component_of A` & V meets B
proof
  let G be non empty TopSpace,A be Subset of G;
  assume
A1: A`<>{};
  hereby
    reconsider A1=A` as non empty Subset of G by A1;
    reconsider A2=A` as Subset of G;
    assume A is boundary;
    then A` is dense by TOPS_1:def 4;
    then
A2: Cl (A`)=[#] G by TOPS_1:def 3;
    let x be set,V be Subset of G;
    assume that
    x in A and
A3: x in V & V is open;
    A2 meets V by A3,A2,PRE_TOPC:def 13;
    then consider z being set such that
A4: z in A` and
A5: z in V by XBOOLE_0:3;
    reconsider p=z as Point of G|A` by A4,PRE_TOPC:29;
    Component_of p c= the carrier of G|A`;
    then Component_of p c= A` by PRE_TOPC:29;
    then reconsider B0=Component_of p as Subset of G by XBOOLE_1:1;
A6: G|A1 is non empty;
    then p in Component_of p by CONNSP_1:40;
    then p in V /\ B0 by A5,XBOOLE_0:def 4;
    then
A7: V meets B0 by XBOOLE_0:4;
    Component_of p is a_component by A6,CONNSP_1:43;
    then B0 is_a_component_of A` by CONNSP_1:def 6;
    hence ex B being Subset of G st B is_a_component_of A` & V meets B by A7;
  end;
  assume
A8: for x being set,V being Subset of G st x in A & x in V & V is open
  ex B being Subset of G st B is_a_component_of A` & V meets B;
  the carrier of G c= Cl (A`)
  proof
    let z be set;
    assume
A9: z in the carrier of G;
    per cases;
    suppose
A10:  z in A;
      for G1 being Subset of G st G1 is open holds z in G1 implies (A`)
      meets G1
      proof
        let G1 be Subset of G;
        assume
A11:    G1 is open;
        assume z in G1;
        then consider B being Subset of G such that
A12:    B is_a_component_of A` and
A13:    G1 meets B by A8,A10,A11;
A14:    G1 /\ B <> {} by A13,XBOOLE_0:def 7;
        consider B1 being Subset of G|A` such that
A15:    B1 = B and
        B1 is a_component by A12,CONNSP_1:def 6;
        B1 c= the carrier of (G|A`);
        then B1 c= A` by PRE_TOPC:29;
        then (A`) /\ G1 <> {}G by A15,A14,XBOOLE_1:3,26;
        hence thesis by XBOOLE_0:def 7;
      end;
      hence thesis by A9,PRE_TOPC:def 13;
    end;
    suppose
A16:  not z in A;
A17:  A` c= Cl(A`) by PRE_TOPC:48;
      z in (the carrier of G) \ A by A9,A16,XBOOLE_0:def 5;
      hence thesis by A17;
    end;
  end;
  then Cl (A`)=[#] G by XBOOLE_0:def 10;
  then A` is dense by TOPS_1:def 3;
  hence thesis by TOPS_1:def 4;
end;

theorem Th106:
  for A being Subset of TOP-REAL 2 st A`<>{} holds A is boundary
  & A is Jordan iff ex A1,A2 being Subset of TOP-REAL 2 st A` = A1 \/ A2 & A1
  misses A2 & (Cl A1) \ A1 = (Cl A2) \ A2 & A=(Cl A1) \ A1 & for C1,C2 being
  Subset of (TOP-REAL 2) | A` st C1 = A1 & C2 = A2 holds C1 is a_component
  & C2 is a_component
proof
  let A be Subset of TOP-REAL 2;
  assume
A1: A`<>{};
  hereby
    assume that
A2: A is boundary and
A3: A is Jordan;
    consider A1,A2 being Subset of TOP-REAL 2 such that
A4: A` = A1 \/ A2 and
A5: A1 misses A2 and
A6: (Cl A1) \ A1 = (Cl A2) \ A2 and
A7: for C1,C2 being Subset of (TOP-REAL 2) | A` st C1 = A1 & C2 = A2
holds C1 is a_component & C2 is a_component by A3,JORDAN1:def 2;
    A=(A1 \/ A2)` by A4;
    then
A8: A=A1` /\ A2` by XBOOLE_1:53;
    A2 c= A` by A4,XBOOLE_1:7;
    then reconsider D2=A2 as Subset of (TOP-REAL 2) | A` by PRE_TOPC:29;
    A1 c= A` by A4,XBOOLE_1:7;
    then reconsider D1=A1 as Subset of (TOP-REAL 2) | A` by PRE_TOPC:29;
    D2=A2;
    then
A9: D1 is a_component by A7;
A10: A c= (Cl A1) \ A1
    proof
      let z be set;
      assume
A11:  z in A;
      for G being Subset of (TOP-REAL 2) st G is open holds z in G
      implies (A1 \/ A2) meets G
      proof
        let G be Subset of (TOP-REAL 2);
        assume
A12:    G is open;
        hereby
          assume z in G;
          then consider B being Subset of TOP-REAL 2 such that
A13:      B is_a_component_of A` and
A14:      G meets B by A1,A2,A11,A12,Th105;
          consider B1 being Subset of (TOP-REAL 2) | A` such that
A15:      B1 = B and
A16:      B1 is a_component by A13,CONNSP_1:def 6;
A17:      now
            per cases by A9,A16,CONNSP_1:36;
            case
              B1=D1;
              hence B1 c= A1 \/ A2 by XBOOLE_1:7;
            end;
            case
              B1,D1 are_separated;
              then
A18:          Cl B1 misses D1 or B1 misses Cl D1 by CONNSP_1:def 1;
              B1 is closed & D1 is closed by A9,A16,CONNSP_1:35;
              then B1 misses D1 by A18,PRE_TOPC:52;
              then
A19:          B1 /\ D1={} by XBOOLE_0:def 7;
              B1 c= the carrier of (TOP-REAL 2) | A`;
              then B1 c= A` by PRE_TOPC:29;
              then B1 = B1 /\ A` by XBOOLE_1:28
                .=B1 /\ A1 \/ B1 /\ A2 by A4,XBOOLE_1:23
                .= B1 /\ A2 by A19;
              then
A20:          B1 c= A2 by XBOOLE_1:17;
              A2 c= A1 \/ A2 by XBOOLE_1:7;
              hence B1 c= A1 \/ A2 by A20,XBOOLE_1:1;
            end;
          end;
          G /\ B <> {} by A14,XBOOLE_0:def 7;
          then (A1 \/ A2) /\ G <> {} by A15,A17,XBOOLE_1:3,26;
          hence (A1 \/ A2) meets G by XBOOLE_0:def 7;
        end;
      end;
      then z in Cl (A1 \/ A2) by A11,PRE_TOPC:def 13;
      then z in (Cl A1) \/ Cl A2 by PRE_TOPC:50;
      then
A21:  z in Cl A1 or z in Cl A2 by XBOOLE_0:def 3;
      not z in A` by A11,XBOOLE_0:def 5;
      then ( not z in A1)& not z in A2 by A4,XBOOLE_0:def 3;
      hence thesis by A6,A21,XBOOLE_0:def 5;
    end;
    (Cl A1) \A1 c= A1` & (Cl A2) \A2 c= A2` by XBOOLE_1:33;
    then (Cl A1) \A1 c= A by A6,A8,XBOOLE_1:19;
    then A=Cl A1 \ A1 by A10,XBOOLE_0:def 10;
    hence
    ex A1,A2 being Subset of TOP-REAL 2 st A` = A1 \/ A2 & A1 misses A2 &
    (Cl A1) \ A1 = (Cl A2) \ A2 & A=(Cl A1) \ A1 & for C1,C2 being Subset of
    (TOP-REAL 2) | A` st C1 = A1 & C2 = A2 holds C1 is a_component
    & C2 is a_component by A4,A5,A6,A7;
  end;
  hereby
    assume ex A1,A2 being Subset of TOP-REAL 2 st A` = A1 \/ A2 & A1 misses
A2 & (Cl A1) \ A1 = (Cl A2) \ A2 & A=(Cl A1) \ A1 & for C1,C2 being Subset of
(TOP-REAL 2) | A` st C1 = A1 & C2 = A2 holds C1 is a_component
    & C2 is a_component;
    then consider A1,A2 being Subset of TOP-REAL 2 such that
A22: A` = A1 \/ A2 and
A23: A1 misses A2 & (Cl A1) \ A1 = (Cl A2) \ A2 and
A24: A=(Cl A1) \ A1 and
A25: for C1,C2 being Subset of (TOP-REAL 2) | A` st C1 = A1 & C2 = A2
holds C1 is a_component & C2 is a_component;
    for x being set,V being Subset of TOP-REAL 2 st x in A & x in V & V
is open ex B being Subset of TOP-REAL 2 st B is_a_component_of A` & V meets B
    proof
      A2 c= A` by A22,XBOOLE_1:7;
      then reconsider D2=A2 as Subset of (TOP-REAL 2) | A` by PRE_TOPC:29;
      A1 c= A` by A22,XBOOLE_1:7;
      then reconsider D1=A1 as Subset of (TOP-REAL 2) | A` by PRE_TOPC:29;
      let x be set,V be Subset of TOP-REAL 2;
      assume that
A26:  x in A and
A27:  x in V & V is open;
      D2=A2;
      then D1 is a_component by A25;
      then
A28:  A1 is_a_component_of A` by CONNSP_1:def 6;
      x in Cl A1 by A24,A26,XBOOLE_0:def 5;
      then A1 meets V by A27,PRE_TOPC:def 13;
      hence thesis by A28;
    end;
    hence A is boundary & A is Jordan by A1,A22,A23,A25,Th105,JORDAN1:def 2;
  end;
end;

theorem Th107:
  for p being Point of TOP-REAL n, P being Subset of TOP-REAL n
  st n>=1 & P={p} holds P is boundary
proof
  let p be Point of TOP-REAL n, P be Subset of TOP-REAL n;
  assume that
A1: n>=1 and
A2: P={p};
  the carrier of (TOP-REAL n) c= Cl (P`)
  proof
    let z be set;
    assume
A3: z in the carrier of TOP-REAL n;
    per cases;
    suppose
A4:   z=p;
      reconsider ez=z as Point of Euclid n by A3,TOPREAL3:13;
      for G1 being Subset of (TOP-REAL n) st G1 is open holds z in G1
      implies (P`) meets G1
      proof
        let G1 be Subset of TOP-REAL n;
        assume
A5:     G1 is open;
        thus z in G1 implies (P`) meets G1
        proof
A6:       the TopStruct of TOP-REAL n = TopSpaceMetr Euclid n by EUCLID:def 8;
          then reconsider GG = G1 as Subset of TopSpaceMetr Euclid n;
          assume
A7:       z in G1;
          GG is open by A5,A6,PRE_TOPC:60;
          then consider r be real number such that
A8:       r>0 and
A9:       Ball(ez,r) c= GG by A7,TOPMETR:22;
          reconsider r as Real by XREAL_0:def 1;
          set p2=p-(r/2/sqrt n)*(1.REAL n);
          reconsider ep2=p2 as Point of Euclid n by TOPREAL3:13;
A10:      0<sqrt n by A1,SQUARE_1:93;
A11:      |.p-p2.|=|.p-p+(r/2/sqrt n)*(1.REAL n).| by EUCLID:51
            .=|.(r/2/sqrt n)*(1.REAL n)+p-p.| by EUCLID:49
            .=|.(r/2/sqrt n)*(1.REAL n).| by EUCLID:52
            .=abs(r/2/sqrt n)*|.(1.REAL n).| by TOPRNS_1:8
            .=abs(r/2/sqrt n)*(sqrt n) by Th35
            .=abs(r/2)/abs(sqrt n)*(sqrt n) by COMPLEX1:153
            .=abs(r/2)/(sqrt n)*(sqrt n) by A10,ABSVALUE:def 1
            .=abs(r/2) by A10,XCMPLX_1:88
            .=r/2 by A8,ABSVALUE:def 1;
          r/2>0 by A8,XREAL_1:141;
          then p<>p2 by A11,TOPRNS_1:29;
          then not p2 in P by A2,TARSKI:def 1;
          then
A12:      p2 in P` by XBOOLE_0:def 5;
          r/2<r by A8,XREAL_1:218;
          then dist(ez,ep2)<r by A4,A11,JGRAPH_1:45;
          then p2 in Ball(ez,r) by METRIC_1:12;
          hence thesis by A9,A12,XBOOLE_0:3;
        end;
      end;
      hence thesis by A3,PRE_TOPC:def 13;
    end;
    suppose
      z<>p;
      then not z in P by A2,TARSKI:def 1;
      then
A13:  z in P` by A3,XBOOLE_0:def 5;
      P` c= Cl(P`) by PRE_TOPC:48;
      hence thesis by A13;
    end;
  end;
  then Cl (P`)=[#] (TOP-REAL n) by XBOOLE_0:def 10;
  then P` is dense by TOPS_1:def 3;
  hence thesis by TOPS_1:def 4;
end;

theorem Th108:
  for p,q being Point of TOP-REAL 2,r st p`1=q`2 & -p`2=q`1 & p=r
  *q holds p`1=0 & p`2=0 & p=0.TOP-REAL 2
proof
  let p,q be Point of TOP-REAL 2,r;
A1: 1+r*r>0+0 by XREAL_1:10,65;
  assume p`1=q`2 & -p`2=q`1 & p=r*q;
  then
A2: p=|[r*(-p`2),r*(p`1)]| by EUCLID:61;
  then p`2=r*(p`1) by EUCLID:56;
  then p`1=-(r*(r*(p`1))) by A2,EUCLID:56
    .=-(r*r*(p`1));
  then (1+r*r)*p`1=0;
  hence
A3: p`1=0 by A1,XCMPLX_1:6;
  p`1=r*(-p`2) by A2,EUCLID:56;
  then p`2=-(r*r*(p`2)) by A2,EUCLID:56;
  then (1+r*r)*p`2=0;
  hence p`2=0 by A1,XCMPLX_1:6;
  hence thesis by A3,EUCLID:57,58;
end;

theorem Th109:
  for q1,q2 being Point of TOP-REAL 2 holds LSeg(q1,q2) is boundary
proof
  let q1,q2 be Point of TOP-REAL 2;
  per cases;
  suppose
    q1=q2;
    then LSeg(q1,q2)={q1} by RLTOPSP1:71;
    hence thesis by Th107;
  end;
  suppose
A1: q1<>q2;
    set P=LSeg(q1,q2);
    the carrier of (TOP-REAL 2) c= Cl (P`)
    proof
      let z be set;
      assume
A2:   z in the carrier of TOP-REAL 2;
      per cases;
      suppose
A3:     z in P;
        reconsider ez=z as Point of Euclid 2 by A2,TOPREAL3:13;
        set p1=q1-q2;
        consider s being Real such that
A4:     z=(1-s)*q1+s*q2 and
        0<=s and
        s<=1 by A3;
        set p=(1-s)*q1+s*q2;
A5:     now
          assume |.p1.|=0;
          then p1=0.TOP-REAL 2 by TOPRNS_1:25;
          hence contradiction by A1,EUCLID:47;
        end;
        for G1 being Subset of (TOP-REAL 2) st G1 is open holds z in G1
        implies (P`) meets G1
        proof
          let G1 be Subset of TOP-REAL 2;
          assume
A6:       G1 is open;
          thus z in G1 implies (P`) meets G1
          proof
A7:         the TopStruct of TOP-REAL 2 = TopSpaceMetr Euclid 2 by EUCLID:def 8
;
            then reconsider GG = G1 as Subset of TopSpaceMetr Euclid 2;
            assume
A8:         z in G1;
            GG is open by A6,A7,PRE_TOPC:60;
            then consider r be real number such that
A9:         r>0 and
A10:        Ball(ez,r) c= G1 by A8,TOPMETR:22;
            reconsider r as Real by XREAL_0:def 1;
A11:        r/2<r by A9,XREAL_1:218;
            set p2=(r/2/|.p1.|)*|[-p1`2,p1`1]| +p;
            now
              assume p2 in P;
              then consider s2 being Real such that
A12:          p2=(1-s2)*q1+s2*q2 and
              0<=s2 and
              s2<=1;
A13:          now
                assume s-s2=0;
                then (r/2/|.p1.|)*|[-p1`2,p1`1]|=p-p by A12,EUCLID:52;
                then
A14:            (r/2/|.p1.|)*|[-p1`2,p1`1]|=0.TOP-REAL 2 by EUCLID:46;
A15:            (r/2/|.p1.|)=r*2"*(|.p1.|)" by XCMPLX_0:def 9
                  .=r*(2"*(|. p1.|)");
                2"*(|.p1.|)" <>0 by A5;
                then |[-p1`2,p1`1]|=0.TOP-REAL 2 by A9,A14,A15,EUCLID:35
,XCMPLX_1:6;
                then
A16:            (0.TOP-REAL 2)`1=-p1`2 & (0.TOP-REAL 2)`2=p1`1 by EUCLID:56;
                (0.TOP-REAL 2)`1=0 & (0.TOP-REAL 2)`2=0 by EUCLID:56,58;
                hence contradiction by A1,A16,EUCLID:47,57,58;
              end;
A17:          p2-p = (r/2/|.p1.|)*|[-p1`2,p1`1]| by EUCLID:52;
              p2-p=(1-s2)*q1+s2*q2 -(1-s)*q1-s*q2 by A12,EUCLID:50
                .=(1-s2)*q1-(1-s)*q1+s2*q2 -s*q2 by EUCLID:30
                .=((1-s2)-(1-s))*q1+s2*q2 -s*q2 by EUCLID:54
                .=(s-s2)*q1+(s2*q2 -s*q2) by EUCLID:49
                .=(s-s2)*q1+(s2-s)*q2 by EUCLID:54
                .=(s-s2)*q1+(-(s-s2))*q2
                .=(s-s2)*q1-(s-s2)*q2 by EUCLID:44
                .=(s-s2)*p1 by EUCLID:53;
              then 1/(s-s2)*(s-s2)*p1= 1/(s-s2)*((r/2/|.p1.|)*|[- p1`2,p1`1]|
              ) by A17,EUCLID:34;
              then 1 *p1= 1/(s-s2)*((r/2/|.p1.|)*|[-p1`2,p1`1]|) by A13,
XCMPLX_1:107;
              then p1= 1/(s-s2)*((r/2/|.p1.|)*|[-p1`2,p1`1]|) by EUCLID:33;
              then
A18:          p1= 1/(s-s2)*(r/2/|.p1.|)*|[-p1`2,p1`1]| by EUCLID:34;
              p1`1=(|[-p1`2,p1`1]|)`2 & -p1`2=(|[-p1`2,p1`1]|)`1 by EUCLID:56;
              then p1=0.TOP-REAL 2 by A18,Th108;
              hence contradiction by A1,EUCLID:47;
            end;
            then
A19:        p2 in (the carrier of TOP-REAL 2) \P by XBOOLE_0:def 5;
            reconsider ep2=p2 as Point of Euclid 2 by TOPREAL3:13;
A20:        p+-(r/2/|.p1.|)*|[-p1`2,p1`1]| -p = -(r/2/|.p1.|)*|[-p1`2,p1
            `1]| by EUCLID:52;
A21:        |[-p1`2,p1`1]|`1=-p1`2 & |[-p1`2,p1`1]|`2=p1`1 by EUCLID:56;
            |.p-p2.|=|.p-(r/2/|.p1.|)*|[-p1`2,p1`1]| -p.| by EUCLID:50
              .=|.-(r/2/|.p1.|)*|[-p1`2,p1`1]|.| by A20
              .=|.(r/2/|.p1.|)*|[-p1`2,p1`1]|.| by TOPRNS_1:27
              .=abs(r/2/|.p1.|)*|.|[-p1`2,p1`1]|.| by TOPRNS_1:8
              .=abs(r/2/|.p1.|)*(sqrt ((-p1`2)^2+(p1`1)^2)) by A21,JGRAPH_1:47
              .=abs(r/2/|.p1.|)*(sqrt ((p1`1)^2+(p1`2)^2))
              .=abs(r/2/|.p1.|)*|.p1.| by JGRAPH_1:47
              .=abs(r/2)/(abs(|.p1.|))*|.p1.| by COMPLEX1:153
              .=abs(r/2)/(|.p1.|)*|.p1.| by ABSVALUE:def 1
              .=abs (r/2) by A5,XCMPLX_1:88
              .=r/2 by A9,ABSVALUE:def 1;
            then dist(ez,ep2)<r by A4,A11,JGRAPH_1:45;
            then p2 in Ball(ez,r) by METRIC_1:12;
            hence thesis by A10,A19,XBOOLE_0:3;
          end;
        end;
        hence thesis by A2,PRE_TOPC:def 13;
      end;
      suppose
A22:    not z in P;
A23:    P` c= Cl(P`) by PRE_TOPC:48;
        z in (the carrier of (TOP-REAL 2)) \ P by A2,A22,XBOOLE_0:def 5;
        hence thesis by A23;
      end;
    end;
    then Cl (P`)=[#] (TOP-REAL 2) by XBOOLE_0:def 10;
    then P` is dense by TOPS_1:def 3;
    hence thesis by TOPS_1:def 4;
  end;
end;

registration
  let q1,q2 be Point of TOP-REAL 2;
  cluster LSeg(q1,q2) -> boundary;
  coherence by Th109;
end;

theorem Th110:
  for f being FinSequence of TOP-REAL 2 holds L~f is boundary
proof
  let f be FinSequence of TOP-REAL 2;
A1: L~f=union { LSeg(f,i) : 1 <= i & i+1 <= len f } by TOPREAL1:def 6;
  defpred P[Element of NAT] means for R1 being Subset of TOP-REAL 2 st R1=
  union { LSeg(f,i) : 1 <= i & i+1 <= $1 } holds R1 is boundary;
A2: for k being Element of NAT st P[k] holds P[k+1]
  proof
    let k be Element of NAT;
A3: now
      per cases;
      case
        1<=k & k+1<=len f;
        then LSeg(f,k)=LSeg(f/.k,f/.(k+1)) by TOPREAL1:def 5;
        hence LSeg(f,k) is boundary;
      end;
      case
        not(1<=k & k+1<=len f);
        then LSeg(f,k)={} by TOPREAL1:def 5;
        hence LSeg(f,k) is boundary;
      end;
    end;
    union { LSeg(f,i2) : 1 <= i2 & i2+1 <= k } c= the carrier of TOP-REAL 2
    proof
      let z be set;
      assume z in union { LSeg(f,i2) : 1 <= i2 & i2+1 <= k };
      then consider x such that
A4:   z in x & x in { LSeg(f,i) : 1 <= i & i+1 <= k } by TARSKI:def 4;
      ex i st x=LSeg(f,i) & 1 <= i & i+1 <= k by A4;
      hence thesis by A4;
    end;
    then reconsider R3=union { LSeg(f,i2) : 1 <= i2 & i2+1 <= k } as Subset of
    TOP-REAL 2;
    assume for R1 being Subset of TOP-REAL 2 st R1=union { LSeg(f,i) : 1 <= i
    & i+1 <= k } holds R1 is boundary;
    then
A5: R3 is boundary;
    thus for R2 being Subset of TOP-REAL 2 st R2=union { LSeg(f,i2) : 1 <= i2
    & i2+1 <= k+1 } holds R2 is boundary
    proof
      let R2 be Subset of TOP-REAL 2;
      assume
A6:   R2=union { LSeg(f,i2) : 1 <= i2 & i2+1 <= k+1 };
A7:   R3 \/ LSeg(f,k) c= R2
      proof
        let z be set;
        assume
A8:     z in R3 \/ LSeg(f,k);
        per cases by A8,XBOOLE_0:def 3;
        suppose
          z in R3;
          then consider x such that
A9:       z in x & x in { LSeg(f,i2) : 1 <= i2 & i2+1 <= k } by TARSKI:def 4;
          consider i2 such that
A10:      x=LSeg(f,i2) & 1 <= i2 and
A11:      i2+1 <= k by A9;
          i2+1<k+1 by A11,NAT_1:13;
          then x in { LSeg(f,j) : 1 <= j & j+1 <= k+1 } by A10;
          hence thesis by A6,A9,TARSKI:def 4;
        end;
        suppose
A12:      z in LSeg(f,k);
          now
            per cases;
            suppose
              1<=k;
              then LSeg(f,k) in { LSeg(f,i2) : 1 <= i2 & i2+1 <= k+1 };
              hence thesis by A6,A12,TARSKI:def 4;
            end;
            suppose
              k<1;
              hence thesis by A12,TOPREAL1:def 5;
            end;
          end;
          hence thesis;
        end;
      end;
      R2 c= R3 \/ LSeg(f,k)
      proof
        let z be set;
        assume z in R2;
        then consider x such that
A13:    z in x & x in { LSeg(f,i2) : 1 <= i2 & i2+1 <= k+1 } by A6,TARSKI:def 4
;
        consider i2 such that
A14:    x=LSeg(f,i2) and
A15:    1 <= i2 and
A16:    i2+1 <= k+1 by A13;
        now
          per cases;
          case
            i2+1<=k;
            then x in { LSeg(f,j) : 1 <= j & j+1 <= k } by A14,A15;
            hence z in R3 or z in LSeg(f,k) by A13,TARSKI:def 4;
          end;
          case
            i2+1>k;
            then k+1<=i2+1 by NAT_1:13;
            then i2+1=k+1 by A16,XXREAL_0:1;
            hence z in R3 or z in LSeg(f,k) by A13,A14;
          end;
        end;
        hence thesis by XBOOLE_0:def 3;
      end;
      then R2=R3 \/ LSeg(f,k) by A7,XBOOLE_0:def 10;
      hence thesis by A5,A3,TOPS_1:85;
    end;
  end;
  union { LSeg(f,i) : 1 <= i & i+1 <= 0 } c= {}
  proof
    let z be set;
    assume z in union { LSeg(f,i) : 1 <= i & i+1 <= 0 };
    then consider x such that
A17: z in x & x in { LSeg(f,i) : 1 <= i & i+1 <= 0 } by TARSKI:def 4;
    ex i st x=LSeg(f,i) & 1 <= i & i+1 <= 0 by A17;
    hence thesis;
  end;
  then
A18: P[0];
  for j holds P[j] from NAT_1:sch 1(A18,A2);
  hence thesis by A1;
end;

registration
  let f be FinSequence of TOP-REAL 2;
  cluster L~f -> boundary;
  coherence by Th110;
end;

theorem Th111:
  for ep being Point of Euclid n,p,q being Point of TOP-REAL n st
  p=ep & q in Ball(ep,r) holds |.p-q.|<r & |.q-p.|<r
proof
  let ep be Point of Euclid n,p,q be Point of TOP-REAL n;
  assume that
A1: p=ep and
A2: q in Ball(ep,r);
  reconsider eq=q as Point of Euclid n by TOPREAL3:13;
  dist(ep,eq)<r by A2,METRIC_1:12;
  hence thesis by A1,JGRAPH_1:45;
end;

theorem
  for a being Real,p being Point of TOP-REAL 2 st a>0 & p in L~SpStSeq D
  holds ex q being Point of TOP-REAL 2 st q in UBD (L~SpStSeq D) & |.p-q.|<a
proof
  let a be Real,p be Point of TOP-REAL 2;
  assume that
A1: a>0 and
A2: p in L~SpStSeq D;
  consider q1 being Element of UBD (L~SpStSeq D);
  set A=L~SpStSeq D;
  A`<>{} by SPRECT_1:def 3;
  then consider A1,A2 being Subset of TOP-REAL 2 such that
A3: A` = A1 \/ A2 and
  A1 misses A2 and
A4: (Cl A1) \ A1 = (Cl A2) \ A2 and
A5: A=(Cl A1) \ A1 and
A6: for C1,C2 being Subset of (TOP-REAL 2) | A` st C1 = A1 & C2 = A2 holds
C1 is a_component & C2 is a_component
by Th106;
A7: Down(A2,A`)=A2 by A3,XBOOLE_1:21;
  UBD A is_outside_component_of A by Th76;
  then UBD (L~SpStSeq D) is_a_component_of A` by Def4;
  then consider B1 being Subset of (TOP-REAL 2) | A` such that
A8: B1 = UBD (L~SpStSeq D) and
A9: B1 is a_component by CONNSP_1:def 6;
  B1 c= [#]((TOP-REAL 2) | A`);
  then
A10: UBD (L~SpStSeq D) c= A1 \/ A2 by A3,A8,PRE_TOPC:def 10;
A11: Down(A1,A`)=A1 by A3,XBOOLE_1:21;
  then
A12: Down(A1,A`) is a_component by A6,A7;
A13: Down(A2,A`) is a_component by A6,A11,A7;
A14: UBD (L~SpStSeq D) <>{} by Th104;
  then
A15: q1 in UBD (L~SpStSeq D);
  per cases by A10,A15,XBOOLE_0:def 3;
  suppose
    q1 in A1;
    then B1 /\ Down(A1,A`)<>{}((TOP-REAL 2) | A`) by A11,A8,A14,XBOOLE_0:def 4;
    then B1 meets Down(A1,A`) by XBOOLE_0:def 7;
    then B1=Down(A1,A`) by A12,A9,CONNSP_1:37;
    then
A16: p in Cl(UBD (L~SpStSeq D)) by A2,A5,A11,A8,XBOOLE_0:def 5;
    reconsider ep=p as Point of Euclid 2 by TOPREAL3:13;
    reconsider G2=Ball(ep,a) as Subset of TOP-REAL 2 by TOPREAL3:13;
    the distance of Euclid 2 is Reflexive by METRIC_1:def 7;
    then (the distance of Euclid 2).(ep,ep)=0 by METRIC_1:def 3;
    then dist(ep,ep)=0 by METRIC_1:def 1;
    then
A17: p in Ball(ep,a) by A1,METRIC_1:12;
    G2 is open by GOBOARD6:6;
    then (UBD (L~SpStSeq D)) meets G2 by A16,A17,PRE_TOPC:def 13;
    then consider t2 being set such that
A18: t2 in UBD (L~SpStSeq D) and
A19: t2 in G2 by XBOOLE_0:3;
    reconsider qt2=t2 as Point of TOP-REAL 2 by A18;
    |.p-qt2.|<a by A19,Th111;
    hence thesis by A18;
  end;
  suppose
    q1 in A2;
    then B1 /\ Down(A2,A`)<>{}((TOP-REAL 2) | A`) by A7,A8,A14,XBOOLE_0:def 4;
    then B1 meets Down(A2,A`) by XBOOLE_0:def 7;
    then B1=Down(A2,A`) by A13,A9,CONNSP_1:37;
    then
A20: p in Cl(UBD (L~SpStSeq D)) by A2,A4,A5,A7,A8,XBOOLE_0:def 5;
    reconsider ep=p as Point of Euclid 2 by TOPREAL3:13;
    reconsider G2=Ball(ep,a) as Subset of TOP-REAL 2 by TOPREAL3:13;
    (the distance of (Euclid 2)) is Reflexive by METRIC_1:def 7;
    then (the distance of Euclid 2).(ep,ep)=0 by METRIC_1:def 3;
    then dist(ep,ep)=0 by METRIC_1:def 1;
    then
A21: p in Ball(ep,a) by A1,METRIC_1:12;
    G2 is open by GOBOARD6:6;
    then (UBD (L~SpStSeq D)) meets G2 by A20,A21,PRE_TOPC:def 13;
    then consider t2 being set such that
A22: t2 in (UBD (L~SpStSeq D)) and
A23: t2 in G2 by XBOOLE_0:3;
    reconsider qt2=t2 as Point of TOP-REAL 2 by A22;
    |.p-qt2.|<a by A23,Th111;
    hence thesis by A22;
  end;
end;

theorem Th113:
  REAL 0 = {0.TOP-REAL 0}
proof
  thus REAL 0 = { <*>REAL } by FINSEQ_2:112
    .= {0.TOP-REAL 0};
end;

theorem Th114:
  for A being Subset of TOP-REAL n st A is Bounded holds BDD A is Bounded
proof
  let A be Subset of TOP-REAL n;
  assume A is Bounded;
  then consider r being Real such that
A1: for q being Point of TOP-REAL n st q in A holds |.q.|<r by Th40;
  per cases;
  suppose
A2: n>=1;
    set a=r;
    reconsider P=(REAL n)\ {q : (|.q.|) < a } as Subset of TOP-REAL n by
EUCLID:25;
A3: P c= A`
    proof
      let z;
      assume
A4:   z in P;
      then reconsider q0=z as Point of TOP-REAL n;
      not z in {q : (|.q.|) < a } by A4,XBOOLE_0:def 5;
      then (|.q0.|) >= a;
      then not q0 in A by A1;
      hence thesis by XBOOLE_0:def 5;
    end;
    then
A5: Down(P,A`)=P by XBOOLE_1:28;
    now
      per cases;
      suppose
        n>=2;
        then
A6:     P is connected by Th61;
        now
          assume not BDD A is Bounded;
          then consider q being Point of TOP-REAL n such that
A7:       q in BDD A and
A8:       not |.q.|<r by Th40;
          consider y such that
A9:       q in y and
A10:      y in {B3 where B3 is Subset of TOP-REAL n: B3
          is_inside_component_of A} by A7,TARSKI:def 4;
          consider B3 being Subset of TOP-REAL n such that
A11:      y=B3 and
A12:      B3 is_inside_component_of A by A10;
          q in the carrier of TOP-REAL n;
          then
A13:      q in REAL n by EUCLID:25;
          B3 is_a_component_of A` by A12,Def3;
          then consider B4 being Subset of (TOP-REAL n) | A` such that
A14:      B4 = B3 and
A15:      B4 is a_component by CONNSP_1:def 6;
          for q2 being Point of TOP-REAL n st q2=q holds |.q2.| >= a by A8;
          then not q in {q2 where q2 is Point of TOP-REAL n: (|.q2.|) < a };
          then q in P by A13,XBOOLE_0:def 5;
          then P /\ B4<>{}((TOP-REAL n) | A`) by A9,A11,A14,XBOOLE_0:def 4;
          then P meets B4 by XBOOLE_0:def 7;
          then
A16:      P c= B4 by A5,A6,A15,Th15,CONNSP_1:38;
          B3 is Bounded by A12,Def3;
          hence contradiction by A2,A14,A16,Th16,Th62;
        end;
        hence thesis;
      end;
      suppose
A17:    n<2;
        {q where q is Point of TOP-REAL n: for r2 being Real st q=|[r2]|
        holds r2>=a} c= the carrier of TOP-REAL n
        proof
          let z;
          assume z in {q where q is Point of TOP-REAL n: for r2 being Real
          st q=|[r2]| holds r2>=a};
          then ex q being Point of TOP-REAL n st q=z & for r2 being Real st q=
          |[r2]| holds r2>=a;
          hence thesis;
        end;
        then reconsider
        P2={q where q is Point of TOP-REAL n: for r2 being Real st
        q=|[r2]| holds r2>=a} as Subset of TOP-REAL n;
        {q where q is Point of TOP-REAL n: for r2 being Real st q=|[r2]|
        holds r2<=-a} c= the carrier of TOP-REAL n
        proof
          let z;
          assume z in {q where q is Point of TOP-REAL n: for r2 being Real
          st q=|[r2]| holds r2<=-a};
          then ex q being Point of TOP-REAL n st q=z & for r2 being Real st q=
          |[r2]| holds r2<=-a;
          hence thesis;
        end;
        then reconsider
        P1={q where q is Point of TOP-REAL n: for r2 being Real st
        q=|[r2]| holds r2<=-a} as Subset of TOP-REAL n;
        n<1+1 by A17;
        then
A18:    n<=1 by NAT_1:13;
        then
A19:    n=1 by A2,XXREAL_0:1;
A20:    P c= P1 \/ P2
        proof
          let z;
          assume
A21:      z in P;
          then reconsider q0=z as Point of TOP-REAL n;
          consider r3 being Real such that
A22:      q0=<*r3*> by A19,JORDAN2B:24;
          not z in {q : (|.q.|) < a } by A21,XBOOLE_0:def 5;
          then (|.q0.|) >= a;
          then
A23:      abs r3>=a by A22,Th12;
          per cases by A23,SEQ_2:9;
          suppose
            -a>=r3;
            then for r2 being Real st q0=|[r2]| holds r2<=-a by A22,JORDAN2B:29
;
            then q0 in P1;
            hence thesis by XBOOLE_0:def 3;
          end;
          suppose
            r3>=a;
            then for r2 being Real st q0=|[r2]| holds r2>=a by A22,JORDAN2B:29;
            then q0 in P2;
            hence thesis by XBOOLE_0:def 3;
          end;
        end;
        P1 \/ P2 c= P
        proof
          let z;
          assume
A24:      z in P1 \/ P2;
          per cases by A24,XBOOLE_0:def 3;
          suppose
A25:        z in P1;
            then
A26:        ex q being Point of TOP-REAL n st q=z & for r2 being Real st q
            =|[r2]| holds r2<=-a;
            for q2 being Point of TOP-REAL n st q2=z holds |.q2.| >= a
            proof
              let q2 be Point of TOP-REAL n;
              consider r3 being Real such that
A27:          q2=<*r3*> by A19,JORDAN2B:24;
              assume
A28:          q2=z;
              then
A29:          r3<=-a by A26,A27;
              now
                per cases;
                case
                  a<0;
                  hence abs r3 >=a by COMPLEX1:132;
                end;
                case
                  a>=0;
                  then -a<=-0;
                  then abs r3=-r3 by A29,Th1;
                  hence abs r3>=a by A26,A28,A27,XREAL_1:27;
                end;
              end;
              hence thesis by A27,Th12;
            end;
            then
A30:        not z in {q2 where q2 is Point of TOP-REAL n: |.q2.| < a };
            z in the carrier of TOP-REAL n by A25;
            then z in REAL n by EUCLID:25;
            hence thesis by A30,XBOOLE_0:def 5;
          end;
          suppose
A31:        z in P2;
            then
A32:        ex q being Point of TOP-REAL n st q=z & for r2 being Real st q
            =|[r2]| holds r2>=a;
            for q2 being Point of TOP-REAL n st q2=z holds |.q2.| >= a
            proof
              let q2 be Point of TOP-REAL n;
              consider r3 being Real such that
A33:          q2=<*r3*> by A19,JORDAN2B:24;
              assume q2=z;
              then
A34:          r3>=a by A32,A33;
              now
                per cases;
                suppose
                  a<0;
                  hence abs r3>=a by COMPLEX1:132;
                end;
                suppose
                  a>=0;
                  hence abs r3 >=a by A34,ABSVALUE:def 1;
                end;
              end;
              hence thesis by A33,Th12;
            end;
            then
A35:        not z in {q2 where q2 is Point of TOP-REAL n: |.q2.| < a };
            z in the carrier of TOP-REAL n by A31;
            then z in REAL n by EUCLID:25;
            hence thesis by A35,XBOOLE_0:def 5;
          end;
        end;
        then
A36:    P=P1 \/ P2 by A20,XBOOLE_0:def 10;
        then P2 c= P by XBOOLE_1:7;
        then
A37:    Down(P2,A`)=P2 by A3,XBOOLE_1:1,28;
        for w1,w2 being Point of TOP-REAL n st w1 in P2 & w2 in P2 holds
        LSeg(w1,w2) c= P2
        proof
          let w1,w2 be Point of TOP-REAL n;
          assume that
A38:      w1 in P2 and
A39:      w2 in P2;
A40:      ex q2 being Point of TOP-REAL n st q2=w2 & for r2 being Real st
          q2=|[r2]| holds r2>=a by A39;
          consider r3 being Real such that
A41:      w1=<*r3*> by A19,JORDAN2B:24;
          consider r4 being Real such that
A42:      w2=<*r4*> by A19,JORDAN2B:24;
A43:      ex q1 being Point of TOP-REAL n st q1=w1 & for r2 being Real st
          q1=|[r2]| holds r2>=a by A38;
          thus LSeg(w1,w2) c= P2
          proof
            let z;
            assume z in LSeg(w1,w2);
            then consider r2 such that
A44:        z=(1-r2)*w1 + r2*w2 and
A45:        0 <= r2 and
A46:        r2 <= 1;
            reconsider q4=z as Point of TOP-REAL n by A44;
            (1-r2)*w1=|[(1-r2)*r3]| & r2*w2=|[r2*r4]| by A19,A41,A42,
JORDAN2B:26;
            then
A47:        z=|[(1-r2)*r3+r2*r4]| by A19,A44,JORDAN2B:27;
            for r5 being Real st q4=|[r5]| holds r5>=a
            proof
              let r5 be Real;
              assume q4=|[r5]|;
              then
A48:          r5=(1-r2)*r3+r2*r4 by A47,JORDAN2B:29;
              1-r2>=0 by A46,XREAL_1:50;
              then
A49:          (1-r2)*r3>=(1-r2)*(a) by A43,A41,XREAL_1:66;
              r2*r4>=r2*(a) & (1-r2)*(a)+r2*(a)=a by A40,A42,A45,XREAL_1:66;
              hence thesis by A48,A49,XREAL_1:9;
            end;
            hence thesis;
          end;
        end;
        then P2 is convex by JORDAN1:def 1;
        then
A50:    Down(P2,A`) is connected by A37,Th15;
        P1 c= P by A36,XBOOLE_1:7;
        then
A51:    Down(P1,A`)=P1 by A3,XBOOLE_1:1,28;
A52:    now
          assume P2 is Bounded;
          then consider r being Real such that
A53:      for q being Point of TOP-REAL n st q in P2 holds |.q.|<r by Th40;
          0<=abs(r) & 0<=abs(a) by COMPLEX1:132;
          then
A54:      abs((abs(r)+abs(a)))=abs(r)+abs(a) by ABSVALUE:def 1;
          set p3=|[(abs(r)+abs(a))]|;
A55:      abs(r)<=abs(r)+abs(a) by COMPLEX1:132,XREAL_1:33;
          for r5 being Real st p3=|[r5]| holds r5>=a
          proof
            let r5 be Real;
            assume p3=|[r5]|;
            then
A56:        r5=(abs(r)+abs(a)) by JORDAN2B:29;
            a<=abs(a) & abs(a)<=abs(a)+abs(r) by ABSVALUE:11,COMPLEX1:132
,XREAL_1:33;
            hence thesis by A56,XXREAL_0:2;
          end;
          then
A57:      p3 in P2 by A19;
          |.p3.|=abs((abs(r)+abs(a))) & r<=abs(r) by Th12,ABSVALUE:11;
          hence contradiction by A53,A57,A54,A55,XXREAL_0:2;
        end;
A58:    now
          assume P1 is Bounded;
          then consider r being Real such that
A59:      for q being Point of TOP-REAL n st q in P1 holds |.q.|<r by Th40;
          0<=abs(r) & 0<=abs(a) by COMPLEX1:132;
          then
A60:      abs((abs(r)+abs(a)))=abs(r)+abs(a) by ABSVALUE:def 1;
          set p3=|[-(abs(r)+abs(a))]|;
A61:      r<=abs(r) & abs(r)<=abs(r)+abs(a) by ABSVALUE:11,COMPLEX1:132
,XREAL_1:33;
          for r5 being Real st p3=|[r5]| holds r5<=-a
          proof
            let r5 be Real;
            a<=abs(a) by ABSVALUE:11;
            then
A62:        -abs(a)<=-a by XREAL_1:26;
            abs(a)<=abs(a)+abs(r) by COMPLEX1:132,XREAL_1:33;
            then
A63:        -abs(a)>= -(abs(a)+abs(r)) by XREAL_1:26;
            assume p3=|[r5]|;
            then r5=-(abs(r)+abs(a)) by JORDAN2B:29;
            hence thesis by A62,A63,XXREAL_0:2;
          end;
          then
A64:      p3 in P1 by A19;
          |.p3.|=abs(-(abs(r)+abs(a))) by Th12
            .=abs ((abs(r)+abs(a))) by COMPLEX1:138;
          hence contradiction by A59,A64,A60,A61,XXREAL_0:2;
        end;
        for w1,w2 being Point of TOP-REAL n st w1 in P1 & w2 in P1 holds
        LSeg(w1,w2) c= P1
        proof
          let w1,w2 be Point of TOP-REAL n;
          assume that
A65:      w1 in P1 and
A66:      w2 in P1;
          consider r4 being Real such that
A67:      w2=<*r4*> by A19,JORDAN2B:24;
          ex q2 being Point of TOP-REAL n st q2=w2 & for r2 being Real st
          q2=|[r2]| holds r2<=-a by A66;
          then
A68:      r4<=-a by A67;
          consider r3 being Real such that
A69:      w1=<*r3*> by A19,JORDAN2B:24;
          ex q1 being Point of TOP-REAL n st q1=w1 & for r2 being Real st
          q1=|[r2]| holds r2<=-a by A65;
          then
A70:      r3<=-a by A69;
          thus LSeg(w1,w2) c= P1
          proof
            let z;
            assume z in LSeg(w1,w2);
            then consider r2 such that
A71:        z=(1-r2)*w1 + r2*w2 and
A72:        0 <= r2 and
A73:        r2 <= 1;
            reconsider q4=z as Point of TOP-REAL n by A71;
A74:        r2*w2=|[r2*r4]| by A19,A67,JORDAN2B:26;
            (1-r2)*w1 = (1-r2)*|[r3]| by A2,A18,A69,XXREAL_0:1
              .=|[(1-r2)*r3]| by JORDAN2B:26;
            then
A75:        z=|[(1-r2)*r3+r2*r4]| by A19,A71,A74,JORDAN2B:27;
            for r5 being Real st q4=|[r5]| holds r5<=-a
            proof
              let r5 be Real;
              assume q4=|[r5]|;
              then
A76:          r5=(1-r2)*r3+r2*r4 by A75,JORDAN2B:29;
              1-r2>=0 by A73,XREAL_1:50;
              then
A77:          (1-r2)*r3<=(1-r2)*(-a) by A70,XREAL_1:66;
              r2*r4<=r2*(-a) & (1-r2)*(-a)+r2*(-a)=-a by A68,A72,XREAL_1:66;
              hence thesis by A76,A77,XREAL_1:9;
            end;
            hence thesis;
          end;
        end;
        then P1 is convex by JORDAN1:def 1;
        then
A78:    Down(P1,A`) is connected by A51,Th15;
        now
          assume not BDD A is Bounded;
          then consider q being Point of TOP-REAL n such that
A79:      q in BDD A and
A80:      not |.q.|<r by Th40;
          consider y such that
A81:      q in y and
A82:      y in {B3 where B3 is Subset of TOP-REAL n: B3
          is_inside_component_of A} by A79,TARSKI:def 4;
          consider B3 being Subset of TOP-REAL n such that
A83:      y=B3 and
A84:      B3 is_inside_component_of A by A82;
          q in the carrier of TOP-REAL n;
          then
A85:      q in REAL n by EUCLID:25;
          for q2 being Point of TOP-REAL n st q2=q holds |.q2.| >= a by A80;
          then not q in {q2 where q2 is Point of TOP-REAL n: |.q2.| < a };
          then
A86:      q in P by A85,XBOOLE_0:def 5;
          B3 is_a_component_of A` by A84,Def3;
          then consider B4 being Subset of (TOP-REAL n) | A` such that
A87:      B4 = B3 and
A88:      B4 is a_component by CONNSP_1:def 6;
          per cases by A20,A86,XBOOLE_0:def 3;
          suppose
            q in P1;
            then P1 /\ B4<>{}((TOP-REAL n) | A`) by A81,A83,A87,XBOOLE_0:def 4;
            then
A89:        P1 meets B4 by XBOOLE_0:def 7;
            B3 is Bounded by A84,Def3;
            hence contradiction by A51,A58,A78,A87,A88,A89,Th16,CONNSP_1:38;
          end;
          suppose
            q in P2;
            then P2 /\ B4<>{}((TOP-REAL n) | A`) by A81,A83,A87,XBOOLE_0:def 4;
            then
A90:        P2 meets B4 by XBOOLE_0:def 7;
            B3 is Bounded by A84,Def3;
            hence contradiction by A37,A52,A50,A87,A88,A90,Th16,CONNSP_1:38;
          end;
        end;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
  suppose
    n<1;
    then n<0+1;
    then n<=0 by NAT_1:13;
    then
A91: n=0 by NAT_1:2;
    for q2 being Point of TOP-REAL n holds |.q2.|<1
    proof
      let q2 be Point of TOP-REAL n;
      q2=0.TOP-REAL n by A91,Th113;
      hence thesis by TOPRNS_1:24;
    end;
    then for q2 being Point of TOP-REAL n st q2 in [#] (TOP-REAL n) holds |.
    q2.|<1;
    then [#](TOP-REAL n) is Bounded by Th40;
    hence thesis by Th16;
  end;
end;

theorem Th115:
  for G being non empty TopSpace,A,B,C,D being Subset of G st B
  is a_component & C is a_component & A \/ B=the carrier of G & C
  misses A holds C=B
proof
  let G be non empty TopSpace,A,B,C,D be Subset of G;
  assume that
A1: B is a_component and
A2: C is a_component and
A3: A \/ B=the carrier of G and
A4: C misses A;
  now
    C /\ (the carrier of G)=C by XBOOLE_1:28;
    then
A5: C /\ A \/ C /\ B=C by A3,XBOOLE_1:23;
    assume C misses B;
    then
A6: C /\ B = {} by XBOOLE_0:def 7;
    C <> {}G by A2,CONNSP_1:34;
    hence contradiction by A4,A6,A5,XBOOLE_0:def 7;
  end;
  hence thesis by A1,A2,CONNSP_1:37;
end;

theorem Th116:
  for A being Subset of TOP-REAL 2 st A is Bounded & A is Jordan
  holds BDD A is_inside_component_of A
proof
  let A be Subset of TOP-REAL 2;
  assume that
A1: A is Bounded and
A2: A is Jordan;
  reconsider D=A` as non empty Subset of TOP-REAL 2 by A2,JORDAN1:def 2;
  consider A1,A2 being Subset of TOP-REAL 2 such that
A3: A` = A1 \/ A2 and
A4: A1 misses A2 and
  (Cl A1) \ A1 = (Cl A2) \ A2 and
A5: for C1,C2 being Subset of (TOP-REAL 2) | A` st C1 = A1 & C2 = A2 holds
C1 is a_component & C2 is a_component
by A2,JORDAN1:def 2;
A6: UBD A is_outside_component_of A by A1,Th76;
  then UBD A is_a_component_of A` by Def4;
  then consider B1 being Subset of (TOP-REAL 2) | A` such that
A7: B1 = UBD A and
A8: B1 is a_component by CONNSP_1:def 6;
A9: Down(A1,A`)=A1 by A3,XBOOLE_1:21;
A10: Down(A2,A`)=A2 by A3,XBOOLE_1:21;
  then
A11: Down(A2,A`) is a_component by A5,A9;
  then
A12: A2 is_a_component_of A` by A10,CONNSP_1:def 6;
A13: (TOP-REAL 2) | D is non empty;
  then
A14: Down(A2,A`)<>{}((TOP-REAL 2) | A`) by A11,CONNSP_1:34;
A15: Down(A1,A`) is a_component by A5,A9,A10;
  then
A16: A1 is_a_component_of A` by A9,CONNSP_1:def 6;
  per cases by A9,A15,A8,CONNSP_1:37;
  suppose
A17: B1=A1;
A18: now
      assume not BDD A c= A2;
      then consider x such that
A19:  x in BDD A and
A20:  not x in A2 by TARSKI:def 3;
      consider y such that
A21:  x in y and
A22:  y in {B3 where B3 is Subset of TOP-REAL 2: B3
      is_inside_component_of A} by A19,TARSKI:def 4;
      consider B3 being Subset of TOP-REAL 2 such that
A23:  y=B3 and
A24:  B3 is_inside_component_of A by A22;
A25:  B3 is_a_component_of A` by A24,Def3;
      then consider B4 being Subset of (TOP-REAL 2) | A` such that
A26:  B4 = B3 and
A27:  B4 is a_component by CONNSP_1:def 6;
A28:  B3<>{}((TOP-REAL 2) | A`) by A13,A26,A27,CONNSP_1:34;
      now
        assume B4=Down(A1,A`);
        then UBD A is Bounded by A9,A7,A17,A24,A26,Def3;
        hence contradiction by A6,Def4;
      end;
      then
A29:  B3 misses A1 by A9,A15,A26,A27,CONNSP_1:37;
      B4=Down(A2,A`) or B4 misses Down(A2,A`) by A11,A27,CONNSP_1:37;
      then
A30:  B4=Down(A2,A`) or B4 /\ Down(A2,A`)={}((TOP-REAL 2) | A`) by
XBOOLE_0:def 7;
      B3=B3 /\ (A1 \/ A2) by A3,A25,SPRECT_1:7,XBOOLE_1:28
        .=(B3 /\ A1) \/ (B3 /\ A2 ) by XBOOLE_1:23
        .={} by A10,A20,A21,A23,A26,A30,A29,XBOOLE_0:def 7;
      hence contradiction by A28;
    end;
    now
      assume not A2 is Bounded;
      then A2 is_outside_component_of A by A12,Def4;
      then A2 /\ UBD A <> {} by A10,A14,Th27,XBOOLE_1:28;
      hence contradiction by A4,A7,A17,XBOOLE_0:def 7;
    end;
    then
A31: A2 is_inside_component_of A by A12,Def3;
    then A2 c= BDD A by Th26;
    hence thesis by A31,A18,XBOOLE_0:def 10;
  end;
  suppose
A32: B1 misses Down(A1,A`);
    set E1=Down(A1,A`), E2=Down(A2,A`);
    E1 \/ E2=[#]((TOP-REAL 2) | A`) by A3,A9,A10,PRE_TOPC:def 10;
    then
A33: UBD A=A2 by A10,A11,A13,A7,A8,A32,Th115;
A34: BDD A \/ UBD A=A` by Th31;
A35: BDD A misses UBD A by Th28;
A36: BDD A c= A1
    proof
      let z;
      assume z in BDD A;
      then z in A` & not z in UBD A by A35,A34,XBOOLE_0:3,def 3;
      hence thesis by A3,A33,XBOOLE_0:def 3;
    end;
A37: BDD A is Bounded by A1,Th114;
    A1 c= BDD A
    proof
      let z;
      assume z in A1;
      then z in A` & not z in UBD A by A3,A4,A33,XBOOLE_0:3,def 3;
      hence thesis by A34,XBOOLE_0:def 3;
    end;
    then BDD A = A1 by A36,XBOOLE_0:def 10;
    hence thesis by A16,A37,Def3;
  end;
end;

theorem
  for a being Real,p being Point of TOP-REAL 2 st a>0 & p in (L~SpStSeq
  D) holds ex q being Point of TOP-REAL 2 st q in BDD (L~SpStSeq D) & |.p-q.|<a
proof
  let a be Real,p be Point of TOP-REAL 2;
  assume that
A1: a>0 and
A2: p in (L~SpStSeq D);
  consider q1 being Element of BDD (L~SpStSeq D);
  set A=L~SpStSeq D;
  A`<>{} by SPRECT_1:def 3;
  then consider A1,A2 being Subset of TOP-REAL 2 such that
A3: A` = A1 \/ A2 and
  A1 misses A2 and
A4: (Cl A1) \ A1 = (Cl A2) \ A2 and
A5: A=(Cl A1) \ A1 and
A6: for C1,C2 being Subset of (TOP-REAL 2) | A` st C1 = A1 & C2 = A2 holds
C1 is a_component & C2 is a_component
by Th106;
A7: Down(A2,A`)=A2 by A3,XBOOLE_1:21;
  BDD A is_inside_component_of A by Th116;
  then BDD (L~SpStSeq D) is_a_component_of A` by Def3;
  then consider B1 being Subset of (TOP-REAL 2) | A` such that
A8: B1 = BDD (L~SpStSeq D) and
A9: B1 is a_component by CONNSP_1:def 6;
  B1 c= the carrier of (TOP-REAL 2) | A`;
  then
A10: BDD (L~SpStSeq D) c= A1 \/ A2 by A3,A8,PRE_TOPC:29;
A11: Down(A1,A`)=A1 by A3,XBOOLE_1:21;
  then
A12: Down(A1,A`) is a_component by A6,A7;
A13: Down(A2,A`) is a_component by A6,A11,A7;
A14: BDD (L~SpStSeq D) <>{} by Th104;
  then
A15: q1 in BDD (L~SpStSeq D);
  per cases by A10,A15,XBOOLE_0:def 3;
  suppose
    q1 in A1;
    then B1 /\ Down(A1,A`)<>{}((TOP-REAL 2) | A`) by A11,A8,A14,XBOOLE_0:def 4;
    then B1 meets Down(A1,A`) by XBOOLE_0:def 7;
    then B1=Down(A1,A`) by A12,A9,CONNSP_1:37;
    then
A16: p in Cl(BDD (L~SpStSeq D)) by A2,A5,A11,A8,XBOOLE_0:def 5;
    reconsider ep=p as Point of Euclid 2 by TOPREAL3:13;
    reconsider G2=Ball(ep,a) as Subset of TOP-REAL 2 by TOPREAL3:13;
    (the distance of (Euclid 2)) is Reflexive by METRIC_1:def 7;
    then (the distance of Euclid 2).(ep,ep)=0 by METRIC_1:def 3;
    then dist(ep,ep)=0 by METRIC_1:def 1;
    then
A17: p in Ball(ep,a) by A1,METRIC_1:12;
    G2 is open by GOBOARD6:6;
    then (BDD (L~SpStSeq D)) meets G2 by A16,A17,PRE_TOPC:def 13;
    then consider t2 being set such that
A18: t2 in (BDD (L~SpStSeq D)) and
A19: t2 in G2 by XBOOLE_0:3;
    reconsider qt2=t2 as Point of TOP-REAL 2 by A18;
    |.p-qt2.|<a by A19,Th111;
    hence thesis by A18;
  end;
  suppose
    q1 in A2;
    then B1 /\ Down(A2,A`)<>{}((TOP-REAL 2) | A`) by A7,A8,A14,XBOOLE_0:def 4;
    then B1 meets Down(A2,A`) by XBOOLE_0:def 7;
    then B1=Down(A2,A`) by A13,A9,CONNSP_1:37;
    then
A20: p in Cl(BDD (L~SpStSeq D)) by A2,A4,A5,A7,A8,XBOOLE_0:def 5;
    reconsider ep=p as Point of Euclid 2 by TOPREAL3:13;
    reconsider G2=Ball(ep,a) as Subset of TOP-REAL 2 by TOPREAL3:13;
    (the distance of (Euclid 2)) is Reflexive by METRIC_1:def 7;
    then (the distance of Euclid 2).(ep,ep)=0 by METRIC_1:def 3;
    then dist(ep,ep)=0 by METRIC_1:def 1;
    then
A21: p in Ball(ep,a) by A1,METRIC_1:12;
    G2 is open by GOBOARD6:6;
    then (BDD (L~SpStSeq D)) meets G2 by A20,A21,PRE_TOPC:def 13;
    then consider t2 being set such that
A22: t2 in (BDD (L~SpStSeq D)) and
A23: t2 in G2 by XBOOLE_0:3;
    reconsider qt2=t2 as Point of TOP-REAL 2 by A22;
    |.p-qt2.|<a by A23,Th111;
    hence thesis by A22;
  end;
end;

begin :: Points in LeftComp

reserve f for clockwise_oriented (non constant standard
  special_circular_sequence);

theorem
  for p being Point of TOP-REAL 2 st f/.1 = N-min L~f & p`1 < W-bound (
  L~f) holds p in LeftComp f
proof
  let p be Point of TOP-REAL 2;
  assume that
A1: f/.1 = N-min L~f and
A2: p`1< W-bound(L~f);
  set g=SpStSeq L~f;
A3: LeftComp g c= LeftComp f by A1,SPRECT_3:58;
  W-bound L~ g=W-bound L~f by SPRECT_1:66;
  then p in LeftComp g by A2,SPRECT_3:57;
  hence thesis by A3;
end;

theorem
  for p being Point of TOP-REAL 2 st f/.1 = N-min L~f & p`1 > E-bound (
  L~f) holds p in LeftComp f
proof
  let p be Point of TOP-REAL 2;
  assume that
A1: f/.1 = N-min L~f and
A2: p`1> E-bound(L~f);
  set g=SpStSeq L~f;
A3: LeftComp g c= LeftComp f by A1,SPRECT_3:58;
  E-bound L~ g=E-bound L~f by SPRECT_1:69;
  then p in LeftComp g by A2,SPRECT_3:57;
  hence thesis by A3;
end;

theorem
  for p being Point of TOP-REAL 2 st f/.1 = N-min L~f & p`2 < S-bound (
  L~f) holds p in LeftComp f
proof
  let p be Point of TOP-REAL 2;
  assume that
A1: f/.1 = N-min L~f and
A2: p`2< S-bound(L~f);
  set g=SpStSeq L~f;
A3: LeftComp g c= LeftComp f by A1,SPRECT_3:58;
  S-bound L~ g=S-bound L~f by SPRECT_1:67;
  then p in LeftComp g by A2,SPRECT_3:57;
  hence thesis by A3;
end;

theorem
  for p being Point of TOP-REAL 2 st f/.1 = N-min L~f & p`2 > N-bound (
  L~f) holds p in LeftComp f
proof
  let p be Point of TOP-REAL 2;
  assume that
A1: f/.1 = N-min L~f and
A2: p`2>N-bound(L~f);
  set g=SpStSeq L~f;
A3: LeftComp g c= LeftComp f by A1,SPRECT_3:58;
  N-bound L~ g=N-bound L~f by SPRECT_1:68;
  then p in LeftComp g by A2,SPRECT_3:57;
  hence thesis by A3;
end;
 
:: Moved from GOBRD14, AK, 22.02.2006

theorem
  for T being TopSpace, A being Subset of T, B being Subset of T st B
  is_a_component_of A holds B is connected
proof
  let T be TopSpace, A be Subset of T, B be Subset of T;
  assume B is_a_component_of A;
  then consider C being Subset of T|A such that
A1: C = B and
A2: C is a_component by CONNSP_1:def 6;
  C is connected by A2,CONNSP_1:def 5;
  hence thesis by A1,CONNSP_1:24;
end;

theorem
  for A being Subset of TOP-REAL n, B being Subset of TOP-REAL n st B
  is_inside_component_of A holds B is connected
proof
  let A be Subset of TOP-REAL n, B be Subset of TOP-REAL n;
  assume B is_inside_component_of A;
  then consider C being Subset of (TOP-REAL n) | A` such that
A1: C = B and
A2: C is a_component and
  C is bounded Subset of Euclid n by Th17;
  C is connected by A2,CONNSP_1:def 5;
  hence thesis by A1,CONNSP_1:24;
end;

theorem Th124:
  for A being Subset of TOP-REAL n, B being Subset of TOP-REAL n
  st B is_outside_component_of A holds B is connected
proof
  let A be Subset of TOP-REAL n, B be Subset of TOP-REAL n;
  assume B is_outside_component_of A;
  then consider C being Subset of (TOP-REAL n) | A` such that
A1: C = B and
A2: C is a_component and
  not C is bounded Subset of Euclid n by Th18;
  C is connected by A2,CONNSP_1:def 5;
  hence thesis by A1,CONNSP_1:24;
end;

theorem
  for A being Subset of TOP-REAL n, B being Subset of TOP-REAL n st B
  is_a_component_of A` holds A misses B
proof
  let A be Subset of TOP-REAL n, B be Subset of TOP-REAL n;
  assume B is_a_component_of A`;
  then B c= A` by SPRECT_1:7;
  then A misses B by SUBSET_1:43;
  hence A /\ B = {} by XBOOLE_0:def 7;
end;

theorem
  P is_outside_component_of Q & R is_inside_component_of Q implies P misses R
proof
  assume
A1: P is_outside_component_of Q;
  assume
A2: R is_inside_component_of Q;
  (BDD Q) misses (UBD Q) by Th28;
  then P misses BDD Q by A1,Th27,XBOOLE_1:63;
  hence thesis by A2,Th26,XBOOLE_1:63;
end;

theorem
  2 <= n implies for A, B, P being Subset of TOP-REAL n st P is Bounded
  & A is_outside_component_of P & B is_outside_component_of P holds A = B
proof
  assume
A1: 2 <= n;
  let A, B, P be Subset of TOP-REAL n such that
A2: P is Bounded and
A3: A is_outside_component_of P and
A4: B is_outside_component_of P;
A5: B is_a_component_of P` by A4,Def4;
  UBD P is_outside_component_of P by A1,A2,Th76;
  then
A6: UBD P is_a_component_of P` by Def4;
A7: P` is non empty by A1,A2,Th74,XXREAL_0:2;
  then
A8: B <> {} by A5,SPRECT_1:6;
A9: B c= UBD P by A4,Th27;
A10: A c= UBD P by A3,Th27;
A11: A is_a_component_of P` by A3,Def4;
  then A <> {} by A7,SPRECT_1:6;
  then A = UBD P by A11,A6,A10,GOBOARD9:3,XBOOLE_1:69;
  hence thesis by A5,A8,A6,A9,GOBOARD9:3,XBOOLE_1:69;
end;

registration
  let C be closed Subset of TOP-REAL 2;
  cluster BDD C -> open;
  coherence
  proof
    set F = {B where B is Subset of TOP-REAL 2: B is_inside_component_of C};
    F c= bool the carrier of TOP-REAL 2
    proof
      let f be set;
      assume f in F;
      then
      ex B being Subset of TOP-REAL 2 st f = B & B is_inside_component_of C;
      hence thesis;
    end;
    then reconsider F as Subset-Family of TOP-REAL 2;
    F is open
    proof
      let P be Subset of TOP-REAL 2;
      assume P in F;
      then consider B being Subset of TOP-REAL 2 such that
A1:   P = B and
A2:   B is_inside_component_of C;
      B is_a_component_of C` by A2,Def3;
      hence thesis by A1,SPRECT_3:18;
    end;
    hence thesis by TOPS_2:26;
  end;
  cluster UBD C -> open;
  coherence
  proof
    set F = {B where B is Subset of TOP-REAL 2: B is_outside_component_of C};
    F c= bool the carrier of TOP-REAL 2
    proof
      let f be set;
      assume f in F;
      then ex B being Subset of TOP-REAL 2 st f = B & B
      is_outside_component_of C;
      hence thesis;
    end;
    then reconsider F as Subset-Family of TOP-REAL 2;
    F is open
    proof
      let P be Subset of TOP-REAL 2;
      assume P in F;
      then consider B being Subset of TOP-REAL 2 such that
A3:   P = B and
A4:   B is_outside_component_of C;
      B is_a_component_of C` by A4,Def4;
      hence thesis by A3,SPRECT_3:18;
    end;
    hence thesis by TOPS_2:26;
  end;
end;

registration
  let C be compact Subset of TOP-REAL 2;
  cluster UBD C -> connected;
  coherence by Th76,Th124;
end;
 
:: Moved from JORDAN1C, AK, 22.02.2006

reserve p for Point of TOP-REAL 2;

theorem Th128:
  west_halfline p is non Bounded
proof
  set Wp = west_halfline p;
  set p11 = p`1, p12 = p`2;
  assume Wp is Bounded;
  then reconsider C = Wp as bounded Subset of Euclid 2 by Def2;
  consider r being Real such that
A1: 0 < r and
A2: for x,y being Point of Euclid 2 st x in C & y in C holds dist(x,y)
  <= r by TBSP_1:def 9;
  set EX1 = p`1-2*r;
  reconsider p1 = p, EX = |[p`1-2*r, p`2]| as Point of Euclid 2 by EUCLID:71;
  0 + p`1 <= 2*r + p`1 by A1,XREAL_1:8;
  then p`1 - 2*r <= p`1 by XREAL_1:22;
  then
A3: |[p`1-2*r, p`2]|`1 <= p`1 by EUCLID:56;
  then
A4: p1 in Wp by TOPREAL1:def 15;
  |[p`1-2*r, p`2]|`2 = p`2 by EUCLID:56;
  then
A5: EX in Wp by A3,TOPREAL1:def 15;
  p = |[p11,p12]| by EUCLID:57;
  then dist (p1, EX) = sqrt ((p11 - EX1)^2 + (p12 - p`2)^2) by GOBOARD6:9
    .= 2*r by A1,SQUARE_1:89;
  hence thesis by A1,A2,A5,A4,XREAL_1:157;
end;

theorem Th129:
  east_halfline p is non Bounded
proof
  set Wp = east_halfline p;
  set p11 = p`1, p12 = p`2;
  assume Wp is Bounded;
  then reconsider C = Wp as bounded Subset of Euclid 2 by Def2;
  consider r being Real such that
A1: 0 < r and
A2: for x,y being Point of Euclid 2 st x in C & y in C holds dist(x,y)
  <= r by TBSP_1:def 9;
  set EX1 = p`1+2*r, EX2 = p`2;
  reconsider p1 = p, EX = |[p`1+2*r, p`2]| as Point of Euclid 2 by EUCLID:71;
  0 + p`1 <= 2*r + p`1 by A1,XREAL_1:8;
  then
A3: |[EX1, p`2]|`1 >= p`1 by EUCLID:56;
  then
A4: p1 in Wp by TOPREAL1:def 13;
  |[EX1, p`2]|`2 = p`2 by EUCLID:56;
  then
A5: EX in Wp by A3,TOPREAL1:def 13;
  p = |[p11,p12]| by EUCLID:57;
  then dist (p1, EX) = sqrt ((p11 - EX1)^2 + (p12 - EX2)^2) by GOBOARD6:9
    .= sqrt ((EX1 - p11)^2 + 0)
    .= 2*r by A1,SQUARE_1:89;
  hence thesis by A1,A2,A5,A4,XREAL_1:157;
end;

theorem Th130:
  north_halfline p is non Bounded
proof
  set Wp = north_halfline p;
  set p11 = p`1, p12 = p`2;
  assume Wp is Bounded;
  then reconsider C = Wp as bounded Subset of Euclid 2 by Def2;
  consider r being Real such that
A1: 0 < r and
A2: for x,y being Point of Euclid 2 st x in C & y in C holds dist(x,y)
  <= r by TBSP_1:def 9;
  set EX2 = p`2+2*r, EX1 = p`1;
  reconsider p1 = p, EX = |[p`1, p`2+2*r]| as Point of Euclid 2 by EUCLID:71;
A3: |[p`1, EX2]|`1 = p`1 by EUCLID:56;
  then
A4: p1 in Wp by TOPREAL1:def 12;
  0 + p`2 <= 2*r + p`2 by A1,XREAL_1:8;
  then |[p`1, EX2]|`2 >= p`2 by EUCLID:56;
  then
A5: EX in Wp by A3,TOPREAL1:def 12;
  p = |[p11,p12]| by EUCLID:57;
  then dist (p1, EX) = sqrt ((p11 - EX1)^2 + (p12 - EX2)^2) by GOBOARD6:9
    .= sqrt ((EX2 - p12)^2 + 0)
    .= 2*r by A1,SQUARE_1:89;
  hence thesis by A1,A2,A5,A4,XREAL_1:157;
end;

theorem Th131:
  south_halfline p is non Bounded
proof
  set Wp = south_halfline p;
  set p11 = p`1, p12 = p`2;
  assume Wp is Bounded;
  then reconsider C = Wp as bounded Subset of Euclid 2 by Def2;
  consider r being Real such that
A1: 0 < r and
A2: for x,y being Point of Euclid 2 st x in C & y in C holds dist(x,y)
  <= r by TBSP_1:def 9;
  set EX2 = p`2-2*r, EX1 = p`1;
  reconsider p1 = p, EX = |[p`1, p`2-2*r]| as Point of Euclid 2 by EUCLID:71;
  p = |[p11,p12]| by EUCLID:57;
  then
A3: dist (p1, EX) = sqrt ((p11 - EX1)^2 + (p12 - EX2)^2) by GOBOARD6:9
    .= 2*r by A1,SQUARE_1:89;
A4: |[p`1, EX2]|`1 = p`1 by EUCLID:56;
  then
A5: p1 in Wp by TOPREAL1:def 14;
  0 + p`2 <= 2*r + p`2 by A1,XREAL_1:8;
  then p`2 - 2*r <= p`2 by XREAL_1:22;
  then |[p`1, EX2]|`2 <= p`2 by EUCLID:56;
  then EX in Wp by A4,TOPREAL1:def 14;
  hence thesis by A1,A2,A5,A3,XREAL_1:157;
end;

registration
  let C be compact Subset of TOP-REAL 2;
  cluster UBD C -> non empty;
  coherence
  proof
    UBD C is_outside_component_of C by Th76;
    then
A1: UBD C is_a_component_of C` by Def4;
    C` <> {} by Th74;
    hence thesis by A1,SPRECT_1:6;
  end;
end;

theorem Th132:
  for C being compact Subset of TOP-REAL 2 holds UBD C is_a_component_of C`
proof
  let C be compact Subset of TOP-REAL 2;
  UBD C is_outside_component_of C by Th76;
  hence thesis by Def4;
end;

theorem Th133:
  for C being compact Subset of TOP-REAL 2 for WH being connected
  Subset of TOP-REAL 2 st WH is non Bounded & WH misses C holds WH c= UBD C
proof
  let C be compact Subset of TOP-REAL 2;
  let WH be connected Subset of TOP-REAL 2;
  assume that
A1: WH is non Bounded and
A2: WH misses C;
A3: WH meets UBD C
  proof
    (BDD C) \/ (UBD C) = C` & [#]the carrier of TOP-REAL 2 = C \/ C` by Th31,
SUBSET_1:25;
    then
A4: WH c= (UBD C) \/ BDD C by A2,XBOOLE_1:73;
    assume
A5: WH misses UBD C;
    BDD C is Bounded by Th114;
    hence thesis by A1,A5,A4,Th16,XBOOLE_1:73;
  end;
  WH c= C` by A2,SUBSET_1:43;
  hence thesis by A3,Th132,GOBOARD9:6;
end;

theorem
  for C being compact Subset of TOP-REAL 2 for p being Point of TOP-REAL
  2 st west_halfline p misses C holds west_halfline p c= UBD C
proof
  let C be compact Subset of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  set WH = west_halfline p;
  assume
A1: WH misses C;
  WH is non Bounded non empty connected by Th128;
  hence thesis by A1,Th133;
end;

theorem
  for C being compact Subset of TOP-REAL 2 for p being Point of TOP-REAL
  2 st east_halfline p misses C holds east_halfline p c= UBD C
proof
  let C be compact Subset of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  set WH = east_halfline p;
  assume
A1: WH misses C;
  WH is non Bounded non empty connected by Th129;
  hence thesis by A1,Th133;
end;

theorem
  for C being compact Subset of TOP-REAL 2 for p being Point of TOP-REAL
  2 st south_halfline p misses C holds south_halfline p c= UBD C
proof
  let C be compact Subset of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  set WH = south_halfline p;
  assume
A1: WH misses C;
  WH is non Bounded non empty connected by Th131;
  hence thesis by A1,Th133;
end;

theorem
  for C being compact Subset of TOP-REAL 2 for p being Point of TOP-REAL
  2 st north_halfline p misses C holds north_halfline p c= UBD C
proof
  let C be compact Subset of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  set WH = north_halfline p;
  assume
A1: WH misses C;
  WH is non Bounded non empty connected by Th130;
  hence thesis by A1,Th133;
end;
