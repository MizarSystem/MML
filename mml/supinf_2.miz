:: Series of Positive Real Numbers. Measure Theory
::  by J\'ozef Bia{\l}as
::
:: Received September 27, 1990
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies CARD_1, SUPINF_1, ARYTM_3, REAL_1, ARYTM_1, XBOOLE_0, SUBSET_1,
      NUMBERS, TARSKI, XXREAL_0, MEMBERED, ORDINAL2, ORDINAL1, XXREAL_2,
      FUNCT_1, RELAT_1, VALUED_0, FUNCT_3, CARD_3, FUNCT_4, FUNCOP_1, PARTFUN1,
      NAT_1, SERIES_1, SUPINF_2;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, MEMBERED, XXREAL_0,
      XXREAL_3, XCMPLX_0, REAL_1, RELAT_1, FUNCT_1, RELSET_1, PARTFUN1,
      FUNCT_2, FUNCT_3, FUNCOP_1, VALUED_0, FUNCT_4, NAT_1, CARD_3, XXREAL_2,
      SUPINF_1;
 constructors FUNCT_3, FUNCOP_1, FUNCT_4, FINSET_1, REAL_1, NAT_1, SUPINF_1,
      VALUED_1, XXREAL_2, XXREAL_3, CARD_3, RELSET_1;
 registrations RELAT_1, FUNCT_1, ORDINAL1, FUNCT_2, FINSET_1, NUMBERS, XREAL_0,
      MEMBERED, VALUED_0, XXREAL_3, RELSET_1;
 requirements NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, XBOOLE_0, XCMPLX_0, XXREAL_0, XXREAL_3;
 theorems TARSKI, FUNCT_1, FUNCT_2, NAT_1, RELSET_1, CARD_4, SUBSET_1, FUNCT_4,
      FUNCOP_1, RELAT_1, ZFMISC_1, XBOOLE_0, XBOOLE_1, XREAL_1, XXREAL_0,
      ORDINAL1, NUMBERS, XXREAL_2, XXREAL_3, CARD_3;
 schemes SUBSET_1, FUNCT_2, RECDEF_1, NAT_1;

begin :: operations " + "," - " in R_eal numbers

notation
  synonym 0. for 0;
end;

definition
  canceled;
  redefine func 0. -> R_eal;
  coherence by XXREAL_0:def 1;
end;

definition
  let x,y be R_eal;
  redefine func x + y -> R_eal;
  coherence by XXREAL_0:def 1;
end;

theorem
  for x,y being R_eal, a,b being Real st x = a & y = b holds x + y = a +
  b by XXREAL_3:def 2;

definition
  let x be R_eal;
  redefine func - x -> R_eal;
  coherence by XXREAL_0:def 1;
end;

definition
  let x,y be R_eal;
  redefine func x - y -> R_eal;
  coherence by XXREAL_0:def 1;
end;

canceled;

theorem
  for x being R_eal, a being Real st x = a holds - x = - a by XXREAL_3:
  def 3;

canceled;

theorem
  for x,y being R_eal, a,b being Real st x = a & y = b holds x - y = a - b
proof
  let x,y be R_eal, a,b be Real;
  assume
A1: x = a;
  assume y = b;
  then -y = -b by XXREAL_3:def 3;
  hence thesis by A1,XXREAL_3:def 2;
end;

canceled 15;

::
::       operations " + "," - " in a subset of ExtREAL
::

definition
  canceled 3;
  let X,Y be non empty Subset of ExtREAL;
  func X + Y -> Subset of ExtREAL means
  :Def5:
  for z being R_eal holds z in it
  iff ex x,y being R_eal st x in X & y in Y & z = x + y;
  existence
  proof
    consider y being Element of Y;
    consider x being Element of X;
    defpred P[R_eal] means ex x,y being R_eal st x in X & y in Y & $1 = x + y;
    consider Z being Subset of ExtREAL such that
A1: for z being R_eal holds z in Z iff P[z] from SUBSET_1:sch 3;
    x + y = x + y;
    then reconsider Z as non empty Subset of ExtREAL by A1;
    take Z;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let Z1,Z2 be Subset of ExtREAL such that
A2: for z being R_eal holds z in Z1 iff ex x,y being R_eal st x in X &
    y in Y & z = x + y and
A3: for z being R_eal holds z in Z2 iff ex x,y being R_eal st x in X &
    y in Y & z = x + y;
    for z being set holds z in Z1 iff z in Z2
    proof
      let z be set;
      hereby
        assume
A4:     z in Z1;
        then reconsider z9 = z as R_eal;
        ex x,y being R_eal st x in X & y in Y & z9 = x + y by A2,A4;
        hence z in Z2 by A3;
      end;
      assume
A5:   z in Z2;
      then reconsider z as R_eal;
      ex x,y being R_eal st x in X & y in Y & z = x + y by A3,A5;
      hence thesis by A2;
    end;
    hence thesis by TARSKI:2;
  end;
end;

registration
  let X,Y be non empty Subset of ExtREAL;
  cluster X + Y -> non empty;
  coherence
  proof
    consider y being Element of Y;
    consider x being Element of X;
    reconsider x,y as R_eal;
    x + y = x + y;
    hence thesis by Def5;
  end;
end;

definition
  let X be non empty Subset of ExtREAL;
  func - X -> Subset of ExtREAL means
  :Def6:
  for z being R_eal holds z in it iff ex x being R_eal st x in X & z = - x;
  existence
  proof
    consider x being Element of X;
    defpred P[R_eal] means ex x being R_eal st x in X & $1 = - x;
    consider Z being Subset of ExtREAL such that
A1: for z being R_eal holds z in Z iff P[z] from SUBSET_1:sch 3;
    -x = -x;
    then reconsider Z as non empty Subset of ExtREAL by A1;
    take Z;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let Z1,Z2 be Subset of ExtREAL such that
A2: for z being R_eal holds z in Z1 iff ex x being R_eal st x in X & z
    = - x and
A3: for z being R_eal holds z in Z2 iff ex x being R_eal st x in X & z = - x;
    for z being set holds z in Z1 iff z in Z2
    proof
      let z be set;
      thus z in Z1 implies z in Z2
      proof
        assume
A4:     z in Z1;
        then reconsider z as R_eal;
        ex x being R_eal st x in X & z = - x by A2,A4;
        hence thesis by A3;
      end;
      assume
A5:   z in Z2;
      then reconsider z as R_eal;
      ex x being R_eal st x in X & z = - x by A3,A5;
      hence thesis by A2;
    end;
    hence thesis by TARSKI:2;
  end;
end;

registration
  let X be non empty Subset of ExtREAL;
  cluster - X -> non empty;
  coherence
  proof
    consider x being Element of X;
    -x = -x;
    hence thesis by Def6;
  end;
end;

canceled;

theorem Th22:
  for X being non empty Subset of ExtREAL holds --X = X
proof
  let X be non empty Subset of ExtREAL;
  for z being R_eal holds z in X iff ex x being R_eal st x in - X & z = - x
  proof
    let z be R_eal;
    thus z in X implies ex x being R_eal st x in - X & z = - x
    proof
      set x = - z;
      assume z in X;
      then
A1:   x in - X by Def6;
      z = - x;
      hence thesis by A1;
    end;
    given x being R_eal such that
A2: x in - X and
A3: z = - x;
    ex a being R_eal st a in X & x = - a by A2,Def6;
    hence thesis by A3;
  end;
  hence thesis by Def6;
end;

theorem Th23:
  for X being non empty Subset of ExtREAL, z being R_eal holds z
  in X iff - z in - X
proof
  let X be non empty Subset of ExtREAL, z be R_eal;
  - z in - X implies z in X
  proof
    assume - z in - X;
    then
A1: ex x being R_eal st x in X & - z = - x by Def6;
    --z = z;
    hence thesis by A1;
  end;
  hence thesis by Def6;
end;

theorem
  for X,Y being non empty Subset of ExtREAL holds X c= Y iff - X c= - Y
proof
  let X,Y be non empty Subset of ExtREAL;
  hereby
    assume
A1: X c= Y;
    thus - X c= - Y
    proof
      let x be set;
      assume
A2:   x in - X;
      then reconsider x as R_eal;
      - x in --X by A2,Th23;
      then -x in X by Th22;
      then --x in - Y by A1,Th23;
      hence thesis;
    end;
  end;
  assume
A3: - X c= - Y;
  let x be set;
  assume
A4: x in X;
  then reconsider x as R_eal;
  - x in - X by A4,Th23;
  hence thesis by A3,Th23;
end;

theorem
  for z being R_eal holds z in REAL iff - z in REAL
proof
  let z be R_eal;
A1: for z being R_eal holds z in REAL implies - z in REAL
  proof
    let z be R_eal;
A2: - z in REAL or - z in {-infty,+infty} by XBOOLE_0:def 3;
    assume z in REAL;
    hence thesis by A2,TARSKI:def 2;
  end;
  - z in REAL implies z in REAL
  proof
    assume -z in REAL;
    then --z in REAL by A1;
    hence thesis;
  end;
  hence thesis by A1;
end;

definition
  let X be ext-real-membered set;
  redefine func inf X -> R_eal;
  coherence by XXREAL_0:def 1;
  redefine func sup X -> R_eal;
  coherence by XXREAL_0:def 1;
end;

theorem Th26:
  for X,Y being non empty Subset of ExtREAL holds sup (X + Y) <= sup X + sup Y
proof
  let X,Y be non empty Subset of ExtREAL;
  for z being ext-real number holds z in X + Y implies z <= sup X + sup Y
  proof
    let z be ext-real number;
    assume z in X + Y;
    then consider x,y being R_eal such that
A1: x in X and
A2: y in Y and
A3: z = x + y by Def5;
A4: y <= sup Y by A2,XXREAL_2:4;
    x <= sup X by A1,XXREAL_2:4;
    hence thesis by A3,A4,XXREAL_3:38;
  end;
  then sup X + sup Y is UpperBound of X + Y by XXREAL_2:def 1;
  hence thesis by XXREAL_2:def 3;
end;

theorem Th27:
  for X,Y being non empty Subset of ExtREAL holds inf X + inf Y <= inf (X + Y)
proof
  let X,Y be non empty Subset of ExtREAL;
  for z being ext-real number holds z in X + Y implies inf X + inf Y <= z
  proof
    let z be ext-real number;
    assume z in X + Y;
    then consider x,y being R_eal such that
A1: x in X and
A2: y in Y and
A3: z = x + y by Def5;
A4: inf Y <= y by A2,XXREAL_2:3;
    inf X <= x by A1,XXREAL_2:3;
    hence thesis by A3,A4,XXREAL_3:38;
  end;
  then inf X + inf Y is LowerBound of X + Y by XXREAL_2:def 2;
  hence thesis by XXREAL_2:def 4;
end;

theorem
  for X,Y being non empty Subset of ExtREAL holds X is bounded_above & Y
  is bounded_above implies sup (X + Y) <= sup X + sup Y by Th26;

theorem
  for X,Y being non empty Subset of ExtREAL st X is bounded_below & Y is
  bounded_below holds inf X + inf Y <= inf (X + Y) by Th27;

theorem Th30:
  for X being non empty Subset of ExtREAL, a being R_eal holds a
  is UpperBound of X iff - a is LowerBound of - X
proof
  let X be non empty Subset of ExtREAL;
  let a be R_eal;
  hereby
    assume
A1: a is UpperBound of X;
    for x being ext-real number st x in - X holds -a <= x
    proof
      let x be ext-real number;
      assume
A2:   x in - X;
      reconsider x as Element of ExtREAL by XXREAL_0:def 1;
      - x in --X by A2,Th23;
      then - x in X by Th22;
      then - x <= a by A1,XXREAL_2:def 1;
      then - a <= --x by XXREAL_3:40;
      hence thesis;
    end;
    hence - a is LowerBound of - X by XXREAL_2:def 2;
  end;
  assume
A3: - a is LowerBound of - X;
  for x being ext-real number st x in X holds x <=a
  proof
    let x be ext-real number;
    assume
A4: x in X;
    reconsider x as Element of ExtREAL by XXREAL_0:def 1;
    - x in - X by A4,Th23;
    then - a <= - x by A3,XXREAL_2:def 2;
    hence thesis by XXREAL_3:40;
  end;
  hence thesis by XXREAL_2:def 1;
end;

theorem Th31:
  for X being non empty Subset of ExtREAL holds for a being R_eal
  holds a is LowerBound of X iff - a is UpperBound of - X
proof
  let X be non empty Subset of ExtREAL;
  let a be R_eal;
  hereby
    assume
A1: a is LowerBound of X;
    for x being ext-real number st x in - X holds x <= - a
    proof
      let x be ext-real number;
      assume
A2:   x in - X;
      reconsider x as Element of ExtREAL by XXREAL_0:def 1;
      - x in - (- X) by A2,Th23;
      then - x in X by Th22;
      then a <= - x by A1,XXREAL_2:def 2;
      then - (- x) <= - a by XXREAL_3:40;
      hence thesis;
    end;
    hence - a is UpperBound of - X by XXREAL_2:def 1;
  end;
  assume
A3: - a is UpperBound of - X;
  for x being ext-real number st x in X holds a <= x
  proof
    let x be ext-real number;
    assume
A4: x in X;
    reconsider x as Element of ExtREAL by XXREAL_0:def 1;
    - x in - X by A4,Th23;
    then - x <= - a by A3,XXREAL_2:def 1;
    hence thesis by XXREAL_3:40;
  end;
  hence thesis by XXREAL_2:def 2;
end;

theorem Th32:
  for X being non empty Subset of ExtREAL holds inf(- X) = - sup X
proof
  let X be non empty Subset of ExtREAL;
  set a = inf(- X);
  set b = sup X;
  a is LowerBound of - X by XXREAL_2:def 4;
  then - a is UpperBound of -(-X) by Th31;
  then - a is UpperBound of X by Th22;
  then b <= - a by XXREAL_2:def 3;
  then
A1: - (- a) <= - b by XXREAL_3:40;
  b is UpperBound of X by XXREAL_2:def 3;
  then - b is LowerBound of - X by Th30;
  then - b <= a by XXREAL_2:def 4;
  hence thesis by A1,XXREAL_0:1;
end;

theorem Th33:
  for X be non empty Subset of ExtREAL holds sup(- X) = - inf X
proof
  let X be non empty Subset of ExtREAL;
  set a = sup(- X);
  set b = inf X;
  a is UpperBound of - X by XXREAL_2:def 3;
  then - a is LowerBound of - (- X) by Th30;
  then - a is LowerBound of X by Th22;
  then - a <= b by XXREAL_2:def 4;
  then
A1: - b <= - (-a) by XXREAL_3:40;
  b is LowerBound of X by XXREAL_2:def 4;
  then - b is UpperBound of - X by Th31;
  then a <= - b by XXREAL_2:def 3;
  hence thesis by A1,XXREAL_0:1;
end;

definition
  let X be non empty set;
  let Y be non empty Subset of ExtREAL;
  let F be Function of X,Y;
  redefine func rng F -> non empty Subset of ExtREAL;
  coherence
  proof
    consider x being Element of X;
    F.x in rng F by FUNCT_2:6;
    hence thesis by XBOOLE_1:1;
  end;
end;
 
::
::       sup F and inf F for Function of X,ExtREAL
::

definition
  let X be non empty set;
  let Y be non empty Subset of ExtREAL;
  let F be Function of X,Y;
  func sup F -> R_eal equals
  sup rng F;
  coherence;
  func inf F -> R_eal equals
  inf rng F;
  coherence;
end;

definition
  let F be ext-real-valued Function;
  let x be set;
  redefine func F.x -> R_eal;
  coherence by XXREAL_0:def 1;
end;

definition
  let X be non empty set;
  let Y,Z be non empty Subset of ExtREAL;
  let F be Function of X,Y;
  let G be Function of X,Z;
  func F + G -> Function of X,Y + Z means
  :Def9:
  for x being Element of X holds it.x = F.x + G.x;
  existence
  proof
    deffunc F(Element of X) = F.$1 + G.$1;
A1: for x being Element of X holds F(x) in Y + Z
    proof
      let x be Element of X;
A2:   G.x in Z by FUNCT_2:7;
      F.x in Y by FUNCT_2:7;
      hence thesis by A2,Def5;
    end;
    ex H being Function of X,Y + Z st for x being Element of X holds H.x =
    F(x) from FUNCT_2:sch 8(A1);
    then consider H being Function of X,Y + Z such that
A3: for x being Element of X holds H.x = F.x + G.x;
    take H;
    thus thesis by A3;
  end;
  uniqueness
  proof
    let H1,H2 be Function of X,Y + Z such that
A4: for x being Element of X holds H1.x = F.x + G.x and
A5: for x being Element of X holds H2.x = F.x + G.x;
    for x being set st x in X holds H1.x = H2.x
    proof
      let x be set;
      assume x in X;
      then reconsider x as Element of X;
      H1.x = F.x + G.x by A4
        .= H2.x by A5;
      hence thesis;
    end;
    hence thesis by FUNCT_2:18;
  end;
end;

theorem Th34:
  for X being non empty set, Y,Z being non empty Subset of ExtREAL
, F being Function of X,Y, G being Function of X,Z holds rng(F + G) c= rng F +
  rng G
proof
  let X be non empty set, Y,Z be non empty Subset of ExtREAL, F be Function of
  X,Y, G be Function of X,Z;
A1: for x being set st x in X holds (F + G).x in rng F + rng G
  proof
    let x be set;
    assume x in X;
    then reconsider x as Element of X;
    reconsider a = F.x, b = G.x as R_eal;
A2: a in rng F by FUNCT_2:6;
A3: b in rng G by FUNCT_2:6;
    (F + G).x = a + b by Def9;
    hence thesis by A2,A3,Def5;
  end;
  dom (F + G) = X by FUNCT_2:def 1;
  then F + G is Function of X,rng F + rng G by A1,FUNCT_2:5;
  hence thesis by RELAT_1:def 19;
end;

theorem Th35:
  for X being non empty set, Y,Z being non empty Subset of ExtREAL
for F being Function of X,Y for G being Function of X,Z holds sup(F + G) <= sup
  F + sup G
proof
  let X be non empty set, Y,Z be non empty Subset of ExtREAL;
  let F be Function of X,Y;
  let G be Function of X,Z;
A1: sup(rng F + rng G) <= sup(rng F) + sup(rng G) by Th26;
  sup(F + G) <= sup(rng F + rng G) by Th34,XXREAL_2:59;
  hence thesis by A1,XXREAL_0:2;
end;

theorem Th36:
  for X being non empty set holds for Y,Z being non empty Subset
of ExtREAL for F being Function of X,Y for G being Function of X,Z holds inf F
  + inf G <= inf(F + G)
proof
  let X be non empty set;
  let Y,Z be non empty Subset of ExtREAL;
  let F be Function of X,Y;
  let G be Function of X,Z;
A1: inf(rng F) + inf(rng G) <= inf(rng F + rng G) by Th27;
  inf(rng F + rng G) <= inf(F + G) by Th34,XXREAL_2:60;
  hence thesis by A1,XXREAL_0:2;
end;

definition
  let X be non empty set;
  let Y be non empty Subset of ExtREAL;
  let F be Function of X,Y;
  func - F -> Function of X,- Y means
  :Def10:
  for x being Element of X holds it.x = - F.x;
  existence
  proof
    deffunc F(Element of X) = - F.$1;
A1: for x being Element of X holds F(x) in - Y
    proof
      let x be Element of X;
      F.x in Y by FUNCT_2:7;
      hence thesis by Th23;
    end;
    ex H being Function of X,- Y st for x being Element of X holds H.x = F
    (x) from FUNCT_2:sch 8(A1);
    then consider H being Function of X,- Y such that
A2: for x being Element of X holds H.x = - F.x;
    take H;
    thus thesis by A2;
  end;
  uniqueness
  proof
    let H1,H2 be Function of X,- Y such that
A3: for x being Element of X holds H1.x = - F.x and
A4: for x being Element of X holds H2.x = - F.x;
    for x being set st x in X holds H1.x = H2.x
    proof
      let x be set;
      assume x in X;
      then reconsider x as Element of X;
      H1.x = - F.x by A3
        .= H2.x by A4;
      hence thesis;
    end;
    hence thesis by FUNCT_2:18;
  end;
end;

theorem Th37:
  for X being non empty set, Y being non empty Subset of ExtREAL,
  F being Function of X,Y holds rng(- F) = - rng F
proof
  let X be non empty set, Y be non empty Subset of ExtREAL, F be Function of X
  ,Y;
  thus rng(- F) c= - rng F
  proof
    let y be set;
A1: dom F = X by FUNCT_2:def 1;
    assume
A2: y in rng(- F);
    then reconsider y as R_eal;
    dom(- F) = X by FUNCT_2:def 1;
    then consider a being set such that
A3: a in X and
A4: y = (- F).a by A2,FUNCT_1:def 5;
    reconsider a as Element of X by A3;
    y = - F.a by A4,Def10;
    then - y in rng F by A1,FUNCT_1:def 5;
    then - (- y) in - rng F by Th23;
    hence thesis;
  end;
  thus - rng F c= rng(- F)
  proof
    let y be set;
    assume
A5: y in - rng F;
    then reconsider y as R_eal;
    - y in - (- rng F) by A5,Th23;
    then
A6: - y in rng F by Th22;
    dom F = X by FUNCT_2:def 1;
    then consider a being set such that
A7: a in X and
A8: - y = F.a by A6,FUNCT_1:def 5;
    reconsider a as Element of X by A7;
    y = - F.a by A8;
    then
A9: y = (- F).a by Def10;
    dom (- F) = X by FUNCT_2:def 1;
    hence thesis by A9,FUNCT_1:def 5;
  end;
end;

theorem Th38:
  for X being non empty set holds for Y being non empty Subset of
ExtREAL holds for F being Function of X,Y holds inf(- F) = - sup F & sup(- F) =
  - inf F
proof
  let X be non empty set;
  let Y be non empty Subset of ExtREAL;
  let F be Function of X,Y;
  thus inf(- F) = inf(- rng F) by Th37
    .= - sup F by Th32;
  thus sup(- F) = sup(- rng F) by Th37
    .= - inf F by Th33;
end;
 
::
::       Bounded Function of X,ExtREAL
::

definition
  let X be non empty set;
  let Y be non empty Subset of ExtREAL;
  let F be Function of X,Y;
  attr F is bounded_above means
  :Def11:
  sup F <+infty;
  attr F is bounded_below means
  :Def12:
  -infty <inf F;
end;

definition
  let X be non empty set, Y be non empty Subset of ExtREAL, F be Function of X
  ,Y;
  attr F is bounded means
  :Def13:
  F is bounded_above bounded_below;
end;

registration
  let X be non empty set;
  let Y be non empty Subset of ExtREAL;
  cluster bounded -> bounded_above bounded_below Function of X, Y;
  coherence by Def13;
  cluster bounded_above bounded_below -> bounded Function of X, Y;
  coherence by Def13;
end;

theorem
  for X being non empty set, Y being non empty Subset of ExtREAL, F
  being Function of X,Y holds F is bounded iff sup F <+infty & -infty <inf F
proof
  let X be non empty set, Y be non empty Subset of ExtREAL, F be Function of X
  ,Y;
  thus F is bounded implies sup F <+infty & -infty <inf F by Def11,Def12;
  assume that
A1: sup F <+infty and
A2: -infty <inf F;
A3: F is bounded_below by A2,Def12;
  F is bounded_above by A1,Def11;
  hence thesis by A3;
end;

theorem Th40:
  for X being non empty set, Y being non empty Subset of ExtREAL,
  F being Function of X,Y holds F is bounded_above iff - F is bounded_below
proof
  let X be non empty set, Y be non empty Subset of ExtREAL, F be Function of X
  ,Y;
  hereby
    assume F is bounded_above;
    then sup F <+infty by Def11;
    then
A1: - (+infty) <- sup F by XXREAL_3:40;
    - (+infty) = -infty by XXREAL_3:def 3;
    then -infty <inf(- F) by A1,Th38;
    hence - F is bounded_below by Def12;
  end;
  assume - F is bounded_below;
  then -infty <inf(- F) by Def12;
  then - inf(- F) <- (-infty) by XXREAL_3:40;
  then - (- sup F) <- (-infty) by Th38;
  hence thesis by Def11,XXREAL_3:5;
end;

theorem Th41:
  for X being non empty set, Y being non empty Subset of ExtREAL,
  F being Function of X,Y holds F is bounded_below iff - F is bounded_above
proof
  let X be non empty set, Y be non empty Subset of ExtREAL, F be Function of X
  ,Y;
  hereby
    assume F is bounded_below;
    then -infty <inf F by Def12;
    then - inf F <- (-infty) by XXREAL_3:40;
    then sup(- F) <+infty by Th38,XXREAL_3:5;
    hence - F is bounded_above by Def11;
  end;
  assume - F is bounded_above;
  then sup(- F) <+infty by Def11;
  then - inf F <+infty by Th38;
  then
A1: - (+infty) <- (- inf F) by XXREAL_3:40;
  - (+infty) = -infty by XXREAL_3:def 3;
  hence thesis by A1,Def12;
end;

theorem
  for X being non empty set, Y being non empty Subset of ExtREAL, F
  being Function of X,Y holds F is bounded iff - F is bounded
proof
  let X be non empty set, Y be non empty Subset of ExtREAL, F be Function of X
  ,Y;
  hereby
    assume
A1: F is bounded;
    then
A2: - F is bounded_below by Th40;
    - F is bounded_above by A1,Th41;
    hence - F is bounded by A2;
  end;
  assume
A3: - F is bounded;
  then
A4: F is bounded_below by Th41;
  F is bounded_above by A3,Th40;
  hence thesis by A4;
end;

theorem
  for X being non empty set, Y being non empty Subset of ExtREAL, F
being Function of X,Y, x being Element of X holds -infty <= F.x & F.x <= +infty
  by XXREAL_0:4,5;

theorem
  for X being non empty set, Y being non empty Subset of ExtREAL, F
being Function of X,Y, x being Element of X st Y c= REAL holds -infty <F.x & F.
  x <+infty
proof
  let X be non empty set, Y be non empty Subset of ExtREAL, F be Function of X
  ,Y, x be Element of X;
A1: -infty <= F.x by XXREAL_0:5;
  assume Y c= REAL;
  hence thesis by A1,XXREAL_0:1,4;
end;

theorem Th45:
  for X being non empty set, Y being non empty Subset of ExtREAL,
F being Function of X,Y, x being Element of X holds inf F <= F.x & F.x <= sup F
proof
  let X be non empty set, Y be non empty Subset of ExtREAL, F be Function of X
  ,Y, x be Element of X;
  X = dom F by FUNCT_2:def 1;
  then F.x in rng F by FUNCT_1:def 5;
  hence thesis by XXREAL_2:3,4;
end;

theorem Th46:
  for X being non empty set, Y being non empty Subset of ExtREAL,
  F being Function of X,Y holds Y c= REAL implies (F is bounded_above iff sup F
  in REAL)
proof
  let X be non empty set, Y be non empty Subset of ExtREAL, F be Function of X
  ,Y;
  assume
A1: Y c= REAL;
  hereby
    consider x being Element of X;
    X = dom F by FUNCT_2:def 1;
    then
A2: F.x in rng F by FUNCT_1:def 5;
    rng F c= Y by RELAT_1:def 19;
    then F.x in Y by A2;
    then
A3: not sup F = -infty by A1,Th45,XXREAL_0:12;
    assume F is bounded_above;
    then
A4: sup F <+infty by Def11;
    sup F in REAL or sup F in {-infty,+infty} by XBOOLE_0:def 3;
    hence sup F in REAL by A4,A3,TARSKI:def 2;
  end;
  assume sup F in REAL;
  then sup F <+infty by XXREAL_0:9;
  hence thesis by Def11;
end;

theorem Th47:
  for X being non empty set, Y being non empty Subset of ExtREAL,
  F being Function of X,Y st Y c= REAL holds (F is bounded_below iff inf F in
  REAL)
proof
  let X be non empty set, Y be non empty Subset of ExtREAL, F be Function of X
  ,Y;
  assume
A1: Y c= REAL;
  thus F is bounded_below implies inf F in REAL
  proof
    consider x being Element of X;
    X = dom F by FUNCT_2:def 1;
    then
A2: F.x in rng F by FUNCT_1:def 5;
    rng F c= Y by RELAT_1:def 19;
    then F.x in Y by A2;
    then
A3: not inf F = +infty by A1,Th45,XXREAL_0:9;
    assume F is bounded_below;
    then
A4: -infty <inf F by Def12;
    inf F in REAL or inf F in {-infty,+infty} by XBOOLE_0:def 3;
    hence thesis by A4,A3,TARSKI:def 2;
  end;
  assume inf F in REAL;
  then -infty <inf F by XXREAL_0:12;
  hence thesis by Def12;
end;

theorem
  for X being non empty set, Y being non empty Subset of ExtREAL, F
being Function of X,Y st Y c= REAL holds (F is bounded iff inf F in REAL & sup
  F in REAL)
proof
  let X be non empty set, Y be non empty Subset of ExtREAL, F be Function of X
  ,Y;
  assume
A1: Y c= REAL;
  hence F is bounded implies inf F in REAL & sup F in REAL by Th46,Th47;
  assume that
A2: inf F in REAL and
A3: sup F in REAL;
A4: F is bounded_below by A1,A2,Th47;
  F is bounded_above by A1,A3,Th46;
  hence thesis by A4;
end;

theorem Th49:
  for X being non empty set, Y,Z being non empty Subset of ExtREAL
  holds for F1 being Function of X,Y, F2 being Function of X,Z st F1 is
  bounded_above & F2 is bounded_above holds F1 + F2 is bounded_above
proof
  let X be non empty set, Y,Z be non empty Subset of ExtREAL;
  let F1 be Function of X,Y, F2 be Function of X,Z;
  assume that
A1: F1 is bounded_above and
A2: F2 is bounded_above;
A3: not sup F2 = +infty by A2,Def11;
A4: sup F1 in REAL & sup F2 in REAL implies sup F1 + sup F2 <+infty
  proof
    reconsider a = sup F1, b = sup F2 as R_eal;
    assume that
A5: sup F1 in REAL and
A6: sup F2 in REAL;
    reconsider a,b as Real by A5,A6;
    sup F1 + sup F2 = a + b by XXREAL_3:def 2;
    hence thesis by XXREAL_0:9;
  end;
A7: sup F1 in REAL & sup F2 = -infty implies sup F1 + sup F2 <+infty by
  XXREAL_0:7,XXREAL_3:def 2;
A8: sup F1 = -infty & sup F2 = -infty implies sup F1 + sup F2 <+infty by
  XXREAL_0:7,XXREAL_3:def 2;
A9: sup F1 = -infty & sup F2 in REAL implies sup F1 + sup F2 <+infty by
  XXREAL_0:7,XXREAL_3:def 2;
A10: not sup F1 = +infty by A1,Def11;
  sup(F1 + F2) <+infty
  proof
    assume not sup(F1 + F2) <+infty;
    then not sup(F1 + F2) <= +infty or sup(F1 + F2) = +infty by
    XXREAL_0:1;
    hence thesis by A10,A3,A4,A7,A9,A8,Th35,XXREAL_0:4,XXREAL_3:def 1;
  end;
  hence thesis by Def11;
end;

theorem Th50:
  for X being non empty set, Y,Z being non empty Subset of ExtREAL
  holds for F1 being Function of X,Y, F2 being Function of X,Z st F1 is
  bounded_below & F2 is bounded_below holds F1 + F2 is bounded_below
proof
  let X be non empty set, Y,Z be non empty Subset of ExtREAL;
  let F1 be Function of X,Y, F2 be Function of X,Z;
  assume that
A1: F1 is bounded_below and
A2: F2 is bounded_below;
A3: not inf F2 = -infty by A2,Def12;
A4: inf F1 in REAL & inf F2 in REAL implies -infty <inf F1 + inf F2
  proof
    reconsider a = inf F1, b = inf F2 as R_eal;
    assume that
A5: inf F1 in REAL and
A6: inf F2 in REAL;
    reconsider a,b as Real by A5,A6;
    inf F1 + inf F2 = a + b by XXREAL_3:def 2;
    hence thesis by XXREAL_0:12;
  end;
A7: inf F1 in REAL & inf F2 = +infty implies -infty <inf F1 + inf F2 by
  XXREAL_0:7,XXREAL_3:def 2;
A8: inf F1 = +infty & inf F2 = +infty implies -infty <inf F1 + inf F2 by
  XXREAL_0:7,XXREAL_3:def 2;
A9: inf F1 = +infty & inf F2 in REAL implies -infty <inf F1 + inf F2 by
  XXREAL_0:7,XXREAL_3:def 2;
A10: not inf F1 = -infty by A1,Def12;
  -infty <inf(F1 + F2)
  proof
    assume inf(F1 + F2) <= -infty;
    then not -infty <= inf(F1 + F2) or inf(F1 + F2) = -infty by
    XXREAL_0:1;
    hence thesis by A10,A3,A4,A7,A9,A8,Th36,XXREAL_0:6,XXREAL_3:def 1;
  end;
  hence thesis by Def12;
end;

theorem
  for X being non empty set, Y,Z being non empty Subset of ExtREAL holds
for F1 being Function of X,Y, F2 being Function of X,Z st F1 is bounded & F2 is
  bounded holds F1 + F2 is bounded
proof
  let X be non empty set, Y,Z be non empty Subset of ExtREAL;
  let F1 be Function of X,Y, F2 be Function of X,Z;
  assume that
A1: F1 is bounded and
A2: F2 is bounded;
A3: F1 + F2 is bounded_below by A1,A2,Th50;
  F1 + F2 is bounded_above by A1,A2,Th49;
  hence thesis by A3;
end;

theorem Th52:
  ex F being Function of NAT,ExtREAL st F is one-to-one & NAT =
  rng F & rng F is non empty Subset of ExtREAL
proof
  reconsider F = incl NAT as Function of NAT,ExtREAL by FUNCT_2:9,NUMBERS:31;
  take F;
A1: dom F = NAT by FUNCT_2:def 1;
  for y being set holds y in NAT iff ex x being set st x in dom F & y = F. x
  proof
    let y be set;
    y in NAT implies ex x being set st x in dom F & y = F.x
    proof
      assume
A2:   y in NAT;
      take y;
      thus thesis by A2,FUNCT_1:35,FUNCT_2:def 1;
    end;
    hence thesis by A1,FUNCT_1:35;
  end;
  hence thesis by FUNCT_1:def 5;
end;
 
::
::       Series of Elements of ExtREAL numbers
::

definition
  let D be non empty set, IT be Subset of D;
  redefine attr IT is countable means
  :Def14:
  IT is empty or ex F being Function of NAT,D st IT = rng F;
  compatibility
  proof
    thus IT is countable implies IT is empty or ex F being Function of NAT,D
    st IT = rng F
    proof
      assume that
A1:   IT is countable and
A2:   IT is not empty;
      consider f being Function such that
A3:   dom f = NAT and
A4:   IT c= rng f by A1,CARD_3:127;
      consider x being Element of D such that
A5:   x in IT by A2,SUBSET_1:10;
      set F = f|(f"IT) +* (NAT \ f"IT --> x);
A6:   rng F = IT & dom F = NAT
      proof
A7:     f"IT c= NAT by A3,RELAT_1:167;
A8:     dom(f|(f"IT)) = NAT /\ (f"IT) by A3,RELAT_1:90;
        per cases;
        suppose
A9:       NAT = f"IT;
          then
A10:      NAT \ f"IT = {} by XBOOLE_1:37;
          then dom(NAT \ f"IT --> x) = {};
          then dom(f|(f"IT)) /\ dom(NAT \ f"IT --> x) = {};
          then dom(f|(f"IT)) misses dom(NAT \ f"IT --> x) by XBOOLE_0:def
          7;
          then F = (f|(f"IT)) \/ (NAT \ f"IT --> x) by FUNCT_4:32;
          hence rng F = rng(f|(f"IT)) \/ rng(NAT \ f"IT --> x) by RELAT_1:
          26
            .= rng(f|(f"IT)) \/ {} by A10
            .= f.:(f"IT) by RELAT_1:148
            .= IT by A4,FUNCT_1:147;
          thus dom F = dom(f|(f"IT)) \/ dom(NAT \ f"IT --> x) by FUNCT_4:def 1
            .= dom(f|(f"IT)) \/ {} by A10
            .= NAT by A8,A9;
        end;
        suppose
A11:      NAT <> f"IT;
A12:      now
            assume NAT \ f"IT is empty;
            then NAT c= f"IT by XBOOLE_1:37;
            hence contradiction by A7,A11,XBOOLE_0:def 10;
          end;
          dom(NAT \ f"IT --> x) = NAT \ f"IT by FUNCOP_1:19;
          then F = (f|(f"IT)) \/ (NAT \ f"IT --> x) by A8,FUNCT_4:32 ,
          XBOOLE_1:89;
          hence rng F = rng(f|(f"IT)) \/ rng(NAT \ f"IT --> x) by RELAT_1:
          26
            .= rng(f|(f"IT)) \/ {x} by A12,FUNCOP_1:14
            .= f.:(f"IT) \/ {x} by RELAT_1:148
            .= IT \/ {x} by A4,FUNCT_1:147
            .= IT by A5,ZFMISC_1:46;
          thus dom F = dom(f|(f"IT)) \/ dom(NAT \ f"IT --> x) by FUNCT_4:def 1
            .= NAT /\ (f"IT) \/ (NAT \ f"IT) by A8,FUNCOP_1:19
            .= NAT by XBOOLE_1:51;
        end;
      end;
      then reconsider F as Function of NAT, D by FUNCT_2:def 1,RELSET_1:11;
      take F;
      thus thesis by A6;
    end;
    assume
A13: IT is empty or ex F being Function of NAT,D st IT = rng F;
    per cases;
    suppose
      IT is empty;
      hence thesis by CARD_4:43;
    end;
    suppose
      IT is not empty;
      then consider F being Function of NAT,D such that
A14:  IT = rng F by A13;
      dom F = NAT by FUNCT_2:def 1;
      hence thesis by A14,CARD_3:127;
    end;
  end;
end;

registration
  cluster countable (non empty Subset of ExtREAL);
  existence
  proof
    consider F being Function of NAT,ExtREAL;
    reconsider A = rng F as non empty Subset of ExtREAL;
    take A;
    assume A is non empty;
    thus thesis;
  end;
end;

definition
  mode Denum_Set_of_R_EAL is countable non empty Subset of ExtREAL;
end;

definition
  let IT be set;
  attr IT is nonnegative means
  :Def15:
  for x being R_eal holds x in IT implies 0. <= x;
end;

registration
  cluster nonnegative Denum_Set_of_R_EAL;
  existence
  proof
    reconsider N = NAT as Denum_Set_of_R_EAL by Def14,Th52;
    take N;
    thus for x being R_eal holds x in N implies 0. <= x by NAT_1:2;
  end;
end;

definition
  mode Pos_Denum_Set_of_R_EAL is nonnegative Denum_Set_of_R_EAL;
end;

definition
  let D be Denum_Set_of_R_EAL;
  mode Num of D -> Function of NAT,ExtREAL means
    :Def16:
    D = rng it;
  existence by Def14;
end;

theorem Th53:
  for D being Denum_Set_of_R_EAL holds for N being Num of D holds
ex F being Function of NAT,ExtREAL st F.0 = N.0 & for n being Element of NAT, y
  being R_eal st y = F.n holds F.(n + 1) = y + N.(n+1)
proof
  let D be Denum_Set_of_R_EAL;
  let N be Num of D;
  defpred P[set,set,set] means for a,b being R_eal,s being Element of NAT
  holds (s = $1 & a = $2 & b = $3 implies b = a + N.(s+1));
  reconsider A = N.0 as R_eal;
A1: for n being Element of NAT for x being Element of ExtREAL ex y being
  Element of ExtREAL st P[n,x,y]
  proof
    let n be Element of NAT;
    let x be Element of ExtREAL;
    reconsider y = x + N.(n+1) as R_eal;
    take y;
    thus thesis;
  end;
  consider F being Function of NAT,ExtREAL such that
A2: F.0 = A & for n being Element of NAT holds P[n,F.n,F.(n+1)] from
  RECDEF_1:sch 2(A1);
  take F;
  thus thesis by A2;
end;

definition
  let D be Denum_Set_of_R_EAL;
  let N be Num of D;
  func Ser(D,N) -> Function of NAT,ExtREAL means
  :Def17:
  it.0 = N.0 & for n
being Element of NAT holds for y being R_eal st y = it.n holds it.(n + 1) = y +
  N.(n + 1);
  existence by Th53;
  uniqueness
  proof
    let S1,S2 be Function of NAT,ExtREAL such that
A1: S1.0 = N.0 and
A2: for n being Element of NAT holds for y being R_eal st y = S1.n
    holds S1.(n + 1) = y + N.(n + 1) and
A3: S2.0 = N.0 and
A4: for n being Element of NAT holds for y being R_eal st y = S2.n
    holds S2.(n + 1) = y + N.(n + 1);
    defpred P[set] means S1.$1 = S2.$1;
    for n being set holds n in NAT implies P[n]
    proof
      let n be set;
      assume
A5:   n in NAT;
      then reconsider n as Element of REAL;
      reconsider n as Element of NAT by A5;
A6:   for k being Element of NAT st P[k] holds P[k+1]
      proof
        let k be Element of NAT;
        reconsider y2 = S2.k as R_eal;
        assume S1.k = S2.k;
        hence S1.(k + 1) = y2 + N.(k + 1) by A2
          .= S2.(k + 1) by A4;
      end;
A7:   P[0] by A1,A3;
      for k being Element of NAT holds P[k] from NAT_1:sch 1(A7, A6);
      then S1.n = S2.n;
      hence thesis;
    end;
    hence thesis by FUNCT_2:18;
  end;
end;

theorem Th54:
  for D being Pos_Denum_Set_of_R_EAL, N being Num of D, n being
  Element of NAT holds 0. <= N.n
proof
  let D be Pos_Denum_Set_of_R_EAL, N be Num of D, n be Element of NAT;
  D = rng N by Def16;
  then N.n in D by FUNCT_2:6;
  hence thesis by Def15;
end;

theorem Th55:
  for D being Pos_Denum_Set_of_R_EAL, N being Num of D, n being
  Element of NAT holds Ser(D,N).n <= Ser(D,N).(n + 1) & 0. <= Ser(D,N).n
proof
  let D be Pos_Denum_Set_of_R_EAL, N be Num of D, n be Element of NAT;
  set F = Ser(D,N);
  defpred P[Element of NAT] means F.$1 <= F.($1 + 1) & 0. <= F.$1;
A1: F.(0 + 1) = F.0 + N.(1) by Def17;
A2: for k being Element of NAT st P[k] holds P[k + 1]
  proof
    let k be Element of NAT;
    assume that
A3: F.k <= F.(k + 1) and
A4: 0. <= F.k;
A5: 0. <= N.((k+1) + 1) by Th54;
    F.((k+1) + 1) = F.(k + 1) + N.((k+1) + 1) by Def17;
    hence thesis by A3,A4,A5,XXREAL_0:2,XXREAL_3:42;
  end;
  F.0 = N.0 by Def17;
  then
A6: P[0] by A1,Th54,XXREAL_3:42;
  for n being Element of NAT holds P[n] from NAT_1:sch 1(A6,A2);
  hence thesis;
end;

theorem Th56:
  for D being Pos_Denum_Set_of_R_EAL, N being Num of D, n,m being
  Element of NAT holds Ser(D,N).n <= Ser(D,N).(n + m)
proof
  let D be Pos_Denum_Set_of_R_EAL, N be Num of D, n,m be Element of NAT;
  defpred P[Element of NAT] means Ser(D,N).n <= Ser(D,N).(n + $1);
A1: for k being Element of NAT st P[k] holds P[k + 1]
  proof
    let k be Element of NAT;
    Ser(D,N).(n + (k+1)) = Ser(D,N).((n + k) + 1);
    then
A2: Ser(D,N).(n + k) <= Ser(D,N).(n + (k+1)) by Th55;
    assume Ser(D,N).n <= Ser(D,N).(n + k);
    hence thesis by A2,XXREAL_0:2;
  end;
A3: P[0];
  for n being Element of NAT holds P[n] from NAT_1:sch 1(A3,A1);
  hence thesis;
end;

definition
  let D be Denum_Set_of_R_EAL;
  mode Set_of_Series of D -> non empty Subset of ExtREAL means
    ex N being Num of D st it = rng Ser(D,N);
  existence
  proof
    consider N being Num of D;
    set S = Ser(D,N);
    reconsider SER = rng S as non empty Subset of ExtREAL;
    take SER;
    thus thesis;
  end;
end;

Lm1: for F being Function of NAT,ExtREAL holds rng F is non empty Subset of
ExtREAL;

definition
  let F be Function of NAT,ExtREAL;
  redefine func rng F -> non empty Subset of ExtREAL;
  coherence by Lm1;
end;

definition
  let D be Pos_Denum_Set_of_R_EAL;
  let N be Num of D;
  func SUM(D,N) -> R_eal equals
  sup(rng Ser(D,N));
  coherence;
end;

definition
  let D be Pos_Denum_Set_of_R_EAL;
  let N be Num of D;
  pred D is_sumable N means
  SUM(D,N) in REAL;
end;

theorem Th57:
  for F being Function of NAT,ExtREAL holds rng F is
  Denum_Set_of_R_EAL by Def14;

definition
  let F be Function of NAT,ExtREAL;
  redefine func rng F -> Denum_Set_of_R_EAL;
  coherence by Th57;
end;

definition
  let F be Function of NAT,ExtREAL;
  func Ser(F) -> Function of NAT,ExtREAL means
  :Def21:
  for N being Num of rng F st N = F holds it = Ser(rng F,N);
  existence
  proof
    reconsider N = F as Num of rng F by Def16;
    take Ser(rng F,N);
    thus thesis;
  end;
  uniqueness
  proof
    reconsider N = F as Num of rng F by Def16;
    let S1,S2 be Function of NAT,ExtREAL such that
A1: for N being Num of rng F st N = F holds S1 = Ser(rng F,N) and
A2: for N being Num of rng F st N = F holds S2 = Ser(rng F,N);
    thus S1 = Ser(rng F,N) by A1
      .= S2 by A2;
  end;
end;

definition
  let R be Relation;
  attr R is nonnegative means
  :Def22:
  rng R is nonnegative;
end;

definition
  let F be Function of NAT,ExtREAL;
  func SUM(F) -> R_eal equals
  sup rng Ser(F);
  coherence;
end;

theorem Th58:
  for X being set, F being PartFunc of X,ExtREAL holds F is
  nonnegative iff for n being Element of X holds 0. <= F.n
proof
  let X be set, F be PartFunc of X,ExtREAL;
  hereby
    assume F is nonnegative;
    then
A1: rng F is nonnegative by Def22;
    let n be Element of X;
    per cases;
    suppose
      n in dom F;
      then F.n in rng F by FUNCT_1:def 5;
      hence 0. <= F.n by A1,Def15;
    end;
    suppose
      not n in dom F;
      hence 0. <= F.n by FUNCT_1:def 4;
    end;
  end;
  assume
A2: for n being Element of X holds 0. <= F.n;
  let y be R_eal;
  assume y in rng F;
  then ex x being set st x in dom F & y = F.x by FUNCT_1:def 5;
  hence thesis by A2;
end;

theorem Th59:
  for F being Function of NAT,ExtREAL, n being Element of NAT st F
  is nonnegative holds (Ser F).n <= (Ser F).(n + 1) & 0. <= (Ser F).n
proof
  let F be Function of NAT,ExtREAL, n be Element of NAT;
  reconsider N = F as Num of rng F by Def16;
  assume F is nonnegative;
  then
A1: rng F is Pos_Denum_Set_of_R_EAL by Def22;
  Ser F = Ser(rng F,N) by Def21;
  hence thesis by A1,Th55;
end;

theorem Th60:
  for F being Function of NAT,ExtREAL st F is nonnegative holds
  for n,m being Element of NAT holds Ser(F).n <= Ser(F).(n + m)
proof
  let F be Function of NAT,ExtREAL;
  reconsider N = F as Num of rng F by Def16;
  assume F is nonnegative;
  then
A1: rng F is Pos_Denum_Set_of_R_EAL by Def22;
  let n,m be Element of NAT;
  Ser(F) = Ser(rng F,N) by Def21;
  hence thesis by A1,Th56;
end;

theorem Th61:
  for F1,F2 being Function of NAT,ExtREAL st (for n being Element
of NAT holds F1.n <= F2.n) holds for n being Element of NAT holds Ser(F1).n <=
  Ser(F2).n
proof
  let F1,F2 be Function of NAT,ExtREAL;
  reconsider N2 = F2 as Num of rng F2 by Def16;
  defpred P[Element of NAT] means Ser(F1).$1 <= Ser(F2).$1;
  reconsider N1 = F1 as Num of rng F1 by Def16;
  assume
A1: for n being Element of NAT holds F1.n <= F2.n;
  let n be Element of NAT;
A2: Ser(F2) = Ser(rng F2,N2) by Def21;
  then
A3: Ser(F2).0 = F2.0 by Def17;
A4: Ser(F1) = Ser(rng F1,N1) by Def21;
A5: for k being Element of NAT st P[k] holds P[k+1]
  proof
    let k be Element of NAT;
    assume
A6: Ser(F1).k <= Ser(F2).k;
A7: F1.(k+1) <= F2.(k+1) by A1;
A8: Ser(F2).(k+1) = Ser(F2).k + F2.(k+1) by A2,Def17;
    Ser(F1).(k+1) = Ser(F1).k + F1.(k+1) by A4,Def17;
    hence thesis by A6,A8,A7,XXREAL_3:38;
  end;
  Ser(F1).0 = F1.0 by A4,Def17;
  then
A9: P[0] by A1,A3;
  for n being Element of NAT holds P[n] from NAT_1:sch 1(A9,A5);
  hence thesis;
end;

theorem Th62:
  for F1,F2 being Function of NAT,ExtREAL st (for n being Element
  of NAT holds F1.n <= F2.n) holds SUM(F1) <= SUM(F2)
proof
  let F1,F2 be Function of NAT,ExtREAL;
  assume
A1: for n being Element of NAT holds F1.n <= F2.n;
  for x being ext-real number st x in rng Ser(F1) holds ex y being
  ext-real number st y in rng Ser(F2) & x <= y
  proof
    let x be ext-real number;
A2: dom Ser(F1) = NAT by FUNCT_2:def 1;
    assume x in rng Ser(F1);
    then consider n being set such that
A3: n in NAT and
A4: x = Ser(F1).n by A2,FUNCT_1:def 5;
    reconsider n as Element of NAT by A3;
    reconsider y = Ser(F2).n as R_eal;
    take y;
    dom Ser(F2) = NAT by FUNCT_2:def 1;
    hence thesis by A1,A4,Th61,FUNCT_1:def 5;
  end;
  hence thesis by XXREAL_2:63;
end;

theorem Th63:
  for F being Function of NAT,ExtREAL holds Ser(F).0 = F.0 & for n
being Element of NAT, y being R_eal st y = Ser(F).n holds Ser(F).(n + 1) = y +
  F.(n + 1)
proof
  let F be Function of NAT,ExtREAL;
  reconsider N = F as Num of rng F by Def16;
  Ser F = Ser(rng F,N) by Def21;
  hence thesis by Def17;
end;

theorem Th64:
  for F being Function of NAT,ExtREAL st F is nonnegative holds (
  ex n being Element of NAT st F.n = +infty) implies SUM(F) = +infty
proof
  let F be Function of NAT,ExtREAL;
  assume
A1: F is nonnegative;
  given n being Element of NAT such that
A2: F.n = +infty;
A3: (ex k being Nat st n = k + 1) implies SUM(F) = +infty
  proof
    given k being Nat such that
A4: n = k + 1;
    reconsider k as Element of NAT by ORDINAL1:def 13;
    reconsider y = Ser(F).k as R_eal;
    reconsider x = Ser(F).(k + 1) as R_eal;
A5: Ser(F).(k + 1) = y + F.(k + 1) by Th63;
    Ser(F).k <> -infty by A1,Th59,XXREAL_0:6;
    then
A6: x = +infty by A2,A4,A5,XXREAL_3:def 2;
    then x is UpperBound of rng Ser(F) by XXREAL_2:41;
    hence thesis by A6,FUNCT_2:6,XXREAL_2:55;
  end;
  n = 0 implies SUM(F) = +infty
  proof
    reconsider x = Ser(F).0 as R_eal;
    assume n = 0;
    then
A7: Ser(F).0 = +infty by A2,Th63;
    then x is UpperBound of rng Ser(F) by XXREAL_2:41;
    hence thesis by A7,FUNCT_2:6,XXREAL_2:55;
  end;
  hence thesis by A3,NAT_1:6;
end;

definition
  let F be Function of NAT,ExtREAL;
  attr F is summable means
  :Def24:
  SUM(F) in REAL;
end;

theorem
  for F being Function of NAT,ExtREAL st F is nonnegative holds (ex n
  being Element of NAT st F.n = +infty) implies F is not summable
proof
  let F be Function of NAT,ExtREAL;
  assume
A1: F is nonnegative;
  assume ex n being Element of NAT st F.n = +infty;
  then not SUM(F) in REAL by A1,Th64;
  hence thesis by Def24;
end;

theorem
  for F1,F2 being Function of NAT,ExtREAL st F1 is nonnegative & (for n
  being Element of NAT holds F1.n <= F2.n) holds (F2 is summable implies F1 is
  summable)
proof
  let F1,F2 be Function of NAT,ExtREAL;
  assume F1 is nonnegative;
  then
A1: 0. <= Ser(F1).0 by Th59;
  Ser(F1).0 <= sup(rng Ser(F1)) by FUNCT_2:6,XXREAL_2:4;
  then
A2: SUM(F1) <> -infty by A1,XXREAL_0:2,6;
  assume
A3: for n being Element of NAT holds F1.n <= F2.n;
  assume F2 is summable;
  then SUM(F2) in REAL by Def24;
  then
A4: not SUM(F1) = +infty by A3,Th62,XXREAL_0:9;
  SUM(F1) in REAL or SUM(F1) in {-infty,+infty} by XBOOLE_0:def 3;
  hence thesis by A4,A2,Def24,TARSKI:def 2;
end;

theorem Th67:
  for F being Function of NAT,ExtREAL st F is nonnegative holds
for n being Nat st (for r being Element of NAT st n <= r holds F.r = 0.) holds
  SUM(F) = Ser(F).n
proof
  let F be Function of NAT,ExtREAL;
  assume
A1: F is nonnegative;
  let n be Nat;
  reconsider n9=n as Element of NAT by ORDINAL1:def 13;
  assume
A2: for r being Element of NAT st n <= r holds F.r = 0.;
A3: for r being Element of NAT st n <= r holds Ser(F).r <= Ser(F).n
  proof
    defpred P[Nat] means Ser(F).(n + $1) <= Ser(F).n;
    let r be Element of NAT;
    assume n <= r;
    then
A4: ex m being Nat st r = n + m by NAT_1:10;
A5: for s being Nat st P[s] holds P[s+1]
    proof
      let s be Nat;
      reconsider s9=s as Element of NAT by ORDINAL1:def 13;
      reconsider y = Ser(F).(n + s) as R_eal;
      n9 + (s9 + 1) = (n9 + s9) + 1;
      then
A6:   Ser(F).(n9 + (s9 + 1)) = y + F.(n9 + (s9 + 1)) by Th63;
      0 <= s+1 by NAT_1:2;
      then n + 0 <= n + (s+1) by XREAL_1:9;
      then
A7:   F.(n9 + (s9+1)) = 0. by A2;
      assume Ser(F).(n + s) <= Ser(F).n;
      hence thesis by A6,A7,XXREAL_3:4;
    end;
A8: P[0];
    for m being Nat holds P[m] from NAT_1:sch 2(A8,A5);
    hence thesis by A4;
  end;
A9: for r being Element of NAT st r <= n holds Ser(F).r <= Ser(F).n
  proof
    let r be Element of NAT;
    assume r <= n;
    then consider m being Nat such that
A10: n = r + m by NAT_1:10;
    reconsider m as Element of NAT by ORDINAL1:def 13;
    n = r + m by A10;
    hence thesis by A1,Th60;
  end;
  for y being ext-real number st y in rng Ser(F) holds y <= Ser(F).n
  proof
    let y be ext-real number;
A11: dom Ser(F) = NAT by FUNCT_2:def 1;
    assume y in rng Ser(F);
    then consider m being set such that
A12: m in NAT and
A13: y = Ser(F).m by A11,FUNCT_1:def 5;
    reconsider m as Element of NAT by A12;
    m <= n or n <= m;
    hence thesis by A3,A9,A13;
  end;
  then
A14: Ser(F).n is UpperBound of rng Ser(F) by XXREAL_2:def 1;
  Ser(F).n9 in rng Ser(F) by FUNCT_2:6;
  hence thesis by A14,XXREAL_2:55;
end;

theorem Th68:
  for F being Function of NAT,ExtREAL st (for n being Element of
NAT holds F.n in REAL) holds for n being Element of NAT holds Ser(F).n in REAL
proof
  let F be Function of NAT,ExtREAL;
  defpred P[Element of NAT] means Ser(F).$1 in REAL;
  assume
A1: for n being Element of NAT holds F.n in REAL;
A2: for s being Element of NAT st P[s] holds P[s+1]
  proof
    let s be Element of NAT;
    reconsider b = F.(s+1) as Real by A1;
    reconsider y = Ser(F).s as R_eal;
    assume Ser(F).s in REAL;
    then reconsider a = y as Real;
A3: y + F.(s+1) = a + b by XXREAL_3:def 2;
    Ser(F).(s+1) = y + F.(s+1) by Th63;
    hence thesis by A3;
  end;
  Ser(F).0 = F.0 by Th63;
  then
A4: P[0] by A1;
  thus for s being Element of NAT holds P[s] from NAT_1:sch 1(A4,A2);
end;

theorem
  for F being Function of NAT,ExtREAL st F is nonnegative & (ex n being
Element of NAT st (for k being Element of NAT st n <= k holds F.k = 0.) & (for
  k being Element of NAT st k <= n holds F.k <> +infty)) holds F is summable
proof
  let F be Function of NAT,ExtREAL;
  assume
A1: F is nonnegative;
  given n being Element of NAT such that
A2: for k being Element of NAT st n <= k holds F.k = 0. and
A3: for k being Element of NAT st k <= n holds F.k <> +infty;
  for s being Element of NAT holds F.s in REAL
  proof
    let s be Element of NAT;
A4: s <= n implies F.s in REAL
    proof
      0. <= F.s by A1,Th58;
      then
A5:   F.s <> -infty by XXREAL_0:6;
      assume s <= n;
      then
A6:   not F.s = +infty by A3;
      F.s in REAL or F.s in {-infty,+infty} by XBOOLE_0:def 3;
      hence thesis by A6,A5,TARSKI:def 2;
    end;
    n <= s implies F.s in REAL
    proof
      assume n <= s;
      then F.s = 0. by A2;
      hence thesis;
    end;
    hence thesis by A4;
  end;
  then
A7: Ser(F).n in REAL by Th68;
  SUM(F) = Ser(F).n by A1,A2,Th67;
  hence thesis by A7,Def24;
end;
 
:: Added by AK, 2006.02.06

theorem
  for X being set, F being PartFunc of X,ExtREAL holds F is nonnegative
  iff for n being set holds 0. <= F.n
proof
  let X be set, F be PartFunc of X,ExtREAL;
  hereby
    assume F is nonnegative;
    then
A1: rng F is nonnegative by Def22;
    let n be set;
    per cases;
    suppose
      n in dom F;
      then F.n in rng F by FUNCT_1:def 5;
      hence 0. <= F.n by A1,Def15;
    end;
    suppose
      not n in dom F;
      hence 0. <= F.n by FUNCT_1:def 4;
    end;
  end;
  assume
A2: for n being set holds 0. <= F.n;
  let y be R_eal;
  assume y in rng F;
  then ex x being set st x in dom F & y = F.x by FUNCT_1:def 5;
  hence thesis by A2;
end;

theorem
  for X being set, F being PartFunc of X,ExtREAL st for n being set st n
  in dom F holds 0. <= F.n holds F is nonnegative
proof
  let X be set, F be PartFunc of X,ExtREAL such that
A1: for n being set st n in dom F holds 0. <= F.n;
  let y be R_eal;
  assume y in rng F;
  then ex x being set st x in dom F & y = F.x by FUNCT_1:def 5;
  hence thesis by A1;
end;
