:: Convergent Sequences and the Limit of Sequences
::  by Jaros{\l}aw Kotowicz
::
:: Received July 4, 1989
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies NUMBERS, SUBSET_1, XREAL_0, ORDINAL1, SEQ_1, VALUED_0, FUNCT_1,
      ARYTM_1, ARYTM_3, COMPLEX1, XXREAL_0, CARD_1, RELAT_1, XXREAL_2, NAT_1,
      ORDINAL2, REAL_1, VALUED_1, SEQ_2;
 notations SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0, COMPLEX1, REAL_1,
      NAT_1, RELAT_1, FUNCT_1, VALUED_0, VALUED_1, SEQ_1, XXREAL_0;
 constructors XXREAL_0, REAL_1, NAT_1, COMPLEX1, PARTFUN1, VALUED_1, SEQ_1,
      RELSET_1;
 registrations ORDINAL1, RELSET_1, XXREAL_0, XREAL_0, MEMBERED, XBOOLE_0,
      VALUED_0, VALUED_1, FUNCT_2, NUMBERS;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions VALUED_1;
 theorems SEQ_1, ABSVALUE, NAT_1, XREAL_0, XCMPLX_0, XCMPLX_1, XREAL_1,
      COMPLEX1, XXREAL_0, VALUED_1, FUNCT_2;
 schemes NAT_1;

begin

reserve n,n1,n2,m for Element of NAT,
  r,r1,r2,p,g1,g2,g for real number,
  seq,seq9,seq1 for Real_Sequence,
  y for set;

canceled 8;

theorem Th9:
  -g<r & r<g iff abs(r)<g
proof
  thus -g<r & r<g implies abs(r)<g
  proof
    assume that
A1: -g<r and
A2: r<g;
A3: abs(r)<=g by A1,A2,ABSVALUE:12;
    now
      assume
A4:   abs(r)=g;
      now
        assume r<0;
        then g=-r by A4,ABSVALUE:def 1;
        hence contradiction by A1;
      end;
      hence contradiction by A2,A4,ABSVALUE:def 1;
    end;
    hence thesis by A3,XXREAL_0:1;
  end;
  assume
A5: abs(r)<g;
  then
A6: -g<=r by ABSVALUE:12;
A7: 0<=abs(r) by COMPLEX1:132;
A8: 0<g by A5,COMPLEX1:132;
A9: -g<-0 by A5,A7,XREAL_1:26;
  now
    assume r=-g;
    then abs(r)=--g by A9,ABSVALUE:def 1
      .=g;
    hence contradiction by A5;
  end;
  hence -g<r by A6,XXREAL_0:1;
  thus thesis by A5,A8,ABSVALUE:def 1;
end;

canceled;

theorem Th11:
  g<>0 & r<>0 implies abs(g"-r")=abs(g-r)/(abs(g)*abs(r))
proof
  assume that
A1: g<>0 and
A2: r<>0;
  thus abs(g"-r")=abs(1/g-r") by XCMPLX_1:217
    .=abs(1/g-1/r) by XCMPLX_1:217
    .=abs((1*r-1*g)/(g*r)) by A1,A2,XCMPLX_1:131
    .=abs(r-g)/abs(g*r) by COMPLEX1:153
    .=abs(-(g-r))/(abs(g)*abs(r)) by COMPLEX1:151
    .=abs(g-r)/(abs(g)*abs(r)) by COMPLEX1:138;
end;

definition
  let f be real-valued Function;
  attr f is bounded_above means
  :Def1:
  ex r st for y st y in dom f holds f.y<r;
  attr f is bounded_below means
  :Def2:
  ex r st for y st y in dom f holds r<f.y;
end;

definition
  let seq;
A1: dom seq = NAT by SEQ_1:3;
  redefine attr seq is bounded_above means
  :Def3:
  ex r st for n holds seq.n<r;
  compatibility
  proof
    thus seq is bounded_above implies ex r st for n holds seq.n<r
    proof
      given r such that
A2:   for y st y in dom seq holds seq.y<r;
      take r;
      let n;
      thus thesis by A1,A2;
    end;
    given r such that
A3: for n holds seq.n<r;
    take r;
    let y;
    assume y in dom seq;
    hence thesis by A1,A3;
  end;
  redefine attr seq is bounded_below means
  :Def4:
  ex r st for n holds r<seq.n;
  compatibility
  proof
    thus seq is bounded_below implies ex r st for n holds r < seq.n
    proof
      given r such that
A4:   for y st y in dom seq holds r < seq.y;
      take r;
      let n;
      thus thesis by A1,A4;
    end;
    given r such that
A5: for n holds r < seq.n;
    take r;
    let y;
    assume y in dom seq;
    hence thesis by A1,A5;
  end;
end;

definition
  let f be complex-valued Function;
  attr f is bounded means
  ex r st for y st y in dom f holds abs(f.y) <r;
end;

registration
  cluster bounded -> bounded_above bounded_below (real-valued Function);
  coherence
  proof
    let f be real-valued Function;
    given r such that
A1: for y st y in dom f holds abs(f.y) <r;
    thus f is bounded_above
    proof
      take r;
      let y;
A2:   f.y <= abs(f.y) by ABSVALUE:11;
      assume y in dom f;
      hence thesis by A1,A2,XXREAL_0:2;
    end;
    take -abs r;
    let y;
A3: -abs(f.y) <= f.y by ABSVALUE:11;
    r <= abs r by ABSVALUE:11;
    then
A4: - abs r <= -r by XREAL_1:26;
    assume y in dom f;
    then abs(f.y) <r by A1;
    then -r < -abs(f.y) by XREAL_1:26;
    then -abs r < -abs(f.y) by A4,XXREAL_0:2;
    hence thesis by A3,XXREAL_0:2;
  end;
  cluster bounded_above bounded_below -> bounded (real-valued Function);
  coherence
  proof
    let f be real-valued Function;
    assume f is bounded_above;
    then consider r1 such that
A5: for y st y in dom f holds f.y<r1 by Def1;
    assume f is bounded_below;
    then consider r2 such that
A6: for y st y in dom f holds r2<f.y by Def2;
    take g=abs(r1)+abs(r2)+1;
A7: 0<=abs(r1) by COMPLEX1:132;
    let y such that
A8: y in dom f;
    r1<=abs(r1) by ABSVALUE:11;
    then f.y<abs(r1) by A5,A8,XXREAL_0:2;
    then f.y +0<abs(r1)+abs(r2) by COMPLEX1:132,XREAL_1:10;
    then
A9: f.y<g by XREAL_1:10;
    -abs(r2)<=r2 by ABSVALUE:11;
    then -abs(r2)<f.y by A6,A8,XXREAL_0:2;
    then -abs(r1)+-abs(r2)<0+f.y by A7,XREAL_1:10;
    then
A10: -abs(r1)-abs(r2)+-1<f.y+0 by XREAL_1:10;
    -abs(r1)-abs(r2)-1=-g;
    hence abs(f.y)<g by A9,A10,Th9;
  end;
end;

canceled 3;

theorem Th15:
  seq is bounded iff ex r st (0<r & for n holds abs(seq.n)<r)
proof
  thus seq is bounded implies ex r st (0<r & for n holds abs(seq.n)<r)
  proof
    assume
A1: seq is bounded;
    then consider r1 such that
A2: for n holds seq.n<r1 by Def3;
    consider r2 such that
A3: for n holds r2<seq.n by A1,Def4;
    take g=abs(r1)+abs(r2)+1;
A4: 0<=abs(r1) by COMPLEX1:132;
    0<=abs(r2) by COMPLEX1:132;
    hence 0<g by A4;
    let n;
    r1<=abs(r1) by ABSVALUE:11;
    then seq.n<abs(r1) by A2,XXREAL_0:2;
    then seq.n +0<abs(r1)+abs(r2) by COMPLEX1:132,XREAL_1:10;
    then
A5: seq.n<g by XREAL_1:10;
    -abs(r2)<=r2 by ABSVALUE:11;
    then -abs(r2)<seq.n by A3,XXREAL_0:2;
    then -abs(r1)+-abs(r2)<0+seq.n by A4,XREAL_1:10;
    then
A6: -abs(r1)-abs(r2)+-1<seq.n+0 by XREAL_1:10;
    -abs(r1)-abs(r2)-1=-g;
    hence thesis by A5,A6,Th9;
  end;
  given r such that 0<r and
A7: for n holds abs(seq.n)<r;
  take r;
  let y;
  assume y in dom seq;
  then y is Element of NAT by FUNCT_2:def 1;
  hence thesis by A7;
end;

theorem Th16:
  for n ex r st (0<r & for m st m<=n holds abs(seq.m)<r)
proof
  defpred X[Nat] means ex r1 st 0<r1 & for m st m<=$1 holds abs(seq.m)<r1;
A1: X[0]
  proof
    reconsider r=abs(seq.0)+1 as real number;
    take r;
    0+0<abs(seq.0)+1 by COMPLEX1:132,XREAL_1:10;
    hence 0<r;
    let m;
    assume m<=0;
    then 0=m by NAT_1:2;
    then abs(seq.m)+0<r by XREAL_1:10;
    hence thesis;
  end;
A2: for n st X[n] holds X[n+1]
  proof
    let n;
    given r1 such that
A3: 0<r1 and
A4: for m st m<=n holds abs(seq.m)<r1;
A5: now
      assume
A6:   abs(seq.(n+1))<=r1;
      take r=r1+1;
      thus 0<r by A3;
      let m such that
A7:   m<=n+1;
A8:   now
        assume m<=n;
        then
A9:     abs(seq.m)<r1 by A4;
        r1+0<r by XREAL_1:10;
        hence abs(seq.m)<r by A9,XXREAL_0:2;
      end;
      now
        assume
A10:    m=n+1;
        r1+0<r by XREAL_1:10;
        hence abs(seq.m)<r by A6,A10,XXREAL_0:2;
      end;
      hence abs(seq.m)<r by A7,A8,NAT_1:8;
    end;
    now
      assume
A11:  r1<=abs(seq.(n+1));
      take r=abs(seq.(n+1))+1;
      0+0<r by COMPLEX1:132,XREAL_1:10;
      hence 0<r;
      let m such that
A12:  m<=n+1;
A13:  now
        assume m<=n;
        then abs(seq.m)<r1 by A4;
        then abs(seq.m)<abs(seq.(n+1)) by A11,XXREAL_0:2;
        then abs(seq.m)+0<r by XREAL_1:10;
        hence abs(seq.m)<r;
      end;
      now
        assume m=n+1;
        then abs(seq.m)+0<r by XREAL_1:10;
        hence abs(seq.m)<r;
      end;
      hence abs(seq.m)<r by A12,A13,NAT_1:8;
    end;
    hence thesis by A5;
  end;
  thus for n holds X[n] from NAT_1:sch 1(A1,A2);
end;
 
::
::          CONVERGENT REAL SEQUENCES
::           THE LIMIT OF SEQUENCES
::

definition
  let seq;
  attr seq is convergent means
  :Def6:
  ex g st for p st 0<p ex n st for m st n<=m holds abs (seq.m-g) < p;
end;

definition
  let seq;
  assume
A1: seq is convergent;
  func lim seq -> real number means
  :Def7:
  for p st 0<p ex n st for m st n<=m holds abs(seq.m-it)<p;
  existence by A1,Def6;
  uniqueness
  proof
    given g1,g2 such that
A2: for p st 0<p ex n st for m st n<=m holds abs(seq.m-g1)<p and
A3: for p st 0<p ex n st for m st n<=m holds abs(seq.m-g2)<p and
A4: g1<>g2;
A5: now
      assume abs(g1-g2)=0;
      then 0+g2=g1-g2+g2 by ABSVALUE:7;
      hence contradiction by A4;
    end;
A6: 0<=abs(g1-g2) by COMPLEX1:132;
    then consider n1 such that
A7: for m st n1<=m holds abs(seq.m-g1)<abs(g1-g2)/4 by A2,A5,XREAL_1:226;
    consider n2 such that
A8: for m st n2<=m holds abs(seq.m-g2)<abs(g1-g2)/4 by A3,A5,A6,XREAL_1:226;
A9: abs(seq.(n1+n2)-g1)<abs(g1-g2)/4 by A7,NAT_1:12;
    abs(seq.(n1+n2)-g2)<abs(g1-g2)/4 by A8,NAT_1:12;
    then
A10: abs(seq.(n1+n2)-g2)+abs(seq.(n1+n2)-g1)<abs(g1-g2)/4+abs(g1-g2)/4
    by A9,XREAL_1:10;
    abs(g1-g2)=abs(-(seq.(n1+n2)-g1)+(seq.(n1+n2)-g2));
    then abs(g1-g2)<=abs(-(seq.(n1+n2)-g1))+abs(seq.(n1+n2)-g2) by
    COMPLEX1:142;
    then
A11: abs(g1-g2)<=abs(seq.(n1+n2)-g1)+abs(seq.(n1+n2)-g2) by
    COMPLEX1:138;
    abs(g1-g2)/2 <abs(g1-g2) by A5,A6,XREAL_1:218;
    hence contradiction by A10,A11,XXREAL_0:2;
  end;
end;

definition
  let f be real-valued Function;
  redefine attr f is bounded means
  f is bounded_above bounded_below;
  compatibility;
end;

definition
  let seq;
  redefine func lim seq -> Real;
  coherence by XREAL_0:def 1;
end;

canceled 2;

theorem Th19:
  seq is convergent & seq9 is convergent implies seq + seq9 is convergent
proof
  assume that
A1: seq is convergent and
A2: seq9 is convergent;
  consider g1 such that
A3: for p st 0<p ex n st for m st n<=m holds abs(seq.m-g1)<p by A1,Def6;
  consider g2 such that
A4: for p st 0<p ex n st for m st n<=m holds abs(seq9.m-g2)<p by A2,Def6;
  take g=g1+g2;
  let p;
  assume
A5: 0<p;
  then consider n1 such that
A6: for m st n1<=m holds abs(seq.m-g1)<p/2 by A3,XREAL_1:217;
  consider n2 such that
A7: for m st n2<=m holds abs(seq9.m-g2)<p/2 by A4,A5,XREAL_1:217;
  take k=n1+n2;
  let m such that
A8: k<=m;
  n1<=n1+n2 by NAT_1:12;
  then n1<=m by A8,XXREAL_0:2;
  then
A9: abs(seq.m-g1)<p/2 by A6;
  n2<=k by NAT_1:12;
  then n2<=m by A8,XXREAL_0:2;
  then abs(seq9.m-g2)<p/2 by A7;
  then
A10: abs(seq.m-g1)+abs(seq9.m-g2)<p/2+p/2 by A9,XREAL_1:10;
A11: abs((seq+seq9).m-g)=abs(seq.m+seq9.m-(g1+g2)) by SEQ_1:11
    .=abs(seq.m-g1+(seq9.m-g2));
  abs(seq.m-g1+(seq9.m-g2))<=abs(seq.m-g1)+abs(seq9.m-g2) by COMPLEX1:142;
  hence thesis by A10,A11,XXREAL_0:2;
end;

theorem Th20:
  seq is convergent & seq9 is convergent implies
  lim (seq + seq9)=(lim seq)+(lim seq9)
proof
  assume that
A1: seq is convergent and
A2: seq9 is convergent;
A3: seq+seq9 is convergent by A1,A2,Th19;
  now
    let p;
    assume 0<p;
    then
A4: 0<p/2 by XREAL_1:217;
    then consider n1 such that
A5: for m st n1<=m holds abs(seq.m-(lim seq))<p/2 by A1,Def7;
    consider n2 such that
A6: for m st n2<=m holds abs(seq9.m-(lim seq9))<p/2 by A2,A4,Def7;
    take k=n1+n2;
    let m such that
A7: k<=m;
    n1<=n1+n2 by NAT_1:12;
    then n1<=m by A7,XXREAL_0:2;
    then
A8: abs(seq.m-(lim seq))<p/2 by A5;
    n2<=k by NAT_1:12;
    then n2<=m by A7,XXREAL_0:2;
    then abs(seq9.m-(lim seq9))<p/2 by A6;
    then
A9: abs(seq.m-(lim seq))+abs(seq9.m-(lim seq9))<p/2+p/2 by A8,
    XREAL_1:10;
A10: abs(((seq+seq9).m)-((lim seq)+(lim seq9)))
    =abs(seq.m+seq9.m-((lim seq)+(lim seq9))) by SEQ_1:11
      .=abs(seq.m-(lim seq)+(seq9.m-(lim seq9)));
    abs(seq.m-(lim seq)+(seq9.m-(lim seq9)))<=
    abs(seq.m-(lim seq))+abs(seq9.m-(lim seq9)) by COMPLEX1:142;
    hence abs((seq+seq9).m-((lim seq)+(lim seq9)))<p by A9,A10,
    XXREAL_0:2;
  end;
  hence thesis by A3,Def7;
end;

theorem Th21:
  seq is convergent implies r(#)seq is convergent
proof
  assume seq is convergent;
  then consider g1 such that
A1: for p st 0<p ex n st for m st n<=m holds abs(seq.m-g1)<p by Def6;
  take g=r*g1;
A2: now
    assume
A3: r=0;
    let p such that
A4: 0<p;
    take k=0;
    let m such that k<=m;
    abs(((r(#)seq).m)-g)=abs(0*seq.m-0) by A3,SEQ_1:13
      .=0 by ABSVALUE:7;
    hence abs(((r(#)seq).m)-g)<p by A4;
  end;
  now
    assume
A5: r<>0;
    then
A6: 0<abs(r) by COMPLEX1:133;
    let p such that
A7: 0<p;
A8: 0<>abs(r) by A5,COMPLEX1:133;
    0/abs(r)=0;
    then consider n1 such that
A9: for m st n1<=m holds abs(seq.m-g1)<p/abs(r) by A1,A6,A7,XREAL_1:76;
    take k=n1;
    let m;
    assume k<=m;
    then
A10: abs(seq.m-g1)<p/abs(r) by A9;
A11: abs(((r(#)seq).m)-g)=abs(r*seq.m-r*g1) by SEQ_1:13
      .=abs(r*(seq.m-g1))
      .=abs(r)*abs(seq.m-g1) by COMPLEX1:151;
    abs(r)*(p/abs(r))=abs(r)*((abs(r))"*p) by XCMPLX_0:def 9
      .=abs(r)*(abs(r))"*p
      .=1*p by A8,XCMPLX_0:def 7
      .=p;
    hence abs(((r(#)seq).m)-g)<p by A6,A10,A11,XREAL_1:70;
  end;
  hence thesis by A2;
end;

theorem Th22:
  seq is convergent implies lim(r(#)seq)=r*(lim seq)
proof
  assume
A1: seq is convergent;
  then
A2: r(#)seq is convergent by Th21;
A3: now
    assume
A4: r=0;
    let p such that
A5: 0<p;
    take k=0;
    let m such that k<=m;
    abs(((r(#)seq).m)-r*(lim seq))=abs(0*seq.m-0) by A4,SEQ_1:13
      .=0 by ABSVALUE:7;
    hence abs(((r(#)seq).m)-r*(lim seq))<p by A5;
  end;
  now
    assume
A6: r<>0;
    then
A7: 0<abs(r) by COMPLEX1:133;
    let p such that
A8: 0<p;
A9: 0<>abs(r) by A6,COMPLEX1:133;
    0/abs(r)=0;
    then 0<p/abs(r) by A7,A8,XREAL_1:76;
    then consider n1 such that
A10: for m st n1<=m holds abs(seq.m-(lim seq))<p/abs(r) by A1,Def7;
    take k=n1;
    let m;
    assume k<=m;
    then
A11: abs(seq.m-(lim seq))<p/abs(r) by A10;
A12: abs(((r(#)seq).m)-r*(lim seq))=abs(r*seq.m-r*(lim seq)) by SEQ_1:13
      .=abs(r*(seq.m-(lim seq)))
      .=abs(r)*abs(seq.m-(lim seq)) by COMPLEX1:151;
    abs(r)*(p/abs(r))=abs(r)*((abs(r))"*p) by XCMPLX_0:def 9
      .=abs(r)*(abs(r))"*p
      .=1*p by A9,XCMPLX_0:def 7
      .=p;
    hence abs(((r(#)seq).m)-r*(lim seq))<p by A7,A11,A12,XREAL_1:70;
  end;
  hence thesis by A2,A3,Def7;
end;

theorem
  seq is convergent implies - seq is convergent by Th21;

theorem Th24:
  seq is convergent implies lim(-seq) = -(lim seq)
proof
  assume seq is convergent;
  then lim ((-1)(#)seq)=(-1)*(lim seq) by Th22;
  hence thesis;
end;

theorem Th25:
  seq is convergent & seq9 is convergent implies seq - seq9 is convergent
proof
  assume that
A1: seq is convergent and
A2: seq9 is convergent;
  -seq9 is convergent by A2,Th21;
  hence thesis by A1,Th19;
end;

theorem Th26:
  seq is convergent & seq9 is convergent implies
  lim(seq - seq9)=(lim seq)-(lim seq9)
proof
  assume that
A1: seq is convergent and
A2: seq9 is convergent;
  - seq9 is convergent by A2,Th21;
  hence lim(seq - seq9)=(lim seq)+(lim(- seq9)) by A1,Th20
    .=(lim seq)+-(lim seq9) by A2,Th24
    .=(lim seq)-(lim seq9);
end;

theorem Th27:
  seq is convergent implies seq is bounded
proof
  assume seq is convergent;
  then consider g such that
A1: for p st 0<p ex n st for m st n<=m holds abs(seq.m-g)<p by Def6;
  consider n1 such that
A2: for m st n1<=m holds abs(seq.m-g)<1 by A1;
A3: now
    take r=abs(g)+1;
    0+0<r by COMPLEX1:132,XREAL_1:10;
    hence 0<r;
    let m;
    assume n1<=m;
    then
A4: abs(seq.m-g)<1 by A2;
    abs(seq.m)-abs(g)<=abs(seq.m-g) by COMPLEX1:145;
    then
A5: abs(seq.m)-abs(g)<1 by A4,XXREAL_0:2;
    abs(seq.m)-abs(g)+abs(g)=abs(seq.m);
    hence abs(seq.m)<r by A5,XREAL_1:8;
  end;
  now consider r1 such that
A6: 0<r1 and
A7: for m st n1<=m holds abs(seq.m)<r1 by A3;
    consider r2 such that
A8: 0<r2 and
A9: for m st m<=n1 holds abs(seq.m)<r2 by Th16;
    take r=r1+r2;
    thus 0<r by A6,A8;
A10: r1+0<r by A8,XREAL_1:10;
A11: 0+r2<r by A6,XREAL_1:10;
    let m;
A12: now
      assume n1<=m;
      then abs(seq.m)<r1 by A7;
      hence abs(seq.m)<r by A10,XXREAL_0:2;
    end;
    now
      assume m<=n1;
      then abs(seq.m)<r2 by A9;
      hence abs(seq.m)<r by A11,XXREAL_0:2;
    end;
    hence abs(seq.m)<r by A12;
  end;
  hence thesis by Th15;
end;

theorem Th28:
  seq is convergent & seq9 is convergent implies seq (#) seq9 is convergent
proof
  assume that
A1: seq is convergent and
A2: seq9 is convergent;
  consider g1 such that
A3: for p st 0<p ex n st for m st n<=m holds abs(seq.m-g1)<p by A1,Def6;
  consider g2 such that
A4: for p st 0<p ex n st for m st n<=m holds abs(seq9.m-g2)<p by A2,Def6;
  take g=g1*g2;
  seq is bounded by A1,Th27;
  then consider r such that
A5: 0<r and
A6: for n holds abs(seq.n)<r by Th15;
A7: 0<=abs(g2) by COMPLEX1:132;
A8: 0+0<abs(g2)+r by A5,COMPLEX1:132,XREAL_1:10;
  let p;
  assume
A9: 0<p;
  then consider n1 such that
A10: for m st n1<=m holds abs(seq.m-g1)<p/(abs(g2)+r) by A3,A8,XREAL_1:141;
  consider n2 such that
A11: for m st n2<=m holds abs(seq9.m-g2)<p/(abs(g2)+r) by A4,A8,A9,XREAL_1:141;
  take n=n1+n2;
  let m such that
A12: n<=m;
A13: 0<=abs(seq.m) by COMPLEX1:132;
A14: 0<=abs(seq9.m-g2) by COMPLEX1:132;
  n2<=n by NAT_1:12;
  then n2<=m by A12,XXREAL_0:2;
  then
A15: abs(seq9.m-g2)<p/(abs(g2)+r) by A11;
  n1<=n1+n2 by NAT_1:12;
  then n1<=m by A12,XXREAL_0:2;
  then
A16: abs(seq.m-g1)<=p/(abs(g2)+r) by A10;
  abs(((seq(#)seq9).m)-g)=abs(seq.m*seq9.m-(seq.m*g2-seq.m*g2)-g1*g2) by
  SEQ_1:12
    .=abs(seq.m*(seq9.m-g2)+(seq.m-g1)*g2);
  then
A17: abs(((seq(#)seq9).m)-g)<=
  abs(seq.m*(seq9.m-g2))+abs((seq.m-g1)*g2) by COMPLEX1:142;
  abs(seq.m)<r by A6;
  then abs(seq.m)*abs(seq9.m-g2)<r*(p/(abs(g2)+r)) by A13,A14,A15,
  XREAL_1:98;
  then
A18: abs(seq.m*(seq9.m-g2))<r*(p/(abs(g2)+r)) by COMPLEX1:151;
  abs((seq.m-g1)*g2)=abs(g2)*abs(seq.m-g1) by COMPLEX1:151;
  then
A19: abs((seq.m-g1)*g2)<=abs(g2)*(p/(abs(g2)+r)) by A7,A16,XREAL_1:66;
  r*(p/(abs(g2)+r))+abs(g2)*(p/(abs(g2)+r)) =(p/(abs(g2)+r))*(abs(g2)+r)
    .=p by A8,XCMPLX_1:88;
  then abs(seq.m*(seq9.m-g2))+abs((seq.m-g1)*g2)<p by A18,A19,XREAL_1:10;
  hence thesis by A17,XXREAL_0:2;
end;

theorem Th29:
  seq is convergent & seq9 is convergent implies
  lim(seq(#)seq9)=(lim seq)*(lim seq9)
proof
  assume that
A1: seq is convergent and
A2: seq9 is convergent;
A3: seq(#)seq9 is convergent by A1,A2,Th28;
  seq is bounded by A1,Th27;
  then consider r such that
A4: 0<r and
A5: for n holds abs(seq.n)<r by Th15;
A6: 0<=abs((lim seq9)) by COMPLEX1:132;
A7: 0+0<abs((lim seq9))+r by A4,COMPLEX1:132,XREAL_1:10;
  now
    let p;
    assume 0<p;
    then
A8: 0<p/(abs((lim seq9))+r) by A7,XREAL_1:141;
    then consider n1 such that
A9: for m st n1<=m holds abs(seq.m-(lim seq))<p/(abs((lim seq9))+r) by A1,Def7;
    consider n2 such that
A10: for m st n2<=m holds abs(seq9.m-(lim seq9))<p/(abs((lim seq9))+r)
    by A2,A8,Def7;
    take n=n1+n2;
    let m such that
A11: n<=m;
A12: 0<=abs(seq.m) by COMPLEX1:132;
A13: 0<=abs(seq9.m-(lim seq9)) by COMPLEX1:132;
    n2<=n by NAT_1:12;
    then n2<=m by A11,XXREAL_0:2;
    then
A14: abs(seq9.m-(lim seq9))<p/(abs((lim seq9))+r) by A10;
    n1<=n1+n2 by NAT_1:12;
    then n1<=m by A11,XXREAL_0:2;
    then
A15: abs(seq.m-(lim seq))<=p/(abs((lim seq9))+r) by A9;
    abs(((seq(#)seq9).m)-(lim seq)*(lim seq9))
    =abs(seq.m*seq9.m-(seq.m*(lim seq9)-seq.m*(lim seq9))-
    (lim seq)*(lim seq9)) by SEQ_1:12
      .=abs(seq.m*(seq9.m-(lim seq9))+
    (seq.m-(lim seq))*(lim seq9));
    then
A16: abs(((seq(#)seq9).m)-(lim seq)*(lim seq9))<=
    abs(seq.m*(seq9.m-(lim seq9)))+
    abs((seq.m-(lim seq))*(lim seq9)) by COMPLEX1:142;
    abs(seq.m)<r by A5;
    then abs(seq.m)*abs(seq9.m-(lim seq9))<r*(p/(abs((lim seq9))+r))
    by A12,A13,A14,XREAL_1:98;
    then
A17: abs(seq.m*(seq9.m-(lim seq9)))<r*(p/(abs((lim seq9))+r)) by
    COMPLEX1:151;
    abs((seq.m-(lim seq))*(lim seq9))
    =abs((lim seq9))*abs(seq.m-(lim seq)) by COMPLEX1:151;
    then
A18: abs((seq.m-(lim seq))*(lim seq9))<=abs((lim seq9))*
    (p/(abs((lim seq9))+r)) by A6,A15,XREAL_1:66;
    r*(p/(abs((lim seq9))+r))+abs((lim seq9))*(p/(abs((lim seq9))+r))
    =(p/(abs((lim seq9))+r))*(abs((lim seq9))+r)
      .=p by A7,XCMPLX_1:88;
    then abs(seq.m*(seq9.m-(lim seq9)))+abs((seq.m-(lim seq))*
    (lim seq9))<p by A17,A18,XREAL_1:10;
    hence abs(((seq(#)seq9).m)-(lim seq)*(lim seq9))<p by A16,
    XXREAL_0:2;
  end;
  hence thesis by A3,Def7;
end;

theorem Th30:
  seq is convergent implies (lim seq<>0 implies
  ex n st for m st n<=m holds abs(lim seq)/2<abs(seq.m))
proof
  assume that
A1: seq is convergent and
A2: lim seq<>0;
  0<abs(lim seq) by A2,COMPLEX1:133;
  then 0<abs(lim seq)/2 by XREAL_1:217;
  then consider n1 such that
A3: for m st n1<=m holds abs(seq.m-(lim seq))<abs(lim seq)/2 by A1,Def7;
  take n=n1;
  let m;
  assume n<=m;
  then
A4: abs(seq.m-(lim seq))<abs(lim seq)/2 by A3;
A5: abs(lim seq)-abs(seq.m)<=abs((lim seq)-seq.m) by COMPLEX1:145;
  abs((lim seq)-seq.m)=abs(-(seq.m-(lim seq)))
    .=abs(seq.m-(lim seq)) by COMPLEX1:138;
  then
A6: abs(lim seq)-abs(seq.m)<abs(lim seq)/2 by A4,A5,XXREAL_0:2;
A7: abs(lim seq)/2+(abs(seq.m)-abs(lim seq)/2) = abs(seq.m);
  abs(lim seq)-abs(seq.m)+(abs(seq.m)-abs(lim seq)/2) = abs(lim seq)/2;
  hence thesis by A6,A7,XREAL_1:8;
end;

theorem Th31:
  seq is convergent & (for n holds 0<=seq.n) implies 0<=lim seq
proof
  assume that
A1: seq is convergent and
A2: for n holds 0<=seq.n and
A3: not 0<=(lim seq);
  0<-(lim seq) by A3,XREAL_1:60;
  then consider n1 such that
A4: for m st n1<=m holds abs(seq.m-(lim seq))<-(lim seq) by A1,Def7;
  abs(seq.n1-(lim seq))<=-(lim seq) by A4;
  then seq.n1-(lim seq)<=-(lim seq) by ABSVALUE:12;
  then
A5: seq.n1-(lim seq)+(lim seq)<=-(lim seq)+(lim seq) by XREAL_1:8;
  now
    assume seq.n1=0;
    then abs(seq.n1-(lim seq))=-(lim seq) by A3,ABSVALUE:def 1;
    hence contradiction by A4;
  end;
  hence contradiction by A2,A5;
end;

theorem
  seq is convergent & seq9 is convergent &
  (for n holds seq.n<=(seq9.n)) implies (lim seq)<=(lim seq9)
proof
  assume that
A1: seq is convergent and
A2: seq9 is convergent and
A3: for n holds seq.n<=(seq9.n);
A4: seq9-seq is convergent by A1,A2,Th25;
  now
    let n;
    seq.n<=seq9.n by A3;
    then
A5: seq.n-seq.n<=seq9.n-seq.n by XREAL_1:11;
    (seq9-seq).n=seq9.n+(-seq).n by SEQ_1:11
      .=seq9.n+-seq.n by SEQ_1:14
      .=seq9.n-seq.n;
    hence 0<=(seq9-seq).n by A5;
  end;
  then
A6: 0<=lim(seq9-seq) by A4,Th31;
  lim (seq9-seq)=(lim seq9)- (lim seq) by A1,A2,Th26;
  then 0+(lim seq)<=(lim seq9)-(lim seq)+(lim seq) by A6,XREAL_1:8;
  hence thesis;
end;

theorem Th33:
  seq is convergent & seq9 is convergent &
  (for n holds seq.n<=(seq1.n) & seq1.n<=seq9.n) & (lim seq)=(lim seq9)
  implies seq1 is convergent
proof
  assume that
A1: seq is convergent and
A2: seq9 is convergent and
A3: for n holds seq.n<=(seq1.n) & seq1.n<=seq9.n and
A4: (lim seq)=(lim seq9);
  take lim seq;
  let p;
  assume
A5: 0<p;
  then consider n1 such that
A6: for m st n1<=m holds abs(seq.m-(lim seq))<p by A1,Def7;
  consider n2 such that
A7: for m st n2<=m holds abs(seq9.m-(lim seq))<p by A2,A4,A5,Def7;
  take n=n1+n2;
  let m such that
A8: n<=m;
  n2<=n by NAT_1:12;
  then n2<=m by A8,XXREAL_0:2;
  then abs(seq9.m-(lim seq))<p by A7;
  then
A9: seq9.m-(lim seq)<p by Th9;
  n1<=n1+n2 by NAT_1:12;
  then n1<=m by A8,XXREAL_0:2;
  then abs(seq.m-(lim seq))<p by A6;
  then
A10: -p<seq.m-(lim seq) by Th9;
  seq.m<=seq1.m by A3;
  then seq.m-(lim seq)<=seq1.m-(lim seq) by XREAL_1:11;
  then
A11: -p<seq1.m-(lim seq) by A10,XXREAL_0:2;
  seq1.m<=seq9.m by A3;
  then seq1.m-(lim seq)<=seq9.m-(lim seq) by XREAL_1:11;
  then seq1.m-(lim seq)<p by A9,XXREAL_0:2;
  hence thesis by A11,Th9;
end;

theorem
  seq is convergent & seq9 is convergent &
  (for n holds seq.n<=(seq1.n) & seq1.n<=seq9.n) &
  (lim seq)=(lim seq9) implies (lim seq1)=(lim seq)
proof
  assume that
A1: seq is convergent and
A2: seq9 is convergent and
A3: for n holds seq.n<=(seq1.n) & seq1.n<=seq9.n and
A4: (lim seq)=(lim seq9);
A5: seq1 is convergent by A1,A2,A3,A4,Th33;
  now
    let p;
    assume
A6: 0<p;
    then consider n1 such that
A7: for m st n1<=m holds abs(seq.m-(lim seq))<p by A1,Def7;
    consider n2 such that
A8: for m st n2<=m holds abs(seq9.m-(lim seq))<p by A2,A4,A6,Def7;
    take n=n1+n2;
    let m such that
A9: n<=m;
    n2<=n by NAT_1:12;
    then n2<=m by A9,XXREAL_0:2;
    then abs(seq9.m-(lim seq))<p by A8;
    then
A10: seq9.m-(lim seq)<p by Th9;
    n1<=n1+n2 by NAT_1:12;
    then n1<=m by A9,XXREAL_0:2;
    then abs(seq.m-(lim seq))<p by A7;
    then
A11: -p<seq.m-(lim seq) by Th9;
    seq.m<=seq1.m by A3;
    then seq.m-(lim seq)<=seq1.m-(lim seq) by XREAL_1:11;
    then
A12: -p<seq1.m-(lim seq) by A11,XXREAL_0:2;
    seq1.m<=seq9.m by A3;
    then seq1.m-(lim seq)<=seq9.m-(lim seq) by XREAL_1:11;
    then seq1.m-(lim seq)<p by A10,XXREAL_0:2;
    hence abs(seq1.m-(lim seq))<p by A12,Th9;
  end;
  hence thesis by A5,Def7;
end;

theorem Th35:
  seq is convergent & (lim seq)<>0 & seq is non-zero implies seq" is convergent
proof
  assume that
A1: seq is convergent and
A2: (lim seq)<>0 and
A3: seq is non-zero;
A4: 0<abs(lim seq) by A2,COMPLEX1:133;
A5: 0<>abs(lim seq) by A2,COMPLEX1:133;
  consider n1 such that
A6: for m st n1<=m holds abs(lim seq)/2<abs(seq.m) by A1,A2,Th30;
  0*0<abs(lim seq)*abs(lim seq) by A4,XREAL_1:98;
  then
A7: 0<(abs(lim seq)*abs(lim seq))/2 by XREAL_1:217;
  take (lim seq)";
  let p;
  assume
A8: 0<p;
  then 0*0<p*((abs(lim seq)*abs(lim seq))/2) by A7,XREAL_1:98;
  then consider n2 such that
A9: for m st n2<=m holds
  abs(seq.m-(lim seq))<p*((abs(lim seq)*abs(lim seq))/2) by A1,Def7;
  take n=n1+n2;
  let m such that
A10: n<=m;
  n2<=n by NAT_1:12;
  then n2<=m by A10,XXREAL_0:2;
  then
A11: abs(seq.m-(lim seq))<p*((abs(lim seq)*abs(lim seq))/2) by A9;
  n1<=n1+n2 by NAT_1:12;
  then n1<=m by A10,XXREAL_0:2;
  then
A12: abs(lim seq)/2<abs(seq.m) by A6;
A13: seq.m<>0 by A3,SEQ_1:7;
  then seq.m*(lim seq)<>0 by A2,XCMPLX_1:6;
  then 0<abs(seq.m*(lim seq)) by COMPLEX1:133;
  then 0<abs(seq.m)*abs(lim seq) by COMPLEX1:151;
  then
A14: abs(seq.m-(lim seq))/(abs(seq.m)*abs(lim seq))<
  (p*((abs(lim seq)*abs(lim seq))/2))/(abs(seq.m)*abs(lim seq))
  by A11,XREAL_1:76;
A15: (p*((abs(lim seq)*abs(lim seq))/2))/(abs(seq.m)*abs(lim seq))
  =(p*(2"*(abs(lim seq)*abs(lim seq))))*
  (abs(seq.m)*abs(lim seq))" by XCMPLX_0:def 9
    .=p*2"*((abs(lim seq)*abs(lim seq))*(abs(lim seq)*abs(seq.m))")
    .=p*2"*((abs(lim seq)*abs(lim seq))*
  (abs(lim seq)"*(abs(seq.m))")) by XCMPLX_1:205
    .=p*2"*(abs(lim seq)*(abs(lim seq)*abs(lim seq)")*abs(seq.m)")
    .=p*2"*(abs(lim seq)*1*(abs(seq.m))") by A5,XCMPLX_0:def 7
    .=p*(abs(lim seq)/2)*abs(seq.m)"
    .=(p*(abs(lim seq)/2))/abs(seq.m) by XCMPLX_0:def 9;
A16: abs((seq").m-(lim seq)")=abs((seq.m)"-(lim seq)") by VALUED_1:10
    .=abs(seq.m-(lim seq))/(abs(seq.m)*abs(lim seq)) by A2,A13,Th11;
A17: 0<abs(lim seq)/2 by A4,XREAL_1:217;
A18: 0<>abs(lim seq)/2 by A2,COMPLEX1:133;
  0*0<p*(abs(lim seq)/2) by A8,A17,XREAL_1:98;
  then
A19: (p*(abs(lim seq)/2))/abs(seq.m)<
  (p*(abs(lim seq)/2))/(abs(lim seq)/2) by A12,A17,XREAL_1:78;
  (p*(abs(lim seq)/2))/(abs(lim seq)/2 )
  =(p*(abs(lim seq)/2))*(abs(lim seq)/2 )" by XCMPLX_0:def 9
    .=p*((abs(lim seq)/2)*(abs(lim seq)/2 )")
    .=p*1 by A18,XCMPLX_0:def 7
    .=p;
  hence thesis by A14,A15,A16,A19,XXREAL_0:2;
end;

theorem Th36:
  seq is convergent & (lim seq)<>0 & seq is non-zero
  implies lim seq"=(lim seq)"
proof
  assume that
A1: seq is convergent and
A2: (lim seq)<>0 and
A3: seq is non-zero;
A4: seq" is convergent by A1,A2,A3,Th35;
A5: 0<abs(lim seq) by A2,COMPLEX1:133;
A6: 0<>abs(lim seq) by A2,COMPLEX1:133;
  consider n1 such that
A7: for m st n1<=m holds abs(lim seq)/2<abs(seq.m) by A1,A2,Th30;
  0*0<abs(lim seq)*abs(lim seq) by A5,XREAL_1:98;
  then
A8: 0<(abs(lim seq)*abs(lim seq))/2 by XREAL_1:217;
  now
    let p;
    assume
A9: 0<p;
    then 0*0<p*((abs(lim seq)*abs(lim seq))/2) by A8,XREAL_1:98;
    then consider n2 such that
A10: for m st n2<=m holds
    abs(seq.m-(lim seq))<p*((abs(lim seq)*abs(lim seq))/2) by A1,Def7;
    take n=n1+n2;
    let m such that
A11: n<=m;
    n2<=n by NAT_1:12;
    then n2<=m by A11,XXREAL_0:2;
    then
A12: abs(seq.m-(lim seq))<p*((abs(lim seq)*abs(lim seq))/2) by A10;
    n1<=n1+n2 by NAT_1:12;
    then n1<=m by A11,XXREAL_0:2;
    then
A13: abs(lim seq)/2<abs(seq.m) by A7;
A14: seq.m<>0 by A3,SEQ_1:7;
    then seq.m*(lim seq)<>0 by A2,XCMPLX_1:6;
    then 0<abs(seq.m*(lim seq)) by COMPLEX1:133;
    then 0<abs(seq.m)*abs(lim seq) by COMPLEX1:151;
    then
A15: abs(seq.m-(lim seq))/(abs(seq.m)*abs(lim seq))<
    (p*((abs(lim seq)*abs(lim seq))/2))/(abs(seq.m)*abs(lim seq))
    by A12,XREAL_1:76;
A16: (p*((abs(lim seq)*abs(lim seq))/2))/(abs(seq.m)*abs(lim seq))
    =(p*(2"*(abs(lim seq)*abs(lim seq))))*
    (abs(seq.m)*abs(lim seq))" by XCMPLX_0:def 9
      .=p*2"*((abs(lim seq)*abs(lim seq))*(abs(lim seq)*abs(seq.m))")
      .=p*2"*((abs(lim seq)*abs(lim seq))*
    ((abs(lim seq))"*(abs(seq.m))")) by XCMPLX_1:205
      .=p*2"*(abs(lim seq)*(abs(lim seq)*abs(lim seq)")*abs(seq.m)")
      .=p*2"*(abs(lim seq)*1*abs(seq.m)") by A6,XCMPLX_0:def 7
      .=p*(abs(lim seq)/2)*abs(seq.m)"
      .=(p*(abs(lim seq)/2))/abs(seq.m) by XCMPLX_0:def 9;
A17: abs((seq").m-(lim seq)")=abs((seq.m)"-(lim seq)") by VALUED_1:10
      .=abs(seq.m-(lim seq))/(abs(seq.m)*abs(lim seq)) by A2,A14,Th11;
A18: 0<abs(lim seq)/2 by A5,XREAL_1:217;
A19: 0<>abs(lim seq)/2 by A2,COMPLEX1:133;
    0*0<p*(abs(lim seq)/2) by A9,A18,XREAL_1:98;
    then
A20: (p*(abs(lim seq)/2))/abs(seq.m)<
    (p*(abs(lim seq)/2))/(abs(lim seq)/2) by A13,A18,XREAL_1:78;
    (p*(abs(lim seq)/2))/(abs(lim seq)/2 )
    =(p*(abs(lim seq)/2))*(abs(lim seq)/2 )" by XCMPLX_0:def 9
      .=p*((abs(lim seq)/2)*(abs(lim seq)/2 )")
      .=p*1 by A19,XCMPLX_0:def 7
      .=p;
    hence abs((seq").m-(lim seq)")<p by A15,A16,A17,A20,XXREAL_0:2;
  end;
  hence thesis by A4,Def7;
end;

theorem
  seq9 is convergent & seq is convergent & (lim seq)<>0
  & seq is non-zero implies seq9/"seq is convergent
proof
  assume that
A1: seq9 is convergent and
A2: seq is convergent and
A3: (lim seq)<>0 and
A4: seq is non-zero;
  seq" is convergent by A2,A3,A4,Th35;
  hence thesis by A1,Th28;
end;

theorem
  seq9 is convergent & seq is convergent & (lim seq)<>0
  & seq is non-zero implies lim(seq9/"seq)=(lim seq9)/(lim seq)
proof
  assume that
A1: seq9 is convergent and
A2: seq is convergent and
A3: (lim seq)<>0 and
A4: seq is non-zero;
  seq" is convergent by A2,A3,A4,Th35;
  then lim (seq9(#)(seq"))=(lim seq9)*(lim seq") by A1,Th29
    .=(lim seq9)*(lim seq)" by A2,A3,A4,Th36
    .=(lim seq9)/(lim seq) by XCMPLX_0:def 9;
  hence thesis;
end;

theorem Th39:
  seq is convergent & seq1 is bounded & lim seq=0 implies
  seq(#)seq1 is convergent
proof
  assume that
A1: seq is convergent and
A2: seq1 is bounded and
A3: lim seq=0;
  reconsider g1=0 as Real;
  take g=g1;
  let p such that
A4: 0<p;
  consider r such that
A5: 0<r and
A6: for m holds abs(seq1.m)<r by A2,Th15;
A7: 0<p/r by A4,A5,XREAL_1:141;
  then consider n1 such that
A8: for m st n1<=m holds abs(seq.m-0)<p/r by A1,A3,Def7;
  take n=n1;
  let m such that
A9: n<=m;
  abs(seq.m)=abs(seq.m-0);
  then
A10: abs(seq.m)<p/r by A8,A9;
A11: abs(((seq(#)seq1).m)-g)=abs(seq.m*seq1.m-0) by SEQ_1:12
    .=abs(seq.m)*abs(seq1.m) by COMPLEX1:151;
  now
    assume
A12: abs(seq1.m)<>0;
    (p/r)*r=p*r"*r by XCMPLX_0:def 9
      .=p*(r"*r)
      .=p*1 by A5,XCMPLX_0:def 7
      .=p;
    then
A13: (p/r)*abs(seq1.m)<p by A6,A7,XREAL_1:70;
    0<=abs(seq1.m) by COMPLEX1:132;
    then abs(((seq(#)seq1).m)-g)<(p/r)*abs(seq1.m) by A10,A11,A12,
    XREAL_1:70;
    hence thesis by A13,XXREAL_0:2;
  end;
  hence thesis by A4,A11;
end;

theorem
  seq is convergent & seq1 is bounded & lim seq=0 implies lim(seq(#)seq1)=0
proof
  assume that
A1: seq is convergent and
A2: seq1 is bounded and
A3: lim seq=0;
A4: seq(#)seq1 is convergent by A1,A2,A3,Th39;
  now
    let p such that
A5: 0<p;
    consider r such that
A6: 0<r and
A7: for m holds abs(seq1.m)<r by A2,Th15;
A8: 0<p/r by A5,A6,XREAL_1:141;
    then consider n1 such that
A9: for m st n1<=m holds abs(seq.m-0)<p/r by A1,A3,Def7;
    take n=n1;
    let m;
    assume n<=m;
    then
A10: abs(seq.m-0)<p/r by A9;
A11: abs(((seq(#)seq1).m)-0)=abs(seq.m*seq1.m-0) by SEQ_1:12
      .=abs(seq.m)*abs(seq1.m) by COMPLEX1:151;
    now
      assume
A12:  abs(seq1.m)<>0;
      (p/r)*r=p*r"*r by XCMPLX_0:def 9
        .=p*(r"*r)
        .=p*1 by A6,XCMPLX_0:def 7;
      then
A13:  (p/r)*abs(seq1.m)<p by A7,A8,XREAL_1:70;
      0<=abs(seq1.m) by COMPLEX1:132;
      then abs(((seq(#)seq1).m)-0)<(p/r)*abs(seq1.m) by A10,A11,A12,
      XREAL_1:70;
      hence abs(((seq(#)seq1).m)-0)<p by A13,XXREAL_0:2;
    end;
    hence abs(((seq(#)seq1).m)-0)<p by A5,A11;
  end;
  hence thesis by A4,Def7;
end;
