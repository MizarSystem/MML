:: Partial Differentiation on Normed Linear Spaces $ {\cal R}^n$
::  by Noboru Endou , Yasunari Shidama and Keiichi Miyajima
:: 
:: Received June 6, 2007
:: Copyright (c) 2007 Association of Mizar Users

environ

 vocabularies PRE_TOPC, RLVECT_1, FUNCT_1, ARYTM, ARYTM_1, FINSEQ_1, FINSEQ_2,
      FINSEQ_7, RVSUM_1, RELAT_1, COMPLEX1, ABSVALUE, LATTICES, SQUARE_1,
      ORDINAL2, SEQ_1, SEQ_2, RCOMP_1, BOOLE, FINSEQ_4, PARTFUN1, NORMSP_1,
      LOPBAN_1, FDIFF_1, PDIFF_1, REAL_NS1, SGRAPH1, SUPINF_1, BORSUK_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, RELSET_1, PARTFUN1,
      FUNCT_2, RFUNCT_2, PRE_TOPC, STRUCT_0, PARTFUN2, XCMPLX_0, XREAL_0,
      COMPLEX1, ORDINAL1, NUMBERS, REAL_1, FINSEQ_4, RLVECT_1, SEQ_1, SEQ_2,
      RVSUM_1, FINSEQ_1, FINSEQ_2, FINSEQ_7, SQUARE_1, RCOMP_1, VFUNCT_1,
      NORMSP_1, EUCLID, LOPBAN_1, NFCONT_1, FDIFF_1, NDIFF_1, REAL_NS1,
      XXREAL_0, EXTREAL1, SUPINF_1;
 constructors REAL_1, SQUARE_1, RSSPACE3, SEQ_2, PARTFUN1, PARTFUN2, RFUNCT_2,
      FINSEQ_7, VFUNCT_1, COMPLEX1, REVROT_1, NFCONT_1, RCOMP_1, FDIFF_1,
      NDIFF_1, REAL_NS1, EXTREAL1, RELAT_1, FUNCT_1, FINSEQ_1, XBOOLE_0;
 registrations RELSET_1, STRUCT_0, ORDINAL1, XREAL_0, MEMBERED, LOPBAN_1,
      FDIFF_1, NDIFF_1, FUNCT_1, FUNCT_2, NAT_1, REAL_NS1, RVSUM_1, SEQ_1,
      NUMBERS, XBOOLE_0, XXREAL_0, FINSEQ_1;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;
 definitions TARSKI, LOPBAN_1, EUCLID;
 theorems TARSKI, ABSVALUE, XBOOLE_0, XBOOLE_1, XREAL_0, REAL_1, EUCLID,
      XREAL_1, RCOMP_1, RLVECT_1, SEQ_4, COMPLEX1, SEQ_1, SEQ_2, FINSEQ_1,
      FINSEQ_2, FINSEQ_7, RVSUM_1, RFUNCT_2, RELAT_1, FUNCT_1, FINSEQ_4,
      VFUNCT_1, FUNCT_2, NORMSP_1, LOPBAN_1, PARTFUN1, PARTFUN2, NFCONT_1,
      FDIFF_1, NDIFF_1, FRECHET, REAL_NS1, EXTREAL2, NUMBERS;
 schemes SEQ_1, FUNCT_2, GRAPH_5;

begin :: Preliminaries

definition let i,n be Element of NAT;
  func proj(i,n) -> Function of REAL n,REAL means :Def1:
    for x be Element of REAL n holds it.x=x.i;
existence
proof
   deffunc F(Element of REAL n) = $1.i;
   thus ex f being Function of REAL n,REAL st
    for x being Element of REAL n holds f.x = F(x) from FUNCT_2:sch 4;
end;
uniqueness
proof
   let f,g be Function of REAL n,REAL;
   assume that
A2: for x being Element of REAL n holds f.x = x.i and
A3: for x being Element of REAL n holds g.x = x.i;
   now let x be Element of REAL n;
    f.x =x.i by A2;
    hence f.x = g.x by A3;
   end;
   hence thesis by FUNCT_2:113;
end;
end;

ID01:
proj(1,1) is bijective & dom proj(1,1) = REAL 1 & rng proj(1,1) = REAL &
for x be Element of REAL holds proj(1,1).<*x*> = x & proj(1,1)".x = <*x*>
proof
   set f=proj(1,1);
P1:dom f= REAL 1 by FUNCT_2:def 1;
   for x1,x2 be set st x1 in dom f & x2 in dom f & f.x1 = f.x2 holds x1 = x2
   proof
    let x1,x2 be set;
    assume AS: x1 in dom f & x2 in dom f & f.x1 = f.x2; then
    reconsider y1=x1,y2=x2 as Element of REAL 1;
    consider d1 be Element of REAL such that
Q1:  x1=<*d1*> by AS,FINSEQ_2:117;
Q2: ex d2 be Element of REAL st x2=<*d2*> by AS,FINSEQ_2:117;
    d1 = <*d1*>.1 by FINSEQ_1:57; then
    d1 = f.y1 by Q1,Def1; then
    d1 = y2.1 by AS,Def1;
    hence x1=x2 by Q1,Q2,FINSEQ_1:57; 
   end; then
Q1:f is one-to-one by FUNCT_1:def 8;
Q3:for y be set st y in REAL ex x be set st x in REAL 1 & y = f.x
   proof
    let y be set;
    assume y in REAL; then
    reconsider y1=y as Element of REAL;
    reconsider x=<*y1*> as Element of REAL 1 by FINSEQ_2:118;
    f.x = x.1 by Def1; then
    f.x = y by FINSEQ_1:57;
    hence thesis;
   end; then
   rng f = REAL by FUNCT_2:16; then
Q2:f is onto by FUNCT_2:def 3;
   now let x be Element of REAL;
Q4: <*x*> is Element of 1-tuples_on REAL by FINSEQ_2:118; then
    proj(1,1).<*x*> =<*x*>.1 by Def1;
    hence proj(1,1).<*x*> = x by FINSEQ_1:57;
    hence proj(1,1)".x = <*x*> by FUNCT_1:56,P1,Q1,Q4;
   end;
   hence thesis by Q1,Q2,Q3,FUNCT_2:def 1,def 4,16;
end;

theorem
dom proj(1,1) = REAL 1 & rng proj(1,1) = REAL &
for x be Element of REAL holds proj(1,1).<*x*> = x & proj(1,1)".x = <*x*>
  by ID01;

theorem ID02:
proj(1,1)" is Function of REAL,REAL 1 & proj(1,1)" is one-to-one &
dom(proj(1,1)") = REAL & rng(proj(1,1)") = REAL 1 &
ex g be Function of REAL,REAL 1 st g is bijective & proj(1,1)" = g
proof  
   set f=proj(1,1);
P1:f is one-to-one by ID01,FUNCT_2:def 4; then
P2:REAL = dom(f") & REAL 1 = rng(f") by ID01,FUNCT_1:55;
P3:f" is one-to-one by P1,FUNCT_1:62;
   reconsider g=f" as Function of REAL,REAL 1 by P2,FUNCT_2:3;
   g is onto by FUNCT_2:def 3,P2; then
   g is bijective by FUNCT_2:def 4,P3;
   hence thesis by P1,ID01,FUNCT_1:55,62;
end;

registration
  cluster proj(1,1) -> bijective;
  coherence by ID01;
end;

definition
  let g be PartFunc of REAL,REAL;
  func <>*g -> PartFunc of REAL 1,REAL 1 equals
    proj(1,1)"*g*proj(1,1);
  coherence;
end;

definition
  let n be Element of NAT;
  let g be PartFunc of REAL n,REAL;
  func <>*g -> PartFunc of REAL n,REAL 1 equals
    proj(1,1)"*g;
  correctness;
end;

definition let i,n be Element of NAT;
  func Proj(i,n) -> Function of REAL-NS n,REAL-NS 1 means :Def4:
   for x be Point of REAL-NS n holds it.x=<* proj(i,n).x *>;
existence
proof
   deffunc F(Point of REAL-NS n) = <* proj(i,n).$1 *>;
A0:for x be Point of REAL-NS n holds F(x) is Element of REAL-NS 1
   proof
    let x be Point of REAL-NS n;
    len F(x) = 1 by FINSEQ_1:56; then
    F(x) is Element of 1-tuples_on REAL by FINSEQ_2:110; then
    F(x) in REAL 1;
    hence F(x) is Element of REAL-NS 1 by REAL_NS1:def 4;
   end; 
   thus ex f being Function of REAL-NS n,REAL-NS 1 st
   for x being Point of REAL-NS n holds f.x = F(x) from FUNCT_2:sch 9(A0);
end;
uniqueness
proof
   let f,g be Function of REAL-NS n, REAL-NS 1;
   assume that
A2: for x being Point of REAL-NS n holds f.x = <* proj(i,n).x *> and
A3: for x being Point of REAL-NS n holds g.x = <* proj(i,n).x *>;
   now
    let x be Point of REAL-NS n;
    f.x = <* proj(i,n).x *> by A2;
    hence f.x = g.x by A3;
   end;
   hence thesis by FUNCT_2:113;
end;
end;

definition
 let i be Element of NAT;
 let x be FinSequence of REAL;
 func reproj(i,x) -> Function means :DDef1:
   dom it = REAL & for r be Element of REAL holds it.r = Replace(x,i,r);
existence
proof
   deffunc F(Element of REAL) = Replace(x,i,$1);
   consider f being Function such that 
A1: dom f = REAL & 
    for r being Element of REAL st r in REAL holds f.r = F(r) 
     from GRAPH_5:sch 1;
   take f;
   thus thesis by A1;
end;
uniqueness
proof
   let f,g be Function;
   assume that
A2: dom f = REAL & for r being Element of REAL holds f.r = Replace(x,i,r) and
A3: dom g = REAL & for r being Element of REAL holds g.r = Replace(x,i,r);
   now
    let p be set;
    assume p in dom f; then
    reconsider r = p as Element of REAL by A2;
    f.r = Replace(x,i,r) by A2;
    hence f.p = g.p by A3;
   end;
   hence thesis by A2,A3,FUNCT_1:9;
end;
end;

definition
  let n,i be Element of NAT;
  let x be Element of REAL n;
redefine func reproj(i,x) -> Function of REAL,REAL n;
correctness
proof
A1:dom reproj(i,x) = REAL &
   for r be Element of REAL holds reproj(i,x).r = Replace(x,i,r) by DDef1;
   now let p be set;
    assume p in REAL; then
    reconsider r=p as Element of REAL;
A2: reproj(i,x).r = Replace(x,i,r) by DDef1; then
    reconsider IT = reproj(i,x).r as FinSequence of REAL;
    len IT = len x by A2,FINSEQ_7:7; then
    len IT = n by EUCLID:2; then
    reconsider IT as Element of n-tuples_on REAL by FINSEQ_2:110;
    IT is Element of REAL n;
    hence reproj(i,x).p in REAL n;
   end;
   hence thesis by A1,FUNCT_2:5;
end;
end;

definition
  let n,i be Element of NAT;
  let x be Point of REAL-NS n;
  func reproj(i,x) -> Function of REAL-NS 1,REAL-NS n means :Def6:
   for r be Element of REAL-NS 1
    ex q be Element of REAL, y be Element of REAL n st
      r=<*q*> & y=x & it.r = reproj(i,y).q;
existence
proof
   defpred P[Element of REAL-NS 1, Point of REAL-NS n] means 
    ex q be Element of REAL, z be Element of REAL n st
     $1 = <*q*> & z = x & $2 = reproj(i,z).q;
A0:for r being Element of REAL-NS 1 ex y be Element of REAL-NS n st P[r,y]
   proof
    let r be Element of REAL-NS 1;
    reconsider z = x as Element of REAL n by REAL_NS1:def 4;
    r is Element of  REAL 1 by REAL_NS1:def 4; then
    consider q be Element of REAL such that 
A01: r=<*q*> by FINSEQ_2:117;
    reproj(i,z).q is Element of REAL-NS n by REAL_NS1:def 4;
    hence thesis by A01;
   end;
   thus ex f being Function of REAL-NS 1,REAL-NS n st
    for r be Element of REAL-NS 1 holds P[r,f.r] from FUNCT_2:sch 10(A0);
end;
uniqueness
proof
   let f,g be Function of REAL-NS 1,REAL-NS n;
   assume that
A2: for r be Element of REAL-NS 1
     ex q be Element of REAL, y be Element of REAL n st
      r=<*q*> & y=x & f.r= reproj(i,y).q and
A3: for r be Element of REAL-NS 1
     ex q be Element of REAL, y be Element of REAL n st
      r=<*q*> & y=x & g.r= reproj(i,y).q;
   now
    let r be Point of REAL-NS 1;
    consider p be Element of REAL, y be Element of REAL n such that
A4:  r=<*p*> & y=x & f.r= reproj(i,y).p by A2;
A5: ex q be Element of REAL,z be Element of REAL n st
     r=<*q*> & z=x & g.r= reproj(i,z).q by A3;
    p = <*p*>.1 by FINSEQ_1:def 8;
    hence f.r = g.r by A4,A5,FINSEQ_1:def 8;
   end;
   hence thesis by FUNCT_2:113;
end;
end;

definition
  let m,n be non empty Element of NAT;
  let f be PartFunc of REAL m,REAL n;
  let x be Element of REAL m;
  pred f is_differentiable_in x means :Def7:
   ex g be PartFunc of REAL-NS m,REAL-NS n, y be Point of REAL-NS m st
    f=g & x=y & g is_differentiable_in y;
end;

definition
  let m,n be non empty Element of NAT;
  let f be PartFunc of REAL m,REAL n;
  let x be Element of REAL m;
  assume AS: f is_differentiable_in x;
  func diff(f,x) -> Function of REAL m, REAL n means :Def8:
   ex g be PartFunc of REAL-NS m,REAL-NS n, y be Point of REAL-NS m st
    f=g & x=y & it = diff(g,y);
correctness
proof
   consider g be PartFunc of REAL-NS m,REAL-NS n,
            y be Point of REAL-NS m such that
AS1:f=g & x=y & g is_differentiable_in y by AS,Def7;
   the carrier of REAL-NS m = REAL m & the carrier of REAL-NS n = REAL n
     by REAL_NS1:def 4; then
   diff(g,y) is Function of REAL m,REAL n by LOPBAN_1:def 10;
   hence thesis by AS1;
end;
end;

theorem ID07:
for I be Function of REAL,REAL 1 st I=proj(1,1)" holds 
 (for x be VECTOR of REAL-NS 1, y be Element of REAL st x=I.y holds 
   ||.x.|| = abs y) &
 (for x,y be VECTOR of REAL-NS 1, a,b be Element of REAL st x=I.a & y=I.b holds
   x+y = I.(a+b)) &
 (for x be VECTOR of REAL-NS 1, y be Element of REAL, a be Real st x=I.y holds
   a*x = I.(a*y)) &
 (for x be VECTOR of REAL-NS 1, a be Element of REAL st x=I.a holds 
   -x = I.(-a)) &
 (for x,y be VECTOR of REAL-NS 1, a,b be Element of REAL st x=I.a & y=I.b holds
   x-y =I.(a-b))
proof
   let I be Function of REAL,REAL 1;
   assume AS: I=proj(1,1)";
   hereby let x be VECTOR of REAL-NS 1, y be Element of REAL;
    reconsider xx=x as Element of REAL 1 by REAL_NS1:def 4;
    assume x = I.y; then
    xx = <*y*> by AS,ID01; then
    sqrt Sum sqr xx = sqrt Sum <*y^2*> by RVSUM_1:81; then
L:  sqrt Sum sqr xx = sqrt (y^2) by RVSUM_1:103;
    ||.x.|| = |.xx.| by REAL_NS1:1;
    hence ||.x.|| = abs y by L,COMPLEX1:158;
   end;
   hereby let x,y be VECTOR of REAL-NS 1, a,b be Element of REAL;
    reconsider xx=x, yy = y as Element of REAL 1 by REAL_NS1:def 4;
    assume x=I.a & y=I.b; then
    <*a*> = xx & <*b*> = yy by AS,ID01; then
    x+y = <*a*> + <*b*> by REAL_NS1:2; then
    x+y =<*a+b*> by RVSUM_1:29;
    hence x+y =I.(a + b) by AS,ID01;
   end;
P3:now let x be VECTOR of REAL-NS 1, y be Element of REAL, a be Real;
    reconsider xx=x as Element of REAL 1 by REAL_NS1:def 4;
    assume x=I.y; then
P31:xx=<*y*> by AS,ID01;
    a*x = a*xx by REAL_NS1:3; then
    a*x =<* a*y *> by P31,RVSUM_1:69;
    hence a*x =I.(a*y) by AS,ID01;
   end;
P4:now let x be VECTOR of REAL-NS 1, a be Element of REAL;
    assume x=I.a; then
    (-1)*x = I.((-1)*a) by P3;
    hence -x = I.(-a) by RLVECT_1:29;
   end;
   now let x,y be VECTOR of REAL-NS 1, a,b be Element of REAL;
    reconsider xx=x, yy=y as Element of REAL 1 by REAL_NS1:def 4;
    assume x=I.a & y=I.b; then
    <*a*> = xx & <*b*> = yy by AS,ID01; then
    x-y = <*a*> - <*b*> by REAL_NS1:5; then
    x-y = <*a-b*> by RVSUM_1:50;
    hence x-y =I.(a-b) by AS,ID01;
   end;
   hence thesis by P3,P4;
end;

theorem ID08:
for J be Function of REAL 1,REAL st J=proj(1,1) holds
 (for x be VECTOR of REAL-NS 1, y be Element of REAL st J.x=y 
      holds ||.x.|| = abs y) &
 (for x,y be VECTOR of REAL-NS 1, a,b be Element of REAL st J.x=a & J.y=b
      holds J.(x+y) = a+b) &
 (for x be VECTOR of REAL-NS 1, y be Element of REAL, a be Real st J.x=y
      holds J.(a*x) = a*y) &
 (for x be VECTOR of REAL-NS 1, a be Element of REAL st J.x=a
      holds J.(-x) = -a) &
 (for x,y be VECTOR of REAL-NS 1, a,b be Element of REAL st J.x=a & J.y=b
      holds J.(x-y) = a-b)
proof
   let J be Function of REAL 1,REAL;
   assume AS:J=proj(1,1);
   reconsider I=proj(1,1)" as Function of REAL,REAL 1 by ID02;
   hereby let x be VECTOR of REAL-NS 1, y be Element of REAL;
    reconsider xx=x as Element of REAL 1 by REAL_NS1:def 4;
    assume J.x=y; then
    I.(J.xx) = I.y; then
    x = I.y by ID01,AS,FUNCT_1:56;
    hence ||.x.|| = abs y by ID07;
   end;
   hereby let x,y be VECTOR of REAL-NS 1, a,b be Element of REAL;
    reconsider xx=x, yy=y as Element of REAL 1 by REAL_NS1:def 4;
    assume J.x=a & J.y=b; then
    I.(J.xx) = I.a & I.(J.yy) = I.b; then
    x = I.a & y = I.b by ID01,AS,FUNCT_1:56; then
    J.(x+y) = J.(I.(a+b)) by ID07;
    hence J.(x+y) = a+b by ID01,AS,FUNCT_1:57;
   end;
   hereby let x be VECTOR of REAL-NS 1, y be Element of REAL, a be Real;
    reconsider xx=x as Element of REAL 1 by REAL_NS1:def 4;
    assume J.x=y; then
    I.(J.xx) = I.y; then
    x = I.y by ID01,AS,FUNCT_1:56; then
    J.(a*x) = J.(I.(a*y)) by ID07;
    hence J.(a*x) = a*y by ID01,AS,FUNCT_1:57;
   end;
   hereby let x be VECTOR of REAL-NS 1, y be Element of REAL;
    reconsider xx=x as Element of REAL 1 by REAL_NS1:def 4;
    assume J.x=y; then
    I.y = I.(J.xx); then
    x = I.y by ID01,AS,FUNCT_1:56; then
    J.(-x) = J.(I.(-y)) by ID07;
    hence J.(-x) = -y by ID01,AS,FUNCT_1:57;
   end; 
   let x,y be VECTOR of REAL-NS 1, a,b be Element of REAL;
   reconsider xx=x, yy=y as Element of REAL 1 by REAL_NS1:def 4;
   assume J.x=a & J.y=b; then
   I.(J.xx) = I.a & I.(J.yy) = I.b; then
   x = I.a & y = I.b by ID01,AS,FUNCT_1:56; then
   J.(x-y) =J.(I.(a - b)) by ID07;
   hence J.(x-y) = a-b by ID01,AS,FUNCT_1:57;
end;
 
theorem ID09:
for I be Function of REAL,REAL 1, J be Function of REAL 1,REAL st 
 I=proj(1,1)" & J=proj(1,1) holds
  (for R being REST of REAL-NS 1,REAL-NS 1 holds J*R*I is REST) &
  (for L being LinearOperator of REAL-NS 1,REAL-NS 1 holds J*L*I is LINEAR)
proof
   let I be Function of REAL,REAL 1, J be Function of REAL 1,REAL;
   assume AS: I=proj(1,1)" & J=proj(1,1);
   thus for R being REST of REAL-NS 1, REAL-NS 1 holds J*R*I is REST
   proof
    let R be REST of REAL-NS 1,REAL-NS 1;
Q10:R is total by NDIFF_1:def 5;
    the carrier of REAL-NS 1 = REAL 1 by REAL_NS1:def 4; then
    reconsider R0=R as Function of REAL 1,REAL 1 by FUNCT_2:131,Q10;
    reconsider R1=J*R*I as PartFunc of REAL,REAL;
Q13:J*R0*I is Function of REAL,REAL; then
R11:dom R1 = REAL by FUNCT_2:def 1;
Q17:for r be Real st r > 0
     ex d be Real st d > 0 & 
      (for z1 be Real st z1 <> 0 & abs z1 < d holds abs(z1)"*abs(R1.z1) < r)
    proof
     let r be Real;
     assume r > 0; then
     consider d be Real such that
R2:   d > 0 and
R3:   (for z be Point of REAL-NS 1 st z <> 0.(REAL-NS 1) & ||.z.|| < d holds
        ||.z.||"*||. R/.z .|| < r) by Q10,NDIFF_1:26;
     take d;
     for z1 be Real st z1 <> 0 & abs z1 < d holds abs(z1)" * abs(R1.z1) < r
     proof
      let z1 be Real such that
R5:   z1 <> 0 & abs z1 < d;
      reconsider z = I.z1 as Point of REAL-NS 1 by REAL_NS1:def 4;
R6:   ||.z.|| < d by ID07,AS,R5;
      z1 in REAL; then
      reconsider z1r = z1 as R_eal by NUMBERS:31;
      0 < |.z1r.| by R5,EXTREAL2:52; then
      abs z1 > 0 by EXTREAL2:49; then
      ||.z.|| <> 0 by ID07,AS; then
R7:   z <> 0.(REAL-NS 1) by NORMSP_1:def 2;
R8:   ||.z.||" = abs(z1)" by ID07,AS;
      R is total by NDIFF_1:def 5; then
R9:   dom R = the carrier of REAL-NS 1 by PARTFUN1:def 4;
      dom J = REAL 1 by FUNCT_2:def 1; then
R10:  R0.z in dom J by FUNCT_2:7;
      I*J =id dom(proj(1,1)) by AS,FUNCT_1:61; then
R12:  I*J =id REAL 1 by FUNCT_2:def 1;
R13:  dom(J*R0) = REAL 1 by FUNCT_2:def 1;
      R/.z = R.z by R9,FINSEQ_4:def 4; then
      R/.z =((id the carrier of REAL-NS 1)*R).(I.z1) by FUNCT_2:23; then
      R/.z =(I*J*R).(I.z1) by R12,REAL_NS1:def 4; then
      R/.z =(I*J).(R.(I.z1)) by R9,FUNCT_1:23; then
      R/.z =I.(J.(R0.z)) by R10,FUNCT_1:23; then
      R/.z =I.((J*R0).(I.z1)) by R13,FUNCT_1:22; then
      R/.z =I.(R1.z1) by R11,FUNCT_1:22; then
      ||. R/.z .|| =abs(R1.z1) by ID07,AS;
      hence thesis by R3,R8,R7,R6;
     end;
     hence thesis by R2;
    end;
    for h be convergent_to_0 Real_Sequence holds
     h"(#)(R1*h) is convergent & lim(h"(#)(R1*h)) = 0
    proof
     let h be convergent_to_0 Real_Sequence;
A:   now let r0 be real number;
      reconsider r = r0 as Real by XREAL_0:def 1;
      assume r0 > 0; then
      consider d be Real such that
RS3:   d > 0 and
RS4:   (for z1 be Real st z1 <> 0 & abs z1 < d holds abs(z1)" * abs(R1.z1) < r)
          by Q17;
      reconsider d1 =d as real number;
      h is convergent & lim h = 0 by FDIFF_1:def 1; then
      consider m be Element of NAT such that
RS5:   for n be Element of NAT st m <= n holds abs(h.n-0) < d1 
        by RS3,SEQ_2:def 7;
      take m;
      hereby let n be Element of NAT;
       assume m <= n; then
RS8:   abs(h.n - 0) < d by RS5;
       h is_not_0 by FDIFF_1:def 1; then
RS9:   h.n <> 0 by SEQ_1:7;
       rng h c= dom R1 by R11; then
       abs(h.n)" * abs(R1.(h.n)) = abs(h.n)" * abs((R1*h).n) by RFUNCT_2:21
        .= ((abs h).n)" * abs((R1*h).n) by SEQ_1:16
        .= (abs h)".n * abs((R1*h).n) by SEQ_1:def 8
        .= abs(h").n * abs((R1*h).n) by SEQ_1:62
        .= abs(h".n) * abs((R1*h).n) by SEQ_1:16
        .= abs(h".n * (R1*h).n) by COMPLEX1:151
        .= abs((h"(#)(R1*h)).n - 0) by SEQ_1:12;
       hence abs((h"(#)(R1*h)).n - 0) < r0 by RS4,RS9,RS8;
      end;
     end;
     hence h"(#)(R1*h) is convergent by SEQ_2:def 6;
     hence lim(h"(#)(R1*h)) = 0 by A,SEQ_2:def 7;
    end;
    hence thesis by Q13,FDIFF_1:def 3;
   end;
   thus for L being LinearOperator of REAL-NS 1,REAL-NS 1 holds J*L*I is LINEAR
   proof
    let L be LinearOperator of REAL-NS 1,REAL-NS 1;
P0: the carrier of REAL-NS 1 = REAL 1 by REAL_NS1:def 4; then
    reconsider L0=L as Function of REAL 1, REAL 1;
    reconsider L1=J*L0*I as PartFunc of REAL,REAL;
QM: dom L0 = REAL 1 & dom(J*L0) = REAL 1 & dom(J*L0*I) = REAL by FUNCT_2:def 1;
    consider r be Real such that
Q8:  r = L1.1;
    for p be Real holds L1.p = r*p
    proof
     let p be Real;
     reconsider 1p = I.1 as VECTOR of REAL-NS 1 by REAL_NS1:def 4;
     dom I = REAL by FUNCT_2:def 1; then
     L1.p =(J*L).(I.(p*1)) by FUNCT_1:23; then
     L1.p =(J*L).(p*1p) by ID07,AS; then
     L1.p =J.(L.(p*1p)) by P0,QM,FUNCT_1:23; then
     L1.p =J.(p*(L.1p)) by LOPBAN_1:def 6; then
     L1.p =p*J.(L.1p) by ID08,AS; then
     L1.p =p*((J*L0).(I.1)) by QM,FUNCT_1:22;
     hence L1.p =r*p by Q8,QM,FUNCT_1:22;
    end;
    hence J*L*I is LINEAR by FDIFF_1:def 4;
   end;
end;

theorem ID10:
for I be Function of REAL,REAL 1, J be Function of REAL 1,REAL st
 I=proj(1,1)" & J=proj(1,1) holds
  (for R being REST holds I*R*J is REST of REAL-NS 1,REAL-NS 1) &
  (for L being LINEAR holds 
    I*L*J is bounded LinearOperator of REAL-NS 1,REAL-NS 1)
proof
   let I be Function of REAL,REAL 1, J be Function of REAL 1,REAL;
   assume AS:I=proj(1,1)" & J=proj(1,1);
   thus for R being REST holds I*R*J is REST of REAL-NS 1,REAL-NS 1
   proof
    let R be REST;
    R is total by FDIFF_1:def 3; then
    reconsider R0=R as Function of REAL,REAL by FUNCT_2:131;
Q31:the carrier of REAL-NS 1 = REAL 1 by REAL_NS1:def 4; then
    reconsider R1 = I*R0*J as PartFunc of REAL-NS 1,REAL-NS 1;
    for h be convergent_to_0 sequence of REAL-NS 1 holds
     ||.h.||"(#)(R1*h) is convergent &
     lim(||.h.||"(#)(R1*h)) = 0.(REAL-NS 1)
    proof
     let h be convergent_to_0 sequence of REAL-NS 1;
R1:  h is_not_0 by NDIFF_1:def 4;
R2:  h is convergent & lim h = 0.(REAL-NS 1) by NDIFF_1:def 4;
     deffunc F(Element of NAT)=J.(h.$1);
     consider s be Real_Sequence such that
AQAS: for n be Element of NAT holds s.n = F(n) from SEQ_1:sch 1;
     reconsider s0 = s as Function of NAT,REAL;
     s is convergent_to_0 Real_Sequence
     proof
      now let x be set;
       assume x in NAT; then
       reconsider n=x as Element of NAT;
       h.n <> 0.(REAL-NS 1) by R1,NDIFF_1:6; then
U1:    ||. h.n .|| <> 0 by NORMSP_1:def 2;
       s.n = J.(h.n) by AQAS; then
U2:    abs(s.n) <> 0 by AS, ID08, U1;
       0 <= abs(s.n) by COMPLEX1:132; then
       0 < abs(s.n) by U2,REAL_1:def 5;
       hence s.x <> 0 by COMPLEX1:133;
      end; then
AQ01: s is_not_0 by SEQ_1:6;
F00:  now let p be real number;
       assume F0: 0 < p;
       p is Real by XREAL_0:def 1; then
       consider m be Element of NAT such that
F1:     for n be Element of NAT st m <= n holds 
         ||. h.n - 0.(REAL-NS 1).|| < p by NORMSP_1:def 11,F0,R2;
       take m;
       now let n be Element of NAT;
        assume m <= n; then
        ||. h.n - 0.(REAL-NS 1).|| < p by F1; then
F2:     ||. h.n .|| < p by RLVECT_1:26;
        s.n = J.(h.n) by AQAS;
        hence abs(s.n-0) < p by F2,AS,ID08;
       end;
       hence for n be Element of NAT st m <= n holds abs(s.n-0)< p;
      end; then
AQ02: s is convergent by SEQ_2:def 6; then
      lim s = 0 by F00,SEQ_2:def 7;
      hence s is convergent_to_0 Real_Sequence by AQ01,AQ02,FDIFF_1:def 1;
     end; then
     reconsider s as convergent_to_0 Real_Sequence;
R113:J*I =id REAL by ID01,AS,FUNCT_1:61;
     now let n be Element of NAT;
      h.n in the carrier of (REAL-NS 1); then
R115: h.n in (REAL 1) by REAL_NS1:def 4;
      reconsider f1=R1 as Function;
G119: dom R1 = REAL 1 by FUNCT_2:def 1;
      rng h c= the carrier of REAL-NS 1; then
R119: rng h c= dom R1 by G119,REAL_NS1:def 4;
      NAT = dom h by FUNCT_2:def 1; then
      R1.(h.n) =(f1*h).n by FUNCT_1:23; then
R120: R1.(h.n) =(R1*h).n by R119,NFCONT_1:def 7;
      R1 is total by Q31; then
      (R*s).n =R.(s.n) by RFUNCT_2:30; then
      (R*s).n =R.(J.(h.n)) by AQAS; then
      (R*s).n =(J*I).(R0.(J.(h.n))) by R113,FUNCT_1:35; then
      (R*s).n =(J*I).((R0*J).(h.n)) by R115,FUNCT_2:21; then
      (R*s).n =J.(I.((R0*J).(h.n))) by FUNCT_2:21; then
      (R*s).n =J.((I*(R0*J)).(h.n)) by R115,FUNCT_2:21; then
E1:   (R*s).n =J.((R1*h).n) by R120,RELAT_1:55;
E2:   s.n = J.(h.n) by AQAS;
E31:  ||.h.n.|| >= 0 by NORMSP_1:8;
      h.n <> 0.(REAL-NS 1) by R1,NDIFF_1:6; then
      ||.h.n.|| <> 0 by NORMSP_1:def 2; then 
      ||.h.n.|| > 0 by E31, REAL_1:def 5; then
E3:   0 < ||.h.n.||" by XREAL_1:124;
      ||. ||.h.||"(#)(R1*h).|| .n
        = ||.(||.h.||"(#)(R1*h)).n .|| by NORMSP_1:def 10
       .= ||.(||.h.||").n * (R1*h).n .|| by NDIFF_1:def 2
       .= abs((||.h.||").n) * ||.(R1*h).n .|| by NORMSP_1:def 2
       .= abs((||.h.||.n)") * ||.(R1*h).n .|| by SEQ_1:def 8
       .= abs(||.h.n.||") * ||.(R1*h).n .|| by NORMSP_1:def 10
       .= ||.h.n .||" *||.(R1*h).n .|| by E3,ABSVALUE:def 1
       .= (abs(s.n))" *||.(R1*h).n .|| by E2,AS,ID08
       .= (abs(s.n))" *abs((R*s).n) by E1,AS,ID08
       .= (abs(s.n))" *(abs(R*s)).n by SEQ_1:16
       .= ((abs s).n)" *(abs(R*s)).n by SEQ_1:16
       .= ((abs s)").n *(abs(R*s)).n by SEQ_1:def 8
       .= ((abs s)"(#)abs(R*s)).n by SEQ_1:12
       .= (abs(s")(#)abs(R*s)).n by SEQ_1:62;
      hence ||. ||.h.||"(#)(R1*h) .|| .n = (abs(s"(#)(R*s))).n by SEQ_1:60;
     end; then
Q52: ||. ||.h.||"(#)(R1*h) .|| = abs(s"(#)(R*s)) by FUNCT_2:113;
Q53: s"(#)(R*s) is convergent & lim(s"(#)(R*s))=0 by FDIFF_1:def 3; then
Q54: abs(s"(#)(R*s)) is convergent by SEQ_4:26;
     lim abs(s"(#)(R*s)) =abs lim(s"(#)(R*s)) by Q53,SEQ_4:27; then
Q55: lim abs(s"(#)(R*s)) =0 by Q53,ABSVALUE:7;
F01: now let p be Real;
      assume 0 < p; then
      consider m be Element of NAT such that 
F1:    for n be Element of NAT st m <= n holds 
        abs(||. ||.h.||"(#)(R1*h).||.n - 0) < p by Q52,Q54,Q55,SEQ_2:def 7;
      take m;
      hereby let n be Element of NAT;
       assume m <= n; then
       abs(||. ||.h.||"(#)(R1*h).|| .n - 0) < p by F1; then
J1:    abs ||.(||.h.||"(#)(R1*h)).n.|| < p by NORMSP_1:def 10;
       0 <= ||.(||.h.||"(#)(R1*h)).n.|| by NORMSP_1:8; then
       ||.(||.h.||"(#)(R1*h)).n.|| < p by J1,ABSVALUE:def 1;
       hence ||.(||.h.||"(#)(R1*h)).n -0.(REAL-NS 1).|| < p by RLVECT_1:26;
      end;
     end; then
     ||.h.||"(#)(R1*h) is convergent by NORMSP_1:def 9;
     hence thesis by F01,NORMSP_1:def 11;
    end;
    hence thesis by Q31,NDIFF_1:def 5;
   end;
   thus for L being LINEAR holds
    I*L*J is bounded LinearOperator of REAL-NS 1,REAL-NS 1
   proof
    let L be LINEAR;
    consider r be Real such that
Q31: for p be Real holds L.p = r * p by FDIFF_1:def 4;
    L is total by FDIFF_1:def 4; then
    reconsider L0 = L as Function of REAL,REAL by FUNCT_2:131;
Q45:dom L0 = REAL by FUNCT_2:def 1;
Q55:the carrier of REAL-NS 1 = REAL 1 by REAL_NS1:def 4;
    I*L0*J is Function of REAL 1,REAL 1; then
    reconsider L1 = I*L*J as Function of REAL-NS 1,REAL-NS 1 by Q55;
Q56:dom L1 = REAL 1 by Q55,FUNCT_2:def 1;
Q6: now let x be VECTOR of REAL-NS 1, a be Real;
     I.(L.(J.x)) = (I*L).(J.x) by Q45,FUNCT_1:23; then
R08: I.(L.(J.x)) = L1.x by Q55,Q56,FUNCT_1:22;
     L1.(a*x) =(I*L).(J.(a*x)) by Q55,Q56,FUNCT_1:22; then
     L1.(a*x) =(I*L).(a*J.x) by ID08,AS; then
     L1.(a*x) =I.(L.((a*J.x))) by Q45,FUNCT_1:23; then
     L1.(a*x) =I.(r*(a*J.x)) by Q31; then
     L1.(a*x) =I.(a*(r*J.x)); then
     L1.(a*x) =I.(a*L.(J.x)) by Q31;
     hence L1.(a*x) =a*(L1.x) by ID07,AS,R08;
    end;
    now let x,y be VECTOR of REAL-NS 1;
     I.(L.(J.x)) = (I*L).(J.x) by Q45,FUNCT_1:23; then
R041:I.(L.(J.x)) = L1.x by Q55,Q56,FUNCT_1:22;
     I.(L.(J.y)) = (I*L).(J.y) by Q45,FUNCT_1:23; then
R042:I.(L.(J.y)) = L1.y by Q55,Q56,FUNCT_1:22;
     L1.(x+y) =(I*L).(J.(x+y)) by Q55,Q56,FUNCT_1:22; then
     L1.(x+y) =(I*L).(J.x+J.y) by ID08,AS; then
     L1.(x+y) =I.(L.((J.x+J.y))) by Q45,FUNCT_1:23; then
     L1.(x+y) =I.(r*(J.x+J.y)) by Q31; then
     L1.(x+y) =I.(r*J.x+r*J.y); then
     L1.(x+y) =I.(L.(J.x)+(r*J.y)) by Q31; then
     L1.(x+y) =I.(L.(J.x)+L.(J.y)) by Q31;
     hence L1.(x+y) =L1.x + L1.y by ID07,AS,R041,R042;
    end; then
    reconsider L1 as LinearOperator of REAL-NS 1,REAL-NS 1 
      by Q6,LOPBAN_1:def 5,def 6; 
    set K = abs r;
P1: 0 <= K by COMPLEX1:132;
    now let x be VECTOR of REAL-NS 1;
     I.(L.(J.x)) =(I*L).(J.x) by Q45,FUNCT_1:23; then
     I.(L.(J.x)) =L1.x by Q55,Q56,FUNCT_1:22; then
     ||. L1.x .|| =abs(L.(J.x)) by AS,ID07; then
     ||. L1.x .|| =abs(r*J.x) by Q31; then
     ||. L1.x .|| =abs(r)*abs(J.x) by COMPLEX1:151;
     hence ||. L1.x .|| <= K* ||.x.|| by AS,ID08;
    end;
    hence thesis by P1,LOPBAN_1:def 9;
   end;
end;

 reserve f for PartFunc of REAL-NS 1,REAL-NS 1;
 reserve g for PartFunc of REAL,REAL;
 reserve x for Point of REAL-NS 1;
 reserve y for Element of REAL;

theorem Lm0120L:
f=<>*g& x=<*y*> & f is_differentiable_in x implies
 g is_differentiable_in y & diff(g,y)=(proj(1,1)*diff(f,x)*proj(1,1)").1
proof
   reconsider I=proj(1,1)" as Function of REAL,REAL 1 by ID02;
   set J =proj(1,1);
   assume AS0: f=<>*g & x=<*y*> & f is_differentiable_in x; then
   consider NN being Neighbourhood of x such that
A4N:NN c= dom f &
    ex R being REST of REAL-NS 1,REAL-NS 1 st
     for y be Point of REAL-NS 1 st y in NN holds 
      f/.y - f/.x = diff(f,x).(y-x) + R/.(y-x) by NDIFF_1:def 7;
   consider R being REST of REAL-NS 1,REAL-NS 1 such that
A5N:for x' be Point of REAL-NS 1 st x' in NN holds
     f/.x' - f/.x = diff(f,x).(x'-x) + R/.(x'-x) by A4N;
   consider e be Real such that
PN1:0 < e & 
    {z where z is Point of REAL-NS 1 : ||.z-x.|| < e} c= NN by NFCONT_1:def 3;
   set N={z where z is Point of REAL-NS 1 : ||.z-x.|| < e};
KI:N c= the carrier of REAL-NS 1
   proof
    let y be set;
    assume y in N; then
    ex z be Point of REAL-NS 1 st y=z & ||.z-x.|| < e;
    hence y in the carrier of REAL-NS 1;
   end; then
   reconsider N as Neighbourhood of x by NFCONT_1:def 3,PN1;
A4:N c= dom f by PN1,A4N,XBOOLE_1:1;
   set N0={z where z is Element of REAL: abs(z-y) < e};
V1:N0 c= ].y-e,y+e.[
   proof
    let d be set;
    assume d in N0; then
    ex r be Element of REAL st d=r & abs(r-y) < e;
    hence d in ].y-e,y+e.[ by RCOMP_1:8;
   end;
   ].y-e,y+e.[ c= N0
   proof
    let d be set such that
V21:d in ].y-e,y+e.[;
    reconsider y0=d as Element of REAL by V21;
    abs(y0-y) < e by RCOMP_1:8,V21;
    hence d in N0;
   end; then
   N0 = ].y-e,y+e.[ by V1,XBOOLE_0:def 10; then
N2:N0 is Neighbourhood of y by PN1,RCOMP_1:def 7;
   dom f = J"(dom(I*g)) & rng g c= dom I by ID02,AS0,RELAT_1:182; then
   J.:(dom f) = J.:(J"(dom g)) by RELAT_1:46; then
U5:J.:(dom f) = dom g by ID01,FUNCT_1:147;
   now let z be set;
    hereby assume z in N0; then 
     consider y' be Element of REAL such that
U411: z=y' & abs(y'-y) < e;
     reconsider w=I.y' as Point of REAL-NS 1 by REAL_NS1:def 4;
     x=I.y by AS0,ID01; then
     w-x=I.(y'-y) by ID07; then
     ||.w-x.||=abs(y'-y) by ID07; then
     w in {z0 where z0 is Point of REAL-NS 1 : ||.z0 - x.|| < e} by U411; then
     J.w in J.:N by FUNCT_2:43;
     hence z in J.:N by FUNCT_1:57,U411,ID01;
    end;
    assume z in J.:N; then
    consider ww be set such that
Y1:  ww in REAL 1 & ww in N & z=J.ww by FUNCT_2:115;
    consider w be Point of REAL-NS 1 such that
Y2:  ww=w & ||.w-x.|| < e by Y1;
    reconsider y'=J.w as Element of REAL;
    J.x=y by AS0,ID01; then
    J.(w-x) =y'-y by ID08; then
    abs(y'-y) < e by ID08,Y2;
    hence z in N0 by Y1,Y2;
   end; then
U4:N0=J.:N by TARSKI:2;
   N c= dom f by PN1,A4N,XBOOLE_1:1; then
PN3:N0 c= dom g by U5,U4,RELAT_1:156;
   reconsider L = diff(f,x) as bounded LinearOperator of REAL-NS 1,REAL-NS 1
     by LOPBAN_1:def 10;
   reconsider L0=J*L*I as LINEAR by ID09;
   reconsider R0=J*R*I as REST by ID09;
R113: J*I =id REAL by ID01,FUNCT_1:61;
R113b:I*J =id (REAL 1) by ID01,FUNCT_1:61;
   N c= REAL 1 by KI,REAL_NS1:def 4; then
   (I*J).:N = N by FRECHET:13,R113b; then
UR4:I.:N0 = N by RELAT_1:159,U4;
PN4: for y0 be Real st y0 in N0 holds g.y0 - g.y = L0.(y0-y) + R0.(y0-y)
   proof
    let y0 be Real;
    assume Q1: y0 in N0; then
Q2: I.y0 in N by FUNCT_2:43,UR4;
Q3: x=I.y by AS0,ID01;
Q01:y in N0 by N2,RCOMP_1:37;
KD1:dom g c= REAL;
KD2:rng(g*J) c= REAL;
    J*f=J*(I*(g*J)) by AS0,RELAT_1:55; then
    J*f=(id REAL)*(g*J) by RELAT_1:55,R113; then
    J*f*I = g*J*I by RELAT_1:79,KD2; then
    J*f*I = g*(id REAL) by RELAT_1:55,R113; then
Q4: g=J*f*I by RELAT_1:77,KD1;
    reconsider y' = I.y0 as Point of REAL-NS 1 by REAL_NS1:def 4;
    J.(f/.y' - f/.x)= J.(L.(y'-x) + R/.(y'-x)) by A5N,PN1,Q2; then
    J.(f/.y')-J.(f/.x)=J.(L.(y'-x) + R/.(y'-x)) by ID08; then
    J.(f/.(I.y0))-J.(f/.(I.y))=J.(L.(y'-x))+J.(R/.(y'-x)) by ID08,Q3; then
PP1:J.(f/.(I.y0))-J.(f/.(I.y))=J.(L.(I.(y0-y))) + J.(R/.(y'-x)) by ID07,Q3;
    y0 in dom g & y in dom g by Q1,Q01,PN3; then
PP23:y0 in dom(J*(f*I)) & y in dom(J*(f*I)) by Q4,RELAT_1:55;
    x in N by NFCONT_1:4; then
    J.(f/.(I.y0)) =J.(f.(I.y0)) & J.(f/.(I.y)) =J.(f.(I.y))
      by FINSEQ_4:def 4,Q2,Q3,A4; then
    J.(f/.(I.y0)) =J.((f*I).y0) & J.(f/.(I.y)) =J.((f*I).y)
      by ID02,FUNCT_1:23; then
    J.(f/.(I.y0)) =(J*(f*I)).y0 & J.(f/.(I.y)) =(J*(f*I)).y
      by PP23,FUNCT_1:22; then
PP2:J.(f/.(I.y0)) =g.y0 & J.(f/.(I.y)) =g.y by Q4,RELAT_1:55;
    L0 is total by FDIFF_1:def 4; then
    dom(J*L*I) = REAL by PARTFUN1:def 4; then
    y0-y in dom(J*L*I); then
PP5:y0-y in dom(J*(L*I)) by RELAT_1:55;
    R0 is total by FDIFF_1:def 3; then
    dom(J*R*I) = REAL by PARTFUN1:def 4; then
    y0-y in dom(J*R*I); then
PP7:y0-y in dom(J*(R*I)) by RELAT_1:55;
    R is total by NDIFF_1:def 5; then
R100:dom R = the carrier of REAL-NS 1 by PARTFUN1:def 4;
    I.(y0-y) in REAL 1; then
    I.(y0-y) in dom R by R100,REAL_NS1:def 4; then
    J.(R/.(I.(y0-y))) =J.(R.(I.(y0-y))) by FINSEQ_4:def 4; then
    J.(R/.(I.(y0-y))) =J.((R*I).(y0-y)) by ID02,FUNCT_1:23; then
    J.(R/.(I.(y0-y))) =(J*(R*I)).(y0-y) by FUNCT_1:22,PP7; then
PP6:J.(R/.(I.(y0-y))) =R0.(y0-y) by RELAT_1:55;
    J.(L.(I.(y0-y))) =J.((L*I).(y0-y)) by ID02,FUNCT_1:23; then
    J.(L.(I.(y0-y))) =(J*(L*I)).(y0-y) by PP5,FUNCT_1:22; then
    J.(L.(I.(y0-y))) =L0.(y0-y) by RELAT_1:55;
    hence g.y0-g.y = L0.(y0-y) + R0.(y0-y) by PP6,PP2,PP1,Q3,ID07;
   end; then
   g is_differentiable_in y by N2,PN3,FDIFF_1:def 5; then
   diff(g,y)=L0.1 by N2,PN3,PN4,FDIFF_1:def 6;
   hence thesis by N2,PN3,PN4,FDIFF_1:def 5;
end;

theorem Lm0120R:
f=<>*g & x=<*y*> & g is_differentiable_in y implies
 f is_differentiable_in x & diff(f,x).<*1*> =<*diff(g,y)*>
proof
   assume AS0:f=<>*g & x=<*y*> & g is_differentiable_in y;
   reconsider x0=y as Real;
   reconsider I=proj(1,1)" as Function of REAL,REAL 1 by ID02;
   set J =proj(1,1);
   consider N0 being Neighbourhood of x0 such that
A2: N0 c= dom g &
    ex L0 be LINEAR, R0 be REST st
     diff(g,x0)=L0.1 &
     for y0 be Real st y0 in N0 holds
      g.y0 - g.x0 = L0.(y0-x0) + R0.(y0-x0) by AS0,FDIFF_1:def 6;
   consider e0 be real number such that
P1: 0 < e0 & N0 = ].x0 - e0,x0 + e0.[ by RCOMP_1:def 7;
   reconsider e = e0 as Real by XREAL_0:def 1;
   set N = {z where z is Point of REAL-NS 1 : ||.z-x.|| < e};
   N c= the carrier of REAL-NS 1
   proof
    let y be set;
    assume y in N; then
    ex z be Point of REAL-NS 1 st y=z & ||.z-x.|| < e;
    hence y in the carrier of REAL-NS 1;
   end; then
P2:N is Neighbourhood of x by NFCONT_1:def 3,P1;
R1:J*I=id REAL by ID01,FUNCT_1:61;
K1:dom g c= REAL;
K2:rng(g*J) c= REAL;
   J*f=J*(I*(g*J)) by AS0,RELAT_1:55; then
   J*f=(id REAL)*(g*J) by R1,RELAT_1:55; then
   J*f*I = g*J*I by RELAT_1:79,K2; then
   J*f*I = g*(id REAL) by R1,RELAT_1:55; then
   g=J*f*I by RELAT_1:77,K1; then
U1:dom g = I"(dom(J*f)) by RELAT_1:182;
   rng f c= the carrier of REAL-NS 1; then
   rng f c= dom J by ID01,REAL_NS1:def 4; then
U3:I.:(dom g) = I.:(I"(dom f)) by U1,RELAT_1:46;
   dom f c= the carrier of REAL-NS 1; then
   dom f c= rng I by ID02,REAL_NS1:def 4; then
U5:I.:(dom g) = dom f by U3,FUNCT_1:147;
U4:N=I.:N0
   proof
    now let z be set;
     hereby assume z in N; then
      consider w be Point of REAL-NS 1 such that
Y1:    z=w & ||.w-x.|| < e;
      reconsider y=J.w as Element of REAL;
      J.x=x0 by AS0,ID01; then
      J.(w-x) =y-x0 by ID08; then
      abs(y-x0) < e by ID08,Y1; then
Y2:   y in N0 by P1,RCOMP_1:8;
      w in the carrier of REAL-NS 1; then
      w in dom J by ID01, REAL_NS1:def 4; then
      w=I.y by FUNCT_1:56;
      hence z in I.:N0 by Y1,Y2,FUNCT_2:43;
     end;
     assume z in I.:N0; then
     consider yy be set such that
Y1:   yy in REAL & yy in N0 & z=I.yy by FUNCT_2:115;
     reconsider y=yy as Element of REAL by Y1;
Y2:  abs(y-x0) < e by Y1,RCOMP_1:8,P1;
     reconsider w=I.y as Point of REAL-NS 1 by REAL_NS1:def 4;
     I.x0=x by AS0,ID01; then
     w-x =I.(y-x0) by ID07; then
     ||.w-x.|| < e by ID07,Y2;
     hence z in N by Y1;
    end;
    hence thesis by TARSKI:2; 
   end; then
P3:N c= dom f by U5,A2,RELAT_1:156;
   consider L0 be LINEAR, R0 be REST such that
A3: diff(g,x0)=L0.1 & 
    for y0 be Real st y0 in N0 holds 
     g.y0 - g.x0 = L0.(y0-x0) + R0.(y0-x0) by A2;
   reconsider L=I*L0*J as bounded LinearOperator of REAL-NS 1,REAL-NS 1
     by ID10;
A4:L is Point of R_NormSpace_of_BoundedLinearOperators(REAL-NS 1,REAL-NS 1)
     by LOPBAN_1:def 10;
   reconsider R=I*R0*J as REST of REAL-NS 1,REAL-NS 1 by ID10;
   J*I=id REAL by ID01,FUNCT_1:61; then
   (J*I).:N0 = N0 by FRECHET:13; then
UR4:J.:N = N0 by RELAT_1:159,U4;
P4:for y be Point of REAL-NS 1 st y in N holds f/.y - f/.x = L.(y-x) + R/.(y-x)
   proof
    let y be Point of REAL-NS 1;
    assume Q1: y in N;
Q01:x in N by P2,NFCONT_1:4;
    y in the carrier of REAL-NS 1; then
    y in REAL 1 by REAL_NS1:def 4; then
Q2: J.y in N0 by FUNCT_2:43,Q1,UR4;
Q3: J.x=x0 by AS0,ID01;
    reconsider y0 = J.y as Element of REAL;
    reconsider p1=I.(g.y0),p2=I.(g.x0), q1=I.(L0.(y0-x0)), q2=I.(R0.(y0-x0))
      as VECTOR of REAL-NS 1 by REAL_NS1:def 4;
    I.(g.y0-g.x0) = I.(L0.(y0-x0) + R0.(y0-x0)) by A3,Q2; then
    p1-p2=I.(L0.(y0-x0) + R0.(y0-x0)) by ID07; then
    p1-p2=q1+q2 by ID07; then
    I.(g.y0) - I.(g.x0)=q1+q2 by REAL_NS1:5; then
    I.(g.(J.y))-I.(g.(J.x))=I.(L0.(y0-x0)) + I.(R0.(y0-x0)) 
      by REAL_NS1:2,Q3; then
    I.(g.(J.y))-I.(g.(J.x))=I.(L0.(J.(y-x))) + I.(R0.(y0-x0)) by ID08,Q3; then
PP1:I.(g.(J.y))-I.(g.(J.x))=I.(L0.(J.(y-x))) + I.(R0.(J.(y-x))) by ID08,Q3;
    y in dom f & x in dom f by Q1,Q01,P3; then
PP3:y in dom(I*(g*J)) & x in dom(I*(g*J)) by AS0,RELAT_1:55;
    y in the carrier of REAL-NS 1 & x in the carrier of REAL-NS 1; then
    y in dom J & x in dom J by ID01,REAL_NS1:def 4; then
    I.(g.(J.y)) =I.((g*J).y) & I.(g.(J.x)) =I.((g*J).x) by FUNCT_1:23; then
    I.(g.(J.y)) =(I*(g*J)).y & I.(g.(J.x)) = (I*(g*J)).x 
      by PP3,FUNCT_1:22; then
    I.(g.(J.y)) =f.y & I.(g.(J.x)) =f.x by AS0,RELAT_1:55; then
    I.(g.(J.y)) =f/.y & I.(g.(J.x)) =f/.x by P3,Q1,Q01,FINSEQ_4:def 4; then
PP4:I.(g.(J.y))-I.(g.(J.x)) = f/.y -f/.x by REAL_NS1:5;
    dom L = the carrier of REAL-NS 1 by FUNCT_2:def 1; then
    y-x in dom(I*L0*J); then
PP5:y-x in dom(I*(L0*J)) by RELAT_1:55;
    R is total by NDIFF_1:def 5; then
PP6:dom(I*R0*J) = the carrier of REAL-NS 1 by PARTFUN1:def 4; then
    y-x in dom(I*R0*J); then
PP7:y-x in dom(I*(R0*J)) by RELAT_1:55;
    y-x in the carrier of REAL-NS 1; then
PP8:y-x in dom J by ID01,REAL_NS1:def 4; then
    I.(R0.(J.(y-x))) =I.((R0*J).(y-x)) by FUNCT_1:23; then
    I.(R0.(J.(y-x))) =(I*(R0*J)).(y-x) by FUNCT_1:22,PP7; then
    I.(R0.(J.(y-x))) =(I*R0*J).(y-x) by RELAT_1:55; then
PP9:I.(R0.(J.(y-x))) =R/.(y-x) by PP6,FINSEQ_4:def 4;
    I.(L0.(J.(y-x))) =I.((L0*J).(y-x)) by PP8,FUNCT_1:23; then
    I.(L0.(J.(y-x))) =(I*(L0*J)).(y-x) by PP5,FUNCT_1:22; then
    I.(L0.(J.(y-x))) =L.(y-x) by RELAT_1:55;
    hence f/.y-f/.x = L.(y-x) + R/.(y-x) by PP9,REAL_NS1:2,PP4,PP1;
   end; then
   f is_differentiable_in x by A4,NDIFF_1:def 6,P2,P3; then
   diff(f,x)=I*L0*J by NDIFF_1:def 7,P2,P3,P4,A4; then
   diff(f,x).<*1*> =(I*L0*J).(I.1) by ID01; then
P6:diff(f,x).<*1*> =(I*L0).(J.(I.1)) by ID01,FUNCT_1:23;
   L0 is total by FDIFF_1:def 4; then
   dom L0 = REAL by PARTFUN1:def 4; then
   diff(f,x).<*1*> =I.(L0.(J.(I.1))) by P6,FUNCT_1:23; then
   diff(f,x).<*1*> =I.(L0.1) by FUNCT_1:57,ID01;
   hence thesis by A3,A4,NDIFF_1:def 6,P2,P3,P4,ID01;
end;

theorem
f=<>*g & x=<*y*> implies
(f is_differentiable_in x iff g is_differentiable_in y) by Lm0120L,Lm0120R;

theorem Lm0140:
f=<>*g & x=<*y*> & f is_differentiable_in x implies
 diff(f,x).<*1*>=<*diff(g,y)*>
proof
   assume AS: f=<>*g & x=<*y*> & f is_differentiable_in x; then
   g is_differentiable_in y by Lm0120L;
   hence thesis by Lm0120R,AS;
end;

begin :: Partial Differentiation

 reserve m,n for non empty Element of NAT;
 reserve i,j for Element of NAT;
 reserve f for PartFunc of REAL-NS n,REAL-NS 1;
 reserve g for PartFunc of REAL n,REAL;
 reserve x for Point of REAL-NS n;
 reserve y for Element of REAL n;

definition
  let n,m be non empty Element of NAT;
  let i be Element of NAT;
  let f be PartFunc of REAL-NS m,REAL-NS n;
  let x be Point of REAL-NS m;
  pred f is_partial_differentiable_in x,i means :Def9:
    f*reproj(i,x) is_differentiable_in Proj(i,m).x;
end;

definition
  let m,n be non empty Element of NAT;
  let i be Element of NAT;
  let f be PartFunc of REAL-NS m, REAL-NS n;
  let x be Point of REAL-NS m;
  func partdiff(f,x,i) ->
  Point of R_NormSpace_of_BoundedLinearOperators(REAL-NS 1,REAL-NS n) equals
    diff(f*reproj(i,x),Proj(i,m).x);
  coherence;
end;

definition
  let n be non empty Element of NAT;
  let i be Element of NAT;
  let f be PartFunc of REAL n,REAL;
  let x be Element of REAL n;
  pred f is_partial_differentiable_in x,i means :Def11:
    f*reproj(i,x) is_differentiable_in proj(i,n).x;
end;

definition
  let n be non empty Element of NAT;
  let i be Element of NAT;
  let f be PartFunc of REAL n,REAL;
  let x be Element of REAL n;
  func partdiff(f,x,i) -> real number equals
    diff(f*reproj(i,x),proj(i,n).x);
  coherence;
end;

theorem ID03:
Proj(i,n)=proj(1,1)"*proj(i,n)
proof
P1:the carrier of REAL-NS 1 = REAL 1 &
   the carrier of REAL-NS n = REAL n by REAL_NS1:def 4;
   reconsider h = proj(1,1)" as Function of REAL,REAL 1 by ID02;
   now let x be Element of REAL n;
    reconsider z=x as Point of REAL-NS n by REAL_NS1:def 4;
X1: (h*proj(i,n)).x =h.(proj(i,n).x) by FUNCT_2:21;
    hence (h*proj(i,n)).x =<*proj(i,n).x*> by ID01;
    Proj(i,n).x =Proj(i,n).z; then
    Proj(i,n).x =<*proj(i,n).x*> by Def4;
    hence Proj(i,n).x = (h*proj(i,n)).x by X1,ID01;
   end;
   hence thesis by P1,FUNCT_2:113;
end;

theorem ID04:
x = y implies reproj(i,y)*proj(1,1)=reproj(i,x)
proof
   assume AS: x=y;
P1:the carrier of REAL-NS 1 = REAL 1 &
   the carrier of REAL-NS n = REAL n by REAL_NS1:def 4;
   reconsider k = proj(1,1) as Function of REAL 1,REAL;
   now let s be Element of REAL 1;
    reconsider r=s as Point of REAL-NS 1 by REAL_NS1:def 4;
    consider q be Element of REAL, z be Element of REAL n such that
P3:  r=<*q*> & z=x & reproj(i,x).r=reproj(i,z).q by Def6;
    (reproj(i,y)*k).s = reproj(i,y).(k.s) by FUNCT_2:21;
    hence reproj(i,x).s=(reproj(i,y)*k).s by AS,ID01,P3;
   end;
   hence thesis by P1,FUNCT_2:113;
end;

theorem Lm0X01:
f=<>*g & x=y implies <>*(g*reproj(i,y)) = f*reproj(i,x)
proof
   assume AS: f=<>*g & x=y;
   reconsider h=proj(1,1)" as Function of REAL,REAL 1 by ID02;
   reproj(i,y)*proj(1,1) = reproj(i,x) by AS,ID04; then
   (h*g)*reproj(i,y)*proj(1,1) = f*reproj(i,x) by AS,RELAT_1:55;
   hence thesis by RELAT_1:55;
end;

theorem Lm0200:
f=<>*g & x=y implies
 (f is_partial_differentiable_in x,i iff g is_partial_differentiable_in y,i)
proof
   assume f=<>*g& x=y; then
P1:f*reproj(i,x)=<>*(g*reproj(i,y)) & <*proj(i,n).y*> = Proj(i,n).x
     by Lm0X01,Def4;
   hereby assume f is_partial_differentiable_in x,i; then
    f*reproj(i,x) is_differentiable_in Proj(i,n).x by Def9; then
    g*reproj(i,y) is_differentiable_in proj(i,n).y by P1,Lm0120L;
    hence g is_partial_differentiable_in y,i by Def11;
   end;
   assume g is_partial_differentiable_in y,i; then
   g*reproj(i,y) is_differentiable_in proj(i,n).y by Def11; then
   f*reproj(i,x) is_differentiable_in Proj(i,n).x by P1,Lm0120R;
   hence f is_partial_differentiable_in x,i by Def9;
end;

theorem Lm0210:
  f = <>*g & x = y & f is_partial_differentiable_in x,i implies
    partdiff(f,x,i).<*1*> = <*partdiff(g,y,i)*>
proof
   assume f=<>*g & x=y & f is_partial_differentiable_in x,i; then
   f*reproj(i,x) =<>*(g*reproj(i,y)) &
   f*reproj(i,x) is_differentiable_in Proj(i,n).x &
   <*proj(i,n).y*> = Proj(i,n).x by Lm0X01,Def4,Def9;
   hence thesis by Lm0140;
end;

definition
  let m,n be non empty Element of NAT;
  let i be Element of NAT;
  let f be PartFunc of REAL m,REAL n;
  let x be Element of REAL m;
  pred f is_partial_differentiable_in x,i means :Def13:
   ex g be PartFunc of REAL-NS m,REAL-NS n, y be Point of REAL-NS m st
    f=g & x=y & g is_partial_differentiable_in y,i;
end;

definition
  let m,n be non empty Element of NAT;
  let i be Element of NAT;
  let f be PartFunc of REAL m,REAL n;
  let x be Element of REAL m;
  assume A1: f is_partial_differentiable_in x,i;
  func partdiff(f,x,i) -> Element of REAL n means :Def14:
   ex g be PartFunc of REAL-NS m,REAL-NS n, y be Point of REAL-NS m st
    f=g & x=y & it = partdiff(g,y,i).<*1*>;
correctness
proof
   consider g be PartFunc of REAL-NS m,REAL-NS n,
            y be Point of REAL-NS m such that
A2: f=g & x=y & g is_partial_differentiable_in y,i by A1,Def13;
P0:<*1*> is Element of REAL 1 by FINSEQ_2:118;
   the carrier of REAL-NS 1 = REAL 1 & the carrier of REAL-NS n = REAL n
     by REAL_NS1:def 4; then
   partdiff(g,y,i) is Function of REAL 1,REAL n by LOPBAN_1:def 10; then
   partdiff(g,y,i).<*1*> is Element of REAL n by P0,FUNCT_2:7;
   hence thesis by A2;
end;
end;

theorem Lm0300:
for m,n be non empty Element of NAT,
    F be PartFunc of REAL-NS m,REAL-NS n, G be PartFunc of REAL m,REAL n,
    x be Point of REAL-NS m, y be Element of REAL m st
F = G & x = y holds
 F is_partial_differentiable_in x,i iff G is_partial_differentiable_in y,i
proof
   let m,n be non empty Element of NAT,
    F be PartFunc of REAL-NS m,REAL-NS n, G be PartFunc of REAL m,REAL n,
    x be Point of REAL-NS m, y be Element of REAL m;
   assume AS: F=G & x=y;
   now assume G is_partial_differentiable_in y,i; then
    ex f1 be PartFunc of REAL-NS m,REAL-NS n, y1 be Point of REAL-NS m st
     G=f1 & y=y1 & f1 is_partial_differentiable_in y1,i by Def13;
    hence F is_partial_differentiable_in x,i by AS;
   end;
   hence thesis by Def13,AS;
end;

theorem Lm0310:
for m,n be non empty Element of NAT,
    F be PartFunc of REAL-NS m,REAL-NS n, G be PartFunc of REAL m,REAL n,
    x be Point of REAL-NS m, y be Element of REAL m st
F=G & x=y & F is_partial_differentiable_in x,i holds
 partdiff(F,x,i).<*1*> = partdiff(G,y,i)
proof   
   let m,n be non empty Element of NAT,
    F be PartFunc of REAL-NS m,REAL-NS n, G be PartFunc of REAL m,REAL n,
    x be Point of REAL-NS m, y be Element of REAL m;
   assume AS: F=G & x=y & F is_partial_differentiable_in x,i; then
A1:G is_partial_differentiable_in y,i by Lm0300;
P0:<*1*> is Element of REAL 1 by FINSEQ_2:118;
   the carrier of REAL-NS 1 = REAL 1 & 
   the carrier of REAL-NS n = REAL n by REAL_NS1:def 4; then
   partdiff(F,x,i) is Function of REAL 1,REAL n by LOPBAN_1:def 10; then
   partdiff(F,x,i).<*1*> is Element of REAL n by P0,FUNCT_2:7;
   hence partdiff(F,x,i).<*1*> = partdiff(G,y,i) by AS,A1,Def14;
end;

theorem
for g1 be PartFunc of REAL n,REAL 1 holds
g1 = <>*g implies
 (g1 is_partial_differentiable_in y,i iff g is_partial_differentiable_in y,i)
proof
   let g1 be PartFunc of REAL n,REAL 1;
   assume AS: g1=<>*g;
   the carrier of REAL-NS 1 = REAL 1 &
   the carrier of REAL-NS n = REAL n by REAL_NS1:def 4; then
   reconsider h=g1 as PartFunc of REAL-NS n,REAL-NS 1;
   reconsider y'=y as Point of REAL-NS n by REAL_NS1:def 4;
   h is_partial_differentiable_in y',i iff g1 is_partial_differentiable_in y,i
     by Lm0300;
   hence thesis by AS,Lm0200;
end;

theorem
for g1 be PartFunc of REAL n,REAL 1 st
g1 = <>*g & g1 is_partial_differentiable_in y,i holds
  partdiff(g1,y,i) = <*partdiff(g,y,i)*>
proof
   let g1 be PartFunc of REAL n,REAL 1;
   assume AS: g1=<>*g & g1 is_partial_differentiable_in y,i;
   the carrier of REAL-NS 1 = REAL 1 &
   the carrier of REAL-NS n = REAL n by REAL_NS1:def 4; then
   reconsider h=g1 as PartFunc of REAL-NS n,REAL-NS 1;
   reconsider y'=y as Point of REAL-NS n by REAL_NS1:def 4;
P2:h is_partial_differentiable_in y',i by AS,Lm0300; then
   partdiff(h,y',i).<*1*>= partdiff(g1,y,i) by Lm0310;
   hence thesis by P2,AS,Lm0210;
end;

begin :: Linearity of Partial Differential Operator

 reserve X for set;
 reserve r for Real;
 reserve f,f1,f2 for PartFunc of REAL-NS m,REAL-NS n;
 reserve g,g1,g2 for PartFunc of REAL n,REAL;
 reserve h for PartFunc of REAL m,REAL n;
 reserve x for Point of REAL-NS m;
 reserve y for Element of REAL n;
 reserve z for Element of REAL m;

definition
  let m,n be non empty Element of NAT;
  let i,j be Element of NAT;
  let f be PartFunc of REAL-NS m,REAL-NS n;
  let x be Point of REAL-NS m;
  pred f is_partial_differentiable_in x,i,j means :Def15:
    Proj(j,n)*f*reproj(i,x) is_differentiable_in Proj(i,m).x;
end;

definition
  let m,n be non empty Element of NAT;
  let i,j be Element of NAT;
  let f be PartFunc of REAL-NS m,REAL-NS n;
  let x be Point of REAL-NS m;
  func partdiff(f,x,i,j) ->
    Point of R_NormSpace_of_BoundedLinearOperators(REAL-NS 1,REAL-NS 1) equals
    diff(Proj(j,n)*f*reproj(i,x),Proj(i,m).x);
  correctness;
end;

definition
  let m,n be non empty Element of NAT;
  let i,j be Element of NAT;
  let h be PartFunc of REAL m,REAL n;
  let z be Element of REAL m;
  pred h is_partial_differentiable_in z,i,j means :Def17:
    proj(j,n)*h*reproj(i,z) is_differentiable_in proj(i,m).z;
end;

definition
  let m,n be non empty Element of NAT;
  let i,j be Element of NAT;
  let h be PartFunc of REAL m,REAL n;
  let z be Element of REAL m;
  func partdiff(h,z,i,j) -> real number equals
    diff(proj(j,n)*h*reproj(i,z),proj(i,m).z);
  correctness;
end;

theorem Lm0100:
for m,n be non empty Element of NAT,
    F be PartFunc of REAL-NS m,REAL-NS n, G be PartFunc of REAL m,REAL n,
    x be Point of REAL-NS m, y be Element of REAL m st
F=G & x=y holds F is_differentiable_in x iff G is_differentiable_in y
proof
   let m,n be non empty Element of NAT,
    F be PartFunc of REAL-NS m,REAL-NS n, G be PartFunc of REAL m,REAL n,
    x be Point of REAL-NS m, y be Element of REAL m;
   assume AS: F=G & x=y;
   now assume G is_differentiable_in y; then
    ex f1 be PartFunc of REAL-NS m,REAL-NS n, y1 be Point of REAL-NS m st
     G=f1 & y=y1 & f1 is_differentiable_in y1 by Def7;
    hence F is_differentiable_in x by AS;
   end;
   hence thesis by Def7,AS;
end;

theorem
for m,n be non empty Element of NAT,
    F be PartFunc of REAL-NS m,REAL-NS n, G be PartFunc of REAL m,REAL n,
    x be Point of REAL-NS m, y be Element of REAL m st
F=G & x=y & F is_differentiable_in x holds diff(F,x)=diff(G,y)
proof   
   let m,n be non empty Element of NAT,
    F be PartFunc of REAL-NS m,REAL-NS n, G be PartFunc of REAL m,REAL n,
    x be Point of REAL-NS m, y be Element of REAL m;
   assume AS: F=G & x=y & F is_differentiable_in x; then
A1:G is_differentiable_in y by Lm0100;
   the carrier of REAL-NS m = REAL m & the carrier of REAL-NS n = REAL n 
      by REAL_NS1:def 4; then
   diff(F,x) is Function of REAL m,REAL n by LOPBAN_1:def 10;
   hence diff(G,y) = diff(F,x) by AS,A1,Def8;
end;

theorem Lm0X02:
f=h & x=z implies Proj(j,n)*f*reproj(i,x) = <>*(proj(j,n)*h*reproj(i,z))
proof
   assume AS: f=h & x=z;
   reconsider h1=proj(1,1)" as Function of REAL,REAL 1 by ID02;
   <>*(proj(j,n)*h*reproj(i,z))
    = h1*(proj(j,n)*(h*reproj(i,z)))*proj(1,1) by RELAT_1:55
   .= h1*proj(j,n)*(h*reproj(i,z))*proj(1,1) by RELAT_1:55
   .= h1*proj(j,n)*(h*reproj(i,z)*proj(1,1)) by RELAT_1:55
   .= h1*proj(j,n)*(h*(reproj(i,z)*proj(1,1))) by RELAT_1:55
   .= Proj(j,n)*(h*(reproj(i,z)*proj(1,1))) by ID03
   .= Proj(j,n)*(h*reproj(i,x)) by ID04,AS;
   hence thesis by AS,RELAT_1:55;
end;

theorem
f = h & x = z implies
 (f is_partial_differentiable_in x,i,j 
   iff h is_partial_differentiable_in z,i,j)
proof
   assume AS: f=h & x=z; then
P2:Proj(j,n)*f*reproj(i,x) = <>*(proj(j,n)*h*reproj(i,z)) by Lm0X02;
P3:Proj(i,m).x=<*proj(i,m).z*> by AS,Def4;
   hereby assume f is_partial_differentiable_in x,i,j; then
    Proj(j,n)*f*reproj(i,x) is_differentiable_in Proj(i,m).x
      by Def15; then
    proj(j,n)*h*reproj(i,z) is_differentiable_in proj(i,m).z by P2,P3,Lm0120L;
    hence h is_partial_differentiable_in z,i,j by Def17;
   end;
   assume h is_partial_differentiable_in z,i,j; then
   proj(j,n)*h*reproj(i,z) is_differentiable_in proj(i,m).z by Def17; then
   Proj(j,n)*f*reproj(i,x) is_differentiable_in Proj(i,m).x by P2,P3,Lm0120R;
   hence f is_partial_differentiable_in x,i,j by Def15;
end;

theorem
f=h & x=z & f is_partial_differentiable_in x,i,j implies
 partdiff(f,x,i,j).<*1*> = <*partdiff(h,z,i,j)*>
proof
   assume AS: f=h & x=z & f is_partial_differentiable_in x,i,j; then
P2: Proj(j,n)*f*reproj(i,x) =<>*(proj(j,n)*h*reproj(i,z)) by Lm0X02;
P3:Proj(i,m).x=<*proj(i,m).z*> by AS,Def4;
   Proj(j,n)*f*reproj(i,x) is_differentiable_in Proj(i,m).x by AS,Def15;
   hence thesis by P2,P3,Lm0140;
end;

definition
  let m,n be non empty Element of NAT;
  let i be Element of NAT;
  let f be PartFunc of REAL-NS m,REAL-NS n;
  let X be set;
  pred f is_partial_differentiable_on X,i means :Def19:
    X c=dom f &
    for x be Point of REAL-NS m st x in X holds
    f|X is_partial_differentiable_in x,i;
end;

theorem Th35:
  f is_partial_differentiable_on X,i implies X is Subset of REAL-NS m
proof
   assume f is_partial_differentiable_on X,i; then
   X c=dom f by Def19;
   hence thesis by XBOOLE_1:1;
end;

definition
let m,n be non empty Element of NAT;
let i be Element of NAT;
let f be PartFunc of REAL-NS m,REAL-NS n;
let X;
assume A1:f is_partial_differentiable_on X,i;
 func f `partial|(X,i) -> PartFunc of
  REAL-NS m,R_NormSpace_of_BoundedLinearOperators(REAL-NS 1,REAL-NS n) means
  dom it = X &
  for x be Point of REAL-NS m st x in X holds it/.x = partdiff(f,x,i);
existence
proof
   defpred P[Element of REAL-NS m] means $1 in X;
   deffunc F(Element of REAL-NS m) = partdiff(f,$1,i);
   consider F being PartFunc of REAL-NS m,
    R_NormSpace_of_BoundedLinearOperators(REAL-NS 1,REAL-NS n) such that
A2: (for x be Point of REAL-NS m holds x in dom F iff P[x]) &
    for x be Point of REAL-NS m st x in dom F holds F.x = F(x) 
      from SEQ_1:sch 3;
   take F;
   for y be set st y in dom F holds y in X by A2; then
A3:dom F c= X by TARSKI:def 3;
   now let y be set such that
A4: y in X;
    X is Subset of REAL-NS m by A1,Th35;
    hence y in dom F by A2,A4;
   end; then
   X c= dom F by TARSKI:def 3;
   hence dom F = X by A3,XBOOLE_0:def 10;
   hereby let x be Point of REAL-NS m;
    assume x in X; then
A5: x in dom F by A2; then
    F.x = partdiff(f,x,i) by A2;
    hence F/.x = partdiff(f,x,i) by A5,FINSEQ_4:def 4;
   end;
end;
uniqueness
proof
   let F,G be PartFunc of
    REAL-NS m,R_NormSpace_of_BoundedLinearOperators(REAL-NS 1,REAL-NS n);
   assume that 
A6: dom F = X & 
    for x be Point of REAL-NS m st x in X holds F/.x = partdiff(f,x,i) and
A7: dom G = X &
    for x be Point of REAL-NS m st x in X holds G/.x = partdiff(f,x,i);
   now let x be Point of REAL-NS m;
    assume A8: x in dom F; then
    F/.x = partdiff(f,x,i) by A6;
    hence F/.x=G/.x by A6,A7,A8;
   end;
   hence thesis by A6,A7,PARTFUN2:3;
end;
end;

theorem ID05:
(f1+f2)*reproj(i,x) = f1*reproj(i,x)+f2*reproj(i,x) &
(f1-f2)*reproj(i,x) = f1*reproj(i,x)-f2*reproj(i,x)
proof
S0:dom(f1+f2) = dom f1 /\ dom f2 by VFUNCT_1:def 1;
T0:dom(f1-f2) = dom f1 /\ dom f2 by VFUNCT_1:def 2;
L1:dom(reproj(i,x))=the carrier of REAL-NS 1 by FUNCT_2:def 1;
S6:for s be Element of REAL-NS 1 holds
    s in dom((f1+f2)*reproj(i,x)) iff s in dom(f1*reproj(i,x)+f2*reproj(i,x))
   proof
    let s be Element of REAL-NS 1;
    s in dom((f1+f2)*reproj(i,x))
     iff reproj(i,x).s in dom f1 /\ dom f2 by S0,L1,FUNCT_1:21; then
    s in dom((f1+f2)*reproj(i,x))
     iff reproj(i,x).s in dom f1 & reproj(i,x).s in dom f2
       by XBOOLE_0:def 3; then
    s in dom((f1+f2)*reproj(i,x))
     iff s in dom(f1*reproj(i,x)) & s in dom(f2*reproj(i,x))
       by L1,FUNCT_1:21; then
    s in dom((f1+f2)*reproj(i,x))
     iff s in dom(f1*reproj(i,x)) /\ dom(f2*reproj(i,x)) by XBOOLE_0:def 3;
    hence thesis by VFUNCT_1:def 1;
   end;
T6:for s be Element of REAL-NS 1 holds
    s in dom((f1-f2)*reproj(i,x)) iff s in dom(f1*reproj(i,x)-f2*reproj(i,x))
   proof
    let s be Element of REAL-NS 1;
    s in dom((f1-f2)*reproj(i,x))
    iff reproj(i,x).s in dom f1 /\ dom f2 by T0,L1,FUNCT_1:21; then
    s in dom((f1-f2)*reproj(i,x))
    iff reproj(i,x).s in dom f1 & reproj(i,x).s in dom f2
      by XBOOLE_0:def 3; then
    s in dom((f1-f2)*reproj(i,x))
    iff s in dom(f1*reproj(i,x)) & s in dom(f2*reproj(i,x))
      by L1,FUNCT_1:21; then
    s in dom((f1-f2)*reproj(i,x))
    iff s in dom(f1*reproj(i,x)) /\ dom(f2*reproj(i,x)) by XBOOLE_0:def 3;
    hence thesis by VFUNCT_1:def 2;
   end;
   for s be set holds
    s in dom((f1+f2)*reproj(i,x))
    iff s in dom(f1*reproj(i,x) + f2*reproj(i,x)) by S6; then
P1:dom((f1+f2)*reproj(i,x)) = dom(f1*reproj(i,x)+f2*reproj(i,x)) by TARSKI:2;
   for s be set holds
    s in dom((f1-f2)*reproj(i,x))
    iff s in dom(f1*reproj(i,x) - f2*reproj(i,x)) by T6; then
R1:dom((f1-f2)*reproj(i,x)) = dom(f1*reproj(i,x)-f2*reproj(i,x)) by TARSKI:2;
P2:for z being Element of REAL-NS 1 st z in dom((f1+f2)*reproj(i,x)) holds
    ((f1+f2)*reproj(i,x)).z = (f1*reproj(i,x)+f2*reproj(i,x)).z
   proof
    let z be Element of REAL-NS 1;
    assume AS:z in dom((f1+f2)*reproj(i,x)); then
    reproj(i,x).z in dom f1 /\ dom f2 by S0,FUNCT_1:21; then
P29:reproj(i,x).z in dom f1 & reproj(i,x).z in dom f2 by XBOOLE_0:def 3; then
P31:z in dom(f1*reproj(i,x)) & z in dom(f2*reproj(i,x)) by L1,FUNCT_1:21;
P33:f1/.(reproj(i,x).z) = f1.(reproj(i,x).z) by P29,FINSEQ_4:def 4
     .=(f1*reproj(i,x)).z by P31,FUNCT_1:22
     .=(f1*reproj(i,x))/.z by P31,FINSEQ_4:def 4;
P34:f2/.(reproj(i,x).z) = f2.(reproj(i,x).z) by P29,FINSEQ_4:def 4
     .=(f2*reproj(i,x)).z by P31,FUNCT_1:22
     .=(f2*reproj(i,x))/.z by P31,FINSEQ_4:def 4;
P35:reproj(i,x).z in dom(f1+f2) by AS,FUNCT_1:21;
    ((f1+f2)*reproj(i,x)).z
     = (f1+f2).(reproj(i,x).z)  by AS,FUNCT_1:22
    .=(f1+f2)/.(reproj(i,x).z) by P35,FINSEQ_4:def 4
    .= f1/.(reproj(i,x).z) +f2/.(reproj(i,x).z) by VFUNCT_1:def 1,P35
    .=(f1*reproj(i,x)+ f2*reproj(i,x))/.z by VFUNCT_1:def 1,AS,P1,P33,P34;
    hence ((f1+f2)*reproj(i,x)).z = (f1*reproj(i,x)+ f2*reproj(i,x)).z
     by AS,P1,FINSEQ_4:def 4;
   end;
   for z being Element of REAL-NS 1 st z in dom((f1-f2)*reproj(i,x)) holds
    ((f1-f2)*reproj(i,x)).z = (f1*reproj(i,x)-f2*reproj(i,x)).z
   proof
    let z be Element of REAL-NS 1;
    assume AS:z in dom((f1-f2)*reproj(i,x)); then
    reproj(i,x).z in dom f1 /\ dom f2 by T0,FUNCT_1:21; then
R29:reproj(i,x).z in dom f1 & reproj(i,x).z in dom f2 by XBOOLE_0:def 3; then
R31:z in dom(f1* reproj(i,x)) & z in dom(f2* reproj(i,x)) by L1,FUNCT_1:21;
R33:f1/.(reproj(i,x).z) = f1.(reproj(i,x).z) by R29,FINSEQ_4:def 4
     .=(f1*reproj(i,x)).z by R31,FUNCT_1:22
     .=(f1*reproj(i,x))/.z by R31,FINSEQ_4:def 4;
R34:f2/.(reproj(i,x).z) = f2.(reproj(i,x).z) by R29,FINSEQ_4:def 4
     .=(f2*reproj(i,x)).z by R31,FUNCT_1:22
     .=(f2*reproj(i,x))/.z by R31,FINSEQ_4:def 4;
R35:reproj(i,x).z in dom (f1-f2) by AS,FUNCT_1:21;
    thus ((f1-f2)*reproj(i,x)).z
      =(f1-f2).(reproj(i,x).z) by AS,FUNCT_1:22
     .=(f1-f2)/.(reproj(i,x).z) by R35,FINSEQ_4:def 4
     .= f1/.(reproj(i,x).z) - f2/.(reproj(i,x).z) by VFUNCT_1:def 2,R35
     .=(f1*reproj(i,x)- f2*reproj(i,x))/.z by VFUNCT_1:def 2,AS,R1,R33,R34
     .=(f1*reproj(i,x)- f2*reproj(i,x)).z by AS,R1,FINSEQ_4:def 4;
   end;
   hence thesis by PARTFUN1:34,P1,P2,R1;
end;

theorem ID06:
r(#)(f*reproj(i,x)) = (r(#)f)*reproj(i,x)
proof
S0:dom(r(#)f) = dom f by VFUNCT_1:def 4;
L0:dom(reproj(i,x))=the carrier of REAL-NS 1 by FUNCT_2:def 1;
S3:for s be Element of REAL-NS 1 holds
    s in dom((r(#)f)*reproj(i,x)) iff reproj(i,x).s in dom(r(#)f)
   proof
    let s be Element of REAL-NS 1;
    dom(reproj(i,x))=the carrier of REAL-NS 1 by FUNCT_2:def 1;
    hence thesis by FUNCT_1:21;
   end;
S5:for s be Element of REAL-NS 1 holds
    s in dom((r(#)f)*reproj(i,x)) iff s in dom(f*reproj(i,x))
   proof
    let s be Element of REAL-NS 1;
    s in dom((r(#)f)*reproj(i,x)) iff reproj(i,x).s in dom(r(#)f)
      by L0,FUNCT_1:21;
    hence thesis by L0,S0,FUNCT_1:21;
   end;
M1:dom(r(#)(f*reproj(i,x))) =dom((r(#)f)*reproj(i,x))
   proof
    dom(r(#)(f*reproj(i,x))) =dom(f*reproj(i,x)) by VFUNCT_1:def 4; then
    for s be set holds
     s in dom(r(#)(f*reproj(i,x))) iff s in dom((r(#)f)*reproj(i,x)) by S5;
    hence thesis by TARSKI:2;
   end;
   for z being Element of REAL-NS 1 st z in dom(r(#)(f*reproj(i,x))) holds
    (r(#)(f*reproj(i,x))).z = ((r(#)f)*reproj(i,x)).z
   proof
    let z be Element of REAL-NS 1;
    assume AS: z in dom(r(#)(f*reproj(i,x))); then
M22:reproj(i,x).z in dom f by M1,S3,S0;
M23:z in dom(f*reproj(i,x)) by AS,VFUNCT_1:def 4;
M25:f/.(reproj(i,x).z)
      = f.(reproj(i,x).z) by M22,FINSEQ_4:def 4
     .= (f*reproj(i,x)).z by M23,FUNCT_1:22
     .= (f*reproj(i,x))/.z by M23,FINSEQ_4:def 4;
P22:(r(#)(f*reproj(i,x))).z
      =(r(#)(f*reproj(i,x)))/.z by AS,FINSEQ_4:def 4
     .= r * f/.(reproj(i,x).z) by M25,AS,VFUNCT_1:def 4;
    ((r(#)f)*reproj(i,x)).z
      = (r(#)f).(reproj(i,x).z) by AS,M1,FUNCT_1:22
     .= (r(#)f)/.(reproj(i,x).z) by M22,S0,FINSEQ_4:def 4
     .= r * f/.(reproj(i,x).z) by M22,S0,VFUNCT_1:def 4;
    hence thesis by P22;
   end;
   hence r(#)(f*reproj(i,x)) = (r(#)f)*reproj(i,x) by PARTFUN1:34,M1;
end;

theorem Th40:
f1 is_partial_differentiable_in x,i & f2 is_partial_differentiable_in x,i
 implies
  f1+f2 is_partial_differentiable_in x,i &
        partdiff(f1+f2,x,i)=partdiff(f1,x,i)+partdiff(f2,x,i)
proof
   assume f1 is_partial_differentiable_in x,i &
          f2 is_partial_differentiable_in x,i; then
A1:f1*reproj(i,x) is_differentiable_in Proj(i,m).x &
   f2*reproj(i,x) is_differentiable_in Proj(i,m).x by Def9;
   (f1+f2)*reproj(i,x) = f1*reproj(i,x)+f2*reproj(i,x) by ID05; then
   (f1+f2)*reproj(i,x) is_differentiable_in Proj(i,m).x by A1,NDIFF_1:40;
   hence f1+f2 is_partial_differentiable_in x,i by Def9;
   diff(f1*reproj(i,x)+f2*reproj(i,x),Proj(i,m).x)
    = partdiff((f1+f2),x,i) by ID05;
   hence partdiff(f1+f2,x,i)=partdiff(f1,x,i)+partdiff(f2,x,i)
     by A1,NDIFF_1:40;
end;

theorem
g1 is_partial_differentiable_in y,i & g2 is_partial_differentiable_in y,i
implies
 g1+g2 is_partial_differentiable_in y,i &
 partdiff(g1+g2,y,i) = partdiff(g1,y,i) + partdiff(g2,y,i)
proof
   assume AS:g1 is_partial_differentiable_in y,i &
             g2 is_partial_differentiable_in y,i;
E: the carrier of REAL-NS n = REAL n &
   the carrier of REAL-NS 1 = REAL 1 by REAL_NS1:def 4; then
   reconsider f1=<>*g1, f2=<>*g2 as PartFunc of REAL-NS n,REAL-NS 1;
   reconsider x=y as Point of REAL-NS n by REAL_NS1:def 4;
   rng g1 c= dom(proj(1,1)") & rng g2 c= dom(proj(1,1)") by ID02; then
E2a:dom(proj(1,1)"*g1) = dom g1 &
    dom(proj(1,1)"*g2) = dom g2 by RELAT_1:46; then
   dom(f1+f2) = dom g1 /\ dom g2 by VFUNCT_1:def 1; then
E2:dom(f1+f2) = dom(g1+g2) by SEQ_1:def 3;
E3a:rng(g1+g2) c= dom(proj(1,1)") by ID02; then
E3:dom(proj(1,1)"*(g1+g2)) = dom(g1+g2) by RELAT_1:46;
E4:dom(f1+f2) = dom <>*(g1+g2) by E3a,RELAT_1:46,E2;
   now let x be Element of the carrier of REAL-NS n;
    assume F1:x in dom(f1+f2); then
F2: x in dom f1 /\ dom f2 by VFUNCT_1:def 1; then
F2b:x in dom f1 & x in dom f2 by XBOOLE_0:def 3;
F2a:x in dom g1 & x in dom g2 by F2, XBOOLE_0:def 3,E2a;
    (f1+f2).x = (f1+f2)/.x by F1,FINSEQ_4:def 4; then
F3: (f1+f2).x = f1/.x + f2/.x by F1,VFUNCT_1:def 1;
    (<>*(g1+g2)).x = proj(1,1)".((g1+g2).x) by F1,E3,E2,FUNCT_1:22; then
F4: (<>*(g1+g2)).x = proj(1,1)".(g1.x + g2.x) by F1,E2,SEQ_1:def 3;
    f1/.x = (proj(1,1)"*g1).x & f2/.x = (proj(1,1)"*g2).x
       by F2b,FINSEQ_4:def 4; then
    f1/.x = proj(1,1)".(g1.x) & f2/.x = proj(1,1)".(g2.x) by F2a,FUNCT_1:23;
    hence (f1+f2).x = (<>*(g1+g2)).x by ID02,ID07,F3,F4;
   end; then
P0:f1+f2 = <>*(g1+g2) by E,E4,PARTFUN1:34;
P1:f1 is_partial_differentiable_in x,i & f2 is_partial_differentiable_in x,i
     by AS,Lm0200; then
P8:f1+f2 is_partial_differentiable_in x,i by Th40;
   reconsider One = <*1*> as VECTOR of REAL-NS 1 by E,FINSEQ_2:118;
PP3:partdiff(f1,x,i).One = <*partdiff(g1,y,i)*> &
    partdiff(f2,x,i).One = <*partdiff(g2,y,i)*> by Lm0210,P1;
   reconsider Pd1=<*partdiff(g1,y,i)*> as Element of REAL 1 by FINSEQ_2:118;
   reconsider Pd2=<*partdiff(g2,y,i)*> as Element of REAL 1 by FINSEQ_2:118;
   <*partdiff(g1+g2,y,i)*> 
    =partdiff(f1+f2,x,i).<*1*> by P0,P8,Lm0210
   .=(partdiff(f1,x,i)+partdiff(f2,x,i)).<*1*> by P1,Th40
   .=partdiff(f1,x,i).One + partdiff(f2,x,i).One by LOPBAN_1:41
   .=Pd1 + Pd2 by PP3,REAL_NS1:2
   .=<*partdiff(g1,y,i)+partdiff(g2,y,i)*> by RVSUM_1:29; then
   partdiff(g1+g2,y,i)
    =<*partdiff(g1,y,i) + partdiff(g2,y,i)*>.1 by FINSEQ_1:57;
   hence thesis by P8,Lm0200,P0,FINSEQ_1:57;
end;

theorem Th41:
f1 is_partial_differentiable_in x,i & f2 is_partial_differentiable_in x,i 
implies
 f1-f2 is_partial_differentiable_in x,i &
 partdiff(f1-f2,x,i)=partdiff(f1,x,i)-partdiff(f2,x,i)
proof
   assume f1 is_partial_differentiable_in x,i &
          f2 is_partial_differentiable_in x,i; then
A2:f1*reproj(i,x) is_differentiable_in Proj(i,m).x &
   f2*reproj(i,x) is_differentiable_in Proj(i,m).x by Def9;
   (f1-f2)*reproj(i,x) = f1*reproj(i,x)-f2*reproj(i,x) by ID05; then
   (f1-f2)*reproj(i,x) is_differentiable_in Proj(i,m).x by A2,NDIFF_1:41;
   hence f1-f2 is_partial_differentiable_in x,i by Def9;
   diff(f1*reproj(i,x)-f2*reproj(i,x),Proj(i,m).x)
    = partdiff(f1-f2,x,i) by ID05;
   hence partdiff(f1-f2,x,i)=partdiff(f1,x,i)-partdiff(f2,x,i)
      by A2,NDIFF_1:41;
end;

theorem
g1 is_partial_differentiable_in y,i & g2 is_partial_differentiable_in y,i 
implies
  g1-g2 is_partial_differentiable_in y,i &
  partdiff(g1-g2,y,i)=partdiff(g1,y,i)-partdiff(g2,y,i)
proof
   assume AS: g1 is_partial_differentiable_in y,i &
              g2 is_partial_differentiable_in y,i;
E: the carrier of REAL-NS n = REAL n &
   the carrier of REAL-NS 1 = REAL 1 by REAL_NS1:def 4; then
   reconsider f1=<>*g1, f2=<>*g2 as PartFunc of REAL-NS n,REAL-NS 1;
   reconsider x=y as Point of REAL-NS n by REAL_NS1:def 4;
   rng g1 c= dom(proj(1,1)") & rng g2 c= dom(proj(1,1)") by ID02; then
E2a:dom(proj(1,1)"*g1) = dom g1 &
    dom(proj(1,1)"*g2) = dom g2 by RELAT_1:46; then
   dom(f1-f2) = dom g1 /\ dom g2 by VFUNCT_1:def 2; then
E2:dom(f1-f2) = dom(g1-g2) by SEQ_1:def 4;
E3a:rng(g1-g2) c= dom(proj(1,1)") by ID02; then
E3:dom(proj(1,1)"*(g1-g2)) = dom(g1-g2) by RELAT_1:46; 
E4:dom(f1-f2) = dom <>*(g1-g2) by E3a,E2,RELAT_1:46;
   now let x be Element of the carrier of REAL-NS n;
    assume F1:x in dom(f1-f2); then
F2b:x in dom f1 /\ dom f2 by VFUNCT_1:def 2; then
F2: x in dom f1 & x in dom f2 by XBOOLE_0:def 3; 
F2a:x in dom g1 & x in dom g2 by F2b,XBOOLE_0:def 3,E2a;
    (f1-f2).x = (f1-f2)/.x by F1,FINSEQ_4:def 4; then
F3: (f1-f2).x = f1/.x - f2/.x by F1,VFUNCT_1:def 2;
    (<>*(g1-g2)).x = proj(1,1)".((g1-g2).x) by F1,E3,E2,FUNCT_1:22; then
F4: (<>*(g1-g2)).x = proj(1,1)".(g1.x-g2.x) by F1,E2,SEQ_1:def 4;
    f1/.x = (proj(1,1)"*g1).x & f2/.x = (proj(1,1)"*g2).x
       by F2,FINSEQ_4:def 4; then
    f1/.x = proj(1,1)".(g1.x) & f2/.x = proj(1,1)".(g2.x) by F2a,FUNCT_1:23;
    hence (f1-f2).x = (<>*(g1-g2)).x by F3,F4,ID02,ID07;
   end; then
P0:f1-f2=<>*(g1-g2) by E,E4,PARTFUN1:34;
P1:f1 is_partial_differentiable_in x,i &
   f2 is_partial_differentiable_in x,i by AS,Lm0200; then
P8:f1-f2 is_partial_differentiable_in x,i by Th41;
   reconsider One = <*1*> as VECTOR of REAL-NS 1 by FINSEQ_2:118,E;
PP3:partdiff(f1,x,i).One = <*partdiff(g1,y,i)*> &
    partdiff(f2,x,i).One = <*partdiff(g2,y,i)*> by Lm0210,P1;
   reconsider Pd1=<*partdiff(g1,y,i)*> as Element of REAL 1 by FINSEQ_2:118;
   reconsider Pd2=<*partdiff(g2,y,i)*> as Element of REAL 1 by FINSEQ_2:118;
   <*partdiff(g1-g2,y,i)*> = partdiff(f1-f2,x,i).<*1*> by P0,P8,Lm0210
   .= (partdiff(f1,x,i)-partdiff(f2,x,i)).<*1*> by P1,Th41
   .= partdiff(f1,x,i).One - partdiff(f2,x,i).One by LOPBAN_1:46
   .= Pd1 - Pd2 by PP3,REAL_NS1:5
   .= <*partdiff(g1,y,i) - partdiff(g2,y,i)*> by RVSUM_1:50; then
   partdiff(g1-g2,y,i) 
    = <*partdiff(g1,y,i)-partdiff(g2,y,i)*>.1 by FINSEQ_1:57;
   hence thesis by Lm0200,P0,P8,FINSEQ_1:57;
end;

theorem Th42:
f is_partial_differentiable_in x,i implies
 r(#)f is_partial_differentiable_in x,i &
 partdiff((r(#)f),x,i) = r*partdiff(f,x,i)
proof
   assume f is_partial_differentiable_in x,i; then
A1:f*reproj(i,x) is_differentiable_in Proj(i,m).x by Def9;
   r(#)(f*reproj(i,x)) = (r(#)f)*reproj(i,x) by ID06; then
   (r(#)f)*reproj(i,x) is_differentiable_in Proj(i,m).x by A1,NDIFF_1:42;
   hence r(#)f is_partial_differentiable_in x,i by Def9;
   diff(r(#)(f*reproj(i,x)),Proj(i,m).x) = partdiff(r(#)f,x,i) by ID06;
   hence partdiff(r(#)f,x,i)=r*partdiff(f,x,i) by A1,NDIFF_1:42;
end;

theorem
g is_partial_differentiable_in y,i implies
  r(#)g is_partial_differentiable_in y,i &
  partdiff(r(#)g,y,i) = r*partdiff(g,y,i)
proof
   assume AS: g is_partial_differentiable_in y,i;
E: the carrier of REAL-NS n = REAL n &
   the carrier of REAL-NS 1 = REAL 1 by REAL_NS1:def 4; then
   reconsider f=<>*g as PartFunc of REAL-NS n,REAL-NS 1;
   reconsider x=y as Point of REAL-NS n by REAL_NS1:def 4;
E1:dom(r(#)f) = dom(proj(1,1)"*g) by VFUNCT_1:def 4;
E2b:rng g c= dom(proj(1,1)") by ID02; then
E2a:dom(proj(1,1)"*g) = dom g by RELAT_1:46;
   dom(r(#)f) = dom g by RELAT_1:46,E2b,E1; then
E2:dom(r(#)f) = dom(r(#)g) by SEQ_1:def 6;
E3a:rng(r(#)g) c= dom(proj(1,1)") by ID02; then
E3:dom(proj(1,1)"*(r(#)g)) = dom(r(#)g) by RELAT_1:46;
E4:dom(r(#)f) = dom <>*(r(#)g) by E3a,E2,RELAT_1:46;
   now let x be Element of the carrier of REAL-NS n;
    assume F1:x in dom(r(#)f); then
F2: x in dom f by VFUNCT_1:def 4;
F2a:x in dom g by F1,VFUNCT_1:def 4,E2a; then
F2b:x in dom(r(#)g) by SEQ_1:def 6;
    (r(#)f).x = (r(#)f)/.x by F1,FINSEQ_4:def 4; then
F3: (r(#)f).x = r * (f/.x) by F1,VFUNCT_1:def 4;
F4: (<>*(r(#)g)).x = proj(1,1)".((r(#)g).x) by F1,E3,E2,FUNCT_1:22;
    f/.x = (proj(1,1)"*g).x by F2,FINSEQ_4:def 4; then
F5: f/.x = proj(1,1)".(g.x) by F2a,FUNCT_1:23;
    consider I be Function of REAL,REAL 1 such that
F6:  I is bijective & proj(1,1)" = I by ID02;
    r*(f/.x) = I.(r*(g.x)) by F5,F6,ID07;
    hence (r(#)f).x = (<>*(r(#)g)).x by F2b,SEQ_1:def 6,F3,F4,F6;
   end; then
P0:r(#)f=<>*(r(#)g) by E,E4,PARTFUN1:34;
P1:f is_partial_differentiable_in x,i by AS,Lm0200; then
P8:r(#)f is_partial_differentiable_in x,i by Th42;
   reconsider One = <*1*> as VECTOR of REAL-NS 1 by FINSEQ_2:118,E;
PP3:partdiff(f,x,i).One = <*partdiff(g,y,i)*> by Lm0210,P1;
   reconsider Pd = <*partdiff(g,y,i)*> as Element of REAL 1 by FINSEQ_2:118;
   <*partdiff(r(#)g,y,i)*> = partdiff(r(#)f,x,i).<*1*> by P0,P8,Lm0210
   .= (r*partdiff(f,x,i)).<*1*> by P1,Th42
   .= r*(partdiff(f,x,i).One) by LOPBAN_1:42
   .= r*Pd by PP3,REAL_NS1:3
   .= <*r*partdiff(g,y,i)*> by RVSUM_1:69; then
   partdiff(r(#)g,y,i) = <*r*partdiff(g,y,i)*>.1 by FINSEQ_1:57;
   hence thesis by Lm0200,P0,P8,FINSEQ_1:57;
end;

