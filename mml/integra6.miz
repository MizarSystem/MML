:: Integrability and the Integral of Partial Functions from $\Bbb R$ into
:: $\Bbb R$
::  by Noboru Endou , Yasunari Shidama and Masahiko Yamazaki
::
:: Received November 17, 2006
:: Copyright (c) 2006 Association of Mizar Users
 
environ

 vocabularies NUMBERS, XREAL_0, ORDINAL1, INTEGRA1, SUBSET_1, PARTFUN1,
      XXREAL_0, ARYTM_3, XXREAL_1, ARYTM_1, TARSKI, RELAT_1, FUNCT_3, XBOOLE_0,
      FUNCT_1, MEASURE7, INTEGRA5, ORDINAL2, COMPLEX1, CARD_1, REALSET1,
      XXREAL_2, REAL_1, VALUED_1, FINSEQ_1, NAT_1, INTEGRA2, SEQ_2, INTEGRA4,
      INT_1, RFINSEQ, ORDINAL4, CARD_3, FCONT_1, FDIFF_1, RSSPACE, SEQ_1,
      FUNCT_2, FUNCOP_1, RCOMP_1, VALUED_0, SEQ_4;
 notations TARSKI, XBOOLE_0, SUBSET_1, RELAT_1, ORDINAL1, NUMBERS, XCMPLX_0,
      XREAL_0, COMPLEX1, REAL_1, NAT_1, INT_1, FUNCT_1, PARTFUN1, FUNCT_2,
      FUNCOP_1, RCOMP_1, FINSEQ_1, VALUED_0, VALUED_1, SEQ_1, RFUNCT_1, SEQ_2,
      FDIFF_1, RFINSEQ, XXREAL_2, MEASURE6, RVSUM_1, INTEGRA1, INTEGRA2,
      FCONT_1, RSSPACE, INTEGRA3, INTEGRA4, INTEGRA5, XXREAL_0, SEQ_4;
 constructors PARTFUN1, REAL_1, NAT_1, BINOP_2, COMPLEX1, RFUNCT_1, RFUNCT_2,
      FCONT_1, FDIFF_1, RFINSEQ, RSSPACE, MEASURE6, INTEGRA2, INTEGRA4,
      INTEGRA5, SEQ_1, XXREAL_2, RVSUM_1, SEQ_4, RELSET_1, FUNCOP_1, SEQ_2,
      INTEGRA3;
 registrations XBOOLE_0, RELAT_1, ORDINAL1, FUNCT_2, NUMBERS, XXREAL_0,
      XREAL_0, NAT_1, INT_1, MEMBERED, FINSEQ_1, RCOMP_1, FDIFF_1, SEQM_3,
      INTEGRA1, VALUED_0, VALUED_1, FINSET_1, XXREAL_2, CARD_1, RELSET_1;
 requirements NUMERALS, BOOLE, SUBSET, REAL, ARITHM;
 definitions TARSKI, XCMPLX_0, INTEGRA1, FINSEQ_1, INTEGRA5, INTEGRA3;
 theorems ORDINAL1, PARTFUN1, INTEGRA1, RFUNCT_1, FUNCT_1, FINSEQ_1, RVSUM_1,
      SEQ_1, SEQ_2, FDIFF_1, ABSVALUE, NAT_1, INT_1, COMPLEX1, TARSKI,
      INTEGRA2, INTEGRA3, RFINSEQ, INTEGRA4, FINSEQ_2, FINSEQ_3, RCOMP_1,
      FCONT_1, XREAL_0, XXREAL_0, FUNCT_2, FINSEQ_5, XBOOLE_0, XBOOLE_1,
      XCMPLX_1, XREAL_1, INTEGRA5, RSSPACE, RELAT_1, VALUED_1, XXREAL_2,
      XXREAL_1, SEQM_3, FUNCOP_1, RELSET_1;
 schemes FUNCT_2, PARTFUN1, FINSEQ_2;

begin :: Preliminaries

reserve a,b,c,d,e,x for real number,
  A for closed-interval Subset of REAL,
  f,g for PartFunc of REAL,REAL;

theorem Th1: ::: XREAL_0
  a <= b & c <= d & a+c = b+d implies a = b & c = d
proof
  assume that
A1: a <=b & c <=d and
A2: a+c =b+d;
  assume not (a=b & c =d);
  then a < b or c < d by A1,XXREAL_0:1;
  hence contradiction by A1,A2,XREAL_1:10;
end;

theorem Th2: ::: XXREAL_1
  a <= b implies ].x-a,x+a.[ c= ].x-b,x+b.[
proof
  assume a <= b;
  then x-b <= x - a & x+a <= x+b by XREAL_1:9,12;
  hence thesis by XXREAL_1:46;
end;

theorem Th3:
  for R be Relation, A,B,C be set st A c= B & A c= C holds (R|B)|A
  = (R|C)|A
proof
  let R be Relation, A,B,C be set;
  assume that
A1: A c= B and
A2: A c= C;
  (R|C)|A = R|A by A2,RELAT_1:103;
  hence thesis by A1,RELAT_1:103;
end;

theorem Th4:
  for A,B,C be set st A c= B & A c= C holds chi(B,B)|A = chi(C,C)|A
proof
  let A,B,C be set;
  assume that
A1: A c= B and
A2: A c= C;
A3: now
A4: dom(chi(C,C)|A) =dom(chi(C,C)) /\ A by RELAT_1:90;
    assume
A5: A is non empty;
    then C is non empty by A2,XBOOLE_1:58,61;
    then dom(chi(C,C)|A) =C /\ A by A4,RFUNCT_1:77;
    then
A6: dom(chi(C,C)|A) =A by A2,XBOOLE_1:28;
A7: dom(chi(B,B)|A) =dom(chi(B,B)) /\ A by RELAT_1:90;
    B is non empty by A1,A5,XBOOLE_1:58,61;
    then dom(chi(B,B)|A) =B /\ A by A7,RFUNCT_1:77;
    then
A8: dom(chi(B,B)|A) =A by A1,XBOOLE_1:28;
    now
      let x be set;
      assume
A9:   x in A;
      then (chi(B,B)|A).x =(chi(B,B)).x by A8,FUNCT_1:70;
      then (chi(B,B)|A).x = 1 by A1,A9,RFUNCT_1:77;
      then (chi(B,B)|A).x = (chi(C,C)).x by A2,A9,RFUNCT_1:77;
      hence (chi(B,B)|A).x = (chi(C,C)|A).x by A6,A9,FUNCT_1:70;
    end;
    hence thesis by A8,A6,FUNCT_1:9;
  end;
  now
    assume
A10: A is empty;
    then chi(B,B)|A = {};
    hence thesis by A10;
  end;
  hence thesis by A3;
end;

theorem Th5:
  a <= b implies vol([' a,b '])=b-a
proof
  assume a<=b;
  then
A1: [' a,b '] = [.a,b.] by INTEGRA5:def 4;
  then
A2: [.a,b.] = [.lower_bound [.a,b.], upper_bound [.a,b.].] by INTEGRA1:5;
  then a=lower_bound([.a,b.]) by A1,INTEGRA1:6;
  hence thesis by A1,A2,INTEGRA1:6;
end;

theorem Th6:
  vol([' min(a,b),max(a,b) ']) = abs(b-a)
proof
  per cases;
  suppose
A1: a<= b;
    then min(a,b) = a & max(a,b) = b by XXREAL_0:def 9,def 10;
    then
A2: vol([' min(a,b),max(a,b) '])=b-a by Th5,XXREAL_0:25;
    0 <= b-a by A1,XREAL_1:50;
    hence thesis by A2,ABSVALUE:def 1;
  end;
  suppose
A3: not a<= b;
    then 0 <= a-b by XREAL_1:50;
    then
A4: a-b = abs(a-b) by ABSVALUE:def 1
      .=abs(b-a) by COMPLEX1:146;
    min(a,b) = b & max(a,b) = a by A3,XXREAL_0:def 9,def 10;
    hence thesis by A4,Th5,XXREAL_0:17;
  end;
end;

begin :: Integrability and The Integral of Partial Functions from REAL to REAL

Lm1: A c= dom f implies f||A is Function of A,REAL
proof
  rng f c= REAL;
  then
A1: f is Function of dom f,REAL by FUNCT_2:4;
  assume A c= dom f;
  hence thesis by A1,FUNCT_2:38;
end;

theorem Th7:
  A c= dom f & f is_integrable_on A & f|A is bounded implies abs f
  is_integrable_on A & abs(integral(f,A)) <= integral(abs f,A)
proof
A1: abs(f||A) = abs(f)||A by RFUNCT_1:62;
  assume A c= dom f;
  then
A2: f||A is Function of A,REAL by Lm1;
  assume f is_integrable_on A & f|A is bounded;
  then
A3: f||A is integrable & f||A|A is bounded by INTEGRA5:9,def 2;
  then abs(f||A) is integrable by A2,INTEGRA4:23;
  hence thesis by A3,A2,A1,INTEGRA4:23,INTEGRA5:def 2;
end;

theorem Th8:
  a<=b & [' a,b '] c= dom f & f is_integrable_on [' a,b '] & f|[' a
  ,b '] is bounded implies abs(integral(f,a,b)) <=integral(abs f,a,b)
proof
  assume a<=b;
  then
  integral(f,a,b) = integral(f,[' a,b ']) & integral(abs f,a,b) = integral
  (abs f,[' a,b ']) by INTEGRA5:def 5;
  hence thesis by Th7;
end;

theorem Th9:
  for r be Real st A c= dom f & f is_integrable_on A & f|A is
  bounded holds r(#)f is_integrable_on A & integral(r(#)f,A)=r*integral(f,A)
proof
  let r be Real;
  assume that
A1: A c= dom f and
A2: f is_integrable_on A & f|A is bounded;
A3: f||A is integrable & f||A|A is bounded by A2,INTEGRA5:9,def 2;
  rng f c= REAL;
  then f is Function of dom f,REAL by FUNCT_2:4;
  then
A4: f|A is Function of A,REAL by A1,FUNCT_2:38;
  r(#)(f||A) =(r(#)f)|A by RFUNCT_1:65;
  then (r(#)f)||A is integrable by A3,A4,INTEGRA2:31;
  hence r(#)f is_integrable_on A by INTEGRA5:def 2;
  integral((r(#)f),A) =integral(r(#)(f||A)) by RFUNCT_1:65;
  hence thesis by A3,A4,INTEGRA2:31;
end;

theorem Th10:
  a<=b & [' a,b '] c= dom f & f is_integrable_on [' a,b '] & f|['
  a,b '] is bounded implies integral(c(#)f,a,b) =c*integral(f,a,b)
proof
A1: c is Real by XREAL_0:def 1;
  assume that
A2: a<=b and
A3: [' a,b '] c= dom f & f is_integrable_on [' a,b '] & f|[' a,b '] is
  bounded;
  integral(f,a,b) = integral(f,[' a,b ']) & integral(c(#)f,a,b) = integral
  (c (#)f,[' a,b ']) by A2,INTEGRA5:def 5;
  hence thesis by A1,A3,Th9;
end;

theorem Th11:
  A c= dom f & A c= dom g & f is_integrable_on A & f|A is bounded
  & g is_integrable_on A & g|A is bounded implies f+g is_integrable_on A & f-g
is_integrable_on A & integral(f+g,A) = integral(f,A) + integral(g,A) & integral
  (f-g,A) = integral(f,A) - integral(g,A)
proof
  assume that
A1: A c= dom f & A c= dom g and
A2: f is_integrable_on A and
A3: f|A is bounded and
A4: g is_integrable_on A and
A5: g|A is bounded;
A6: f||A|A is bounded & g||A|A is bounded by A3,A5,INTEGRA5:9;
A7: f||A is Function of A,REAL & g||A is Function of A,REAL by A1,Lm1;
A8: f||A + g||A = (f+g)||A & f||A - g||A = (f-g)||A by RFUNCT_1:60,63;
A9: f||A is integrable & g||A is integrable by A2,A4,INTEGRA5:def 2;
  then f||A + g||A is integrable & f||A - g||A is integrable by A6,A7,
  INTEGRA1:59,INTEGRA2:33;
  hence f+g is_integrable_on A & f-g is_integrable_on A by A8, INTEGRA5:def
  2;
  thus thesis by A9,A6,A7,A8,INTEGRA1:59,INTEGRA2:33;
end;

theorem Th12:
  a<=b & [' a,b '] c= dom f & [' a,b '] c= dom g & f
  is_integrable_on [' a,b '] & g is_integrable_on [' a,b '] & f|[' a,b '] is
  bounded & g|[' a,b '] is bounded implies integral(f+g,a,b) =integral(f,a,b) +
  integral(g,a,b) & integral(f-g,a,b) =integral(f,a,b) - integral(g,a,b)
proof
  assume that
A1: a<=b and
A2: [' a,b '] c= dom f & [' a,b '] c= dom g & f is_integrable_on [' a,b
  '] & g is_integrable_on [' a,b '] & f|[' a,b '] is bounded & g|[' a,b '] is
  bounded;
A3: integral(f+g,a,b) = integral(f+g,[' a,b ']) & integral(f-g,a,b) =
  integral(f -g,[' a,b ']) by A1,INTEGRA5:def 5;
  integral(f,a,b) = integral(f,[' a,b ']) & integral(g,a,b) = integral(g,
  [' a, b ']) by A1,INTEGRA5:def 5;
  hence thesis by A2,A3,Th11;
end;

theorem
  f|A is bounded & g|A is bounded implies (f(#)g)|A is bounded
proof
  assume f|A is bounded & g|A is bounded;
  then (f(#)g)|(A /\ A) is bounded by RFUNCT_1:101;
  hence thesis;
end;

theorem
  A c= dom f & A c= dom g & f is_integrable_on A & f|A is bounded & g
  is_integrable_on A & g|A is bounded implies f(#)g is_integrable_on A
proof
  assume that
A1: A c= dom f & A c= dom g and
A2: f is_integrable_on A & f|A is bounded and
A3: g is_integrable_on A & g|A is bounded;
A4: f||A is integrable & f||A|A is bounded by A2,INTEGRA5:9,def 2;
A5: g||A is integrable & g||A|A is bounded by A3,INTEGRA5:9,def 2;
A6: (f||A)(#)(g||A) = (f(#)g)||A by INTEGRA5:4;
  f||A is Function of A,REAL & g||A is Function of A,REAL by A1,Lm1;
  then (f(#)g)||A is integrable by A4,A5,A6,INTEGRA4:29;
  hence thesis by INTEGRA5:def 2;
end;

theorem Th15:
  for n be Element of NAT st n > 0 & vol A > 0 holds ex D being
  Division of A st len D = n & for i be Element of NAT st i in dom D holds D.i=
  lower_bound A + vol(A)/n*i
proof
  let n be Element of NAT;
  assume that
A1: n > 0 and
A2: vol A > 0;
  deffunc F(Nat)=lower_bound A + vol(A)/n*$1;
  consider D being FinSequence of REAL such that
A3: len D = n & for i be Nat st i in dom D holds D.i=F(i) from FINSEQ_2:
  sch 1;
A4: for i,j be Element of NAT st i in dom D & j in dom D & i<j holds D.i < D
  .j
  proof
    let i,j be Element of NAT;
    assume that
A5: i in dom D and
A6: j in dom D and
A7: i < j;
    vol(A)/n > 0 by A1,A2,XREAL_1:141;
    then vol(A)/n*i < vol(A)/n*j by A7,XREAL_1:70;
    then lower_bound A + vol(A)/n*i < lower_bound A + vol(A)/n*j by XREAL_1:8;
    then D.i < lower_bound A + vol(A)/n*j by A3,A5;
    hence thesis by A3,A6;
  end;
A8: dom D = Seg n by A3,FINSEQ_1:def 3;
  reconsider D as non empty increasing FinSequence of REAL by A1,A3,A4,SEQM_3:
  def 1;
  D.(len D)=lower_bound A + vol(A)/n*n by A3,A8,FINSEQ_1:5;
  then
A9: D.(len D)=lower_bound A + vol(A) by A1,XCMPLX_1:88;
  for x1 being set st x1 in rng D holds x1 in A
  proof
    let x1 be set;
    assume x1 in rng D;
    then consider i be Element of NAT such that
A10: i in dom D and
A11: D.i=x1 by PARTFUN1:26;
A12: 1 <= i by A10,FINSEQ_3:27;
    i <= len D by A10,FINSEQ_3:27;
    then vol(A)/n*i <= vol(A)/n*n by A2,A3,XREAL_1:66;
    then vol(A)/n*i <= vol A by A1,XCMPLX_1:88;
    then
A13: lower_bound A + vol(A)/n*i <= lower_bound A+ vol A by XREAL_1:8;
    vol(A)/n>0 by A1,A2,XREAL_1:141;
    then
A14: lower_bound A <= lower_bound A + vol(A)/n*i by A12,XREAL_1:31,131;
    x1 = lower_bound A + vol(A)/n*i by A3,A10,A11;
    hence thesis by A14,A13,INTEGRA2:1;
  end;
  then rng D c= A by TARSKI:def 3;
  then reconsider D as Division of A by A9,INTEGRA1:def 2;
  take D;
  thus thesis by A3;
end;

begin :: Integrability on a Subinterval

theorem Th16:
  vol A > 0 implies ex T being DivSequence of A st delta T is
  convergent & lim delta T = 0 & for n be Element of NAT holds ex Tn being
  Division of A st Tn divide_into_equal n+1 & T.n =Tn
proof
  defpred P[set,set] means ex n being Element of NAT, e being Division of A st
  n = $1 & e = $2 & e divide_into_equal n+1;
  assume
A1: vol A > 0;
A2: for n be Element of NAT ex D being Element of divs A st P[n,D]
  proof
    let n be Element of NAT;
    consider D being Division of A such that
A3: len D = n+1 & for i be Element of NAT st i in dom D holds D.i=lower_bound
    A+vol(A)/( n+1)*i by A1,Th15;
    reconsider D1=D as Element of divs A by INTEGRA1:def 3;
    take D1;
    D divide_into_equal n+1 by A3,INTEGRA4:def 1;
    hence thesis;
  end;
  consider T being Function of NAT,divs A such that
A4: for n be Element of NAT holds P[n,T.n] from FUNCT_2:sch 3(A2);
  reconsider T as DivSequence of A;
A5: for n be Element of NAT holds ex Tn being Division of A st Tn
  divide_into_equal n+1 & T.n =Tn
  proof
    let n be Element of NAT;
    ex n1 be Element of NAT, Tn being Division of A st n1 = n & Tn = T.n &
    Tn divide_into_equal n1+1 by A4;
    hence thesis;
  end;
A6: for i be Element of NAT holds (delta T).i = vol(A)/(i+1)
  proof
    let i be Element of NAT;
    for x1 being set st x1 in rng upper_volume(chi(A,A),(T.i)) holds x1 in
    {vol(A)/(i+1)}
    proof
      reconsider D = T.i as Division of A;
      let x1 be set;
      assume x1 in rng upper_volume(chi(A,A),(T.i));
      then consider k be Element of NAT such that
A7:   k in dom upper_volume(chi(A,A),(T.i)) and
A8:   (upper_volume(chi(A,A),(T.i))).k=x1 by PARTFUN1:26;
      k in Seg len upper_volume(chi(A,A),(T.i)) by A7,FINSEQ_1:def 3;
      then k in Seg len D by INTEGRA1:def 7;
      then
A9:   k in dom D by FINSEQ_1:def 3;
A10:  ex n being Element of NAT, e being Division of A st n = i & e = D &
      e divide_into_equal n+1 by A4;
A11:  upper_bound divset(D,k)-lower_bound divset(D,k)=vol(A)/(i+1)
      proof
A12:    now
A13:      len D = i+1 by A10,INTEGRA4:def 1;
          assume
A14:      k=1;
          then upper_bound divset(D,k)=D.1 by A9,INTEGRA1:def 5;
          then upper_bound divset(D,k)=lower_bound A+vol(A)/(i+1)*1
          by A10,A9,A14,A13,
          INTEGRA4:def 1;
          then
          upper_bound divset(D,k)-lower_bound divset(D,k)=
          lower_bound A+vol(A)/(i+1)-lower_bound A by A9
          ,A14,INTEGRA1:def 5;
          hence thesis;
        end;
        now
          assume
A15:      k <> 1;
          then k-1 in dom D by A9,INTEGRA1:9;
          then
A16:      D.(k-1)=lower_bound A + vol(A)/(len D)*(k-1) by A10,
          INTEGRA4:def 1;
A17:      D.k = lower_bound A + vol(A)/(len D)* k by A10,A9,INTEGRA4:def 1;
          lower_bound divset(D,k)=D.(k-1) & upper_bound divset(D,k)=D.k
          by A9,A15,
          INTEGRA1:def 5;
          hence thesis by A10,A16,A17,INTEGRA4:def 1;
        end;
        hence thesis by A12;
      end;
      x1=vol(divset(D,k)) by A8,A9,INTEGRA1:22;
      hence thesis by A11,TARSKI:def 1;
    end;
    then
A18: rng upper_volume(chi(A,A),(T.i)) c= {vol(A)/(i+1)} by
    TARSKI:def
    3;
    for x1 being set st x1 in {vol(A)/(i+1)} holds x1 in rng upper_volume
    (chi(A,A),(T.i))
    proof
      reconsider D = T.i as Division of A;
      let x1 be set;
      assume x1 in {vol(A)/(i+1)};
      then
A19:  x1 = vol(A)/(i+1) by TARSKI:def 1;
A20:  ex n being Element of NAT, e being Division of A st n = i & e = D &
      e divide_into_equal n+1 by A4;
      rng D <> {};
      then
A21:  1 in dom D by FINSEQ_3:34;
      then upper_bound divset(D,1)=D.1 by INTEGRA1:def 5;
      then upper_bound divset(D,1)=lower_bound A+vol(A)/(len D)*1 by A21,
      A20, INTEGRA4:def
      1;
      then
A22:  vol(divset(D,1))=lower_bound A+vol(A)/(len D)-lower_bound A
by A21, INTEGRA1:
      def 5;
      1 in Seg len D by A21,FINSEQ_1:def 3;
      then 1 in Seg len upper_volume(chi(A,A),D) by INTEGRA1:def 7;
      then 1 in dom upper_volume(chi(A,A),D) by FINSEQ_1:def 3;
      then
A23:  (upper_volume(chi(A,A),D)).1 in rng upper_volume(chi(A,A), D) by
      FUNCT_1:def 5;
      (upper_volume(chi(A,A),D)).1 = vol(divset(D,1)) by A21,INTEGRA1:22;
      hence thesis by A19,A23,A20,A22,INTEGRA4:def 1;
    end;
    then {vol(A)/(i+1)} c= rng upper_volume(chi(A,A),(T.i)) by
    TARSKI:def 3; then
    (delta T).i = delta(T.i) & rng upper_volume(chi(A,A),(T.i))={vol(A)/(
    i+1)} by A18,INTEGRA3:def 2,XBOOLE_0:def 10;
    then (delta T).i in {vol(A)/(i+1)} by XXREAL_2:def 8;
    hence thesis by TARSKI:def 1;
  end;
A24: for a be Real st 0 < a ex i be Element of NAT st abs((delta T).i-0)<a
  proof
    let a be Real;
A25: vol A >= 0 by INTEGRA1:11;
    reconsider i1=[\vol(A)/a/]+1 as Integer;
    assume
A26: 0<a;
    then [\vol(A)/a/]+1>0 by A25,INT_1:52;
    then reconsider i1 as Element of NAT by INT_1:16;
    i1 < i1+1 by NAT_1:13;
    then vol(A)/a < 1*(i1+1) by INT_1:52,XXREAL_0:2;
    then
A27: vol(A)/(i1+1) < 1*a by A26,XREAL_1:115;
A28: (delta T).i1 = vol(A)/(i1+1) by A6;
    vol(A)/(i1+1)-0 = abs(vol(A)/(i1+1)-0) by A25,ABSVALUE:def 1;
    hence thesis by A27,A28;
  end;
A29: for i,j be Element of NAT st i <= j holds (delta T).i >= (delta T).j
  proof
    let i,j be Element of NAT;
    assume i <= j;
    then
A30: i+1 <= j+1 by XREAL_1:8;
    vol A >= 0 by INTEGRA1:11;
    then vol A/(i+1) >= vol(A)/(j+1) by A30,XREAL_1:120;
    then (delta T).i >= vol(A)/(j+1) by A6;
    hence thesis by A6;
  end;
A31: for a be real number st 0<a ex i be Element of NAT st for j be Element
  of NAT st i <= j holds abs((delta T).j-0)<a
  proof
    let a be real number;
    assume
A32: 0<a;
    a is Real by XREAL_0:def 1;
    then consider i be Element of NAT such that
A33: abs((delta T).i-0)<a by A24,A32;
    (delta T).i = delta(T.i) by INTEGRA3:def 2;
    then (delta T).i >= 0 by INTEGRA3:9;
    then
A34: (delta T).i < a by A33,ABSVALUE:def 1;
    now
      let j be Element of NAT;
      assume i <= j;
      then (delta T).j <= (delta T).i by A29;
      then
A35:  (delta T).j < a by A34,XXREAL_0:2;
      (delta T).j = delta(T.j) by INTEGRA3:def 2;
      then (delta T).j >= 0 by INTEGRA3:9;
      hence abs((delta T).j-0)<a by A35,ABSVALUE:def 1;
    end;
    hence thesis;
  end;
  then
A36: delta T is convergent by SEQ_2:def 6;
  then lim delta(T)=0 by A31,SEQ_2:def 7;
  hence thesis by A5,A36;
end;

Lm2: a <= b & [' a,b '] c= dom f & f is_integrable_on [' a,b '] & f|[' a,b ']
is bounded & c in [' a,b '] & b <> c implies integral(f,a,b) = integral(f,a,c)
+ integral(f,c,b) & f is_integrable_on ['a,c '] & f is_integrable_on ['c,b ']
proof
  assume that
A1: a <= b and
A2: [' a,b '] c= dom f and
A3: f is_integrable_on [' a,b '] and
A4: f|[' a,b '] is bounded and
A5: c in [' a,b '] and
A6: b <> c;
A7: [' a,b '] = [.a,b.] by A1,INTEGRA5:def 4;
  then
A8: c <= b by A5,XXREAL_1:1;
  then
A9: [' c,b '] = [.c,b.] by INTEGRA5:def 4;
  then
A10: [.c,b.] = [.lower_bound([.c,b.]), upper_bound([.c,b.]).] by INTEGRA1:5;
  then
A11: upper_bound [' c,b '] = b by A9,INTEGRA1:6;
  set FAB = f||[' a,b '], FAC = f||[' a,c '], FCB = f||[' c,b '];
A12: FAB is Function of [' a,b '],REAL by A2,Lm1;
A13: a <= c by A5,A7,XXREAL_1:1;
  then
A14: [' a,c '] = [.a,c.] by INTEGRA5:def 4;
  then
A15: [.a,c.] = [.lower_bound([.a,c.]), upper_bound([.a,c.]).] by INTEGRA1:5;
  then
A16: lower_bound [' a,c '] = a by A14,INTEGRA1:6;
A17: [' c,b '] c= [' a,b '] by A7,A13,A9,XXREAL_1:34;
  then f|[' c,b '] is bounded by A4,RFUNCT_1:91;
  then
A18: FCB|[' c,b '] is bounded by INTEGRA5:9;
A19: lower_bound [' c,b '] = c by A9,A10,INTEGRA1:6;
  ex PS2 be DivSequence of [' c,b '] st delta PS2 is convergent & lim
delta PS2 = 0 & ex K be Element of NAT st for i be Element of NAT st K <= i ex
  S2i be Division of [' c,b '] st S2i=PS2.i & 2 <= len S2i
  proof
    c < b by A6,A8,XXREAL_0:1;
    then vol([' c,b '] ) > 0 by A19,A11,XREAL_1:52;
    then consider T being DivSequence of [' c,b '] such that
A20: delta T is convergent & lim delta(T)=0 and
A21: for n be Element of NAT holds ex Tn being Division of [' c,b ']
    st Tn divide_into_equal n+1 & T.n =Tn by Th16;
    take T;
    now
      let i be Element of NAT;
      assume
A22:  2 <= i;
      consider Tn being Division of [' c,b '] such that
A23:  Tn divide_into_equal i+1 and
A24:  T.i =Tn by A21;
      reconsider Tn as Division of [' c,b '];
      take Tn;
      thus Tn=T.i by A24;
      len Tn = i+1 by A23,INTEGRA4:def 1;
      then i <= len Tn by NAT_1:11;
      hence 2 <= len Tn by A22,XXREAL_0:2;
    end;
    hence thesis by A20;
  end;
  then consider PS2 be DivSequence of [' c,b '] such that
A25: delta PS2 is convergent & lim delta PS2 = 0 and
A26: ex K be Element of NAT st for i be Element of NAT st K <= i ex S2i
  be Division of [' c,b '] st S2i = PS2.i & 2 <= len S2i;
  consider K be Element of NAT such that
A27: for i be Element of NAT st K <= i ex S2i be Division of [' c,b ']
  st S2i=PS2.i & 2 <= len S2i by A26;
  defpred Q[set,set] means ex n being Element of NAT, e being Division of [' c
  ,b '] st n = $1 & e = $2 & e = PS2.(n+K);
A28: for n be Element of NAT ex D being Element of divs [' c,b '] st Q[n,D]
  proof
    let n be Element of NAT;
    reconsider D = PS2.(n+K) as Element of divs [' c,b '] by INTEGRA1:def 3;
    take D;
    thus thesis;
  end;
  consider S2 being Function of NAT,divs [' c,b '] such that
A29: for n be Element of NAT holds Q[n,S2.n] from FUNCT_2:sch 3(A28);
  reconsider S2 as DivSequence of [' c,b '];
  defpred P1[Element of NAT, set] means ex S be Division of [' c,b '] st S =
  S2.$1 & $2 = S/^1;
A30: now
    let i be Element of NAT;
    ex n being Element of NAT,e being Division of ['c,b '] st n = i & e =
    S2.i & e = PS2.(n+K) by A29;
    hence ex S2i be Division of [' c,b '] st S2i=S2.i & 2 <= len S2i by A27,
    NAT_1:11;
  end;
A31: for i be Element of NAT ex T be Element of divs [' c,b '] st P1[i,T]
  proof
    let i be Element of NAT;
    consider S be Division of [' c,b '] such that
A32: S=S2.i and
A33: 2 <= len S by A30;
    set T=S/^1;
A34: 1 <= len S by A33,XXREAL_0:2;
    2-1 <= len S -1 by A33,XREAL_1:15;
    then
A35: 1 <= len T by A34,RFINSEQ:def 2;
    then len T in Seg (len T);
    then len T in dom T by FINSEQ_1:def 3;
    then T.(len T) =S.(len T+ 1) by A34,RFINSEQ:def 2;
    then T.(len T) =S.(len S -1+ 1) by A34,RFINSEQ:def 2;
    then
A36: T.(len T) = upper_bound [' c,b '] by INTEGRA1:def 2;
    rng S c= [' c,b '] & rng T c= rng S by FINSEQ_5:36,INTEGRA1:def 2;
    then
A37: rng T c= [' c,b '] by XBOOLE_1:1;
    T is non empty increasing by A33,A35,INTEGRA1:36,XXREAL_0:2;
    then T is Division of [' c,b '] by A37,A36,INTEGRA1:def 2;
    then T is Element of divs [' c,b '] by INTEGRA1:def 3;
    hence thesis by A32;
  end;
  consider T2 be DivSequence of [' c,b '] such that
A38: for i be Element of NAT holds P1[i,T2.i] from FUNCT_2:sch 3(A31);
A39: for n be Element of NAT holds ex D be Division of [' c,b '] st D = T2.n
  & for i be Element of NAT st i in dom D holds c < D.i
  proof
    let n be Element of NAT;
    reconsider D=T2.n as Division of [' c,b '];
    take D;
    consider E be Division of [' c,b '] such that
A40: E=S2.n and
A41: T2.n=E/^1 by A38;
A42: ex E1 be Division of [' c,b '] st E1=S2.n & 2 <= len E1 by A30;
    then
A43: 2-1 <= len E -1 by A40,XREAL_1:15;
A44: 1 <= len E by A40,A42,XXREAL_0:2;
    then 1 in Seg len E;
    then
A45: 1 in dom E by FINSEQ_1:def 3;
    then rng E c=[' c,b '] & E.1 in rng E by
    FUNCT_1:def 5,INTEGRA1:def 2;
    then
A46: c <= E.1 by A9,XXREAL_1:1;
    2 in Seg len E by A40,A42;
    then 2 in dom E by FINSEQ_1:def 3;
    then
A47: E.1 < E.2 by A45,SEQM_3:def 1;
    len D = len E -1 by A41,A44,RFINSEQ:def 2;
    then 1 in Seg len D by A43;
    then
A48: 1 in dom D by FINSEQ_1:def 3;
    then
A49: D.1 = E.(1+1) by A41,A44,RFINSEQ:def 2;
    then
A50: c < D.1 by A46,A47,XXREAL_0:2;
    now
      let i be Element of NAT;
      assume
A51:  i in dom D;
      then
A52:  1 <=i by FINSEQ_3:27;
      thus now
        assume i <> 1;
        then 1 < i by A52,XXREAL_0:1;
        then D.1 < D.i by A48,A51,SEQM_3:def 1;
        hence c < D.i by A50,XXREAL_0:2;
      end;
      hence c < D.i by A49,A46,A47,XXREAL_0:2;
    end;
    hence thesis;
  end;
  set XAC = chi([' a,c '],[' a,c ']);
  set XCB = chi(['c,b '],['c,b ']);
  consider T1 be DivSequence of [' a,c '] such that
A53: delta T1 is convergent & lim delta T1 = 0 by INTEGRA4:11;
A54: for n be Element of NAT holds ex D be Division of [' c,b '] st D = T2.n
  & 1 <= len D
  proof
    let n be Element of NAT;
    reconsider D=T2.n as Division of [' c,b '];
    take D;
    consider E be Division of [' c,b '] such that
A55: E=S2.n and
A56: T2.n=E/^1 by A38;
    ex E1 be Division of [' c,b '] st E1=S2.n & 2 <= len E1 by A30;
    then 1 <= len E & 2-1 <= len E -1 by A55,XREAL_1:15,XXREAL_0:2;
    hence thesis by A56,RFINSEQ:def 2;
  end;
A57: integral(f,c,b) = integral(f,[' c,b ']) by A8,INTEGRA5:def 5
    .= integral FCB;
A58: integral(f,a,c) = integral(f,[' a,c ']) by A13,INTEGRA5:def 5;
  defpred P2[Element of NAT,set] means ex S1 be Division of [' a,c '], S2 be
  Division of [' c,b '] st S1=T1.$1 & S2=T2.$1 & $2=S1^S2;
A59: [.a,b.] = [.lower_bound([.a,b.]), upper_bound([.a,b.]).] by A7,INTEGRA1:5;
  then
A60: upper_bound [' a,b '] = b by A7,INTEGRA1:6;
A61: for i be Element of NAT ex T be Element of divs [' a,b '] st P2[i,T]
  proof
    let i0 be Element of NAT;
    reconsider S1=T1.i0 as Division of [' a,c '];
    reconsider S2=T2.i0 as Division of [' c,b '];
    set T=S1^S2;
    ex D be Division of [' c,b '] st D = T2.i0 & 1 <= len D by A54;
    then len S2 in Seg len S2;
    then
A62: len S2 in dom S2 by FINSEQ_1:def 3;
A63: rng S1 c= [' a,c '] by INTEGRA1:def 2;
    now
      let i,j be Element of NAT;
      assume that
A64:  i in dom T and
A65:  j in dom T and
A66:  i < j;
A67:  1 <= i by A64,FINSEQ_3:27;
A68:  j <= len T by A65,FINSEQ_3:27;
A69:  i <= len T by A64,FINSEQ_3:27;
A70:  now
        j <= len S1 + len S2 by A68,FINSEQ_1:35;
        then
A71:    j-len S1 <= len S2 by XREAL_1:22;
        assume
A72:    j > len S1;
        then
A73:    T.j = S2.(j-len S1) by A68,FINSEQ_1:37;
A74:    len S1 + 1 <=j by A72,NAT_1:13;
        then consider m be Nat such that
A75:    (len S1 + 1)+m=j by NAT_1:10;
        reconsider m as Element of NAT by ORDINAL1:def 13;
        1+m = j - len S1 by A75;
        then 1 <= 1+m by A74,XREAL_1:21;
        then 1+m in Seg len S2 by A75,A71;
        then
A76:    j - len S1 in dom S2 by A75,FINSEQ_1:def 3;
A77:    now
          i <= len S1 + len S2 by A69,FINSEQ_1:35;
          then
A78:      i-len S1 <= len S2 by XREAL_1:22;
A79:      i - len S1 < j -len S1 by A66,XREAL_1:16;
          assume
A80:      i > len S1;
          then
A81:      len S1 + 1 <=i by NAT_1:13;
          then consider m be Nat such that
A82:      (len S1 + 1)+m=i by NAT_1:10;
          reconsider m as Element of NAT by ORDINAL1:def 13;
          1+m = i - len S1 by A82;
          then 1 <= 1+m by A81,XREAL_1:21;
          then 1+m in Seg len S2 by A82,A78;
          then
A83:      i - len S1 in dom S2 by A82,FINSEQ_1:def 3;
          T.i = S2.(i-len S1) by A69,A80,FINSEQ_1:37;
          hence T.i < T.j by A73,A76,A79,A83,SEQM_3:def 1;
        end;
        now
          assume i <= len S1;
          then
A84:      i in dom S1 by A67,FINSEQ_3:27;
          then T.i = S1.i by FINSEQ_1:def 7;
          then T.i in rng S1 by A84,FUNCT_1:def 5;
          then
A85:      T.i <= c by A14,A63,XXREAL_1:1;
          ex DD be Division of [' c,b '] st DD = T2.i0 & for k be
          Element of NAT st k in dom DD holds c < DD.k by A39;
          hence T.i < T.j by A73,A76,A85,XXREAL_0:2;
        end;
        hence T.i < T.j by A77;
      end;
A86:  1 <= j by A65,FINSEQ_3:27;
      now
        assume
A87:    j <= len S1;
        then
A88:    j in dom S1 by A86,FINSEQ_3:27;
        then
A89:    T.j = S1.j by FINSEQ_1:def 7;
        i <= len S1 by A66,A87,XXREAL_0:2;
        then
A90:    i in dom S1 by A67,FINSEQ_3:27;
        then T.i = S1.i by FINSEQ_1:def 7;
        hence T.i < T.j by A66,A90,A88,A89,SEQM_3:def 1;
      end;
      hence T.i < T.j by A70;
    end;
    then
A91: T is non empty increasing FinSequence of REAL by SEQM_3:def 1;
    rng S2 c= [' c,b '] by INTEGRA1:def 2;
    then rng S1 \/ rng S2 c= [' a,c '] \/ [' c,b '] by A63,XBOOLE_1:13;
    then rng S1 \/ rng S2 c=[.a,b.] by A13,A8,A14,A9,XXREAL_1:174;
    then
A92: rng T c= [' a,b '] by A7,FINSEQ_1:44;
    T.(len T) = T.(len S1 + len S2) by FINSEQ_1:35
      .= S2.(len S2) by A62,FINSEQ_1:def 7
      .= upper_bound [' a,b '] by A60,A11,INTEGRA1:def 2;
    then T is Division of [' a,b '] by A92,A91,INTEGRA1:def 2;
    then T is Element of divs [' a,b '] by INTEGRA1:def 3;
    hence thesis;
  end;
  consider T be DivSequence of [' a,b '] such that
A93: for i be Element of NAT holds P2[i,T.i] from FUNCT_2:sch 3(A61);
A94: lower_bound [' a,b '] = a by A7,A59,INTEGRA1:6;
A95: for i, k be Element of NAT, S0 be Division of [' a,c '] st S0=T1.i & k
  in Seg len S0 holds divset(T.i,k) = divset(T1.i,k)
  proof
    let i,k be Element of NAT, S0 be Division of [' a,c '];
    assume
A96: S0=T1.i;
    reconsider S=T.i as Division of [' a,b '];
A97: divset(T1.i,k) =
 [. lower_bound divset(T1.i,k),upper_bound divset(T1.i,k).] by
    INTEGRA1:5;
    consider S1 be Division of [' a,c '], S2 be Division of [' c,b '] such
    that
A98: S1=T1.i and
    S2=T2.i and
A99: T.i=S1^S2 by A93;
    assume
A100: k in Seg len S0;
    then
A101: k in dom S1 by A96,A98,FINSEQ_1:def 3;
    len S = len S1 + len S2 by A99,FINSEQ_1:35;
    then len S1 <= len S by NAT_1:11;
    then Seg len S1 c= Seg len S by FINSEQ_1:7;
    then k in Seg len S by A96,A100,A98;
    then
A102: k in dom S by FINSEQ_1:def 3;
A103: divset(T.i,k) =
[.lower_bound divset(T.i,k),upper_bound divset(T.i,k).] by INTEGRA1:5;
A104: now
      k <= len S1 & k-1 <= k -0 by A96,A100,A98,FINSEQ_1:3,XREAL_1:12;
      then
A105: k-1 <= len S1 by XXREAL_0:2;
      assume
A106: k <> 1;
      1 <=k by A100,FINSEQ_1:3;
      then
A107: 1 < k by A106,XXREAL_0:1;
      then 1+1 <= k by NAT_1:13;
      then
A108: 2-1 <= k-1 by XREAL_1:11;
      k-1 is Element of NAT by A107,NAT_1:20;
      then k-1 in Seg len S1 by A105,A108;
      then
A109: k-1 in dom S1 by FINSEQ_1:def 3;
      thus divset(T.i,k) =[. S.(k-1),upper_bound divset(T.i,k) .]
      by A102,A103,A106,INTEGRA1:def 5
        .=[. S.(k-1),S.k .] by A102,A106,INTEGRA1:def 5
        .=[. S.(k-1),S1.k .] by A99,A101,FINSEQ_1:def 7
        .=[. S1.(k-1),S1.k .] by A99,A109,FINSEQ_1:def 7
        .=[. lower_bound divset(T1.i,k),S1.k .] by A98,A101,A106,INTEGRA1:def 5
        .= divset(T1.i,k) by A98,A101,A97,A106,INTEGRA1:def 5;
    end;
    now
      assume
A110: k=1;
      hence divset(T.i,k) =[. lower_bound[' a,b '],upper_bound divset(T.i,k) .]
       by A102,A103,
      INTEGRA1:def 5
        .=[. lower_bound[' a,b '],S.k .] by A102,A110,INTEGRA1:def 5
        .=[. lower_bound[' a,b '],S1.k .] by A99,A101,FINSEQ_1:def 7
        .=[. lower_bound divset(T1.i,k),S1.1 .]
        by A94,A16,A98,A101,A110, INTEGRA1:def 5
        .= divset(T1.i,k) by A98,A101,A97,A110,INTEGRA1:def 5;
    end;
    hence thesis by A104;
  end;
A111: upper_bound [' a,c '] = c by A14,A15,INTEGRA1:6;
A112: for i, k be Element of NAT, S01 be Division of [' a,c '], S02 be
Division of [' c,b '] st S01=T1.i & S02=T2.i & k in Seg len S02 holds divset(T.
  i,(len S01 + k)) = divset(T2.i,k)
  proof
    let i,k be Element of NAT, S01 be Division of [' a,c '], S02 be Division
    of [' c,b '];
    assume that
A113: S01=T1.i and
A114: S02=T2.i;
    reconsider S=T.i as Division of [' a,b '];
    consider S1 be Division of [' a,c '], S2 be Division of [' c,b '] such
    that
A115: S1=T1.i and
A116: S2=T2.i and
A117: T.i = S1^S2 by A93;
    assume
A118: k in Seg len S02;
    then
A119: k in dom S2 by A114,A116,FINSEQ_1:def 3;
    then
A120: len S1 + k in dom S by A117,FINSEQ_1:41;
A121: divset(T2.i,k) =
[. lower_bound divset(T2.i,k), upper_bound divset(T2.i,k).] by
    INTEGRA1:5;
A122: divset(T.i,(len S1 + k)) =
[.lower_bound divset(T.i,(len S1 + k)), upper_bound divset
    (T .i,(len S1 + k)).] by INTEGRA1:5;
A123: now
      k <= len S2 & k-1 <= k -0 by A114,A118,A116,FINSEQ_1:3,XREAL_1:12;
      then
A124: k-1 <= len S2 by XXREAL_0:2;
      assume
A125: k <> 1;
A126: 1 <=k by A118,FINSEQ_1:3;
      then
A127: 1 < k by A125,XXREAL_0:1;
      then 1+1 <= k by NAT_1:13;
      then
A128: 2-1 <= k-1 by XREAL_1:11;
      k-1 is Element of NAT by A127,NAT_1:20;
      then k-1 in Seg len S2 by A124,A128;
      then
A129: k-1 in dom S2 by FINSEQ_1:def 3;
A130: 1+0 < k + len S1 by A126,XREAL_1:10;
      hence divset(T.i,(len S1 + k)) =
      [. S.(len S1 + k-1),upper_bound divset(T.i,len S1
      + k) .] by A120,A122,INTEGRA1:def 5
        .=[. S.(len S1 + k-1),S.(len S1 + k) .] by A120,A130,INTEGRA1:def 5
        .=[. S.(len S1 + (k-1)),S2.k .] by A117,A119,FINSEQ_1:def 7
        .=[. S2.(k-1),S2.k .] by A117,A129,FINSEQ_1:def 7
        .=[. lower_bound divset(T2.i,k), S2.k .]
        by A116,A119,A125,INTEGRA1:def 5
        .= divset(T2.i,k) by A116,A119,A121,A125,INTEGRA1:def 5;
    end;
    now
      len S1 in Seg len S1 by FINSEQ_1:5;
      then
A131: len S1 in dom S1 by FINSEQ_1:def 3;
      assume
A132: k=1;
      len S1 <> 0;
      then
A133: len S1 + k <> 1 by A132;
      hence
      divset(T.i,(len S1 + k)) =
      [. S.(len S1 + k-1),upper_bound divset(T.i,len S1
      + k) .] by A120,A122,INTEGRA1:def 5
        .=[. S1.(len S1),upper_bound divset(T.i,len S1 +k) .]
        by A117,A132,A131,FINSEQ_1:def 7
        .=[. upper_bound ([' a,c ']), upper_bound divset(T.i,len S1 + k) .]
        by INTEGRA1:def 2
        .=[. lower_bound ([' c,b ']), S.(len S1 + k) .] by A19,A111,A120,A133,
      INTEGRA1:def 5
        .=[. lower_bound [' c,b '], S2.k .] by A117,A119,FINSEQ_1:def 7
        .=[. lower_bound divset(T2.i,k), S2.1 .]
        by A116,A119,A132,INTEGRA1:def 5
        .= divset(T2.i,k) by A116,A119,A121,A132,INTEGRA1:def 5;
    end;
    hence thesis by A113,A115,A123;
  end;
  set XAB = chi([' a,b '],[' a,b ']);
A134: for i, k be Element of NAT, S0 be Division of [' c,b '] st S0=T2.i & k
  in Seg len S0 holds divset(T2.i,k) c= [' c,b ']
  proof
    let i,k be Element of NAT, S0 be Division of [' c,b '];
    assume that
A135: S0=T2.i and
A136: k in Seg len S0;
    k in dom S0 by A136,FINSEQ_1:def 3;
    hence thesis by A135,INTEGRA1:10;
  end;
A137: [' a,c '] c= [' a,b '] by A7,A8,A14,XXREAL_1:34;
  then f|[' a,c '] is bounded by A4,RFUNCT_1:91;
  then
A138: FAC|[' a,c '] is bounded by INTEGRA5:9;
A139: for i,k be Element of NAT, S0 be Division of [' a,c '] st S0=T1.i & k
  in Seg len S0 holds divset(T1.i,k) c= [' a,c ']
  proof
    let i,k be Element of NAT, S0 be Division of [' a,c '];
    assume that
A140: S0=T1.i and
A141: k in Seg (len S0);
    k in dom S0 by A141,FINSEQ_1:def 3;
    hence thesis by A140,INTEGRA1:10;
  end;
A142: for i be Element of NAT holds upper_volume(f||[' a,b '],T.i) = (
  upper_volume(FAC,T1.i))^(upper_volume(FCB,T2.i))
  proof
    let i be Element of NAT;
    reconsider F = upper_volume(FAB,T.i) as FinSequence of REAL;
    reconsider F1 = upper_volume(FAC,T1.i) as FinSequence of REAL;
    reconsider F2 = upper_volume(FCB,T2.i) as FinSequence of REAL;
    reconsider S=T.i as Division of [' a,b '];
    consider S1 be Division of [' a,c '], S2 be Division of [' c,b '] such
    that
A143: S1=T1.i and
A144: S2=T2.i and
A145: T.i =S1^S2 by A93;
A146: len F = len S by INTEGRA1:def 7
      .= len S1+ len S2 by A145,FINSEQ_1:35
      .= len F1 + len S2 by A143,INTEGRA1:def 7
      .= len F1 + len F2 by A144,INTEGRA1:def 7;
A147: now
      let k be Nat;
      assume
A148: k in dom F2;
      then
A149: k in Seg len F2 by FINSEQ_1:def 3;
      then 1 <=k by FINSEQ_1:3;
      then
A150: 1 + len F1 <= k + len F1 by XREAL_1:8;
      k <= len F2 by A149,FINSEQ_1:3;
      then
A151: len F1 + k <= len F by A146,XREAL_1:8;
      1 <= 1+ len F1 by NAT_1:11;
      then 1 <= k + len F1 by A150,XXREAL_0:2;
      then k+ len F1 in Seg len F by A151;
      then len F1 + k in Seg len S by INTEGRA1:def 7;
      then len F1 + k in dom S by FINSEQ_1:def 3;
      then
A152: F.(len F1 + k) =(upper_bound rng(FAB|divset(T.i,(len F1 + k)))) * vol
      divset(T.i,len F1 + k) by INTEGRA1:def 7;
      k in Seg len F2 by A148,FINSEQ_1:def 3;
      then
A153: k in Seg len S2 by A144,INTEGRA1:def 7;
      then
A154: k in dom S2 by FINSEQ_1:def 3;
      len F1= len S1 by A143,INTEGRA1:def 7;
      then
A155: divset(T.i,len F1 + k) = divset(T2.i,k) by A112,A143,A144,A153;
      then
A156: divset(T.i,len F1 + k) c= [' c,b '] by A134,A144,A153;
      then divset(T.i,len F1 + k) c= [' a,b '] by A17,XBOOLE_1:1;
      then F.(len F1 + k)=
      (upper_bound rng(FCB|divset(T2.i,k))) * vol divset(T2.i,k)
      by A152,A155,A156,Th3;
      hence F.(len F1 + k) =F2.k by A144,A154,INTEGRA1:def 7;
    end;
A157: now
      let k be Nat;
      assume k in dom F1;
      then
A158: k in Seg len F1 by FINSEQ_1:def 3;
      then
A159: k in Seg(len S1) by A143,INTEGRA1:def 7;
      then
A160: k in dom S1 by FINSEQ_1:def 3;
      len F1 <= len F by A146,NAT_1:11;
      then Seg len F1 c= Seg len F by FINSEQ_1:7;
      then k in Seg len F by A158;
      then k in Seg(len S) by INTEGRA1:def 7;
      then k in dom S by FINSEQ_1:def 3;
      then
A161: F.k = (upper_bound rng(FAB|divset(T.i,k)))*vol divset(T.i,k) by
      INTEGRA1:def 7;
A162: divset(T.i,k) = divset(T1.i,k) by A95,A143,A159;
      then
A163: divset(T.i,k) c= [' a,c '] by A139,A143,A159;
      then divset(T.i,k) c= [' a,b '] by A137,XBOOLE_1:1;
      then F.k =
       (upper_bound rng(FAC|divset(T1.i,k))) * vol divset(T1.i,k) by A161,
      A162,A163,Th3;
      hence F.k = F1.k by A143,A160,INTEGRA1:def 7;
    end;
    dom F = Seg (len F1 + len F2) by A146,FINSEQ_1:def 3;
    hence thesis by A157,A147,FINSEQ_1:def 7;
  end;
A164: for i be Element of NAT holds Sum upper_volume(FAB,T.i) = Sum
  upper_volume(FAC,T1.i) + Sum upper_volume(FCB,T2.i)
  proof
    let i be Element of NAT;
    upper_volume(FAB,T.i) = (upper_volume(FAC,T1.i))^(upper_volume(FCB,
    T2.i) ) by A142;
    hence thesis by RVSUM_1:105;
  end;
  now
    let i be Element of NAT;
    thus upper_sum(FAB,T).i =upper_sum(FAB,T.i) by INTEGRA2:def 4
      .=upper_sum(FAC,T1.i) +Sum(upper_volume(FCB,T2.i)) by A164
      .=upper_sum(FAC,T1).i + upper_sum(FCB,T2.i) by INTEGRA2:def 4
      .=upper_sum(FAC,T1).i +upper_sum(FCB,T2).i by INTEGRA2:def 4;
  end;
  then
A165: upper_sum(FAB,T) = upper_sum(FAC,T1) +upper_sum(FCB,T2) by SEQ_1
  :11;
A166: FCB is Function of [' c,b '],REAL by A2,A17,Lm1,XBOOLE_1:1;
  then
A167: lower_integral FCB <= upper_integral FCB by A18,INTEGRA1:51;
A168: now
    let e be real number;
    assume 0 < e;
    then consider m be Element of NAT such that
A169: for n be Element of NAT st m <= n holds abs(delta(PS2).n -0 ) < e
    by A25,SEQ_2:def 7;
    take m;
    hereby
      let n be Element of NAT;
A170: n <=n+K by NAT_1:11;
      assume m <=n;
      then m <=n+K by A170,XXREAL_0:2;
      then abs(delta(PS2).(n+K) -0 ) < e by A169;
      then
A171: abs(delta(PS2.(n+K)) -0 ) < e by INTEGRA3:def 2;
      ex k being Element of NAT, e1 being Division of [' c,b '] st k = n
      & e1 =S2.n & e1 = PS2.(k+K) by A29;
      hence abs(delta(S2).n -0) < e by A171,INTEGRA3:def 2;
    end;
  end;
A172: now
    let e be real number;
    assume
A173: e > 0;
    then consider m be Element of NAT such that
A174: for n be Element of NAT st m <=n holds abs( delta(S2).n - 0) < e
    /2 by A168,XREAL_1:217;
    take m;
A175: e/2 < e by A173,XREAL_1:218;
    now
      let n be Element of NAT;
      assume m <=n;
      then abs( delta(S2).n - 0) < e/2 by A174;
      then 0 <= delta(S2.n) & abs delta(S2.n) < e/2 by INTEGRA3:def 2,
      INTEGRA3:9;
      then
A176: max rng upper_volume(XCB,S2.n) < e/2 by ABSVALUE:def 1;
A177: for y be real number st y in rng upper_volume(XCB,T2.n) holds y <  e
      proof
        reconsider D=T2.n as Division of [' c,b '];
        let y be real number;
        assume y in rng upper_volume(XCB,T2.n);
        then consider x be set such that
A178:   x in dom upper_volume(XCB,T2.n) and
A179:   y=(upper_volume(XCB,T2.n)).x by FUNCT_1:def 5;
        reconsider i=x as Element of NAT by A178;
A180:   x in Seg len upper_volume(XCB,T2.n) by A178,FINSEQ_1:def 3;
        consider E1 be Division of [' c,b '] such that
A181:   E1=S2.n and
A182:   2 <= len E1 by A30;
        set i1=i+1;
        consider E be Division of [' c,b '] such that
A183:   E=S2.n and
A184:   T2.n=E/^1 by A38;
A185:   1 <= len E1 by A182,XXREAL_0:2;
        then
A186:   len D = len E -1 by A183,A184,A181,RFINSEQ:def 2;
A187:   len (upper_volume(XCB,T2.n)) = len D by INTEGRA1:def 7;
        then
A188:   dom (upper_volume(XCB,T2.n)) = dom D by FINSEQ_3:31;
A189:   now
          len(upper_volume(XCB,S2.n)) = len E by A183,INTEGRA1:def 7;
          then 2 in Seg len (upper_volume(XCB,S2.n)) by A183,A181,A182;
          then 2 in dom upper_volume(XCB,S2.n) by FINSEQ_1:def 3;
          then (upper_volume(XCB,S2.n)).2 in rng upper_volume(XCB,S2.n) by
          FUNCT_1:def 5;
          then (upper_volume(XCB,S2.n)).2 <= max rng upper_volume(XCB,S2.n)
          by XXREAL_2:def 8;
          then
A190:     (upper_volume(XCB,S2.n)).2 < e/2 by A176, XXREAL_0:2;
          assume
A191:     i = 1;
          then
A192:     1 in dom D by A180,A187,FINSEQ_1:def 3;
          len(upper_volume(XCB,S2.n))= len D + 1 by A183,A186, INTEGRA1:
          def 7;
          then len(upper_volume(XCB,S2.n)) = len (upper_volume(XCB,T2.n)) + 1
          by INTEGRA1:def 7;
          then 1 in Seg len(upper_volume(XCB,S2.n)) by A180,A191,
          FINSEQ_2:9;
          then 1 in dom upper_volume(XCB,S2.n) by FINSEQ_1:def 3;
          then (upper_volume(XCB,S2.n)).1 in rng upper_volume(XCB,S2.n) by
          FUNCT_1:def 5;
          then (upper_volume(XCB,S2.n)).1 <= max rng upper_volume(XCB,S2.n)
          by XXREAL_2:def 8;
          then
A193:     (upper_volume(XCB,S2.n)).1 < e/2 by A176, XXREAL_0:2;
A194:     2 in dom E by A183,A181,A182,FINSEQ_3:27;
          1 in Seg len E by A183,A181,A185;
          then
A195:     1 in dom E by FINSEQ_1:def 3;
          divset(S2.n,1) =
          [. lower_bound divset(S2.n,1),upper_bound divset(S2.n,1) .]
          by INTEGRA1:5;
          then
A196:     divset(S2.n,1) =
 [. lower_bound([' c,b ']),upper_bound divset(S2.n,1) .] by
          A183,A195,INTEGRA1:def 5;
          then
A197:     [. lower_bound([' c,b ']),upper_bound divset(S2.n,1) .]
 = [.lower_bound([. lower_bound([' c,b
']),upper_bound divset(S2.n,1) .]), upper_bound([. lower_bound([' c,b ']),
upper_bound divset(S2.n,1) .]).] by INTEGRA1:5;
A198:     divset((T2.n),i) =
 [. lower_bound(divset((T2.n),1)),upper_bound(divset((T2.n),
          1)) .] by A191,INTEGRA1:5
            .= [. lower_bound([' c,b ']),upper_bound(divset((T2.n),1)) .]
            by A192,INTEGRA1:def 5
            .= [. lower_bound([' c,b ']),D.1 .] by A192,INTEGRA1:def 5
            .= [. lower_bound([' c,b ']),E.(1+1) .]
            by A183,A184,A181,A185,A192,RFINSEQ:def 2
            .= [. lower_bound([' c,b ']),upper_bound(divset((S2.n),2)) .]
            by A183,A194,INTEGRA1:def 5;
          then [. lower_bound([' c,b ']),upper_bound(divset((S2.n),2)) .]
           = [.lower_bound([. lower_bound([' c
,b ']),upper_bound(divset((S2.n),2)) .]),
upper_bound([. lower_bound([' c,b ']),upper_bound(divset((S2.n),2))
          .]).] by INTEGRA1:5;
          then
A199:     lower_bound([' c,b '])=lower_bound([. lower_bound([' c,b ']),
upper_bound(divset(( S2.n),2)) .]
) & upper_bound(divset ((S2.n),2)) =upper_bound([. lower_bound([' c,b ']),
upper_bound(divset((S2.n),2)) .]) by
          A198, INTEGRA1:6;
          y =vol divset(T2.n,1) by A178,A179,A188,A191,INTEGRA1:22;
          then y =(upper_bound divset(S2.n,2)-upper_bound divset(S2.n,1)) +
           (upper_bound([. lower_bound([' c,
          b ']),upper_bound divset(S2.n,1) .])-lower_bound([' c,b ']))
          by A191,A198,A199,A196;
          then
A200:     y =(upper_bound divset(S2.n,2) - upper_bound divset(S2.n,1))
 + vol divset(S2.n
          ,1) by A196,A197,INTEGRA1:6;
          divset(S2.n,2) = [. lower_bound divset(S2.n,2),
          upper_bound divset(S2.n,2) .] by INTEGRA1:5;
          then divset(S2.n,2) = [. E.(2-1),upper_bound divset(S2.n,2) .]
          by A183,A194,INTEGRA1:def 5;
          then
A201:     divset(S2.n,2) = [. upper_bound divset(S2.n,1),
upper_bound divset( S2.n,2) .]
          by A183,A195,INTEGRA1:def 5;
          then [. upper_bound divset(S2.n,1) ,upper_bound divset(S2.n,2) .]
           = [.lower_bound([. upper_bound
divset(S2.n,1) ,upper_bound divset(S2.n,2) .]),
upper_bound([. upper_bound divset(S2.n,1) ,upper_bound divset(
          S2.n,2) .]).] by INTEGRA1:5;
          then y = vol divset(S2.n,2) + vol divset(S2.n,1) by A201,A200,
          INTEGRA1:6;
          then y = (upper_volume(XCB,S2.n)).2 + vol divset(S2.n,1) by A183,
          A194,INTEGRA1:22;
          then y = (upper_volume(XCB,S2.n)).2 + (upper_volume(XCB,S2.n)).1 by
          A183,A195,INTEGRA1:22;
          then y < (e/2)+(e/2) by A190,A193,XREAL_1:10;
          hence thesis;
        end;
A202:   y=( upper_bound rng(XCB|divset(T2.n,i)) ) * vol divset(T2.n,i) by A178,
        A179,A188,INTEGRA1:def 7;
        now
          len upper_volume(XCB,S2.n) = len D +1 by A183,A186, INTEGRA1:
          def 7;
          then len upper_volume(XCB,S2.n) = len (upper_volume(XCB,T2.n)) + 1
          by INTEGRA1:def 7;
          then
A203:     i1 in Seg len (upper_volume(XCB,S2.n)) by A180, FINSEQ_1:81;
          then
A204:     i1 in dom upper_volume(XCB,S2.n) by FINSEQ_1:def 3;
A205:     i1 in dom upper_volume(XCB,S2.n) by A203,FINSEQ_1:def 3;
A206:     i in dom D by A180,A187,FINSEQ_1:def 3;
          Seg (len D + 1) = Seg len E by A186;
          then i1 in Seg len E by A180,A187,FINSEQ_1:81;
          then
A207:     i1 in dom E by FINSEQ_1:def 3;
          len upper_volume(XCB,S2.n) = len E by A183,INTEGRA1:def 7;
          then
A208:     dom upper_volume(XCB,S2.n) = dom E by FINSEQ_3:31;
          assume
A209:     i <> 1;
          i in Seg len D by A178,A187,FINSEQ_1:def 3;
          then 1 <= i by FINSEQ_1:3;
          then
A210:     1 < i by A209,XXREAL_0:1;
          then
A211:     i-1 in Seg len D by A180,A187,FINSEQ_3:13;
          then
A212:     i-1 in dom D by FINSEQ_1:def 3;
          reconsider i9 = i-1 as Element of NAT by A211;
          1+1 < i+1 by A210,XREAL_1:10;
          then
A213:     i1 <> 1;
          divset(T2.n,i) = [. lower_bound divset(T2.n,i),
          upper_bound divset(T2.n,i) .] by INTEGRA1:5
            .= [. D.(i-1),upper_bound divset(T2.n,i) .]
            by A209,A206,INTEGRA1:def 5
            .= [. D.(i-1),D.i .] by A209,A206,INTEGRA1:def 5
            .= [. E.(i9+1),D.i .] by A183,A184,A181,A185,A212,RFINSEQ:def 2
            .= [. E.((i-1)+1),E.(i+1) .] by A183,A184,A181,A185,A206,
          RFINSEQ:def 2
            .= [. E.(i1-1),upper_bound divset(S2.n,i1) .]
            by A183,A213,A207,INTEGRA1:def 5
            .= [. lower_bound divset(S2.n,i1),upper_bound divset(S2.n,i1) .]
             by A183,A213,A207,INTEGRA1:def 5
            .= divset(S2.n,i1) by INTEGRA1:5;
          then y = (upper_volume(XCB,S2.n)).i1 by A202,A183,A204,A208,
          INTEGRA1:def 7;
          then y in rng upper_volume(XCB,S2.n) by A205,FUNCT_1:def 5;
          then y <= max rng upper_volume(XCB,S2.n) by XXREAL_2:def 8;
          then y < e/2 by A176,XXREAL_0:2;
          hence thesis by A175,XXREAL_0:2;
        end;
        hence thesis by A189;
      end;
      max rng upper_volume(XCB,T2.n) in rng upper_volume(XCB,T2.n) by
      XXREAL_2:def 8;
      then 0 <= delta(T2.n) & delta(T2.n) < e by A177,INTEGRA3:9;
      then abs delta(T2.n) < e by ABSVALUE:def 1;
      hence abs(delta(T2).n-0) < e by INTEGRA3:def 2;
    end;
    hence for n be Element of NAT st m <=n holds abs( delta(T2).n - 0) < e;
  end;
  then
A214: delta T2 is convergent by SEQ_2:def 6;
  then
A215: lim delta T2 =0 by A172,SEQ_2:def 7;
  then
A216: upper_sum(FCB,T2) is convergent & lim upper_sum(FCB,T2) =
  upper_integral FCB by A166,A18,A214,INTEGRA4:9;
A217: now
    let e be real number;
    assume
A218: 0 < e;
    then consider n1 be Element of NAT such that
A219: for m be Element of NAT st n1 <= m holds abs(delta(T1).m -0 ) <
    e by A53,SEQ_2:def 7;
    consider n2 be Element of NAT such that
A220: for m be Element of NAT st n2 <= m holds abs(delta(T2).m -0 ) <
    e by A172,A218;
    reconsider n = max(n1,n2) as Element of NAT by XXREAL_0:16;
    take n;
    now
      let m be Element of NAT;
      assume
A221: n <=m;
      n2 <= n by XXREAL_0:25;
      then n2 <= m by A221,XXREAL_0:2;
      then abs(delta(T2).m -0 ) < e by A220;
      then
A222: abs(delta(T2.m) ) < e by INTEGRA3:def 2;
      0 <= delta(T2.m) by INTEGRA3:9;
      then
A223: max rng upper_volume(XCB,T2.m) < e by A222,ABSVALUE:def 1;
      n1 <= n by XXREAL_0:25;
      then n1 <= m by A221,XXREAL_0:2;
      then abs(delta(T1).m -0 ) < e by A219;
      then
A224: abs(delta(T1.m) ) < e by INTEGRA3:def 2;
      0 <= delta(T1.m) by INTEGRA3:9;
      then
A225: max rng upper_volume(XAC,T1.m) < e by A224,ABSVALUE:def 1;
A226: for r be real number st r in rng upper_volume(XAB,T.m) holds r < e
      proof
        reconsider Tm=T.m as Division of [' a,b '];
        let y be real number;
        assume y in rng upper_volume(XAB,T.m);
        then consider x be set such that
A227:   x in dom upper_volume(XAB,T.m) and
A228:   y = (upper_volume(XAB,T.m)).x by FUNCT_1:def 5;
        reconsider i=x as Element of NAT by A227;
A229:   x in Seg len (upper_volume(XAB,T.m)) by A227,FINSEQ_1:def 3;
        then
A230:   1 <= i by FINSEQ_1:3;
A231:   len(upper_volume(XAB,T.m)) =len Tm by INTEGRA1:def 7;
        then
A232:   i <= len Tm by A229,FINSEQ_1:3;
        dom(upper_volume(XAB,T.m)) =dom Tm by A231,FINSEQ_3:31;
        then
A233:   y=(upper_bound rng(XAB|divset(T.m,i))) * vol divset(T.m,i)
          by A227,A228,INTEGRA1:def 7;
        consider S1 be Division of [' a,c '], S2 be Division of [' c,b '] such
        that
A234:   S1=T1.m and
A235:   S2=T2.m and
A236:   T.m = S1^S2 by A93;
A237:   len Tm = len S1 + len S2 by A236,FINSEQ_1:35;
        per cases;
        suppose
          i <= len S1;
          then
A238:     i in Seg len S1 by A230;
          then
A239:     i in dom S1 by FINSEQ_1:def 3;
          len (upper_volume(XAC,T1.m)) = len S1 by A234,INTEGRA1:def 7;
          then
A240:     i in dom (upper_volume(XAC,T1.m)) by A238,FINSEQ_1:def 3;
A241:     divset(T1.m,i) = divset(T.m,i) by A95,A234,A238;
A242:     divset(T1.m,i) c= [' a,c '] by A139,A234,A238;
          then divset(T1.m,i) c= [' a,b '] by A137,XBOOLE_1:1;
          then XAC|divset(T1.m,i) = XAB|divset(T.m,i) by A241,A242,Th4;
          then y=(upper_volume(XAC,T1.m)).i by A233,A234,A239,A241,
          INTEGRA1:def 7;
          then y in rng upper_volume(XAC,T1.m) by A240,FUNCT_1:def 5;
          then y <= max rng upper_volume(XAC,T1.m) by XXREAL_2:def 8;
          hence thesis by A225,XXREAL_0:2;
        end;
        suppose
          i > len S1;
          then
A243:     len S1 + 1 <=i by NAT_1:13;
          then consider k be Nat such that
A244:     (len S1 + 1)+k=i by NAT_1:10;
          reconsider k as Element of NAT by ORDINAL1:def 13;
          set i1=1+k;
A245:     i-len S1 <= len S2 by A237,A232,XREAL_1:22;
          1+k = i - len S1 by A244;
          then 1 <= 1+k by A243,XREAL_1:21;
          then
A246:     1+k in Seg len S2 by A244,A245;
          then
A247:     1+k in dom S2 by FINSEQ_1:def 3;
A248:     divset(T2.m,i1) =divset(T.m,len S1 + i1) by A112,A234,A235,
          A246;
A249:     divset(T2.m,i1) c= [' c,b '] by A134,A235,A246;
          then divset(T2.m,i1) c= [' a,b '] by A17,XBOOLE_1:1;
          then y=(upper_bound rng(XCB|divset(T2.m,i1))) * vol divset(T2.m,i1)
           by A233,A244,A248,A249,Th4;
          then
A250:     y=(upper_volume(XCB,T2.m)).i1 by A235,A247, INTEGRA1:def 7;
          len (upper_volume(XCB,T2.m)) = len S2 by A235,INTEGRA1:def 7;
          then i1 in dom (upper_volume(XCB,T2.m)) by A246, FINSEQ_1:def 3;
          then y in rng upper_volume(XCB,T2.m) by A250,FUNCT_1:def 5;
          then y <= max rng upper_volume(XCB,T2.m) by XXREAL_2:def 8;
          hence thesis by A223,XXREAL_0:2;
        end;
      end;
A251: 0 <= delta(T.m) by INTEGRA3:9;
      max rng upper_volume(XAB,T.m) in rng upper_volume(XAB,T.m) by
      XXREAL_2:def 8;
      then delta(T.m) < e by A226;
      then abs( delta(T.m) ) < e by A251,ABSVALUE:def 1;
      hence abs(delta(T).m -0 ) < e by INTEGRA3:def 2;
    end;
    hence for m be Element of NAT st n <= m holds abs(delta(T).m -0 ) < e;
  end;
  then
A252: delta T is convergent by SEQ_2:def 6;
  then
A253: lim delta(T)=0 by A217,SEQ_2:def 7;
  FAB|[' a,b '] is bounded by A4,INTEGRA5:9;
  then
A254: upper_integral FAB = lim upper_sum(FAB,T) by A12,A252,A253,
  INTEGRA4:9;
A255: for i be Element of NAT holds lower_volume(FAB,T.i) =(lower_volume(FAC
  ,T1.i))^(lower_volume(FCB,T2.i))
  proof
    let i be Element of NAT;
    reconsider F = lower_volume(FAB,T.i) as FinSequence of REAL;
    reconsider F1 = lower_volume(FAC,T1.i) as FinSequence of REAL;
    reconsider F2 = lower_volume(FCB,T2.i) as FinSequence of REAL;
    reconsider S=T.i as Division of [' a,b '];
    consider S1 be Division of [' a,c '], S2 be Division of [' c,b '] such
    that
A256: S1=T1.i and
A257: S2=T2.i and
A258: T.i = S1^S2 by A93;
A259: len F = len S by INTEGRA1:def 8
      .= len S1+ len S2 by A258,FINSEQ_1:35
      .= len F1 + len S2 by A256,INTEGRA1:def 8;
    then
A260: len F = len F1 + len F2 by A257,INTEGRA1:def 8;
A261: now
      let k be Nat;
      assume k in dom F2;
      then
A262: k in Seg len F2 by FINSEQ_1:def 3;
      then 1 <=k by FINSEQ_1:3;
      then
A263: 1 + len F1 <= k + len F1 by XREAL_1:8;
      k <= len F2 by A262,FINSEQ_1:3;
      then
A264: len F1 + k <= len F by A260,XREAL_1:8;
      1 <= 1+ len F1 by NAT_1:11;
      then 1 <= k + len F1 by A263,XXREAL_0:2;
      then k+ len F1 in Seg len F by A264;
      then len F1 + k in Seg len S by INTEGRA1:def 8;
      then len F1 + k in dom S by FINSEQ_1:def 3;
      then
A265: F.(len F1 + k) = (lower_bound rng(FAB|divset(T.i,len F1 + k))) * vol
      divset(T.i,(len F1 + k)) by INTEGRA1:def 8;
A266: k in Seg len S2 by A257,A262,INTEGRA1:def 8;
      then
A267: k in dom S2 by FINSEQ_1:def 3;
      len F1= len S1 by A256,INTEGRA1:def 8;
      then
A268: divset(T.i,len F1 + k) = divset(T2.i,k) by A112,A256,A257,A266;
A269: divset(T2.i,k) c= [' c,b '] by A134,A257,A266;
      then divset(T.i,len F1 + k) c= [' a,b '] by A17,A268,XBOOLE_1:1;
      then F.(len F1 + k)=
      (lower_bound rng(FCB|divset(T2.i,k))) * vol divset(T2.i,k)
      by A265,A268,A269,Th3;
      hence F.(len F1 + k)=F2.k by A257,A267,INTEGRA1:def 8;
    end;
A270: now
      let k be Nat;
      len lower_volume(FAB,T.i) = len S by INTEGRA1:def 8;
      then
A271: dom lower_volume(FAB,T.i) = dom S by FINSEQ_3:31;
      assume
A272: k in dom F1;
      then k in Seg len F1 by FINSEQ_1:def 3;
      then
A273: k in Seg len S1 by A256,INTEGRA1:def 8;
      then
A274: k in dom S1 by FINSEQ_1:def 3;
      len F1 <= len F by A259,NAT_1:11;
      then dom F1 c= dom F by FINSEQ_3:32;
      then
A275: F.k=(lower_bound rng(FAB|divset(T.i,k))) * vol divset(T.i,k) by A272,
      A271,INTEGRA1:def 8;
A276: divset(T.i,k) = divset(T1.i,k) & divset(T1.i,k) c=[' a,c '] by
      A139,A95,A256,A273;
      then divset(T.i,k) c= [' a,b '] by A137,XBOOLE_1:1;
      then F.k=(lower_bound rng(FAC|divset(T1.i,k))) * vol divset(T1.i,k)
      by A275,A276, Th3;
      hence F.k=F1.k by A256,A274,INTEGRA1:def 8;
    end;
    dom F = Seg (len F1 + len F2) by A260,FINSEQ_1:def 3;
    hence thesis by A270,A261,FINSEQ_1:def 7;
  end;
A277: for i be Element of NAT holds Sum lower_volume(FAB,T.i) = Sum
  lower_volume(FAC,T1.i) + Sum lower_volume(FCB,T2.i)
  proof
    let i be Element of NAT;
    lower_volume(FAB,T.i) = (lower_volume(FAC,T1.i))^(lower_volume(FCB,
    T2.i)) by A255;
    hence thesis by RVSUM_1:105;
  end;
  now
    let i be Element of NAT;
    thus lower_sum(FAB,T).i =lower_sum(FAB,T.i) by INTEGRA2:def 5
      .=lower_sum(FAC,T1.i) + Sum lower_volume(FCB,T2.i) by A277
      .=lower_sum(FAC,T1).i + lower_sum(FCB,T2.i) by INTEGRA2:def 5
      .=lower_sum(FAC,T1).i + lower_sum(FCB,T2).i by INTEGRA2:def 5;
  end;
  then
A278: lower_sum(FAB,T) = lower_sum(FAC,T1) + lower_sum(FCB,T2) by
  SEQ_1:11;
A279: FAC is Function of [' a,c '],REAL by A2,A137,Lm1,XBOOLE_1:1;
  then
A280: FAC is upper_integrable & FAC is lower_integrable by
  A138,INTEGRA4:
  10;
A281: upper_sum(FAC,T1) is convergent & lim upper_sum(FAC,T1) =
  upper_integral FAC by A279,A138,A53,INTEGRA4:9;
  then
A282: upper_integral FAC + upper_integral FCB = upper_integral FAB by A165,
  A216,A254,SEQ_2:20;
A283: lower_sum(FAC,T1) is convergent & lim lower_sum(FAC,T1) =
  lower_integral FAC by A279,A138,A53,INTEGRA4:8;
  FAB|[' a,b '] is bounded by A4,INTEGRA5:9;
  then
A284: lower_integral FAB = lim lower_sum(FAB,T) by A12,A252,A253,
  INTEGRA4:8;
  lower_sum(FCB,T2) is convergent & lim lower_sum(FCB,T2) =
  lower_integral FCB by A166,A18,A214,A215,INTEGRA4:8;
  then
A285: lower_integral FAC + lower_integral FCB = lower_integral FAB by A278,
  A283,A284,SEQ_2:20;
  integral(f,a,b) = integral(f,[' a,b ']) by A1,INTEGRA5:def 5
    .= integral FAC + integral FCB by A165,A281,A216,A254,SEQ_2:20;
  hence integral(f,a,b) = integral(f,a,c) + integral(f,c,b) by A58,A57;
  FAB is integrable by A3,INTEGRA5:def 2;
  then
A286: upper_integral FAB = lower_integral FAB by INTEGRA1:def 17;
A287: FCB is upper_integrable & FCB is lower_integrable by A166,A18
  ,INTEGRA4:10;
A288: lower_integral FAC <= upper_integral FAC by A279,A138,INTEGRA1:51;
  then lower_integral FCB = upper_integral FCB by A286,A282,A285,A167,Th1;
  then
A289: FCB is integrable by A287,INTEGRA1:def 17;
  lower_integral FAC = upper_integral FAC by A286,A282,A285,A288,A167,Th1;
  then FAC is integrable by A280,INTEGRA1:def 17;
  hence thesis by A289,INTEGRA5:def 2;
end;

theorem Th17:
  a <= b & f is_integrable_on [' a,b '] & f|[' a,b '] is bounded &
  [' a,b '] c= dom f & c in [' a,b '] implies f is_integrable_on ['a,c '] & f
  is_integrable_on ['c,b '] & integral(f,a,b )=integral(f,a,c )+integral(f,c,b)
proof
  assume that
A1: a <= b and
A2: f is_integrable_on [' a,b '] and
A3: f|[' a,b '] is bounded & [' a,b '] c= dom f & c in [' a,b '];
  now
    assume
A4: b=c;
    then
A5: [' c,b ']= [.c,b.] by INTEGRA5:def 4;
    thus f is_integrable_on ['a,c '] by A2,A4;
    [' c,b ']= [. lower_bound([' c,b ']),upper_bound([' c,b ']) .]
    by INTEGRA1:5;
    then lower_bound([' c,b ']) = c & upper_bound([' c,b ']) = b
    by A5,INTEGRA1:6;
    then
A6: vol(['c,b ']) =0 by A4;
    then f||['c,b '] is integrable by INTEGRA4:6;
    hence f is_integrable_on ['c,b '] by INTEGRA5:def 2;
    c is Real & b is Real by XREAL_0:def 1;
    then integral(f,c,b ) = integral(f,[' c,b '] ) by A5,INTEGRA5:19;
    then integral(f,c,b ) =0 by A6,INTEGRA4:6;
    hence integral(f, a,b )= integral(f,a,c ) +integral(f,c,b) by A4;
  end;
  hence thesis by A1,A2,A3,Lm2;
end;

Lm3: for a,b,c be real number st a <= c & c <= b holds c in [' a,b '] & [' a,c
'] c= [' a,b '] & [' c,b '] c= [' a,b ']
proof
  let a,b,c be real number;
  assume that
A1: a <= c and
A2: c <= b;
A3: c in [.a,b.] by A1,A2,XXREAL_1:1;
  hence c in [' a,b '] by A1,A2,INTEGRA5:def 4,XXREAL_0:2;
A4: [' c,b '] = [.c,b.] by A2,INTEGRA5:def 4;
  a <= b by A1,A2,XXREAL_0:2;
  then
A5: a in [.a,b.] & b in [.a,b.] by XXREAL_1:1;
  [' a,b '] = [.a,b.] & [' a,c '] = [.a,c.] by A1,A2,INTEGRA5:def 4 ,
  XXREAL_0:2;
  hence thesis by A4,A3,A5,XXREAL_2:def 12;
end;

theorem Th18:
  a <= c & c <= d & d <= b & f is_integrable_on [' a,b '] & f|[' a
,b '] is bounded & [' a,b '] c= dom f implies f is_integrable_on ['c,d '] & f|
  [' c,d '] is bounded & ['c,d '] c= dom f
proof
  assume that
A1: a <= c and
A2: c <= d and
A3: d <= b and
A4: f is_integrable_on [' a,b '] and
A5: f|[' a,b '] is bounded and
A6: [' a,b '] c= dom f;
  a <= d by A1,A2,XXREAL_0:2;
  then
A7: a <= b by A3,XXREAL_0:2;
A8: c <= b by A2,A3,XXREAL_0:2;
  then c in [' a,b '] by A1,Lm3;
  then
A9: f is_integrable_on ['c,b '] by A4,A5,A6,A7,Th17;
A10: d in [' c,b '] by A2,A3,Lm3;
A11: ['c,b '] c= [' a,b '] by A1,A8,Lm3;
  then ['c,b '] c= dom f & f|[' c,b '] is bounded by A5,A6,RFUNCT_1:91 ,
  XBOOLE_1:1;
  hence f is_integrable_on ['c,d '] by A8,A10,A9,Th17;
  [' c,d '] c= [' c,b '] by A2,A3,Lm3;
  then
A12: [' c,d '] c= [' a,b '] by A11,XBOOLE_1:1;
  hence f|[' c,d '] is bounded by A5,RFUNCT_1:91;
  thus thesis by A6,A12,XBOOLE_1:1;
end;

theorem
  a <= c & c <= d & d <= b & f is_integrable_on [' a,b '] & g
is_integrable_on [' a,b '] & f|[' a,b '] is bounded & g|[' a,b '] is bounded &
[' a,b '] c= dom f & [' a,b '] c= dom g implies f+g is_integrable_on ['c,d '] &
  (f+g)|[' c,d '] is bounded
proof
  assume that
A1: a <= c and
A2: c <= d & d <= b and
A3: f is_integrable_on [' a,b '] & g is_integrable_on [' a,b '] & f|[' a
  ,b '] is bounded & g|[' a,b '] is bounded and
A4: [' a,b '] c= dom f and
A5: [' a,b '] c= dom g;
A6: [' c,d '] c= [' c,b '] by A2,Lm3;
A7: f|[' c,d '] is bounded & g|[' c,d '] is bounded by A1,A2,A3,A4,A5,Th18;
  c <=b by A2,XXREAL_0:2;
  then
A8: ['c,b '] c= [' a,b '] by A1,Lm3;
  then ['c,b '] c= dom f by A4,XBOOLE_1:1;
  then
A9: [' c,d '] c= dom f by A6,XBOOLE_1:1;
  ['c,b '] c= dom g by A5,A8,XBOOLE_1:1;
  then
A10: [' c,d '] c= dom g by A6,XBOOLE_1:1;
  f is_integrable_on ['c,d '] & g is_integrable_on ['c,d '] by A1,A2,A3,A4
  ,A5,Th18;
  hence f+g is_integrable_on ['c,d '] by A7,A9,A10,Th11;
  (f+g)|([' c,d '] /\ ['c,d ']) is bounded by A7,RFUNCT_1:100;
  hence thesis;
end;

Lm4: for a,b,c,d be real number, f be PartFunc of REAL,REAL st a<=b & c <=d &
f is_integrable_on [' a,b '] & f|[' a,b '] is bounded & [' a,b '] c= dom f & c
in [' a,b '] & d in [' a,b '] holds integral(f,a,d)=integral(f,a,c)+integral(f,
c,d)
proof
  let a,b,c,d be real number, f be PartFunc of REAL,REAL;
  assume that
A1: a<=b and
A2: c <= d and
A3: f is_integrable_on [' a,b '] & f|[' a,b '] is bounded and
A4: [' a,b '] c= dom f and
A5: c in [' a,b '] and
A6: d in [' a,b '];
A7: f is_integrable_on [' a,d '] by A1,A3,A4,A6,Th17;
A8: [' a,b '] = [.a,b.] by A1,INTEGRA5:def 4;
  then
A9: a <= d by A6,XXREAL_1:1;
A10: d <= b by A6,A8,XXREAL_1:1;
  then [' a,d '] c= [' a,b '] by A9,Lm3;
  then
A11: [' a,d '] c= dom f by A4,XBOOLE_1:1;
  a <= c by A5,A8,XXREAL_1:1;
  then
A12: c in [' a,d '] by A2,Lm3;
  f|[' a,d '] is bounded by A3,A4,A9,A10,Th18;
  hence thesis by A9,A12,A11,A7,Th17;
end;

theorem Th20:
  a<=b & f is_integrable_on [' a,b '] & f|[' a,b '] is bounded &
  [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] implies integral(f,a,d)=
  integral(f,a,c)+integral(f,c,d)
proof
  assume
A1: a<=b & f is_integrable_on [' a,b '] & f|[' a,b '] is bounded & [' a,
  b '] c= dom f & c in [' a,b '] & d in [' a,b '];
  now
    assume
A2: not c <= d;
    then integral(f,a,c)=integral(f,a,d)+integral(f,d,c) by A1,Lm4;
    then
A3: integral(f,a,d) =integral(f,a,c) - integral(f,d,c);
    integral(f,c,d) = -integral(f,[' d,c ']) by A2,INTEGRA5:def 5;
    hence thesis by A2,A3,INTEGRA5:def 5;
  end;
  hence thesis by A1,Lm4;
end;

Lm5: for a,b,c,d be real number, f be PartFunc of REAL,REAL st a <= b & c <=d
& f is_integrable_on [' a,b '] & f|[' a,b '] is bounded & [' a,b '] c= dom f &
c in [' a,b '] & d in [' a,b '] holds [' c,d '] c= dom abs f & abs f
is_integrable_on [' c,d '] & abs f|[' c,d '] is bounded & abs integral(f,c,d)
<= integral(abs f,c,d)
proof
  let a,b,c,d be real number, f be PartFunc of REAL,REAL such that
A1: a <= b and
A2: c <= d and
A3: f is_integrable_on [' a,b '] & f|[' a,b '] is bounded & [' a,b '] c=
  dom f and
A4: c in [' a,b '] & d in [' a,b '];
  [' a,b '] = [.a,b.] by A1,INTEGRA5:def 4;
  then
A5: a<=c & d <= b by A4,XXREAL_1:1;
  then
A6: f|[' c,d '] is bounded by A2,A3,Th18;
  [' c,d '] c= dom f & f is_integrable_on [' c,d '] by A2,A3,A5,Th18;
  hence thesis by A2,A6,Th7,Th8,RFUNCT_1:99,VALUED_1:def 11;
end;

theorem Th21:
  a <= b & f is_integrable_on [' a,b '] & f|[' a,b '] is bounded &
[' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] implies [' min(c,d),max(c,
d) '] c= dom (abs f) & abs f is_integrable_on [' min(c,d),max(c,d) '] & abs f|
[' min(c,d),max(c,d) '] is bounded & abs integral(f,c,d) <= integral(abs f,min(
  c,d),max(c,d))
proof
  assume
A1: a <= b & f is_integrable_on [' a,b '] & f|[' a,b '] is bounded & ['
  a,b '] c= dom f & c in [' a,b '] & d in [' a,b '];
A2: now
    assume
A3: not c <= d;
    then integral(f,c,d) = -integral(f,[' d,c ']) by INTEGRA5:def 5;
    then integral(f,c,d) = -integral(f,d,c) by A3,INTEGRA5:def 5;
    then
A4: abs integral(f,c,d) = abs integral(f,d,c) by COMPLEX1:138;
    d = min(c,d) & c = max(c,d) by A3,XXREAL_0:def 9,def 10;
    hence thesis by A1,A3,A4,Lm5;
  end;
  now
    assume
A5: c <= d;
    then c = min(c,d) & d = max(c,d) by XXREAL_0:def 9,def 10;
    hence thesis by A1,A5,Lm5;
  end;
  hence thesis by A2;
end;

Lm6: a <= b & c <= d & f is_integrable_on [' a,b '] & f|[' a,b '] is bounded &
[' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] implies [' c,d '] c= dom (
abs f) & abs f is_integrable_on [' c,d '] & abs f|[' c,d '] is bounded & abs
integral(f,c,d) <= integral(abs f,c,d)
proof
  assume that
A1: a<=b and
A2: c <= d and
A3: f is_integrable_on [' a,b '] & f|[' a,b '] is bounded & [' a,b '] c=
  dom f & c in [' a,b '] & d in [' a,b '];
  min(c,d) = c & max(c,d) = d by A2,XXREAL_0:def 9,def 10;
  hence thesis by A1,A3,Th21;
end;

Lm7: a <= b & c <= d & f is_integrable_on [' a,b '] & f|[' a,b '] is bounded &
[' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] implies abs integral(f,d,c
) <= integral(abs f,c,d)
proof
  assume that
A1: a<=b and
A2: c <= d and
A3: f is_integrable_on [' a,b '] & f|[' a,b '] is bounded & [' a,b '] c=
  dom f & c in [' a,b '] and
A4: d in [' a,b '];
A5: abs integral(f,c,d) <= integral(abs f,c,d) & integral(f,c,d) = integral(
  f,[' c,d ']) by A1,A2,A3,A4,Lm6,INTEGRA5:def 5;
  per cases;
  suppose
    c = d;
    hence thesis by A1,A3,Lm6;
  end;
  suppose
    c <> d;
    then c < d by A2,XXREAL_0:1;
    then integral(f,d,c) = -integral(f,[' c,d ']) by INTEGRA5:def 5;
    hence thesis by A5,COMPLEX1:138;
  end;
end;

theorem
  a <= b & c <= d & f is_integrable_on [' a,b '] & f|[' a,b '] is
  bounded & [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] implies [' c,d
  '] c= dom (abs f) & abs f is_integrable_on [' c,d '] & abs f|[' c,d '] is
  bounded & abs integral(f,c,d) <= integral(abs f,c,d) & abs integral(f,d,c) <=
  integral(abs f,c,d) by Lm6,Lm7;

Lm8: ( a<=b & f is_integrable_on [' a,b '] & f|[' a,b '] is bounded & [' a,b
'] c= dom f & c in [' a,b '] & d in [' a,b '] & for x be real number st x in ['
min(c,d),max(c,d) '] holds abs(f.x) <= e ) implies abs integral(f,c,d) <= e *
abs(d-c)
proof
  set A = [' min(c,d),max(c,d) '];
  assume that
A1: a<=b & f is_integrable_on [' a,b '] & f|[' a,b '] is bounded & [' a,
  b '] c= dom f & c in [' a,b '] & d in [' a,b '] and
A2: for x be real number st x in [' min(c,d),max(c,d) '] holds abs(f.x)
  <= e;
  rng abs f c= REAL;
  then
A3: abs f is Function of dom abs f,REAL by FUNCT_2:4;
  [' min(c,d),max(c,d) '] c= dom (abs f) by A1,Th21;
  then reconsider g =abs(f)||A as Function of A,REAL by A3,FUNCT_2:38;
A4: vol A = abs(d-c) by Th6;
  abs f is_integrable_on A by A1,Th21;
  then
A5: g is integrable by INTEGRA5:def 2;
  reconsider e as Real by XREAL_0:def 1;
  consider h be Function of A,REAL such that
A6: rng h = {e} and
A7: h|A is bounded by INTEGRA4:5;
A8: now
    let x be Real;
    assume
A9: x in A;
    then g.x = (abs f).x by FUNCT_1:72;
    then
A10: g.x = abs(f.x) by VALUED_1:18;
    h.x in {e} by A6,A9,FUNCT_2:6;
    then h.x =e by TARSKI:def 1;
    hence g.x <= h.x by A2,A9,A10;
  end;
A11: abs integral(f,c,d) <= integral(abs f,min(c,d),max(c,d)) by A1,Th21;
  min(c,d) <= c & c <= max(c,d) by XXREAL_0:17,25;
  then min(c,d) <= max(c,d) by XXREAL_0:2;
  then
A12: integral(abs f,min(c,d),max(c,d)) = integral(abs f,A) by INTEGRA5:
  def 5;
  abs f|A is bounded by A1,Th21;
  then
A13: g|A is bounded by INTEGRA5:9;
  h is integrable & integral h = e*vol A by A6,INTEGRA4:4;
  then
  integral(abs f,min(c,d),max(c,d)) <= e * abs(d-c) by A12,A7,A8,A5,A13
  ,A4,INTEGRA2:34;
  hence thesis by A11,XXREAL_0:2;
end;

Lm9: ( a<=b & c <= d & f is_integrable_on [' a,b '] & f|[' a,b '] is bounded &
[' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] & for x be real number st
x in [' c,d '] holds abs(f.x) <= e ) implies abs integral(f,c,d) <= e * (d-c)
proof
  assume that
A1: a<=b and
A2: c <= d and
A3: f is_integrable_on [' a,b '] & f|[' a,b '] is bounded & [' a,b '] c=
dom f & c in [' a,b '] &( d in [' a,b '] & for x be real number st x in [' c,d
  '] holds abs(f.x) <= e);
  0 <= d - c by A2,XREAL_1:50;
  then
A4: abs(d-c) = d-c by ABSVALUE:def 1;
  min(c,d) = c & max(c,d) = d by A2,XXREAL_0:def 9,def 10;
  hence thesis by A1,A3,A4,Lm8;
end;

Lm10: ( a<=b & c <= d & f is_integrable_on [' a,b '] & f|[' a,b '] is bounded
& [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] & for x be real number
st x in [' c,d '] holds abs(f.x) <= e ) implies abs integral(f,d,c) <= e * (d-c
)
proof
  assume that
A1: a<=b and
A2: c <= d and
A3: f is_integrable_on [' a,b '] & f|[' a,b '] is bounded & [' a,b '] c=
  dom f & c in [' a,b '] and
A4: d in [' a,b '] and
A5: for x be real number st x in [' c,d '] holds abs(f.x) <= e;
A6: abs integral(f,c,d) <= e * (d-c) & integral(f,c,d) = integral(f,[' c,d
  ']) by A1,A2,A3,A4,A5,Lm9,INTEGRA5:def 5;
  per cases;
  suppose
    c = d;
    hence thesis by A1,A3,A5,Lm9;
  end;
  suppose
    c <> d;
    then c < d by A2,XXREAL_0:1;
    then integral(f,d,c) = -integral(f,[' c,d ']) by INTEGRA5:def 5;
    hence thesis by A6,COMPLEX1:138;
  end;
end;

theorem
  ( a<=b & c <= d & f is_integrable_on [' a,b '] & f|[' a,b '] is
bounded & [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] & for x be real
number st x in [' c,d '] holds abs(f.x) <= e ) implies abs integral(f,c,d) <= e
  * (d-c) & abs integral(f,d,c) <= e * (d-c) by Lm9,Lm10;

Lm11: for a,b,c,d be real number, f,g be PartFunc of REAL,REAL st a <= b & c
<= d & f is_integrable_on [' a,b '] & g is_integrable_on [' a,b '] & f|[' a,b
'] is bounded & g|[' a,b '] is bounded & [' a,b '] c= dom f & [' a,b '] c= dom
g & c in ['a,b '] & d in ['a,b '] holds f+g is_integrable_on ['c,d '] & (f+g)|
[' c,d '] is bounded & integral(f+g,c,d) =integral(f,c,d)+integral(g,c,d) & f-g
is_integrable_on ['c,d '] & (f-g)|[' c,d '] is bounded & integral(f-g,c,d) =
integral(f,c,d)-integral(g,c,d)
proof
  let a,b,c,d be real number, f,g be PartFunc of REAL,REAL;
  assume that
A1: a <= b and
A2: c <= d and
A3: f is_integrable_on [' a,b '] and
A4: g is_integrable_on [' a,b '] and
A5: f|[' a,b '] is bounded and
A6: g|[' a,b '] is bounded and
A7: [' a,b '] c= dom f and
A8: [' a,b '] c= dom g and
A9: c in ['a,b '] & d in ['a,b '];
  ['a,b '] = [.a,b.] by A1,INTEGRA5:def 4;
  then
A10: a<=c & d <= b by A9,XXREAL_1:1;
  then
A11: f is_integrable_on ['c,d '] & ['c,d '] c= dom f by A2,A3,A5,A7,
  Th18;
A12: g is_integrable_on ['c,d '] & ['c,d '] c= dom g by A2,A4,A6,A8,A10,Th18;
A13: f|[' c,d '] is bounded & g|[' c,d '] is bounded by A2,A3,A4,A5,A6,A7,A8
  ,A10,Th18;
  then (f+g)|([' c,d '] /\ ['c,d ']) is bounded by RFUNCT_1:100;
  hence thesis by A2,A11,A13,A12,Th11,Th12,RFUNCT_1:101;
end;

theorem Th24:
  a <= b & f is_integrable_on [' a,b '] & g is_integrable_on [' a,
  b '] & f|[' a,b '] is bounded & g|[' a,b '] is bounded & [' a,b '] c= dom f &
[' a,b '] c= dom g & c in ['a,b '] & d in ['a,b '] implies integral(f+g,c,d) =
  integral(f,c,d) + integral(g,c,d) & integral(f-g,c,d) = integral(f,c,d) -
  integral(g,c,d)
proof
  assume
A1: a <= b & f is_integrable_on [' a,b '] & g is_integrable_on [' a,b ']
& f| [' a,b '] is bounded & g|[' a,b '] is bounded & [' a,b '] c= dom f & [' a,
  b '] c= dom g & c in ['a,b '] & d in ['a,b '];
  now
    assume
A2: not c <= d;
    then
A3: integral(f,c,d) = -integral(f,[' d,c ']) by INTEGRA5:def 5;
A4: integral(g,c,d) = -integral(g,[' d,c ']) by A2,INTEGRA5:def 5;
    integral(f+g,c,d) = -integral(f+g,[' d,c ']) by A2,INTEGRA5:def 5;
    hence integral(f+g,c,d) = -integral(f+g,d,c) by A2,INTEGRA5:def 5
      .= -(integral(f,d,c)+integral(g,d,c)) by A1,A2,Lm11
      .= -integral(f,d,c)-integral(g,d,c)
      .= integral(f,c,d) -integral(g,d,c) by A2,A3,INTEGRA5:def 5
      .= integral(f,c,d) +integral(g,c,d) by A2,A4,INTEGRA5:def 5;
    integral(f-g,c,d) = -integral(f-g,[' d,c ']) by A2,INTEGRA5:def 5;
    hence integral(f-g,c,d) =-integral(f-g,d,c) by A2,INTEGRA5:def 5
      .= -(integral(f,d,c)-integral(g,d,c)) by A1,A2,Lm11
      .= -(integral(f,d,c)+integral(g,c,d)) by A2,A4,INTEGRA5:def 5
      .=-integral(f,d,c)-integral(g,c,d)
      .= integral(f,c,d) -integral(g,c,d) by A2,A3,INTEGRA5:def 5;
  end;
  hence thesis by A1,Lm11;
end;

Lm12: for a,b,c,d,e be real number, f be PartFunc of REAL,REAL st a <= b & c
<= d & f is_integrable_on [' a,b '] & f|[' a,b '] is bounded & [' a,b '] c= dom
f & c in ['a,b '] & d in ['a,b '] holds integral(e(#)f,c,d)=e*integral(f,c,d)
proof
  let a,b,c,d,e be real number, f be PartFunc of REAL,REAL;
  assume that
A1: a <= b and
A2: c <= d and
A3: f is_integrable_on [' a,b '] & f|[' a,b '] is bounded & [' a,b '] c=
  dom f and
A4: c in ['a,b '] & d in ['a,b '];
  ['a,b '] = [.a,b.] by A1,INTEGRA5:def 4;
  then
A5: a <= c & d <= b by A4,XXREAL_1:1;
  then
A6: ['c,d '] c= dom f by A2,A3,Th18;
  f is_integrable_on ['c,d '] & f|[' c,d '] is bounded by A2,A3,A5,Th18;
  hence thesis by A2,A6,Th10;
end;

theorem
  a <= b & f is_integrable_on [' a,b '] & f|[' a,b '] is bounded & [' a,
  b '] c= dom f & c in ['a,b '] & d in ['a,b '] implies integral(e(#)f,c,d)=e*
  integral(f,c,d)
proof
  assume
A1: a <= b & f is_integrable_on [' a,b '] & f|[' a,b '] is bounded & ['
  a,b '] c= dom f & c in ['a,b '] & d in ['a,b '];
  now
    assume
A2: not c <= d;
    then
A3: integral(f,c,d) = -integral(f,[' d,c ']) by INTEGRA5:def 5;
    thus integral(e(#)f,c,d) = -integral(e(#)f,[' d,c ']) by A2,INTEGRA5:def 5
      .= -integral(e(#)f,d,c) by A2,INTEGRA5:def 5
      .= -e*integral(f,d,c) by A1,A2,Lm12
      .= e*(-integral(f,d,c))
      .= e*integral(f,c,d) by A2,A3,INTEGRA5:def 5;
  end;
  hence thesis by A1,Lm12;
end;

theorem Th26:
  ( a <= b & [' a,b '] c= dom f & for x be real number st x in ['
  a,b '] holds f.x=e ) implies f is_integrable_on [' a,b '] & f|[' a,b '] is
  bounded & integral(f,a,b) = e*(b-a)
proof
  assume that
A1: a <= b and
A2: [' a,b '] c= dom f and
A3: for x be real number st x in [' a,b '] holds f.x = e;
  rng f c= REAL;
  then f is Function of dom f,REAL by FUNCT_2:4;
  then reconsider g =f||[' a,b '] as Function of [' a,b '],REAL by A2,
  FUNCT_2:38;
  reconsider e1=e as Real by XREAL_0:def 1;
  consider h be Function of [' a,b '],REAL such that
A4: rng h = {e1} and
A5: h|[' a,b '] is bounded by INTEGRA4:5;
  integral h = e1*vol([' a,b ']) by A4,INTEGRA4:4;
  then
A6: integral h = e*(b-a) by A1,Th5;
A7: for x be set st x in [' a,b '] holds g.x = h.x
  proof
    let x0 be set;
    assume
A8: x0 in [' a,b '];
    then reconsider x=x0 as real number;
    g.x0 = f.x by A8,FUNCT_1:72;
    then
A9: g.x0 = e by A3,A8;
    h.x in {e1} by A4,A8,FUNCT_2:6;
    hence thesis by A9,TARSKI:def 1;
  end;
  then
A10: h=g by FUNCT_2:18;
  h is integrable by A4,INTEGRA4:4;
  hence f is_integrable_on [' a,b '] by A10,INTEGRA5:def 2;
  thus f|[' a,b '] is bounded
  proof
    consider r be real number such that
A11: for c be set st c in [' a,b '] /\ dom g holds abs(g.c)<=r by A5,
    A10,RFUNCT_1:90;
    now
      let c being set;
      [' a,b '] /\ dom g = [' a,b '] /\ (dom f /\ [' a,b ']) by RELAT_1:
      90;
      then
A12:  [' a,b '] /\ dom g = [' a,b '] /\ [' a,b '] /\ dom f by
      XBOOLE_1:16;
      assume c in [' a,b '] /\ dom f;
      then abs(g.c) <= r & c in dom g by A11,A12,XBOOLE_0:def 4;
      hence abs(f.c)<=r by FUNCT_1:70;
    end;
    hence thesis by RFUNCT_1:90;
  end;
  integral(f,a,b) = integral(f,[' a,b ']) by A1,INTEGRA5:def 5;
  hence thesis by A7,A6,FUNCT_2:18;
end;

theorem Th27:
  a <= b & (for x be real number st x in [' a,b '] holds f.x=e) &
[' a,b '] c= dom f & c in ['a,b '] & d in ['a,b '] implies integral(f,c,d) = e*
  (d-c)
proof
  assume that
A1: a<=b and
A2: for x be real number st x in [' a,b '] holds f.x=e and
A3: [' a,b '] c= dom f and
A4: c in ['a,b '] and
A5: d in ['a,b '];
A6: f is_integrable_on ['a,b '] & f|[' a,b '] is bounded by A1,A2,A3,Th26;
A7: ['a,b '] = [.a,b.] by A1,INTEGRA5:def 4;
  then
A8: c <= b by A4,XXREAL_1:1;
A9: a <= c by A4,A7,XXREAL_1:1;
  then
A10: ['c,b '] c= [' a,b '] by A8,Lm3;
A11: d <= b by A5,A7,XXREAL_1:1;
A12: a <= d by A5,A7,XXREAL_1:1;
A13: ['a,c '] c= [' a,b '] by A9,A8,Lm3;
  per cases;
  suppose
A14: c <= d;
    then ['c,d '] c= [' c,b '] by A11,Lm3;
    then ['c,d '] c= ['a,b '] by A10,XBOOLE_1:1;
    then
A15: for x be real number st x in ['c,d '] holds f.x=e by A2;
    ['c,d '] c= dom f by A3,A9,A11,A6,A14,Th18;
    hence thesis by A14,A15,Th26;
  end;
  suppose
A16: not c <= d;
    then ['d,c '] c= [' a,c '] by A12,Lm3;
    then ['d,c '] c= ['a,b '] by A13,XBOOLE_1:1;
    then
A17: for x be real number st x in ['d,c '] holds f.x=e by A2;
    integral(f,c,d) = -integral(f,[' d,c ']) by A16,INTEGRA5:def 5;
    then
A18: integral(f,c,d) = -integral(f,d,c) by A16,INTEGRA5:def 5;
    ['d,c '] c= dom f by A3,A12,A8,A6,A16,Th18;
    then integral(f,d,c) = e*(c-d) by A16,A17,Th26;
    hence thesis by A18;
  end;
end;

begin :: Fundamental Theorem of Calculus

theorem Th28:
  for x0 be real number, F be PartFunc of REAL,REAL st a <= b & f
is_integrable_on [' a,b '] & f|[' a,b '] is bounded & [' a,b '] c= dom f & ].a,
b.[ c= dom F & (for x be real number st x in ].a,b.[ holds F.x = integral(f,a,x
  )) & x0 in ].a,b.[ & f is_continuous_in x0 holds F is_differentiable_in x0 &
  diff(F,x0)=f.x0
proof
  deffunc F2(set) = 0;
  let x0 be real number, F be PartFunc of REAL,REAL;
  assume that
A1: a <= b and
A2: f is_integrable_on [' a,b '] and
A3: f|[' a,b '] is bounded and
A4: [' a,b '] c= dom f and
A5: ].a,b.[ c= dom F and
A6: for x be real number st x in ].a,b.[ holds F.x = integral(f,a,x) and
A7: x0 in ].a,b.[ and
A8: f is_continuous_in x0;
  defpred C1[set] means (x0+R_id($1)) in ].a,b.[;
  deffunc F0(Element of REAL)=(f.x0)*$1;
  consider L be Function of REAL,REAL such that
A9: for h be Real holds L.h=F0(h) from FUNCT_2:sch 4;
  reconsider L as PartFunc of REAL,REAL;
  deffunc F1(set) = F.(x0+R_id($1))-F.x0-L.R_id($1);
  reconsider L as LINEAR by A9,FDIFF_1:def 4;
  consider R be Function such that
A10: dom R=REAL & for x be set st x in REAL holds (C1[x] implies R.x =
  F1(x)) & (not C1[x] implies R.x = F2(x)) from PARTFUN1:sch 1;
  rng R c= REAL
  proof
    let y be set;
    assume y in rng R;
    then consider x be set such that
A11: x in dom R and
A12: y=R.x by FUNCT_1:def 5;
A13: not C1[x] implies R.x = F2(x) by A10,A11;
    C1[x] implies R.x = F1(x) by A10,A11;
    hence thesis by A12,A13;
  end;
  then reconsider R as PartFunc of REAL,REAL by A10,RELSET_1:11;
A14: R is total by A10,PARTFUN1:def 4;
A15: [' a,b '] = [.a,b.] by A1,INTEGRA5:def 4;
A16: for h be convergent_to_0 Real_Sequence holds (h")(#)(R/*h) is
  convergent & lim ((h")(#)(R/*h)) = 0
  proof
    let h be convergent_to_0 Real_Sequence;
A17: for e be real number st 0<e ex n be Element of NAT st for m be
    Element of NAT st n <= m holds abs(((h")(#)(R/*h)).m - 0) < e
    proof
      set g = REAL --> f.x0;
      let e0 be real number;
      set e=e0/2;
A18:  h is convergent & lim(h) = 0 by FDIFF_1:def 1;
      assume
A19:  0 < e0;
      then 0 < e by XREAL_1:217;
      then consider p be real number such that
A20:  0 < p and
A21:  for t be real number st t in dom f & abs(t - x0) < p holds abs(
      f.t - f.x0) < e by A8,FCONT_1:3;
A22:  0 < p/2 by A20,XREAL_1:217;
A23:  p/2 < p by A20,XREAL_1:218;
      consider N be Neighbourhood of x0 such that
A24:  N c= ].a,b.[ by A7,RCOMP_1:39;
      consider q be real number such that
A25:  0<q and
A26:  N = ].x0-q,x0+q.[ by RCOMP_1:def 7;
A27:  q/2 < q by A25,XREAL_1:218;
      set r=min(p/2,q/2);
      0 < q/2 by A25,XREAL_1:217;
      then 0 < r by A22,XXREAL_0:15;
      then consider n be Element of NAT such that
A28:  for m be Element of NAT st n <= m holds abs(h.m - 0)<r by
      A18,SEQ_2:def 7;
      take n;
      let m be Element of NAT;
      r <= q/2 by XXREAL_0:17;
      then
A29:  ].x0-r,x0+r.[ c= ].x0-q,x0+q.[ by A27,Th2,XXREAL_0:2;
      assume n <= m;
      then
A30:  abs(h.m - 0) < r by A28;
      then abs((x0 + h.m)-x0) < r;
      then x0 + h.m in ].x0-r,x0+r.[ by RCOMP_1:8;
      then
A31:  x0 + h.m in ].x0-q,x0+q.[ by A29;
      then
A32:  x0+h.m in ].a,b.[ by A24,A26;
      then x0+R_id(h.m) in ].a,b.[ by RSSPACE:def 3;
      then R.(h.m)=F.(x0+R_id(h.m))-F.x0-L.R_id(h.m) by A10;
      then R.(h.m)=F.(x0+h.m)-F.x0-L.R_id(h.m) by RSSPACE:def 3;
      then R.(h.m) = F.(x0+h.m)-F.x0-L.(h.m) by RSSPACE:def 3;
      then
A33:  R.(h.m) = F.(x0+h.m)-F.x0-(f.x0)*h.m by A9;
      F.(x0+h.m) = integral(f,a,x0+h.m) by A6,A24,A26,A31;
      then
A34:  R.(h.m) =integral(f,a,x0+h.m ) -integral(f,a,x0) -(f.x0)*h.m by A6
      ,A7,A33;
A35:  dom g = REAL by FUNCT_2:def 1;
      then [' a,b '] /\ [' a,b '] c= dom f /\ dom g by A4,XBOOLE_1:27;
      then
A36:  [' a,b '] c= dom (f-g) by VALUED_1:12;
A37:  ].a,b.[ c= [.a,b.] by XXREAL_1:25;
      then
A38:  integral(f,a,x0+h.m) = integral(f,a,x0) +integral(f,x0,x0+h.m) by
      A1,A2,A3,A4,A7,A15,A32,Th20;
A39:  r <= p/2 by XXREAL_0:17;
A40:  for x be real number st x in [' min(x0,x0+h.m),max(x0,x0+h.m) ']
      holds abs((f-g).x) <= e
      proof
        let x be real number;
        reconsider x1 = x as Real by XREAL_0:def 1;
A41:    min(x0,x0+h.m) <= x0 & x0 <= max(x0,x0+h.m) by XXREAL_0:17,25;
        assume x in [' min(x0,x0+h.m),max(x0,x0+h.m) '];
        then
A42:    x in [. min(x0,x0+h.m),max(x0,x0+h.m) .] by
        A41, INTEGRA5:def
        4,XXREAL_0:2;
        abs(x-x0) <=abs(h.m)
        proof
          per cases;
          suppose
            x0<= x0+h.m;
            then x0 = min(x0,x0+h.m) & x0+h.m = max(x0,x0+h.m) by
            XXREAL_0
            :def 9,def 10;
            then x in {z where z is Real: x0<=z & z<=x0+h.m } by A42,
            RCOMP_1:def 1;
            then
A43:        ex z be Real st x=z & x0<=z & z<=x0+h.m;
            then 0 <= x-x0 by XREAL_1:50;
            then
A44:        abs(x-x0) = x-x0 by ABSVALUE:def 1;
A45:        x-x0 <= x0+h.m - x0 by A43,XREAL_1:11;
            then 0 <= h.m by A43,XREAL_1:50;
            hence thesis by A45,A44,ABSVALUE:def 1;
          end;
          suppose
A46:        not x0<=x0 +h.m;
            then x0 = max(x0,x0+h.m) & x0+h.m = min(x0,x0+h.m) by
            XXREAL_0
            :def 9,def 10;
            then x in {z where z is Real: x0+h.m<=z & z<=x0 } by A42,
            RCOMP_1:def 1;
            then
A47:        ex z be Real st x=z & x0+h.m<=z & z<=x0;
            then
A48:        x0 +h.m -x0 <= x-x0 by XREAL_1:11;
            per cases;
            suppose
A49:          x-x0 <> 0;
              x0 + h.m - x0 < x0-x0 by A46,XREAL_1:16;
              then
A50:          abs(h.m) = -(h.m) by ABSVALUE:def 1;
              x-x0 < 0 by A47,A49,XREAL_1:49;
              then abs(x-x0) = -(x-x0) by ABSVALUE:def 1;
              hence thesis by A48,A50,XREAL_1:26;
            end;
            suppose
              x-x0 = 0;
              then abs(x-x0) = 0 by ABSVALUE:def 1;
              hence thesis by COMPLEX1:132;
            end;
          end;
        end;
        then
A51:    abs(x-x0) < r by A30,XXREAL_0:2;
        then x in ].x0-r,x0+r.[ by RCOMP_1:8;
        then x in ].x0-q,x0+q.[ by A29;
        then x in ].a,b.[ by A24,A26;
        then
A52:    x in [.a,b.] by A37;
        abs(x-x0) < p/2 by A39,A51,XXREAL_0:2;
        then abs(x-x0) < p by A23,XXREAL_0:2;
        then abs(f.x- f.x0)<=e by A4,A15,A21,A52;
        then abs(f.x1- g.x1)<=e by FUNCOP_1:13;
        hence thesis by A15,A36,A52,VALUED_1:13;
      end;
A53:  for x be real number st x in [' a,b '] holds g.x = f.x0 by FUNCOP_1
      :13;
      then
A54:  g|[' a,b '] is bounded by A1,A35,Th26;
      then
A55:  (f-g)|(['a,b '] /\ ['a,b ']) is bounded by A3,RFUNCT_1:101;
      rng h c= dom R by A10;
      then R.(h.m)/(h.m) =(R/*h).m /(h.m) by FUNCT_2:185;
      then R.(h.m)/(h.m) =((R/*h).m) *((h").m) by VALUED_1:10;
      then
A56:  R.(h.m)/(h.m) = ((h")(#)(R/*h)).m by SEQ_1:12;
      h is non-zero by FDIFF_1:def 1;
      then h.m <> 0 by SEQ_1:6;
      then
A57:  0 < abs(h.m) by COMPLEX1:133;
A58:  g is_integrable_on [' a,b '] by A1,A35,A53,Th26;
      then f-g is_integrable_on [' a,b '] by A2,A3,A4,A35,A54,Th11;
      then
A59:  abs integral(f-g,x0,x0+h.m) <= e*abs(x0+h.m-x0) by A1,A7,A15,A37,A32
      ,A55,A36,A40,Lm8;
      integral(g,x0,x0+h.m)=(f.x0)*((x0+h.m)-x0) by A1,A7,A15,A37,A32,A35,
      A53, Th27;
      then R.(h.m) =integral(f-g,x0,x0+h.m) by A1,A2,A3,A4,A7,A15,A37,A32,A34
      ,
      A35,A58,A54,A38,Th24;
      then abs(R.(h.m)/(h.m)) = abs(R.(h.m))/abs(h.m) & abs(R.(h.m))/abs(h.m)
      <= e*abs( h.m)/abs(h.m) by A59,A57,COMPLEX1:153,XREAL_1:74;
      then
A60:  abs(R.(h.m)/(h.m)) <= e by A57,XCMPLX_1:90;
      e < e0 by A19,XREAL_1:218;
      hence thesis by A60,A56,XXREAL_0:2;
    end;
    hence (h")(#)(R/*h) is convergent by SEQ_2:def 6;
    hence thesis by A17,SEQ_2:def 7;
  end;
  consider N be Neighbourhood of x0 such that
A61: N c= ].a,b.[ by A7,RCOMP_1:39;
  reconsider R as REST by A14,A16,FDIFF_1:def 3;
A62: for x be Real st x in N holds F.x-F.x0 = L.(x-x0) + R.(x-x0)
  proof
    let x be Real;
    assume x in N;
    then x0 + (x-x0) in N;
    then x0+R_id(x-x0) in N by RSSPACE:def 3;
    then x0+R_id((x-x0)) = x0 + (x-x0) & R.(x-x0) = F1(x-x0) by A10,A61,
    RSSPACE:def 3;
    hence thesis;
  end;
A63: N c= dom F by A5,A61,XBOOLE_1:1;
  hence
A64: F is_differentiable_in x0 by A62,FDIFF_1:def 5;
  L.1=(f.x0)*1 by A9;
  hence thesis by A63,A62,A64,FDIFF_1:def 6;
end;

Lm13: for a,b be real number, f be PartFunc of REAL,REAL holds ex F be
PartFunc of REAL,REAL st ].a,b.[ c= dom F & for x be real number st x in ].a,b
.[ holds F.x = integral(f,a,x)
proof
  deffunc G(real number) = 0;
  let a,b be real number;
  let f be PartFunc of REAL,REAL;
  defpred C[set] means $1 in ].a,b.[;
  deffunc F(real number) = integral(f,a,$1);
  consider F be Function such that
A1: dom F = REAL & for x be Element of REAL holds (C[x] implies F.x = F(
  x)) & (not C[x] implies F.x = G(x)) from PARTFUN1:sch 4;
  now
    let y be set;
    assume y in rng F;
    then consider x be set such that
A2: x in dom F and
A3: y = F.x by FUNCT_1:def 5;
    reconsider x as Element of REAL by A1,A2;
A4: now
      assume not x in ].a,b.[;
      then F.x = 0 by A1;
      hence y in REAL by A3;
    end;
    now
      assume x in ].a,b.[;
      then F.x = integral(f,a,x) by A1;
      hence y in REAL by A3;
    end;
    hence y in REAL by A4;
  end;
  then rng F c= REAL by TARSKI:def 3;
  then reconsider F as PartFunc of REAL,REAL by A1,RELSET_1:11;
  take F;
  thus thesis by A1;
end;

theorem
  for x0 be real number st a <= b & f is_integrable_on [' a,b '] & f|['
a,b '] is bounded & [' a,b '] c= dom f & x0 in ].a,b.[ & f is_continuous_in x0
holds ex F be PartFunc of REAL,REAL st ].a,b.[ c= dom F & (for x be real number
st x in ].a,b.[ holds F.x = integral(f,a,x)) & F is_differentiable_in x0 & diff
  (F,x0)=f.x0
proof
  let x0 be real number;
  consider F be PartFunc of REAL,REAL such that
A1: ].a,b.[ c= dom F & for x be real number st x in ].a,b.[ holds F.x =
  integral (f,a,x) by Lm13;
  assume a <= b & f is_integrable_on [' a,b '] & f|[' a,b '] is bounded & ['
  a,b '] c= dom f & x0 in ].a,b.[ & f is_continuous_in x0;
  then F is_differentiable_in x0 & diff(F,x0)=f.x0 by A1,Th28;
  hence thesis by A1;
end;
