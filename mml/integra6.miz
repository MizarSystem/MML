:: Integrability and the Integral of Partial Functions from $\Bbb R$ into
:: $\Bbb R$
::  by Noboru Endou , Yasunari Shidama and Masahiko Yamazaki
::
:: Received November 17, 2006
:: Copyright (c) 2006 Association of Mizar Users

environ

 vocabularies INTEGRA1, FINSEQ_1, RLVECT_1, ARYTM_1, RELAT_1, FUNCT_1,
      PARTFUN1, REALSET1, SEQ_1, BOOLE, RFUNCT_1, FCONT_1, INT_1, SEQ_2,
      ABSVALUE, INTEGRA2, FUNCT_3, ORDINAL2, MEASURE5, ARYTM_3, SQUARE_1,
      FDIFF_1, RCOMP_1, INTEGRA5, ARYTM, RFINSEQ, INTEGRA4, RSSPACE, NAT_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, RELAT_1, ORDINAL1, NUMBERS, XCMPLX_0,
      XREAL_0, COMPLEX1, REAL_1, NAT_1, NAT_D, INT_1, FUNCT_1, RELSET_1,
      PARTFUN1, FUNCT_2, RCOMP_1, PSCOMP_1, FINSEQ_1, SEQ_1, RFUNCT_1, SEQ_2,
      GOBOARD1, FDIFF_1, RFINSEQ, PRE_CIRC, RVSUM_1, INTEGRA1, INTEGRA2,
      FCONT_1, RFUNCT_2, RSSPACE, INTEGRA4, INTEGRA5, XXREAL_0;
 constructors ARYTM_0, REAL_1, PRE_CIRC, FDIFF_1, RFINSEQ, PARTFUN1, RFUNCT_1,
      COMPLEX1, INTEGRA2, FCONT_1, RFUNCT_2, PSCOMP_1, NAT_1, BINOP_2,
      INTEGRA4, INTEGRA5, RSSPACE, XXREAL_0, NAT_D;
 registrations XREAL_0, RELSET_1, INTEGRA1, SEQ_1, RCOMP_1, INT_1, FUNCT_2,
      ORDINAL1, MEMBERED, FDIFF_1, GOBOARD1, FINSEQ_1, RELAT_1, XXREAL_0;
 requirements NUMERALS, BOOLE, SUBSET, REAL, ARITHM;
 definitions TARSKI, XCMPLX_0, INTEGRA1, FINSEQ_1, INTEGRA5;
 theorems ORDINAL1, REAL_1, REAL_2, PARTFUN1, INTEGRA1, RFUNCT_1, FUNCT_1,
      FINSEQ_1, RVSUM_1, SEQ_1, SEQ_2, FDIFF_1, ABSVALUE, PRE_CIRC, NAT_1,
      INT_1, COMPLEX1, TARSKI, INTEGRA2, INTEGRA3, RFINSEQ, GOBOARD1, INTEGRA4,
      FINSEQ_2, FINSEQ_3, RCOMP_1, FCONT_1, RFUNCT_2, XREAL_0, XXREAL_0,
      FUNCT_2, FINSEQ_5, XBOOLE_0, XBOOLE_1, XCMPLX_1, XREAL_1, INTEGRA5,
      RSSPACE, RELAT_1;
 schemes FUNCT_2, PARTFUN1, FINSEQ_2;

begin :: Preliminaries

reserve a,b,c,d,e,x for real number,
        A for closed-interval Subset of REAL,
        f,g for PartFunc of REAL,REAL;

theorem Th1:
a <= b & c <= d & a+c = b+d implies a = b & c = d
proof
   assume A1:a <=b & c <=d & a+c =b+d;
   assume not (a=b & c =d); then
   a < b or c < d by A1,REAL_1:def 5;
   hence contradiction by A1,XREAL_1:10;
end;

theorem Th2:
a <= b implies ].x-a,x+a.[ c= ].x-b,x+b.[
proof
   assume a <= b; then
   x-b <= x - a & x+a <= x+b by XREAL_1:9,12;
   hence thesis by RCOMP_1:52;
end;

theorem Th3:
for R be Relation, A,B,C be set st A c= B & A c= C holds (R|B)|A = (R|C)|A
proof
   let R be Relation, A,B,C be set;
   assume A1:A c= B & A c= C; then
   (R|C)|A = R|A by RELAT_1:103;
   hence thesis by A1,RELAT_1:103;
end;

theorem Th4:
for A,B,C be set st A c= B & A c= C holds
  chi(B,B)|A = chi(C,C)|A
proof
   let A,B,C be set;
   assume A1:A c= B & A c= C;
A2:now assume A is non empty; then
    {} c< A by XBOOLE_1:61; then
A3:B is non empty & C is non empty by A1,XBOOLE_1:58;
    dom(chi(B,B)|A) =dom(chi(B,B)) /\ A &
    dom(chi(C,C)|A) =dom(chi(C,C)) /\ A by FUNCT_1:68; then
    dom(chi(B,B)|A) =B /\ A & dom(chi(C,C)|A) =C /\ A by RFUNCT_1:77,A3; then
A4: dom(chi(B,B)|A) =A & dom(chi(C,C)|A) =A by A1,XBOOLE_1:28;
    now let x be set;
     assume A5:x in A; then
     (chi(B,B)|A).x =(chi(B,B)).x by FUNCT_1:68,A4; then
     (chi(B,B)|A).x = 1 by A5,A1,RFUNCT_1:77; then
     (chi(B,B)|A).x = (chi(C,C)).x by A1,A5,RFUNCT_1:77;
     hence (chi(B,B)|A).x = (chi(C,C)|A).x by A5,FUNCT_1:68,A4;
    end;
    hence chi(B,B)|A = chi(C,C)|A by FUNCT_1:9,A4;
   end;
   now assume A is empty; then
    chi(B,B)|A = {} & chi(C,C)|A = {} by RELAT_1:110;
    hence chi(B,B)|A = chi(C,C)|A;
   end;
   hence thesis by A2;
end;

theorem Th5:
a <= b implies vol([' a,b '])=b-a
proof
   assume a<=b; then
A1:[' a,b '] = [.a,b.] by INTEGRA5:def 4; then
   [.a,b.] = [.inf([.a,b.]), sup([.a,b.]).] by INTEGRA1:5; then
   a=inf([.a,b.]) & b=sup([.a,b.]) by A1,INTEGRA1:6;
   hence vol(['a,b ']) = b-a by A1;
end;

theorem Th6:
vol([' min(a,b),max(a,b) ']) = abs(b-a)
proof
A1:min(a,b) <= a & a <= max(a,b) by XXREAL_0:17,25;
   per cases;
   suppose A2:a<= b; then
    min(a,b) = a & max(a,b) = b by XXREAL_0:def 8,def 9; then
A3: vol([' min(a,b),max(a,b) '])=b-a by Th5,A1;
    0 <= b-a by A2,XREAL_1:50;
    hence vol([' min(a,b),max(a,b) '])=abs(b-a) by A3,ABSVALUE:def 1;
   end;
   suppose A4:not(a<= b); then
A5: min(a,b) = b & max(a,b) = a by XXREAL_0:def 8,def 9;
    0 <= a-b by A4,XREAL_1:50; then
    a-b = abs(a-b) by ABSVALUE:def 1
        .=abs(b-a) by COMPLEX1:146;
    hence vol([' min(a,b),max(a,b) '])=abs(b-a) by A5,Th5,A1;
   end;
end;

begin :: Integrability and The Integral of Partial Functions from REAL to REAL

Lm1:
A c= dom f implies f||A is Function of A,REAL
proof
   assume A1:A c= dom f;
   rng f c= REAL; then
   f is Function of dom f,REAL by FUNCT_2:4;
   hence f||A is Function of A,REAL by A1,FUNCT_2:38;
end;

theorem Th7:
 A c= dom f & f is_integrable_on A & f is_bounded_on A implies
  abs f is_integrable_on A & abs(integral(f,A)) <= integral(abs f,A)
proof
   assume A1:A c= dom f;
   assume f is_integrable_on A & f is_bounded_on A; then
A2:f||A is_integrable_on A & f||A is_bounded_on A by INTEGRA5:9,def 2;
   f||A is Function of A,REAL by A1,Lm1; then
A3:abs(f||A) is_integrable_on A &
   abs(integral(f||A))<=integral(abs(f||A)) by A2,INTEGRA4:23;
   abs(f||A) = abs(f)||A by RFUNCT_1:62;
   hence thesis by A3,INTEGRA5:def 2;
end;

theorem Th8:
 a<=b & [' a,b '] c= dom f & f is_integrable_on [' a,b '] &
 f is_bounded_on [' a,b '] implies
   abs(integral(f,a,b)) <=integral(abs f,a,b)
proof
   assume a<=b; then
   integral(f,a,b) = integral(f,[' a,b ']) &
   integral(abs f,a,b) = integral(abs f,[' a,b ']) by INTEGRA5:def 5;
   hence thesis by Th7;
end;

theorem Th9:
for r be Real st A c= dom f & f is_integrable_on A & f is_bounded_on A
 holds r(#)f is_integrable_on A & integral(r(#)f,A)=r*integral(f,A)
proof
   let r be Real;
   assume A1:A c= dom f & f is_integrable_on A & f is_bounded_on A; then
A2:f||A is_integrable_on A & f||A is_bounded_on A by INTEGRA5:9,def 2;
   rng f c= REAL; then
   f is Function of dom f,REAL by FUNCT_2:4; then
A3:f|A is Function of A,REAL by A1,FUNCT_2:38;
   r(#)(f||A) =(r(#)f)|A by RFUNCT_1:65; then
   (r(#)f)||A is_integrable_on A by A2,A3,INTEGRA2:31;
   hence r(#)f is_integrable_on A by INTEGRA5:def 2;
   integral((r(#)f),A) =integral(r(#)(f||A)) by RFUNCT_1:65;
   hence integral((r(#)f),A) =r*integral(f,A) by A2,A3,INTEGRA2:31;
end;

theorem Th10:
 a<=b & [' a,b '] c= dom f & f is_integrable_on [' a,b '] &
 f is_bounded_on [' a,b '] implies
   integral(c(#)f,a,b) =c*integral(f,a,b)
proof
   set F = f||['a,b '], G = (c(#)f)|['a,b '];
A1:c is Real by XREAL_0:def 1;
   assume A2:a<=b & [' a,b '] c= dom f & f is_integrable_on [' a,b '] &
    f is_bounded_on [' a,b ']; then
   integral(f,a,b) = integral(f,[' a,b ']) &
   integral(c(#)f,a,b) = integral(c(#)f,[' a,b ']) by INTEGRA5:def 5;
   hence thesis by A1,A2,Th9;
end;

theorem Th11:
 A c= dom f & A c= dom g & f is_integrable_on A & f is_bounded_on A &
 g is_integrable_on A & g is_bounded_on A implies
    f+g is_integrable_on A & f-g is_integrable_on A &
    integral(f+g,A) = integral(f,A) + integral(g,A) &
    integral(f-g,A) = integral(f,A) - integral(g,A)
proof
   assume
A1:A c= dom f & A c= dom g &
   f is_integrable_on A & f is_bounded_on A &
   g is_integrable_on A & g is_bounded_on A; then
A2:f||A is_integrable_on A & g||A is_integrable_on A &
   f||A is_bounded_on A & g||A is_bounded_on A by INTEGRA5:9,def 2;
A3:f||A is Function of A,REAL & g||A is Function of A,REAL by A1,Lm1;then
A4:f||A + g||A is_integrable_on A &
   f||A - g||A is_integrable_on A by INTEGRA1:59,INTEGRA2:33,A2;
A5:f||A + g||A = (f+g)||A & f||A - g||A = (f-g)||A by RFUNCT_1:60,63;
   hence f+g is_integrable_on A & f-g is_integrable_on A by A4,INTEGRA5:def 2;
   thus thesis by A2,A3,A5,INTEGRA1:59,INTEGRA2:33;
end;

theorem Th12:
 a<=b & [' a,b '] c= dom f & [' a,b '] c= dom g &
 f is_integrable_on [' a,b '] & g is_integrable_on [' a,b '] &
 f is_bounded_on [' a,b '] & g is_bounded_on [' a,b ']
implies
   integral(f+g,a,b) =integral(f,a,b) + integral(g,a,b) &
   integral(f-g,a,b) =integral(f,a,b) - integral(g,a,b)
proof
   set F = f||['a,b '], G = g||['a,b '];
   assume
A1:a<=b & [' a,b '] c= dom f & [' a,b '] c= dom g &
   f is_integrable_on [' a,b '] & g is_integrable_on [' a,b '] &
   f is_bounded_on [' a,b '] & g is_bounded_on [' a,b ']; then
   integral(f,a,b) = integral(f,[' a,b ']) &
   integral(g,a,b) = integral(g,[' a,b ']) &
   integral(f+g,a,b) = integral(f+g,[' a,b ']) &
   integral(f-g,a,b) = integral(f-g,[' a,b ']) by INTEGRA5:def 5;
   hence thesis by A1,Th11;
end;

theorem
 f is_bounded_on A & g is_bounded_on A implies f(#)g is_bounded_on A
proof
   assume f is_bounded_on A & g is_bounded_on A; then
   f(#)g is_bounded_on A /\ A by RFUNCT_1:101;
   hence f(#)g is_bounded_on A;
end;

theorem
 A c= dom f & A c= dom g & f is_integrable_on A & f is_bounded_on A &
 g is_integrable_on A & g is_bounded_on A implies
   f(#)g is_integrable_on A
proof
   assume
A1: A c= dom f & A c= dom g & f is_integrable_on A & f is_bounded_on A &
    g is_integrable_on A & g is_bounded_on A; then
A2:f||A is_integrable_on A & f||A is_bounded_on A &
   g||A is_integrable_on A & g||A is_bounded_on A by INTEGRA5:9,def 2;
A3:f||A is Function of A,REAL & g||A is Function of A,REAL by A1,Lm1;
   (f||A)(#)(g||A) = (f(#)g)||A by INTEGRA5:4; then
   (f(#)g)||A is_integrable_on A by A2,A3,INTEGRA4:29;
   hence f(#)g is_integrable_on A by INTEGRA5:def 2;
end;

theorem Th15:
for n be Element of NAT st n > 0 & vol A > 0 holds
 ex D being Element of divs A st
  len D = n &
  for i be Element of NAT st i in dom D holds D.i=inf A + vol(A)/n*i
proof
   let n be Element of NAT;
   assume that
A1: n > 0 & vol A > 0;
   deffunc F(Element of NAT)=inf A + vol(A)/n*$1;
   consider D being FinSequence of REAL such that
A2: len D = n &
    for i be Element of NAT st i in Seg n holds D.i=F(i) from FINSEQ_2:sch 1;
   for i,j be Element of NAT st i in dom D & j in dom D & i<j holds D.i < D.j
   proof
    let i,j be Element of NAT; assume
A3:  i in dom D & j in dom D & i < j; then
A4: i in Seg n & j in Seg n by A2,FINSEQ_1:def 3;
    vol(A)/n > 0 by A1,REAL_2:127; then
    vol(A)/n*i < vol(A)/n*j by A3,XREAL_1:70; then
    inf A + vol(A)/n*i < inf A + vol(A)/n*j by XREAL_1:8; then
    D.i < inf A + vol(A)/n*j by A2,A4;
    hence thesis by A2,A4;
   end; then
   reconsider D as non empty increasing FinSequence of REAL
     by A1,A2,FINSEQ_1:25,GOBOARD1:def 1;
   for x1 being set st x1 in rng D holds x1 in A
   proof
    let x1 be set;
    assume x1 in rng D; then
    consider i be Element of NAT such that
A5:  i in dom D & D.i=x1 by PARTFUN1:26;
    i in Seg n by A2,A5,FINSEQ_1:def 3; then
A6: x1 = inf A + vol(A)/n*i by A2,A5;
A7: 1 <= i & i <= len D & vol(A)/n>0 by A1,A5,REAL_2:127,FINSEQ_3:27; then
A8: vol(A)/n*i>0 by REAL_2:122;
    vol(A)/n*i <= vol(A)/n*n by A2,A7,XREAL_1:66; then
    vol(A)/n*i <= vol A by A1,XCMPLX_1:88; then
    inf A <= inf A + vol(A)/n*i & inf A + vol(A)/n*i <= inf A+ vol A
      by A8,XREAL_1:8,31;
    hence thesis by A6,INTEGRA2:1;
   end; then
A9:rng D c= A by TARSKI:def 3;
   len D in Seg n by A1,A2,FINSEQ_1:5; then
   D.(len D)=inf A + vol(A)/n*n by A2; then
   D.(len D)=inf A + vol(A) by A1,XCMPLX_1:88; then
   D is DivisionPoint of A by A9,INTEGRA1:def 2; then
   reconsider D as Element of divs A by INTEGRA1:def 3;
   take D;
   now let i be Element of NAT; assume i in dom D; then
    i in Seg n by A2,FINSEQ_1:def 3;
    hence D.i=inf A+vol(A)/n*i by A2;
   end;
   hence thesis by A2;
end;

begin :: Integrability on a Subinterval

theorem Th16:
vol A > 0 implies
  ex T being DivSequence of A st
   delta T is convergent & lim delta T = 0 &
   for n be Element of NAT holds
     ex Tn being Element of divs A st Tn divide_into_equal n+1 & T.n =Tn
proof
   assume
A1: vol A > 0;
   defpred P[set,set] means
    ex n being Element of NAT, e being Element of divs A st
     n = $1 & e = $2 & e divide_into_equal n+1;
A2:for n be Element of NAT ex D being Element of divs A st P[n,D]
   proof
    let n be Element of NAT;
    consider D being Element of divs A such that
A3:  len D = n+1 &
     for i be Element of NAT st i in dom D holds
      D.i=inf A+vol(A)/(n+1)*i by A1,Th15;
    D divide_into_equal n+1 by A3,INTEGRA4:def 1;
    hence thesis;
   end;
   consider T being Function of NAT,divs A such that
A4: for n be Element of NAT holds P[n,T.n] from FUNCT_2:sch 3(A2);
   reconsider T as DivSequence of A;
A5:for n be Element of NAT holds
     ex Tn being Element of divs A st Tn divide_into_equal n+1 & T.n =Tn
   proof
    let n be Element of NAT;
    ex n1 be Element of NAT, Tn being Element of divs A st
     n1 = n & Tn = T.n & Tn divide_into_equal n1+1 by A4;
    hence thesis;
   end;
A6:for i be Element of NAT holds (delta T).i = vol(A)/(i+1)
   proof
    let i be Element of NAT;
A7: (delta T).i = delta(T.i) by INTEGRA2:def 3;
    rng upper_volume(chi(A,A),(T.i))={vol(A)/(i+1)}
    proof
     for x1 being set st x1 in rng upper_volume(chi(A,A),(T.i)) holds
       x1 in {vol(A)/(i+1)}
     proof
      let x1 be set;
      assume x1 in rng upper_volume(chi(A,A),(T.i)); then
      consider k be Element of NAT such that
A8:    k in dom upper_volume(chi(A,A),(T.i))&
       (upper_volume(chi(A,A),(T.i))).k=x1 by PARTFUN1:26;
      reconsider D = T.i as Element of divs A;
A9:   ex n being Element of NAT, e being Element of divs A st
       n = i & e = D & e divide_into_equal n+1 by A4;
      k in Seg len upper_volume(chi(A,A),(T.i)) by A8,FINSEQ_1:def 3; then
A10:  k in Seg len D by INTEGRA1:def 7; then
A11:  x1=vol(divset(D,k)) by A8,INTEGRA1:22;
A12:  k in dom D by A10,FINSEQ_1:def 3;
      sup divset(D,k)-inf divset(D,k)=vol(A)/(i+1)
      proof
A13:   now assume A14:k=1; then
A15:    inf divset(D,k)=inf A & sup divset(D,k)=D.1 by A12,INTEGRA1:def 5;
        len D = i+1 &
        for j be Element of NAT st j in dom D holds
         D.j=inf A + vol(A)/(len D)*j by A9,INTEGRA4:def 1; then
        sup divset(D,k)=inf A+vol(A)/(i+1)*1 by A14,A15,A12; then
        sup divset(D,k)-inf divset(D,k)=inf A+vol(A)/(i+1)-inf A
          by A14,A12,INTEGRA1:def 5;
        hence sup divset(D,k)-inf divset(D,k)=vol(A)/(i+1);
       end;
       now assume A16:k <> 1; then
A17:    inf divset(D,k)=D.(k-1) & sup divset(D,k)=D.k by A12,INTEGRA1:def 5;
        k-1 in dom D & k-1 in NAT by A16,A12,INTEGRA1:9; then
        D.(k-1)=inf A + vol(A)/(len D)*(k-1) &
        D.k = inf A + vol(A)/(len D)* k by A9,A12,INTEGRA4:def 1;
        hence sup divset(D,k)-inf divset(D,k)=vol(A)/(i+1)
          by A17,A9,INTEGRA4:def 1;
       end;
       hence sup divset(D,k)-inf divset(D,k)=vol(A)/(i+1) by A13;
      end;
      hence thesis by A11,TARSKI:def 1;
     end; then
A18: rng upper_volume(chi(A,A),(T.i)) c= {vol(A)/(i+1)} by TARSKI:def 3;
     for x1 being set st x1 in {vol(A)/(i+1)} holds
       x1 in rng upper_volume(chi(A,A),(T.i))
     proof
      let x1 be set;
      assume x1 in {vol(A)/(i+1)}; then
A19:  x1 = vol(A)/(i+1) by TARSKI:def 1;
      reconsider D = T.i as Element of divs A;
      rng D <> {}; then
A20:  1 in dom D by FINSEQ_3:34; then
A21:  1 in Seg len D by FINSEQ_1:def 3; then
      1 in Seg len upper_volume(chi(A,A),D) by INTEGRA1:def 7; then
      1 in dom upper_volume(chi(A,A),D) by FINSEQ_1:def 3; then
A22:  (upper_volume(chi(A,A),D)).1 in rng upper_volume(chi(A,A),D)
         by FUNCT_1:def 5;
A23:  (upper_volume(chi(A,A),D)).1 = vol(divset(D,1)) by A21,INTEGRA1:22;
A24:  sup divset(D,1)=D.1 & inf divset(D,1)=inf A by A20,INTEGRA1:def 5;
A25:  ex n being Element of NAT, e being Element of divs A st
       n = i & e = D & e divide_into_equal n+1 by A4; then
      sup divset(D,1)=inf A+vol(A)/(len D)*1 by A20,A24,INTEGRA4:def 1; then
      vol(divset(D,1))=inf A+vol(A)/(len D)-inf A by A20,INTEGRA1:def 5;
      hence thesis by A19,A22,A23,A25,INTEGRA4:def 1;
     end; then
     {vol(A)/(i+1)} c= rng upper_volume(chi(A,A),(T.i)) by TARSKI:def 3;
     hence thesis by A18,XBOOLE_0:def 10;
    end; then
    (delta T).i in {vol(A)/(i+1)} by A7,PRE_CIRC:def 1;
    hence thesis by TARSKI:def 1;
   end;
A26:for i,j be Element of NAT st i <= j holds (delta T).i >= (delta T).j
   proof
    let i,j be Element of NAT;
    assume i <= j; then
A27:i+1 <= j+1 by XREAL_1:8;
    vol A >= 0 by INTEGRA1:11; then
    vol A/(i+1) >= vol(A)/(j+1) by A27,REAL_2:201; then
    (delta T).i >= vol(A)/(j+1) by A6;
    hence thesis by A6;
   end;
A28:for a be Real st 0 < a ex i be Element of NAT st abs((delta T).i-0)<a
   proof
    let a be Real;
    assume
A29: 0<a;
A30:vol A >= 0 by INTEGRA1:11; then
A31:vol(A)/a >= 0 by A29,REAL_2:125;
A32:vol(A)/a < [\ vol(A)/a /] +1 by INT_1:52;
    reconsider i1=[\vol(A)/a/]+1 as Integer;
    [\vol(A)/a/]+1>0 by A31,INT_1:52; then
    reconsider i1 as Element of NAT by INT_1:16;
    i1 < i1+1 by NAT_1:13; then
    vol(A)/a < 1*(i1+1) by A32,XXREAL_0:2; then
A33:vol(A)/(i1+1) < 1*a by A29,REAL_2:193;
    vol(A)/(i1+1) >= 0 by A30,REAL_2:125; then
A34:vol(A)/(i1+1)-0 = abs(vol(A)/(i1+1)-0) by ABSVALUE:def 1;
    (delta T).i1 = vol(A)/(i1+1) by A6;
    hence thesis by A33,A34;
   end;
A35:for a be real number st 0<a
    ex i be Element of NAT st
     for j be Element of NAT st i <= j holds abs((delta T).j-0)<a
   proof
    let a be real number;
A36:a is Real by XREAL_0:def 1;
    assume 0<a; then
    consider i be Element of NAT such that
A37: abs((delta T).i-0)<a by A28,A36;
    (delta T).i = delta(T.i) by INTEGRA2:def 3; then
    (delta T).i >= 0 by INTEGRA3:8; then
A38:(delta T).i < a by A37,ABSVALUE:def 1;
    now let j be Element of NAT;
     assume i <= j; then
A39: (delta T).j <= (delta T).i by A26;
     (delta T).j = delta(T.j) by INTEGRA2:def 3; then
     (delta T).j < a & (delta T).j >= 0 by A38,A39,XXREAL_0:2,INTEGRA3:8;
     hence abs((delta T).j-0)<a by ABSVALUE:def 1;
    end;
    hence thesis;
   end; then
A40:delta T is convergent by SEQ_2:def 6; then
   lim delta(T)=0 by A35,SEQ_2:def 7;
   hence thesis by A40,A5;
end;

Lm2:
 a <= b & [' a,b '] c= dom f &
 f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
 c in [' a,b '] & b <> c
implies
  integral(f,a,b) = integral(f,a,c) + integral(f,c,b) &
  f is_integrable_on ['a,c '] & f is_integrable_on ['c,b ']
proof
   assume that
A1: a <= b & [' a,b '] c= dom f & f is_integrable_on [' a,b '] &
    f is_bounded_on [' a,b '] & c in [' a,b '] & b <> c;
A2:[' a,b '] = [.a,b.] by A1,INTEGRA5:def 4; then
A3:a <= c & c <= b by A1,RCOMP_1:48; then
A4:[' a,c '] = [.a,c.] & [' c,b '] = [.c,b.] by INTEGRA5:def 4; then
A5:[' a,c '] c= [' a,b '] & [' c,b '] c= [' a,b '] by A2,A3,RCOMP_1:49;
   [.a,b.] = [.inf([.a,b.]), sup([.a,b.]).] &
   [.c,b.] = [.inf([.c,b.]), sup([.c,b.]).] &
   [.a,c.] = [.inf([.a,c.]), sup([.a,c.]).] by A2,A4,INTEGRA1:5; then
A6:inf [' a,b '] = a & sup [' a,b '] = b &
   inf [' c,b '] = c & sup [' c,b '] = b &
   inf [' a,c '] = a & sup [' a,c '] = c by A2,A4,INTEGRA1:6;
   set FAB = f||[' a,b '], FAC = f||[' a,c '], FCB = f||[' c,b '];
   [' a,c '] c= dom f & [' c,b '] c= dom f by A1,XBOOLE_1:1,A5; then
A7:FAB is Function of [' a,b '],REAL &
   FAC is Function of [' a,c '],REAL &
   FCB is Function of [' c,b '],REAL by A1,Lm1;
   FAB is_integrable_on [' a,b '] by A1,INTEGRA5:def 2; then
A8:FAB is_upper_integrable_on [' a,b '] &
   FAB is_lower_integrable_on [' a,b '] &
   upper_integral FAB = lower_integral FAB by INTEGRA1:def 17;
   f is_bounded_on [' a,c '] &
   f is_bounded_on [' c,b '] by A1,A5,RFUNCT_1:91; then
A9:FAC is_bounded_on [' a,c '] & FCB is_bounded_on [' c,b '] by INTEGRA5:9;
   consider T1 be DivSequence of [' a,c '] such that
A10:delta T1 is convergent & lim delta T1 = 0 by INTEGRA4:11;
   ex PS2 be DivSequence of [' c,b '] st
    delta PS2 is convergent & lim delta PS2 = 0 &
    ex K be Element of NAT st
     for i be Element of NAT st K <= i
      ex S2i be DivisionPoint of [' c,b '] st S2i=PS2.i & 2 <= len S2i
   proof
    c < b by A3,A1,REAL_1:def 5; then
    vol([' c,b '] ) > 0 by A6,XREAL_1:52; then
    consider T being DivSequence of [' c,b '] such that
A11: delta T is convergent & lim delta(T)=0 &
     for n be Element of NAT holds
      ex Tn being Element of divs [' c,b '] st
        Tn divide_into_equal n+1 & T.n =Tn by Th16;
    take T;
    now let i be Element of NAT;
     assume A12:2 <= i;
     consider Tn being Element of divs [' c,b '] such that
A13:  Tn divide_into_equal i+1 & T.i =Tn by A11;
     reconsider Tn as DivisionPoint of [' c,b '];
     take Tn;
     thus Tn=T.i by A13;
     len Tn = i+1 by A13,INTEGRA4:def 1; then
     i <= len Tn by NAT_1:11;
     hence 2 <= len Tn by A12,XXREAL_0:2;
    end;
    hence thesis by A11;
   end; then
   consider PS2 be DivSequence of [' c,b '] such that
A14:delta PS2 is convergent & lim delta PS2 = 0 &
     ex K be Element of NAT st
      for i be Element of NAT st  K <= i
       ex S2i be DivisionPoint of [' c,b '] st S2i = PS2.i & 2 <= len S2i;
   consider K be Element of NAT such that
A15:for i be Element of NAT st K <= i
     ex S2i be DivisionPoint of [' c,b '] st S2i=PS2.i & 2 <= len S2i by A14;
   defpred Q[set,set] means
    ex n being Element of NAT, e being Element of divs [' c,b '] st
     n = $1 & e = $2 & e = PS2.(n+K);
A16:for n be Element of NAT ex D being Element of divs [' c,b '] st Q[n,D]
   proof
    let n be Element of NAT;
    reconsider D =PS2.(n+K) as Element of divs [' c,b '];
    take D;
    thus thesis;
   end;
   consider S2 being Function of NAT,divs [' c,b '] such that
A17:for n be Element of NAT holds Q[n,S2.n] from FUNCT_2:sch 3(A16);
   reconsider S2 as DivSequence of [' c,b '];
A18:(for i be Element of NAT holds
     ex S2i be DivisionPoint of [' c,b '] st S2i=S2.i & 2 <= len S2i) &
   delta S2 is convergent & lim delta(S2)=0
   proof
A19:now let i be Element of NAT;
A20: K <= i+K by NAT_1:11;
     ex n being Element of NAT,e being Element of divs ['c,b '] st
     n = i & e =S2.i & e = PS2.(n+K) by A17;
     hence ex S2i be DivisionPoint of [' c,b '] st S2i=S2.i & 2 <= len S2i
       by A20,A15;
    end;
    delta S2 is convergent & lim delta(S2)=0
    proof
A21: now let e be real number;
      assume 0 < e; then
      consider m be Element of NAT such that
A22:   for n be Element of NAT st m <= n holds abs(delta(PS2).n -0 ) < e
          by A14,SEQ_2:def 7;
      take m;
      hereby let n be Element of NAT;
       assume A23:m <=n;
       n <=n+K by NAT_1:11; then
       m <=n+K by A23,XXREAL_0:2; then
       abs(delta(PS2).(n+K) -0 ) < e by A22; then
A24:   abs(delta(PS2.(n+K)) -0 ) < e by INTEGRA2:def 3;
       ex k being Element of NAT, e1 being Element of divs [' c,b '] st
        k = n & e1 =S2.n & e1 = PS2.(k+K) by A17;
       hence abs(delta(S2).n -0) < e by A24,INTEGRA2:def 3;
      end;
     end;
     hence delta S2 is convergent by SEQ_2:def 6;
     hence lim delta S2 =0 by SEQ_2:def 7,A21;
    end;
    hence thesis by A19;
   end;
   defpred P1[Element of NAT, set] means
    ex S be DivisionPoint of [' c,b '] st S = S2.$1 & $2 = S/^1;
A25:for i be Element of NAT ex T be Element of divs [' c,b '] st P1[i,T]
   proof
    let i be Element of NAT;
    consider S be DivisionPoint of [' c,b '] such that
A26: S=S2.i & 2 <= len S by A18;
    take T=S/^1;
A27:S is non empty increasing FinSequence of REAL &
    rng S c= [' c,b '] & S.(len S) = sup [' c,b '] by INTEGRA1:def 2;
A28:1 <= len S by A26,XXREAL_0:2;
    2-1 <= len S -1 by A26,REAL_1:92; then
A29:1 <= len T by A28,RFINSEQ:def 2; then
A30:T is non empty increasing FinSequence of REAL
      by A28,FINSEQ_1:25,INTEGRA1:36;
    rng T c= rng S by FINSEQ_5:36; then
A31:rng T c= [' c,b '] by A27,XBOOLE_1:1;
    len T in Seg (len T) by A29; then
    len T in dom T by FINSEQ_1:def 3; then
    T.(len T) =S.(len T+ 1) by A28,RFINSEQ:def 2; then
    T.(len T) =S.(len S -1+ 1) by A28,RFINSEQ:def 2; then
    T.(len T) = sup [' c,b '] by INTEGRA1:def 2; then
    T is DivisionPoint of [' c,b '] by A30,A31,INTEGRA1:def 2;
    hence thesis by A26,INTEGRA1:def 4;
   end;
   consider T2 be DivSequence of [' c,b '] such that
A32:for i be Element of NAT holds P1[i,T2.i] from FUNCT_2:sch 3(A25);
A33:for n be Element of NAT holds
     ex D be DivisionPoint of [' c,b '] st D = T2.n & 1 <= len D
   proof
    let n be Element of NAT;
    consider E be DivisionPoint of [' c,b '] such that
A34: E=S2.n & T2.n=E/^1 by A32;
    reconsider D=T2.n as DivisionPoint of [' c,b '] by INTEGRA1:def 3;
    take D;
    ex E1 be DivisionPoint of [' c,b '] st E1=S2.n & 2 <= len E1 by A18; then
    1 <= len E & 2-1 <= len E -1 by A34,XXREAL_0:2,REAL_1:92;
    hence thesis by A34,RFINSEQ:def 2;
   end;
A35:for n be Element of NAT holds
    ex D be DivisionPoint of [' c,b '] st
      D = T2.n & (for i be Element of NAT st i in dom D holds c < D.i)
   proof
    let n be Element of NAT;
    consider E be DivisionPoint of [' c,b '] such that
A36: E=S2.n & T2.n=E/^1 by A32;
    consider E1 be DivisionPoint of [' c,b '] such that
A37: E1=S2.n & 2 <= len E1 by A18;
    reconsider D=T2.n as DivisionPoint of [' c,b '] by INTEGRA1:def 3;
    take D;
A38:1 <= len E by A37,A36,XXREAL_0:2; then
A39:len D = len E -1 by A36,RFINSEQ:def 2;
    2-1 <= len E -1 by A37,A36,REAL_1:92; then
    1 in Seg len D by A39; then
A40:1 in dom D by FINSEQ_1:def 3; then
A41:D.1 = E.(1+1) by A36,A38,RFINSEQ:def 2;
A42:rng E c=[' c,b '] by INTEGRA1:def 2;
    1 in Seg len E & 2 in Seg len E by A37,A36,A38; then
A43:1 in dom E & 2 in dom E by FINSEQ_1:def 3; then
    E.1 in rng E by FUNCT_1:def 5; then
A44:c <= E.1 & E.1 <= b by A4,A42,RCOMP_1:48;
A45:E.1 < E.2 by A43,GOBOARD1:def 1; then
A46:c < D.1 by A41,A44,XXREAL_0:2;
    now let i be Element of NAT;
     assume A47: i in dom D; then
     i in Seg len D by FINSEQ_1:def 3; then
A48: 1 <=i by FINSEQ_1:3;
     hereby assume i <> 1; then
      1 < i by A48,REAL_1:def 5; then
      D.1 < D.i by A47,A40,GOBOARD1:def 1;
      hence c < D.i by A46,XXREAL_0:2;
     end;
     hence c < D.i by A45,A41,A44,XXREAL_0:2;
    end;
    hence thesis;
   end;
   set XCB = chi(['c,b '],['c,b ']);
A49:delta T2 is convergent & lim delta T2 =0
   proof
A50:now let e be real number;
     assume e > 0; then
A51: 0 < e/2 & e/2 < e by XREAL_1:217,218; then
     consider m be Element of NAT such that
A52:  for n be Element of NAT st m <=n holds
       abs( delta(S2).n - 0) < e/2 by A18,SEQ_2:def 7;
     take m;
     now let n be Element of NAT;
A53:  0 <= delta(S2.n) & 0 <= delta(T2.n) by INTEGRA3:8;
      assume m <=n; then
      abs( delta(S2).n  - 0) < e/2 by A52; then
      abs delta(S2.n) < e/2 by INTEGRA2:def 3; then
A54:  max rng upper_volume(XCB,S2.n) < e/2 by A53,ABSVALUE:def 1;
A55:  for y be real number st
       y in rng upper_volume(XCB,T2.n) holds y < e
      proof
       let y be real number;
       assume y in rng upper_volume(XCB,T2.n); then
       consider x be set such that
A56:    x in dom upper_volume(XCB,T2.n) & y=(upper_volume(XCB,T2.n)).x
          by FUNCT_1:def 5;
A57:   x in Seg len upper_volume(XCB,T2.n) by A56,FINSEQ_1:def 3;
       reconsider i=x as Element of NAT by A56;
       reconsider D=T2.n as DivisionPoint of [' c,b '] by INTEGRA1:def 3;
A58:   len (upper_volume(XCB,T2.n)) = len D by INTEGRA1:def 7; then
A59:   y=( sup rng(XCB|divset(T2.n,i)) ) * vol divset(T2.n,i)
         by A56,A57,INTEGRA1:def 7;
       consider E be DivisionPoint of [' c,b '] such that
A60:    E=S2.n & T2.n=E/^1 by A32;
       consider E1 be DivisionPoint of [' c,b '] such that
A61:    E1=S2.n & 2 <= len E1 by A18;
A62:   1 <= len E1 by A61,XXREAL_0:2; then
A63:   len D = len E -1 by A60,A61,RFINSEQ:def 2;
       set i1=i+1;
A64:    now assume A65:i = 1; then
A66:    1 in dom D by FINSEQ_1:def 3,A58,A57;
A67:    2 in Seg len E by A60,A61; then
A68:    2 in dom E by FINSEQ_1:def 3;
A69:    divset((T2.n),i)
         = [. inf(divset((T2.n),1)),sup(divset((T2.n),1)) .] by A65,INTEGRA1:5
        .= [. inf([' c,b ']),sup(divset((T2.n),1)) .] by INTEGRA1:def 5,A66
        .= [. inf([' c,b ']),D.1 .] by INTEGRA1:def 5,A66
        .= [. inf([' c,b ']),E.(1+1) .] by A61,A62,A60,A66,RFINSEQ:def 2
        .= [. inf([' c,b ']),sup(divset((S2.n),2)) .]
            by A60,A68,INTEGRA1:def 5; then
        [. inf([' c,b ']),sup(divset((S2.n),2)) .]
         = [.inf([. inf([' c,b ']),sup(divset((S2.n),2)) .]),
             sup([. inf([' c,b ']),sup(divset((S2.n),2)) .]).]
              by INTEGRA1:5; then
A70:    inf([' c,b '])=inf([. inf([' c,b ']),sup(divset((S2.n),2)) .]) &
        sup(divset((S2.n),2)) =sup([. inf([' c,b ']),sup(divset((S2.n),2)) .])
          by A69,INTEGRA1:6;
        len(upper_volume(XCB,S2.n)) = len E by A60,INTEGRA1:def 7; then
        2 in Seg len (upper_volume(XCB,S2.n)) by A60,A61; then
        2 in dom upper_volume(XCB,S2.n) by FINSEQ_1:def 3; then
        (upper_volume(XCB,S2.n)).2 in rng upper_volume(XCB,S2.n)
           by FUNCT_1:def 5; then
        (upper_volume(XCB,S2.n)).2 <= max rng upper_volume(XCB,S2.n)
           by PRE_CIRC:def 1; then
A71:    (upper_volume(XCB,S2.n)).2 < e/2 by A54,XXREAL_0:2;
        len(upper_volume(XCB,S2.n))= len D + 1 by A63,A60,INTEGRA1:def 7; then
        len(upper_volume(XCB,S2.n))
          = len (upper_volume(XCB,T2.n)) + 1 by INTEGRA1:def 7; then
        1 in Seg len(upper_volume(XCB,S2.n)) by FINSEQ_2:9,A57,A65; then
        1 in dom upper_volume(XCB,S2.n) by FINSEQ_1:def 3; then
        (upper_volume(XCB,S2.n)).1 in rng upper_volume(XCB,S2.n)
           by FUNCT_1:def 5; then
        (upper_volume(XCB,S2.n)).1 <= max rng upper_volume(XCB,S2.n)
           by PRE_CIRC:def 1; then
A72:    (upper_volume(XCB,S2.n)).1 < e/2 by A54,XXREAL_0:2;
A73:    1 in Seg len E by A60,A61,A62; then
A74:    1 in dom E by FINSEQ_1:def 3;
        divset(S2.n,1) = [. inf divset(S2.n,1),sup divset(S2.n,1) .]
            by INTEGRA1:5; then
  A75: divset(S2.n,1) = [. inf([' c,b ']),sup divset(S2.n,1) .]
            by INTEGRA1:def 5,A74,A60; then
    A76:[. inf([' c,b ']),sup divset(S2.n,1) .]
         = [.inf([. inf([' c,b ']),sup divset(S2.n,1) .]),
             sup([. inf([' c,b ']),sup divset(S2.n,1) .]).] by INTEGRA1:5;
        divset(S2.n,2)
         = [. inf divset(S2.n,2),sup divset(S2.n,2) .] by INTEGRA1:5; then
        divset(S2.n,2)
         = [. E.(2-1),sup divset(S2.n,2) .] by INTEGRA1:def 5,A68,A60; then
  A77: divset(S2.n,2)
         = [. sup divset(S2.n,1),sup divset(S2.n,2) .]
             by INTEGRA1:def 5,A74,A60; then
    A78:[. sup divset(S2.n,1) ,sup divset(S2.n,2) .]
         = [.inf([. sup divset(S2.n,1) ,sup divset(S2.n,2) .]),
             sup([. sup divset(S2.n,1) ,sup divset(S2.n,2) .]).] by INTEGRA1:5;
        y =vol divset(T2.n,1) by A65,A56,A58,A57,INTEGRA1:22; then
        y =(sup divset(S2.n,2)-sup divset(S2.n,1))
          + (sup([. inf([' c,b ']),sup divset(S2.n,1) .])-inf([' c,b ']))
            by A75,A70,A69,A65; then
        y =(sup divset(S2.n,2) - sup divset(S2.n,1)) + vol divset(S2.n,1)
            by A76,A75,INTEGRA1:6; then
        y = vol divset(S2.n,2) + vol divset(S2.n,1) by A78,A77,INTEGRA1:6;then
        y = (upper_volume(XCB,S2.n)).2 + vol divset(S2.n,1)
            by A60,A67,INTEGRA1:22; then
        y = (upper_volume(XCB,S2.n)).2 + (upper_volume(XCB,S2.n)).1
            by A60,A73,INTEGRA1:22; then
        y < (e/2)+(e/2) by A71,A72,XREAL_1:10;
        hence y < e;
       end;
       now assume A79:i <> 1;
        i in Seg len D by A58,A56,FINSEQ_1:def 3; then
        1 <= i by FINSEQ_1:3; then
A80:    1 < i by A79,REAL_1:def 5; then
A81:   i-1 in Seg len D by FINSEQ_3:13,A58,A57; then
A82:    i-1 in dom D by FINSEQ_1:def 3;
        reconsider i' = i-1 as Element of NAT by A81;
        1+1 < i+1 by A80,XREAL_1:10; then
A83:    i1 <> 1;
        Seg (len D + 1) = Seg len E by A63; then
        i1 in Seg len E by A58,A57,FINSEQ_1:81; then
A84:    i1 in dom E by FINSEQ_1:def 3;
A85:    i in dom D by FINSEQ_1:def 3,A58,A57;
A86:    divset(T2.n,i)
         = [. inf divset(T2.n,i),sup divset(T2.n,i) .] by INTEGRA1:5
        .= [. D.(i-1),sup divset(T2.n,i) .] by INTEGRA1:def 5,A79,A85
        .= [. D.(i-1),D.i .] by INTEGRA1:def 5,A79,A85
        .= [. E.(i'+1),D.i .] by A61,A62,A60,A82,RFINSEQ:def 2
        .= [. E.((i-1)+1),E.(i+1) .] by A61,A62,A60,A85,RFINSEQ:def 2
        .= [. E.(i1-1),sup divset(S2.n,i1) .] by A60,A83,A84,INTEGRA1:def 5
        .= [. inf divset(S2.n,i1),sup divset(S2.n,i1) .]
            by A60,A83,A84,INTEGRA1:def 5
        .= divset(S2.n,i1) by INTEGRA1:5;
        len upper_volume(XCB,S2.n) = len D +1 by A63,A60,INTEGRA1:def 7; then
        len upper_volume(XCB,S2.n) = len (upper_volume(XCB,T2.n)) + 1
          by INTEGRA1:def 7; then
A87:    i1 in Seg len (upper_volume(XCB,S2.n)) by A57,FINSEQ_1:81;
        len upper_volume(XCB,S2.n) = len E by A60,INTEGRA1:def 7; then
A88:    y = (upper_volume(XCB,S2.n)).i1 by A60,A59,A86,A87,INTEGRA1:def 7;
        i1 in dom upper_volume(XCB,S2.n) by FINSEQ_1:def 3,A87; then
        y in rng upper_volume(XCB,S2.n) by A88,FUNCT_1:def 5; then
        y <= max rng upper_volume(XCB,S2.n) by PRE_CIRC:def 1; then
        y < e/2 by A54,XXREAL_0:2;
        hence y < e by A51,XXREAL_0:2;
       end;
       hence y < e by A64;
      end;
      max rng upper_volume(XCB,T2.n) in rng upper_volume(XCB,T2.n)
          by PRE_CIRC:def 1; then
      delta(T2.n) < e by A55; then
      abs delta(T2.n) < e by A53,ABSVALUE:def 1;
      hence abs(delta(T2).n-0) < e by INTEGRA2:def 3;
     end;
     hence for n be Element of NAT st m <=n holds abs( delta(T2).n - 0) < e;
    end;
    hence delta T2 is convergent by SEQ_2:def 6;
    hence lim delta T2 =0 by A50,SEQ_2:def 7;
   end;

   defpred P2[Element of NAT,set] means
    ex S1 be DivisionPoint of [' a,c '], S2 be DivisionPoint of [' c,b '] st
     S1=T1.$1 & S2=T2.$1 & $2=S1^S2;

A89:
   for i be Element of NAT
    ex T be Element of divs [' a,b '] st P2[i,T]
   proof
    let i0 be Element of NAT;
    reconsider S1=T1.i0 as DivisionPoint of [' a,c '] by INTEGRA1:def 3;
    reconsider S2=T2.i0 as DivisionPoint of [' c,b '] by INTEGRA1:def 3;
    take T=S1^S2;
A90:S1 is non empty increasing FinSequence of REAL &
    rng S1 c= [' a,c '] & S1.(len S1) = sup [' a,c '] &
    S2 is non empty increasing FinSequence of REAL &
    rng S2 c= [' c,b '] & S2.(len S2) = sup [' c,b '] by INTEGRA1:def 2; then
    rng S1 \/ rng S2 c= [' a,c '] \/ [' c,b '] by XBOOLE_1:13; then
    rng S1 \/ rng S2 c=[.a,b.] by A3,A4,RCOMP_1:50; then
A91: rng T c= [' a,b '] by A2,FINSEQ_1:44;

    ex D be DivisionPoint of [' c,b '] st D = T2.i0 & 1 <= len D by A33; then
    len S2 in Seg len S2; then
A92: len S2 in dom S2 by FINSEQ_1:def 3;

A93: T.(len T) = T.(len S1 + len S2) by FINSEQ_1:35
             .= S2.(len S2) by A92,FINSEQ_1:def 7
             .= sup [' a,b '] by A6,INTEGRA1:def 2;

    now let i,j be Element of NAT;
     assume A94:i in dom T & j in dom T & i < j; then
     i in Seg len T & j in Seg len T by FINSEQ_1:def 3; then
A95: 1 <= i & i <= len T & 1 <= j & j <= len T by FINSEQ_1:3;
A96:  now assume A97:j <= len S1; then
      i <= len S1 by A94,XXREAL_0:2; then
      i in Seg len S1 & j in Seg len S1 by A97,A95; then
  A98: i in dom S1 & j in dom S1 by FINSEQ_1:def 3; then
      T.i = S1.i & T.j = S1.j by FINSEQ_1:def 7;
      hence T.i < T.j by A94,A98,GOBOARD1:def 1;
     end;

     now assume A99:j > len S1; then
 A100:  T.j = S2.(j-len S1) by A95,FINSEQ_1:37;
 A101:  len S1 + 1 <=j by NAT_1:13,A99; then
      consider m be Nat such that
 A102:   (len S1 + 1)+m=j by NAT_1:10;
      reconsider m as Element of NAT by ORDINAL1:def 13;
      1+m = j - len S1 by A102; then
  A103: 1 <= 1+m by A101,XREAL_1:21;
      j <= len S1 + len S2 by A95,FINSEQ_1:35; then
      j-len S1 <= len S2 by XREAL_1:22; then
      1+m in Seg len S2 by A102,A103; then
  A104: j - len S1 in dom S2 by A102,FINSEQ_1:def 3;
A105:    now assume i <= len S1; then
       i in Seg len S1 by A95; then
  A106: i in dom S1 by FINSEQ_1:def 3; then
       T.i = S1.i by FINSEQ_1:def 7; then
       T.i in rng S1 by FUNCT_1:def 5,A106; then
A107:  a <= T.i & T.i <= c by A90,A4,RCOMP_1:48;
       ex DD be DivisionPoint of [' c,b '] st
        DD = T2.i0 &
        for k be Element of NAT st k in dom DD holds c < DD.k by A35; then
       c < S2.(j-len S1) by A104;
       hence T.i < T.j by A107,A100,XXREAL_0:2;
      end;
      now assume A108:i > len S1; then
  A109:  T.i = S2.(i-len S1) by A95,FINSEQ_1:37;
   A110: i - len S1 < j -len S1 by A94,REAL_1:92;
  A111:  len S1 + 1 <=i by NAT_1:13,A108; then
       consider m be Nat such that
    A112: (len S1 + 1)+m=i by NAT_1:10;
       reconsider m as Element of NAT by ORDINAL1:def 13;
       1+m = i - len S1 by A112; then
   A113: 1 <= 1+m by A111,XREAL_1:21;
       i <= len S1 + len S2 by A95,FINSEQ_1:35; then
       i-len S1 <= len S2 by XREAL_1:22; then
       1+m in Seg len S2 by A112,A113; then
       i - len S1 in dom S2 by A112,FINSEQ_1:def 3;
       hence T.i < T.j by A109,A100,A110,A104,GOBOARD1:def 1;
      end;
      hence T.i < T.j by A105;
     end; ::CA2 end
     hence T.i < T.j by A96;
    end; then
    T is non empty increasing FinSequence of REAL by GOBOARD1:def 1; then
    T is DivisionPoint of [' a,b '] by A91,A93,INTEGRA1:def 2;
    hence thesis by INTEGRA1:def 4;
   end;

   consider T be DivSequence of [' a,b '] such that
A114:for i be Element of NAT holds P2[i,T.i] from FUNCT_2:sch 3(A89);

A115:
   for i,k be Element of NAT, S0 be DivisionPoint of [' a,c '] st
    S0=T1.i & k in Seg len S0 holds divset(T1.i,k) c= [' a,c ']
   proof
    let i,k be Element of NAT, S0 be DivisionPoint of [' a,c '];
    assume A116:S0=T1.i & k in Seg (len S0); then
    k in dom S0 by FINSEQ_1:def 3;
    hence thesis by INTEGRA1:10,A116;
   end;

A117:
   for i, k be Element of NAT, S0 be DivisionPoint of [' c,b '] st
    S0=T2.i & k in Seg len S0 holds divset(T2.i,k) c= [' c,b ']
   proof
    let i,k be Element of NAT, S0 be DivisionPoint of [' c,b '];
    assume A118:S0=T2.i & k in Seg len S0; then
    k in dom S0 by FINSEQ_1:def 3;
    hence thesis by INTEGRA1:10,A118;
   end;

A119:
   for i, k be Element of NAT, S0 be DivisionPoint of [' a,c '] st
    S0=T1.i & k in Seg len S0 holds divset(T.i,k) = divset(T1.i,k)
   proof
    let i,k be Element of NAT, S0 be DivisionPoint of [' a,c '];
    assume A120:S0=T1.i;
    assume A121:k in Seg len S0;
    consider S1 be DivisionPoint of [' a,c '],
             S2 be DivisionPoint of [' c,b '] such that
A122:  S1=T1.i & S2=T2.i & T.i=S1^S2 by A114;

    reconsider S=T.i as DivisionPoint of [' a,b '] by INTEGRA1:def 3;

A123:k in dom S1 by A120,A121,A122,FINSEQ_1:def 3;
    len S = len S1 + len S2 by FINSEQ_1:35,A122; then
    len S1 <= len S by NAT_1:11; then
    Seg len S1 c= Seg len S by FINSEQ_1:7; then
    k in Seg len S by A120,A121,A122; then
A124:k in dom S by FINSEQ_1:def 3;
A125: divset(T.i,k) =[.inf divset(T.i,k),sup divset(T.i,k).] &
    divset(T1.i,k) = [. inf divset(T1.i,k),sup divset(T1.i,k).] by INTEGRA1:5;
A126:  now assume A127: k=1;
     thus
     divset(T.i,k)
      =[. inf[' a,b '],sup divset(T.i,k) .] by A127,A124,A125,INTEGRA1:def 5
     .=[. inf[' a,b '],S.k .] by A127,A124,INTEGRA1:def 5
     .=[. inf[' a,b '],S1.k .] by A122,FINSEQ_1:def 7,A123
     .=[. inf divset(T1.i,k),S1.1 .] by A127,A123,A122,A6,INTEGRA1:def 5
     .= divset(T1.i,k) by A127,A123,A122,A125,INTEGRA1:def 5;
    end;
    now assume A128:k <> 1;
 A129: 1 <=k & k <= len S1 by A120,A121,A122,FINSEQ_1:3;
     k-1 <= k -0 by XREAL_1:12; then
 A130: k-1 <= len S1 by A129,XXREAL_0:2;
 A131: 1 < k by A129,A128,REAL_1:def 5; then
     1+1 <= k by NAT_1:25; then
 A132: 2-1 <= k-1 by XREAL_1:11;
     k-1 is Element of NAT by A131,NAT_1:20; then
     k-1 in Seg len S1 by A130,A132; then
A133: k-1 in dom S1 by FINSEQ_1:def 3;
     thus divset(T.i,k)
      =[. S.(k-1),sup divset(T.i,k) .] by A128,A124,A125,INTEGRA1:def 5
     .=[. S.(k-1),S.k .] by A128,A124,INTEGRA1:def 5
     .=[. S.(k-1),S1.k .] by A122,FINSEQ_1:def 7,A123
     .=[. S1.(k-1),S1.k .]  by A122,FINSEQ_1:def 7,A133
     .=[. inf divset(T1.i,k),S1.k .] by A128,A123,A122,INTEGRA1:def 5
     .= divset(T1.i,k) by A128,A123,A122,A125,INTEGRA1:def 5;
    end;
    hence divset(T.i,k) = divset(T1.i,k) by A126;
   end;

A134:
   for i, k be Element of NAT, S01 be DivisionPoint of [' a,c '],
    S02 be DivisionPoint of [' c,b '] st
     S01=T1.i & S02=T2.i & k in Seg len S02 holds
      divset(T.i,(len S01 + k)) = divset(T2.i,k)
   proof
    let i,k be Element of NAT,
        S01 be DivisionPoint of [' a,c '],
        S02 be DivisionPoint of [' c,b '];
    assume A135:S01=T1.i & S02=T2.i;
    assume A136:k in Seg len S02;
    consider S1 be DivisionPoint of [' a,c '],
             S2 be DivisionPoint of [' c,b '] such that
A137:  S1=T1.i & S2=T2.i & T.i = S1^S2 by A114;
    reconsider S=T.i as DivisionPoint of [' a,b '] by INTEGRA1:def 3;
A138:k in dom S2 by A135,A136,A137,FINSEQ_1:def 3; then
A139:len S1 + k in dom S by FINSEQ_1:41,A137;
A140:divset(T.i,(len S1 + k))
     =[.inf divset(T.i,(len S1 + k)), sup divset(T.i,(len S1 + k)).] &
    divset(T2.i,k)
     =[. inf divset(T2.i,k), sup divset(T2.i,k).] by INTEGRA1:5;

A141: now assume A142: k=1; then
 A143:len S1 + k <> 1 by FINSEQ_1:25;
     len S1 <> 0 by FINSEQ_1:25; then
     len S1 in Seg len S1 by FINSEQ_1:5; then
 A144:len S1 in dom S1 by FINSEQ_1:def 3;
     thus divset(T.i,(len S1 + k))
      =[. S.(len S1 + k-1),sup divset(T.i,len S1 + k) .]
       by A143,A139,A140,INTEGRA1:def 5
     .=[. S1.(len S1),sup divset(T.i,len S1 +k) .] by A142,A137,A144
,FINSEQ_1:def 7
     .=[. sup ([' a,c ']),  sup divset(T.i,len S1 + k) .] by INTEGRA1:def 2
     .=[. inf ([' c,b ']),  S.(len S1 + k) .] by A6,A143,A139,INTEGRA1:def 5
     .=[. inf [' c,b '],  S2.k .] by A137,FINSEQ_1:def 7,A138
     .=[. inf divset(T2.i,k), S2.1 .] by A142,A138,A137,INTEGRA1:def 5
     .= divset(T2.i,k) by A142,A138,A140,A137,INTEGRA1:def 5;
    end;
    now assume A145:k <> 1;
  A146:1 <=k & k <= len S2 by A135,A136,A137,FINSEQ_1:3; then
A147:1 < k by A145,REAL_1:def 5; then
A148: 1+0 < k + len S1 by XREAL_1:10;
     k-1 <= k -0 by XREAL_1:12; then
  A149:k-1 <= len S2 by A146,XXREAL_0:2;
     1+1 <= k by A147,NAT_1:25; then
  A150:2-1 <= k-1 by XREAL_1:11;
     k-1 is Element of NAT by A147,NAT_1:20; then
     k-1 in Seg len S2 by A149,A150; then
 A151:k-1 in dom S2 by FINSEQ_1:def 3;
     thus
     divset(T.i,(len S1 + k))
      =[. S.(len S1 + k-1),sup divset(T.i,len S1 + k) .]
         by A148,A139,A140,INTEGRA1:def 5
     .=[. S.(len S1 + k-1),S.(len S1 + k) .] by A148,A139,INTEGRA1:def 5
     .=[. S.(len S1 + (k-1)),S2.k .] by A137,FINSEQ_1:def 7,A138
     .=[. S2.(k-1),S2.k .]  by A137,FINSEQ_1:def 7,A151
     .=[. inf divset(T2.i,k), S2.k .] by A145,A138,A137,INTEGRA1:def 5
     .= divset(T2.i,k) by A145,A138,A140,A137,INTEGRA1:def 5;
    end;
    hence thesis by A135,A137,A141;
   end;

   set XAC = chi([' a,c '],[' a,c ']);
   set XAB = chi([' a,b '],[' a,b ']);
A152:delta T is convergent & lim delta(T)=0
   proof
 A153: now let e be real number;
     assume A154:0 < e; then
     consider n1 be Element of NAT such that
  A155: for m be Element of NAT st n1 <= m holds abs(delta(T1).m -0 ) < e
         by A10,SEQ_2:def 7;
     consider n2 be Element of NAT such that
  A156: for m be Element of NAT st n2 <= m holds abs(delta(T2).m -0 ) < e
         by A154,A49,SEQ_2:def 7;
     reconsider n = max(n1,n2) as Element of NAT by XXREAL_0:16;
     take n;
     now let m be Element of NAT;
      assume A157:n <=m;
      n1 <= n by XXREAL_0:25; then
      n1 <= m by A157,XXREAL_0:2; then
      abs(delta(T1).m -0 ) < e by A155; then
   A158:abs(delta(T1.m) ) < e by INTEGRA2:def 3;
      0 <= delta(T1.m) by INTEGRA3:8; then
 A159:  max rng upper_volume(XAC,T1.m) < e by A158,ABSVALUE:def 1;
      n2 <= n by XXREAL_0:25; then
      n2 <= m  by A157, XXREAL_0:2; then
      abs(delta(T2).m -0 ) < e by A156; then
   A160:abs(delta(T2.m) ) < e by INTEGRA2:def 3;
      0 <= delta(T2.m) by INTEGRA3:8; then
 A161:  max rng upper_volume(XCB,T2.m) < e by A160,ABSVALUE:def 1;
 A162:  for r be real number st
       r in rng upper_volume(XAB,T.m) holds r < e
      proof
       let y be real number;
       assume y in rng upper_volume(XAB,T.m); then
       consider x be set such that
  A163:  x in dom upper_volume(XAB,T.m) &
        y = (upper_volume(XAB,T.m)).x by FUNCT_1:def 5;

  A164: x in Seg len (upper_volume(XAB,T.m)) by A163,FINSEQ_1:def 3;
       reconsider i=x as Element of NAT by A163;
       reconsider Tm=T.m as DivisionPoint of [' a,b '] by INTEGRA1:def 3;
  A165: len(upper_volume(XAB,T.m)) =len Tm by INTEGRA1:def 7; then
  A166: y=(sup rng(XAB|divset(T.m,i))) * vol divset(T.m,i)
          by A163,A164,INTEGRA1:def 7;
       consider S1 be DivisionPoint of [' a,c '],
                S2 be DivisionPoint of [' c,b '] such that
  A167: S1=T1.m & S2=T2.m & T.m = S1^S2 by A114;
  A168:len Tm = len S1 + len S2 by A167,FINSEQ_1:35;
  A169: 1 <= i & i <= len Tm by A164,A165,FINSEQ_1:3;
       per cases;
       suppose i <= len S1; then
    A170: i in Seg len S1 by A169; then
    A171: divset(T1.m,i) = divset(T.m,i) by A119,A167;
    A172: divset(T1.m,i) c= [' a,c '] by A115,A170,A167; then
        divset(T1.m,i) c= [' a,b '] by A5,XBOOLE_1:1; then
        XAC|divset(T1.m,i) = XAB|divset(T.m,i) by A171,A172,Th4; then
    A173: y=(upper_volume(XAC,T1.m)).i by A171,A166,INTEGRA1:def 7,A170,A167;
        len (upper_volume(XAC,T1.m)) = len S1 by A167,INTEGRA1:def 7; then
        i in dom (upper_volume(XAC,T1.m)) by A170,FINSEQ_1:def 3; then
        y in rng upper_volume(XAC,T1.m) by FUNCT_1:def 5,A173; then
        y <= max rng upper_volume(XAC,T1.m) by PRE_CIRC:def 1;
        hence y < e by A159,XXREAL_0:2;
       end;
       suppose i > len S1; then
   A174:  len S1 + 1 <=i by NAT_1:13; then
        consider k be Nat such that
     A175: (len S1 + 1)+k=i by NAT_1:10;
        reconsider k as Element of NAT by ORDINAL1:def 13;
        1+k = i - len S1 by A175; then
    A176: 1 <= 1+k by A174,XREAL_1:21;
        set i1=1+k;
        i-len S1 <= len S2 by A169,A168,XREAL_1:22; then
    A177: 1+k in Seg len S2 by A175,A176; then
    A178: divset(T2.m,i1) =divset(T.m,len S1 + i1) by A134,A167;
    A179: divset(T2.m,i1) c= [' c,b '] by A117,A177,A167; then
        divset(T2.m,i1) c= [' a,b '] by A5,XBOOLE_1:1; then
        y=(sup rng(XCB|divset(T2.m,i1))) * vol divset(T2.m,i1)
           by A166,A178,A179,A175,Th4; then
    A180: y=(upper_volume(XCB,T2.m)).i1 by INTEGRA1:def 7,A177,A167;
        len (upper_volume(XCB,T2.m)) = len S2 by A167,INTEGRA1:def 7; then
        i1 in dom (upper_volume(XCB,T2.m)) by A177,FINSEQ_1:def 3; then
        y in rng upper_volume(XCB,T2.m) by FUNCT_1:def 5,A180; then
        y <= max rng upper_volume(XCB,T2.m) by PRE_CIRC:def 1;
        hence y < e by A161,XXREAL_0:2;
       end;
      end;
      max rng upper_volume(XAB,T.m) in rng upper_volume(XAB,T.m)
        by PRE_CIRC:def 1; then
 A181: delta(T.m) < e by A162;
      0 <= delta(T.m) by INTEGRA3:8; then
      abs( delta(T.m) ) < e by A181,ABSVALUE:def 1;
      hence abs(delta(T).m -0 ) < e by INTEGRA2:def 3;
     end;
     hence for m  be Element of NAT st n <= m holds abs(delta(T).m -0 ) < e;
    end;
    hence delta T is convergent by SEQ_2:def 6;
    hence thesis by A153,SEQ_2:def 7;
   end;

A182:upper_integral FAC + upper_integral FCB = upper_integral FAB
   proof
A183:for i be Element of NAT holds
     upper_volume(f||[' a,b '],T.i)
      = (upper_volume(FAC,T1.i))^(upper_volume(FCB,T2.i))
    proof
     let i be Element of NAT;
     reconsider F = upper_volume(FAB,T.i) as FinSequence of REAL;
     reconsider F1 = upper_volume(FAC,T1.i) as FinSequence of REAL;
     reconsider F2 = upper_volume(FCB,T2.i) as FinSequence of REAL;
     consider S1 be DivisionPoint of [' a,c '],
              S2 be DivisionPoint of [' c,b '] such that
A184:   S1=T1.i & S2=T2.i & T.i =S1^S2 by A114;
     reconsider S=T.i as DivisionPoint of [' a,b '] by INTEGRA1:def 3;
A185:  len F = len S by INTEGRA1:def 7
          .= len S1+ len S2 by A184,FINSEQ_1:35
          .= len F1 + len S2 by A184,INTEGRA1:def 7
          .= len F1 + len F2 by A184,INTEGRA1:def 7; then
A186:dom F = Seg (len F1 + len F2) by FINSEQ_1:def 3;
A187:now let k be Element of NAT;
      len F1 <= len F by A185,NAT_1:11; then
  A188: Seg len F1 c= Seg len F by FINSEQ_1:7;
      assume k in dom F1; then
      k in Seg len F1 by FINSEQ_1:def 3; then
  A189: k in Seg len F & k in Seg(len S1) by A188,A184,INTEGRA1:def 7; then
      k in Seg(len S) by INTEGRA1:def 7; then
  A190: F.k = (sup rng(FAB|divset(T.i,k)))*vol divset(T.i,k) by INTEGRA1:def 7;
  A191: divset(T.i,k) = divset(T1.i,k) by A119,A189,A184;
A192:   divset(T.i,k) c= [' a,c '] by A184,A189,A191,A115; then
      divset(T.i,k) c= [' a,b '] by A5,XBOOLE_1:1; then
      F.k = (sup rng(FAC|divset(T1.i,k))) * vol divset(T1.i,k) by A190,A191,
A192,Th3;
      hence F.k = F1.k by A184,A189,INTEGRA1:def 7;
     end;
     now let k be Element of NAT;
      assume A193: k in dom F2; then
      k in Seg len F2 by FINSEQ_1:def 3; then
  A194: 1 <=k & k <= len F2 by FINSEQ_1:3; then
  A195: len F1 + k <= len F by A185,XREAL_1:8;
 A196:  1 + len F1 <= k + len F1 by A194,XREAL_1:8;
      1 <= 1+ len F1 by NAT_1:11; then
      1 <= k + len F1 by A196,XXREAL_0:2; then
 A197:  k+ len F1 in Seg len F by A195;
      k in Seg len F2 by A193,FINSEQ_1:def 3; then
  A198: k in Seg len S2 by A184,INTEGRA1:def 7;
 A199: len F1= len S1 by A184,INTEGRA1:def 7;
      len F1 + k in Seg len S by A197,INTEGRA1:def 7; then
  A200: F.(len F1 + k)
       =(sup rng(FAB|divset(T.i,(len F1 + k)))) * vol divset(T.i,len F1 + k)
        by INTEGRA1:def 7;
  A201: divset(T.i,len F1 + k) = divset(T2.i,k) by A134,A184,A198,A199;
  A202: divset(T.i,len F1 + k) c= [' c,b '] by A184,A198,A201,A117; then
      divset(T.i,len F1 + k) c= [' a,b '] by A5,XBOOLE_1:1; then
      F.(len F1 + k)=(sup rng(FCB|divset(T2.i,k))) * vol divset(T2.i,k)
        by A200,A201,A202,Th3;
      hence F.(len F1 + k) =F2.k by A184,A198,INTEGRA1:def 7;
     end;
     hence thesis by FINSEQ_1:def 7,A186,A187;
    end;

A203:for i be Element of NAT holds
     Sum upper_volume(FAB,T.i)
      = Sum upper_volume(FAC,T1.i) + Sum upper_volume(FCB,T2.i)
    proof
     let i be Element of NAT;
     upper_volume(FAB,T.i) = (upper_volume(FAC,T1.i))^(upper_volume(FCB,T2.i))
       by A183;
     hence thesis by RVSUM_1:105;
    end;
    now let i be Element of NAT;
     thus upper_sum(FAB,T).i
       =upper_sum(FAB,T.i) by INTEGRA2:def 4
      .=upper_sum(FAC,T1.i) +Sum(upper_volume(FCB,T2.i)) by A203
      .=upper_sum(FAC,T1).i + upper_sum(FCB,T2.i) by INTEGRA2:def 4
      .=upper_sum(FAC,T1).i +upper_sum(FCB,T2).i by INTEGRA2:def 4;
    end; then
 A204:upper_sum(FAB,T) = upper_sum(FAC,T1) +upper_sum(FCB,T2) by SEQ_1:11;
A205: upper_sum(FAC,T1) is convergent &
    lim upper_sum(FAC,T1) = upper_integral FAC &
    upper_sum(FCB,T2) is convergent & lim upper_sum(FCB,T2) =upper_integral FCB
       by A10,A49,A7,A9,INTEGRA4:9;
    FAB is_bounded_on [' a,b '] by A1,INTEGRA5:9; then
    upper_integral FAB = lim upper_sum(FAB,T) by A152,A7,INTEGRA4:9;
    hence upper_integral FAB = upper_integral FAC + upper_integral FCB
      by A204,A205,SEQ_2:20;
   end;
A206:integral(f,a,b) = integral(f,[' a,b ']) by A1,INTEGRA5:def 5
                   .= integral FAC + integral FCB by A182;
A207:integral(f,a,c) = integral(f,[' a,c ']) by A3,INTEGRA5:def 5;
   integral(f,c,b) = integral(f,[' c,b ']) by A3,INTEGRA5:def 5
                   .= integral FCB;
   hence integral(f,a,b) = integral(f,a,c) + integral(f,c,b) by A206,A207;
A208:lower_integral FAC + lower_integral FCB = lower_integral FAB
   proof
A209:for i be Element of NAT holds
     lower_volume(FAB,T.i) =(lower_volume(FAC,T1.i))^(lower_volume(FCB,T2.i))
    proof
     let i be Element of NAT;
     reconsider F = lower_volume(FAB,T.i) as FinSequence of REAL;
     reconsider F1 = lower_volume(FAC,T1.i) as FinSequence of REAL;
     reconsider F2 = lower_volume(FCB,T2.i) as FinSequence of REAL;
     consider S1 be DivisionPoint of [' a,c '],
              S2 be DivisionPoint of [' c,b '] such that
A210:   S1=T1.i & S2=T2.i & T.i = S1^S2 by A114;
     reconsider S=T.i as DivisionPoint of [' a,b '] by INTEGRA1:def 3;
 A211:len F = len S by INTEGRA1:def 8
          .= len S1+ len S2 by A210,FINSEQ_1:35
          .= len F1 + len S2 by A210,INTEGRA1:def 8; then
 A212: len F = len F1 + len F2 by A210,INTEGRA1:def 8; then
A213:dom F = Seg (len F1 + len F2) by FINSEQ_1:def 3;
A214:now let k be Element of NAT;
      assume A215: k in dom F1;
      len F1 <= len F by A211,NAT_1:11; then
  A216: Seg len F1 c= Seg len F by FINSEQ_1:7;
  A217: k in Seg len F1 by A215,FINSEQ_1:def 3; then
  A218: k in Seg len S1 by A210,INTEGRA1:def 8;
      len lower_volume(FAB,T.i) = len S by INTEGRA1:def 8; then
  A219: F.k=(inf rng(FAB|divset(T.i,k))) * vol divset(T.i,k)
        by A216,A217,INTEGRA1:def 8;
  A220: divset(T.i,k) = divset(T1.i,k) & divset(T1.i,k) c=[' a,c ']
        by A115,A119,A218,A210; then
      divset(T.i,k) c= [' a,b '] by A5,XBOOLE_1:1; then
      F.k=(inf rng(FAC|divset(T1.i,k))) * vol divset(T1.i,k) by Th3,A219,A220;
      hence F.k=F1.k by A210,A218,INTEGRA1:def 8;
     end;

     now let k be Element of NAT;
      assume k in dom F2; then
  A221: k in Seg len F2 by FINSEQ_1:def 3; then
  A222: 1 <=k & k <= len F2 by FINSEQ_1:3; then
  A223: len F1 + k <= len F by A212,XREAL_1:8;
  A224: 1 + len F1 <= k + len F1 by A222,XREAL_1:8;
      1 <= 1+ len F1 by NAT_1:11; then
      1 <= k + len F1 by A224,XXREAL_0:2; then
      k+ len F1 in Seg len F by A223; then
      len F1 + k in Seg len S by INTEGRA1:def 8; then
  A225: F.(len F1 + k)
       = (inf rng(FAB|divset(T.i,len F1 + k))) * vol divset(T.i,(len F1 + k))
           by INTEGRA1:def 8;
  A226: k in Seg len S2 & len F1= len S1 by A221,A210,INTEGRA1:def 8; then
  A227: divset(T.i,len F1 + k) = divset(T2.i,k) &
      divset(T2.i,k) c= [' c,b '] by A117,A134,A210; then
      divset(T.i,len F1 + k) c= [' a,b '] by A5,XBOOLE_1:1; then
      F.(len F1 + k)=(inf rng(FCB|divset(T2.i,k))) * vol divset(T2.i,k)
        by A225,A227,Th3;
      hence F.(len F1 + k)=F2.k by A210,A226,INTEGRA1:def 8;
     end;
     hence thesis by FINSEQ_1:def 7,A213,A214;
    end;

A228:for i be Element of NAT holds
     Sum lower_volume(FAB,T.i)
      = Sum lower_volume(FAC,T1.i) + Sum lower_volume(FCB,T2.i)
    proof
     let i be Element of NAT;
     lower_volume(FAB,T.i)
      = (lower_volume(FAC,T1.i))^(lower_volume(FCB,T2.i)) by A209;
     hence thesis by RVSUM_1:105;
    end;
    now let i be Element of NAT;
     thus
     lower_sum(FAB,T).i
      =lower_sum(FAB,T.i) by INTEGRA2:def 5
     .=lower_sum(FAC,T1.i) + Sum lower_volume(FCB,T2.i) by A228
     .=lower_sum(FAC,T1).i + lower_sum(FCB,T2.i) by INTEGRA2:def 5
     .=lower_sum(FAC,T1).i + lower_sum(FCB,T2).i by INTEGRA2:def 5;
    end; then
A229: lower_sum(FAB,T) = lower_sum(FAC,T1) + lower_sum(FCB,T2) by SEQ_1:11;

 A230:lower_sum(FAC,T1) is convergent &
    lim lower_sum(FAC,T1) =lower_integral FAC &
    lower_sum(FCB,T2) is convergent &
    lim lower_sum(FCB,T2) =lower_integral FCB by A49,A7,A9,A10,INTEGRA4:8;

    FAB is_bounded_on [' a,b '] by A1,INTEGRA5:9; then
    lower_integral FAB = lim lower_sum(FAB,T) by A152,A7,INTEGRA4:8;
    hence lower_integral FAB = lower_integral FAC + lower_integral FCB
      by A229,A230,SEQ_2:20;
   end;

   lower_integral FAC <= upper_integral FAC &
   lower_integral FCB <= upper_integral FCB by A7,A9,INTEGRA1:51; then
A231:lower_integral FAC = upper_integral FAC &
   lower_integral FCB = upper_integral FCB by Th1,A8,A182,A208;

   FAC is_upper_integrable_on [' a,c '] &
   FAC is_lower_integrable_on [' a,c '] &
   FCB is_upper_integrable_on [' c,b '] &
   FCB is_lower_integrable_on [' c,b '] by A7,A9,INTEGRA4:10; then
   FAC is_integrable_on [' a,c '] &
   FCB is_integrable_on [' c,b '] by A231,INTEGRA1:def 17;
   hence f is_integrable_on [' a,c '] & f is_integrable_on [' c,b ']
            by INTEGRA5:def 2;
end;

theorem Th17:
 a <= b & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
 [' a,b '] c= dom f & c in [' a,b ']
implies
  f is_integrable_on ['a,c '] & f is_integrable_on ['c,b '] &
  integral(f,a,b )=integral(f,a,c )+integral(f,c,b)
proof
   assume that
A1:a <= b and
A2:f is_integrable_on [' a,b '] and
A3:f is_bounded_on [' a,b '] and
A4:[' a,b '] c= dom f and
A5:c in [' a,b '];
   now assume A6:b=c; then
A7: [' c,b ']= [.c,b.] by INTEGRA5:def 4;
    thus f is_integrable_on ['a,c '] by A6,A2;
    [' c,b ']= [. inf([' c,b ']),sup([' c,b ']) .] by INTEGRA1:5; then
    inf([' c,b ']) = c & sup([' c,b ']) = b by A7,INTEGRA1:6; then
A8: vol(['c,b ']) =0 by A6; then
    f||['c,b '] is_integrable_on ['c,b '] & integral(f||['c,b '])=0
      by INTEGRA4:6;
    hence f is_integrable_on ['c,b '] by INTEGRA5:def 2;
    c is Real & b is Real by XREAL_0:def 1; then
    integral(f,c,b ) = integral(f,[' c,b '] ) by INTEGRA5:19,A7; then
    integral(f,c,b ) =0 by A8,INTEGRA4:6;
    hence integral(f, a,b )= integral(f,a,c ) +integral(f,c,b) by A6;
   end;
   hence f is_integrable_on ['a,c '] & f is_integrable_on ['c,b '] &
         integral(f,a,b)=integral(f,a,c)+integral(f,c,b)
         by A1,A2,A3,A4,A5,Lm2;
end;

Lm3:
for a,b,c be real number st a <= c & c <= b holds
 c in [' a,b '] & [' a,c '] c= [' a,b '] & [' c,b ']  c= [' a,b ']
proof
   let a,b,c be real number;
   assume A1:a <= c & c <= b; then
A2:a <= b by XXREAL_0:2; then
A3:[' a,b '] = [.a,b.] & [' a,c '] = [.a,c.] & [' c,b '] = [.c,b.]
     by A1,INTEGRA5:def 4;
A4:c in [.a,b.] by A1,RCOMP_1:48;
   hence c in [' a,b '] by A2,INTEGRA5:def 4;
   a in [.a,b.] & b in [.a,b.] by A2,RCOMP_1:48;
   hence thesis by A3,A4,RCOMP_1:16;
end;

theorem Th18:
 a <= c & c <= d & d <= b & f is_integrable_on [' a,b '] &
 f is_bounded_on [' a,b '] & [' a,b '] c= dom f
implies
  f is_integrable_on ['c,d '] & f is_bounded_on ['c,d '] & ['c,d '] c= dom f
proof
   assume that
A1:a <= c & c <= d & d <= b and
A2:f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] and
A3:[' a,b '] c= dom f;
   a <= d by A1,XXREAL_0:2; then
A4:a <= b & c <= b by A1,XXREAL_0:2; then
A5:c in [' a,b '] & d in [' c,b '] by Lm3,A1;
A6:['c,b '] c= [' a,b '] by Lm3,A1,A4; then
A7:['c,b '] c= dom f by XBOOLE_1:1,A3;
   f is_integrable_on ['c,b '] &
   f is_bounded_on [' c,b '] by RFUNCT_1:91,A2,A3,A4,A5,A6,Th17;
   hence f is_integrable_on ['c,d '] by A4,A5,A7,Th17;
   [' c,d '] c= [' c,b '] by A1,Lm3; then
A8:[' c,d '] c= [' a,b '] by A6,XBOOLE_1:1;
   hence f is_bounded_on [' c,d '] by RFUNCT_1:91,A2;
   thus [' c,d '] c= dom f by A3,A8,XBOOLE_1:1;
end;

theorem
 a <= c & c <= d & d <= b &
 f is_integrable_on [' a,b '] & g is_integrable_on [' a,b '] &
 f is_bounded_on [' a,b '] & g is_bounded_on [' a,b '] &
 [' a,b '] c= dom f & [' a,b '] c= dom g
implies
  f+g is_integrable_on ['c,d '] & f+g is_bounded_on ['c,d ']
proof
   assume
A1:a <= c & c <= d & d <= b &
   f is_integrable_on [' a,b '] & g is_integrable_on [' a,b '] &
   f is_bounded_on [' a,b '] & g is_bounded_on [' a,b '] &
   [' a,b '] c= dom f & [' a,b '] c= dom g; then

A2:f is_integrable_on ['c,d '] & f is_bounded_on ['c,d '] &
   g is_integrable_on ['c,d '] & g is_bounded_on ['c,d '] by Th18;

   c <=b by A1,XXREAL_0:2; then
   ['c,b '] c= [' a,b '] by A1,Lm3; then
A3:['c,b '] c= dom f & ['c,b '] c= dom g by A1,XBOOLE_1:1;
   [' c,d '] c= [' c,b '] by A1,Lm3; then
   [' c,d '] c= dom f & [' c,d '] c= dom g by A3,XBOOLE_1:1;
   hence f+g is_integrable_on ['c,d '] by A2,Th11;
   f+g is_bounded_on ['c,d '] /\ ['c,d '] by A2,RFUNCT_1:100;
   hence f+g is_bounded_on ['c,d '];
end;

Lm4:
for a,b,c,d be real number, f be PartFunc of REAL,REAL st
 a<=b & c <=d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
 [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b ']
holds
  integral(f,a,d)=integral(f,a,c)+integral(f,c,d)
proof
   let a,b,c,d be real number, f be PartFunc of REAL,REAL;
   assume
A1:a<=b & c <= d &
   f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
   [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b ']; then
   [' a,b '] = [.a,b.] by INTEGRA5:def 4; then
A2:a <= d & a <= c & d <= b by A1,RCOMP_1:48; then
A3:c in [' a,d '] & [' a,d '] c= [' a,b '] by A1,Lm3; then
A4:[' a,d '] c= dom f by A1,XBOOLE_1:1;
A5:f is_integrable_on [' a,d '] by A1,Th17;
   f is_bounded_on [' a,d '] by Th18,A1,A2;
   hence thesis by Th17,A2,A3,A4,A5;
end;

theorem Th20:
 a<=b & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
 [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b ']
implies
  integral(f,a,d)=integral(f,a,c)+integral(f,c,d)
proof
   assume
A1:a<=b & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
   [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '];
   now assume A2:not (c <= d); then
A3: integral(f,c,d) = -integral(f,[' d,c ']) by INTEGRA5:def 5;
    integral(f,a,c)=integral(f,a,d)+integral(f,d,c) by A1,A2,Lm4; then
    integral(f,a,d) =integral(f,a,c) - integral(f,d,c);
    hence thesis by A2,A3,INTEGRA5:def 5;
   end;
   hence thesis by Lm4,A1;
 end;

Lm5:
for a,b,c,d be real number, f be PartFunc of REAL,REAL st
 a <= b & c <=d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
 [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b ']
holds
  [' c,d '] c= dom abs f & abs f is_integrable_on [' c,d '] &
  abs f is_bounded_on [' c,d '] &
  abs integral(f,c,d) <= integral(abs f,c,d)
proof
   let a,b,c,d be real number,
       f be PartFunc of REAL,REAL such that
A1: a <= b & c <= d & f is_integrable_on [' a,b '] &
    f is_bounded_on [' a,b '] & [' a,b '] c= dom f &
    c in [' a,b '] & d in [' a,b '];
   [' a,b '] = [.a,b.] by A1,INTEGRA5:def 4; then
   a<=c & d <= b by A1,RCOMP_1:48; then
   [' c,d '] c= dom f & f is_integrable_on [' c,d '] &
   f is_bounded_on [' c,d '] by A1,Th18;
   hence thesis by Th7,Th8,A1,SEQ_1:def 10,RFUNCT_1:99;
end;

theorem Th21:
 a <= b & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
 [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b ']
implies
  [' min(c,d),max(c,d) '] c= dom (abs f) &
  abs f is_integrable_on [' min(c,d),max(c,d) '] &
  abs f is_bounded_on [' min(c,d),max(c,d) '] &
  abs integral(f,c,d) <= integral(abs f,min(c,d),max(c,d))
proof
   assume that
A1: a <= b & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
    [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '];
A2:now assume A3:c <= d; then
    c = min(c,d) & d = max(c,d) by XXREAL_0:def 8,def 9;
    hence thesis by A1,A3,Lm5;
   end;
   now assume A4:not(c <= d); then
A5: d = min(c,d) & c = max(c,d) by XXREAL_0:def 8,def 9;
    integral(f,c,d) = -integral(f,[' d,c ']) by A4,INTEGRA5:def 5; then
    integral(f,c,d) = -integral(f,d,c) by A4,INTEGRA5:def 5; then
    abs integral(f,c,d) = abs integral(f,d,c) by COMPLEX1:138;
    hence thesis by A1,A4,Lm5,A5;
   end;
   hence thesis by A2;
end;

Lm6:
 a <= b & c <= d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
 [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b ']
implies
  [' c,d '] c= dom (abs f) & abs f is_integrable_on [' c,d '] &
  abs f is_bounded_on [' c,d '] & abs integral(f,c,d) <= integral(abs f,c,d)
proof
   assume
A1: a<=b & c <= d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
    [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b ']; then
   min(c,d) = c & max(c,d) = d by XXREAL_0:def 8,def 9;
   hence thesis by A1,Th21;
end;

Lm7:
 a <= b & c <= d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
 [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b ']
implies abs integral(f,d,c) <= integral(abs f,c,d)
proof
   assume
A1: a<=b & c <= d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
    [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '];
A2:abs integral(f,c,d) <= integral(abs f,c,d) by A1,Lm6;
A3:integral(f,c,d) = integral(f,[' c,d ']) by A1,INTEGRA5:def 5;
   per cases;
   suppose c = d;
    hence abs integral(f,d,c) <= integral(abs f,c,d) by A1,Lm6;
   end;
   suppose c <> d; then
    c < d by A1,REAL_1:def 5; then
    integral(f,d,c) = -integral(f,[' c,d ']) by INTEGRA5:def 5;
    hence abs integral(f,d,c) <= integral(abs f,c,d) by A2,A3,COMPLEX1:138;
   end;
end;

theorem
 a <= b & c <= d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
 [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b ']
implies
  [' c,d '] c= dom (abs f) & abs f is_integrable_on [' c,d '] &
  abs f is_bounded_on [' c,d '] &
  abs integral(f,c,d) <= integral(abs f,c,d) &
  abs integral(f,d,c) <= integral(abs f,c,d) by Lm6,Lm7;

Lm8:
( a<=b & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
 [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] &
 for x be real number st x in [' min(c,d),max(c,d) '] holds abs(f.x) <= e )
implies
  abs integral(f,c,d) <= e * abs(d-c)
proof
   set A = [' min(c,d),max(c,d) '];
   assume
A1: a<=b & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
    [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] &
    for x be real number st x in [' min(c,d),max(c,d) '] holds abs(f.x) <= e;

A2:[' min(c,d),max(c,d) '] c= dom (abs f) &
   abs f is_integrable_on A & abs f is_bounded_on A &
   abs integral(f,c,d) <= integral(abs f,min(c,d),max(c,d)) by Th21,A1;
   min(c,d) <= c & c <= max(c,d) by XXREAL_0:17,25; then
   min(c,d) <= max(c,d) by XXREAL_0:2; then
A3:integral(abs f,min(c,d),max(c,d)) = integral(abs f,A) by INTEGRA5:def 5;
   reconsider e as Real by XREAL_0:def 1;
   rng abs f c= REAL; then
   abs f is Function of dom abs f,REAL by FUNCT_2:4; then
   reconsider g =abs(f)||A as Function of A,REAL by A2,FUNCT_2:38;
   consider h be Function of A,REAL such that
A4: rng h = {e} & h is_bounded_on A by INTEGRA4:5;
A5:now let x be Real;
    assume A6:x in A; then
    h.x in {e} by A4,FUNCT_2:6; then
A7: h.x =e by TARSKI:def 1;
    g.x = (abs f).x by A6,FUNCT_1:72; then
    g.x = abs(f.x) by A2,A6,SEQ_1:def 10;
    hence g.x <= h.x by A1,A6,A7;
   end;

A8:g is_integrable_on A & g is_bounded_on A by INTEGRA5:9,def 2,A2;
A9:h is_integrable_on A & integral h = e*vol A by A4,INTEGRA4:4;
   vol A = abs(d-c) by Th6; then
   integral(abs f,min(c,d),max(c,d)) <= e * abs(d-c)
     by A3,A5,A8,A4,A9,INTEGRA2:34;
   hence thesis by A2,XXREAL_0:2;
end;

Lm9:
( a<=b & c <= d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
 [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] &
 for x be real number st x in [' c,d '] holds abs(f.x) <= e )
implies
  abs integral(f,c,d) <= e * (d-c)
proof
   assume
A1: a<=b & c <= d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
    [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] &
    for x be real number st x in [' c,d '] holds abs(f.x) <= e; then
   0 <= d - c by XREAL_1:50; then
A2:abs(d-c) = d-c by ABSVALUE:def 1;
   min(c,d) = c & max(c,d) = d by A1,XXREAL_0:def 8,def 9;
   hence thesis by A1,A2,Lm8;
end;

Lm10:
( a<=b & c <= d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
 [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] &
 for x be real number st x in [' c,d '] holds abs(f.x) <= e )
implies
  abs integral(f,d,c) <= e * (d-c)
proof
   assume
A1: a<=b & c <= d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
    [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] &
    for x be real number st x in [' c,d '] holds abs(f.x) <= e; then
A2:abs integral(f,c,d) <= e * (d-c) by Lm9;
A3:integral(f,c,d) = integral(f,[' c,d ']) by A1,INTEGRA5:def 5;
   per cases;
   suppose c = d;
    hence abs integral(f,d,c) <= e * (d-c) by A1,Lm9;
   end;
   suppose c <> d; then
    c < d by A1,REAL_1:def 5; then
    integral(f,d,c) = -integral(f,[' c,d ']) by INTEGRA5:def 5;
    hence abs integral(f,d,c) <= e * (d-c) by A2,A3,COMPLEX1:138;
   end;
end;

theorem
( a<=b & c <= d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
 [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] &
 for x be real number st x in [' c,d '] holds abs(f.x) <= e )
implies
  abs integral(f,c,d) <= e * (d-c) & abs integral(f,d,c) <= e * (d-c)
 by Lm9,Lm10;

Lm11:
for a,b,c,d be real number, f,g be PartFunc of REAL,REAL st
 a <= b & c <= d &
 f is_integrable_on [' a,b '] & g is_integrable_on [' a,b '] &
 f is_bounded_on [' a,b '] & g is_bounded_on [' a,b '] &
 [' a,b '] c= dom f & [' a,b '] c= dom g & c in ['a,b '] & d in ['a,b ']
holds
  f+g is_integrable_on ['c,d '] & f+g is_bounded_on ['c,d '] &
  integral(f+g,c,d) =integral(f,c,d)+integral(g,c,d) &
  f-g is_integrable_on ['c,d '] & f-g is_bounded_on ['c,d '] &
  integral(f-g,c,d) =integral(f,c,d)-integral(g,c,d)
proof
   let a,b,c,d be real number,
       f,g be PartFunc of REAL,REAL;
   assume A1:a <= b & c <= d &
   f is_integrable_on [' a,b '] & g is_integrable_on [' a,b '] &
   f is_bounded_on [' a,b '] & g is_bounded_on [' a,b '] &
   [' a,b '] c= dom f & [' a,b '] c= dom g & c in ['a,b '] & d in ['a,b '];
   ['a,b '] = [.a,b.] by A1,INTEGRA5:def 4; then
   a<=c & d <= b by A1,RCOMP_1:48; then
A2:f is_integrable_on ['c,d '] & f is_bounded_on ['c,d '] &
   ['c,d '] c= dom f &
   g is_integrable_on ['c,d '] & g is_bounded_on ['c,d '] &
   ['c,d '] c= dom g by Th18,A1; then
   f+g is_bounded_on ['c,d '] /\ ['c,d '] &
   f-g is_bounded_on ['c,d '] /\ ['c,d '] by RFUNCT_1:100,101;
   hence thesis by A1,Th12,A2,Th11;
end;

theorem Th24:
 a <= b & f is_integrable_on [' a,b '] & g is_integrable_on [' a,b '] &
 f is_bounded_on [' a,b '] & g is_bounded_on [' a,b '] &
 [' a,b '] c= dom f & [' a,b '] c= dom g & c in ['a,b '] & d in ['a,b ']
implies
  integral(f+g,c,d) = integral(f,c,d) + integral(g,c,d) &
  integral(f-g,c,d) = integral(f,c,d) - integral(g,c,d)
proof
   assume A1:a <= b &
    f is_integrable_on [' a,b '] & g is_integrable_on [' a,b '] &
    f is_bounded_on [' a,b '] & g is_bounded_on [' a,b '] &
    [' a,b '] c= dom f & [' a,b '] c= dom g & c in ['a,b '] & d in ['a,b '];
   now assume A2: not (c <= d); then
A3: integral(f,c,d) = -integral(f,[' d,c ']) &
    integral(g,c,d) = -integral(g,[' d,c ']) &
    integral(f+g,c,d) = -integral(f+g,[' d,c ']) &
    integral(f-g,c,d) = -integral(f-g,[' d,c ']) by INTEGRA5:def 5;
    hence
     integral(f+g,c,d) = -integral(f+g,d,c) by A2,INTEGRA5:def 5
               .= -(integral(f,d,c)+integral(g,d,c)) by A2,A1,Lm11
               .= -integral(f,d,c)-integral(g,d,c)
               .= integral(f,c,d) -integral(g,d,c) by A3,A2,INTEGRA5:def 5
               .= integral(f,c,d) +integral(g,c,d) by A3,A2,INTEGRA5:def 5;
    thus integral(f-g,c,d) =-integral(f-g,d,c) by A3,A2,INTEGRA5:def 5
               .= -(integral(f,d,c)-integral(g,d,c)) by A2,A1,Lm11
               .= -(integral(f,d,c)+integral(g,c,d)) by A3,A2,INTEGRA5:def 5
               .=-integral(f,d,c)-integral(g,c,d)
               .= integral(f,c,d) -integral(g,c,d) by A3,A2,INTEGRA5:def 5;
   end;
   hence thesis by A1,Lm11;
end;

Lm12:
for a,b,c,d,e be real number, f be PartFunc of REAL,REAL st
 a <= b & c <= d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
 [' a,b '] c= dom f & c in ['a,b '] & d in ['a,b '] holds
   integral(e(#)f,c,d)=e*integral(f,c,d)
proof
   let a,b,c,d,e be real number,
       f be PartFunc of REAL,REAL;
   assume A1:a <= b & c <= d & f is_integrable_on [' a,b '] &
    f is_bounded_on [' a,b '] & [' a,b '] c= dom f &
    c in ['a,b '] & d in ['a,b ']; then
   ['a,b '] = [.a,b.] by INTEGRA5:def 4; then
   a <= c & d <= b by A1,RCOMP_1:48; then
   f is_integrable_on ['c,d '] & f is_bounded_on ['c,d '] & ['c,d '] c= dom f
      by Th18,A1;
   hence integral(e(#)f,c,d)=e*integral(f,c,d) by A1,Th10;
end;

theorem
 a <= b & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
 [' a,b '] c= dom f & c in ['a,b '] & d in ['a,b '] implies
   integral(e(#)f,c,d)=e*integral(f,c,d)
proof
   assume A1:a <= b & f is_integrable_on [' a,b '] &
    f is_bounded_on [' a,b '] & [' a,b '] c= dom f &
    c in ['a,b '] & d in ['a,b '];
   now assume A2:not(c <= d); then
A3: integral(f,c,d) = -integral(f,[' d,c ']) by INTEGRA5:def 5;
    thus integral(e(#)f,c,d)
     = -integral(e(#)f,[' d,c ']) by A2,INTEGRA5:def 5
    .= -integral(e(#)f,d,c) by A2,INTEGRA5:def 5
    .= -e*integral(f,d,c) by A2,A1,Lm12
    .= e*(-integral(f,d,c))
    .= e*integral(f,c,d) by A3,A2,INTEGRA5:def 5;
   end;
   hence thesis by A1,Lm12;
end;

theorem Th26:
( a <= b & [' a,b '] c= dom f &
 for x be real number st x in [' a,b '] holds f.x=e )
implies
  f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
  integral(f,a,b) = e*(b-a)
proof
   assume A1:a <= b & [' a,b '] c= dom f &
    for x be real number st x in [' a,b '] holds f.x = e;
   reconsider e1=e as Real by XREAL_0:def 1;
A2:integral(f,a,b) = integral(f,[' a,b ']) by A1,INTEGRA5:def 5;
   rng f c= REAL; then
   f is Function of dom f,REAL by FUNCT_2:4; then
   reconsider g =f||[' a,b '] as Function of [' a,b '],REAL by A1,FUNCT_2:38;
   consider h be Function of [' a,b '],REAL such that
A3: rng h = {e1} & h is_bounded_on [' a,b '] by INTEGRA4:5;

A4:for x be set st x in [' a,b '] holds g.x = h.x
   proof
    let x0 be set;
    assume A5:x0 in [' a,b ']; then
    reconsider x=x0 as real number by XREAL_0:def 1;
A6: h.x in {e1} by A3,FUNCT_2:6,A5;
    g.x0 = f.x by A5,FUNCT_1:72; then
    g.x0 = e by A1,A5;
    hence g.x0 = h.x0 by A6,TARSKI:def 1;
   end; then
A7:h=g by FUNCT_2:18;
   h is_integrable_on [' a,b '] by A3,INTEGRA4:4;
   hence f is_integrable_on [' a,b '] by A7,INTEGRA5:def 2;
   thus f is_bounded_on [' a,b ']
   proof
    consider r be real number such that
A8:  for c be set st c in [' a,b '] /\ dom g holds
      abs(g.c)<=r by A3,A7,RFUNCT_1:90;
    now let c being set;
     assume A9:c in [' a,b '] /\ dom f;
     [' a,b '] /\ dom g = [' a,b '] /\ (dom f /\ [' a,b ']) by FUNCT_1:68; then
A10: [' a,b '] /\ dom g = [' a,b '] /\ [' a,b '] /\ dom f by XBOOLE_1:16; then
A11: abs(g.c) <= r by A8,A9;
     c in dom g by A10,A9,XBOOLE_0:def 3;
     hence abs(f.c)<=r by A11,FUNCT_1:68;
    end;
    hence thesis by RFUNCT_1:90;
   end;
   integral h = e1*vol([' a,b ']) by A3,INTEGRA4:4; then
   integral h = e*(b-a) by Th5,A1;
   hence integral(f,a,b) =e*(b-a) by A2,A4,FUNCT_2:18;
end;

theorem Th27:
 a <= b & (for x be real number st x in [' a,b '] holds f.x=e) &
 [' a,b '] c= dom f & c in ['a,b '] & d in ['a,b '] implies
   integral(f,c,d) = e*(d-c)
proof
   assume A1:a<=b & (for x be real number st x in [' a,b '] holds f.x=e) &
   [' a,b '] c= dom f & c in ['a,b '] & d in ['a,b ']; then
   ['a,b '] = [.a,b.] by INTEGRA5:def 4; then
A2:a <= c & d <= b & a <= d & c <= b by A1,RCOMP_1:48; then
A3:['c,b '] c= [' a,b '] & ['a,c '] c= [' a,b '] by Lm3;
A4:f is_integrable_on ['a,b '] & f is_bounded_on ['a,b '] &
    ['a,b '] c= dom f by Th26,A1;
   per cases;
   suppose A5:c <= d; then
A6: f is_integrable_on ['c,d '] & f is_bounded_on ['c,d '] &
    ['c,d '] c= dom f by Th18,A2,A4;
    ['c,d '] c= [' c,b '] by Lm3,A5,A2; then
    ['c,d '] c= ['a,b '] by A3,XBOOLE_1:1; then
    for x be real number st x in ['c,d '] holds f.x=e by A1;
    hence integral(f,c,d) = e*(d-c) by A5,Th26,A6;
   end;
   suppose A7:not(c <= d); then
A8: f is_integrable_on ['d,c '] & f is_bounded_on ['d,c '] &
    ['d,c '] c= dom f by Th18,A2,A4;
    ['d,c '] c= [' a,c '] by Lm3,A7,A2; then
    ['d,c '] c= ['a,b '] by A3,XBOOLE_1:1; then
    for x be real number st x in ['d,c '] holds f.x=e by A1; then
A9: integral(f,d,c) = e*(c-d) by A7,Th26,A8;
    integral(f,c,d) = -integral(f,[' d,c ']) by A7,INTEGRA5:def 5; then
    integral(f,c,d) = -integral(f,d,c) by A7,INTEGRA5:def 5;
    hence integral(f,c,d) = e*(d-c) by A9;
   end;
end;

begin :: Fundamental Theorem of Calculus

theorem Th28:
for x0 be real number, F be PartFunc of REAL,REAL st
 a <= b & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
 [' a,b '] c= dom f & ].a,b.[ c= dom F &
 (for x be real number st x in ].a,b.[ holds F.x = integral(f,a,x)) &
 x0 in ].a,b.[ & f is_continuous_in x0
holds
    F is_differentiable_in x0 & diff(F,x0)=f.x0
proof
   let x0 be real number,
       F be PartFunc of REAL,REAL;
   assume A1:a <= b & f is_integrable_on [' a,b '] &
    f is_bounded_on [' a,b '] & [' a,b '] c= dom f & ].a,b.[ c= dom F &
    (for x be real number st x in ].a,b.[ holds F.x = integral(f,a,x)) &
    x0 in ].a,b.[ & f is_continuous_in x0; then
A2:[' a,b '] = [.a,b.] by INTEGRA5:def 4;
   deffunc F0(Element of REAL)=(f.x0)*$1;
   consider L be Function of REAL,REAL such that
A3: for h be Real  holds L.h=F0(h) from FUNCT_2:sch 4;
   reconsider L as PartFunc of REAL,REAL;
   deffunc F1(set) = F.(x0+R_id($1))-F.x0-L.R_id($1);
   deffunc F2(set) = 0;
   defpred C1[set] means (x0+R_id($1)) in ].a,b.[;
   consider R be Function such that
A4: dom R=REAL &
    for x be set st x in REAL holds
     (C1[x] implies R.x = F1(x)) & (not C1[x] implies R.x = F2(x))
        from PARTFUN1:sch 1;
   rng R c= REAL
   proof
    let y be set;
    assume y in rng R; then
    consider x be set such that
A5:  x in dom R & y=R.x by FUNCT_1:def 5;
    (C1[x] implies R.x = F1(x)) & (not C1[x] implies R.x = F2(x)) by A4,A5;
    hence y in REAL by A5;
   end; then
   reconsider R as PartFunc of REAL,REAL by A4,PARTFUN1:25;
   reconsider L as LINEAR by A3,FDIFF_1:def 4;
A6:R is total by A4,PARTFUN1:def 4;
   for h be convergent_to_0 Real_Sequence holds
    (h")(#)(R*h) is convergent & lim ((h")(#)(R*h)) = 0
   proof
    let h be convergent_to_0 Real_Sequence;
A7: for e be real number st 0<e
     ex n be Element of NAT st
      for m be Element of NAT st n <= m holds abs(((h")(#)(R*h)).m - 0) < e
    proof
     let e0 be real number;
     set e=e0/2;
     assume 0 < e0; then
A8:  0 < e & e < e0 by XREAL_1:217,218; then
     consider p be real number such that
A9:   0 < p &
      for t be real number st
        t in dom f & abs(t - x0) < p holds abs(f.t - f.x0) < e by A1,FCONT_1:3;
     consider N be Neighbourhood of x0 such that
A10:  N c= ].a,b.[ by A1,RCOMP_1:42;
     consider q be real number such that
A11:  0<q & N = ].x0-q,x0+q.[ by RCOMP_1:def 7;
     set r=min(p/2,q/2);
A12: 0 < p/2 & 0 < q/2 & p/2 < p & q/2 < q by A9,A11,XREAL_1:217,
218; then
A13: 0 < r & r <= p/2 & r <= q/2 by XXREAL_0:17,15; then
     r < q by A12,XXREAL_0:2; then
A14: ].x0-r,x0+r.[ c= ].x0-q,x0+q.[ by Th2;

A15: ].a,b.[ c= [.a,b.] by RCOMP_1:15;

     h is convergent & lim(h) = 0 by FDIFF_1:def 1;then
     consider n be Element of NAT such that
A16:  for m be Element of NAT st n <= m holds abs(h.m - 0)<r
       by A13,SEQ_2:def 7;
     take n;
     let m be Element of NAT;
     assume n <= m; then
A17: abs(h.m - 0) < r by A16; then
     abs((x0 + h.m)-x0) < r; then
     x0 + h.m in ].x0-r,x0+r.[ by RCOMP_1:8; then
     x0 + h.m in ].x0-q,x0+q.[ by A14; then
A18: x0+h.m in ].a,b.[ & F.(x0+h.m) = integral(f,a,x0+h.m) by A10,A11,A1; then
     x0+R_id(h.m) in ].a,b.[ by RSSPACE:def 3; then
     R.(h.m)=F.(x0+R_id(h.m))-F.x0-L.R_id(h.m) by A4; then
     R.(h.m)=F.(x0+h.m)-F.x0-L.R_id(h.m) by RSSPACE:def 3; then
     R.(h.m) = F.(x0+h.m)-F.x0-L.(h.m) by RSSPACE:def 3; then
     R.(h.m) = F.(x0+h.m)-F.x0-(f.x0)*h.m by A3; then
A19: R.(h.m) =integral(f,a,x0+h.m ) -integral(f,a,x0) -(f.x0)*h.m by A1,A18;

     deffunc G0(Element of REAL)=f.x0;
     consider g be Function of REAL,REAL such that
A20:  for h be Real holds g.h=G0(h) from FUNCT_2:sch 4;
A21: dom g = REAL by FUNCT_2:def 1;
A22: for x be real number st x in [' a,b '] holds g.x = f.x0 by A20; then
A23: g is_integrable_on [' a,b '] & g is_bounded_on [' a,b ']
        by Th26,A1,A21; then
     f-g is_bounded_on ['a,b '] /\ ['a,b '] by A1,RFUNCT_1:101; then
A24: f-g is_integrable_on [' a,b '] &
     f-g is_bounded_on [' a,b '] by A21,A1,A23,Th11;

     [' a,b '] /\ [' a,b '] c= dom f /\ dom g by A1,A21,XBOOLE_1:27; then
A25: [' a,b '] c= dom (f-g) by SEQ_1:def 4;

     for x be real number st x in [' min(x0,x0+h.m),max(x0,x0+h.m) ']
       holds abs((f-g).x) <= e
     proof
      let x be real number;
      assume A26:x in [' min(x0,x0+h.m),max(x0,x0+h.m) '];
      reconsider x1 = x as Real by XREAL_0:def 1;

      min(x0,x0+h.m) <= x0 & x0 <= max(x0,x0+h.m) by XXREAL_0:17,25
; then
      min(x0,x0+h.m) <= max(x0,x0+h.m) by XXREAL_0:2; then
  A27:x in [. min(x0,x0+h.m),max(x0,x0+h.m) .] by A26,INTEGRA5:def 4;
      abs(x-x0) <=abs(h.m)
      proof
       per cases;
       suppose x0<= x0+h.m; then
        x0 = min(x0,x0+h.m) & x0+h.m = max(x0,x0+h.m)
          by XXREAL_0:def 8,def 9; then
        x in {z where z is Real: x0<=z & z<=x0+h.m } by A27,RCOMP_1:def 1; then
   A28: ex z be Real st x=z & x0<=z & z<=x0+h.m; then
   A29: x-x0 <= x0+h.m - x0 by XREAL_1:11; then
   A30: 0 <= h.m by A28,XREAL_1:50;
        0 <= x-x0 by A28,XREAL_1:50; then
        abs(x-x0) = x-x0 by ABSVALUE:def 1;
        hence abs(x-x0) <= abs(h.m) by A29,A30,ABSVALUE:def 1;
       end;

       suppose A31:not (x0<=x0 +h.m); then
        x0 = max(x0,x0+h.m) &
        x0+h.m = min(x0,x0+h.m) by XXREAL_0:def 8,def 9; then
        x in {z where z is Real: x0+h.m<=z & z<=x0 } by A27,RCOMP_1:def 1; then
   A32: ex z be Real st x=z & x0+h.m<=z & z<=x0; then
   A33: x0 +h.m -x0 <= x-x0 by XREAL_1:11;
        per cases;
        suppose x-x0 <> 0; then
         x-x0 < 0 by A32,XREAL_1:49; then
   A34:  abs(x-x0) = -(x-x0) by ABSVALUE:def 1;
         x0 + h.m - x0 < x0-x0 by A31,XREAL_1:16; then
         abs(h.m) = -(h.m) by ABSVALUE:def 1;
         hence abs(x-x0) <= abs(h.m) by A33,A34,XREAL_1:26;
        end;
        suppose x-x0 = 0; then
         abs(x-x0) = 0 by ABSVALUE:def 1;
         hence abs(x-x0) <= abs(h.m) by COMPLEX1:132;
        end;
       end;
      end; then
  A35:abs(x-x0) < r by A17,XXREAL_0:2; then
      abs(x-x0) < p/2 by A13,XXREAL_0:2; then
  A36:abs(x-x0) < p by A12,XXREAL_0:2;
      x in ].x0-r,x0+r.[ by A35,RCOMP_1:8; then
      x in ].x0-q,x0+q.[ by A14; then
      x in ].a,b.[ by A10,A11; then
  A37:x in [.a,b.] by A15; then
      abs(f.x- f.x0)<=e by A1,A2,A9,A36; then
      abs(f.x1- g.x1)<=e by A20;
      hence abs((f-g).x)<=e by A2,A37,A25,SEQ_1:def 4;
     end; then
A38: abs integral(f-g,x0,x0+h.m) <= e*abs(x0+h.m-x0)
        by Lm8,A1,A15,A18,A2,A24,A25;
     h is_not_0 by FDIFF_1:def 1;then
     h.m <> 0 by SEQ_1:6; then
A39: 0 < abs(h.m) by COMPLEX1:133;
A40: abs(R.(h.m)/(h.m)) = abs(R.(h.m))/abs(h.m) by COMPLEX1:153;
A41: integral(g,x0,x0+h.m)=(f.x0)*((x0+h.m)-x0) by
       A1,A2,A15,A18,A21,A22,Th27;
     integral(f,a,x0+h.m) = integral(f,a,x0) +integral(f,x0,x0+h.m)
         by Th20,A1,A2,A15,A18; then
     R.(h.m)
       =integral(f-g,x0,x0+h.m) by Th24,A1,A2,A15,A18,A21,A23,A19,A41; then
     abs(R.(h.m))/abs(h.m) <= e*abs(h.m)/abs(h.m) by A38,A39,XREAL_1:74; then
A42: abs(R.(h.m)/(h.m)) <= e by A39,A40,XCMPLX_1:90;
     rng h c= dom R by A4; then
     R.(h.m)/(h.m) =(R*h).m /(h.m) by RFUNCT_2:21; then
     R.(h.m)/(h.m) =((R*h).m) *((h").m) by SEQ_1:def 8; then
     R.(h.m)/(h.m) = ((h")(#)(R*h)).m by SEQ_1:12;
     hence thesis by A8,A42,XXREAL_0:2;
    end;
    hence (h")(#)(R*h) is convergent by SEQ_2:def 6;
    hence lim((h")(#)(R*h)) = 0 by A7,SEQ_2:def 7;
   end; then
   reconsider R as REST by A6,FDIFF_1:def 3;
   consider N be Neighbourhood of x0 such that
A43:N c= ].a,b.[ by A1,RCOMP_1:42;
A44:N c= dom F by A1,XBOOLE_1:1,A43;
A45:for x be Real st x in N holds F.x-F.x0 = L.(x-x0) + R.(x-x0)
   proof
    let x be Real;
    assume A46:x in N;
A47:x-x0 in REAL by XREAL_0:def 1; then
A48:x0+R_id((x-x0)) = x0 + (x-x0) by RSSPACE:def 3;
    x0 + (x-x0) in N by A46;then
    x0+R_id(x-x0) in N by A47,RSSPACE:def 3; then
    R.(x-x0) = F1(x-x0) by A4,A43,A47;
    hence F.x-F.x0 = L.(x-x0) + R.(x-x0) by A48;
   end;
   hence A49:F is_differentiable_in x0 by A44,FDIFF_1:def 5;
   L.1=(f.x0)*1 by A3;
   hence diff(F,x0)=f.x0 by A44,A45,A49,FDIFF_1:def 6;
end;

Lm13:
for a,b be real number, f be PartFunc of REAL,REAL holds
 ex F be PartFunc of REAL,REAL st
  ].a,b.[ c= dom F &
  (for x be real number st x in ].a,b.[ holds F.x = integral(f,a,x))
proof
   let a,b be real number;
   let f be PartFunc of REAL,REAL;
   defpred C[set] means $1 in ].a,b.[;
   deffunc F(real number) = integral(f,a,$1);
   deffunc G(real number) = 0;
   consider F be Function such that
A1: dom F = REAL &
    for x be Element of REAL holds
     (C[x] implies F.x = F(x)) & (not C[x] implies F.x = G(x))
      from PARTFUN1:sch 4;
   now let y be set;
    assume y in rng F; then
    consider x be set such that
A2:  x in dom F & y = F.x by FUNCT_1:def 5;
    reconsider x as Element of REAL by A1,A2;
A3: now assume x in ].a,b.[; then
     F.x = integral(f,a,x) by A1;
     hence y in REAL by A2;
    end;
    now assume not x in ].a,b.[; then
     F.x = 0 by A1;
     hence y in REAL by A2;
    end;
    hence y in REAL by A3;
   end; then
   rng F c= REAL by TARSKI:def 3; then
   reconsider F as PartFunc of REAL,REAL by A1,PARTFUN1:25;
   take F;
   thus thesis by A1;
end;

theorem
for x0 be real number st
 a <= b & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
 [' a,b '] c= dom f & x0 in ].a,b.[ & f is_continuous_in x0
holds
 ex F be PartFunc of REAL,REAL st
  ].a,b.[ c= dom F &
  (for x be real number st x in ].a,b.[ holds F.x = integral(f,a,x)) &
  F is_differentiable_in x0 & diff(F,x0)=f.x0
proof
   let x0 be real number;
   assume
A1:a <= b & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
   [' a,b '] c= dom f & x0 in ].a,b.[ & f is_continuous_in x0;
   consider F be PartFunc of REAL,REAL such that
A2: ].a,b.[ c= dom F &
    (for x be real number st x in ].a,b.[ holds F.x = integral(f,a,x))
      by Lm13;
   F is_differentiable_in x0 & diff(F,x0)=f.x0 by A1,A2,Th28;
   hence thesis by A2;
end;
