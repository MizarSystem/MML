:: Integrability and the Integral of Partial Functions from $\Bbb R$ into
:: $\Bbb R$
::  by Noboru Endou , Yasunari Shidama and Masahiko Yamazaki
::
:: Received November 17, 2006
:: Copyright (c) 2006 Association of Mizar Users

environ

 vocabularies INTEGRA1, FINSEQ_1, RLVECT_1, ARYTM_1, RELAT_1, FUNCT_1,
      PARTFUN1, REALSET1, SEQ_1, BOOLE, RFUNCT_1, FCONT_1, INT_1, SEQ_2,
      ABSVALUE, INTEGRA2, FUNCT_3, ORDINAL2, MEASURE5, ARYTM_3, SQUARE_1,
      FDIFF_1, RCOMP_1, INTEGRA5, ARYTM, RFINSEQ, INTEGRA4, RSSPACE;
 notations TARSKI, XBOOLE_0, SUBSET_1, RELAT_1, ORDINAL1, NUMBERS, XCMPLX_0,
      XREAL_0, COMPLEX1, REAL_1, NAT_1, INT_1, FUNCT_1, PARTFUN1, FUNCT_2,
      RCOMP_1, PSCOMP_1, FINSEQ_1, VALUED_1, SEQ_1, RFUNCT_1, SEQ_2, GOBOARD1,
      FDIFF_1, RFINSEQ, PRE_CIRC, RVSUM_1, INTEGRA1, INTEGRA2, FCONT_1,
      RFUNCT_2, RSSPACE, INTEGRA4, INTEGRA5, XXREAL_0;
 constructors PARTFUN1, REAL_1, NAT_1, BINOP_2, COMPLEX1, RFUNCT_1, RFUNCT_2,
      FCONT_1, FDIFF_1, RFINSEQ, PRE_CIRC, RSSPACE, PSCOMP_1, INTEGRA2,
      INTEGRA4, INTEGRA5, INT_1, VALUED_1, SEQ_1;
 registrations XBOOLE_0, RELAT_1, ORDINAL1, FUNCT_2, NUMBERS, XXREAL_0,
      XREAL_0, NAT_1, INT_1, MEMBERED, FINSEQ_1, SEQ_1, RCOMP_1, FDIFF_1,
      GOBOARD1, INTEGRA1, VALUED_0, VALUED_1;
 requirements NUMERALS, BOOLE, SUBSET, REAL, ARITHM;
 definitions TARSKI, XCMPLX_0, INTEGRA1, FINSEQ_1, INTEGRA5, VALUED_1;
 theorems ORDINAL1, REAL_1, REAL_2, PARTFUN1, INTEGRA1, RFUNCT_1, FUNCT_1,
      FINSEQ_1, RVSUM_1, SEQ_1, SEQ_2, FDIFF_1, ABSVALUE, PRE_CIRC, NAT_1,
      INT_1, COMPLEX1, TARSKI, INTEGRA2, INTEGRA3, RFINSEQ, GOBOARD1, INTEGRA4,
      FINSEQ_2, FINSEQ_3, RCOMP_1, FCONT_1, RFUNCT_2, XREAL_0, XXREAL_0,
      FUNCT_2, FINSEQ_5, XBOOLE_0, XBOOLE_1, XCMPLX_1, XREAL_1, INTEGRA5,
      RSSPACE, RELAT_1, VALUED_1;
 schemes FUNCT_2, PARTFUN1, FINSEQ_2;

begin :: Preliminaries

reserve a,b,c,d,e,x for real number,
  A for closed-interval Subset of REAL,
  f,g for PartFunc of REAL,REAL;

theorem Th1:
  a <= b & c <= d & a+c = b+d implies a = b & c = d
proof
  assume
A1: a <=b & c <=d & a+c =b+d;
  assume not (a=b & c =d);
  then a < b or c < d by A1,REAL_1:def 5;
  hence contradiction by A1,XREAL_1:10;
end;

theorem Th2:
  a <= b implies ].x-a,x+a.[ c= ].x-b,x+b.[
proof
  assume a <= b;
  then x-b <= x - a & x+a <= x+b by XREAL_1:9,12;
  hence thesis by RCOMP_1:52;
end;

theorem Th3:
  for R be Relation, A,B,C be set st A c= B & A c= C holds (R|B)|A = (R|C)|A
proof
  let R be Relation, A,B,C be set;
  assume
A1: A c= B & A c= C;
  then (R|C)|A = R|A by RELAT_1:103;
  hence thesis by A1,RELAT_1:103;
end;

theorem Th4:
  for A,B,C be set st A c= B & A c= C holds chi(B,B)|A = chi(C,C)|A
proof
  let A,B,C be set;
  assume
A1: A c= B & A c= C;
A2: now
    assume A is non empty;
    then
A3: B is non empty & C is non empty by A1,XBOOLE_1:58,61;
    dom(chi(B,B)|A) =dom(chi(B,B)) /\ A &
    dom(chi(C,C)|A) =dom(chi(C,C)) /\ A by FUNCT_1:68;
    then dom(chi(B,B)|A) =B /\ A & dom(chi(C,C)|A) =C /\ A by A3,RFUNCT_1:77;
    then
A4: dom(chi(B,B)|A) =A & dom(chi(C,C)|A) =A by A1,XBOOLE_1:28;
    now
      let x be set;
      assume
A5:   x in A;
      then (chi(B,B)|A).x =(chi(B,B)).x by A4,FUNCT_1:68;
      then (chi(B,B)|A).x = 1 by A1,A5,RFUNCT_1:77;
      then (chi(B,B)|A).x = (chi(C,C)).x by A1,A5,RFUNCT_1:77;
      hence (chi(B,B)|A).x = (chi(C,C)|A).x by A4,A5,FUNCT_1:68;
    end;
    hence chi(B,B)|A = chi(C,C)|A by A4,FUNCT_1:9;
  end;
  now
    assume A is empty;
    then chi(B,B)|A = {} & chi(C,C)|A = {} by RELAT_1:110;
    hence chi(B,B)|A = chi(C,C)|A;
  end;
  hence thesis by A2;
end;

theorem Th5:
  a <= b implies vol([' a,b '])=b-a
proof
  assume a<=b;
  then
A1: [' a,b '] = [.a,b.] by INTEGRA5:def 4;
  then [.a,b.] = [.inf([.a,b.]), sup([.a,b.]).] by INTEGRA1:5;
  then a=inf([.a,b.]) & b=sup([.a,b.]) by A1,INTEGRA1:6;
  hence vol(['a,b ']) = b-a by A1;
end;

theorem Th6:
  vol([' min(a,b),max(a,b) ']) = abs(b-a)
proof
A1: min(a,b) <= a & a <= max(a,b) by XXREAL_0:17,25;
  per cases;
  suppose
A2: a<= b;
    then min(a,b) = a & max(a,b) = b by XXREAL_0:def 8,def 9;
    then
A3: vol([' min(a,b),max(a,b) '])=b-a by A1,Th5;
    0 <= b-a by A2,XREAL_1:50;
    hence vol([' min(a,b),max(a,b) '])=abs(b-a) by A3,ABSVALUE:def 1;
  end;
  suppose
A4: not(a<= b);
    then
A5: min(a,b) = b & max(a,b) = a by XXREAL_0:def 8,def 9;
    0 <= a-b by A4,XREAL_1:50;
    then a-b = abs(a-b) by ABSVALUE:def 1
      .=abs(b-a) by COMPLEX1:146;
    hence vol([' min(a,b),max(a,b) '])=abs(b-a) by A1,A5,Th5;
  end;
end;

begin :: Integrability and The Integral of Partial Functions from REAL to REAL

Lm1: A c= dom f implies f||A is Function of A,REAL
proof
  assume
A1: A c= dom f;
  rng f c= REAL;
  then f is Function of dom f,REAL by FUNCT_2:4;
  hence f||A is Function of A,REAL by A1,FUNCT_2:38;
end;

theorem Th7:
  A c= dom f & f is_integrable_on A & f is_bounded_on A implies
  abs f is_integrable_on A & abs(integral(f,A)) <= integral(abs f,A)
proof
  assume
A1: A c= dom f;
  assume f is_integrable_on A & f is_bounded_on A;
  then
A2: f||A is_integrable_on A & f||A is_bounded_on A by INTEGRA5:9,def 2;
  f||A is Function of A,REAL by A1,Lm1;
  then
A3: abs(f||A) is_integrable_on A &
  abs(integral(f||A))<=integral(abs(f||A)) by A2,INTEGRA4:23;
  abs(f||A) = abs(f)||A by RFUNCT_1:62;
  hence thesis by A3,INTEGRA5:def 2;
end;

theorem Th8:
  a<=b & [' a,b '] c= dom f & f is_integrable_on [' a,b '] &
  f is_bounded_on [' a,b '] implies abs(integral(f,a,b)) <=integral(abs f,a,b)
proof
  assume a<=b;
  then integral(f,a,b) = integral(f,[' a,b ']) &
  integral(abs f,a,b) = integral(abs f,[' a,b ']) by INTEGRA5:def 5;
  hence thesis by Th7;
end;

theorem Th9:
  for r be Real st A c= dom f & f is_integrable_on A & f is_bounded_on A
  holds r(#)f is_integrable_on A & integral(r(#)f,A)=r*integral(f,A)
proof
  let r be Real;
  assume
A1: A c= dom f & f is_integrable_on A & f is_bounded_on A;
  then
A2: f||A is_integrable_on A & f||A is_bounded_on A by INTEGRA5:9,def 2;
  rng f c= REAL;
  then f is Function of dom f,REAL by FUNCT_2:4;
  then
A3: f|A is Function of A,REAL by A1,FUNCT_2:38;
  r(#)(f||A) =(r(#)f)|A by RFUNCT_1:65;
  then (r(#)f)||A is_integrable_on A by A2,A3,INTEGRA2:31;
  hence r(#)f is_integrable_on A by INTEGRA5:def 2;
  integral((r(#)f),A) =integral(r(#)(f||A)) by RFUNCT_1:65;
  hence integral((r(#)f),A) =r*integral(f,A) by A2,A3,INTEGRA2:31;
end;

theorem Th10:
  a<=b & [' a,b '] c= dom f & f is_integrable_on [' a,b '] &
  f is_bounded_on [' a,b '] implies integral(c(#)f,a,b) =c*integral(f,a,b)
proof
  set F = f||['a,b '], G = (c(#)f)|['a,b '];
A1: c is Real by XREAL_0:def 1;
  assume
A2: a<=b & [' a,b '] c= dom f & f is_integrable_on [' a,b '] &
  f is_bounded_on [' a,b '];
  then integral(f,a,b) = integral(f,[' a,b ']) &
  integral(c(#)f,a,b) = integral(c(#)f,[' a,b ']) by INTEGRA5:def 5;
  hence thesis by A1,A2,Th9;
end;

theorem Th11:
  A c= dom f & A c= dom g & f is_integrable_on A & f is_bounded_on A &
  g is_integrable_on A & g is_bounded_on A implies
  f+g is_integrable_on A & f-g is_integrable_on A &
  integral(f+g,A) = integral(f,A) + integral(g,A) &
  integral(f-g,A) = integral(f,A) - integral(g,A)
proof
  assume
A1: A c= dom f & A c= dom g & f is_integrable_on A & f is_bounded_on A &
  g is_integrable_on A & g is_bounded_on A;
  then
A2: f||A is_integrable_on A & g||A is_integrable_on A &
  f||A is_bounded_on A & g||A is_bounded_on A by INTEGRA5:9,def 2;
A3: f||A is Function of A,REAL & g||A is Function of A,REAL by A1,Lm1;
  then
A4: f||A + g||A is_integrable_on A &
  f||A - g||A is_integrable_on A by A2,INTEGRA1:59,INTEGRA2:33;
A5: f||A + g||A = (f+g)||A & f||A - g||A = (f-g)||A by RFUNCT_1:60,63;
  hence f+g is_integrable_on A & f-g is_integrable_on A by A4,INTEGRA5:def 2;
  thus thesis by A2,A3,A5,INTEGRA1:59,INTEGRA2:33;
end;

theorem Th12:
  a<=b & [' a,b '] c= dom f & [' a,b '] c= dom g &
  f is_integrable_on [' a,b '] & g is_integrable_on [' a,b '] &
  f is_bounded_on [' a,b '] & g is_bounded_on [' a,b '] implies
  integral(f+g,a,b) =integral(f,a,b) + integral(g,a,b) &
  integral(f-g,a,b) =integral(f,a,b) - integral(g,a,b)
proof
  set F = f||['a,b '], G = g||['a,b '];
  assume
A1: a<=b & [' a,b '] c= dom f & [' a,b '] c= dom g &
  f is_integrable_on [' a,b '] & g is_integrable_on [' a,b '] &
  f is_bounded_on [' a,b '] & g is_bounded_on [' a,b '];
  then integral(f,a,b) = integral(f,[' a,b ']) &
  integral(g,a,b) = integral(g,[' a,b ']) &
  integral(f+g,a,b) = integral(f+g,[' a,b ']) &
  integral(f-g,a,b) = integral(f-g,[' a,b ']) by INTEGRA5:def 5;
  hence thesis by A1,Th11;
end;

theorem
  f is_bounded_on A & g is_bounded_on A implies f(#)g is_bounded_on A
proof
  assume f is_bounded_on A & g is_bounded_on A;
  then f(#)g is_bounded_on A /\ A by RFUNCT_1:101;
  hence f(#)g is_bounded_on A;
end;

theorem
  A c= dom f & A c= dom g & f is_integrable_on A & f is_bounded_on A &
  g is_integrable_on A & g is_bounded_on A implies f(#)g is_integrable_on A
proof
  assume
A1: A c= dom f & A c= dom g & f is_integrable_on A & f is_bounded_on A &
  g is_integrable_on A & g is_bounded_on A;
  then
A2: f||A is_integrable_on A & f||A is_bounded_on A &
  g||A is_integrable_on A & g||A is_bounded_on A by INTEGRA5:9,def 2;
A3: f||A is Function of A,REAL & g||A is Function of A,REAL by A1,Lm1;
  (f||A)(#)(g||A) = (f(#)g)||A by INTEGRA5:4;
  then (f(#)g)||A is_integrable_on A by A2,A3,INTEGRA4:29;
  hence f(#)g is_integrable_on A by INTEGRA5:def 2;
end;

theorem Th15:
  for n be Element of NAT st n > 0 & vol A > 0 holds
  ex D being Element of divs A st len D = n &
  for i be Element of NAT st i in dom D holds D.i=inf A + vol(A)/n*i
proof
  let n be Element of NAT;
  assume that
A1: n > 0 & vol A > 0;
  deffunc F(Nat)=inf A + vol(A)/n*$1;
  consider D being FinSequence of REAL such that
A2: len D = n & for i be Nat st i in Seg n holds D.i=F(i) from FINSEQ_2:sch 1;
  for i,j be Element of NAT st i in dom D & j in dom D & i<j holds D.i < D.j
  proof
    let i,j be Element of NAT;
    assume
A3: i in dom D & j in dom D & i < j;
    then
A4: i in Seg n & j in Seg n by A2,FINSEQ_1:def 3;
    vol(A)/n > 0 by A1,REAL_2:127;
    then vol(A)/n*i < vol(A)/n*j by A3,XREAL_1:70;
    then inf A + vol(A)/n*i < inf A + vol(A)/n*j by XREAL_1:8;
    then D.i < inf A + vol(A)/n*j by A2,A4;
    hence thesis by A2,A4;
  end;
  then reconsider D as non empty increasing FinSequence of REAL
  by A1,A2,FINSEQ_1:25,GOBOARD1:def 1;
  for x1 being set st x1 in rng D holds x1 in A
  proof
    let x1 be set;
    assume x1 in rng D;
    then consider i be Element of NAT such that
A5: i in dom D & D.i=x1 by PARTFUN1:26;
    i in Seg n by A2,A5,FINSEQ_1:def 3;
    then
A6: x1 = inf A + vol(A)/n*i by A2,A5;
A7: 1 <= i & i <= len D & vol(A)/n>0 by A1,A5,FINSEQ_3:27,REAL_2:127;
    then
A8: vol(A)/n*i>0 by REAL_2:122;
    vol(A)/n*i <= vol(A)/n*n by A2,A7,XREAL_1:66;
    then vol(A)/n*i <= vol A by A1,XCMPLX_1:88;
    then inf A <= inf A + vol(A)/n*i & inf A + vol(A)/n*i <= inf A+ vol A
    by A8,XREAL_1:8,31;
    hence thesis by A6,INTEGRA2:1;
  end;
  then
A9: rng D c= A by TARSKI:def 3;
  D.(len D)=inf A + vol(A)/n*n by A1,A2,FINSEQ_1:5;
  then D.(len D)=inf A + vol(A) by A1,XCMPLX_1:88;
  then D is DivisionPoint of A by A9,INTEGRA1:def 2;
  then reconsider D as Element of divs A by INTEGRA1:def 3;
  take D;
  now
    let i be Element of NAT;
    assume i in dom D;
    then i in Seg n by A2,FINSEQ_1:def 3;
    hence D.i=inf A+vol(A)/n*i by A2;
  end;
  hence thesis by A2;
end;

begin :: Integrability on a Subinterval

theorem Th16:
  vol A > 0 implies ex T being DivSequence of A st
  delta T is convergent & lim delta T = 0 & for n be Element of NAT holds
  ex Tn being Element of divs A st Tn divide_into_equal n+1 & T.n =Tn
proof
  assume
A1: vol A > 0;
  defpred P[set,set] means
  ex n being Element of NAT, e being Element of divs A st
  n = $1 & e = $2 & e divide_into_equal n+1;
A2: for n be Element of NAT ex D being Element of divs A st P[n,D]
  proof
    let n be Element of NAT;
    consider D being Element of divs A such that
A3: len D = n+1 & for i be Element of NAT st i in dom D holds
    D.i=inf A+vol(A)/(n+1)*i by A1,Th15;
    D divide_into_equal n+1 by A3,INTEGRA4:def 1;
    hence thesis;
  end;
  consider T being Function of NAT,divs A such that
A4: for n be Element of NAT holds P[n,T.n] from FUNCT_2:sch 3(A2);
  reconsider T as DivSequence of A;
A5: for n be Element of NAT holds
  ex Tn being Element of divs A st Tn divide_into_equal n+1 & T.n =Tn
  proof
    let n be Element of NAT;
    ex n1 be Element of NAT, Tn being Element of divs A st
    n1 = n & Tn = T.n & Tn divide_into_equal n1+1 by A4;
    hence thesis;
  end;
A6: for i be Element of NAT holds (delta T).i = vol(A)/(i+1)
  proof
    let i be Element of NAT;
A7: (delta T).i = delta(T.i) by INTEGRA2:def 3;
    rng upper_volume(chi(A,A),(T.i))={vol(A)/(i+1)}
    proof
      for x1 being set st x1 in rng upper_volume(chi(A,A),(T.i)) holds
      x1 in {vol(A)/(i+1)}
      proof
        let x1 be set;
        assume x1 in rng upper_volume(chi(A,A),(T.i));
        then consider k be Element of NAT such that
A8:     k in dom upper_volume(chi(A,A),(T.i))&
        (upper_volume(chi(A,A),(T.i))).k=x1 by PARTFUN1:26;
        reconsider D = T.i as Element of divs A;
A9:     ex n being Element of NAT, e being Element of divs A st
        n = i & e = D & e divide_into_equal n+1 by A4;
        k in Seg len upper_volume(chi(A,A),(T.i)) by A8,FINSEQ_1:def 3;
        then
A10:    k in Seg len D by INTEGRA1:def 7;
        then
A11:    x1=vol(divset(D,k)) by A8,INTEGRA1:22;
A12:    k in dom D by A10,FINSEQ_1:def 3;
        sup divset(D,k)-inf divset(D,k)=vol(A)/(i+1)
        proof
A13:      now
            assume
A14:        k=1;
            then
A15:        inf divset(D,k)=inf A & sup divset(D,k)=D.1 by A12
            ,INTEGRA1:def 5;
            len D = i+1 & for j be Element of NAT st j in dom D holds
            D.j=inf A + vol(A)/(len D)*j by A9,INTEGRA4:def 1;
            then sup divset(D,k)=inf A+vol(A)/(i+1)*1 by A12,A14,A15;
            then sup divset(D,k)-inf divset(D,k)=inf A+vol(A)/(i+1)-inf A
            by A12,A14,INTEGRA1:def 5;
            hence sup divset(D,k)-inf divset(D,k)=vol(A)/(i+1);
          end;
          now
            assume
A16:        k <> 1;
            then
A17:        inf divset(D,k)=D.(k-1) & sup divset(D,k)=D.k by A12,
            INTEGRA1:def 5;
            k-1 in dom D & k-1 in NAT by A12,A16,INTEGRA1:9;
            then D.(k-1)=inf A + vol(A)/(len D)*(k-1) &
            D.k = inf A + vol(A)/(len D)* k by A9,A12,INTEGRA4:def 1;
            hence sup divset(D,k)-inf divset(D,k)=vol(A)/(i+1)
            by A9,A17,INTEGRA4:def 1;
          end;
          hence sup divset(D,k)-inf divset(D,k)=vol(A)/(i+1) by A13;
        end;
        hence thesis by A11,TARSKI:def 1;
      end;
      then
A18:  rng upper_volume(chi(A,A),(T.i)) c= {vol(A)/(i+1)} by TARSKI: def 3;
      for x1 being set st x1 in {vol(A)/(i+1)} holds
      x1 in rng upper_volume(chi(A,A),(T.i))
      proof
        let x1 be set;
        assume x1 in {vol(A)/(i+1)};
        then
A19:    x1 = vol(A)/(i+1) by TARSKI:def 1;
        reconsider D = T.i as Element of divs A;
        rng D <> {};
        then
A20:    1 in dom D by FINSEQ_3:34;
        then
A21:    1 in Seg len D by FINSEQ_1:def 3;
        then 1 in Seg len upper_volume(chi(A,A),D) by INTEGRA1:def 7;
        then 1 in dom upper_volume(chi(A,A),D) by FINSEQ_1:def 3;
        then
A22:    (upper_volume(chi(A,A),D)).1 in rng upper_volume(chi(A,A), D)
        by FUNCT_1:def 5;
A23:    (upper_volume(chi(A,A),D)).1 = vol(divset(D,1)) by A21,INTEGRA1:22;
A24:    sup divset(D,1)=D.1 & inf divset(D,1)=inf A by A20,INTEGRA1:def 5;
A25:    ex n being Element of NAT, e being Element of divs A st
        n = i & e = D & e divide_into_equal n+1 by A4;
        then sup divset(D,1)=inf A+vol(A)/(len D)*1 by A20,A24,INTEGRA4:def 1;
        then vol(divset(D,1))=inf A+vol(A)/(len D)-inf A by A20,INTEGRA1:def 5;
        hence thesis by A19,A22,A23,A25,INTEGRA4:def 1;
      end;
      then {vol(A)/(i+1)} c= rng upper_volume(chi(A,A),(T.i)) by TARSKI:def 3;
      hence thesis by A18,XBOOLE_0:def 10;
    end;
    then (delta T).i in {vol(A)/(i+1)} by A7,PRE_CIRC:def 1;
    hence thesis by TARSKI:def 1;
  end;
A26: for i,j be Element of NAT st i <= j holds (delta T).i >= (delta T).j
  proof
    let i,j be Element of NAT;
    assume i <= j;
    then
A27: i+1 <= j+1 by XREAL_1:8;
    vol A >= 0 by INTEGRA1:11;
    then vol A/(i+1) >= vol(A)/(j+1) by A27,REAL_2:201;
    then (delta T).i >= vol(A)/(j+1) by A6;
    hence thesis by A6;
  end;
A28: for a be Real st 0 < a ex i be Element of NAT st abs((delta T).i-0)<a
  proof
    let a be Real;
    assume
A29: 0<a;
A30: vol A >= 0 by INTEGRA1:11;
    then
A31: vol(A)/a >= 0 by A29,REAL_2:125;
    reconsider i1=[\vol(A)/a/]+1 as Integer;
    [\vol(A)/a/]+1>0 by A31,INT_1:52;
    then reconsider i1 as Element of NAT by INT_1:16;
    i1 < i1+1 by NAT_1:13;
    then vol(A)/a < 1*(i1+1) by INT_1:52,XXREAL_0:2;
    then
A32: vol(A)/(i1+1) < 1*a by A29,REAL_2:193;
    vol(A)/(i1+1) >= 0 by A30,REAL_2:125;
    then
A33: vol(A)/(i1+1)-0 = abs(vol(A)/(i1+1)-0) by ABSVALUE:def 1;
    (delta T).i1 = vol(A)/(i1+1) by A6;
    hence thesis by A32,A33;
  end;
A34: for a be real number st 0<a ex i be Element of NAT st
  for j be Element of NAT st i <= j holds abs((delta T).j-0)<a
  proof
    let a be real number;
A35: a is Real by XREAL_0:def 1;
    assume 0<a;
    then consider i be Element of NAT such that
A36: abs((delta T).i-0)<a by A28,A35;
    (delta T).i = delta(T.i) by INTEGRA2:def 3;
    then (delta T).i >= 0 by INTEGRA3:8;
    then
A37: (delta T).i < a by A36,ABSVALUE:def 1;
    now
      let j be Element of NAT;
      assume i <= j;
      then
A38:  (delta T).j <= (delta T).i by A26;
      (delta T).j = delta(T.j) by INTEGRA2:def 3;
      then (delta T).j < a & (delta T).j >= 0 by A37,A38,INTEGRA3:8,XXREAL_0:2;
      hence abs((delta T).j-0)<a by ABSVALUE:def 1;
    end;
    hence thesis;
  end;
  then
A39: delta T is convergent by SEQ_2:def 6;
  then lim delta(T)=0 by A34,SEQ_2:def 7;
  hence thesis by A5,A39;
end;

Lm2: a <= b & [' a,b '] c= dom f &
f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
c in [' a,b '] & b <> c implies
integral(f,a,b) = integral(f,a,c) + integral(f,c,b) &
f is_integrable_on ['a,c '] & f is_integrable_on ['c,b ']
proof
  assume that
A1: a <= b & [' a,b '] c= dom f & f is_integrable_on [' a,b '] &
  f is_bounded_on [' a,b '] & c in [' a,b '] & b <> c;
A2: [' a,b '] = [.a,b.] by A1,INTEGRA5:def 4;
  then
A3: a <= c & c <= b by A1,RCOMP_1:48;
  then
A4: [' a,c '] = [.a,c.] & [' c,b '] = [.c,b.] by INTEGRA5:def 4;
  then
A5: [' a,c '] c= [' a,b '] & [' c,b '] c= [' a,b '] by A2,A3,RCOMP_1:49;
  [.a,b.] = [.inf([.a,b.]), sup([.a,b.]).] &
  [.c,b.] = [.inf([.c,b.]), sup([.c,b.]).] &
  [.a,c.] = [.inf([.a,c.]), sup([.a,c.]).] by A2,A4,INTEGRA1:5;
  then
A6: inf [' a,b '] = a & sup [' a,b '] = b &
  inf [' c,b '] = c & sup [' c,b '] = b &
  inf [' a,c '] = a & sup [' a,c '] = c by A2,A4,INTEGRA1:6;
  set FAB = f||[' a,b '], FAC = f||[' a,c '], FCB = f||[' c,b '];
A7: FAB is Function of [' a,b '],REAL & FAC is Function of [' a,c '],REAL &
  FCB is Function of [' c,b '],REAL by A1,A5,Lm1,XBOOLE_1:1;
  FAB is_integrable_on [' a,b '] by A1,INTEGRA5:def 2;
  then
A8: FAB is_upper_integrable_on [' a,b '] &
  FAB is_lower_integrable_on [' a,b '] &
  upper_integral FAB = lower_integral FAB by INTEGRA1:def 17;
A9: f is_bounded_on [' a,c '] &
  f is_bounded_on [' c,b '] by A1,A5,RFUNCT_1:91;
  then
A10: FAC is_bounded_on [' a,c '] & FCB is_bounded_on [' c,b '] by INTEGRA5: 9;
  consider T1 be DivSequence of [' a,c '] such that
A11: delta T1 is convergent & lim delta T1 = 0 by INTEGRA4:11;
  ex PS2 be DivSequence of [' c,b '] st
  delta PS2 is convergent & lim delta PS2 = 0 & ex K be Element of NAT st
  for i be Element of NAT st K <= i
  ex S2i be DivisionPoint of [' c,b '] st S2i=PS2.i & 2 <= len S2i
  proof
    c < b by A1,A3,REAL_1:def 5;
    then vol([' c,b '] ) > 0 by A6,XREAL_1:52;
    then consider T being DivSequence of [' c,b '] such that
A12: delta T is convergent & lim delta(T)=0 & for n be Element of NAT holds
    ex Tn being Element of divs [' c,b '] st
    Tn divide_into_equal n+1 & T.n =Tn by Th16;
    take T;
    now
      let i be Element of NAT;
      assume
A13:  2 <= i;
      consider Tn being Element of divs [' c,b '] such that
A14:  Tn divide_into_equal i+1 & T.i =Tn by A12;
      reconsider Tn as DivisionPoint of [' c,b '];
      take Tn;
      thus Tn=T.i by A14;
      len Tn = i+1 by A14,INTEGRA4:def 1;
      then i <= len Tn by NAT_1:11;
      hence 2 <= len Tn by A13,XXREAL_0:2;
    end;
    hence thesis by A12;
  end;
  then consider PS2 be DivSequence of [' c,b '] such that
A15: delta PS2 is convergent & lim delta PS2 = 0 & ex K be Element of NAT st
  for i be Element of NAT st K <= i
  ex S2i be DivisionPoint of [' c,b '] st S2i = PS2.i & 2 <= len S2i;
  consider K be Element of NAT such that
A16: for i be Element of NAT st K <= i
  ex S2i be DivisionPoint of [' c,b '] st S2i=PS2.i & 2 <= len S2i by A15;
  defpred Q[set,set] means
  ex n being Element of NAT, e being Element of divs [' c,b '] st
  n = $1 & e = $2 & e = PS2.(n+K);
A17: for n be Element of NAT ex D being Element of divs [' c,b '] st Q[n,D]
  proof
    let n be Element of NAT;
    reconsider D =PS2.(n+K) as Element of divs [' c,b '];
    take D;
    thus thesis;
  end;
  consider S2 being Function of NAT,divs [' c,b '] such that
A18: for n be Element of NAT holds Q[n,S2.n] from FUNCT_2:sch 3(A17);
  reconsider S2 as DivSequence of [' c,b '];
A19: (for i be Element of NAT holds
  ex S2i be DivisionPoint of [' c,b '] st S2i=S2.i & 2 <= len S2i) &
  delta S2 is convergent & lim delta(S2)=0
  proof
A20: now
      let i be Element of NAT;
A21:  K <= i+K by NAT_1:11;
      ex n being Element of NAT,e being Element of divs ['c,b '] st
      n = i & e =S2.i & e = PS2.(n+K) by A18;
      hence ex S2i be DivisionPoint of [' c,b '] st S2i=S2.i & 2 <= len S2i
      by A16,A21;
    end;
    delta S2 is convergent & lim delta(S2)=0
    proof
A22:  now
        let e be real number;
        assume 0 < e;
        then consider m be Element of NAT such that
A23:    for n be Element of NAT st m <= n holds abs(delta(PS2).n -0 ) < e
        by A15,SEQ_2:def 7;
        take m;
        hereby
          let n be Element of NAT;
          assume
A24:      m <=n;
          n <=n+K by NAT_1:11;
          then m <=n+K by A24,XXREAL_0:2;
          then abs(delta(PS2).(n+K) -0 ) < e by A23;
          then
A25:      abs(delta(PS2.(n+K)) -0 ) < e by INTEGRA2:def 3;
          ex k being Element of NAT, e1 being Element of divs [' c,b '] st
          k = n & e1 =S2.n & e1 = PS2.(k+K) by A18;
          hence abs(delta(S2).n -0) < e by A25,INTEGRA2:def 3;
        end;
      end;
      hence delta S2 is convergent by SEQ_2:def 6;
      hence lim delta S2 =0 by A22,SEQ_2:def 7;
    end;
    hence thesis by A20;
  end;
  defpred P1[Element of NAT, set] means
  ex S be DivisionPoint of [' c,b '] st S = S2.$1 & $2 = S/^1;
A26: for i be Element of NAT ex T be Element of divs [' c,b '] st P1[i,T]
  proof
    let i be Element of NAT;
    consider S be DivisionPoint of [' c,b '] such that
A27: S=S2.i & 2 <= len S by A19;
    take T=S/^1;
A28: S is non empty increasing FinSequence of REAL &
    rng S c= [' c,b '] & S.(len S) = sup [' c,b '] by INTEGRA1:def 2;
A29: 1 <= len S by A27,XXREAL_0:2;
    2-1 <= len S -1 by A27,REAL_1:92;
    then
A30: 1 <= len T by A29,RFINSEQ:def 2;
    then
A31: T is non empty increasing FinSequence of REAL
    by A29,FINSEQ_1:25,INTEGRA1:36;
    rng T c= rng S by FINSEQ_5:36;
    then
A32: rng T c= [' c,b '] by A28,XBOOLE_1:1;
    len T in Seg (len T) by A30;
    then len T in dom T by FINSEQ_1:def 3;
    then T.(len T) =S.(len T+ 1) by A29,RFINSEQ:def 2;
    then T.(len T) =S.(len S -1+ 1) by A29,RFINSEQ:def 2;
    then T.(len T) = sup [' c,b '] by INTEGRA1:def 2;
    then T is DivisionPoint of [' c,b '] by A31,A32,INTEGRA1:def 2;
    hence thesis by A27,INTEGRA1:def 4;
  end;
  consider T2 be DivSequence of [' c,b '] such that
A33: for i be Element of NAT holds P1[i,T2.i] from FUNCT_2:sch 3(A26);
A34: for n be Element of NAT holds
  ex D be DivisionPoint of [' c,b '] st D = T2.n & 1 <= len D
  proof
    let n be Element of NAT;
    consider E be DivisionPoint of [' c,b '] such that
A35: E=S2.n & T2.n=E/^1 by A33;
    reconsider D=T2.n as DivisionPoint of [' c,b '] by INTEGRA1:def 3;
    take D;
    ex E1 be DivisionPoint of [' c,b '] st E1=S2.n & 2 <= len E1 by A19;
    then 1 <= len E & 2-1 <= len E -1 by A35,REAL_1:92,XXREAL_0:2;
    hence thesis by A35,RFINSEQ:def 2;
  end;
A36: for n be Element of NAT holds ex D be DivisionPoint of [' c,b '] st
  D = T2.n & (for i be Element of NAT st i in dom D holds c < D.i)
  proof
    let n be Element of NAT;
    consider E be DivisionPoint of [' c,b '] such that
A37: E=S2.n & T2.n=E/^1 by A33;
    consider E1 be DivisionPoint of [' c,b '] such that
A38: E1=S2.n & 2 <= len E1 by A19;
    reconsider D=T2.n as DivisionPoint of [' c,b '] by INTEGRA1:def 3;
    take D;
A39: 1 <= len E by A37,A38,XXREAL_0:2;
    then
A40: len D = len E -1 by A37,RFINSEQ:def 2;
    2-1 <= len E -1 by A37,A38,REAL_1:92;
    then 1 in Seg len D by A40;
    then
A41: 1 in dom D by FINSEQ_1:def 3;
    then
A42: D.1 = E.(1+1) by A37,A39,RFINSEQ:def 2;
A43: rng E c=[' c,b '] by INTEGRA1:def 2;
    1 in Seg len E & 2 in Seg len E by A37,A38,A39;
    then
A44: 1 in dom E & 2 in dom E by FINSEQ_1:def 3;
    then E.1 in rng E by FUNCT_1:def 5;
    then
A45: c <= E.1 & E.1 <= b by A4,A43,RCOMP_1:48;
A46: E.1 < E.2 by A44,GOBOARD1:def 1;
    then
A47: c < D.1 by A42,A45,XXREAL_0:2;
    now
      let i be Element of NAT;
      assume
A48:  i in dom D;
      then i in Seg len D by FINSEQ_1:def 3;
      then
A49:  1 <=i by FINSEQ_1:3;
      thus now
        assume i <> 1;
        then 1 < i by A49,REAL_1:def 5;
        then D.1 < D.i by A41,A48,GOBOARD1:def 1;
        hence c < D.i by A47,XXREAL_0:2;
      end;
      hence c < D.i by A42,A45,A46,XXREAL_0:2;
    end;
    hence thesis;
  end;
  set XCB = chi(['c,b '],['c,b ']);
A50: delta T2 is convergent & lim delta T2 =0
  proof
A51: now
      let e be real number;
      assume e > 0;
      then
A52:  0 < e/2 & e/2 < e by XREAL_1:217,218;
      then consider m be Element of NAT such that
A53:  for n be Element of NAT st m <=n holds
      abs( delta(S2).n - 0) < e/2 by A19,SEQ_2:def 7;
      take m;
      now
        let n be Element of NAT;
A54:    0 <= delta(S2.n) & 0 <= delta(T2.n) by INTEGRA3:8;
        assume m <=n;
        then abs( delta(S2).n - 0) < e/2 by A53;
        then abs delta(S2.n) < e/2 by INTEGRA2:def 3;
        then
A55:    max rng upper_volume(XCB,S2.n) < e/2 by A54,ABSVALUE:def 1;
A56:    for y be real number st y in rng upper_volume(XCB,T2.n) holds y < e
        proof
          let y be real number;
          assume y in rng upper_volume(XCB,T2.n);
          then consider x be set such that
A57:      x in dom upper_volume(XCB,T2.n) & y=(upper_volume(XCB,T2.n)).x
          by FUNCT_1:def 5;
A58:      x in Seg len upper_volume(XCB,T2.n) by A57,FINSEQ_1:def 3;
          reconsider i=x as Element of NAT by A57;
          reconsider D=T2.n as DivisionPoint of [' c,b '] by INTEGRA1:def 3;
A59:      len (upper_volume(XCB,T2.n)) = len D by INTEGRA1:def 7;
          then
A60:      y=( sup rng(XCB|divset(T2.n,i)) ) * vol divset(T2.n,i)
          by A57,A58,INTEGRA1:def 7;
          consider E be DivisionPoint of [' c,b '] such that
A61:      E=S2.n & T2.n=E/^1 by A33;
          consider E1 be DivisionPoint of [' c,b '] such that
A62:      E1=S2.n & 2 <= len E1 by A19;
A63:      1 <= len E1 by A62,XXREAL_0:2;
          then
A64:      len D = len E -1 by A61,A62,RFINSEQ:def 2;
          set i1=i+1;
A65:      now
            assume
A66:        i = 1;
            then
A67:        1 in dom D by A58,A59,FINSEQ_1:def 3;
A68:        2 in Seg len E by A61,A62;
            then
A69:        2 in dom E by FINSEQ_1:def 3;
A70:        divset((T2.n),i)

= [. inf(divset((T2.n),1)),sup(divset((T2.n),1)) .] by A66,INTEGRA1:5

.= [. inf([' c,b ']),sup(divset((T2.n),1)) .] by A67,INTEGRA1:def 5
              .= [. inf([' c,b ']),D.1 .] by A67,INTEGRA1:def 5
              .= [. inf([' c,b ']),E.(1+1) .] by A61,A62,A63,A67,RFINSEQ:def 2
              .= [. inf([' c,b ']),sup(divset((S2.n),2)) .]
            by A61,A69,INTEGRA1:def 5;
            then [. inf([' c,b ']),sup(divset((S2.n),2)) .]
            = [.inf([. inf([' c,b ']),sup(divset((S2.n),2)) .]),
            sup([. inf([' c,b ']),sup(divset((S2.n),2)) .]).] by INTEGRA1:5;
            then
A71:        inf([' c,b '])=inf([. inf([' c,b ']),sup(divset(( S2.n),2)) .]) &

sup(divset((S2.n),2)) =sup([. inf([' c,b ']),sup(divset((S2.n),2)) .])
            by A70,INTEGRA1:6;
            len(upper_volume(XCB,S2.n)) = len E by A61,INTEGRA1:def 7;
            then 2 in Seg len (upper_volume(XCB,S2.n)) by A61,A62;
            then 2 in dom upper_volume(XCB,S2.n) by FINSEQ_1:def 3;
            then (upper_volume(XCB,S2.n)).2 in rng upper_volume(XCB,S2.n)
            by FUNCT_1:def 5;
            then (upper_volume(XCB,S2.n)).2 <= max rng upper_volume(XCB,S2.n)
            by PRE_CIRC:def 1;
            then
A72:        (upper_volume(XCB,S2.n)).2 < e/2 by A55,XXREAL_0:2;
            len(upper_volume(XCB,S2.n))= len D + 1 by A61,A64,INTEGRA1:def 7;
            then len(upper_volume(XCB,S2.n))
            = len (upper_volume(XCB,T2.n)) + 1 by INTEGRA1:def 7;
            then 1 in Seg len(upper_volume(XCB,S2.n)) by A58,A66,FINSEQ_2:9;
            then 1 in dom upper_volume(XCB,S2.n) by FINSEQ_1:def 3;
            then (upper_volume(XCB,S2.n)).1 in rng upper_volume(XCB,S2.n)
            by FUNCT_1:def 5;
            then (upper_volume(XCB,S2.n)).1 <= max rng upper_volume(XCB,S2.n)
            by PRE_CIRC:def 1;
            then
A73:        (upper_volume(XCB,S2.n)).1 < e/2 by A55,XXREAL_0:2;
A74:        1 in Seg len E by A61,A62,A63;
            then
A75:        1 in dom E by FINSEQ_1:def 3;
            divset(S2.n,1) = [. inf divset(S2.n,1),sup divset(S2.n,1) .]
            by INTEGRA1:5;
            then
A76:        divset(S2.n,1) = [. inf([' c,b ']),sup divset(S2.n,1) .]
            by A61,A75,INTEGRA1:def 5;
            then
A77:        [. inf([' c,b ']),sup divset(S2.n,1) .]
            = [.inf([. inf([' c,b ']),sup divset(S2.n,1) .]),
            sup([. inf([' c,b ']),sup divset(S2.n,1) .]).] by INTEGRA1:5;
            divset(S2.n,2)
            = [. inf divset(S2.n,2),sup divset(S2.n,2) .] by INTEGRA1:5;
            then divset(S2.n,2)
            = [. E.(2-1),sup divset(S2.n,2) .] by A61,A69,INTEGRA1:def 5;
            then
A78:        divset(S2.n,2) = [. sup divset(S2.n,1),sup divset( S2.n,2) .]
            by A61,A75,INTEGRA1:def 5;
            then
A79:        [. sup divset(S2.n,1) ,sup divset(S2.n,2) .]
            = [.inf([. sup divset(S2.n,1) ,sup divset(S2.n,2) .]),
            sup([. sup divset(S2.n,1) ,sup divset(S2.n,2) .]).] by INTEGRA1:5;
            y =vol divset(T2.n,1) by A57,A58,A59,A66,INTEGRA1:22;
            then y =(sup divset(S2.n,2)-sup divset(S2.n,1))
            + (sup([. inf([' c,b ']),sup divset(S2.n,1) .])-inf([' c,b ']))
            by A66,A70,A71,A76;

then y =(sup divset(S2.n,2) - sup divset(S2.n,1)) + vol divset(S2.n,1)
            by A76,A77,INTEGRA1:6;

then y = vol divset(S2.n,2) + vol divset(S2.n,1) by A78,A79,INTEGRA1:6;
            then y = (upper_volume(XCB,S2.n)).2 + vol divset(S2.n,1)
            by A61,A68,INTEGRA1:22;
            then y = (upper_volume(XCB,S2.n)).2 + (upper_volume(XCB,S2.n)).1
            by A61,A74,INTEGRA1:22;
            then y < (e/2)+(e/2) by A72,A73,XREAL_1:10;
            hence y < e;
          end;
          now
            assume
A80:        i <> 1;
            i in Seg len D by A57,A59,FINSEQ_1:def 3;
            then 1 <= i by FINSEQ_1:3;
            then
A81:        1 < i by A80,REAL_1:def 5;
            then
A82:        i-1 in Seg len D by A58,A59,FINSEQ_3:13;
            then
A83:        i-1 in dom D by FINSEQ_1:def 3;
            reconsider i' = i-1 as Element of NAT by A82;
            1+1 < i+1 by A81,XREAL_1:10;
            then
A84:        i1 <> 1;
            Seg (len D + 1) = Seg len E by A64;
            then i1 in Seg len E by A58,A59,FINSEQ_1:81;
            then
A85:        i1 in dom E by FINSEQ_1:def 3;
A86:        i in dom D by A58,A59,FINSEQ_1:def 3;
A87:        divset(T2.n,i)
            = [. inf divset(T2.n,i),sup divset(T2.n,i) .] by INTEGRA1:5
              .= [. D.(i-1),sup divset(T2.n,i) .] by A80,A86,INTEGRA1:def 5
              .= [. D.(i-1),D.i .] by A80,A86,INTEGRA1:def 5
              .= [. E.(i'+1),D.i .] by A61,A62,A63,A83,RFINSEQ:def 2
              .= [. E.((i-1)+1),E.(i+1) .] by A61,A62,A63,A86,RFINSEQ:def 2

.= [. E.(i1-1),sup divset(S2.n,i1) .] by A61,A84,A85,INTEGRA1:def 5
              .= [. inf divset(S2.n,i1),sup divset(S2.n,i1) .]
            by A61,A84,A85,INTEGRA1:def 5
              .= divset(S2.n,i1) by INTEGRA1:5;
            len upper_volume(XCB,S2.n) = len D +1 by A61,A64,INTEGRA1:def 7;
            then len upper_volume(XCB,S2.n) = len (upper_volume(XCB,T2.n)) + 1
            by INTEGRA1:def 7;
            then
A88:        i1 in Seg len (upper_volume(XCB,S2.n)) by A58, FINSEQ_1:81;
            len upper_volume(XCB,S2.n) = len E by A61,INTEGRA1:def 7;
            then
A89:        y = (upper_volume(XCB,S2.n)).i1 by A60,A61,A87,A88
            ,INTEGRA1:def 7;
            i1 in dom upper_volume(XCB,S2.n) by A88,FINSEQ_1:def 3;
            then y in rng upper_volume(XCB,S2.n) by A89,FUNCT_1:def 5;
            then y <= max rng upper_volume(XCB,S2.n) by PRE_CIRC:def 1;
            then y < e/2 by A55,XXREAL_0:2;
            hence y < e by A52,XXREAL_0:2;
          end;
          hence y < e by A65;
        end;
        max rng upper_volume(XCB,T2.n) in rng upper_volume(XCB,T2.n)
        by PRE_CIRC:def 1;
        then delta(T2.n) < e by A56;
        then abs delta(T2.n) < e by A54,ABSVALUE:def 1;
        hence abs(delta(T2).n-0) < e by INTEGRA2:def 3;
      end;
      hence for n be Element of NAT st m <=n holds abs( delta(T2).n - 0) < e;
    end;
    hence delta T2 is convergent by SEQ_2:def 6;
    hence lim delta T2 =0 by A51,SEQ_2:def 7;
  end;
  defpred P2[Element of NAT,set] means
  ex S1 be DivisionPoint of [' a,c '], S2 be DivisionPoint of [' c,b '] st
  S1=T1.$1 & S2=T2.$1 & $2=S1^S2;
A90: for i be Element of NAT ex T be Element of divs [' a,b '] st P2[i,T]
  proof
    let i0 be Element of NAT;
    reconsider S1=T1.i0 as DivisionPoint of [' a,c '] by INTEGRA1:def 3;
    reconsider S2=T2.i0 as DivisionPoint of [' c,b '] by INTEGRA1:def 3;
    take T=S1^S2;
A91: S1 is non empty increasing FinSequence of REAL &
    rng S1 c= [' a,c '] & S1.(len S1) = sup [' a,c '] &
    S2 is non empty increasing FinSequence of REAL &
    rng S2 c= [' c,b '] & S2.(len S2) = sup [' c,b '] by INTEGRA1:def 2;
    then rng S1 \/ rng S2 c= [' a,c '] \/ [' c,b '] by XBOOLE_1:13;
    then rng S1 \/ rng S2 c=[.a,b.] by A3,A4,RCOMP_1:50;
    then
A92: rng T c= [' a,b '] by A2,FINSEQ_1:44;
    ex D be DivisionPoint of [' c,b '] st D = T2.i0 & 1 <= len D by A34;
    then len S2 in Seg len S2;
    then
A93: len S2 in dom S2 by FINSEQ_1:def 3;
A94: T.(len T) = T.(len S1 + len S2) by FINSEQ_1:35
      .= S2.(len S2) by A93,FINSEQ_1:def 7
      .= sup [' a,b '] by A6,INTEGRA1:def 2;
    now
      let i,j be Element of NAT;
      assume
A95:  i in dom T & j in dom T & i < j;
      then i in Seg len T & j in Seg len T by FINSEQ_1:def 3;
      then
A96:  1 <= i & i <= len T & 1 <= j & j <= len T by FINSEQ_1:3;
A97:  now
        assume
A98:    j <= len S1;
        then i <= len S1 by A95,XXREAL_0:2;
        then i in Seg len S1 & j in Seg len S1 by A96,A98;
        then
A99:    i in dom S1 & j in dom S1 by FINSEQ_1:def 3;
        then T.i = S1.i & T.j = S1.j by FINSEQ_1:def 7;
        hence T.i < T.j by A95,A99,GOBOARD1:def 1;
      end;
      now
        assume
A100:   j > len S1;
        then
A101:   T.j = S2.(j-len S1) by A96,FINSEQ_1:37;
A102:   len S1 + 1 <=j by A100,NAT_1:13;
        then consider m be Nat such that
A103:   (len S1 + 1)+m=j by NAT_1:10;
        reconsider m as Element of NAT by ORDINAL1:def 13;
        1+m = j - len S1 by A103;
        then
A104:   1 <= 1+m by A102,XREAL_1:21;
        j <= len S1 + len S2 by A96,FINSEQ_1:35;
        then j-len S1 <= len S2 by XREAL_1:22;
        then 1+m in Seg len S2 by A103,A104;
        then
A105:   j - len S1 in dom S2 by A103,FINSEQ_1:def 3;
A106:   now
          assume i <= len S1;
          then i in Seg len S1 by A96;
          then
A107:     i in dom S1 by FINSEQ_1:def 3;
          then T.i = S1.i by FINSEQ_1:def 7;
          then T.i in rng S1 by A107,FUNCT_1:def 5;
          then
A108:     a <= T.i & T.i <= c by A4,A91,RCOMP_1:48;
          ex DD be DivisionPoint of [' c,b '] st DD = T2.i0 &
          for k be Element of NAT st k in dom DD holds c < DD.k by A36;
          hence T.i < T.j by A101,A105,A108,XXREAL_0:2;
        end;
        now
          assume
A109:     i > len S1;
          then
A110:     T.i = S2.(i-len S1) by A96,FINSEQ_1:37;
A111:     i - len S1 < j -len S1 by A95,REAL_1:92;
A112:     len S1 + 1 <=i by A109,NAT_1:13;
          then consider m be Nat such that
A113:     (len S1 + 1)+m=i by NAT_1:10;
          reconsider m as Element of NAT by ORDINAL1:def 13;
          1+m = i - len S1 by A113;
          then
A114:     1 <= 1+m by A112,XREAL_1:21;
          i <= len S1 + len S2 by A96,FINSEQ_1:35;
          then i-len S1 <= len S2 by XREAL_1:22;
          then 1+m in Seg len S2 by A113,A114;
          then i - len S1 in dom S2 by A113,FINSEQ_1:def 3;
          hence T.i < T.j by A101,A105,A110,A111,GOBOARD1:def 1;
        end;
        hence T.i < T.j by A106;
      end; ::CA2 end
      hence T.i < T.j by A97;
    end;
    then T is non empty increasing FinSequence of REAL by GOBOARD1:def 1;
    then T is DivisionPoint of [' a,b '] by A92,A94,INTEGRA1:def 2;
    hence thesis by INTEGRA1:def 4;
  end;
  consider T be DivSequence of [' a,b '] such that
A115: for i be Element of NAT holds P2[i,T.i] from FUNCT_2:sch 3(A90);
A116: for i,k be Element of NAT, S0 be DivisionPoint of [' a,c '] st
  S0=T1.i & k in Seg len S0 holds divset(T1.i,k) c= [' a,c ']
  proof
    let i,k be Element of NAT, S0 be DivisionPoint of [' a,c '];
    assume
A117: S0=T1.i & k in Seg (len S0);
    then k in dom S0 by FINSEQ_1:def 3;
    hence thesis by A117,INTEGRA1:10;
  end;
A118: for i, k be Element of NAT, S0 be DivisionPoint of [' c,b '] st
  S0=T2.i & k in Seg len S0 holds divset(T2.i,k) c= [' c,b ']
  proof
    let i,k be Element of NAT, S0 be DivisionPoint of [' c,b '];
    assume
A119: S0=T2.i & k in Seg len S0;
    then k in dom S0 by FINSEQ_1:def 3;
    hence thesis by A119,INTEGRA1:10;
  end;
A120: for i, k be Element of NAT, S0 be DivisionPoint of [' a,c '] st
  S0=T1.i & k in Seg len S0 holds divset(T.i,k) = divset(T1.i,k)
  proof
    let i,k be Element of NAT, S0 be DivisionPoint of [' a,c '];
    assume
A121: S0=T1.i;
    assume
A122: k in Seg len S0;
    consider S1 be DivisionPoint of [' a,c '],
    S2 be DivisionPoint of [' c,b '] such that
A123: S1=T1.i & S2=T2.i & T.i=S1^S2 by A115;
    reconsider S=T.i as DivisionPoint of [' a,b '] by INTEGRA1:def 3;
A124: k in dom S1 by A121,A122,A123,FINSEQ_1:def 3;
    len S = len S1 + len S2 by A123,FINSEQ_1:35;
    then len S1 <= len S by NAT_1:11;
    then Seg len S1 c= Seg len S by FINSEQ_1:7;
    then k in Seg len S by A121,A122,A123;
    then
A125: k in dom S by FINSEQ_1:def 3;
A126: divset(T.i,k) =[.inf divset(T.i,k),sup divset(T.i,k).] &
    divset(T1.i,k) = [. inf divset(T1.i,k),sup divset(T1.i,k).] by INTEGRA1:5;
A127: now
      assume
A128: k=1;
      hence divset(T.i,k)
      =[. inf[' a,b '],sup divset(T.i,k) .] by A125,A126,INTEGRA1:def 5
        .=[. inf[' a,b '],S.k .] by A125,A128,INTEGRA1:def 5
        .=[. inf[' a,b '],S1.k .] by A123,A124,FINSEQ_1:def 7
        .=[. inf divset(T1.i,k),S1.1 .] by A6,A123,A124,A128,INTEGRA1:def 5
        .= divset(T1.i,k) by A123,A124,A126,A128,INTEGRA1:def 5;
    end;
    now
      assume
A129: k <> 1;
A130: 1 <=k & k <= len S1 by A121,A122,A123,FINSEQ_1:3;
      k-1 <= k -0 by XREAL_1:12;
      then
A131: k-1 <= len S1 by A130,XXREAL_0:2;
A132: 1 < k by A129,A130,REAL_1:def 5;
      then 1+1 <= k by NAT_1:13;
      then
A133: 2-1 <= k-1 by XREAL_1:11;
A134: k-1 is Element of NAT by A132,NAT_1:20;
      then k-1 in Seg len S1 by A131,A133;
      then
A135: k-1 in dom S1 by FINSEQ_1:def 3;
      thus divset(T.i,k)
      =[. S.(k-1),sup divset(T.i,k) .] by A125,A126,A129,INTEGRA1:def 5
        .=[. S.(k-1),S.k .] by A125,A129,INTEGRA1:def 5
        .=[. S.(k-1),S1.k .] by A123,A124,FINSEQ_1:def 7
        .=[. S1.(k-1),S1.k .] by A123,A134,A135,FINSEQ_1:def 7
        .=[. inf divset(T1.i,k),S1.k .] by A123,A124,A129,INTEGRA1:def 5
        .= divset(T1.i,k) by A123,A124,A126,A129,INTEGRA1:def 5;
    end;
    hence divset(T.i,k) = divset(T1.i,k) by A127;
  end;
A136: for i, k be Element of NAT, S01 be DivisionPoint of [' a,c '],
  S02 be DivisionPoint of [' c,b '] st
  S01=T1.i & S02=T2.i & k in Seg len S02 holds
  divset(T.i,(len S01 + k)) = divset(T2.i,k)
  proof
    let i,k be Element of NAT, S01 be DivisionPoint of [' a,c '],
    S02 be DivisionPoint of [' c,b '];
    assume
A137: S01=T1.i & S02=T2.i;
    assume
A138: k in Seg len S02;
    consider S1 be DivisionPoint of [' a,c '],
    S2 be DivisionPoint of [' c,b '] such that
A139: S1=T1.i & S2=T2.i & T.i = S1^S2 by A115;
    reconsider S=T.i as DivisionPoint of [' a,b '] by INTEGRA1:def 3;
A140: k in dom S2 by A137,A138,A139,FINSEQ_1:def 3;
    then
A141: len S1 + k in dom S by A139,FINSEQ_1:41;
A142: divset(T.i,(len S1 + k))
    =[.inf divset(T.i,(len S1 + k)), sup divset(T.i,(len S1 + k)).] &
    divset(T2.i,k) =[. inf divset(T2.i,k), sup divset(T2.i,k).] by INTEGRA1:5;
A143: now
      assume
A144: k=1;
      then
A145: len S1 + k <> 1 by FINSEQ_1:25;
      len S1 <> 0 by FINSEQ_1:25;
      then len S1 in Seg len S1 by FINSEQ_1:5;
      then
A146: len S1 in dom S1 by FINSEQ_1:def 3;
      thus divset(T.i,(len S1 + k))
      =[. S.(len S1 + k-1),sup divset(T.i,len S1 + k) .]
      by A141,A142,A145,INTEGRA1:def 5
        .=[. S1.(len S1),sup divset(T.i,len S1 +k) .] by A139,A144,A146,
      FINSEQ_1:def 7
        .=[. sup ([' a,c ']), sup divset(T.i,len S1 + k) .] by INTEGRA1:def 2
        .=[. inf ([' c,b ']), S.(len S1 + k) .] by A6,A141,A145,INTEGRA1:def 5
        .=[. inf [' c,b '], S2.k .] by A139,A140,FINSEQ_1:def 7
        .=[. inf divset(T2.i,k), S2.1 .] by A139,A140,A144,INTEGRA1:def 5
        .= divset(T2.i,k) by A139,A140,A142,A144,INTEGRA1:def 5;
    end;
    now
      assume
A147: k <> 1;
A148: 1 <=k & k <= len S2 by A137,A138,A139,FINSEQ_1:3;
      then
A149: 1 < k by A147,REAL_1:def 5;
      then
A150: 1+0 < k + len S1 by XREAL_1:10;
      k-1 <= k -0 by XREAL_1:12;
      then
A151: k-1 <= len S2 by A148,XXREAL_0:2;
      1+1 <= k by A149,NAT_1:13;
      then
A152: 2-1 <= k-1 by XREAL_1:11;
A153: k-1 is Element of NAT by A149,NAT_1:20;
      then k-1 in Seg len S2 by A151,A152;
      then
A154: k-1 in dom S2 by FINSEQ_1:def 3; thus
      divset(T.i,(len S1 + k))
      =[. S.(len S1 + k-1),sup divset(T.i,len S1 + k) .]
      by A141,A142,A150,INTEGRA1:def 5
        .=[. S.(len S1 + k-1),S.(len S1 + k) .] by A141,A150,INTEGRA1:def 5
        .=[. S.(len S1 + (k-1)),S2.k .] by A139,A140,FINSEQ_1:def 7
        .=[. S2.(k-1),S2.k .] by A139,A153,A154,FINSEQ_1:def 7
        .=[. inf divset(T2.i,k), S2.k .] by A139,A140,A147,INTEGRA1:def 5
        .= divset(T2.i,k) by A139,A140,A142,A147,INTEGRA1:def 5;
    end;
    hence thesis by A137,A139,A143;
  end;
  set XAC = chi([' a,c '],[' a,c ']);
  set XAB = chi([' a,b '],[' a,b ']);
A155: delta T is convergent & lim delta(T)=0
  proof
A156: now
      let e be real number;
      assume
A157: 0 < e;
      then consider n1 be Element of NAT such that
A158: for m be Element of NAT st n1 <= m holds abs(delta(T1).m -0 ) < e
      by A11,SEQ_2:def 7;
      consider n2 be Element of NAT such that
A159: for m be Element of NAT st n2 <= m holds abs(delta(T2).m -0 ) < e
      by A50,A157,SEQ_2:def 7;
      reconsider n = max(n1,n2) as Element of NAT by XXREAL_0:16;
      take n;
      now
        let m be Element of NAT;
        assume
A160:   n <=m;
        n1 <= n by XXREAL_0:25;
        then n1 <= m by A160,XXREAL_0:2;
        then abs(delta(T1).m -0 ) < e by A158;
        then
A161:   abs(delta(T1.m) ) < e by INTEGRA2:def 3;
        0 <= delta(T1.m) by INTEGRA3:8;
        then
A162:   max rng upper_volume(XAC,T1.m) < e by A161,ABSVALUE:def 1;
        n2 <= n by XXREAL_0:25;
        then n2 <= m by A160,XXREAL_0:2;
        then abs(delta(T2).m -0 ) < e by A159;
        then
A163:   abs(delta(T2.m) ) < e by INTEGRA2:def 3;
        0 <= delta(T2.m) by INTEGRA3:8;
        then
A164:   max rng upper_volume(XCB,T2.m) < e by A163,ABSVALUE:def 1;
A165:   for r be real number st r in rng upper_volume(XAB,T.m) holds r < e
        proof
          let y be real number;
          assume y in rng upper_volume(XAB,T.m);
          then consider x be set such that
A166:     x in dom upper_volume(XAB,T.m) &
          y = (upper_volume(XAB,T.m)).x by FUNCT_1:def 5;
A167:     x in Seg len (upper_volume(XAB,T.m)) by A166,FINSEQ_1:def 3;
          reconsider i=x as Element of NAT by A166;
          reconsider Tm=T.m as DivisionPoint of [' a,b '] by INTEGRA1:def 3;
A168:     len(upper_volume(XAB,T.m)) =len Tm by INTEGRA1:def 7;
          then
A169:     y=(sup rng(XAB|divset(T.m,i))) * vol divset(T.m,i)
          by A166,A167,INTEGRA1:def 7;
          consider S1 be DivisionPoint of [' a,c '],
          S2 be DivisionPoint of [' c,b '] such that
A170:     S1=T1.m & S2=T2.m & T.m = S1^S2 by A115;
A171:     len Tm = len S1 + len S2 by A170,FINSEQ_1:35;
A172:     1 <= i & i <= len Tm by A167,A168,FINSEQ_1:3;
          per cases;
          suppose i <= len S1;
            then
A173:       i in Seg len S1 by A172;
            then
A174:       divset(T1.m,i) = divset(T.m,i) by A120,A170;
A175:       divset(T1.m,i) c= [' a,c '] by A116,A170,A173;
            then divset(T1.m,i) c= [' a,b '] by A5,XBOOLE_1:1;
            then XAC|divset(T1.m,i) = XAB|divset(T.m,i) by A174,A175,Th4;
            then
A176:       y=(upper_volume(XAC,T1.m)).i by A169,A170,A173, A174,
            INTEGRA1:def 7;
            len (upper_volume(XAC,T1.m)) = len S1 by A170,INTEGRA1:def 7;
            then i in dom (upper_volume(XAC,T1.m)) by A173,FINSEQ_1:def 3;
            then y in rng upper_volume(XAC,T1.m) by A176,FUNCT_1:def 5;
            then y <= max rng upper_volume(XAC,T1.m) by PRE_CIRC:def 1;
            hence y < e by A162,XXREAL_0:2;
          end;
          suppose i > len S1;
            then
A177:       len S1 + 1 <=i by NAT_1:13;
            then consider k be Nat such that
A178:       (len S1 + 1)+k=i by NAT_1:10;
            reconsider k as Element of NAT by ORDINAL1:def 13;
            1+k = i - len S1 by A178;
            then
A179:       1 <= 1+k by A177,XREAL_1:21;
            set i1=1+k;
            i-len S1 <= len S2 by A171,A172,XREAL_1:22;
            then
A180:       1+k in Seg len S2 by A178,A179;
            then
A181:       divset(T2.m,i1) =divset(T.m,len S1 + i1) by A136, A170;
A182:       divset(T2.m,i1) c= [' c,b '] by A118,A170,A180;
            then divset(T2.m,i1) c= [' a,b '] by A5,XBOOLE_1:1;
            then y=(sup rng(XCB|divset(T2.m,i1))) * vol divset(T2.m,i1)
            by A169,A178,A181,A182,Th4;
            then
A183:       y=(upper_volume(XCB,T2.m)).i1 by A170,A180, INTEGRA1:def 7;
            len (upper_volume(XCB,T2.m)) = len S2 by A170,INTEGRA1:def 7;
            then i1 in dom (upper_volume(XCB,T2.m)) by A180,FINSEQ_1:def 3;
            then y in rng upper_volume(XCB,T2.m) by A183,FUNCT_1:def 5;
            then y <= max rng upper_volume(XCB,T2.m) by PRE_CIRC:def 1;
            hence y < e by A164,XXREAL_0:2;
          end;
        end;
        max rng upper_volume(XAB,T.m) in rng upper_volume(XAB,T.m)
        by PRE_CIRC:def 1;
        then
A184:   delta(T.m) < e by A165;
        0 <= delta(T.m) by INTEGRA3:8;
        then abs( delta(T.m) ) < e by A184,ABSVALUE:def 1;
        hence abs(delta(T).m -0 ) < e by INTEGRA2:def 3;
      end;
      hence for m be Element of NAT st n <= m holds abs(delta(T).m -0 ) < e;
    end;
    hence delta T is convergent by SEQ_2:def 6;
    hence thesis by A156,SEQ_2:def 7;
  end;
A185: upper_integral FAC + upper_integral FCB = upper_integral FAB
  proof
A186: for i be Element of NAT holds upper_volume(f||[' a,b '],T.i)
    = (upper_volume(FAC,T1.i))^(upper_volume(FCB,T2.i))
    proof
      let i be Element of NAT;
      reconsider F = upper_volume(FAB,T.i) as FinSequence of REAL;
      reconsider F1 = upper_volume(FAC,T1.i) as FinSequence of REAL;
      reconsider F2 = upper_volume(FCB,T2.i) as FinSequence of REAL;
      consider S1 be DivisionPoint of [' a,c '],
      S2 be DivisionPoint of [' c,b '] such that
A187: S1=T1.i & S2=T2.i & T.i =S1^S2 by A115;
      reconsider S=T.i as DivisionPoint of [' a,b '] by INTEGRA1:def 3;
A188: len F = len S by INTEGRA1:def 7
        .= len S1+ len S2 by A187,FINSEQ_1:35
        .= len F1 + len S2 by A187,INTEGRA1:def 7
        .= len F1 + len F2 by A187,INTEGRA1:def 7;
      then
A189: dom F = Seg (len F1 + len F2) by FINSEQ_1:def 3;
A190: now
        let k be Nat;
        len F1 <= len F by A188,NAT_1:11;
        then
A191:   Seg len F1 c= Seg len F by FINSEQ_1:7;
        assume k in dom F1;
        then k in Seg len F1 by FINSEQ_1:def 3;
        then
A192:   k in Seg len F & k in Seg(len S1) by A187,A191,INTEGRA1: def 7;
        then k in Seg(len S) by INTEGRA1:def 7;
        then
A193:   F.k = (sup rng(FAB|divset(T.i,k)))*vol divset(T.i,k) by
        INTEGRA1:def 7;
A194:   divset(T.i,k) = divset(T1.i,k) by A120,A187,A192;
        then
A195:   divset(T.i,k) c= [' a,c '] by A116,A187,A192;
        then divset(T.i,k) c= [' a,b '] by A5,XBOOLE_1:1;
        then F.k = (sup rng(FAC|divset(T1.i,k))) * vol divset(T1.i,k) by A193
        ,A194,A195,Th3;
        hence F.k = F1.k by A187,A192,INTEGRA1:def 7;
      end;
      now
        let k be Nat;
        assume
A196:   k in dom F2;
        then k in Seg len F2 by FINSEQ_1:def 3;
        then
A197:   1 <=k & k <= len F2 by FINSEQ_1:3;
        then
A198:   len F1 + k <= len F by A188,XREAL_1:8;
A199:   1 + len F1 <= k + len F1 by A197,XREAL_1:8;
        1 <= 1+ len F1 by NAT_1:11;
        then 1 <= k + len F1 by A199,XXREAL_0:2;
        then
A200:   k+ len F1 in Seg len F by A198;
        k in Seg len F2 by A196,FINSEQ_1:def 3;
        then
A201:   k in Seg len S2 by A187,INTEGRA1:def 7;
A202:   len F1= len S1 by A187,INTEGRA1:def 7;
        len F1 + k in Seg len S by A200,INTEGRA1:def 7;
        then
A203:   F.(len F1 + k)
        =(sup rng(FAB|divset(T.i,(len F1 + k)))) * vol divset(T.i,len F1 + k)
        by INTEGRA1:def 7;
A204:   divset(T.i,len F1 + k) = divset(T2.i,k) by A136,A187,A201,A202;
        then
A205:   divset(T.i,len F1 + k) c= [' c,b '] by A118,A187,A201;
        then divset(T.i,len F1 + k) c= [' a,b '] by A5,XBOOLE_1:1;
        then F.(len F1 + k)=(sup rng(FCB|divset(T2.i,k))) * vol divset(T2.i,k)
        by A203,A204,A205,Th3;
        hence F.(len F1 + k) =F2.k by A187,A201,INTEGRA1:def 7;
      end;
      hence thesis by A189,A190,FINSEQ_1:def 7;
    end;
A206: for i be Element of NAT holds Sum upper_volume(FAB,T.i)
    = Sum upper_volume(FAC,T1.i) + Sum upper_volume(FCB,T2.i)
    proof
      let i be Element of NAT;
      upper_volume(FAB,T.i) = (upper_volume(FAC,T1.i))^(upper_volume(FCB,T2.i)
      ) by A186;
      hence thesis by RVSUM_1:105;
    end;
    now
      let i be Element of NAT;
      thus upper_sum(FAB,T).i =upper_sum(FAB,T.i) by INTEGRA2:def 4
        .=upper_sum(FAC,T1.i) +Sum(upper_volume(FCB,T2.i)) by A206
        .=upper_sum(FAC,T1).i + upper_sum(FCB,T2.i) by INTEGRA2:def 4
        .=upper_sum(FAC,T1).i +upper_sum(FCB,T2).i by INTEGRA2:def 4;
    end;
    then
A207: upper_sum(FAB,T) = upper_sum(FAC,T1) +upper_sum(FCB,T2) by SEQ_1 :11;
A208: upper_sum(FAC,T1) is convergent &
    lim upper_sum(FAC,T1) = upper_integral FAC &
    upper_sum(FCB,T2) is convergent & lim upper_sum(FCB,T2) =upper_integral FCB
    by A7,A10,A11,A50,INTEGRA4:9;
    FAB is_bounded_on [' a,b '] by A1,INTEGRA5:9;
    then upper_integral FAB = lim upper_sum(FAB,T) by A7,A155,INTEGRA4:9;
    hence upper_integral FAB = upper_integral FAC + upper_integral FCB
    by A207,A208,SEQ_2:20;
  end;
A209: integral(f,a,b) = integral(f,[' a,b ']) by A1,INTEGRA5:def 5
    .= integral FAC + integral FCB by A185;
A210: integral(f,a,c) = integral(f,[' a,c ']) by A3,INTEGRA5:def 5;
  integral(f,c,b) = integral(f,[' c,b ']) by A3,INTEGRA5:def 5
    .= integral FCB;
  hence integral(f,a,b) = integral(f,a,c) + integral(f,c,b) by A209,A210;
A211: lower_integral FAC + lower_integral FCB = lower_integral FAB
  proof
A212: for i be Element of NAT holds
    lower_volume(FAB,T.i) =(lower_volume(FAC,T1.i))^(lower_volume(FCB,T2.i))
    proof
      let i be Element of NAT;
      reconsider F = lower_volume(FAB,T.i) as FinSequence of REAL;
      reconsider F1 = lower_volume(FAC,T1.i) as FinSequence of REAL;
      reconsider F2 = lower_volume(FCB,T2.i) as FinSequence of REAL;
      consider S1 be DivisionPoint of [' a,c '],
      S2 be DivisionPoint of [' c,b '] such that
A213: S1=T1.i & S2=T2.i & T.i = S1^S2 by A115;
      reconsider S=T.i as DivisionPoint of [' a,b '] by INTEGRA1:def 3;
A214: len F = len S by INTEGRA1:def 8
        .= len S1+ len S2 by A213,FINSEQ_1:35
        .= len F1 + len S2 by A213,INTEGRA1:def 8;
      then
A215: len F = len F1 + len F2 by A213,INTEGRA1:def 8;
      then
A216: dom F = Seg (len F1 + len F2) by FINSEQ_1:def 3;
A217: now
        let k be Nat;
        assume
A218:   k in dom F1;
        len F1 <= len F by A214,NAT_1:11;
        then
A219:   Seg len F1 c= Seg len F by FINSEQ_1:7;
A220:   k in Seg len F1 by A218,FINSEQ_1:def 3;
        then
A221:   k in Seg len S1 by A213,INTEGRA1:def 8;
        len lower_volume(FAB,T.i) = len S by INTEGRA1:def 8;
        then
A222:   F.k=(inf rng(FAB|divset(T.i,k))) * vol divset(T.i,k)
        by A219,A220,INTEGRA1:def 8;
A223:   divset(T.i,k) = divset(T1.i,k) & divset(T1.i,k) c=[' a,c ']
        by A116,A120,A213,A221;
        then divset(T.i,k) c= [' a,b '] by A5,XBOOLE_1:1;

then F.k=(inf rng(FAC|divset(T1.i,k))) * vol divset(T1.i,k) by A222,A223, Th3;
        hence F.k=F1.k by A213,A221,INTEGRA1:def 8;
      end;
      now
        let k be Nat;
        assume k in dom F2;
        then
A224:   k in Seg len F2 by FINSEQ_1:def 3;
        then
A225:   1 <=k & k <= len F2 by FINSEQ_1:3;
        then
A226:   len F1 + k <= len F by A215,XREAL_1:8;
A227:   1 + len F1 <= k + len F1 by A225,XREAL_1:8;
        1 <= 1+ len F1 by NAT_1:11;
        then 1 <= k + len F1 by A227,XXREAL_0:2;
        then k+ len F1 in Seg len F by A226;
        then len F1 + k in Seg len S by INTEGRA1:def 8;
        then
A228:   F.(len F1 + k)
        = (inf rng(FAB|divset(T.i,len F1 + k))) * vol divset(T.i,(len F1 + k))
        by INTEGRA1:def 8;
A229:   k in Seg len S2 & len F1= len S1 by A213,A224,INTEGRA1:def 8;
        then
A230:   divset(T.i,len F1 + k) = divset(T2.i,k) &
        divset(T2.i,k) c= [' c,b '] by A118,A136,A213;
        then divset(T.i,len F1 + k) c= [' a,b '] by A5,XBOOLE_1:1;
        then F.(len F1 + k)=(inf rng(FCB|divset(T2.i,k))) * vol divset(T2.i,k)
        by A228,A230,Th3;
        hence F.(len F1 + k)=F2.k by A213,A229,INTEGRA1:def 8;
      end;
      hence thesis by A216,A217,FINSEQ_1:def 7;
    end;
A231: for i be Element of NAT holds Sum lower_volume(FAB,T.i)
    = Sum lower_volume(FAC,T1.i) + Sum lower_volume(FCB,T2.i)
    proof
      let i be Element of NAT;
      lower_volume(FAB,T.i)
      = (lower_volume(FAC,T1.i))^(lower_volume(FCB,T2.i)) by A212;
      hence thesis by RVSUM_1:105;
    end;
    now
      let i be Element of NAT; thus
      lower_sum(FAB,T).i =lower_sum(FAB,T.i) by INTEGRA2:def 5
        .=lower_sum(FAC,T1.i) + Sum lower_volume(FCB,T2.i) by A231
        .=lower_sum(FAC,T1).i + lower_sum(FCB,T2.i) by INTEGRA2:def 5
        .=lower_sum(FAC,T1).i + lower_sum(FCB,T2).i by INTEGRA2:def 5;
    end;
    then
A232: lower_sum(FAB,T) = lower_sum(FAC,T1) + lower_sum(FCB,T2) by SEQ_1:11;
A233: lower_sum(FAC,T1) is convergent &
    lim lower_sum(FAC,T1) =lower_integral FAC &
    lower_sum(FCB,T2) is convergent &
    lim lower_sum(FCB,T2) =lower_integral FCB by A7,A10,A11,A50,INTEGRA4:8;
    FAB is_bounded_on [' a,b '] by A1,INTEGRA5:9;
    then lower_integral FAB = lim lower_sum(FAB,T) by A7,A155,INTEGRA4:8;
    hence lower_integral FAB = lower_integral FAC + lower_integral FCB
    by A232,A233,SEQ_2:20;
  end;
  lower_integral FAC <= upper_integral FAC &
  lower_integral FCB <= upper_integral FCB by A7,A9,INTEGRA1:51,INTEGRA5:9;
  then
A234: lower_integral FAC = upper_integral FAC &
  lower_integral FCB = upper_integral FCB by A8,A185,A211,Th1;
  FAC is_upper_integrable_on [' a,c '] &
  FAC is_lower_integrable_on [' a,c '] &
  FCB is_upper_integrable_on [' c,b '] &
  FCB is_lower_integrable_on [' c,b '] by A7,A10,INTEGRA4:10;
  then FAC is_integrable_on [' a,c '] &
  FCB is_integrable_on [' c,b '] by A234,INTEGRA1:def 17;
  hence f is_integrable_on [' a,c '] & f is_integrable_on [' c,b ']
  by INTEGRA5:def 2;
end;

theorem Th17:
  a <= b & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
  [' a,b '] c= dom f & c in [' a,b '] implies
  f is_integrable_on ['a,c '] & f is_integrable_on ['c,b '] &
  integral(f,a,b )=integral(f,a,c )+integral(f,c,b)
proof
  assume that
A1: a <= b and
A2: f is_integrable_on [' a,b '] and
A3: f is_bounded_on [' a,b '] and
A4: [' a,b '] c= dom f and
A5: c in [' a,b '];
  now
    assume
A6: b=c;
    then
A7: [' c,b ']= [.c,b.] by INTEGRA5:def 4;
    thus f is_integrable_on ['a,c '] by A2,A6;
    [' c,b ']= [. inf([' c,b ']),sup([' c,b ']) .] by INTEGRA1:5;
    then inf([' c,b ']) = c & sup([' c,b ']) = b by A7,INTEGRA1:6;
    then
A8: vol(['c,b ']) =0 by A6;
    then f||['c,b '] is_integrable_on ['c,b '] & integral(f||['c,b '])=0
    by INTEGRA4:6;
    hence f is_integrable_on ['c,b '] by INTEGRA5:def 2;
    c is Real & b is Real by XREAL_0:def 1;
    then integral(f,c,b ) = integral(f,[' c,b '] ) by A7,INTEGRA5:19;
    then integral(f,c,b ) =0 by A8,INTEGRA4:6;
    hence integral(f, a,b )= integral(f,a,c ) +integral(f,c,b) by A6;
  end;
  hence f is_integrable_on ['a,c '] & f is_integrable_on ['c,b '] &
  integral(f,a,b)=integral(f,a,c)+integral(f,c,b) by A1,A2,A3,A4,A5,Lm2;
end;

Lm3: for a,b,c be real number st a <= c & c <= b holds
c in [' a,b '] & [' a,c '] c= [' a,b '] & [' c,b '] c= [' a,b ']
proof
  let a,b,c be real number;
  assume
A1: a <= c & c <= b;
  then
A2: a <= b by XXREAL_0:2;
A3: [' a,b '] = [.a,b.] & [' a,c '] = [.a,c.] & [' c,b '] = [.c,b.]
  by A1,INTEGRA5:def 4,XXREAL_0:2;
A4: c in [.a,b.] by A1,RCOMP_1:48;
  hence c in [' a,b '] by A1,INTEGRA5:def 4,XXREAL_0:2;
  a in [.a,b.] & b in [.a,b.] by A2,RCOMP_1:48;
  hence thesis by A3,A4,RCOMP_1:16;
end;

theorem Th18:
  a <= c & c <= d & d <= b & f is_integrable_on [' a,b '] &
  f is_bounded_on [' a,b '] & [' a,b '] c= dom f implies
  f is_integrable_on ['c,d '] & f is_bounded_on ['c,d '] & ['c,d '] c= dom f
proof
  assume that
A1: a <= c & c <= d & d <= b and
A2: f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] and
A3: [' a,b '] c= dom f;
  a <= d by A1,XXREAL_0:2;
  then
A4: a <= b & c <= b by A1,XXREAL_0:2;
  then
A5: c in [' a,b '] & d in [' c,b '] by A1,Lm3;
A6: ['c,b '] c= [' a,b '] by A1,A4,Lm3;
  then
A7: ['c,b '] c= dom f by A3,XBOOLE_1:1;
  f is_integrable_on ['c,b '] &
  f is_bounded_on [' c,b '] by A2,A3,A4,A5,A6,Th17,RFUNCT_1:91;
  hence f is_integrable_on ['c,d '] by A4,A5,A7,Th17;
  [' c,d '] c= [' c,b '] by A1,Lm3;
  then
A8: [' c,d '] c= [' a,b '] by A6,XBOOLE_1:1;
  hence f is_bounded_on [' c,d '] by A2,RFUNCT_1:91;
  thus [' c,d '] c= dom f by A3,A8,XBOOLE_1:1;
end;

theorem
  a <= c & c <= d & d <= b &
  f is_integrable_on [' a,b '] & g is_integrable_on [' a,b '] &
  f is_bounded_on [' a,b '] & g is_bounded_on [' a,b '] &
  [' a,b '] c= dom f & [' a,b '] c= dom g implies
  f+g is_integrable_on ['c,d '] & f+g is_bounded_on ['c,d ']
proof
  assume
A1: a <= c & c <= d & d <= b &
  f is_integrable_on [' a,b '] & g is_integrable_on [' a,b '] &
  f is_bounded_on [' a,b '] & g is_bounded_on [' a,b '] &
  [' a,b '] c= dom f & [' a,b '] c= dom g;
  then
A2: f is_integrable_on ['c,d '] & f is_bounded_on ['c,d '] &
  g is_integrable_on ['c,d '] & g is_bounded_on ['c,d '] by Th18;
  c <=b by A1,XXREAL_0:2;
  then ['c,b '] c= [' a,b '] by A1,Lm3;
  then
A3: ['c,b '] c= dom f & ['c,b '] c= dom g by A1,XBOOLE_1:1;
  [' c,d '] c= [' c,b '] by A1,Lm3;
  then [' c,d '] c= dom f & [' c,d '] c= dom g by A3,XBOOLE_1:1;
  hence f+g is_integrable_on ['c,d '] by A2,Th11;
  f+g is_bounded_on ['c,d '] /\ ['c,d '] by A2,RFUNCT_1:100;
  hence f+g is_bounded_on ['c,d '];
end;

Lm4: for a,b,c,d be real number, f be PartFunc of REAL,REAL st
a<=b & c <=d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
[' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] holds
integral(f,a,d)=integral(f,a,c)+integral(f,c,d)
proof
  let a,b,c,d be real number, f be PartFunc of REAL,REAL;
  assume
A1: a<=b & c <= d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
  [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '];
  then [' a,b '] = [.a,b.] by INTEGRA5:def 4;
  then
A2: a <= d & a <= c & d <= b by A1,RCOMP_1:48;
  then
A3: c in [' a,d '] & [' a,d '] c= [' a,b '] by A1,Lm3;
  then
A4: [' a,d '] c= dom f by A1,XBOOLE_1:1;
A5: f is_integrable_on [' a,d '] by A1,Th17;
  f is_bounded_on [' a,d '] by A1,A2,Th18;
  hence thesis by A2,A3,A4,A5,Th17;
end;

theorem Th20:
  a<=b & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
  [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] implies
  integral(f,a,d)=integral(f,a,c)+integral(f,c,d)
proof
  assume
A1: a<=b & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
  [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '];
  now
    assume
A2: not (c <= d);
    then
A3: integral(f,c,d) = -integral(f,[' d,c ']) by INTEGRA5:def 5;
    integral(f,a,c)=integral(f,a,d)+integral(f,d,c) by A1,A2,Lm4;
    then integral(f,a,d) =integral(f,a,c) - integral(f,d,c);
    hence thesis by A2,A3,INTEGRA5:def 5;
  end;
  hence thesis by A1,Lm4;
end;

Lm5: for a,b,c,d be real number, f be PartFunc of REAL,REAL st
a <= b & c <=d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
[' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] holds
[' c,d '] c= dom abs f & abs f is_integrable_on [' c,d '] &
abs f is_bounded_on [' c,d '] & abs integral(f,c,d) <= integral(abs f,c,d)
proof
  let a,b,c,d be real number, f be PartFunc of REAL,REAL such that
A1: a <= b & c <= d & f is_integrable_on [' a,b '] &
  f is_bounded_on [' a,b '] & [' a,b '] c= dom f &
  c in [' a,b '] & d in [' a,b '];
  [' a,b '] = [.a,b.] by A1,INTEGRA5:def 4;
  then a<=c & d <= b by A1,RCOMP_1:48;
  then [' c,d '] c= dom f & f is_integrable_on [' c,d '] &
  f is_bounded_on [' c,d '] by A1,Th18;
  hence thesis by A1,Th7,Th8,RFUNCT_1:99,VALUED_1:def 11;
end;

theorem Th21:
  a <= b & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
  [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] implies
  [' min(c,d),max(c,d) '] c= dom (abs f) &
  abs f is_integrable_on [' min(c,d),max(c,d) '] &
  abs f is_bounded_on [' min(c,d),max(c,d) '] &
  abs integral(f,c,d) <= integral(abs f,min(c,d),max(c,d))
proof
  assume that
A1: a <= b & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
  [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '];
A2: now
    assume
A3: c <= d;
    then c = min(c,d) & d = max(c,d) by XXREAL_0:def 8,def 9;
    hence thesis by A1,A3,Lm5;
  end;
  now
    assume
A4: not(c <= d);
    then
A5: d = min(c,d) & c = max(c,d) by XXREAL_0:def 8,def 9;
    integral(f,c,d) = -integral(f,[' d,c ']) by A4,INTEGRA5:def 5;
    then integral(f,c,d) = -integral(f,d,c) by A4,INTEGRA5:def 5;
    then abs integral(f,c,d) = abs integral(f,d,c) by COMPLEX1:138;
    hence thesis by A1,A4,A5,Lm5;
  end;
  hence thesis by A2;
end;

Lm6: a
<= b & c <= d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
[' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] implies
[' c,d '] c= dom (abs f) & abs f is_integrable_on [' c,d '] &
abs f is_bounded_on [' c,d '] & abs integral(f,c,d) <= integral(abs f,c,d)
proof
  assume
A1: a<=b & c <= d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
  [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '];
  then min(c,d) = c & max(c,d) = d by XXREAL_0:def 8,def 9;
  hence thesis by A1,Th21;
end;

Lm7: a
<= b & c <= d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
[' a,b '] c= dom f & c in [' a,b '] & d in [' a,b ']
implies abs integral(f,d,c) <= integral(abs f,c,d)
proof
  assume
A1: a<=b & c <= d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
  [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '];
  then
A2: abs integral(f,c,d) <= integral(abs f,c,d) by Lm6;
A3: integral(f,c,d) = integral(f,[' c,d ']) by A1,INTEGRA5:def 5;
  per cases;
  suppose c = d;
    hence abs integral(f,d,c) <= integral(abs f,c,d) by A1,Lm6;
  end;
  suppose c <> d;
    then c < d by A1,REAL_1:def 5;
    then integral(f,d,c) = -integral(f,[' c,d ']) by INTEGRA5:def 5;
    hence abs integral(f,d,c) <= integral(abs f,c,d) by A2,A3,COMPLEX1:138;
  end;
end;

theorem
  a <= b & c <= d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
  [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] implies
  [' c,d '] c= dom (abs f) & abs f is_integrable_on [' c,d '] &
  abs f is_bounded_on [' c,d '] & abs integral(f,c,d) <= integral(abs f,c,d) &
  abs integral(f,d,c) <= integral(abs f,c,d) by Lm6,Lm7;

Lm8: ( a<=b & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
[' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] &
for x be real number st x in [' min(c,d),max(c,d) '] holds abs(f.x) <= e )
implies abs integral(f,c,d) <= e * abs(d-c)
proof
  set A = [' min(c,d),max(c,d) '];
  assume
A1: a<=b & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
  [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] &
  for x be real number st x in [' min(c,d),max(c,d) '] holds abs(f.x) <= e;
  then
A2: [' min(c,d),max(c,d) '] c= dom (abs f) &
  abs f is_integrable_on A & abs f is_bounded_on A &
  abs integral(f,c,d) <= integral(abs f,min(c,d),max(c,d)) by Th21;
  min(c,d) <= c & c <= max(c,d) by XXREAL_0:17,25;
  then min(c,d) <= max(c,d) by XXREAL_0:2;
  then
A3: integral(abs f,min(c,d),max(c,d)) = integral(abs f,A) by INTEGRA5: def 5;
  reconsider e as Real by XREAL_0:def 1;
  rng abs f c= REAL;
  then abs f is Function of dom abs f,REAL by FUNCT_2:4;
  then reconsider g =abs(f)||A as Function of A,REAL by A2,FUNCT_2:38;
  consider h be Function of A,REAL such that
A4: rng h = {e} & h is_bounded_on A by INTEGRA4:5;
A5: now
    let x be Real;
    assume
A6: x in A;
    then h.x in {e} by A4,FUNCT_2:6;
    then
A7: h.x =e by TARSKI:def 1;
    g.x = (abs f).x by A6,FUNCT_1:72;
    then g.x = abs(f.x) by VALUED_1:18;
    hence g.x <= h.x by A1,A6,A7;
  end;
A8: g is_integrable_on A & g is_bounded_on A by A2,INTEGRA5:9,def 2;
A9: h is_integrable_on A & integral h = e*vol A by A4,INTEGRA4:4;
  vol A = abs(d-c) by Th6;
  then integral(abs f,min(c,d),max(c,d)) <= e * abs(d-c)
  by A3,A4,A5,A8,A9,INTEGRA2:34;
  hence thesis by A2,XXREAL_0:2;
end;

Lm9: (
a<=b & c <= d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
[' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] &
for x be real number st x in [' c,d '] holds abs(f.x) <= e ) implies
abs integral(f,c,d) <= e * (d-c)
proof
  assume
A1: a<=b & c <= d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
  [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] &
  for x be real number st x in [' c,d '] holds abs(f.x) <= e;
  then 0 <= d - c by XREAL_1:50;
  then
A2: abs(d-c) = d-c by ABSVALUE:def 1;
  min(c,d) = c & max(c,d) = d by A1,XXREAL_0:def 8,def 9;
  hence thesis by A1,A2,Lm8;
end;

Lm10: (
a<=b & c <= d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
[' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] &
for x be real number st x in [' c,d '] holds abs(f.x) <= e ) implies
abs integral(f,d,c) <= e * (d-c)
proof
  assume
A1: a<=b & c <= d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
  [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] &
  for x be real number st x in [' c,d '] holds abs(f.x) <= e;
  then
A2: abs integral(f,c,d) <= e * (d-c) by Lm9;
A3: integral(f,c,d) = integral(f,[' c,d ']) by A1,INTEGRA5:def 5;
  per cases;
  suppose c = d;
    hence abs integral(f,d,c) <= e * (d-c) by A1,Lm9;
  end;
  suppose c <> d;
    then c < d by A1,REAL_1:def 5;
    then integral(f,d,c) = -integral(f,[' c,d ']) by INTEGRA5:def 5;
    hence abs integral(f,d,c) <= e * (d-c) by A2,A3,COMPLEX1:138;
  end;
end;

theorem
  ( a<=b & c <= d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
  [' a,b '] c= dom f & c in [' a,b '] & d in [' a,b '] &
  for x be real number st x in [' c,d '] holds abs(f.x) <= e ) implies
  abs integral(f,c,d) <= e * (d-c) & abs integral(f,d,c) <= e * (d-c)
  by Lm9,Lm10;

Lm11: for a,b,c,d be real number, f,g be PartFunc of REAL,REAL st
a <= b & c <= d &
f is_integrable_on [' a,b '] & g is_integrable_on [' a,b '] &
f is_bounded_on [' a,b '] & g is_bounded_on [' a,b '] &
[' a,b '] c= dom f & [' a,b '] c= dom g & c in ['a,b '] & d in ['a,b '] holds
f+g is_integrable_on ['c,d '] & f+g is_bounded_on ['c,d '] &
integral(f+g,c,d) =integral(f,c,d)+integral(g,c,d) &
f-g is_integrable_on ['c,d '] & f-g is_bounded_on ['c,d '] &
integral(f-g,c,d) =integral(f,c,d)-integral(g,c,d)
proof
  let a,b,c,d be real number, f,g be PartFunc of REAL,REAL;
  assume
A1: a <= b & c <= d &
  f is_integrable_on [' a,b '] & g is_integrable_on [' a,b '] &
  f is_bounded_on [' a,b '] & g is_bounded_on [' a,b '] &
  [' a,b '] c= dom f & [' a,b '] c= dom g & c in ['a,b '] & d in ['a,b '];
  then ['a,b '] = [.a,b.] by INTEGRA5:def 4;
  then a<=c & d <= b by A1,RCOMP_1:48;
  then
A2: f is_integrable_on ['c,d '] & f is_bounded_on ['c,d '] &
  ['c,d '] c= dom f & g is_integrable_on ['c,d '] & g is_bounded_on ['c,d '] &
  ['c,d '] c= dom g by A1,Th18;
  then f+g is_bounded_on ['c,d '] /\ ['c,d '] &
  f-g is_bounded_on ['c,d '] /\ ['c,d '] by RFUNCT_1:100,101;
  hence thesis by A1,A2,Th11,Th12;
end;

theorem Th24:
  a <= b & f is_integrable_on [' a,b '] & g is_integrable_on [' a,b '] &
  f is_bounded_on [' a,b '] & g is_bounded_on [' a,b '] &
  [' a,b '] c= dom f & [' a,b '] c= dom g & c in ['a,b '] & d in ['a,b ']
  implies integral(f+g,c,d) = integral(f,c,d) + integral(g,c,d) &
  integral(f-g,c,d) = integral(f,c,d) - integral(g,c,d)
proof
  assume
A1: a <= b & f is_integrable_on [' a,b '] & g is_integrable_on [' a,b '] &
  f is_bounded_on [' a,b '] & g is_bounded_on [' a,b '] &
  [' a,b '] c= dom f & [' a,b '] c= dom g & c in ['a,b '] & d in ['a,b '];
  now
    assume
A2: not (c <= d);
    then
A3: integral(f,c,d) = -integral(f,[' d,c ']) &
    integral(g,c,d) = -integral(g,[' d,c ']) &
    integral(f+g,c,d) = -integral(f+g,[' d,c ']) &
    integral(f-g,c,d) = -integral(f-g,[' d,c ']) by INTEGRA5:def 5;
    hence integral(f+g,c,d) = -integral(f+g,d,c) by A2,INTEGRA5:def 5
      .= -(integral(f,d,c)+integral(g,d,c)) by A1,A2,Lm11
      .= -integral(f,d,c)-integral(g,d,c)
      .= integral(f,c,d) -integral(g,d,c) by A2,A3,INTEGRA5:def 5
      .= integral(f,c,d) +integral(g,c,d) by A2,A3,INTEGRA5:def 5;
    thus integral(f-g,c,d) =-integral(f-g,d,c) by A2,A3,INTEGRA5:def 5
      .= -(integral(f,d,c)-integral(g,d,c)) by A1,A2,Lm11
      .= -(integral(f,d,c)+integral(g,c,d)) by A2,A3,INTEGRA5:def 5
      .=-integral(f,d,c)-integral(g,c,d)
      .= integral(f,c,d) -integral(g,c,d) by A2,A3,INTEGRA5:def 5;
  end;
  hence thesis by A1,Lm11;
end;

Lm12: for a,b,c,d,e be real number, f be PartFunc of REAL,REAL st
a <= b & c <= d & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
[' a,b '] c= dom f & c in ['a,b '] & d in ['a,b '] holds
integral(e(#)f,c,d)=e*integral(f,c,d)
proof
  let a,b,c,d,e be real number, f be PartFunc of REAL,REAL;
  assume
A1: a <= b & c <= d & f is_integrable_on [' a,b '] &
  f is_bounded_on [' a,b '] & [' a,b '] c= dom f &
  c in ['a,b '] & d in ['a,b '];
  then ['a,b '] = [.a,b.] by INTEGRA5:def 4;
  then a <= c & d <= b by A1,RCOMP_1:48;
  then f is_integrable_on ['c,d '] & f is_bounded_on ['c,d '] & ['c,d '] c=
  dom f by A1,Th18;
  hence integral(e(#)f,c,d)=e*integral(f,c,d) by A1,Th10;
end;

theorem
  a <= b & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
  [' a,b '] c= dom f & c in ['a,b '] & d in ['a,b '] implies
  integral(e(#)f,c,d)=e*integral(f,c,d)
proof
  assume
A1: a <= b & f is_integrable_on [' a,b '] &
  f is_bounded_on [' a,b '] & [' a,b '] c= dom f &
  c in ['a,b '] & d in ['a,b '];
  now
    assume
A2: not(c <= d);
    then
A3: integral(f,c,d) = -integral(f,[' d,c ']) by INTEGRA5:def 5;
    thus integral(e(#)f,c,d) = -integral(e(#)f,[' d,c ']) by A2,INTEGRA5:def 5
      .= -integral(e(#)f,d,c) by A2,INTEGRA5:def 5
      .= -e*integral(f,d,c) by A1,A2,Lm12
      .= e*(-integral(f,d,c))
      .= e*integral(f,c,d) by A2,A3,INTEGRA5:def 5;
  end;
  hence thesis by A1,Lm12;
end;

theorem Th26:
  ( a <= b & [' a,b '] c= dom f &
  for x be real number st x in [' a,b '] holds f.x=e ) implies
  f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
  integral(f,a,b) = e*(b-a)
proof
  assume
A1: a <= b & [' a,b '] c= dom f &
  for x be real number st x in [' a,b '] holds f.x = e;
  reconsider e1=e as Real by XREAL_0:def 1;
A2: integral(f,a,b) = integral(f,[' a,b ']) by A1,INTEGRA5:def 5;
  rng f c= REAL;
  then f is Function of dom f,REAL by FUNCT_2:4;
  then reconsider g =f||[' a,b '] as Function of [' a,b '],REAL by A1,
  FUNCT_2:38;
  consider h be Function of [' a,b '],REAL such that
A3: rng h = {e1} & h is_bounded_on [' a,b '] by INTEGRA4:5;
A4: for x be set st x in [' a,b '] holds g.x = h.x
  proof
    let x0 be set;
    assume
A5: x0 in [' a,b '];
    then reconsider x=x0 as real number by XREAL_0:def 1;
A6: h.x in {e1} by A3,A5,FUNCT_2:6;
    g.x0 = f.x by A5,FUNCT_1:72;
    then g.x0 = e by A1,A5;
    hence g.x0 = h.x0 by A6,TARSKI:def 1;
  end;
  then
A7: h=g by FUNCT_2:18;
  h is_integrable_on [' a,b '] by A3,INTEGRA4:4;
  hence f is_integrable_on [' a,b '] by A7,INTEGRA5:def 2;
  thus f is_bounded_on [' a,b ']
  proof
    consider r be real number such that
A8: for c be set st c in [' a,b '] /\ dom g holds
    abs(g.c)<=r by A3,A7,RFUNCT_1:90;
    now
      let c being set;
      assume
A9:   c in [' a,b '] /\ dom f;
      [' a,b '] /\ dom g = [' a,b '] /\ (dom f /\ [' a,b ']) by FUNCT_1:68;
      then
A10:  [' a,b '] /\ dom g = [' a,b '] /\ [' a,b '] /\ dom f by XBOOLE_1:16;
      then
A11:  abs(g.c) <= r by A8,A9;
      c in dom g by A9,A10,XBOOLE_0:def 3;
      hence abs(f.c)<=r by A11,FUNCT_1:68;
    end;
    hence thesis by RFUNCT_1:90;
  end;
  integral h = e1*vol([' a,b ']) by A3,INTEGRA4:4;
  then integral h = e*(b-a) by A1,Th5;
  hence integral(f,a,b) =e*(b-a) by A2,A4,FUNCT_2:18;
end;

theorem Th27:
  a <= b & (for x be real number st x in [' a,b '] holds f.x=e) &
  [' a,b '] c= dom f & c in ['a,b '] & d in ['a,b '] implies
  integral(f,c,d) = e*(d-c)
proof
  assume
A1: a<=b & (for x be real number st x in [' a,b '] holds f.x=e) &
  [' a,b '] c= dom f & c in ['a,b '] & d in ['a,b '];
  then ['a,b '] = [.a,b.] by INTEGRA5:def 4;
  then
A2: a <= c & d <= b & a <= d & c <= b by A1,RCOMP_1:48;
  then
A3: ['c,b '] c= [' a,b '] & ['a,c '] c= [' a,b '] by Lm3;
A4: f is_integrable_on ['a,b '] & f is_bounded_on ['a,b '] &
  ['a,b '] c= dom f by A1,Th26;
  per cases;
  suppose
A5: c <= d;
    then
A6: f is_integrable_on ['c,d '] & f is_bounded_on ['c,d '] &
    ['c,d '] c= dom f by A2,A4,Th18;
    ['c,d '] c= [' c,b '] by A2,A5,Lm3;
    then ['c,d '] c= ['a,b '] by A3,XBOOLE_1:1;
    then for x be real number st x in ['c,d '] holds f.x=e by A1;
    hence integral(f,c,d) = e*(d-c) by A5,A6,Th26;
  end;
  suppose
A7: not(c <= d);
    then
A8: f is_integrable_on ['d,c '] & f is_bounded_on ['d,c '] &
    ['d,c '] c= dom f by A2,A4,Th18;
    ['d,c '] c= [' a,c '] by A2,A7,Lm3;
    then ['d,c '] c= ['a,b '] by A3,XBOOLE_1:1;
    then for x be real number st x in ['d,c '] holds f.x=e by A1;
    then
A9: integral(f,d,c) = e*(c-d) by A7,A8,Th26;
    integral(f,c,d) = -integral(f,[' d,c ']) by A7,INTEGRA5:def 5;
    then integral(f,c,d) = -integral(f,d,c) by A7,INTEGRA5:def 5;
    hence integral(f,c,d) = e*(d-c) by A9;
  end;
end;

begin :: Fundamental Theorem of Calculus

theorem Th28:
  for x0 be real number, F be PartFunc of REAL,REAL st
  a <= b & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
  [' a,b '] c= dom f & ].a,b.[ c= dom F &
  (for x be real number st x in ].a,b.[ holds F.x = integral(f,a,x)) &
  x0 in ].a,b.[ & f is_continuous_in x0 holds
  F is_differentiable_in x0 & diff(F,x0)=f.x0
proof
  let x0 be real number, F be PartFunc of REAL,REAL;
  assume
A1: a <= b & f is_integrable_on [' a,b '] &
  f is_bounded_on [' a,b '] & [' a,b '] c= dom f & ].a,b.[ c= dom F &
  (for x be real number st x in ].a,b.[ holds F.x = integral(f,a,x)) &
  x0 in ].a,b.[ & f is_continuous_in x0;
  then
A2: [' a,b '] = [.a,b.] by INTEGRA5:def 4;
  deffunc F0(Element of REAL)=(f.x0)*$1;
  consider L be Function of REAL,REAL such that
A3: for h be Real holds L.h=F0(h) from FUNCT_2:sch 4;
  reconsider L as PartFunc of REAL,REAL;
  deffunc F1(set) = F.(x0+R_id($1))-F.x0-L.R_id($1);
  deffunc F2(set) = 0;
  defpred C1[set] means (x0+R_id($1)) in ].a,b.[;
  consider R be Function such that
A4: dom R=REAL & for x be set st x in REAL holds
  (C1[x] implies R.x = F1(x)) & (not C1[x] implies R.x = F2(x))
  from PARTFUN1:sch 1;
  rng R c= REAL
  proof
    let y be set;
    assume y in rng R;
    then consider x be set such that
A5: x in dom R & y=R.x by FUNCT_1:def 5;
    (C1[x] implies R.x = F1(x)) & (not C1[x] implies R.x = F2(x)) by A4,A5;
    hence y in REAL by A5;
  end;
  then reconsider R as PartFunc of REAL,REAL by A4,PARTFUN1:25;
  reconsider L as LINEAR by A3,FDIFF_1:def 4;
A6: R is total by A4,PARTFUN1:def 4;
  for h be convergent_to_0 Real_Sequence holds
  (h")(#)(R*h) is convergent & lim ((h")(#)(R*h)) = 0
  proof
    let h be convergent_to_0 Real_Sequence;
A7: for e be real number st 0<e ex n be Element of NAT st
    for m be Element of NAT st n <= m holds abs(((h")(#)(R*h)).m - 0) < e
    proof
      let e0 be real number;
      set e=e0/2;
      assume 0 < e0;
      then
A8:   0 < e & e < e0 by XREAL_1:217,218;
      then consider p be real number such that
A9:   0 < p & for t be real number st
      t in dom f & abs(t - x0) < p holds abs(f.t - f.x0) < e by A1,FCONT_1:3;
      consider N be Neighbourhood of x0 such that
A10:  N c= ].a,b.[ by A1,RCOMP_1:42;
      consider q be real number such that
A11:  0<q & N = ].x0-q,x0+q.[ by RCOMP_1:def 7;
      set r=min(p/2,q/2);
A12:  0 < p/2 & 0 < q/2 & p/2 < p & q/2 < q by A9,A11,XREAL_1:217,218;
      then
A13:  0 < r & r <= p/2 & r <= q/2 by XXREAL_0:15,17;
      then
A14:  ].x0-r,x0+r.[ c= ].x0-q,x0+q.[ by A12,Th2,XXREAL_0:2;
A15:  ].a,b.[ c= [.a,b.] by RCOMP_1:15;
      h is convergent & lim(h) = 0 by FDIFF_1:def 1;
      then consider n be Element of NAT such that
A16:  for m be Element of NAT st n <= m holds abs(h.m - 0)<r
      by A13,SEQ_2:def 7;
      take n;
      let m be Element of NAT;
      assume n <= m;
      then
A17:  abs(h.m - 0) < r by A16;
      then abs((x0 + h.m)-x0) < r;
      then x0 + h.m in ].x0-r,x0+r.[ by RCOMP_1:8;
      then x0 + h.m in ].x0-q,x0+q.[ by A14;
      then
A18:  x0+h.m in ].a,b.[ & F.(x0+h.m) = integral(f,a,x0+h.m) by A1, A10,A11;
      then x0+R_id(h.m) in ].a,b.[ by RSSPACE:def 3;
      then R.(h.m)=F.(x0+R_id(h.m))-F.x0-L.R_id(h.m) by A4;
      then R.(h.m)=F.(x0+h.m)-F.x0-L.R_id(h.m) by RSSPACE:def 3;
      then R.(h.m) = F.(x0+h.m)-F.x0-L.(h.m) by RSSPACE:def 3;
      then R.(h.m) = F.(x0+h.m)-F.x0-(f.x0)*h.m by A3;
      then
A19:  R.(h.m) =integral(f,a,x0+h.m ) -integral(f,a,x0) -(f.x0)*h.m by A1,A18;
      deffunc G0(Element of REAL)=f.x0;
      consider g be Function of REAL,REAL such that
A20:  for h be Real holds g.h=G0(h) from FUNCT_2:sch 4;
A21:  dom g = REAL by FUNCT_2:def 1;
A22:  for x be real number st x in [' a,b '] holds g.x = f.x0 by A20;
      then
A23:  g is_integrable_on [' a,b '] & g is_bounded_on [' a,b '] by A1
      ,A21,Th26;
      then f-g is_bounded_on ['a,b '] /\ ['a,b '] by A1,RFUNCT_1:101;
      then
A24:  f-g is_integrable_on [' a,b '] &
      f-g is_bounded_on [' a,b '] by A1,A21,A23,Th11;
      [' a,b '] /\ [' a,b '] c= dom f /\ dom g by A1,A21,XBOOLE_1:27;
      then
A25:  [' a,b '] c= dom (f-g) by VALUED_1:12;
      for x be real number st x in [' min(x0,x0+h.m),max(x0,x0+h.m) ']
      holds abs((f-g).x) <= e
      proof
        let x be real number;
        assume
A26:    x in [' min(x0,x0+h.m),max(x0,x0+h.m) '];
        reconsider x1 = x as Real by XREAL_0:def 1;
        min(x0,x0+h.m) <= x0 & x0 <= max(x0,x0+h.m) by XXREAL_0:17,25;
        then
A27:    x in [. min(x0,x0+h.m),max(x0,x0+h.m) .] by A26,INTEGRA5: def 4,
        XXREAL_0:2;
        abs(x-x0) <=abs(h.m)
        proof
          per cases;
          suppose x0<= x0+h.m;
            then x0 = min(x0,x0+h.m) & x0+h.m = max(x0,x0+h.m)
            by XXREAL_0:def 8,def 9;

then x in {z where z is Real: x0<=z & z<=x0+h.m } by A27,RCOMP_1:def 1;
            then
A28:        ex z be Real st x=z & x0<=z & z<=x0+h.m;
            then
A29:        x-x0 <= x0+h.m - x0 by XREAL_1:11;
            then
A30:        0 <= h.m by A28,XREAL_1:50;
            0 <= x-x0 by A28,XREAL_1:50;
            then abs(x-x0) = x-x0 by ABSVALUE:def 1;
            hence abs(x-x0) <= abs(h.m) by A29,A30,ABSVALUE:def 1;
          end;
          suppose
A31:        not (x0<=x0 +h.m);
            then x0 = max(x0,x0+h.m) &
            x0+h.m = min(x0,x0+h.m) by XXREAL_0:def 8,def 9;

then x in {z where z is Real: x0+h.m<=z & z<=x0 } by A27,RCOMP_1:def 1;
            then
A32:        ex z be Real st x=z & x0+h.m<=z & z<=x0;
            then
A33:        x0 +h.m -x0 <= x-x0 by XREAL_1:11;
            per cases;
            suppose x-x0 <> 0;
              then x-x0 < 0 by A32,XREAL_1:49;
              then
A34:          abs(x-x0) = -(x-x0) by ABSVALUE:def 1;
              x0 + h.m - x0 < x0-x0 by A31,XREAL_1:16;
              then abs(h.m) = -(h.m) by ABSVALUE:def 1;
              hence abs(x-x0) <= abs(h.m) by A33,A34,XREAL_1:26;
            end;
            suppose x-x0 = 0;
              then abs(x-x0) = 0 by ABSVALUE:def 1;
              hence abs(x-x0) <= abs(h.m) by COMPLEX1:132;
            end;
          end;
        end;
        then
A35:    abs(x-x0) < r by A17,XXREAL_0:2;
        then abs(x-x0) < p/2 by A13,XXREAL_0:2;
        then
A36:    abs(x-x0) < p by A12,XXREAL_0:2;
        x in ].x0-r,x0+r.[ by A35,RCOMP_1:8;
        then x in ].x0-q,x0+q.[ by A14;
        then x in ].a,b.[ by A10,A11;
        then
A37:    x in [.a,b.] by A15;
        then abs(f.x- f.x0)<=e by A1,A2,A9,A36;
        then abs(f.x1- g.x1)<=e by A20;
        hence abs((f-g).x)<=e by A2,A25,A37,VALUED_1:13;
      end;
      then
A38:  abs integral(f-g,x0,x0+h.m) <= e*abs(x0+h.m-x0)
      by A1,A2,A15,A18,A24,A25,Lm8;
      h is_not_0 by FDIFF_1:def 1;
      then h.m <> 0 by SEQ_1:6;
      then
A39:  0 < abs(h.m) by COMPLEX1:133;
A40:  abs(R.(h.m)/(h.m)) = abs(R.(h.m))/abs(h.m) by COMPLEX1:153;
A41:  integral(g,x0,x0+h.m)=(f.x0)*((x0+h.m)-x0) by A1,A2,A15,A18,A21,A22,Th27;
      integral(f,a,x0+h.m) = integral(f,a,x0) +integral(f,x0,x0+h.m)
      by A1,A2,A15,A18,Th20;
      then R.(h.m)
      =integral(f-g,x0,x0+h.m) by A1,A2,A15,A18,A19,A21,A23,A41,Th24;
      then abs(R.(h.m))/abs(h.m) <= e*abs(h.m)/abs(h.m) by A38,A39,XREAL_1:74;
      then
A42:  abs(R.(h.m)/(h.m)) <= e by A39,A40,XCMPLX_1:90;
      rng h c= dom R by A4;
      then R.(h.m)/(h.m) =(R*h).m /(h.m) by RFUNCT_2:21;
      then R.(h.m)/(h.m) =((R*h).m) *((h").m) by VALUED_1:10;
      then R.(h.m)/(h.m) = ((h")(#)(R*h)).m by SEQ_1:12;
      hence thesis by A8,A42,XXREAL_0:2;
    end;
    hence (h")(#)(R*h) is convergent by SEQ_2:def 6;
    hence lim((h")(#)(R*h)) = 0 by A7,SEQ_2:def 7;
  end;
  then reconsider R as REST by A6,FDIFF_1:def 3;
  consider N be Neighbourhood of x0 such that
A43: N c= ].a,b.[ by A1,RCOMP_1:42;
A44: N c= dom F by A1,A43,XBOOLE_1:1;
A45: for x be Real st x in N holds F.x-F.x0 = L.(x-x0) + R.(x-x0)
  proof
    let x be Real;
    assume
A46: x in N;
A47: x0+R_id((x-x0)) = x0 + (x-x0) by RSSPACE:def 3;
    x0 + (x-x0) in N by A46;
    then x0+R_id(x-x0) in N by RSSPACE:def 3;
    then R.(x-x0) = F1(x-x0) by A4,A43;
    hence F.x-F.x0 = L.(x-x0) + R.(x-x0) by A47;
  end;
  hence
A48: F is_differentiable_in x0 by A44,FDIFF_1:def 5;
  L.1=(f.x0)*1 by A3;
  hence diff(F,x0)=f.x0 by A44,A45,A48,FDIFF_1:def 6;
end;

Lm13: for a,b be real number, f be PartFunc of REAL,REAL holds
ex F be PartFunc of REAL,REAL st ].a,b.[ c= dom F &
(for x be real number st x in ].a,b.[ holds F.x = integral(f,a,x))
proof
  let a,b be real number;
  let f be PartFunc of REAL,REAL;
  defpred C[set] means $1 in ].a,b.[;
  deffunc F(real number) = integral(f,a,$1);
  deffunc G(real number) = 0;
  consider F be Function such that
A1: dom F = REAL & for x be Element of REAL holds
  (C[x] implies F.x = F(x)) & (not C[x] implies F.x = G(x))
  from PARTFUN1:sch 4;
  now
    let y be set;
    assume y in rng F;
    then consider x be set such that
A2: x in dom F & y = F.x by FUNCT_1:def 5;
    reconsider x as Element of REAL by A1,A2;
A3: now
      assume x in ].a,b.[;
      then F.x = integral(f,a,x) by A1;
      hence y in REAL by A2;
    end;
    now
      assume not x in ].a,b.[;
      then F.x = 0 by A1;
      hence y in REAL by A2;
    end;
    hence y in REAL by A3;
  end;
  then rng F c= REAL by TARSKI:def 3;
  then reconsider F as PartFunc of REAL,REAL by A1,PARTFUN1:25;
  take F;
  thus thesis by A1;
end;

theorem
  for x0 be real number st
  a <= b & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
  [' a,b '] c= dom f & x0 in ].a,b.[ & f is_continuous_in x0 holds
  ex F be PartFunc of REAL,REAL st ].a,b.[ c= dom F &
  (for x be real number st x in ].a,b.[ holds F.x = integral(f,a,x)) &
  F is_differentiable_in x0 & diff(F,x0)=f.x0
proof
  let x0 be real number;
  assume
A1: a <= b & f is_integrable_on [' a,b '] & f is_bounded_on [' a,b '] &
  [' a,b '] c= dom f & x0 in ].a,b.[ & f is_continuous_in x0;
  consider F be PartFunc of REAL,REAL such that
A2: ].a,b.[ c= dom F &
  (for x be real number st x in ].a,b.[ holds F.x = integral(f,a,x)) by Lm13;
  F is_differentiable_in x0 & diff(F,x0)=f.x0 by A1,A2,Th28;
  hence thesis by A2;
end;

