:: Basic Notation of Universal Algebra
::  by Jaros{\l}aw Kotowicz, Beata Madras and Ma{\l}gorzata Korolkiewicz
::
:: Received December 29, 1992
:: Copyright (c) 1992 Association of Mizar Users

environ

 vocabularies FINSEQ_1, PARTFUN1, RELAT_1, FUNCT_2, FUNCOP_1, BOOLE, FUNCT_1,
      ZF_REFLE, INCPROJ, UNIALG_1, FINSEQ_2, FRAENKEL, PRALG_1, NAT_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, NUMBERS, NAT_1, RELAT_1, FUNCT_1,
      FINSEQ_1, FRAENKEL, FINSEQ_2, FUNCOP_1, STRUCT_0, PARTFUN1, XXREAL_0;
 constructors PARTFUN1, FUNCOP_1, FRAENKEL, XXREAL_0, FINSEQ_2, STRUCT_0;
 registrations XBOOLE_0, SUBSET_1, ORDINAL1, RELSET_1, PARTFUN1, FUNCOP_1,
      ARYTM_3, FRAENKEL, XXREAL_0, STRUCT_0;
 requirements NUMERALS, BOOLE, SUBSET;
 definitions TARSKI, STRUCT_0, FUNCT_1, FUNCOP_1;
 theorems TARSKI, FUNCT_1, PARTFUN1, FINSEQ_1, FUNCOP_1, RELAT_1, RELSET_1,
      ORDINAL1, FINSEQ_2, FRAENKEL;
 schemes FINSEQ_1;

begin

reserve A,z for set,
        x,y for FinSequence of A,
        h for PartFunc of A*,A,
        n,m for Nat;

definition
  let IT be Relation;
attr IT is homogeneous means :Def1:
 for x,y being FinSequence st x in dom IT & y in dom IT holds len x = len y;
end;

definition let A;
 let IT be PartFunc of A*,A;
attr IT is quasi_total means
     for x,y st len x = len y & x in dom IT holds y in dom IT;
end;

registration
  let f be Relation;
  let i be Nat;
  cluster f|(i-tuples_on NAT) -> homogeneous;
  coherence
  proof set X = i-tuples_on NAT;
    set g = f|X;
A1: dom g c= X by RELAT_1:87;
    let x,y be FinSequence;
    assume x in dom g & y in dom g;
    then len x = i & len y = i by A1,FINSEQ_2:109;
    hence thesis;
  end;
end;

registration let A be non empty set;
cluster homogeneous quasi_total non empty PartFunc of A*,A;
 existence
  proof
   consider a be Element of A;
   set f = {<*>A} -->a;
   A1: dom f = {<*>A} & rng f = {a} by FUNCOP_1:14,19;
   A2: dom f c= A*
    proof
     let z; assume z in dom f;
     then z = <*>A by A1,TARSKI:def 1;
     hence thesis by FINSEQ_1:def 11;
    end;
      rng f c= A
   proof
    let z; assume z in rng f;
    then z = a by A1,TARSKI:def 1;
    hence thesis;
   end;
  then reconsider f as PartFunc of A*,A by A2,RELSET_1:11;
  A3: f is homogeneous
   proof
    let x,y be FinSequence; assume
        x in dom f & y in dom f;
    then x = <*>A & y = <*>A by A1,TARSKI:def 1;
    hence thesis;
   end;
   f is quasi_total
   proof
    let x,y be FinSequence of A; assume
    A4: len x = len y & x in dom f;
    then x = <*>A by A1,TARSKI:def 1;
    then len x = 0 by FINSEQ_1:32;
    then y = <*>A by A4,FINSEQ_1:32;
    hence thesis by A1,TARSKI:def 1;
   end;
  hence thesis by A3;
 end;
end;

registration
  cluster homogeneous non empty Function;
  existence
  proof consider A being non empty set;
    consider f being homogeneous non empty PartFunc of A*,A;
    take f;
    thus thesis;
  end;
end;

theorem
   h is non empty iff dom h <> {} by RELAT_1:60,64;

theorem Th2:
for A being non empty set, a being Element of A
holds {<*>A} -->a is homogeneous quasi_total non empty PartFunc of A*,A
 proof let A be non empty set, a be Element of A;
   set f = {<*>A} -->a;
   A1: dom f = {<*>A} & rng f = {a} by FUNCOP_1:14,19;
   A2: dom f c= A*
    proof
     let z; assume z in dom f;
     then z = <*>A by A1,TARSKI:def 1;
     hence thesis by FINSEQ_1:def 11;
    end;
      rng f c= A
   proof
    let z; assume z in rng f;
    then z = a by A1,TARSKI:def 1;
    hence thesis;
   end;
  then reconsider f as PartFunc of A*,A by A2,RELSET_1:11;
  A3: f is homogeneous
   proof
    let x,y be FinSequence; assume
        x in dom f & y in dom f;
    then x = <*>A & y = <*>A by A1,TARSKI:def 1;
    hence thesis;
   end;
      f is quasi_total
   proof
    let x,y be FinSequence of A; assume
    A4: len x = len y & x in dom f;
    then x = <*>A by A1,TARSKI:def 1;
    then len x = 0 by FINSEQ_1:32;
    then y = <*>A by A4,FINSEQ_1:32;
    hence thesis by A1,TARSKI:def 1;
   end;
  hence thesis by A3;
 end;

theorem Th3:
for A being non empty set, a being Element of A
 holds {<*>A} -->a is Element of PFuncs(A*,A)
 proof let A be non empty set, a be Element of A;
   set f = {<*>A} -->a;
   A1: dom f = {<*>A} & rng f = {a} by FUNCOP_1:14,19;
   A2: dom f c= A*
    proof
     let z; assume z in dom f;
     then z = <*>A by A1,TARSKI:def 1;
     hence thesis by FINSEQ_1:def 11;
    end;
      rng f c= A
   proof
    let z; assume z in rng f;
    then z = a by A1,TARSKI:def 1;
    hence thesis;
   end;
  then reconsider f as PartFunc of A*,A by A2,RELSET_1:11;
      f in PFuncs(A*,A) by PARTFUN1:119;
  hence {<*>A} -->a is Element of PFuncs(A*,A);
 end;

::::::::::::::::::: stolen from HILBERT3 and improved
registration let A be set, B be functional set;
 cluster -> Function-yielding PartFunc of A,B;
 coherence
  proof let f be PartFunc of A,B, x be set;
   assume x in dom f;
    then A1:   f.x in rng f by FUNCT_1:def 5;
        rng f c= B by RELSET_1:12;
   hence f.x is Function by A1,FRAENKEL:def 1;
  end;
end;
:::::::::::::::::

registration
  let A be set;
  let B be functional set;
  cluster -> Function-yielding PartFunc of A,B;
  coherence;
end;

definition let A;
 mode PFuncFinSequence of A is FinSequence of PFuncs(A*,A);
end;

definition
  struct (1-sorted) UAStr (# carrier -> set,
                             charact -> PFuncFinSequence of the carrier #);
end;

registration
 cluster non empty strict UAStr;
 existence
 proof consider D being non empty set, c being PFuncFinSequence of D;
  take UAStr (#D,c #);
  thus the carrier of UAStr (#D,c #) is non empty;
  thus thesis;
 end;
end;

registration let D be non empty set, c be PFuncFinSequence of D;
 cluster UAStr (#D,c #) -> non empty;
 coherence
 proof
  thus the carrier of UAStr (#D,c #) is non empty;
 end;
end;

definition let A;
 let IT be PFuncFinSequence of A;
 canceled;

attr IT is homogeneous means :Def4:
   for n,h st n in dom IT & h = IT.n holds h is homogeneous;
end;

definition let A;
 let IT be PFuncFinSequence of A;
 attr IT is quasi_total means :Def5:
   for n,h st n in dom IT & h = IT.n holds h is quasi_total;
end;

definition let A be non empty set; let x be Element of PFuncs(A*,A);
 redefine func <*x*> -> PFuncFinSequence of A;
 coherence
  proof
        <*x*> is FinSequence of PFuncs(A*,A);
   hence thesis;
  end;
end;

registration let A be non empty set;
cluster homogeneous quasi_total non-empty PFuncFinSequence of A;
 existence
  proof
   consider a being Element of A;
   reconsider f = {<*>A} -->a as PartFunc of A*,A by Th2;
   reconsider f as Element of PFuncs(A*,A) by PARTFUN1:119;
   take <*f*>;
   thus <*f*> is homogeneous
    proof
     let n; let h be PartFunc of A*,A; assume
     A1: n in dom <*f*> & h =<*f*>.n;
     then n in {1} by FINSEQ_1:4,def 8;
      then h = <*f*>.1 by A1,TARSKI:def 1;
     then h = f & f is homogeneous PartFunc of A*,A by Th2,FINSEQ_1:def 8;
     hence thesis;
    end;
   thus <*f*> is quasi_total
    proof
     let n; let h be PartFunc of A*,A; assume
     A2: n in dom <*f*> & h =<*f*>.n;
     then n in {1} by FINSEQ_1:4,def 8;
      then h = <*f*>.1 by A2,TARSKI:def 1;
     then h = f & f is quasi_total PartFunc of A*,A by Th2,FINSEQ_1:def 8;
     hence thesis;
    end;
   thus <*f*> is non-empty
    proof
     let n be set; assume
     A3: n in dom <*f*>;
     then reconsider n as Element of NAT;
         n in {1} by A3,FINSEQ_1:4,def 8;
     then n = 1 by TARSKI:def 1;
      hence thesis by FINSEQ_1:def 8;
    end;
  end;
end;

registration
  let A be non empty set;
  let f be homogeneous PFuncFinSequence of A;
  let i be set;
  cluster f.i -> homogeneous;
  coherence
  proof per cases;
    suppose
A1:   i in dom f;
      then f.i in rng f & rng f c= PFuncs(A*,A) by FUNCT_1:12,RELSET_1:12;
      then f.i is PartFunc of A*,A & i is Element of NAT by A1,PARTFUN1:120;
      hence f.i is homogeneous by A1,Def4;
    end;
    suppose A2: not i in dom f;
      let x be FinSequence; thus thesis by A2,FUNCT_1:def 4,RELAT_1:60;
    end;
  end;
end;

definition let IT be UAStr;
 canceled;

attr IT is partial means :Def7:
 the charact of IT is homogeneous;
attr IT is quasi_total means :Def8:
 the charact of IT is quasi_total;
attr IT is non-empty means :Def9:
 the charact of IT <> {} & the charact of IT is non-empty;
end;

reserve A for non empty set,
        h for PartFunc of A*,A ,
        x,y for FinSequence of A,
        a for Element of A;

theorem Th4:
for x be Element of PFuncs(A*,A) st x = {<*>A} --> a holds
  <*x*> is homogeneous quasi_total non-empty
 proof let x be Element of PFuncs(A*,A) such that
  A1: x = {<*>A} --> a;
  reconsider f=x as PartFunc of A*,A by PARTFUN1:121;
  A2: for n,h st n in dom <*x*> & h = <*x*>.n holds h is homogeneous
   proof let n,h; assume
    A3: n in dom <*x*> & h =<*x*>.n;
    then n in {1} by FINSEQ_1:4,def 8;
     then h = <*x*>.1 by A3,TARSKI:def 1;
    then h = x & f is homogeneous PartFunc of A*,A by A1,Th2,FINSEQ_1:def 8;
    hence thesis;
   end;
  A4: for n,h st n in dom <*x*> & h = <*x*>.n holds h is quasi_total
   proof let n,h; assume
    A5: n in dom <*x*> & h =<*x*>.n;
    then n in {1} by FINSEQ_1:4,def 8;
     then h = <*x*>.1 by A5,TARSKI:def 1;
    then h = x & f is quasi_total PartFunc of A*,A by A1,Th2,FINSEQ_1:def 8;
    hence thesis;
   end;
      for n being set st n in dom <*x*> holds <*x*>.n is non empty
   proof let n be set; assume
         n in dom <*x*>;
    then n in {1} by FINSEQ_1:4,def 8;
     then <*x*>.n = <*x*>.1 by TARSKI:def 1;
    hence thesis by A1,FINSEQ_1:def 8;
   end;
  hence thesis by A2,A4,Def4,Def5,FUNCT_1:def 15;
 end;

registration
 cluster quasi_total partial non-empty strict non empty UAStr;
 existence
  proof
   consider A be non empty set;
   consider a be Element of A;
   reconsider w = {<*>A} --> a as Element of PFuncs(A*,A) by Th3;
   set U1 = UAStr (# A, <*w*> #);
   take U1;
   A1: the charact of U1 is quasi_total &
      the charact of U1 is homogeneous & the charact of U1 is non-empty
    by Th4;
       the charact of U1 <> {}
    proof assume A2: the charact of U1 = {};
         len(the charact of U1) = 1 by FINSEQ_1:56;
     hence contradiction by A2,FINSEQ_1:25;
    end;
   hence thesis by A1,Def7,Def8,Def9;
  end;
end;

registration let U1 be partial UAStr;
 cluster the charact of U1 -> homogeneous;
 coherence by Def7;
end;

registration let U1 be quasi_total UAStr;
 cluster the charact of U1 -> quasi_total;
 coherence by Def8;
end;

registration let U1 be non-empty UAStr;
 cluster the charact of U1 -> non-empty non empty;
 coherence by Def9;
end;

definition
mode Universal_Algebra is quasi_total partial non-empty non empty UAStr;
end;

reserve U1 for partial non-empty non empty UAStr;

definition
  let f be homogeneous Relation;
  func arity(f) -> Nat means
     for x being FinSequence st x in dom f holds it = len x
    if ex x being FinSequence st x in dom f
    otherwise it = 0;
 consistency;
 existence
  proof
   thus (ex x being FinSequence st x in dom f) implies
      ex n st for x being FinSequence st x in dom f holds n = len x
    proof given x being FinSequence such that
A1:   x in dom f;
     take n = len x;
     let y be FinSequence; assume y in dom f;
     hence n = len y by A1,Def1;
    end;
   thus thesis;
  end;
 uniqueness
  proof
   let n,m;
   hereby given x0 being FinSequence such that
A2:  x0 in dom f;
     assume that A3: (for x being FinSequence st x in dom f holds n = len x) &
                     for x being FinSequence st x in dom f holds m = len x;
       n = len x0 & m = len x0 by A3,A2;
     hence n = m;
   end;
   thus thesis;
  end;
end;

definition
  let f be homogeneous Function;
  redefine func arity(f) -> Element of NAT;
  coherence by ORDINAL1:def 13;
end;

theorem Th5:
  n in dom the charact of U1 implies
  (the charact of U1).n is
   PartFunc of (the carrier of U1)*,the carrier of U1
 proof
  set o = the charact of U1; assume
       n in dom o;
  then o.n in rng o & rng o c= PFuncs((the carrier of U1)*, the carrier of U1)
   by FINSEQ_1:def 4,FUNCT_1:def 5;
hence thesis by PARTFUN1:121;
 end;

definition let U1;
func signature U1 ->FinSequence of NAT means
     len it = len the charact of U1 &
    for n st n in dom it holds
    for h be homogeneous non empty
    PartFunc of (the carrier of U1 )*,the carrier of U1
     st h = (the charact of U1).n
    holds it.n = arity(h);
 existence
  proof
   defpred P[Nat,set] means
   for h be homogeneous non empty
      PartFunc of (the carrier of U1)*,the carrier of U1
       st h = (the charact of U1).$1
        holds $2 = arity(h);
   A1: now let m be Element of NAT; assume
            m in Seg len the charact of U1;
        then m in dom the charact of U1 by FINSEQ_1:def 3;
        then reconsider H=(the charact of U1).m as homogeneous non empty
         PartFunc of (the carrier of U1 )*,the carrier of U1 by Th5,FUNCT_1:def
15;
        reconsider n=arity(H) as Element of NAT;
        take n;
        thus P[m,n];
       end;
   consider p be FinSequence of NAT such that
   A2: dom p = Seg(len the charact of U1) and
   A3: for m being Element of NAT st m in
 Seg(len the charact of U1) holds P[m,p.m] from FINSEQ_1:sch 5(A1);
   take p;
thus len p = len the charact of U1 by A2,FINSEQ_1:def 3;
   let n; assume
   A4: n in dom p;
   let h be homogeneous non empty
          PartFunc of (the carrier of U1 )*,the carrier of U1; assume
       h = (the charact of U1).n;
   hence p.n = arity(h) by A2,A3,A4;
  end;
 uniqueness
  proof
   let x,y be FinSequence of NAT; assume that
   A5: len x = len the charact of U1 and
   A6: for n st n in dom x holds for h be homogeneous non empty
        PartFunc of (the carrier of U1 )*,the carrier of U1
         st h = (the charact of U1).n
         holds x.n = arity(h) and
   A7: len y = len the charact of U1 and
   A8: for n st n in dom y holds for h be homogeneous non empty
         PartFunc of (the carrier of U1 )*,the carrier of U1
          st h = (the charact of U1).n
          holds y.n = arity(h);
        now let m be Element of NAT; assume
         1<=m & m<=len x;
     then m in Seg len x by FINSEQ_1:3;
     then A9:  m in dom the charact of U1 & m in dom x & m in dom y by A5,A7,
     FINSEQ_1:def 3;
     then reconsider h=(the charact of U1).m
       as homogeneous non empty
        PartFunc of (the carrier of U1 )*,the carrier of U1 by Th5,FUNCT_1:def
15;
         x.m=arity(h) & y.m=arity(h) by A6,A8,A9;
     hence x.m=y.m;
    end;
   hence thesis by A5,A7,FINSEQ_1:18;
  end;
end;

begin :: Addenda

:: from MSSUBLAT, 2007.05.13, A.T.

registration
let U0 be Universal_Algebra;
cluster the charact of U0 -> Function-yielding;
 coherence
  proof
   let x be set; assume x in dom (the charact of U0);
   thus (the charact of U0).x is Function;
  end;
end;

