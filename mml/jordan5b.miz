:: The Ordering of Points on a Curve, Part { I }
::  by Adam Grabowski and Yatsuka Nakamura
::
:: Received September 10, 1997
:: Copyright (c) 1997 Association of Mizar Users

environ

 vocabularies ARYTM_1, BORSUK_1, PRE_TOPC, EUCLID, MCART_1, TOPREAL1, RCOMP_1,
      FINSEQ_1, RELAT_1, TOPS_2, FUNCT_1, TOPMETR, ARYTM_3, SUBSET_1, BOOLE,
      TREAL_1, SEQ_1, FUNCT_4, COMPTS_1, TARSKI, ORDINAL2, PCOMPS_1, METRIC_1,
      JORDAN3, RFINSEQ, FINSEQ_5, GROUP_2, SEQM_3, GOBOARD5, GOBOARD2, TREES_1,
      TOPREAL4, GOBOARD1, MATRIX_1, FINSEQ_4, ARYTM, NAT_1, COMPLEX1;
 notations TARSKI, XBOOLE_0, ORDINAL1, SUBSET_1, NUMBERS, XCMPLX_0, XREAL_0,
      REAL_1, NAT_1, NAT_D, BINARITH, RCOMP_1, RELAT_1, FUNCT_1, RELSET_1,
      PARTFUN1, FUNCT_2, FINSEQ_1, TOPMETR, FINSEQ_4, JORDAN3, STRUCT_0,
      TOPREAL1, RFINSEQ, PRE_TOPC, TOPS_2, COMPTS_1, BORSUK_1, EUCLID, TREAL_1,
      METRIC_1, GOBOARD1, GOBOARD2, MATRIX_1, TOPREAL4, GOBOARD5, FUNCT_4,
      FINSEQ_5, PCOMPS_1, XXREAL_0;
 constructors FUNCT_4, XXREAL_0, REAL_1, NAT_1, SEQ_4, RCOMP_1, FINSEQ_4,
      RFINSEQ, BINARITH, FINSEQ_5, TOPS_2, COMPTS_1, TREAL_1, TOPREAL4,
      GOBOARD2, GOBOARD5, JORDAN3, NAT_D;
 registrations BORSUK_1, FUNCT_1, PRE_TOPC, RELSET_1, GOBOARD5, GOBOARD2,
      STRUCT_0, TOPMETR, TOPREAL1, METRIC_1, INT_1, XREAL_0, MEMBERED,
      XXREAL_0, FUNCT_2, ORDINAL1, ARYTM_3;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, JORDAN3, TOPREAL4, XBOOLE_0, PRE_TOPC, STRUCT_0, RELAT_1;
 theorems BORSUK_1, COMPTS_1, EUCLID, FINSEQ_3, FINSEQ_4, FUNCT_1, FUNCT_2,
      RELAT_1, JORDAN3, GOBOARD1, NAT_1, GOBOARD9, FUNCT_4, TOPMETR2, GOBOARD2,
      PRE_TOPC, RCOMP_1, REAL_1, HEINE, PCOMPS_1, SPPOL_2, TARSKI, TOPMETR,
      TOPREAL1, TOPREAL3, RFUNCT_2, BINARITH, RFINSEQ, FINSEQ_5, GOBOARD5,
      GOBOARD7, FINSEQ_1, TOPS_2, TREAL_1, JORDAN5A, ZFMISC_1, JORDAN4,
      PARTFUN2, INT_1, RELSET_1, FINSEQ_6, XBOOLE_0, XBOOLE_1, XCMPLX_1,
      FINSEQ_2, XREAL_1, XXREAL_0;
 schemes NAT_1;

begin :: Preliminaries

theorem Th1:
 for i1 being Element of NAT st 1 <= i1 holds i1-'1<i1
proof let i1 be Element of NAT;
 assume A1: 1 <= i1;
     i1 - 1 < i1 - 0 by REAL_1:92;
 hence thesis by A1,BINARITH:50;
end;

theorem
  for i, k being Element of NAT holds i+1 <= k implies 1 <= k-'i
proof
 let i, k be Element of NAT;
 assume i+1 <= k;
 then i+1 -' i <= k -' i by BINARITH:60;
 hence thesis by BINARITH:39;
end;

theorem
  for i, k being Element of NAT holds 1 <= i & 1 <= k implies (k-'i)+1 <= k
proof
 let i, k be Element of NAT;
 assume A1: 1 <= i & 1 <= k;
 then (k-'i) <= k -' 1 by BINARITH:59;
 then (k-'i)+1 <= k -' 1+1 by XREAL_1:8;
 hence thesis by A1,BINARITH:53;
end;

Lm1:
 for r be real number st 0 <= r & r <= 1 holds 0 <= 1-r & 1-r <= 1
proof
 let r be real number; assume 0 <= r & r <= 1;
 then 1-1 <= 1-r & 1-r <= 1-0 by REAL_1:92;
 hence thesis;
end;

theorem
   for r being real number st r in the carrier of I[01] holds
    1 - r in the carrier of I[01]
proof
  let r be real number;
  assume r in the carrier of I[01];
  then 0 <= r & r <= 1 by BORSUK_1:86;
  then 0 <= 1-r & 1-r <= 1 by Lm1;
  hence thesis by BORSUK_1:86;
end;

theorem
  for p, q, p1 being Point of TOP-REAL 2 st p`2 <> q`2 & p1 in LSeg (p, q)
   holds
    ( p1`2 = p`2 implies p1 = p )
proof
 let p, q, p1 be Point of TOP-REAL 2;
 assume A1: p`2 <> q`2 & p1 in LSeg (p, q);
 assume A2: p1`2 = p`2;
 assume A3: p1 <> p;
     p1 in { (1-l)*p + l*q where l is Real : 0 <= l & l <= 1 }
    by A1,TOPREAL1:def 3;
  then consider l1 be Real such that
A4:  p1 = (1-l1)*p + l1*q & 0 <= l1 & l1 <= 1;
A5: (1-l1)*p + l1*q = |[ ((1-l1)*p)`1 + (l1*q)`1 , ((1-l1)*p)`2 + (l1*q)`2 ]|
       by EUCLID:59;
A6: (1-l1)*p = |[ (1-l1)*p`1, (1-l1)*p`2 ]| &
    l1*q = |[ l1*q`1, l1*q`2 ]| by EUCLID:61;
     p`2 = ( (1-l1)*p )`2 + ( l1*q )`2 by A2,A4,A5,EUCLID:56
    .= (1-l1)*p`2 + (l1*q)`2 by A6,EUCLID:56
    .= (1-l1)*p`2 + l1*q`2 by A6,EUCLID:56;
 then (1 - (1-l1))*p`2 = l1*q`2;
 then l1 = 0 by A1,XCMPLX_1:5;
 then p1 = 1*p + 0.REAL 2 by A4,EUCLID:33
   .= p + 0.REAL 2 by EUCLID:33
   .= p by EUCLID:31;
 hence thesis by A3;
end;

theorem
  for p, q, p1 being Point of TOP-REAL 2 st p`1 <> q`1 & p1 in LSeg (p, q)
   holds
    ( p1`1 = p`1 implies p1 = p )
proof
 let p, q, p1 be Point of TOP-REAL 2;
 assume A1: p`1 <> q`1 & p1 in LSeg (p, q);
 assume A2: p1`1 = p`1;
 assume A3: p1 <> p;
     p1 in { (1-l)*p + l*q where l is Real : 0 <= l & l <= 1 }
                     by A1,TOPREAL1:def 3;
  then consider l1 be Real such that
A4:  p1 = (1-l1)*p + l1*q & 0 <= l1 & l1 <= 1;
A5: (1-l1)*p + l1*q = |[ ((1-l1)*p)`1 + (l1*q)`1 , ((1-l1)*p)`2 + (l1*q)`2 ]|
       by EUCLID:59;
A6: (1-l1)*p = |[ (1-l1)*p`1, (1-l1)*p`2 ]| &
    l1*q = |[ l1*q`1, l1*q`2 ]| by EUCLID:61;
     p`1 = ( (1-l1)*p )`1 + ( l1*q )`1 by A2,A4,A5,EUCLID:56
    .= (1-l1)*p`1 + (l1*q)`1 by A6,EUCLID:56
    .= (1-l1)*p`1 + l1*q`1 by A6,EUCLID:56;
 then (1 - (1-l1))*p`1 = l1*q`1;
 then l1 = 0 by A1,XCMPLX_1:5;
 then p1 = 1*p + 0.REAL 2 by A4,EUCLID:33
   .= p + 0.REAL 2 by EUCLID:33
   .= p by EUCLID:31;
 hence thesis by A3;
end;

Lm2:
  for P being Point of I[01] holds P is Real
proof
 let P be Point of I[01];
     P in [.0,1.] by BORSUK_1:83;
 hence thesis;
end;

theorem Th7:
 for f being FinSequence of TOP-REAL 2,
     P being non empty Subset of TOP-REAL 2,
     F being Function of I[01], (TOP-REAL 2) | P,
     i being Element of NAT st 1 <= i & i+1 <= len f & f is_S-Seq & P = L~f &
       F is_homeomorphism & F.0 = f/.1 & F.1 = f/.len f
      ex p1, p2 being Real st p1 < p2 &
       0 <= p1 & p1 <= 1 & 0 <= p2 & p2 <= 1 &
       LSeg (f, i) = F.:[.p1, p2.] & F.p1 = f/.i & F.p2 = f/.(i+1)
proof
 let f be FinSequence of TOP-REAL 2,
     P be non empty Subset of TOP-REAL 2,
     Ff be Function of I[01], (TOP-REAL 2)|P,
     i be Element of NAT;
 assume A1: 1 <= i & i+1 <= len f & f is_S-Seq & P = L~f &
            Ff is_homeomorphism & Ff.0 = f/.1 & Ff.1 = f/.len f;
then A2: f is one-to-one by TOPREAL1:def 10;
A3: the carrier of Closed-Interval-TSpace(0, 1/2) = [.0,1/2.] &
      the carrier of Closed-Interval-TSpace(1/2,1) = [.1/2,1.]
          by TOPMETR:25;
A4: [#]Closed-Interval-TSpace(0, 1/2) = [.0,1/2.] &
      [#]Closed-Interval-TSpace(1/2,1) = [.1/2,1.] by TOPMETR:25;
A5: len f >= 2 by A1,TOPREAL1:def 10;
 deffunc Q(Element of NAT) = L~(f|($1+2));

 defpred ARC[Element of NAT] means
 1 <= $1 + 2 & $1 + 2 <= len f implies
 ex NE being non empty Subset of TOP-REAL 2 st NE = Q($1) &
  for j be Element of NAT
    for F being Function of I[01], (TOP-REAL 2)|NE st
     1 <= j & j+1 <= $1+2 &
     F is_homeomorphism & F.0 = f/.1 & F.1 = f/.($1+2)
  ex p1, p2 be Real st p1 < p2 &
    0 <= p1 & p1 <= 1 & 0 <= p2 & p2 <= 1 & LSeg (f, j) = F.:[.p1, p2.]
     & F.p1 = f/.j & F.p2 = f/.(j+1);

  reconsider h1 = len f - 2 as Element of NAT by A5,INT_1:18;
     A6: f|(len f) = f|(Seg len f) by FINSEQ_1:def 15
           .= f|(dom f) by FINSEQ_1:def 3
           .= f by RELAT_1:97;
A7: ARC[0]
proof
  assume A8: 1 <= 0 + 2 & 0 + 2 <= len f;
then A9: 1 <= len (f|2) & 2 <= len (f|2) by FINSEQ_1:80;
  then reconsider NE = Q(0) as non empty Subset of TOP-REAL 2
by TOPREAL1:29;
  take NE;
  thus NE = Q(0);
  let j be Element of NAT;
  let F be Function of I[01], (TOP-REAL 2)|NE;
  assume
A10:   1 <= j & j+1 <= 0+2 &
     F is_homeomorphism & F.0 = f/.1 & F.1 = f/.(0+2);
    then j <= 1+1-1 by XREAL_1:21;
then A11: j = 1 by A10,XXREAL_0:1;
A12: len (f|2) = 2 by A8,FINSEQ_1:80;
A13: 1 in dom (f|2) & 2 in dom (f|2) by A9,FINSEQ_3:27;
then A14: (f|2)/.1 = (f|2).1 & (f|2)/.2 = (f|2).2 by FINSEQ_4:def 4;
A15: (f|2)/.1 = f/.1 by A13,FINSEQ_4:85;
A16: 1 + 1 <= len f by A8;
A17: rng F = [#]((TOP-REAL 2)|NE) by A10,TOPS_2:def 5
       .= L~(f|2) by PRE_TOPC:def 10
       .= L~<* (f|2)/.1, (f|2)/.2 *> by A12,A14,FINSEQ_1:61
       .= LSeg ((f|2)/.1, (f|2)/.2) by SPPOL_2:21
       .= LSeg (f/.1, f/.2) by A13,A15,FINSEQ_4:85
       .= LSeg (f, 1) by A16,TOPREAL1:def 5;
 take 0, 1;
 thus thesis by A10,A11,A17,BORSUK_1:83,FUNCT_2:45;
end;
A18: for n being Element of NAT st ARC[n] holds ARC[n+1]
proof
 let n be Element of NAT; assume
A19: ARC[n];
 assume A20: 1 <= n + 1 + 2 & n + 1 + 2 <= len f;
     A21: 2 <= n + 2 by NAT_1:11;
       n+2 <= n+2+1 by NAT_1:11;
 then consider NE being non empty Subset of TOP-REAL 2 such that
A22: NE = Q(n) &
  for j be Element of NAT
    for F being Function of I[01], (TOP-REAL 2)|NE st
     1 <= j & j+1 <= n+2 &
     F is_homeomorphism & F.0 = f/.1 & F.1 = f/.(n+2)
  ex p1, p2 be Real st p1 < p2 &
    0 <= p1 & p1 <= 1 & 0 <= p2 & p2 <= 1 & LSeg (f, j) = F.:[.p1, p2.]
     & F.p1 = f/.j & F.p2 = f/.(j+1) by A19,A20,A21,XXREAL_0:2;
A23:  len (f|(n+1+2)) = n+1+2 by A20,FINSEQ_1:80;
A24:   n + 1 + 2 = n + 2 + 1;
A25: 1 <= n + 1 + 1 & n + 1 + 1 <= n + 2 + 1 & n + 2 + 1 <= len f
      by A20,NAT_1:11;
then A26:  1 <= n + 1 + 1 & n + 1 + 1 <= len f by XXREAL_0:2;
A27:  1 <= n+2 & n+2 <= len f by A25,XXREAL_0:2;
A28: n+2 in dom (f|(n+3)) by A23,A25,FINSEQ_3:27;
then A29: f/.(n+2) = (f|(n+3))/.(n+2) by FINSEQ_4:85;
A30: len (f|(n+3)) >= 2 by A23,NAT_1:11;
  then reconsider NE1 = Q(n+1) as non empty Subset of TOP-REAL 2
by TOPREAL1:29;
  take NE1;
  thus NE1 = Q(n+1);
  let j be Element of NAT, G be Function of I[01], (TOP-REAL 2)|NE1;
  assume
A31:  1 <= j & j+1 <= n+1+2 &
       G is_homeomorphism & G.0 = f/.1 & G.1 = f/.(n+1+2);
then A32: G is one-to-one & rng G = [#]((TOP-REAL 2) | Q(n+1)) &
       dom G = [#]I[01] by TOPS_2:def 5;
then A33: rng G = L~(f|(n+3)) by PRE_TOPC:def 10;
  set pp = G".(f/.(n+2));
A34:  pp = (G qua Function)".(f/.(n+2)) by A32,TOPS_2:def 4;
        n+2 <= len (f|(n+2)) & 1 <= len (f|(n+2)) by A27,FINSEQ_1:80;
then A35: n+2 in dom (f|(n+2)) & 1 in dom (f|(n+2))
          by A25,FINSEQ_3:27;
A37:  f/.(n+2) in rng G by A28,A29,A30,A33,GOBOARD1:16;
then A38:  pp in dom G & f/.(n+2) = G.pp by A32,A34,FUNCT_1:54;
  then reconsider pp as Real by A32,BORSUK_1:83;
A39:  n + 1 + 1 <> n + 2 + 1;
A40:  n+2 <> n+3;
A41: n+2 in dom f & n+3 in dom f by A20,A27,FINSEQ_3:27;
A42:  1 <> pp
  proof
   assume pp = 1;
   then f/.(n+2) = f/.(n+1+2) by A31,A32,A34,A37,FUNCT_1:54;
   hence thesis by A2,A40,A41,PARTFUN2:17;
  end;
A43:  0 <= pp & pp <= 1 by A38,BORSUK_1:86;
then A44:    pp < 1 by A42,REAL_1:def 5;
  set pn = f/.(n+2), pn1 = f/.(n+2+1);
      2 <= n+2 by NAT_1:11;
then A45:  pn = (f|(n+2))/.(n+2) & (f|(n+2))/.1 = f/.1 &
      len (f|(n+2)) = n+2 & f|(n+2) is_S-Seq
        by A1,A27,A35,FINSEQ_1:80,FINSEQ_4:85,JORDAN3:37;
    then NE is_an_arc_of (f|(n+2))/.1, pn by A22,TOPREAL1:31;
  then consider F be Function of I[01], (TOP-REAL 2)|NE such that
A46:  F is_homeomorphism & F.0 = f/.1 & F.1 = pn by A45,TOPREAL1:def 2;
 n + 1 + 1 in dom f & n + 2 + 1 in dom f by A20,A26,FINSEQ_3:27;
     then pn <> pn1 by A2,A39,PARTFUN2:17;
  then LSeg(pn, pn1) is_an_arc_of pn, pn1 by TOPREAL1:15;
  then consider F' be Function of I[01], (TOP-REAL 2)|LSeg(pn, pn1) such that
A47:   F' is_homeomorphism & F'.0 = pn & F'.1 = pn1 by TOPREAL1:def 2;
  set Ex1 = P[01]( 0, 1/2, (#)(0,1),(0,1)(#) ),
      Ex2 = P[01]( 1/2, 1, (#)(0,1),(0,1)(#) );
  set F1 = F * Ex1, F1' = F' * Ex2;
A48: Ex1 is_homeomorphism & Ex2 is_homeomorphism by TREAL_1:20;
then A49:dom Ex1 = [#]Closed-Interval-TSpace(0,1/2) by TOPS_2:def 5;
then A50:dom Ex1 = [.0,1/2.] by TOPMETR:25;
       dom F = [#]I[01] by A46,TOPS_2:def 5;
then A51: rng Ex1 = dom F by A48,TOPMETR:27,TOPS_2:def 5;
   then rng F1 = rng F by RELAT_1:47;
then A52: rng F1 = [#] ((TOP-REAL 2) | NE) by A46,TOPS_2:def 5;
then A53:rng F1 = Q(n) by A22,PRE_TOPC:def 10;
   dom F1 = the carrier of Closed-Interval-TSpace(0, 1/2) &
        rng F1 = the carrier of ((TOP-REAL 2) | NE) by A52,A49,A51,RELAT_1:46;
  then reconsider F1 as Function
 of Closed-Interval-TSpace (0,1/2), (TOP-REAL 2)|NE
by FUNCT_2:def 1;
A55: F1 is_homeomorphism by A46,A48,TOPMETR:27,TOPS_2:71;
then A56: rng F1 = [#]((TOP-REAL 2)|NE) by TOPS_2:def 5
          .= Q(n) by A22,PRE_TOPC:def 10;
A57:dom Ex2 = [#]Closed-Interval-TSpace(1/2, 1) by A48,TOPS_2:def 5;
       dom F' = [#]I[01] by A47,TOPS_2:def 5;
then A58: rng Ex2 = dom F' by A48,TOPMETR:27,TOPS_2:def 5;
   then rng F1' = rng F' by RELAT_1:47;
then A59: rng F1' = [#] ((TOP-REAL 2) | LSeg(pn, pn1)) by A47,TOPS_2:def 5;
   dom F1' = the carrier of Closed-Interval-TSpace(1/2, 1) &
        rng F1' = the carrier of ((TOP-REAL 2) | LSeg(pn, pn1))
    by A59,A57,A58,RELAT_1:46;
  then reconsider F1' as Function of Closed-Interval-TSpace (1/2,1),
                 (TOP-REAL 2)|LSeg(pn, pn1) by FUNCT_2:def 1;
A61:  F1' is_homeomorphism by A47,A48,TOPMETR:27,TOPS_2:71;
then A62: rng F1' = [#]((TOP-REAL 2)|LSeg(pn, pn1)) by TOPS_2:def 5
           .= LSeg(pn, pn1) by PRE_TOPC:def 10;
  set FF = F1 +* F1';
   reconsider T1 = Closed-Interval-TSpace(0,1/2),
              T2 = Closed-Interval-TSpace(1/2, 1) as SubSpace of I[01]
        by TOPMETR:27,TREAL_1:6;
A63: Q(n+1) = Q(n) \/ LSeg(pn, pn1) by A41,TOPREAL3:45;
A64:the carrier of ((TOP-REAL 2)|Q(n+1)) =
        [#]((TOP-REAL 2)|Q(n+1))
            .= Q(n+1) by PRE_TOPC:def 10;
   then dom F1 = the carrier of T1 &
  rng F1 c= the carrier of ((TOP-REAL 2)|NE1)
    by A53,A63,XBOOLE_1:7,A49,A51,RELAT_1:46;
   then reconsider g11 = F1 as Function
 of T1, ((TOP-REAL 2)|NE1) by FUNCT_2:def 1
,RELSET_1:11;
       dom F1' = the carrier of T2 &
    rng F1' c= the carrier of ((TOP-REAL 2)|NE1)
        by A62,A63,A64,XBOOLE_1:7,A57,A58,RELAT_1:46;
   then reconsider g22 = F1' as Function
 of T2, ((TOP-REAL 2)|NE1) by FUNCT_2:def 1
,RELSET_1:11;
A65: [.0,1/2.] = dom F1 & [.1/2, 1.] = dom F1' by A4,A55,A61,TOPS_2:def 5;
        1/2 in { l where l is Real : 0 <= l & l <= 1/2 } &
      1/2 in { l where l is Real : 1/2 <= l & l <= 1 };
then A66: 1/2 in dom F1 & 1/2 in dom F1' by A65,RCOMP_1:def 1;
A67:  dom FF = [.0,1/2.] \/ [.1/2,1.] by A65,FUNCT_4:def 1
       .= [.0,1.] by RCOMP_1:50;
A68:  I[01] is compact by HEINE:11,TOPMETR:27;
A69:  (TOP-REAL 2)|NE1 is_T2 by TOPMETR:3;
A70:  dom FF = [#]I[01] by A67,BORSUK_1:83;
A71: Ex1.(1/2) = Ex1.(0,1/2)(#) by TREAL_1:def 2
             .= (0,1)(#) by TREAL_1:16
             .= 1 by TREAL_1:def 2;
A72: Ex2.(1/2) = Ex2.(#)(1/2,1) by TREAL_1:def 1
             .= (#)(0,1) by TREAL_1:16
             .= 0 by TREAL_1:def 1;
A73: F1.(1/2) = f/.(n+2) by A46,A66,A71,FUNCT_1:22;
A74: F1'.(1/2) = f/.(n+2) by A47,A66,A72,FUNCT_1:22;
B75:    [.0,1/2.] /\ [.1/2,1.] = [.1/2,1/2.] by RCOMP_1:51;
then A75: dom F1 /\ dom F1' = {1/2} by A65,RCOMP_1:14;
A76:    for x being set holds x in Q(n) /\ LSeg(pn,pn1) iff x = pn
        proof
         let x be set;
         thus x in Q(n) /\ LSeg(pn,pn1) implies x = pn
          proof
           assume x in Q(n) /\ LSeg(pn,pn1);
           then A77: x in LSeg(pn,pn1) & x in Q(n) by XBOOLE_0:def 3;
           then x in union { LSeg(f|(n+2),k) where k is Element of NAT :
             1 <= k & k+1 <= len(f|(n+2)) }
            by TOPREAL1:def 6;
           then consider X being set such that
          A78: x in X and
          A79: X in { LSeg(f|(n+2),k) where k is Element of NAT :
                   1 <= k & k+1 <= len(f|(n+2)) }
             by TARSKI:def 4;
           consider k be Element of NAT such that
          A80: X = LSeg(f|(n+2),k) and
          A81: 1 <= k & k+1 <= len(f|(n+2)) by A79;
         A82: len(f|(n+2)) = n+(1+1) by A26,FINSEQ_1:80;
A83:      len(f|(n+2)) = n+1+1 by A26,FINSEQ_1:80;
then A84:     k <= n+1 by A81,XREAL_1:8;
A85:    f is s.n.c. by A1,TOPREAL1:def 10;
A86:    f is unfolded by A1,TOPREAL1:def 10;
             now assume
A87:          k < n+1;
         A88: 1 <= 1+k & k+1 <= n+(1+1)
             by A26,A81,FINSEQ_1:80,NAT_1:11;
A89:      k+1 <= len f by A26,A81,A83,XXREAL_0:2;
            A90: k+1 < n+1+1 by A87,XREAL_1:8;
         set p1' = f/.k, p2' = f/.(k+1);
             n+1 <= n+1+1 & n+1+1 = n+(1+1) by NAT_1:11;
         then k <= n+2 by A87,XXREAL_0:2;
         then k in Seg len(f|(n+2)) & k+1 in Seg len(f|(n+2))
          by A81,A82,A88,FINSEQ_1:3;
          then A91: k in dom(f|(n+2)) & k+1 in dom(f|(n+2)) by FINSEQ_1:def 3;
         then A92: (f|(n+2))/.k = p1' by FINSEQ_4:85;
         A93: (f|(n+2))/.(k+1) = p2' by A91,FINSEQ_4:85;
         A94: LSeg(f,k) = LSeg(p1',p2') by A81,A89,TOPREAL1:def 5
                      .= LSeg(f|(n+2),k) by A81,A92,A93,TOPREAL1:def 5;
             LSeg(f,n+2) = LSeg(pn,pn1) by A25,TOPREAL1:def 5;
         then LSeg(f|(n+2),k) misses LSeg(pn,pn1)
                  by A85,A90,A94,TOPREAL1:def 9;
         then LSeg(f|(n+2),k) /\ LSeg(pn,pn1) = {} by XBOOLE_0:def 7;
          hence contradiction by A77,A78,A80,XBOOLE_0:def 3;
         end;
         then A95: k = n + 1 by A84,REAL_1:def 5;
A96:      1 <= n+1 & 1 <= n+1+1 & n+1+1 <= len f
              by A25,A81,A84,XXREAL_0:2;
         set p1' = f/.(n+1), p2' = f/.(n+1+1);
A97:    n+1 <= n+1+1 & n+1+1 = n+(1+1) & 1 <= 1+n & 1 <= 1+(n+1)
          by NAT_1:11;
         then n+1 in Seg len(f|(n+2)) & n+1+1 in Seg len(f|(n+2))
          by A82,FINSEQ_1:3;
         then A98: n+1 in dom(f|(n+2)) & n+1+1 in dom(f|(n+2)) by
FINSEQ_1:def 3;
         then A99: (f|(n+2))/.(n+1) = p1' by FINSEQ_4:85;
         A100: (f|(n+2))/.(n+1+1) = p2' by A98,FINSEQ_4:85;
         A101: LSeg(f,n+1) = LSeg(p1',p2') by A96,TOPREAL1:def 5
               .= LSeg(f|(n+2),n+1) by A82,A97,A99,A100,TOPREAL1:def 5;
          LSeg(pn,pn1) = LSeg(f,n+1+1)
           by A25,TOPREAL1:def 5;
         then A102: x in LSeg(f,n+1) /\ LSeg(f,n+1+1)
           by A77,A78,A80,A95,A101,XBOOLE_0:def 3;
             1 <= n+1 by NAT_1:11;
         then LSeg(f,n+1) /\ LSeg(f,n+1+1) = {f/.(n+1+1)}
            by A20,A86,TOPREAL1:def 8;
         hence x = pn by A102,TARSKI:def 1;
          end;
         assume A103: x = pn;
          then A104: x in LSeg(pn,pn1) by TOPREAL1:6;
          A105: len(f|(n+2)) = n+2 by A26,FINSEQ_1:80;
            then dom(f|(n+2)) = Seg(n+2) & n + 2 in Seg(n+2)
             by A25,FINSEQ_1:3,def 3;
            then A106: x = (f|(n+2))/.(n+1+1) by A103,FINSEQ_4:85;
       1 <= n+1 by NAT_1:11;
          then A107: x in LSeg(f|(n+2),n+1) by A105,A106,TOPREAL1:27;
               1 <= 1 + n & n + 1 + 1 <= n + (1 + 1) & n+2 <= len f
              by A25,XXREAL_0:2,NAT_1:11;
            then 1 <= n+1 & n+1+1 <= len(f|(n+2)) by FINSEQ_1:80;
            then LSeg(f|(n+2),n+1) in {LSeg(f|(n+2),k)
            where k is Element of NAT :
                   1 <= k & k+1 <= len(f|(n+2))};
          then x in union {LSeg(f|(n+2),k) where k is Element of NAT :
                   1 <= k & k+1 <= len(f|(n+2))}
           by A107,TARSKI:def 4;
          then x in Q(n) by TOPREAL1:def 6;
         hence x in Q(n) /\ LSeg(pn,pn1) by A104,XBOOLE_0:def 3;
        end;
         f/.(n+2) in rng F1' by A66,A74,FUNCT_1:def 5;
then A108:  {f/.(n+2)} c= rng F1' by ZFMISC_1:37;
A109:  F1.:(dom F1 /\ dom F1') = Im(F1,1/2) by B75,A65,RCOMP_1:14
                 .= {f/.(n+2)} by A73,A66,FUNCT_1:117;
then A110:  rng FF = Q(n) \/ LSeg(pn, pn1) by A56,A62,A108,TOPMETR2:3;
then A111:  rng FF = [#]((TOP-REAL 2)|Q(n+1)) by A41,TOPREAL3:45,A64;
         dom FF = the carrier of I[01] &
    rng FF c= the carrier of ((TOP-REAL 2)|Q(n+1))
      by A56,A62,A63,A64,A67,A108,A109,BORSUK_1:83,TOPMETR2:3;
  then reconsider FF as Function of I[01], (TOP-REAL 2)|NE1 by FUNCT_2:4;
A112: rng Ex1 = [#] Closed-Interval-TSpace(0,1) by A48,TOPS_2:def 5;
        0 in { l where l is Real : 0 <= l & l <= 1/2 } &
     1/2 in { l where l is Real : 0 <= l & l <= 1/2 };
then A113: 0 in dom Ex1 & 1/2 in dom Ex1 & Ex1 is one-to-one continuous
         by A48,A50,RCOMP_1:def 1,TOPS_2:def 5;
A114: Ex1.0 = Ex1.(#)(0,1/2) by TREAL_1:def 1
             .= (#)(0,1) by TREAL_1:16
             .= 0 by TREAL_1:def 1;
A115: Ex1".0 = (Ex1 qua Function)".0 by A112,A113,TOPS_2:def 4
          .= 0 by A113,A114,FUNCT_1:54;
A116: Ex2.(1/2) = Ex2.(#)(1/2,1) by TREAL_1:def 1
            .= (#)(0,1) by TREAL_1:16
            .= 0 by TREAL_1:def 1;
A117: Ex1".1 = (Ex1 qua Function)".1 by A112,A113,TOPS_2:def 4
          .= 1/2 by A71,A113,FUNCT_1:54;
A118: Ex2.1 = Ex2.(1/2,1)(#) by TREAL_1:def 2
             .= (0,1)(#) by TREAL_1:16
             .= 1 by TREAL_1:def 2;
A119: LSeg(pn, pn1) = F1'.:[.1/2,1.] by A3,A62,FUNCT_2:45;
A120:  FF.(1/2) = f/.(n+2) by A66,A74,FUNCT_4:14;
A121:  for x be set st x in [.0,1/2.] & x <> 1/2 holds not x in dom F1'
  proof
   let x be set; assume A122: x in [.0,1/2.] & x <> 1/2;
   assume x in dom F1';
   then x in dom F1 /\ dom F1' by A65,A122,XBOOLE_0:def 3;
   hence thesis by A75,A122,TARSKI:def 1;
  end;
A123: FF.:[.1/2,1.] = F1'.:[.1/2,1.]
  proof
   thus FF.:[.1/2,1.] c= F1'.:[.1/2,1.]
   proof
    let a be set; assume a in FF.:[.1/2,1.];
    then consider x be set such that
A124:   x in dom FF & x in [.1/2,1.] & a = FF.x by FUNCT_1:def 12;
    FF.x = F1'.x by A65,A124,FUNCT_4:14;
    hence thesis by A65,A124,FUNCT_1:def 12;
   end;
   thus F1'.:[.1/2,1.] c= FF.:[.1/2,1.]
   proof
    let a be set; assume a in F1'.:[.1/2,1.];
    then consider x be set such that
A125:   x in dom F1' & x in [.1/2,1.] & a = F1'.x by FUNCT_1:def 12;
A126: x in dom FF by A125,FUNCT_4:13;
        a = FF.x by A125,FUNCT_4:14;
    hence thesis by A125,A126,FUNCT_1:def 12;
   end;
  end;
  set GF = G" * FF;
A127:  0 in dom FF & 1 in dom FF by A67,BORSUK_1:83,86;
         0 in { l where l is Real : 0 <= l & l <= 1/2 };
     then 0 in [.0,1/2.] by RCOMP_1:def 1;
     then not 0 in dom F1' by A121;
then A128:  FF.0 = F1.0 by FUNCT_4:12
         .= f/.1 by A46,A113,A114,FUNCT_1:23;
         1 in { l where l is Real : 1/2 <= l & l <= 1 };
then A129:  1 in dom F1' by A65,RCOMP_1:def 1;
then A130:  FF.1 = F1'.1 by FUNCT_4:14
         .= pn1 by A47,A118,A129,FUNCT_1:22;
A131:  0 in dom G & f/.1 = G.0 by A31,A32,BORSUK_1:86;
A132:  G".(f/.1) = (G qua Function)".(f/.1) by A32,TOPS_2:def 4
              .= 0 by A32,A131,FUNCT_1:54;
then A133:  GF.0 = 0 by A127,A128,FUNCT_1:23;
A134:  1 in dom G & pn1 = G.1 by A31,A32,BORSUK_1:86;
A135:  G".pn1 = (G qua Function)".pn1 by A32,TOPS_2:def 4
            .= 1 by A32,A134,FUNCT_1:54;
then A136:  GF.1 = 1 by A127,A130,FUNCT_1:23;
    reconsider ppp = 1/2 as Point of I[01] by BORSUK_1:86;
        TopSpaceMetr RealSpace is_T2 by PCOMPS_1:38;
then A137: I[01] is_T2 by TOPMETR:3,27,def 7;
A138: T1 is compact & T2 is compact by HEINE:11;
       F1 is continuous & F1' is continuous &
   (TOP-REAL 2)|NE is SubSpace of ((TOP-REAL 2)|NE1) &
    (TOP-REAL 2)|LSeg(pn, pn1) is SubSpace of (TOP-REAL 2)|NE1
      by A22,A55,A61,A63,TOPMETR:5,TOPS_2:def 5;
then A139: g11 is continuous & g22 is continuous by TOPMETR:7;
A140: [#] T1 = [.0,1/2.] by TOPMETR:25;
A141: [#] T2 = [.1/2,1.] by TOPMETR:25;
then A142:  ([#] T1) \/ ([#]T2) = [#] I[01] by  A140,RCOMP_1:50,BORSUK_1:83;
       ([#] T1) /\ ([#]T2) = {ppp} by A140,A141,TOPMETR2:1;
  then reconsider FF as continuous Function of I[01],(TOP-REAL 2)|NE1
     by A73,A74,A137,A138,A139,A142,TOPMETR2:4;
A143:  F1 is one-to-one & F1' is one-to-one by A55,A61,TOPS_2:def 5;
        for x1,x2 be set st x1 in dom F1' & x2 in dom F1 \ dom F1' holds
      F1'.x1 <> F1.x2
    proof
     let x1,x2 be set; assume A144: x1 in dom F1' & x2 in dom F1 \ dom F1';
     assume A145: F1'.x1 = F1.x2;
A146:  F1'.x1 in LSeg(pn, pn1) by A62,FUNCT_2:6,A144;
A147:  x2 in dom F1 & not x2 in dom F1' by A144,XBOOLE_0:def 4;
     then F1.x2 in NE by A22,A56,FUNCT_2:6;
     then F1.x2 in NE /\ LSeg(pn, pn1) by A145,A146,XBOOLE_0:def 3;
     then F1.x2 = pn by A22,A76;
     hence thesis by A66,A73,A143,A147,FUNCT_1:def 8;
    end;
 then FF is one-to-one by A143,TOPMETR2:2;
    then G" is_homeomorphism & FF is_homeomorphism by A31,A68,A69,A70,A111
,COMPTS_1:26,TOPS_2:70;
then A148: GF is_homeomorphism by TOPS_2:71;
then A149:GF is continuous by TOPS_2:def 5;
A150: dom GF = [#]I[01] & rng GF = [#]I[01] & GF is one-to-one
      by A148,TOPS_2:def 5;
then A151: dom (GF") = [#]I[01] & rng (GF") = [#]I[01] by TOPS_2:62;
A152: rng G = [#]((TOP-REAL 2)|Q(n+1)) by A31,TOPS_2:def 5
          .= rng FF by A63,A64,A110;
A153:  G * GF = FF
  proof
        G * (G" * FF) = (FF qua Relation) * ( G * (G" qua Function) ) by
RELAT_1:55
        .= id rng G * FF by A32,TOPS_2:65
                .= FF by A152,RELAT_1:80;
   hence thesis;
  end;
A154: 1/2 in dom GF by A150,BORSUK_1:86;
then A155: GF.(1/2) in rng GF by FUNCT_1:def 5;
A156: GF.(1/2) = G".(FF.(1/2)) by A154,FUNCT_1:22
             .= pp by A66,A74,FUNCT_4:14;
      [.pp,1.] = GF.:[.1/2,1.]
  proof
   thus [.pp,1.] c= GF.:[.1/2,1.]
   proof
    let a be set; assume a in [.pp,1.];
    then a in { l where l is Real : pp <= l & l <= 1 } by RCOMP_1:def 1;
    then consider l1 be Real such that
A157:    l1 = a & pp <= l1 & l1 <= 1;
     A158: 0 <= pp by A155,A156,BORSUK_1:86;
    set cos = GF".l1;
        l1 in dom (GF") by A151,A157,A158,BORSUK_1:86;
then A159:cos in rng (GF") by FUNCT_1:def 5;
then A160:cos in [.0,1.] by BORSUK_1:83;
A161: l1 in rng GF by A150,A157,A158,BORSUK_1:86;
A162: GF.cos = GF.((GF qua Function)".l1) by A150,TOPS_2:def 4
          .= l1 by A150,A161,FUNCT_1:57;
    reconsider cos as Real by A160;
    reconsider A3 = cos, A4 = 1, A5 = 1/2 as Point of I[01]
       by A159,BORSUK_1:86;
    reconsider A1 = GF.A3, A2 = GF.A4 as Point of I[01];
    reconsider Fhc = A1, Fh0 = A2, Fh12 = GF.A5 as Real by Lm2;
A163:cos <= 1
    proof
     assume cos > 1;
     then Fhc > Fh0 by A133,A136,A149,A150,JORDAN5A:17;
     hence thesis by A67,A130,A135,A157,A162,BORSUK_1:83,FUNCT_1:23;
    end;
        cos >= 1/2
    proof
     assume cos < 1/2;
     then Fhc < Fh12 by A133,A136,A149,A150,JORDAN5A:17;
     hence thesis by A67,A120,A157,A162,BORSUK_1:83,FUNCT_1:23;
    end;
    then cos in { l where l is Real : 1/2 <= l & l <= 1 } by A163;
    then cos in [.1/2,1.] by RCOMP_1:def 1;
    hence thesis by A150,A157,A159,A162,FUNCT_1:def 12;
   end;
   thus GF.:[.1/2,1.] c= [.pp,1.]
   proof
    let a be set; assume a in GF.:[.1/2,1.];
    then consider x be set such that
A164:  x in dom GF & x in [.1/2,1.] & a = GF.x by FUNCT_1:def 12;
        x in { l where l is Real : 1/2 <= l & l <= 1 } by A164,RCOMP_1:def 1;
    then consider l1 be Real such that
A165:         l1 = x & 1/2 <= l1 & l1 <= 1;
    reconsider ll = l1, pol = 1/2 as Point of I[01]
      by A165,BORSUK_1:86;
    reconsider A1 = GF.1[01], A2 = GF.ll, A3 = GF.pol as Point of I[01];
    reconsider A4 = A1, A5 = A2, A6 = A3 as Real by Lm2;
    A166: A4 >= A5
    proof
     per cases;
     suppose 1 <> l1;
     then 1 > l1 by A165,REAL_1:def 5;
     hence thesis by A133,A136,A149,A150,BORSUK_1:def 18,JORDAN5A:17;
     end;
     suppose 1 = l1;
     hence thesis by BORSUK_1:def 18;
    end;
    end;
        A5 >= A6
    proof
     per cases;
     suppose l1 <> 1/2;
     then l1 > 1/2 by A165,REAL_1:def 5;
     hence thesis by A133,A136,A149,A150,JORDAN5A:17;
     end;
     suppose l1 = 1/2;
     hence thesis;
    end;
    end;
    then A5 in { l where l is Real : pp <= l & l <= 1 } by A136,A156,A166,
BORSUK_1:def 18;
    hence thesis by A164,A165,RCOMP_1:def 1;
   end;
  end;
then A167: G.:[.pp,1.] = LSeg (pn, pn1) by A119,A123,A153,RELAT_1:159;
      ex p1, p2 be Real st p1 < p2 &
    0 <= p1 & p1 <= 1 & 0 <= p2 & p2 <= 1 & LSeg (f, j) = G.:[.p1, p2.]
     & G.p1 = f/.j & G.p2 = f/.(j+1)
 proof
  per cases;
  suppose j+1 <= n+2;
  then consider r1, r2 be Real such that
A168:    r1 < r2 &
    0 <= r1 & r1 <= 1 & 0 <= r2 & r2 <= 1 & LSeg (f, j) = F.:[.r1, r2.]
    & F.r1 = f/.j & F.r2 = f/.(j+1) by A22,A31,A46;
  set f1 = Ex1".r1, f2 = Ex1".r2;
A169: Ex1 is continuous one-to-one & dom Ex1 = [#]Closed-Interval-TSpace(0,1/2)
       & rng Ex1 = [#]Closed-Interval-TSpace(0,1) by A48,TOPS_2:def 5;
then A170:  f1 = (Ex1 qua Function)".r1 & f2 = (Ex1 qua Function)".r2
         by TOPS_2:def 4;
       rng Ex1 = [#]I[01] by A48,TOPMETR:27,TOPS_2:def 5
         .= the carrier of I[01];
then A171:  r1 in rng Ex1 & r2 in rng Ex1 by A168,BORSUK_1:86;
  then f1 in the carrier of Closed-Interval-TSpace(0,1/2) &
   f2 in the carrier of Closed-Interval-TSpace(0,1/2) by A169,A170,FUNCT_1:54;
then A172:  f1 in [.0,1/2.] & f2 in [.0,1/2.] by TOPMETR:25;
  then reconsider f1, f2 as Real;
  reconsider r1' = r1, r2' = r2 as Point of Closed-Interval-TSpace(0,1)
      by A168,BORSUK_1:86,TOPMETR:27;
      Ex1" is_homeomorphism by A48,TOPS_2:70;
then A173:f1 = Ex1".r1' & f2 = Ex1".r2' & Ex1".0 = 0 & Ex1".1 = 1/2 &
     Ex1" is continuous one-to-one by A115,A117,TOPS_2:def 5;
then A174: f1 < f2 by A168,JORDAN5A:16;
A175:  [.0,1/2.] c= [.0,1.] by RCOMP_1:49;
A176:  r1 = Ex1.f1 & r2 = Ex1.f2 by A169,A170,A171,FUNCT_1:54;
A177:    f1 in { l where l is Real : 0 <= l & l <= 1/2 } &
     f2 in { l where l is Real : 0 <= l & l <=
 1/2 } by A172,RCOMP_1:def 1;
    then consider ff1 be Real such that
A178:   ff1 = f1 & 0 <= ff1 & ff1 <= 1/2;
    consider ff2 be Real such that
A179:   ff2 = f2 & 0 <= ff2 & ff2 <= 1/2 by A177;
A180: Ex1.:
[.f1,f2.] = [.r1,r2.] by A48,A71,A114,A174,A176,A178,A179,JORDAN5A:21;
        F1.:[.f1,f2.] = FF.:[.f1, f2.]
    proof
     thus F1.:[.f1,f2.] c= FF.:[.f1, f2.]
    proof
     let a be set; assume a in F1.:[.f1, f2.];
     then consider x be set such that
A181:    x in dom F1 & x in [.f1, f2.] & a = F1.x by FUNCT_1:def 12;
         now per cases;
      suppose x <> 1/2;
      then not x in dom F1' by A65,A121,A181;
      hence FF.x = F1.x by FUNCT_4:12;
      end;
      suppose x = 1/2;
      hence FF.x = F1.x by A66,A73,A74,FUNCT_4:14;
     end;
     end;
     then x in dom FF & x in [.f1, f2.] & a = FF.x by A181,FUNCT_4:13;
     hence thesis by FUNCT_1:def 12;
    end;
     thus FF.:[.f1,f2.] c= F1.:[.f1, f2.]
     proof
      let a be set; assume a in FF.:[.f1, f2.];
     then consider x be set such that
A182:    x in dom FF & x in [.f1, f2.] & a = FF.x by FUNCT_1:def 12;
A183:  [.f1, f2.] c= [.0,1/2.] by A172,RCOMP_1:16;
         now per cases;
      suppose x <> 1/2;
      then not x in dom F1' by A121,A182,A183;
      hence FF.x = F1.x by FUNCT_4:12;
      end;
      suppose x = 1/2;
      hence FF.x = F1.x by A66,A73,A74,FUNCT_4:14;
     end;
     end;
     hence thesis by A65,A182,A183,FUNCT_1:def 12;
     end;
    end;
then A184: F.:[.r1, r2.] = FF.:[.f1, f2.] by A180,RELAT_1:159;
  set e1 = GF.f1, e2 = GF.f2;
      e1 in the carrier of I[01] & e2 in the carrier of I[01]
       by A172,A175,BORSUK_1:83,FUNCT_2:7;
then A185:  e1 in { l where l is Real : 0 <= l & l <= 1 } &
   e2 in { l where l is Real : 0 <= l & l <= 1 }
             by BORSUK_1:83,RCOMP_1:def 1;
  then consider l1 be Real such that
A186:  l1 = e1 & 0 <= l1 & l1 <= 1;
  consider l2 be Real such that
A187: l2 = e2 & 0 <= l2 & l2 <= 1 by A185;
    reconsider f1' = f1, f2' = f2 as Point of I[01] by A172,A175,BORSUK_1:83;
     GF.0 = 0 & GF.1 = 1 & l1 = GF.f1' & l2 = GF.f2' by A127,A128,A130,A132
,A135,A186,A187,FUNCT_1:23;
then A188:  l1 < l2 & 0 <= l1 & l1 <= 1 & 0 <= l2 & l2 <= 1
        by A149,A150,A174,A186,A187,JORDAN5A:17;
A189: GF is one-to-one & GF is continuous &
    f1 < f2 & l1 = GF.f1 & l2 = GF.f2
      by A148,A168,A173,A186,A187,JORDAN5A:16,TOPS_2:def 5;
      0 <= f1 & f2 <= 1 by A172,A175,BORSUK_1:83,86;
then A190: G.:[.l1, l2.] = G.:(GF.:[.f1, f2.])
          by A133,A136,A148,A189,JORDAN5A:21,TOPMETR:27
        .= FF.:[.f1, f2.] by A153,RELAT_1:159;
A191:  FF.f1' = F.r1'
     proof
      per cases;
      suppose A192: f1' = 1/2;
      then FF.f1' = F1'.(1/2) by A66,FUNCT_4:14
            .= F'.0 by A66,A116,FUNCT_1:22
            .= F.r1' by A46,A47,A71,A169,A170,A192,FUNCT_1:54;
      hence thesis;
      end;
      suppose f1' <> 1/2;
      then not f1' in dom F1' by A121,A172;
      then FF.f1' = F1.f1'
        by FUNCT_4:12
            .= F.(Ex1.f1') by A50,A172,FUNCT_1:23
            .= F.r1' by A169,A170,FUNCT_1:54;
      hence thesis;
     end;
     end;
A193:  FF.f2' = F.r2'
     proof
      per cases;
      suppose A194: f2' = 1/2;
      then FF.f2' = F1'.(1/2) by A66,FUNCT_4:14
            .= F'.0 by A66,A116,FUNCT_1:22
            .= F.r2' by A46,A47,A71,A169,A170,A194,FUNCT_1:54;
      hence thesis;
      end;
      suppose f2' <> 1/2;
      then not f2' in dom F1' by A121,A172;
      then FF.f2' = F1.f2' by FUNCT_4:12
            .= F.(Ex1.f2') by A50,A172,FUNCT_1:23
            .= F.r2' by A169,A170,FUNCT_1:54;
      hence thesis;
     end;
     end;
A195: G.l1 = f/.j by A67,A153,A168,A186,A191,BORSUK_1:83,FUNCT_1:22;
        G.l2 = f/.(j+1) by A67,A153,A168,A187,A193,BORSUK_1:83,FUNCT_1:22;
  hence thesis by A168,A184,A188,A190,A195;
  end;
  suppose j+1 > n+2;
  then
B196: j+1 = n+3 by A24,A31,NAT_1:9;
  then LSeg(f,j) = LSeg (pn, pn1) by A25,TOPREAL1:def 5;
  hence thesis by A31,A38,A43,A44,A167,B196;
 end;
 end;
 hence thesis;
end;
A197:  for n be Element of NAT holds ARC[n] from NAT_1:sch 1(A7,A18);
      1 <= h1 + 2 & h1 + 2 <= len f by A5,XXREAL_0:2;
 then consider NE being non empty Subset of TOP-REAL 2 such that
A198:  NE = Q(h1) &
  for j be Element of NAT
    for F being Function of I[01], (TOP-REAL 2)|NE st
     1 <= j & j+1 <= h1+2 &
     F is_homeomorphism & F.0 = f/.1 & F.1 = f/.(h1+2)
  ex p1, p2 be Real st p1 < p2 &
    0 <= p1 & p1 <= 1 & 0 <= p2 & p2 <= 1 & LSeg (f, j) = F.:[.p1, p2.]
     & F.p1 = f/.j & F.p2 = f/.(j+1) by A197;
 thus thesis by A1,A6,A198;
end;

theorem
     for f being FinSequence of TOP-REAL 2,
     Q, R being non empty Subset of TOP-REAL 2,
     F being Function of I[01], (TOP-REAL 2)|Q,
     i being Element of NAT,
     P being non empty Subset of I[01] st
     f is_S-Seq & F is_homeomorphism & F.0 = f/.1 & F.1 = f/.len f &
       1 <= i & i+1 <= len f & F.:P = LSeg (f,i) & Q = L~f & R = LSeg(f,i)
    ex G being Function of I[01]|P, (TOP-REAL 2)|R st
           G = F|P & G is_homeomorphism
proof
 let f be FinSequence of TOP-REAL 2,
     Q, R be non empty Subset of TOP-REAL 2,
     F be Function of I[01], (TOP-REAL 2)|Q,
     i be Element of NAT,
     P be non empty Subset of I[01]; assume
A1: f is_S-Seq & F is_homeomorphism & F.0 = f/.1 & F.1 = f/.len f &
       1 <= i & i+1 <= len f & F.:P = LSeg (f,i) & Q = L~f & R = LSeg(f,i);
  then consider ppi, pi1 be Real such that
A2:   ppi < pi1 & 0 <= ppi & ppi <= 1 & 0 <= pi1 & pi1 <= 1 &
    LSeg (f, i) = F.:[.ppi, pi1.] & F.ppi = f/.i & F.pi1 = f/.(i+1)
     by Th7;
   ppi in { dd where dd is Real : ppi <= dd & dd <= pi1 } &
   pi1 in { dd where dd is Real : ppi <= dd & dd <= pi1 } by A2;
  then reconsider Poz = [.ppi, pi1.] as non empty Subset of I[01]
   by A2,BORSUK_1:83,RCOMP_1:def 1,49;
A3: the carrier of (I[01]|Poz) = [#] (I[01]|Poz)
                             .= Poz by PRE_TOPC:def 10;
A4:  dom F = [#]I[01] by A1,TOPS_2:def 5
        .= [.0,1.] by BORSUK_1:83;
A5:  F is one-to-one by A1,TOPS_2:def 5;
A6: P = Poz
   proof
    thus P c= Poz by A1,A2,A4,A5,BORSUK_1:83,FUNCT_1:157;
    thus Poz c= P by A1,A2,A4,A5,BORSUK_1:83,FUNCT_1:157;
   end;
  set gg = F | P;
  reconsider gg as Function of I[01]|Poz, (TOP-REAL 2)| Q by A3,A6,FUNCT_2:38;
    reconsider SEG= LSeg (f, i) as
    non empty Subset of (TOP-REAL 2)|Q by A1;
A7: the carrier of (((TOP-REAL 2) | Q) | SEG) =
    [#](((TOP-REAL 2) | Q) | SEG)
               .= SEG by PRE_TOPC:def 10;
   A8: rng gg c= SEG
   proof
      let ii be set; assume ii in rng gg;
      then consider j be set such that
A9:     j in dom gg & ii = gg.j by FUNCT_1:def 5;
          j in dom F /\ Poz by A6,A9,RELAT_1:90;
then A10:   j in dom F by XBOOLE_0:def 3;
A11:   dom gg = Poz by A3,FUNCT_2:def 1;
      then j in dom F & F.j in LSeg (f,i) by A2,A9,A10,FUNCT_1:def 12;
      hence thesis by A6,A9,FUNCT_1:72,A2,A9,A10,FUNCT_1:def 12,A11;
    end;
  reconsider SEG as non empty Subset of (TOP-REAL 2)|Q;
A12:   ((TOP-REAL 2) | Q) | SEG = (TOP-REAL 2) | R by A1,GOBOARD9:4;
  reconsider hh' = gg as Function
 of I[01]|Poz, ((TOP-REAL 2) | Q)| SEG by A7,A8,
FUNCT_2:8;
A13:  F is continuous & F is one-to-one by A1,TOPS_2:def 5;
 then gg is continuous by A6,TOPMETR:10;
then A14:  hh' is continuous by TOPMETR:9;
A15: hh' is one-to-one by A13,FUNCT_1:84;
    reconsider hh = hh' as Function of I[01]|Poz, (TOP-REAL 2) | R
      by A12;
A16: dom hh = [#] (I[01] | Poz) by FUNCT_2:def 1;
then A17:dom hh = Poz by PRE_TOPC:def 10;
A18:rng hh = hh.:(dom hh) by  FUNCT_2:45,A16
         .= [#](((TOP-REAL 2) | Q) | SEG) by  A2,A6,A17,RFUNCT_2:5,A7;
 reconsider A = Closed-Interval-TSpace (ppi,pi1) as
        strict SubSpace of I[01] by A2,TOPMETR:27,TREAL_1:6;
A20:Poz = [#] A by A2,TOPMETR:25;
       Closed-Interval-TSpace (ppi,pi1) is compact by A2,HEINE:11;
then [#] Closed-Interval-TSpace (ppi,pi1) is compact by COMPTS_1:10;
   then for P being Subset of A st P=Poz holds P is compact by A2,TOPMETR:25;
   then Poz is compact by A20,COMPTS_1:11;
then A21: I[01]|Poz is compact by COMPTS_1:12;
  (TOP-REAL 2)|R is_T2 by TOPMETR:3;
then hh is_homeomorphism by A12,A14,A15,A16,A18,A21,COMPTS_1:26;
 hence thesis by A6;
end;

begin :: Some properties of real intervals

theorem Th9:
 for p1, p2, p being Point of TOP-REAL 2 st
  p1 <> p2 & p in LSeg(p1,p2) holds LE p,p,p1,p2
proof
 let p1, p2, p be Point of TOP-REAL 2;
 assume A1: p1 <> p2 & p in LSeg(p1,p2);
 thus LE p,p,p1,p2
 proof
  thus p in LSeg(p1,p2) & p in LSeg(p1,p2) by A1;
  let r1,r2 be Real;
  assume 0<=r1 & r1<=1 & p=(1-r1)*p1+r1*p2 &
         0<=r2 & r2<=1 & p=(1-r2)*p1+r2*p2;
  hence thesis by A1,JORDAN5A:3;
 end;
end;

theorem Th10:
 for p, p1, p2 being Point of TOP-REAL 2 st p1 <> p2 &
  p in LSeg(p1,p2) holds LE p1,p,p1,p2
proof
 let p, p1, p2 be Point of TOP-REAL 2;
 assume A1: p1 <> p2 & p in LSeg(p1,p2);
 thus LE p1,p,p1,p2
 proof
  thus p1 in LSeg(p1,p2) & p in LSeg(p1,p2) by A1,TOPREAL1:6;
  let r1,r2 be Real; assume
A2: 0<=r1 & r1<=1 & p1=(1-r1)*p1+r1*p2 &
   0<=r2 & r2<=1 & p=(1-r2)*p1+r2*p2;
  then 0.REAL 2 = (1-r1)*p1+r1*p2+-p1 by EUCLID:40
          .= (1-r1)*p1+r1*p2-p1 by EUCLID:45
          .= (1-r1)*p1+(r1*p2-p1) by EUCLID:49
          .= (1-r1)*p1+(-p1+r1*p2) by EUCLID:45
          .= (1-r1)*p1+-p1+r1*p2 by EUCLID:30
          .= ((1-r1)*p1-p1)+r1*p2 by EUCLID:45;
then -r1*p2=(((1-r1)*p1-p1)+r1*p2)+-r1*p2 by EUCLID:31
   .=(((1-r1)*p1-p1)+r1*p2)-r1*p2 by EUCLID:45
   .=((1-r1)*p1-p1)+(r1*p2-r1*p2) by EUCLID:49
   .=((1-r1)*p1-p1)+0.REAL 2 by EUCLID:46
   .=(1-r1)*p1-p1 by EUCLID:31
   .=(1-r1)*p1-1*p1 by EUCLID:33
   .=(1-r1-1)*p1 by EUCLID:54
   .=(-r1)*p1
   .=-r1*p1 by EUCLID:44;
  then r1*p1=--r1*p2 .= r1*p2;
  hence thesis by A1,A2,EUCLID:38;
 end;
end;

theorem Th11:
 for p, p1, p2 being Point of TOP-REAL 2 st p in LSeg(p1,p2) & p1 <> p2
 holds LE p,p2,p1,p2
proof
 let p, p1, p2 be Point of TOP-REAL 2;
 assume A1: p in LSeg(p1,p2) & p1 <> p2;
 thus LE p,p2,p1,p2
 proof
  thus p in LSeg(p1,p2) & p2 in LSeg(p1,p2) by A1,TOPREAL1:6;
  let r1,r2 be Real such that
A2: 0<=r1 & r1<=1 & p=(1-r1)*p1+r1*p2 &
  0<=r2 & r2<=1 & p2=(1-r2)*p1+r2*p2;
      p2 = 1*p2 by EUCLID:33
    .= 0.REAL 2 + 1*p2 by EUCLID:31
    .= (1-1)*p1 + 1*p2 by EUCLID:33;
  hence thesis by A1,A2,JORDAN5A:3;
 end;
end;

theorem
     for p1, p2, q1, q2, q3 being Point of TOP-REAL 2 st p1 <> p2 &
   LE q1,q2,p1,p2 & LE q2,q3,p1,p2 holds LE q1,q3,p1,p2
proof
 let p1, p2, q1, q2, q3 be Point of TOP-REAL 2;
 assume A1: p1<>p2 & LE q1,q2,p1,p2 & LE q2,q3,p1,p2;
then A2: q1 in LSeg(p1,p2) & q2 in LSeg(p1,p2) & q3 in LSeg(p1,p2)
   by JORDAN3:def 6;
  then q1 in { (1-l)*p1 + l*p2 where l is Real : 0 <= l & l <= 1 }
   by TOPREAL1:def 3;
 then consider s1 being Real such that
    A3: q1=(1-s1)*p1+s1*p2 & 0<=s1 & s1<=1;
     q2 in { (1-l1)*p1 + l1*p2 where l1 is Real : 0 <= l1 & l1 <= 1 }
  by A2,TOPREAL1:def 3;
 then consider s2 being Real such that
    A4: q2=(1-s2)*p1+s2*p2 & 0<=s2 & s2<=1;
A5: s1 <= s2 by A1,A3,A4,JORDAN3:def 6;
     q3 in { (1-l2)*p1 + l2*p2 where l2 is Real : 0 <= l2 & l2 <= 1 }
   by A2,TOPREAL1:def 3;
 then consider s3 being Real such that
    A6: q3=(1-s3)*p1+s3*p2 & 0<=s3 & s3<=1;
     s2 <= s3 by A1,A4,A6,JORDAN3:def 6;
then A7: s1 <= s3 by A5,XXREAL_0:2;
 thus LE q1,q3,p1,p2
 proof
  thus q1 in LSeg(p1,p2) & q3 in LSeg(p1,p2) by A1,JORDAN3:def 6;
  let r1,r2 be Real;
  assume A8: 0<=r1 & r1<=1 & q1=(1-r1)*p1+r1*p2 &
            0<=r2 & r2<=1 & q3=(1-r2)*p1+r2*p2;
  then s1 = r1 by A1,A3,JORDAN5A:3;
  hence thesis by A1,A6,A7,A8,JORDAN5A:3;
 end;
end;

theorem
     for p, q being Point of TOP-REAL 2 st p <> q holds
  LSeg (p, q) =
   { p1 where p1 is Point of TOP-REAL 2 : LE p, p1, p, q & LE p1, q, p, q }
proof
 let p, q be Point of TOP-REAL 2;
 assume A1: p <> q;
 thus LSeg (p, q) c=
   { p1 where p1 is Point of TOP-REAL 2 : LE p, p1, p, q & LE p1, q, p, q }
 proof
  let a be set; assume A2: a in LSeg (p, q);
  then reconsider a' = a as Point of TOP-REAL 2;
      LE p, a', p, q & LE a', q, p, q by A1,A2,Th10,Th11;
  hence thesis;
 end;
 thus { p1 where p1 is Point of TOP-REAL 2 :
          LE p, p1, p, q & LE p1, q, p, q } c= LSeg (p, q)
 proof
  let a be set; assume a in { p1 where p1 is Point of TOP-REAL 2 :
         LE p, p1, p, q & LE p1, q, p, q };
  then consider a' be Point of TOP-REAL 2 such that
A3:    a' = a & LE p, a', p, q & LE a', q, p, q;
  thus thesis by A3,JORDAN3:65;
 end;
end;

theorem
 for n being Element of NAT,
     P being Subset of TOP-REAL n,
     p1, p2 being Point of TOP-REAL n st
    P is_an_arc_of p1,p2 holds P is_an_arc_of p2,p1
proof
 let n be Element of NAT,
     P be Subset of TOP-REAL n,
     p1,p2 be Point of TOP-REAL n;
 assume
A1: P is_an_arc_of p1, p2;
 then consider f being Function of I[01], (TOP-REAL n) | P such that
A2:  f is_homeomorphism & f.0 = p1 & f.1 = p2 by TOPREAL1:def 2;
 set Ex = L[01]((0,1)(#),(#)(0,1));
A3:  Ex is_homeomorphism by TREAL_1:21;
 set g = f * Ex;
A4: Ex.(0,1)(#) = 0 by BORSUK_1:def 17,TREAL_1:8,12;
A5: Ex.(#)(0,1) = 1 by BORSUK_1:def 18,TREAL_1:8,12;
     dom f = [#]I[01] by A2,TOPS_2:def 5;
then A6: rng Ex = dom f by A3,TOPMETR:27,TOPS_2:def 5;
then A7: dom g = dom Ex by RELAT_1:46;
     rng g = rng f by A6,RELAT_1:47;
then A8: rng g = [#] ((TOP-REAL n) | P) by A2,TOPS_2:def 5;
 reconsider P as non empty Subset of TOP-REAL n by A1,TOPREAL1:4;
B9:  dom g = [#]I[01] by A3,A7,TOPMETR:27,TOPS_2:def 5;
then     dom g = the carrier of I[01] & rng g = the carrier of ((TOP-REAL n) |
P)
  by A8;
 then reconsider g as Function of I[01], (TOP-REAL n) | P by FUNCT_2:def 1
,RELSET_1:11;
A10: g is_homeomorphism by A2,A3,TOPMETR:27,TOPS_2:71;
A11: g.0 = p2 by A2,A5,B9,BORSUK_1:def 17,FUNCT_1:22,TREAL_1:8;
     g.1 = p1 by A2,A4,B9,BORSUK_1:def 18,FUNCT_1:22,TREAL_1:8;
 hence thesis by A10,A11,TOPREAL1:def 2;
end;

theorem
  for i being Element of NAT,
     f being FinSequence of TOP-REAL 2,
     P being Subset of TOP-REAL 2 st
  f is_S-Seq & 1 <= i & i+1 <= len f & P = LSeg(f,i) holds
   P is_an_arc_of f/.i, f/.(i+1)
proof
 let i be Element of NAT,
     f be FinSequence of TOP-REAL 2,
     P be Subset of TOP-REAL 2;
 assume A1: f is_S-Seq & 1 <= i & i+1 <= len f & P = LSeg(f,i);
then A2: i in dom f & i+1 in dom f by GOBOARD2:3;
A3: f is one-to-one by A1,TOPREAL1:def 10;
A4: LSeg (f,i) = LSeg (f/.i, f/.(i+1)) by A1,TOPREAL1:def 5;
        f/.i <> f/.(i+1)
    proof
     assume f/.i = f/.(i+1);
     then i = i + 1 by A2,A3,PARTFUN2:17;
     hence thesis;
    end;
  hence thesis by A1,A4,TOPREAL1:15;
end;

begin :: Cutting off sequences

theorem
  for g1 being FinSequence of TOP-REAL 2, i being Element of NAT
   st 1 <= i & i <= len g1
   & g1 is_S-Seq holds
  g1/.1 in L~mid(g1, i, len g1) implies i = 1
proof
 let g1 be FinSequence of TOP-REAL 2, i be Element of NAT;
 assume that
A1: 1 <= i & i <= len g1 and
A2: g1 is_S-Seq;
 assume g1/.1 in L~mid(g1, i, len g1);
 then consider j being Element of NAT such that
A3: 1 <= j & j+1 <= len mid(g1, i, len g1) &
    g1/.1 in LSeg(mid(g1, i, len g1),j) by SPPOL_2:13;
A4: j in dom mid(g1, i, len g1) & j+1 in dom mid(g1, i, len g1)
     by A3,GOBOARD2:3;
A5: mid(g1, i, len g1) = g1/^(i-'1) by A1,JORDAN3:26;
    j <= j+1 by NAT_1:11;
then A6: j <= len (g1/^(i-'1)) by A3,A5,XXREAL_0:2;
then A7: j in dom (g1/^(i-'1)) by A3,FINSEQ_3:27;
    i-'1 <= i by A1,Th1;
then A8: i-'1 <= len g1 by A1,XXREAL_0:2;
then A9: j <= len g1 - (i-'1) by A6,RFINSEQ:def 2;
A10: j <= i-'1+j by NAT_1:11;
then A11: 1 <= i-'1+j by A3,XXREAL_0:2;
A12: j+(i-'1) <= len g1 by A9,XREAL_1:21;
then A13: i-'1+j in dom g1 by A11,FINSEQ_3:27;
A14: (g1/^(i-'1))/.j = (g1/^(i-'1)).j by A7,FINSEQ_4:def 4
                 .= g1.(i-'1+j) by A3,A12,JORDAN3:23
                 .= g1/.(i-'1+j) by A13,FINSEQ_4:def 4;
A15: 1 <= j+1 by NAT_1:11;
        j+1 <= i-'1+(j+1) by NAT_1:11;
then A16: 1 <= i-'1+(j+1) by A15,XXREAL_0:2;
        j+1 <= len (g1/^(i-'1)) by A1,A3,JORDAN3:26;
    then j+1 <= len g1 - (i-'1) by A8,RFINSEQ:def 2;
then A17: 1 <= j+1 & j+1+(i-'1) <= len g1 by A4,FINSEQ_3:27,XREAL_1:21;
then A18: i-'1+(j+1) in dom g1 by A16,FINSEQ_3:27;
        j+1 in dom (g1/^(i-'1)) by A1,A4,JORDAN3:26;
then A19: (g1/^(i-'1))/.(j+1) = (g1/^(i-'1)).(j+1) by FINSEQ_4:def 4
                 .= g1.(i-'1+(j+1)) by A17,JORDAN3:23
                 .= g1/.(i-'1+(j+1)) by A18,FINSEQ_4:def 4;
A20: i-'1+j+1 = i-'1+(j+1);
A21: 1 <= i-'1+j & i-'1+j+1 <= len g1 by A3,A10,A17,XXREAL_0:2;
A22: LSeg(mid(g1, i, len g1),j) =
 LSeg (g1/.(i-'1+j), g1/.(i-'1+(j+1)) ) by A3,A5,A14,A19,TOPREAL1:def 5
    .= LSeg ( g1, i-'1+j ) by A11,A17,A20,TOPREAL1:def 5;
A23: 1+1 <= len g1 by A2,TOPREAL1:def 10;
    then g1/.1 in LSeg ( g1, 1 ) by TOPREAL1:27;
then A24: g1/.1 in LSeg ( g1, 1 ) /\ LSeg ( g1, i-'1+j ) by A3,A22,
XBOOLE_0:def 3;
then A25: LSeg (g1, 1) meets LSeg ( g1, i-'1+j ) by XBOOLE_0:4;
    assume i <> 1;
    then 1 < i by A1,REAL_1:def 5;
    then 1+1 <= i by NAT_1:13;
    then 2-'1 <= i-'1 by BINARITH:60;
    then 2-'1+1 <= i-'1+j by A3,XREAL_1:9;
then A26:  i-'1+j >= 2 by BINARITH:53;
A27:  g1 is s.n.c. unfolded one-to-one by A2,TOPREAL1:def 10;
A28: 1 in dom g1 & 1+1 in dom g1 by A23,GOBOARD2:3;
    per cases by A26,REAL_1:def 5;
    suppose i-'1+j = 2;
    then g1/.1 in { g1/.(1+1) } by A21,A24,A27,TOPREAL1:def 8;
    then g1/.1 = g1/.(1+1) by TARSKI:def 1;
    hence thesis by A27,A28,PARTFUN2:17;
    end;
    suppose i-'1+j > 2;
    then i-'1+j > 1+1;
    hence thesis by A25,A27,TOPREAL1:def 9;
end;
end;

theorem
 for f being FinSequence of TOP-REAL 2,
     p being Point of TOP-REAL 2 st f is_S-Seq & p = f.len f holds
   L_Cut (f,p) = <*p*>
proof
 let f be FinSequence of TOP-REAL 2,
     p be Point of TOP-REAL 2;
 assume A1: f is_S-Seq & p = f.len f;
  then len f >= 2 by TOPREAL1:def 10;
  then p in L~f by A1,JORDAN3:34;
  then A2: p in L~Rev f by SPPOL_2:22;
A3: Rev f is_S-Seq by A1,SPPOL_2:47;
A4: L_Cut(f,p) =L_Cut(Rev Rev f,p) by FINSEQ_6:29
     .= Rev R_Cut(Rev f,p) by A2,A3,JORDAN3:57;
       p = (Rev f).1 by A1,FINSEQ_5:65;
   then R_Cut(Rev f,p) = <*p*> by JORDAN3:def 5;
  hence L_Cut(f,p) = <*p*> by A4,FINSEQ_5:63;
end;

canceled 3;

theorem
 for f being FinSequence of TOP-REAL 2,
     p being Point of TOP-REAL 2 st
      p in L~f & p <> f.len f & f is_S-Seq holds
  Index (p, L_Cut(f,p)) = 1
proof
 let f be FinSequence of TOP-REAL 2,
     p be Point of TOP-REAL 2;
 assume that
A1: p in L~f and
A2: p <> f.len f and
A3: f is_S-Seq;
        L_Cut(f,p) is_S-Seq by A1,A2,A3,JORDAN3:69;
then A4: 2 <= len L_Cut(f,p) by TOPREAL1:def 10;
    then 1 <= len L_Cut(f,p) by XXREAL_0:2;
  then 1 in dom L_Cut(f,p) by FINSEQ_3:27;
  then (L_Cut(f,p))/.1 = (L_Cut(f,p)).1 by FINSEQ_4:def 4
     .= p by A1,JORDAN3:58;
 hence thesis by A4,JORDAN3:44;
end;

theorem Th22:
 for f being FinSequence of TOP-REAL 2,
     p being Point of TOP-REAL 2 st
      p in L~f & f is_S-Seq & p <> f.len f holds
 p in L~L_Cut (f,p)
proof
 let f be FinSequence of TOP-REAL 2,
     p be Point of TOP-REAL 2;
 assume A1: p in L~f & f is_S-Seq;
 assume p <> f.len f;
 then L_Cut (f,p) is_S-Seq by A1,JORDAN3:69;
then A2: len L_Cut (f,p) >= 2 by TOPREAL1:def 10;
     L_Cut (f,p).1 = p by A1,JORDAN3:58;
 hence thesis by A2,JORDAN3:34;
end;

theorem
  for f being FinSequence of TOP-REAL 2,
     p being Point of TOP-REAL 2 st
      p in L~f & f is_S-Seq & p <> f.1 holds
 p in L~R_Cut (f,p)
proof
 let f be FinSequence of TOP-REAL 2,
     p be Point of TOP-REAL 2;
 assume A1: p in L~f & f is_S-Seq;
 assume p <> f.1;
 then R_Cut (f,p) is_S-Seq by A1,JORDAN3:70;
then A2: len R_Cut (f,p) >= 2 by TOPREAL1:def 10;
  R_Cut (f,p).len R_Cut (f,p) = p by A1,JORDAN3:59;
 hence thesis by A2,JORDAN3:34;
end;

theorem Th24:
 for f being FinSequence of TOP-REAL 2,
     p being Point of TOP-REAL 2 st p in L~f & f is one-to-one
 holds B_Cut(f,p,p) = <*p*>
proof
 let f be FinSequence of TOP-REAL 2,
     p be Point of TOP-REAL 2;
 assume that
A1: p in L~f and
A2: f is one-to-one;
A3:Index(p,f) <> Index(p,f)+1;
   Index(p,f) < len f by A1,JORDAN3:41;
then A4:1 <= Index(p,f) & Index(p,f) + 1 <= len f
          by A1,JORDAN3:41,NAT_1:13;
   then Index(p,f) in dom f & Index(p,f) + 1 in dom f by GOBOARD2:3;
then A5:f/.Index(p,f) <> f/.(Index(p,f)+1) by A3,A2,PARTFUN2:17;
       p in LSeg(f,Index(p,f)) by A1,JORDAN3:42;
   then p in LSeg(f/.Index(p,f), f/.(Index(p,f)+1)) by A4,TOPREAL1:def 5;
then A6: LE p,p,f/.Index(p,f),f/.(Index(p,f)+1) by A5,Th9;
    (L_Cut(f,p)).1 = p by A1,JORDAN3:58;
  then R_Cut(L_Cut(f,p),p) = <*p*> by JORDAN3:def 5;
 hence thesis by A6,JORDAN3:def 8;
end;

Lm3:
 for f being FinSequence of TOP-REAL 2,
     p, q being Point of TOP-REAL 2 st
      p in L~f & q in L~f & p <> f.len f & q <> f.len f & f is_S-Seq holds
    p in L~L_Cut(f,q) or q in L~L_Cut(f,p)
proof
 let f be FinSequence of TOP-REAL 2,
     p, q be Point of TOP-REAL 2;
 assume A1: p in L~f & q in L~f & p <> f.len f & q <> f.len f &
   f is_S-Seq;
  then Index (p, f) < len f by JORDAN3:41;
then A2: 1 <= Index (p, f) & Index (p, f) + 1 <= len f by A1,JORDAN3:41
,NAT_1:13;
  then A3: LSeg (f, Index(p,f)) = LSeg (f/.Index(p,f), f/.(Index(p,f)+1))
   by TOPREAL1:def 5;
A4: Index(p,f) in dom f & Index(p,f)+1 in dom f by A2,GOBOARD2:3;
A5: f is one-to-one by A1,TOPREAL1:def 10;
    Index(p,f) < Index(p,f)+1 by NAT_1:13;
then A6: f/.Index(p,f) <> f/.(Index(p,f)+1) by A4,A5,PARTFUN2:17;
 per cases by REAL_1:def 5;
 suppose Index(p,f) < Index(q,f);
 hence thesis by A1,JORDAN3:64;
 end;
 suppose A7: Index(p,f) = Index (q,f);
A8: p in LSeg (f/.Index(p,f), f/.(Index(p,f)+1)) by A1,A3,JORDAN3:42;
     q in LSeg (f/.Index(p,f), f/.(Index(p,f)+1)) by A1,A3,A7,JORDAN3:42;
then A9: LE p, q, f/.Index(p,f), f/.(Index(p,f)+1) or
  LT q, p, f/.Index(p,f), f/.(Index(p,f)+1) by A6,A8,JORDAN3:63;
     now per cases by A9,JORDAN3:def 7;
  suppose A10: LE p, q, f/.Index(p,f), f/.(Index(p,f)+1);
  thus p in L~L_Cut(f,q) or q in L~L_Cut(f,p)
  proof
    per cases;
    suppose p <> q;
    hence thesis by A1,A7,A10,JORDAN3:66;
    end;
    suppose p = q;
    hence thesis by A1,Th22;
  end;
  end;
  end;
  suppose A11: LE q, p, f/.Index(p,f), f/.(Index(p,f)+1);
  thus p in L~L_Cut(f,q) or q in L~L_Cut(f,p)
  proof
    per cases;
    suppose p <> q;
    hence thesis by A1,A7,A11,JORDAN3:66;
    end;
    suppose p = q;
    hence thesis by A1,Th22;
  end;
  end;
 end;
 end;
 hence thesis;
 end;
 suppose Index(p,f) > Index(q,f);
 hence thesis by A1,JORDAN3:64;
end;
end;

theorem Th25:
 for f being FinSequence of TOP-REAL 2,
     p, q being Point of TOP-REAL 2 st
      p in L~f & q in L~f & q <> f.len f & p = f.len f & f is_S-Seq holds
    p in L~L_Cut(f,q)
proof
 let f be FinSequence of TOP-REAL 2,
     p, q be Point of TOP-REAL 2;
 assume A1: p in L~f & q in L~f & q <> f.len f & p = f.len f & f is_S-Seq;
 then 1 + 1 <= len f by TOPREAL1:def 10;
 then A2: 1 < len f by XXREAL_0:2;
 then A3: Index(p,f) + 1 = len f by A1,JORDAN3:45;
     Index(q,f) < len f by A1,JORDAN3:41;
 then A4: Index(q,f) <= Index(p,f) by A3,NAT_1:13;
 per cases by A4,XXREAL_0:1;
  suppose Index (q,f) = Index (p,f);
   then A5:  L_Cut(f,q) = <*q*>^mid(f,len f,len f) by A1,A3,JORDAN3:def 4
           .= <*q*>^<*f/.len f*> by A2,JORDAN4:27
           .= <*q,f/.len f*> by FINSEQ_1:def 9
           .= <*q,p*> by A1,A2,FINSEQ_4:24;
   then rng L_Cut(f,q) = {p,q} by FINSEQ_2:147;
   then A6:  p in rng L_Cut(f,q) by TARSKI:def 2;
       len L_Cut(f,q) = 2 by A5,FINSEQ_1:61;
   then rng L_Cut(f,q) c= L~L_Cut(f,q) by SPPOL_2:18;
  hence thesis by A6;
  end;
  suppose Index (q,f) < Index (p,f);
  hence thesis by A1,JORDAN3:64;
end;
end;

theorem
  for f being FinSequence of TOP-REAL 2,
     p, q being Point of TOP-REAL 2 st
      p <> f.len f &
      p in L~f & q in L~f & f is_S-Seq holds
    p in L~L_Cut(f,q) or q in L~L_Cut(f,p)
proof
 let f be FinSequence of TOP-REAL 2,
     p, q be Point of TOP-REAL 2;
 assume
A1: p <> f.len f;
 assume A2: p in L~f & q in L~f & f is_S-Seq;
 per cases by A1;
 suppose p <> f.len f & q = f.len f;
 hence thesis by A2,Th25;
 end;
 suppose p = f.len f & q <> f.len f;
 hence thesis by A2,Th25;
 end;
 suppose p <> f.len f & q <> f.len f;
 hence thesis by A2,Lm3;
end;
end;

Lm4:
 for f be FinSequence of TOP-REAL 2
 for p,q be Point of TOP-REAL 2 st p in L~f & q in L~f &
   ( Index(p,f) < Index(q,f) or
     Index(p,f) = Index(q,f) & LE p,q,f/.Index(p,f),f/.(Index(p,f)+1) ) &
   p <> q holds
  L~B_Cut(f,p,q) c= L~f
 proof
  let f be FinSequence of TOP-REAL 2;
  let p,q be Point of TOP-REAL 2 such that
   A1: p in L~f and
   A2: q in L~f and
   A3: Index(p,f)<Index(q,f) or
       Index(p,f)=Index(q,f) & LE p,q,f/.Index(p,f),f/.(Index(p,f)+1) and
   A4: p <> q;
  A5: B_Cut(f,p,q) = R_Cut(L_Cut(f,p),q) by A1,A2,A3,JORDAN3:def 8;
  now per cases by A3;
   case Index(p,f)<Index(q,f);
    then q in L~(L_Cut(f,p)) by A1,A2,JORDAN3:64;
    hence ex i1 being Element of NAT st 1<=i1 & i1+1<=len L_Cut(f,p) &
     q in LSeg(L_Cut(f,p),i1) by SPPOL_2:13;
   end;
   case Index(p,f)=Index(q,f) & LE p,q,f/.Index(p,f),f/.(Index(p,f)+1);
    then q in L~(L_Cut(f,p)) by A1,A2,A4,JORDAN3:66;
    hence ex i1 being Element of NAT st 1<=i1 & i1+1<=len L_Cut(f,p) &
     q in LSeg(L_Cut(f,p),i1) by SPPOL_2:13;
   end;
  end;
  then A6: q in L~L_Cut(f,p) by SPPOL_2:17;
  A7: L~B_Cut(f,p,q) c= L~L_Cut(f,p) by A5,A6,JORDAN3:76;
  L~L_Cut(f,p) c= L~f by A1,JORDAN3:77;
  hence L~B_Cut(f,p,q) c= L~f by A7,XBOOLE_1:1;
 end;

theorem
 for f being FinSequence of TOP-REAL 2,
     p, q being Point of TOP-REAL 2 st p in L~f & q in L~f & f is_S-Seq
 holds L~B_Cut(f,p,q) c= L~f
proof
 let f be FinSequence of TOP-REAL 2,
     p, q be Point of TOP-REAL 2 such that
A1: p in L~f and
A2: q in L~f and
A3: f is_S-Seq;
A4: f is one-to-one by A3,TOPREAL1:def 10;
 per cases;
  suppose p = q;
   then B_Cut(f,p,q) = <*p*> by A4,A1,Th24;
   then len B_Cut(f,p,q) = 1 by FINSEQ_1:56;
   then L~B_Cut(f,p,q) = {} by TOPREAL1:28;
   hence thesis by XBOOLE_1:2;
  end;
  suppose p <> q & (Index(p,f)<Index(q,f) or Index(p,f)=Index(q,f)
    & LE p,q,f/.Index(p,f),f/.(Index(p,f)+1));
  hence thesis by A1,A2,Lm4;
  end;
  suppose that
A5: p <> q and
A6:not(Index(p,f)<Index(q,f) or Index(p,f)=Index(q,f)
    & LE p,q,f/.Index(p,f),f/.(Index(p,f)+1));
    A7:B_Cut(f,p,q)=Rev R_Cut(L_Cut(f,q),p) by A6,JORDAN3:def 8;
A8:  Index(q,f) < Index(p,f)
    or Index(p,f)=Index(q,f) & not LE p,q,f/.Index(p,f),f/.(Index(p,f)+1)
       by A6,XXREAL_0:1;
A9:  now assume that
A10:   Index(p,f)=Index(q,f) and
A11:   not LE p,q,f/.Index(p,f),f/.(Index(p,f)+1);
A12: 1 <= Index(p,f) by A1,JORDAN3:41;
A13:  Index(p,f) < len f by A1,JORDAN3:41;
      then A14:   Index(p,f)+1 <= len f by NAT_1:13;
      then A15:   LSeg(f,Index(p,f)) = LSeg(f/.Index(p,f),f/.(Index(p,f)+1))
                    by A12,TOPREAL1:def 5;
      then A16:   p in LSeg(f/.Index(p,f),f/.(Index(p,f)+1)) by A1,JORDAN3:42;
A17:   q in LSeg(f/.Index(p,f),f/.(Index(p,f)+1))
                  by A2,A10,A15,JORDAN3:42;
A18:   Index(p,f) in dom f by A12,A13,FINSEQ_3:27;
          1 <= Index(p,f)+1 by NAT_1:11;
      then A19:   Index(p,f)+1 in dom f by A14,FINSEQ_3:27;
      Index(p,f)+0 <> Index(p,f)+1;
      then f/.Index(p,f)<>f/.(Index(p,f)+1) by A18,A19,A4,PARTFUN2:17;
      then LT q,p,f/.Index(p,f),
f/.(Index(p,f)+1) by A11,A16,A17,JORDAN3:63;
     hence LE q,p,f/.Index(q,f),f/.(Index(q,f)+1) by A10,JORDAN3:def 7;
    end;
then A20:  Rev B_Cut(f,q,p) = B_Cut(f,p,q) by A1,A2,A7,A8,JORDAN3:def 8;
       L~B_Cut(f,q,p) c= L~f by A1,A2,A5,A8,A9,Lm4;
  hence thesis by A20,SPPOL_2:22;
end;
end;

theorem
  for f being non constant standard special_circular_sequence,
     i, j being Element of NAT st 1 <= i & j <= len GoB f & i < j holds
   LSeg((GoB f)*(1,width GoB f), (GoB f)*(i,width GoB f)) /\
   LSeg((GoB f)*(j,width GoB f), (GoB f)*(len GoB f,width GoB f)) = {}
proof
 let f be non constant standard special_circular_sequence,
     i, j be Element of NAT;
 assume A1: 1 <= i & j <= len GoB f & i < j;
 set A = LSeg((GoB f)*(1,width GoB f), (GoB f)*(i,width GoB f)),
     B = LSeg((GoB f)*(j,width GoB f), (GoB f)*(len GoB f,width GoB f));
 assume A /\ B <> {};
 then A meets B by XBOOLE_0:def 7;
 then consider x be set such that
A2:  x in A & x in B by XBOOLE_0:3;
 reconsider x1 = x as Point of TOP-REAL 2 by A2;
A3: 1 <= width GoB f & 1 <= i & i <= len GoB f
           by A1,XXREAL_0:2,GOBOARD7:35;
     ((GoB f)*(1,width GoB f))`1 <= ((GoB f)*(i,width GoB f))`1
 proof
  per cases by A1,REAL_1:def 5;
  suppose i = 1;
  hence thesis;
  end;
  suppose i > 1;
  hence thesis by A3,GOBOARD5:4;
 end;
 end;
then A4: x1`1 >= ((GoB f)*(1,width GoB f))`1 & x1`1 <= ((GoB f)*(i,width GoB f)
)`1
   by A2,TOPREAL1:9;
A5: ((GoB f)*(j,width GoB f))`1 > ((GoB f)*(i,width GoB f))`1
   by A1,A3,GOBOARD5:4;
A6: 1 <= j by A1,XXREAL_0:2;
     ((GoB f)*(j,width GoB f))`1 <= ((GoB f)*(len GoB f,width GoB f))`1
 proof
  per cases by A1,REAL_1:def 5;
  suppose j < len GoB f;
  hence thesis by A3,A6,GOBOARD5:4;
  end;
  suppose j = len GoB f;
  hence thesis;
 end;
 end;
 then x1`1 <= ((GoB f)*(len GoB f,width GoB f))`1 &
    x1`1 >= ((GoB f)*(j,width GoB f))`1 by A2,TOPREAL1:9;
 hence thesis by A4,A5,XXREAL_0:2;
end;

theorem
     for f being non constant standard special_circular_sequence,
     i, j being Element of NAT st 1 <= i & j <= width GoB f & i < j holds
   LSeg((GoB f)*(len GoB f,1), (GoB f)*(len GoB f,i)) /\
   LSeg((GoB f)*(len GoB f,j), (GoB f)*(len GoB f,width GoB f)) = {}
proof
 let f be non constant standard special_circular_sequence,
     i, j be Element of NAT;
 assume A1: 1 <= i & j <= width GoB f & i < j;
 set A = LSeg((GoB f)*(len GoB f,1), (GoB f)*(len GoB f,i)),
     B = LSeg((GoB f)*(len GoB f,j), (GoB f)*(len GoB f,width GoB f));
 assume A /\ B <> {};
 then A meets B by XBOOLE_0:def 7;
 then consider x be set such that
A2:  x in A & x in B by XBOOLE_0:3;
 reconsider x1 = x as Point of TOP-REAL 2 by A2;
A3: 1 <= len GoB f & 1 <= i & i <= width GoB f
           by A1,XXREAL_0:2,GOBOARD7:34;
     ((GoB f)*(len GoB f,1))`2 <= ((GoB f)*(len GoB f,i))`2
 proof
  per cases by A1,REAL_1:def 5;
  suppose i = 1;
  hence thesis;
  end;
  suppose i > 1;
  hence thesis by A3,GOBOARD5:5;
 end;
 end;
then A4: x1`2 >= ((GoB f)*(len GoB f,1))`2 & x1`2 <= ((GoB f)*(len GoB f,i))`2
     by A2,TOPREAL1:10;
A5: ((GoB f)*(len GoB f,j))`2 > ((GoB f)*(len GoB f,i))`2
   by A1,A3,GOBOARD5:5;
A6: 1 <= j by A1,XXREAL_0:2;
     ((GoB f)*(len GoB f,j))`2 <= ((GoB f)*(len GoB f,width GoB f))`2
 proof
  per cases by A1,REAL_1:def 5;
  suppose j < width GoB f;
  hence thesis by A3,A6,GOBOARD5:5;
  end;
  suppose j = width GoB f;
  hence thesis;
 end;
 end;
 then x1`2 <= ((GoB f)*(len GoB f,width GoB f))`2 &
    x1`2 >= ((GoB f)*(len GoB f,j))`2 by A2,TOPREAL1:10;
 hence thesis by A4,A5,XXREAL_0:2;
end;

theorem Th30:
 for f being FinSequence of TOP-REAL 2 st f is_S-Seq holds
   L_Cut (f,f/.1) = f
proof
 let f be FinSequence of TOP-REAL 2;
 assume A1: f is_S-Seq;
then A2: 1+1 <= len f by TOPREAL1:def 10;
then A3: 1 <= len f by XXREAL_0:2;
then A4: 1 in dom f by FINSEQ_3:27;
A5: 1+1 in dom f by A2,FINSEQ_3:27;
A6: 1 < len f by A2,NAT_1:13;
A7: f is one-to-one by A1,TOPREAL1:def 10;
A8: f/.1 = f.1 by A4,FINSEQ_4:def 4;
A9: Index(f/.1,f) = 1 by A2,JORDAN3:44;
   f/.1 <> f/.(1+1) by A4,A5,A7,PARTFUN2:17;
   then f/.1 <> f.(1+1) by A5,FINSEQ_4:def 4;
   hence L_Cut (f,f/.1) = <*f/.1*>^mid(f,Index(f/.1,f)+1,len f)
     by A9,JORDAN3:def 4
     .= mid(f,1,len f) by A4,A6,A8,A9,JORDAN3:56
     .= f by A3,JORDAN3:29;
end;

theorem
  for f being FinSequence of TOP-REAL 2 st f is_S-Seq holds
   R_Cut (f,f/.len f) = f
proof
 let f be FinSequence of TOP-REAL 2;
 assume A1: f is_S-Seq;
then A2: 2 <= len f by TOPREAL1:def 10; then
A3: f <> {} by FINSEQ_1:25;
A4: Rev f is_S-Seq by A1,SPPOL_2:47;
A5: f/.len f in L~f by A2,JORDAN3:34;
A6: f/.len f = (Rev f)/.1 by A3,FINSEQ_5:68;
 thus R_Cut (f,f/.len f) = Rev Rev R_Cut (f,f/.len f) by FINSEQ_6:29
         .= Rev L_Cut(Rev f,f/.len f) by A1,A5,JORDAN3:57
         .= Rev Rev f by A4,A6,Th30
         .= f by FINSEQ_6:29;
end;

theorem
  for f being FinSequence of TOP-REAL 2,
      p being Point of TOP-REAL 2 st p in L~f holds
   p in LSeg ( f/.Index(p,f), f/.(Index(p,f)+1) )
proof
 let f be FinSequence of TOP-REAL 2,
     p be Point of TOP-REAL 2;
 assume
A1: p in L~f;
 then Index(p,f) < len f by JORDAN3:41;
 then A2: 1 <= Index(p,f) & Index(p,f)+1 <= len f by A1,JORDAN3:41,NAT_1:13;
      p in LSeg(f,Index(p,f)) by A1,JORDAN3:42;
 hence thesis by A2,TOPREAL1:def 5;
end;

theorem
   for f be FinSequence of TOP-REAL 2
   for i be Element of NAT st
   f is unfolded s.n.c. one-to-one & len f >= 2 & f/.1 in LSeg (f,i) holds
   i = 1
proof
 let f be FinSequence of TOP-REAL 2,
     i be Element of NAT;
 assume
A1: f is unfolded s.n.c. one-to-one & 2 <= len f;
   then 1 <= len f by XXREAL_0:2;
then A2: 1 in dom f & 2 in dom f by A1,FINSEQ_3:27;
 assume A3: f/.1 in LSeg (f,i);
 assume A4: i <> 1;
 per cases by A4,REAL_1:def 5;
 suppose A5: i > 1;
     1+1 <= len f by A1;
 then f/.1 in LSeg (f,1) by TOPREAL1:27;
then A6: f/.1 in LSeg (f,1) /\ LSeg (f,i) by A3,XBOOLE_0:def 3;
then A7: LSeg (f,1) meets LSeg (f,i) by XBOOLE_0:4;
     now per cases by REAL_1:def 5;
  suppose A8: i = 2;
then A9:  1 + 2 <= len f by A3,TOPREAL1:def 5;
      1 + 1 = 2;
  then f/.1 in { f/.2 } by A1,A6,A8,A9,TOPREAL1:def 8;
  then f/.1 = f/.2 by TARSKI:def 1;
  hence contradiction by A1,A2,PARTFUN2:17;
  end;
  suppose i > 2;
  then 1 + 1 < i;
  hence contradiction by A1,A7,TOPREAL1:def 9;
  end;
  suppose i < 2;
  then i < 1+1;
  hence contradiction by A5,NAT_1:13;
 end;
 end;
 hence thesis;
 end;
 suppose i < 1;
 hence thesis by A3,TOPREAL1:def 5;
end;
end;

theorem
 for f be non constant standard special_circular_sequence,
     j be Element of NAT,
     P be Subset of TOP-REAL 2 st
     1 <= j & j <= width GoB f &
     P = LSeg ((GoB f)*(1,j), (GoB f)*(len (GoB f),j)) holds
 P is_S-P_arc_joining (GoB f)*(1,j),
                      (GoB f)*(len (GoB f),j)
proof
 let f be non constant standard special_circular_sequence,
     j be Element of NAT,
     P be Subset of TOP-REAL 2;
 assume A1: 1 <= j & j <= width GoB f &
           P = LSeg ((GoB f)*(1,j), (GoB f)*(len (GoB f),j));
 set p = (GoB f)*(1,j), q = (GoB f)*(len GoB f, j);
       1 <= j & j <= width GoB f &
   1 <= len GoB f & len GoB f <= len GoB f by A1,GOBOARD7:34;
then A2: p`2 = q`2 by GOBOARD5:2;
A3: p`1 <> q`1
  proof
    assume A4: p`1 = q`1;
A5: GoB f = GoB(Incr(X_axis(f)),Incr(Y_axis(f))) by GOBOARD2:def 3;
     1 <= len GoB f &
     len GoB f <= len GoB(Incr(X_axis(f)),Incr(Y_axis(f)))
          by GOBOARD2:def 3,GOBOARD7:34;
then A6: [1,j] in Indices GoB(Incr(X_axis f),Incr(Y_axis(f))) &
     [len GoB f,j] in Indices GoB(Incr(X_axis f),Incr(Y_axis(f)))
         by A1,A5,GOBOARD7:10;
        (GoB f)*(1,j)=
       (GoB(Incr(X_axis(f)),Incr(Y_axis(f))))*(1,j)
                        by GOBOARD2:def 3
     .= |[Incr(X_axis(f)).1,Incr(Y_axis(f)).j]|
                        by A6,GOBOARD2:def 1;
then A7: p`1 = Incr(X_axis(f)).1 by EUCLID:56;
        (GoB f)*(len GoB f, j) =
       (GoB(Incr(X_axis(f)),Incr(Y_axis(f))))*(len GoB f, j)
                        by GOBOARD2:def 3
     .= |[Incr(X_axis(f)).(len GoB f), Incr(Y_axis(f)).j]|
                        by A6,GOBOARD2:def 1;
then A8: Incr(X_axis(f)).(len GoB f) = Incr(X_axis(f)).1 by A4,A7,EUCLID:56;
        len Incr (X_axis(f)) = len GoB f by A5,GOBOARD2:def 1;
    then 1 <= len GoB f &
    1 <= len Incr (X_axis(f)) & len GoB f <= len Incr (X_axis(f))
      by GOBOARD7:34;
    then len GoB f in dom Incr (X_axis(f)) & 1 in dom Incr (X_axis(f))
                by FINSEQ_3:27;
    then len GoB f = 1 by A8,GOBOARD2:19;
    hence thesis by GOBOARD7:34;
   end;
 reconsider gg = <*p,q*> as FinSequence of the carrier of TOP-REAL 2;
A9: len gg = 2 by FINSEQ_1:61;
 take gg;
 thus gg is_S-Seq by A2,A3,SPPOL_2:46;
 thus P = L~gg by A1,SPPOL_2:21;
 thus p = gg/.1 & q = gg/.len gg by A9,FINSEQ_4:26;
end;

theorem
 for f be non constant standard special_circular_sequence,
     j be Element of NAT,
     P be Subset of TOP-REAL 2 st
     1 <= j & j <= len GoB f &
     P = LSeg ((GoB f)*(j,1), (GoB f)*(j,width GoB f)) holds
 P is_S-P_arc_joining (GoB f)*(j,1),
                      (GoB f)*(j,width GoB f)
proof
 let f be non constant standard special_circular_sequence,
     j be Element of NAT,
     P be Subset of TOP-REAL 2;
 assume A1: 1 <= j & j <= len GoB f &
           P = LSeg ((GoB f)*(j,1),
                     (GoB f)*(j,width GoB f));
 set p = (GoB f)*(j, 1),
     q = (GoB f)*(j, width GoB f);
       1 <= j & j <= len GoB f &
   1 <= width GoB f by A1,GOBOARD7:35;
then A2: p`1 = q`1 by GOBOARD5:3;
A3: p`2 <> q`2
  proof
    assume A4: p`2 = q`2;
A5: GoB f = GoB(Incr(X_axis(f)),Incr(Y_axis(f))) by GOBOARD2:def 3;
     1 <= width GoB f &
     width GoB f <= width GoB(Incr(X_axis(f)),Incr(Y_axis(f)))
          by GOBOARD2:def 3,GOBOARD7:35;
then A6: [j,1] in Indices GoB(Incr(X_axis f),Incr(Y_axis(f))) &
     [j,width GoB f] in Indices GoB(Incr(X_axis f),Incr(Y_axis(f)))
         by A1,A5,GOBOARD7:10;
        (GoB f)*(j,1)=
       (GoB(Incr(X_axis(f)),Incr(Y_axis(f))))*(j,1)
                        by GOBOARD2:def 3
     .= |[Incr(X_axis(f)).j,Incr(Y_axis(f)).1]|
                        by A6,GOBOARD2:def 1;
then A7: p`2 = Incr(Y_axis(f)).1 by EUCLID:56;
        (GoB f)*(j, width GoB f)=
       (GoB(Incr(X_axis(f)),Incr(Y_axis(f))))*(j, width GoB f)
                        by GOBOARD2:def 3
     .= |[Incr(X_axis(f)).j, Incr(Y_axis(f)).(width GoB f)]|
                        by A6,GOBOARD2:def 1;
then A8: Incr(Y_axis(f)).(width GoB f) = Incr(Y_axis(f)).1 by A4,A7,EUCLID:56;
        len Incr (Y_axis(f)) = width GoB f by A5,GOBOARD2:def 1;
    then 1 <= width GoB f &
    1 <= len Incr (Y_axis(f)) & width GoB f <= len Incr (Y_axis(f))
      by GOBOARD7:35;
    then width GoB f in dom Incr (Y_axis(f)) & 1 in dom Incr (Y_axis(f))
                by FINSEQ_3:27;
    then width GoB f = 1 by A8,GOBOARD2:19;
    hence thesis by GOBOARD7:35;
   end;
 reconsider gg = <*p,q*> as FinSequence of the carrier of TOP-REAL 2;
A9: len gg = 2 by FINSEQ_1:61;
 take gg;
 thus gg is_S-Seq by A2,A3,SPPOL_2:46;
 thus P = L~gg by A1,SPPOL_2:21;
 thus p=gg/.1 & q=gg/.len gg by A9,FINSEQ_4:26;
end;

theorem
 for f be FinSequence of TOP-REAL 2
 for p,q be Point of TOP-REAL 2 st p in L~f & q in L~f &
   ( Index(p,f) < Index(q,f) or
     Index(p,f) = Index(q,f) & LE p,q,f/.Index(p,f),f/.(Index(p,f)+1) ) &
   p <> q holds
  L~B_Cut(f,p,q) c= L~f by Lm4;
