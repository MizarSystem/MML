:: Transpose Matrices and Groups of Permutations
::  by Katarzyna Jankowska
::
:: Received May 20, 1992
:: Copyright (c) 1992 Association of Mizar Users

environ

 vocabularies VECTSP_1, FINSEQ_1, RELAT_1, FUNCT_1, FUNCOP_1, MATRIX_1,
      FINSEQ_2, TREES_1, RLVECT_1, BOOLE, INCSP_1, QC_LANG1, FUNCT_2, BINOP_1,
      GROUP_1, NAT_1, ARYTM_1, FINSET_1, FINSUB_1, MATRIX_2, CARD_3, ARYTM;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, RELAT_1, FUNCT_1, PARTFUN1,
      FINSEQ_1, FUNCT_2, STRUCT_0, NAT_1, NAT_D, GROUP_1, GROUP_4, BINOP_1,
      RLVECT_1, VECTSP_1, MATRIX_1, FINSET_1, FINSUB_1, FINSEQ_2, FINSEQ_3,
      FINSEQ_4, FINSEQOP, NUMBERS, XCMPLX_0, XXREAL_0;
 constructors RELAT_2, PARTFUN1, BINOP_1, FINSUB_1, XXREAL_0, NAT_1, NAT_D,
      FINSEQOP, FINSEQ_4, GROUP_4, MATRIX_1;
 registrations XBOOLE_0, SUBSET_1, FUNCT_1, RELSET_1, PARTFUN1, FUNCT_2,
      XXREAL_0, XREAL_0, NAT_1, FINSEQ_1, FINSEQ_2, STRUCT_0, GROUP_1,
      MATRIX_1, ORDINAL1;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 definitions STRUCT_0, FINSEQ_2, MATRIX_1, TARSKI;
 theorems FINSEQ_1, FINSEQ_2, FINSEQ_3, TARSKI, GROUP_1, FUNCT_1, FUNCT_2,
      ZFMISC_1, NAT_1, BINOP_1, FINSUB_1, FINSET_1, GROUP_4, FRAENKEL,
      MATRIX_1, RELAT_1, XBOOLE_0, FUNCOP_1, ORDINAL1, NAT_D;
 schemes BINOP_1, MATRIX_1, XBOOLE_0;

begin :: Some Examples of Matrices

 reserve x,y,x1,x2,y1,y2 for set,
         i,j,k,l,n,m for Nat,
         D for non empty set,
         K for Field,
         s,s2 for FinSequence,
         a,b,c,d for Element of D,
         q,r for FinSequence of D,
         a',b' for Element of K;

definition let n,m; let a be set;
  func (n,m) --> a -> tabular FinSequence equals
     n |-> (m |-> a);
  coherence by MATRIX_1:2;
end;

definition let D,n,m;let d;
  redefine func (n,m) --> d -> Matrix of n,m,D;
  coherence by MATRIX_1:10;
end;

theorem Th1:
  [i,j] in Indices ((n,m)-->a) implies ((n,m)-->a) * (i,j)=a
  proof
    reconsider m1=m as Element of NAT by ORDINAL1:def 13;
    set M=(n,m)-->a;
    assume A1:[i,j] in Indices M;
    then A2:i in dom M & j in Seg width M by ZFMISC_1:106;
    then i in Seg len M by FINSEQ_1:def 3;
    then A3: i in Seg n by MATRIX_1:def 3;
    then n > 0 by FINSEQ_1:4,NAT_1:3;
    then j in Seg m by A2,MATRIX_1:24;
    then M.i=m1|->a & (m1|->a).j = a by A3,FUNCOP_1:13;
    hence M*(i,j)=a by A1,MATRIX_1:def 6;
  end;

theorem
   ((n,n)-->a') + ((n,n)-->b')= (n,n)-->(a'+b')
  proof
 A1:Indices ((n,n)-->a') =Indices ((n,n)-->b') &
    Indices ((n,n)-->a') =Indices ((n,n)-->(a'+b')) by MATRIX_1:27;
     now let i,j;
      assume A2:[i,j] in Indices ((n,n)-->(a'+b'));
      then ((n,n)-->a')*(i,j)=a' by A1,Th1;
      then ((n,n)-->a')*(i,j) +((n,n)-->b')*(i,j)=a'+b' by A1,A2,Th1;
      hence ((n,n)-->a')*(i,j) +((n,n)-->b')*(i,j)=((n,n)-->(a'+b'))*(i,j)
        by A2,Th1;
    end;
    hence thesis by A1,MATRIX_1:def 14;
  end;

definition let a,b,c,d be set;
  func (a,b)][(c,d) ->tabular FinSequence equals
     <*<*a,b*>,<*c,d*>*>;
  correctness
  proof
     len <*a,b*>=2 & len <*c,d*>=2 by FINSEQ_1:61;
    hence thesis by MATRIX_1:4;
  end;
end;

theorem Th3:
  len (x1,x2)][(y1,y2)=2 & width (x1,x2)][(y1,y2)=2 &
    Indices (x1,x2)][(y1,y2)=[:Seg 2,Seg 2:]
  proof set M = (x1,x2)][(y1,y2);
    thus A1:len M=2 by FINSEQ_1:61;
     rng M = { <*x1,x2*>,<*y1,y2*>} by FINSEQ_2:147;
    then <*x1,x2*> in rng M & len <*x1,x2*>=2 & 0<2
      by FINSEQ_1:61,TARSKI:def 2;
    hence width M=2 by A1,MATRIX_1:def 4;
    hence thesis by A1,FINSEQ_1:def 3;
  end;

theorem Th4:
  [1,1] in Indices (x1,x2)][(y1,y2) &
  [1,2] in Indices (x1,x2)][(y1,y2) &
  [2,1] in Indices (x1,x2)][(y1,y2) &
  [2,2] in Indices (x1,x2)][(y1,y2)
  proof
     Indices (x1,x2)][(y1,y2)=[:Seg 2,Seg 2:] & 1 in Seg 2 & 2 in Seg 2
      by Th3,FINSEQ_1:4,TARSKI:def 2;
    hence thesis by ZFMISC_1:106;
  end;

definition let D;let a be Element of D;
  redefine func <*a*> -> Element of 1-tuples_on D;
  coherence by FINSEQ_2:118;
end;

definition let D;let n;let p be Element of n-tuples_on D;
  redefine func <*p*> -> Matrix of 1,n,D;
  coherence
  proof
     len p = n by FINSEQ_2:109;
    hence thesis by MATRIX_1:11;
  end;
end;

theorem
   [1,1] in Indices <*<*a*>*> & <*<*a*>*>*(1,1)=a
  proof set M=<*<*a*>*>;
 A1:Indices M= [:Seg 1,Seg 1:] by MATRIX_1:25;
     1 in Seg 1 by FINSEQ_1:4,TARSKI:def 1;
    hence A2:[1,1] in Indices <*<*a*>*> by A1,ZFMISC_1:106;
     M.1= <*a*> & <*a*>.1=a by FINSEQ_1:57;
    hence <*<*a*>*> *(1,1)=a by A2,MATRIX_1:def 6;
  end;

definition let D;let a,b,c,d be Element of D;
  redefine func (a,b)][(c,d) -> Matrix of 2,D;
  coherence by MATRIX_1:19;
end;

theorem
   (a,b)][(c,d)*(1,1)=a & (a,b)][(c,d)*(1,2)=b &
  (a,b)][(c,d)*(2,1)=c & (a,b)][(c,d)*(2,2)=d
  proof
    set M=(a,b)][(c,d);
     M.1= <*a,b*> & M.2=<*c,d*> & <*a,b*>.1=a & <*a,b*>.2=b
    & <*c,d*>.1=c & <*c,d*>.2=d & [1,1] in Indices M &
    [1,2] in Indices M & [2,1] in Indices M & [2,2] in Indices M
      by Th4,FINSEQ_1:61;
    hence thesis by MATRIX_1:def 6;
  end;

definition let n; let K be Field;
  mode Upper_Triangular_Matrix of n,K -> Matrix of n,K means
     for i,j st [i,j] in Indices it holds
          (i>j implies it*(i,j) = 0.K );
  existence
  proof
    reconsider n1=n as Element of NAT by ORDINAL1:def 13;
    defpred P[Nat,Nat,Element of K] means
     ($1 <= $2 implies $3= 1_ K) & ($1 > $2 implies $3 = 0.K );
A1:  for i,j st [i,j] in [:Seg n1, Seg n1:]
     for x1,x2 being Element of K st P[i,j,x1] & P[i,j,x2]
       holds x1 = x2;
A2:  for i,j st [i,j] in [:Seg n1, Seg n1:]
     ex x being Element of K st P[i,j,x]
     proof
       let i,j;
       assume [i,j] in [:Seg n1, Seg n1:];
        (i<=j implies P[i,j,1_ K]) & (i>j implies P[i,j,0.K]);
       hence thesis;
     end;
     consider M being Matrix of n1,K such that
  A3:for i,j st [i,j] in Indices M holds P[i,j,M*(i,j)]
       from MATRIX_1:sch 2(A1,A2);
       reconsider M as Matrix of n,K;
     take M;
     thus thesis by A3;
   end;
end;

definition let n, K;
  mode Lower_Triangular_Matrix of n,K -> Matrix of n,K means
     for i,j st [i,j] in Indices it &
        i<j holds it*(i,j) = 0.K;
  existence
  proof
    consider M being Diagonal of n,K;
    take M;
    thus thesis by MATRIX_1:def 15;
  end;
end;

theorem
   for M being Matrix of D st len M=n holds M is Matrix of n,width M,D
  proof let M be Matrix of D;
    assume that A1: len M=n and
A2: not M is Matrix of n,width M,D; set m= width M;
    per cases by A2,MATRIX_1:def 3;
      suppose len M<>n;
    hence contradiction by A1;
   end;
   suppose ex p be FinSequence of D st p in rng M & not (len p=m);
    then consider p be FinSequence of D such that
A3: p in rng M and A4: len p<>m;
    consider k such that
A5: for x st x in rng M ex q st x = q & len q = k by MATRIX_1:9;
    reconsider x=p as set;
A6: ex q st x = q & len q = k by A3,A5;
     now per cases;
     suppose n= 0;
      then M={} by A1,FINSEQ_1:25;
      hence len p= m by A3,FINSEQ_1:27;
     end;
     suppose n>0;
      then consider s being FinSequence such that
   A7:s in rng M and A8:len s = width M by A1,MATRIX_1:def 4;
      reconsider y=s as set;
       ex r st y = r & len r = k by A5,A7;
      hence len p= m by A6,A8;
    end;
    end;
  hence contradiction by A4;
  end;
  end;

begin :: Deleting of Rows and Columns in a Matrix

canceled 2;

theorem Th10:
  for M be Matrix of n,m,D holds
  for k st k in Seg n holds M.k=Line(M,k)
  proof
   let M be Matrix of n,m,D;
   let k;
   assume A1: k in Seg n;
A2: len M = n by MATRIX_1:26;
    dom M = Seg len M by FINSEQ_1:def 3;
   then A3: M.k in rng M by A1,A2,FUNCT_1:def 5;
   per cases;
    suppose n=0;
     hence M.k=Line(M,k) by A1,FINSEQ_1:4;
    end;
    suppose A4:0 < n;
     then A5:width M=m by MATRIX_1:24;
     consider l such that
      A6:for x st x in rng M ex p be FinSequence of D
           st x = p & len p = l by MATRIX_1:9;
     consider p being FinSequence of D such that
      A7:M.k = p and len p= l by A3,A6;
     A8:len p=width M by A3,A5,A7,MATRIX_1:def 3;
         for j st j in Seg width M holds p.j = M*(k,j)
      proof
       let j;
       assume j in Seg width M;
       then [k,j] in [:Seg n,Seg m:] by A1,A5,ZFMISC_1:106;
       then [k,j] in Indices M by A4,MATRIX_1:24;
       then ex q being FinSequence of D st q=M.k & M*(k,j)=q.j
            by MATRIX_1:def 6;
       hence thesis by A7;
      end;
     hence M.k=Line(M,k) by A7,A8,MATRIX_1:def 8;
  end;
  end;

Lm1:
  for M be Matrix of K holds
  for k st k in dom M holds M.k=Line(M,k)
  proof
   let M be Matrix of K;
   let k;
   assume
A1: k in dom M;
    then 1 <= k & k <= len M by FINSEQ_3:27;
    then reconsider N = M as Matrix of len M, width M, K by MATRIX_1:20;
     k in Seg len N by A1,FINSEQ_1:def 3;
  hence thesis by Th10;
 end;

definition let i, K;
           let M be Matrix of K;
 canceled;
  func DelCol(M,i) -> Matrix of K means
     len it=len M & for k st k in dom M holds it.k=Del(Line(M,k),i);
  existence
  proof
    per cases;
    suppose
A1:   not i in Seg width M;
      take M;
      thus len M = len M;
      let k such that
A2:     k in dom M;
        len Line(M,k) = width M by MATRIX_1:def 8;
       then A3:     not i in dom Line(M,k) by A1,FINSEQ_1:def 3;
      thus M.k = Line(M,k) by A2,Lm1
       .= Del(Line(M,k),i) by A3,FINSEQ_3:113;
    end;
    suppose
A4:   i in Seg width M;
    defpred P[Nat,Nat,Element of K] means $3=Del(Line(M,$1),i).$2;
    per cases;
      suppose len M=0;
        hence ex N be Matrix of K st len N=len M&
        for k st k in dom M holds N.k=Del(Line(M,k),i)
          by A4,FINSEQ_1:4,MATRIX_1:def 4;
      end;
      suppose A5:len M>0;set n1=width M;
    per cases;
    suppose n1=0;
      hence ex N be Matrix of K st len N=len M&
        for k st k in dom M holds N.k=Del(Line(M,k),i) by A4,FINSEQ_1:4;
    end;
    suppose n1>0;
      then consider m being Nat such that A6:n1=m+1 by NAT_1:6;
      reconsider m as Element of NAT by ORDINAL1:def 13;
     A7:for k st k in dom M holds len (Del(Line(M,k),i))=m
      proof let k;
      assume k in dom M;
       A8:len Line(M,k) = n1 by MATRIX_1:def 8;
       then i in dom Line(M,k) by A4,FINSEQ_1:def 3;
       then ex l being Nat st
       len Line(M,k)=l+1 & len Del(Line(M,k),i)=l
          by FINSEQ_3:113;
        hence thesis by A6,A8;
      end;
   A9:for k,l st [k,l] in [:Seg len M, Seg m:]
        for x1,x2 being Element of K st P[k,l,x1] & P[k,l,x2]
         holds x1 = x2;
     A10:for k,l st [k,l] in [:Seg len M, Seg m:]
        ex x being Element of K st P[k,l,x]
        proof
         let k,l;
         assume A11: [k,l] in [:Seg len M, Seg m:];
          dom M = Seg len M by FINSEQ_1:def 3;
         then A12: k in dom M & l in Seg m by A11,ZFMISC_1:106;
         reconsider p=Del(Line(M,k),i) as FinSequence of K by FINSEQ_3:114;
     A13:len p = m by A7,A12;
          dom p = Seg len p by FINSEQ_1:def 3;
         then reconsider x=p.l as Element of (the carrier of K)
           by A12,A13,FINSEQ_2:13;
         take x;
         thus thesis;
       end;
       consider N being Matrix of len M,m,K such that
   A14:for k,l st [k,l] in Indices N holds P[k,l,N*(k,l)]
         from MATRIX_1:sch 2(A9,A10);
        dom M = Seg len M by FINSEQ_1:def 3;
       then A15: len N=len M & width N = m & Indices N = [:dom M, Seg m:]
                                                         by A5,MATRIX_1:24;
A16:   for k,l st k in dom M & l in Seg m holds N*(k,l)=Del(Line(M,k),i).l
       proof
         let k,l;
         assume k in dom M& l in Seg m;
         then [k,l] in Indices N by A15,ZFMISC_1:106;
         hence thesis by A14;
       end;
       reconsider N as Matrix of K;
       take N;
        for k st k in dom M holds N.k=Del(Line(M,k),i)
       proof
         let k;
         assume A17:k in dom M;
         then k in Seg len M by FINSEQ_1:def 3;
         then A18: N.k=Line(N,k) by Th10;
         then reconsider s=N.k as FinSequence;
         A19:len s= m by A15,A18,MATRIX_1:def 8;
         A20:len Del (Line(M,k),i)=m by A7,A17;
          now let j be Nat;
           assume A21:j in Seg m;
           then Line(N,k).j=N*(k,j) by A15,MATRIX_1:def 8;
           hence s.j=Del(Line(M,k),i).j by A16,A17,A18,A21;
         end;
         hence thesis by A19,A20,FINSEQ_2:10;
       end;
       hence len N=len M&
         for k st k in dom M holds N.k=Del(Line(M,k),i) by A5,MATRIX_1:24;
      end;
    end;
   end;
  end;
  uniqueness
  proof
   let M1,M2 be Matrix of K;
   assume that
    A22: len M1=len M and
     A23:for k st k in dom M holds M1.k=Del(Line(M,k),i) and
    A24: len M2=len M and
      A25:for k st k in dom M holds M2.k=Del(Line(M,k),i);
A26: now let k be Nat;
    assume A27:k in dom M;
    hence M1.k=Del(Line(M,k),i) by A23
             .=M2.k by A25,A27;
   end;
      dom M = Seg len M by FINSEQ_1:def 3;
  hence M1=M2 by A22,A24,A26,FINSEQ_2:10;
  end;
end;

theorem Th11:
  for M1,M2 being Matrix of D st M1@=M2@ & len M1=len M2 holds M1 = M2
  proof
    let M1,M2 be Matrix of D;
    assume A1: M1@=M2@ & len M1=len M2;
     len (M1@) = width M1 &
    (for i,j holds [i,j] in Indices M1@ iff [j,i] in Indices M1) &
    for i,j st [j,i] in Indices M1 holds M1@*(i,j)=M1*(j,i)
      by MATRIX_1:def 7;
    then A2: width M1=width M2 by A1,MATRIX_1:def 7;
A3: Indices M1=[:dom M1,Seg width M1:] &
    Indices M2=[:dom M2,Seg width M2:];
     for i,j st [i,j] in Indices M1 holds M1*(i,j) = M2*(i,j)
    proof let i,j;
      assume A4: [i,j] in Indices M1;
       dom M1 = Seg len M2 by A1,FINSEQ_1:def 3
            .= dom M2 by FINSEQ_1:def 3;
      then M2@*(j,i) = M2*(i,j) by A2,A3,A4,MATRIX_1:def 7;
      hence M1*(i,j)=M2*(i,j) by A1,A4,MATRIX_1:def 7;
    end;
    hence M1=M2 by A1,A2,MATRIX_1:21;
  end;

theorem Th12:
  for M being Matrix of D st width M > 0 holds
    len (M@)=width M & width (M@)=len M
proof
  let M be Matrix of D;
  assume A1:width M>0;
  thus len (M@)=width M by MATRIX_1:def 7;
  per cases;
    suppose len M=0;
      hence width (M@)=len M by A1,MATRIX_1:def 4;
    end;
    suppose A2: len M>0;
       dom M = Seg len M by FINSEQ_1:def 3;
      then A3: Seg width M<>{} & dom M<>{} by A1,A2,FINSEQ_1:5;
       for i,j being set holds [i,j]in [:dom (M@),Seg width (M@):] iff
        [i,j] in [:Seg width M,dom M:]
      proof let i,j be set;
        thus [i,j] in [:dom (M@),Seg width (M@):] implies
        [i,j] in [:Seg width M,dom M:]
        proof
          assume A4:[i,j]in [:dom (M@),Seg width (M@):];
          then i in dom (M@)& j in Seg width (M@) by ZFMISC_1:106;
          then reconsider i,j as Nat by ORDINAL1:def 13;
           [i,j] in Indices (M@) by A4;
          then [j,i] in Indices M by MATRIX_1:def 7;
          hence thesis by ZFMISC_1:107;
        end;
        assume A5:[i,j] in [:Seg width M,dom M:];
        then i in Seg width M & j in dom M by ZFMISC_1:106;
        then reconsider i,j as Nat by ORDINAL1:def 13;
          [j,i] in Indices M by A5,ZFMISC_1:107;
         then [i,j] in Indices (M@) by MATRIX_1:def 7;
        hence thesis;
      end;
      then [:Seg width M,dom M:]=[:dom (M@),Seg width (M@):] by ZFMISC_1:108;
      then Seg width M= dom (M@) & dom M=Seg width (M@) by A3,ZFMISC_1:134;
      hence len M=width (M@) by FINSEQ_1:def 3;
    end;
  end;

theorem
   for M1,M2 being Matrix of D st
    width M1>0 & width M2>0 & M1@=M2@ & width (M1@) = width (M2@) holds
      M1=M2
proof
  let M1,M2 be Matrix of D;
  assume A1:width M1>0 & width M2>0;
  assume A2: M1@=M2@ & width (M1@) =width (M2@);
   width (M1@)=len M1 & width (M2@)=len M2 by A1,Th12;
  hence M1=M2 by A2,Th11;
end;

theorem
  for M1,M2 being Matrix of D st width M1>0 & width M2>0 holds
     M1=M2 iff M1@=M2@ & width M1 = width M2
    proof
     let M1,M2 be Matrix of D;
     assume A1:width M1>0 & width M2>0;
     thus M1=M2 implies M1@=M2@ & width (M1) =width (M2);
     assume A2: M1@=M2@ & width (M1) =width (M2);
        now
           now
             len M1=width (M1@) by A1,Th12;
         then A3:len M1=len M2 by A1,A2,Th12;
A4:       Indices M1=[:dom M1,Seg width M1:] &
         Indices M2=[:dom M2,Seg width M2:];
            (for i,j st [i,j] in Indices M1 holds M1*(i,j) = M2*(i,j))
          proof let i,j;
           assume A5: [i,j] in Indices M1;
               dom M1 = Seg len M2 by A3,FINSEQ_1:def 3
                 .= dom M2 by FINSEQ_1:def 3;
           then M2@*(j,i)=M2*(i,j) by A2,A4,A5,MATRIX_1:def 7;
           hence M1*(i,j)=M2*(i,j) by A2,A5,MATRIX_1:def 7;
          end;
        hence M1=M2 by A2,A3,MATRIX_1:21;
       end;
       hence M1=M2;
      end;
      hence M1=M2;
    end;

theorem Th15:
  for M being Matrix of D st len M>0 & width M>0 holds (M@)@=M
  proof
    let M be Matrix of D;
    assume A1:len M>0 & width M>0;
    set N=M@;
A2: len N = width M &
    (for i,j holds [i,j] in Indices N iff [j,i] in Indices M) &
    for i,j st [j,i] in Indices M holds N*(i,j) = M*(j,i) by MATRIX_1:def 7;
 A3:width N= len M by A1,Th12;
 A4:width N>0 by A1,Th12;
A5: len (N@) = width N &
    (for i,j holds [i,j] in Indices N@ iff [j,i] in Indices N) &
    for i,j st [j,i] in Indices N holds N@*(i,j) = N*(j,i) by MATRIX_1:def 7;
 A6:width (N@)=width M by A2,A4,Th12;
     dom M = Seg len M & dom (N@) = Seg len (N@) by FINSEQ_1:def 3;
then A7: Indices (N@)=Indices M by A1,A2,A3,A5,Th12;
     for i,j st [i,j] in Indices (N@) holds (N@)*(i,j) = M*(i,j)
    proof
      let i,j;
      assume A8:[i,j] in Indices (N@);
      then [j,i] in Indices N by MATRIX_1:def 7;
      then (N@)*(i,j)=N*(j,i) by MATRIX_1:def 7;
      hence (N@)*(i,j)=M*(i,j) by A7,A8,MATRIX_1:def 7;
    end;
    hence thesis by A3,A5,A6,MATRIX_1:21;
  end;

theorem Th16:
  for M being Matrix of D holds
    for i st i in dom M holds Line(M,i)=Col(M@,i)
  proof
    let M be Matrix of D;let i;
    assume A1:i in dom M;
 A2:len Line(M,i) = width M & for j st j in Seg width M holds
      Line(M,i).j = M*(i,j) by MATRIX_1:def 8;
 A3:len Col(M@,i) = len (M@) &
    for j st j in dom (M@) holds Col(M@,i).j = M@*(j,i) by MATRIX_1:def 9;
 A4:len (M@)=width M by MATRIX_1:def 7;
 A5:len Col(M@,i)=width M by A3,MATRIX_1:def 7;
     now let j be Nat;
      assume A6:j in Seg width M;
      then A7:Line(M,i).j=M*(i,j) by MATRIX_1:def 8;
 A8: [i,j] in Indices M by A1,A6,ZFMISC_1:106;
A9:   dom (M@) = Seg len (M@) by FINSEQ_1:def 3;
      thus Line(M,i).j=M@*(j,i) by A7,A8,MATRIX_1:def 7
                    .=Col(M@,i).j by A4,A6,A9,MATRIX_1:def 9;
    end;
    hence thesis by A2,A5,FINSEQ_2:10;
  end;

theorem
   for M being Matrix of D holds
    for j st j in Seg width M holds Line(M@,j)=Col(M,j)
  proof
    let M be Matrix of D;let j;
    assume A1:j in Seg width M;
    then j in Seg len (M@) by MATRIX_1:def 7;
    then A2: j in dom (M@) by FINSEQ_1:def 3;
    per cases;
    suppose len M=0;
      hence Line(M@,j)=Col(M,j) by A1,FINSEQ_1:4,MATRIX_1:def 4;
    end;
    suppose A3:len M>0;
      per cases;
      suppose width M=0;
        hence Line(M@,j)=Col(M,j) by A1,FINSEQ_1:4;
      end;
      suppose width M>0;
        then (M@)@=M by A3,Th15;
        hence Line(M@,j)=Col(M,j) by A2,Th16;
      end;
    end;
  end;

theorem
  for M being Matrix of D holds for i st i in dom M holds M.i=Line(M,i)
proof let M be Matrix of D;let i;
  assume A1:i in dom M;
then A2:M.i in rng M by FUNCT_1:def 5;
    rng M c= D* by FINSEQ_1:def 4;
   then reconsider p=M.i as FinSequence of D by A2,FINSEQ_1:def 11;
    rng M <> {} by A1,FUNCT_1:12;
   then M<> {} by FINSEQ_1:27;
   then len M <> 0 & len M >= 0 by FINSEQ_1:25;
   then M is Matrix of len M,width M,D by MATRIX_1:20;
   then A3:len p = width M by A2,MATRIX_1:def 3;
A4:for j st j in Seg (width M) holds M*(i,j) =p.j
   proof let j;
     assume j in Seg(width M);
     then [i,j] in Indices M by A1,ZFMISC_1:106;
     then ex q being FinSequence of D st q=M.i & q.j=M*(i,j)
       by MATRIX_1:def 6;
     hence thesis;
   end;
A5:len Line(M,i)= width M &
     for j st j in Seg width M holds (Line(M,i)).j=M*(i,j) by MATRIX_1:def 8;
    now let j be Nat;
     assume A6:j in Seg width M;
     hence (Line(M,i)).j=M*(i,j) by MATRIX_1:def 8
                       .=p.j by A4,A6;
   end;
   hence thesis by A3,A5,FINSEQ_2:10;
end;

notation let K, i; let M be Matrix of K;
  synonym DelLine(M,i) for Del(M,i);
end;

registration let K, i; let M be Matrix of K;
  cluster DelLine(M,i) -> tabular;
  coherence
   proof
     consider n being Nat such that
A1:    for x st x in rng M
        ex s being FinSequence st s=x & len s = n by MATRIX_1:def 1;
    take n;
    let x;
A2:   rng DelLine(M,i) c= rng M by FINSEQ_3:115;
    assume x in rng DelLine(M,i);
   hence ex s being FinSequence st s=x & len s = n by A1,A2;
   end;
end;

definition let K, i; let M be Matrix of K;
  redefine func DelLine(M,i) -> Matrix of K;
  coherence by FINSEQ_3:114;
end;

definition let i,j,n,K; let M be Matrix of n,K;
  canceled;
  func Deleting(M,i,j) -> Matrix of K equals
      DelCol(DelLine(M,i),j);
  coherence;
end;

begin :: Sets of Permutations

definition let IT be set;
  attr IT is permutational means
:Def9: ex n st for x st x in IT holds x is Permutation of Seg n;
end;

registration
  cluster permutational non empty set;
  existence
   proof
   consider n;consider p being Permutation of Seg n;
   take P={p};
   thus P is permutational
    proof
     take n;let x;
     assume x in P;
     hence thesis by TARSKI:def 1;
    end;
  thus thesis;
  end;
end;

definition let P be permutational non empty set;
  func len P -> Nat means
:Def10: ex s st s in P & it=len s;
  existence
   proof
    consider x being Element of P;
    consider n such that
A1: for y st y in P holds y is Permutation of Seg n by Def9;
    reconsider n as Element of NAT by ORDINAL1:def 13;
    reconsider q = x as Permutation of Seg n by A1;
A2: dom q=Seg n by FUNCT_2:67;
    then reconsider q as FinSequence by FINSEQ_1:def 2;
    take n,q;
    thus thesis by A2,FINSEQ_1:def 3;
   end;
  uniqueness
    proof
     let n1,n2 be Nat;
     given s1 be FinSequence such that A3:s1 in P & n1=len s1;
     given s2 such that A4:s2 in P & n2=len s2;
     consider n such that
A5:  for y st y in P holds y is Permutation of Seg n by Def9;
A6:  n in NAT by ORDINAL1:def 13;
     A7:s1 is Permutation of Seg n & s2 is Permutation of Seg n by A3,A4,A5;
      s1 is Function of Seg n,Seg n by A3,A5;
     then A8:dom s1=Seg n by FUNCT_2:67;
      dom s2=Seg n by A7,FUNCT_2:67;
     then len s1=n & len s2=n by A6,A8,FINSEQ_1:def 3;
     hence thesis by A3,A4;
    end;
 end;

definition let P be permutational non empty set;
  redefine func len P -> Element of NAT;
  coherence by ORDINAL1:def 13;
end;

definition let P be permutational non empty set;
  redefine mode Element of P -> Permutation of Seg len P;
  coherence
  proof
    let x be Element of P;
    consider n such that
A1: for y st y in P holds y is Permutation of Seg n by Def9;
    reconsider n as Element of NAT by ORDINAL1:def 13;
    reconsider q=x as Permutation of Seg n by A1;
    A2:dom q=Seg n by FUNCT_2:67;
    then reconsider q as FinSequence by FINSEQ_1:def 2;
     len q=n by A2,FINSEQ_1:def 3;
    then Seg (len P) = Seg n by Def10;
    hence thesis by A1;
  end;
 end;

theorem
   ex P being permutational non empty set st len P =n
  proof
    consider p being Permutation of Seg n;
    set P={p};
     P is permutational non empty set
    proof
       now take n;let x;
        assume x in P;
        hence x is Permutation of Seg n by TARSKI:def 1;
      end;
      hence thesis by Def9;
    end;
    then reconsider P as permutational non empty set;
 A1:len P= n
    proof
      consider x being Element of P;
A2:   n in NAT by ORDINAL1:def 13;
      reconsider y=x as Function of Seg n,Seg n by TARSKI:def 1;
   A3:dom y=Seg n by FUNCT_2:67;
      then reconsider s=y as FinSequence by FINSEQ_1:def 2;
       len P= len s by Def10;
      hence thesis by A2,A3,FINSEQ_1:def 3;
    end;
    take P;
    thus thesis by A1;
  end;

definition let n;
  func Permutations n -> set means :Def11:
    x in it iff x is Permutation of Seg n;
existence
 proof
 defpred P[set] means $1 is Permutation of Seg n;
 consider P being set such that A1:for x holds x in P iff x in
 Funcs(Seg n,Seg n) & P[x] from XBOOLE_0:sch 1;
  take P;
  let x;
  thus x in P implies x is Permutation of Seg n by A1;
    assume A2:x is Permutation of Seg n;
    then x in Funcs(Seg n,Seg n) by FUNCT_2:12;
    hence thesis by A1,A2;
end;
uniqueness
 proof let P1,P2 be set;
  assume that A3: for x holds x in P1 iff x is Permutation of Seg n and
             A4:for x holds x in P2 iff x is Permutation of Seg n;
 A5:now let x;
   assume x in P1;
   then x is Permutation of Seg n by A3;
   hence x in P2 by A4;
  end;
   now let x;
   assume x in P2;
   then x is Permutation of Seg n by A4;
   hence x in P1 by A3;
  end;
 hence thesis by A5,TARSKI:2;
 end;
end;

registration let n;
  cluster Permutations(n) -> permutational non empty;
  coherence
 proof
 defpred P[set] means $1 is Permutation of Seg n;
 consider P being set such that A1:for x holds x in P iff x in
 Funcs(Seg n,Seg n) & P[x] from XBOOLE_0:sch 1;
   idseq n in Funcs(Seg n,Seg n) by FUNCT_2:12;
  then reconsider P as non empty set by A1;
      P is permutational non empty set
   proof now take n;let x;
    assume x in P;
    hence x is Permutation of Seg n by A1;
    end;
    hence thesis by Def9;
   end;
  then reconsider P as permutational non empty set;
   x in P iff x is Permutation of Seg n
   proof
    thus x in P implies x is Permutation of Seg n by A1;
    assume A2:x is Permutation of Seg n;
    then x in Funcs(Seg n,Seg n) by FUNCT_2:12;
    hence thesis by A1,A2;
 end;
 hence thesis by Def11;
end;
end;

theorem
   len Permutations(n)=n
 proof
  consider x being Element of Permutations(n);
  reconsider q=x as Permutation of Seg n by Def11;
A1: n in NAT by ORDINAL1:def 13;
  A2:dom q=Seg n by FUNCT_2:67;
  then reconsider q as FinSequence by FINSEQ_1:def 2;
   len q=n by A1,A2,FINSEQ_1:def 3;
  hence thesis by Def10;
 end;

theorem
   Permutations 1 = {idseq 1}
   proof
A1: Permutations 1 c={idseq 1}
    proof let p be set;
     assume p in Permutations 1;
     then reconsider q=p as Permutation of Seg 1 by Def11;
     A2:q is Function of Seg 1,Seg 1 & rng q = Seg 1 by FUNCT_2:def 3;
     A3:dom q=Seg 1 by FUNCT_2:67;
      1 in {1} by TARSKI:def 1;
     then q.1 in Seg 1 by A2,FINSEQ_1:4,FUNCT_2:6;
     then A4:q.1=1 by FINSEQ_1:4,TARSKI:def 1;
     reconsider q as FinSequence by A3,FINSEQ_1:def 2;
      len q= 1 by A3,FINSEQ_1:def 3;
     then q = idseq 1 by A4,FINSEQ_1:57,FINSEQ_2:59;
     hence p in {idseq 1} by TARSKI:def 1;
    end;
     {idseq 1} c= Permutations(1)
    proof let x;
     assume x in {idseq 1};
     then x= idseq 1 by TARSKI:def 1;
     hence x in Permutations(1) by Def11;
    end;
    hence thesis by A1,XBOOLE_0:def 10;
   end;

canceled;

begin :: Group of Permutations

 reserve p,q for Element of Permutations(n);

definition let n;
  canceled;
  func Group_of_Perm(n) -> strict HGrStr means :Def13:
    the carrier of it = Permutations(n) &
    for q,p be Element of Permutations(n) holds (the mult of it).(q,p)=p*q;
 existence
 proof
  defpred P[Element of Permutations(n),Element of Permutations(n),set] means
  $3 = $2*$1;
  A1: for q,p being Element of Permutations(n)
       ex z being Element of Permutations(n) st P[q,p,z]
      proof
       let q,p be Element of Permutations(n);
       reconsider p,q as Permutation of Seg n by Def11;
       reconsider z=p*q as Element of Permutations(n) by Def11;
       take z;
       thus thesis;
      end;
    consider o being BinOp of Permutations(n) such that
    A2:for q,p being Element of Permutations(n)
      holds P[q,p,o.(q,p)] from BINOP_1:sch 3(A1);
   take HGrStr(# Permutations(n),o#);
   thus thesis by A2;
end;
 uniqueness
 proof
  let G1,G2 be strict HGrStr;
  assume that A3:the carrier of G1 = Permutations(n) and
 A4:for q,p be Element of Permutations(n) holds (the mult of G1).(q,p)=p*q
    and A5:the carrier of G2 = Permutations(n) and
    A6:for q,p be Element of Permutations(n) holds
      (the mult of G2).(q,p)=p*q;
      now let q,p be Element of G1;
   reconsider q' = q, p' = p as Element of Permutations(n) by A3;
   thus (the mult of G1).(q,p)=p'*q' by A4
                             .=(the mult of G2).(q,p) by A6;
  end;
  hence thesis by A3,A5,BINOP_1:2;
 end;
end;

registration let n;
  cluster Group_of_Perm(n) -> non empty;
  coherence
  proof
    the carrier of Group_of_Perm(n) = Permutations(n) by Def13;
   hence the carrier of Group_of_Perm(n) is non empty;
  end;
end;

theorem
Th23: idseq n is Element of Group_of_Perm(n)
  proof
     the carrier of Group_of_Perm(n) = Permutations(n) by Def13;
    hence thesis by Def11;
  end;

theorem Th24:
  p *(idseq n)=p & (idseq n)*p=p
  proof
     p is Permutation of Seg n by Def11;
    then A1: rng p= Seg n by FUNCT_2:def 3;
    A2:p is Function of Seg n,Seg n by Def11;
     Seg n = {} implies Seg n= {};
    then dom p= Seg n by A2,FUNCT_2:def 1;
    hence thesis by A1,RELAT_1:78,80;
  end;

theorem
Th25: p *p"=idseq n & p"*p=idseq n
  proof
 p is Permutation of Seg n by Def11;
    hence thesis by FUNCT_2:88;
  end;

theorem Th26:
  p" is Element of Group_of_Perm(n)
  proof
    reconsider p as Permutation of Seg n by Def11;
     p" is Element of Permutations(n) by Def11;
    hence thesis by Def13;
  end;

registration let n;
  cluster Group_of_Perm(n) -> associative Group-like;
  coherence
  proof
A1: for p,q,r being Element of Group_of_Perm(n) holds
      (p * q) * r = p * (q * r)
    proof
      let p,q,r be Element of Group_of_Perm(n);
      reconsider p'=p,q'=q,r'=r as Element of Permutations(n) by Def13;
   A2:q'*p' is Permutation of Seg n & r'*q' is Permutation of Seg n
      proof
        reconsider p',q',r' as Permutation of Seg n by Def11;
    A3: q'*p' is Permutation of Seg n;
         r'*q' is Permutation of Seg n;
        hence thesis by A3;
      end;
      then A4:q' * p' is Element of Permutations(n) by Def11;
   A5:r' * q' is Element of Permutations(n) by A2,Def11;
       (p * q) * r = (the mult of Group_of_Perm n).((p*q),r) by GROUP_1:def 1
                 .= (the mult of Group_of_Perm n).
            (((the mult of Group_of_Perm n).(p',q')),r') by GROUP_1:def 1
                 .=(the mult of Group_of_Perm n).((q'*p'),r') by Def13
                 .=r'*(q' *p' ) by A4,Def13
                 .=(r' *q') * p' by RELAT_1:55
                 .=(the mult of Group_of_Perm n).(p',(r'*q')) by A5,Def13
                 .=(the mult of Group_of_Perm n).
                     (p,(the mult of Group_of_Perm n).(q,r)) by Def13
                 .=(the mult of Group_of_Perm n).(p,(q*r)) by GROUP_1:def 1
                 .=p *(q* r) by GROUP_1:def 1;
      hence thesis;
    end;
     ex e being Element of Group_of_Perm(n) st
    for p being Element of Group_of_Perm(n) holds
    p * e = p & e * p = p &
    ex g being Element of Group_of_Perm(n) st p * g = e & g * p = e
    proof
      reconsider e'=idseq n as Element of Permutations(n) by Def11;
      reconsider e=idseq n as Element of Group_of_Perm(n) by Th23;
 A6:  for p being Element of Group_of_Perm(n) holds p * e =p & e* p=p
      proof
        let p be Element of Group_of_Perm(n);
        reconsider p'=p as Element of Permutations(n) by Def13;
     A7:p * e =(the mult of Group_of_Perm(n)).(p',e') by GROUP_1:def 1
             .=e' * p' by Def13
             .=p by Th24;
         e * p =(the mult of Group_of_Perm(n)).(e',p') by GROUP_1:def 1
             .=p' * e' by Def13
             .=p by Th24;
        hence thesis by A7;
      end;
      take e;
       for p being Element of Group_of_Perm(n) holds
        ex g being Element of Group_of_Perm(n) st p * g= e & g * p=e
      proof
        let p be Element of Group_of_Perm(n);
        reconsider q=p as Element of Permutations(n) by Def13;
        reconsider r=q as Permutation of Seg n by Def11;
        reconsider f=r" as Element of Permutations(n) by Def11;
        reconsider g=f as Element of Group_of_Perm(n) by Th26;
    A8: p * g=(the mult of Group_of_Perm(n)).(q,f) by GROUP_1:def 1
            .=f*q by Def13
            .=e by Th25;
    A9: g * p=(the mult of Group_of_Perm(n)).(f,q) by GROUP_1:def 1
            .=q*f by Def13
            .=e by Th25;
        take g;
        thus thesis by A8,A9;
      end;
      hence thesis by A6;
    end;
    hence thesis by A1,GROUP_1:5;
  end;
end;

canceled;

theorem Th28:
  idseq n= 1_Group_of_Perm(n)
  proof
    reconsider e'=idseq n as Element of Permutations(n) by Def11;
    reconsider e=idseq n as Element of Group_of_Perm(n) by Th23;
     for p being Element of Group_of_Perm(n) holds p * e=p & e* p=p
    proof
      let p be Element of Group_of_Perm(n);
      reconsider p'=p as Element of Permutations(n) by Def13;
   A1:p * e =(the mult of Group_of_Perm(n)).(p',e') by GROUP_1:def 1
           .=e' * p' by Def13
           .=p by Th24;
       e * p =(the mult of Group_of_Perm(n)).(e',p') by GROUP_1:def 1
           .=p' * e' by Def13
           .=p by Th24;
      hence thesis by A1;
    end;
    hence thesis by GROUP_1:10;
  end;

definition let n;let p be Permutation of Seg n;
  attr p is being_transposition means
     ex i,j st i in dom p & j in dom p & i<>j & p.i=j & p.j=i &
      for k st k <>i & k<>j & k in dom p holds p.k=k;
end;

notation let n;let p be Permutation of Seg n;
  synonym p is_transposition for p is being_transposition;
end;

definition let n;
  let IT be Permutation of Seg n;
  attr IT is even means
:Def15:ex l be FinSequence of Group_of_Perm(n)
   st (len l) mod 2=0 & IT=Product l &
  for i st i in dom l ex q st l.i=q & q is_transposition;
end;

notation let n;
  let IT be Permutation of Seg n;
  antonym IT is odd for IT is even;
end;

theorem
   id Seg n is even
  proof
    set l=<*>the carrier of Group_of_Perm n;
     0 mod 2= 0
    proof
       0=2 * 0 + 0;
      hence thesis by NAT_D:def 2;
    end;
    then A1:(len l) mod 2=0 by FINSEQ_1:32;
     Product <*> the carrier of Group_of_Perm(n)=1_Group_of_Perm(n)
      by GROUP_4:11;
      then A2: idseq n=Product l by Th28;
     for i st i in dom l ex q st l.i=q & q is_transposition by FINSEQ_1:26;
    hence thesis by A1,A2,Def15;
  end;

definition let K,n;let x be Element of K;
  let p be Element of Permutations(n);
  func -(x,p) -> Element of K equals
     x if p is even otherwise -x;
  correctness;
end;

definition let X be set;
  assume A1:X is finite;
  func FinOmega X -> Element of Fin X equals
     X;
  coherence by A1,FINSUB_1:def 5;
end;

theorem
   Permutations n is finite
  proof
A1: Permutations n c= Funcs(Seg n,Seg n)
    proof
      let x;
      assume x in Permutations n;
      then x is Function of Seg n,Seg n by Def11;
      hence x in Funcs(Seg n,Seg n) by FUNCT_2:12;
    end;
     Funcs(Seg n,Seg n) is finite by FRAENKEL:16;
    hence thesis by A1,FINSET_1:13;
  end;
