:: Definitions and Basic Properties of Measurable Functions
::  by Noboru Endou , Katsumi Wasaki and Yasunari Shidama
::
:: Received September 7, 2000
:: Copyright (c) 2000 Association of Mizar Users

environ

 vocabularies NUMBERS, SUBSET_1, REAL_1, INT_1, RAT_1, ARYTM_1, CARD_1,
      XBOOLE_0, TARSKI, FUNCT_1, FUNCT_2, RELAT_1, NAT_1, ARYTM_3, ZFMISC_1,
      CARD_3, ORDINAL1, PARTFUN1, XXREAL_0, VALUED_1, MEASURE6, SUPINF_2,
      SUPINF_1, COMPLEX1, XREAL_0, PROB_1, VALUED_0, SETFAM_1, MESFUNC1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, ORDINAL1, CARD_1, NUMBERS,
      XCMPLX_0, XREAL_0, MEASURE6, REAL_1, RELAT_1, FUNCT_1, PARTFUN1, FUNCT_2,
      NAT_1, INT_1, RAT_1, CARD_3, XXREAL_0, VALUED_0, PROB_1, SUPINF_1,
      SUPINF_2, MEASURE2, MEASURE3, EXTREAL1, RELSET_2;
 constructors WELLORD2, REAL_1, NAT_1, RAT_1, MEASURE3, MEASURE6, EXTREAL1,
      SUPINF_1, RELSET_2, PARTFUN1, EQREL_1, RELAT_2, RELSET_1, BINOP_2;
 registrations XBOOLE_0, SUBSET_1, ORDINAL1, RELSET_1, NUMBERS, XXREAL_0,
      XREAL_0, NAT_1, INT_1, CARD_1, MEMBERED, MEASURE1, VALUED_0, XXREAL_3;
 requirements NUMERALS, BOOLE, SUBSET, REAL, ARITHM;
 definitions TARSKI, MEASURE6, RELAT_1;
 theorems MEASURE1, TARSKI, SUBSET_1, PARTFUN1, FUNCT_1, FUNCT_2, MEASURE5,
      SETFAM_1, NAT_1, INT_1, CARD_1, CARD_4, WELLORD2, RAT_1, XREAL_0,
      EXTREAL1, XBOOLE_0, XBOOLE_1, XCMPLX_1, NUMBERS, XREAL_1, FINSUB_1,
      PROB_1, XXREAL_0, ORDINAL1, CARD_3, XXREAL_3, RELAT_1;
 schemes SUBSET_1, FUNCT_2, SEQ_1, XBOOLE_0;

begin  :: Cardinal numbers of INT and RAT

reserve k for Element of NAT;
reserve r for Real;
reserve i for Integer;
reserve q for Rational;

definition
  func INT- -> Subset of REAL means
  :Def1:
  r in it iff ex k st r = - k;
  existence
  proof
    defpred P[Element of REAL] means ex k st $1 = -k;
    consider IT being Subset of REAL such that
A1: for r being Element of REAL holds r in IT iff P[r] from SUBSET_1:sch 3;
    take IT;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let D1,D2 be Subset of REAL such that
A2: r in D1 iff ex k st r = - k and
A3: r in D2 iff ex k st r = - k;
 for r holds r in D1 iff r in D2
    proof
      let r;
      thus r in D1 implies r in D2
      proof
        assume r in D1;
then      ex k st r = - k by A2;
        hence thesis by A3;
      end;
      assume r in D2;
then    ex k st r = - k by A3;
      hence thesis by A2;
    end;
    hence thesis by SUBSET_1:8;
  end;
  correctness;
end;

Lm1: 0 = -0;

registration
  cluster INT- -> non empty;
  coherence by Def1,Lm1;
end;

theorem Th1:
  NAT,INT- are_equipotent
proof
  defpred P[Element of NAT,set] means $2=-$1;
A1: for x being Element of NAT ex y being Element of INT- st P[x,y]
  proof
    let x be Element of NAT;
 -x in INT- by Def1;
    hence thesis;
  end;
  consider f being Function of NAT,INT- such that
A3: for x being Element of NAT holds P[x,f.x] from FUNCT_2:sch 3(A1);
A4: f in Funcs(NAT,INT-) by FUNCT_2:11;
then A5: dom f = NAT by FUNCT_2:169;
A6: for x1,x2 being set st x1 in dom f & x2 in dom f & f.x1=f.x2 holds x1=x2
  proof
    let x1,x2 be set;
    assume that
A7: x1 in dom f and
A8: x2 in dom f and
A9: f.x1=f.x2;
    reconsider x1 as Element of NAT by A4,A7,FUNCT_2:169;
    reconsider x2 as Element of NAT by A4,A8,FUNCT_2:169;
 f.x1 = -x1 & f.x2 = -x2 by A3;
then  --x1=x2 by A9;
    hence thesis;
  end;
A12: for y being set st y in INT- holds f"{y} <> {}
  proof
    let y being set;
    assume
A13: y in INT-;
    then reconsider y as Real;
    consider k being Element of NAT such that
A14: y = -k by A13,Def1;
 f.k = -k by A3;
then  f.k in {y} by A14,TARSKI:def 1;
    hence thesis by A5,FUNCT_1:def 13;
  end;
A17: f is one-to-one by A6,FUNCT_1:def 8;
 rng f = INT- by A12,FUNCT_2:49;
  hence thesis by A5,A17,WELLORD2:def 4;
end;

theorem Th2:
  INT=INT- \/ NAT
proof
 for x being set st x in INT holds x in INT- \/ NAT
  proof
    let x be set;
    assume x in INT;
    then consider k being Element of NAT such that
A3: x = k or x = - k by INT_1:def 1;
 now per cases by A3;
      suppose
     x = k;
        hence thesis by XBOOLE_0:def 3;
      end;
      suppose
     x = -k;
then      x in INT- by Def1;
        hence thesis by XBOOLE_0:def 3;
      end;
    end;
    hence thesis;
  end;
then A8: INT c= INT- \/ NAT by TARSKI:def 3;
 for x being set st x in INT- \/ NAT holds x in INT
  proof
    let x be set;
    assume
A10: x in INT- \/ NAT;
 now per cases by A10,XBOOLE_0:def 3;
      suppose
    x in INT-;
then     ex k being Element of NAT st x = -k by Def1;
        hence thesis by INT_1:def 1;
      end;
      suppose
    x in NAT;
        hence thesis by INT_1:def 1;
      end;
    end;
    hence thesis;
  end;
then  INT- \/ NAT c= INT by TARSKI:def 3;
  hence thesis by A8,XBOOLE_0:def 10;
end;

theorem Th3:
  NAT,INT are_equipotent by Th1,Th2,CARD_3:119;

definition
  redefine func INT -> Subset of REAL;
  correctness by NUMBERS:15;
end;

definition
  let n be Nat;
  func RAT_with_denominator n -> Subset of RAT means
  :Def2:
  q in it iff ex i st q = i/n;
  existence
  proof
    defpred P[Element of RAT] means ex i st $1 = i/n;
    consider X being Subset of RAT such that
A1: for r being Element of RAT holds r in X iff P[r] from SUBSET_1:sch 3;
A2: for q being Rational holds q in X iff ex i st q = i/n
    proof
      let q be Rational;
      reconsider q as Element of RAT by RAT_1:def 2;
   q in X iff ex i st q = i/n by A1;
      hence thesis;
    end;
    take X;
    thus thesis by A2;
  end;
  uniqueness
  proof
    let D1,D2 be Subset of RAT such that
A4: q in D1 iff ex i st q = i/n and
A5: q in D2 iff ex i st q = i/n;
 for q being Element of RAT holds q in D1 iff q in D2
    proof
      let q be Element of RAT;
      thus q in D1 implies q in D2
      proof
        assume
A7:     q in D1;
        reconsider q as Rational;
     ex i st q = i/n by A4,A7;
        hence thesis by A5;
      end;
      assume
A9:   q in D2;
      reconsider q as Rational;
  ex i st q = i/n by A5,A9;
      hence thesis by A4;
    end;
    hence thesis by SUBSET_1:8;
  end;
end;

registration
  let n be Nat;
  cluster RAT_with_denominator(n+1) -> non empty;
  coherence
  proof
    reconsider i1=n+1 as Integer;
    reconsider q=0/i1 as Rational;
 q in RAT_with_denominator(n+1) by Def2;
    hence thesis;
  end;
end;

theorem Th4:
  for n being Nat holds INT,RAT_with_denominator (n+1) are_equipotent
proof
  let n be Nat;
  defpred P[Element of INT,set] means $2=$1/(n+1);
A1: for x being Element of INT ex y being Element of RAT_with_denominator(n+1)
  st P[x,y]
  proof
    let x be Element of INT;
    reconsider x as Integer;
    reconsider i1=n+1 as Integer;
    set y=x/i1;
 y in RAT by RAT_1:def 1;
    then reconsider y as Rational;
 y in RAT_with_denominator(n+1) by Def2;
    hence thesis;
  end;
  consider f being Function of INT,RAT_with_denominator(n+1) such that
A4: for x being Element of INT holds P[x,f.x] from FUNCT_2:sch 3(A1);
A5: f in Funcs(INT,RAT_with_denominator(n+1)) by FUNCT_2:11;
then A6: dom f = INT by FUNCT_2:169;
A7: for x1,x2 being set st x1 in dom f & x2 in dom f & f.x1=f.x2 holds x1=x2
  proof
    let x1,x2 be set;
    assume that
A8: x1 in dom f and
A9: x2 in dom f and
A10: f.x1=f.x2;
    reconsider x1 as Element of INT by A5,A8,FUNCT_2:169;
    reconsider x2 as Element of INT by A5,A9,FUNCT_2:169;
 f.x1 = x1/(n+1) & f.x2 = x2/(n+1) by A4;
    hence thesis by A10,XCMPLX_1:53;
  end;
A12: for y being set st y in RAT_with_denominator(n+1) holds f"{y} <> {}
  proof
    let y being set;
    assume
A13: y in RAT_with_denominator(n+1);
    then reconsider y as Rational;
    consider i being Integer such that
A14: y = i/(n+1) by A13,Def2;
    reconsider i as Element of INT by INT_1:def 2;
 f.i = i/(n+1) by A4;
then  f.i in {y} by A14,TARSKI:def 1;
    hence thesis by A6,FUNCT_1:def 13;
  end;
A17: f is one-to-one by A7,FUNCT_1:def 8;
 rng f = RAT_with_denominator(n+1) by A12,FUNCT_2:49;
  hence thesis by A6,A17,WELLORD2:def 4;
end;

theorem
  NAT,RAT are_equipotent
proof
  assume
A1: not NAT,RAT are_equipotent;
  defpred P[Element of NAT,Subset of REAL] means
  $2 = RAT_with_denominator ($1+1);
A2: for n being Element of NAT ex X being Subset of REAL st P[n,X]
  proof
    let n be Element of NAT;
 for x being set st x in RAT_with_denominator (n+1) holds
    x in REAL by NUMBERS:12,TARSKI:def 3;
    then reconsider X=RAT_with_denominator (n+1) as Subset of REAL
    by TARSKI:def 3;
    take X;
    thus thesis;
  end;
  consider F being Function of NAT,bool REAL such that
A4: for n being Element of NAT holds P[n,F.n] from FUNCT_2:sch 3(A2);
 for x being set st x in union rng F holds x in RAT
  proof
    let x being set;
    assume x in union rng F;
    then consider Y being set such that
A7: x in Y and
A8: Y in rng F by TARSKI:def 4;
 dom F = NAT by FUNCT_2:def 1;
    then consider n being set such that
A10: n in NAT and
A11: F.n = Y by A8,FUNCT_1:def 5;
    reconsider n as Element of NAT by A10;
 Y = RAT_with_denominator (n+1) by A4,A11;
    hence thesis by A7;
  end;
then A13: union rng F c= RAT by TARSKI:def 3;
 for x being set st x in RAT holds x in union rng F
  proof
    let x be set;
    assume x in RAT;
    then reconsider x as Rational;
A16: dom F = NAT by FUNCT_2:def 1;
 denominator x <> 0 by RAT_1:def 3;
    then consider m being Nat such that
A18: denominator x = m + 1 by NAT_1:6;
    reconsider m as Element of NAT by ORDINAL1:def 13;
 denominator x = m + 1 by A18;
    then reconsider n = denominator x - 1 as Element of NAT;
 x = (numerator x)/(n+1) by RAT_1:37;
then  x in RAT_with_denominator (n+1) by Def2;
then A22: x in F.n by A4;
 F.n in rng F by A16,FUNCT_1:def 5;
    hence thesis by A22,TARSKI:def 4;
  end;
then  RAT c= union rng F by TARSKI:def 3;
then A25: union rng F = RAT by A13,XBOOLE_0:def 10;
 dom F = NAT by FUNCT_2:def 1;
then A27: rng F is countable by CARD_3:127;
 for Y being set st Y in rng F holds Y is countable
  proof
    let Y be set;
    assume Y in rng F;
    then consider n being set such that
A30: n in dom F and
A31: F.n = Y by FUNCT_1:def 5;
    reconsider n as Element of NAT by A30,FUNCT_2:def 1;
 Y = RAT_with_denominator (n+1) by A4,A31;
then  INT,Y are_equipotent by Th4;
then  NAT,Y are_equipotent by Th3,WELLORD2:22;
then A35: card NAT = card Y by CARD_1:21;
 card NAT c= omega by CARD_3:def 15,CARD_4:44;
    hence thesis by A35,CARD_3:def 15;
  end;
then  RAT is countable by A25,A27,CARD_4:62;
then  card RAT c= omega by CARD_3:def 15;
then  not omega in card RAT;
then  not omega c= card RAT or not omega <> card RAT by CARD_1:13;
then  card RAT in omega by A1,CARD_1:14,21,84;
  hence contradiction;
end;

begin  :: Basic operations on R_EAL valued functions

definition
  let C be non empty set;
  let f1,f2 be PartFunc of C,ExtREAL;
  deffunc F(Element of C) = f1.$1 + f2.$1;
  func f1+f2 -> PartFunc of C,ExtREAL means
  :Def3:
  dom it = (dom f1 /\ dom f2)\((f1"{-infty} /\ f2"{+infty}) \/ (f1"{+infty} /\
  f2"{-infty}))
  & for c being Element of C st c in dom it holds it.c = f1.c + f2.c;
  existence
  proof
    defpred P[Element of C] means
    $1 in (dom f1 /\ dom f2)\((f1"{-infty} /\ f2"{+infty}) \/
    (f1"{+infty} /\ f2"{-infty}));
    consider F be PartFunc of C,ExtREAL such that
A1: for c being Element of C holds c in dom F iff P[c] and
A2: for c being Element of C st c in dom F holds F.c = F(c) from SEQ_1:sch 3;
    take F;
 for x being set st x in dom F holds
    x in (dom f1 /\ dom f2)\((f1"{-infty} /\ f2"{+infty}) \/
    (f1"{+infty} /\ f2"{-infty}))
    proof
      let x be set;
      assume
A4:   x in dom F;
   dom F c= C by RELAT_1:def 18;
      then reconsider x as Element of C by A4;
   x in dom F by A4;
      hence thesis by A1;
    end;
then A7: dom F c= (dom f1 /\ dom f2)\((f1"{-infty} /\ f2"{+infty})
    \/ (f1"{+infty} /\ f2"{-infty})) by TARSKI:def 3;
     for x being set st x in (dom f1 /\ dom f2)\((f1"{-infty} /\ f2"{+infty
    })\/ (f1"{+infty} /\ f2"{-infty})) holds x in dom F
    proof
      let x be set;
      assume
A9:   x in (dom f1 /\ dom f2)\((f1"{-infty} /\ f2"{+infty}) \/
      (f1"{+infty} /\ f2"{-infty}));
then A10:  x in dom f1 by XBOOLE_0:def 4;
  dom f1 c= C by RELAT_1:def 18;
      then reconsider x as Element of C by A10;
  x in (dom f1 /\ dom f2)\((f1"{-infty} /\ f2"{+infty}) \/
      (f1"{+infty} /\ f2"{-infty})) by A9;
      hence thesis by A1;
    end;
then  (dom f1 /\ dom f2)\((f1"{-infty} /\ f2"{+infty}) \/
    (f1"{+infty} /\ f2"{-infty})) c= dom F by TARSKI:def 3;
    hence
dom F=(dom f1 /\ dom f2)\((f1"{-infty} /\ f2"{+infty}) \/ (f1"{+infty}
    /\ f2"{-infty})) by A7,XBOOLE_0:def 10;
    let c be Element of C;
    assume c in dom F;
    hence thesis by A2;
  end;
  uniqueness
  proof
    set X = (dom f1 /\ dom f2)\((f1"{-infty} /\ f2"{+infty}) \/ (f1"{+infty} /\
    f2"{-infty}));
    thus for f,g being PartFunc of C,ExtREAL st
    (dom f=X & for c be Element of C st c in dom f holds f.c = F(c)) &
    (dom g=X & for c be Element of C st c in dom g holds g.c = F(c))
    holds f = g from SEQ_1:sch 4;
  end;
  deffunc F(Element of C) = f1.$1 - f2.$1;
  func f1-f2 -> PartFunc of C,ExtREAL means
  dom it = (dom f1 /\ dom f2)\((f1"{+infty} /\ f2"{+infty}) \/ (f1"{-infty} /\
  f2"{-infty}))
  & for c being Element of C st c in dom it holds it.c = f1.c - f2.c;
  existence
  proof
    defpred P[Element of C] means
    $1 in (dom f1 /\ dom f2)\((f1"{+infty} /\ f2"{+infty}) \/
    (f1"{-infty} /\ f2"{-infty}));
    consider F be PartFunc of C,ExtREAL such that
A15: for c being Element of C holds c in dom F iff P[c] and
A16: for c being Element of C st c in dom F holds F.c = F(c) from SEQ_1:sch 3;
    take F;
 for x being set st x in dom F holds
    x in (dom f1 /\ dom f2)\((f1"{+infty} /\ f2"{+infty}) \/
    (f1"{-infty} /\ f2"{-infty}))
    proof
      let x be set;
      assume
A18:  x in dom F;
  dom F c= C by RELAT_1:def 18;
      then reconsider x as Element of C by A18;
  x in dom F by A18;
      hence thesis by A15;
    end;
then A21: dom F c= (dom f1 /\ dom f2)\((f1"{+infty} /\ f2"{+infty})
    \/ (f1"{-infty} /\ f2"{-infty})) by TARSKI:def 3;
     for
 x being set st x in (dom f1 /\ dom f2)\((f1"{+infty} /\ f2"{+infty }
    ) \/ (f1"{-infty} /\ f2"{-infty})) holds x in dom F
    proof
      let x be set;
      assume
A23:  x in (dom f1 /\ dom f2)\((f1"{+infty} /\ f2"{+infty}) \/
      (f1"{-infty} /\ f2"{-infty}));
then A24:  x in dom f1 by XBOOLE_0:def 4;
  dom f1 c= C by RELAT_1:def 18;
      then reconsider x as Element of C by A24;
  x in (dom f1 /\ dom f2)\((f1"{+infty} /\ f2"{+infty}) \/
      (f1"{-infty} /\ f2"{-infty})) by A23;
      hence thesis by A15;
    end;
then  (dom f1 /\ dom f2)\((f1"{+infty} /\ f2"{+infty}) \/
    (f1"{-infty} /\ f2"{-infty})) c= dom F by TARSKI:def 3;
    hence
dom F=(dom f1 /\ dom f2)\((f1"{+infty} /\ f2"{+infty}) \/ (f1"{-infty
    } /\ f2"{-infty})) by A21,XBOOLE_0:def 10;
    let c be Element of C;
    assume c in dom F;
    hence thesis by A16;
  end;
  uniqueness
  proof
    set X = (dom f1 /\ dom f2)\((f1"{+infty} /\ f2"{+infty}) \/ (f1"{-infty} /\
    f2"{-infty}));
    thus for f,g being PartFunc of C,ExtREAL st
    (dom f=X & for c be Element of C st c in dom f holds f.c = F(c)) &
    (dom g=X & for c be Element of C st c in dom g holds g.c = F(c))
    holds f = g from SEQ_1:sch 4;
  end;
  deffunc F(Element of C) = f1.$1 * f2.$1;
  func f1(#)f2 -> PartFunc of C,ExtREAL means
  :Def5:
  dom it = dom f1 /\ dom f2
  & for c being Element of C st c in dom it holds it.c = f1.c * f2.c;
  existence
  proof
    defpred P[Element of C] means $1 in dom f1 /\ dom f2;
    consider F be PartFunc of C,ExtREAL such that
A29: for c being Element of C holds c in dom F iff P[c] and
A30: for c being Element of C st c in dom F holds F.c = F(c) from SEQ_1:sch 3;
    take F;
 for x being set st x in dom F holds x in dom f1 /\ dom f2
    proof
      let x be set;
      assume
A32:  x in dom F;
  dom F c= C by RELAT_1:def 18;
      then reconsider x as Element of C by A32;
  x in dom F by A32;
      hence thesis by A29;
    end;
then A35: dom F c= dom f1 /\ dom f2 by TARSKI:def 3;
 for x being set st x in dom f1 /\ dom f2 holds x in dom F
    proof
      let x be set;
      assume
A37:  x in dom f1 /\ dom f2;
then A38:  x in dom f1 by XBOOLE_0:def 4;
  dom f1 c= C by RELAT_1:def 18;
      then reconsider x as Element of C by A38;
  x in dom f1 /\ dom f2 by A37;
      hence thesis by A29;
    end;
then  dom f1 /\ dom f2 c= dom F by TARSKI:def 3;
    hence dom F=dom f1 /\ dom f2 by A35,XBOOLE_0:def 10;
    let c be Element of C;
    assume c in dom F;
    hence thesis by A30;
  end;
  uniqueness
  proof
    set X = dom f1 /\ dom f2;
    thus for F,G being PartFunc of C,ExtREAL st
    (dom F=X & for c be Element of C st c in dom F holds F.c = F(c)) &
    (dom G=X & for c be Element of C st c in dom G holds G.c = F(c))
    holds F = G from SEQ_1:sch 4;
  end;
end;

definition
  let C be non empty set, f be PartFunc of C,ExtREAL, r be Real;
  deffunc F(Element of C) = (R_EAL r) * f.$1;
  func r(#)f -> PartFunc of C,ExtREAL means
  :Def6:
  dom it = dom f
  & for c being Element of C st c in dom it holds it.c = (R_EAL r) * f.c;
  existence
  proof
    defpred P[Element of C] means $1 in dom f;
    consider F be PartFunc of C,ExtREAL such that
A1: for c being Element of C holds c in dom F iff P[c] and
A2: for c being Element of C st c in dom F holds F.c = F(c) from SEQ_1:sch 3;
    take F;
 for x being set st x in dom F holds x in dom f
    proof
      let x be set;
      assume
A4:   x in dom F;
   dom F c= C by RELAT_1:def 18;
      then reconsider x as Element of C by A4;
   x in dom F by A4;
      hence thesis by A1;
    end;
then A7: dom F c= dom f by TARSKI:def 3;
 for x being set st x in dom f holds x in dom F
    proof
      let x be set;
      assume
A9:   x in dom f;
  dom f c= C by RELAT_1:def 18;
      then reconsider x as Element of C by A9;
  x in dom f by A9;
      hence thesis by A1;
    end;
then  dom f c= dom F by TARSKI:def 3;
    hence dom F=dom f by A7,XBOOLE_0:def 10;
    let c be Element of C;
    assume c in dom F;
    hence thesis by A2;
  end;
  uniqueness
  proof
    set X = dom f;
    thus for F,G being PartFunc of C,ExtREAL st
    (dom F=X & for c be Element of C st c in dom F holds F.c = F(c)) &
    (dom G=X & for c be Element of C st c in dom G holds G.c = F(c))
    holds F = G from SEQ_1:sch 4;
  end;
end;

theorem Th6:
  for C being non empty set, f being PartFunc of C,ExtREAL, r being Real
  st r <> 0 holds
  for c being Element of C st c in dom(r(#)f) holds f.c = (r(#)f).c / R_EAL r
proof
  let C being non empty set;
  let f being PartFunc of C,ExtREAL;
  let r being Real;
  assume
A1: r <> 0;
  let c be Element of C;
  assume c in dom(r(#)f);
then A3: (r(#)f).c = (R_EAL r) * f.c by Def6;
  per cases;
  suppose
A4: f.c = +infty;
 now per cases by A1;
      suppose
A6:     0. < R_EAL r;
then      (r(#)f).c = +infty by A3,A4,XXREAL_3:def 5;
        hence thesis by A4,A6,XXREAL_3:94;
      end;
      suppose
A9:     R_EAL r < 0.;
then     (r(#)f).c = -infty by A3,A4,XXREAL_3:def 5;
        hence thesis by A4,A9,XXREAL_3:95;
      end;
    end;
    hence thesis;
  end;
  suppose
A12: f.c = -infty;
 now per cases by A1;
      suppose
A14:    0. < R_EAL r;
then     (r(#)f).c = -infty by A3,A12,XXREAL_3:def 5;
        hence thesis by A12,A14,XXREAL_3:97;
      end;
      suppose
A17:    R_EAL r < 0.;
then     (r(#)f).c = +infty by A3,A12,XXREAL_3:def 5;
        hence thesis by A12,A17,XXREAL_3:96;
      end;
    end;
    hence thesis;
  end;
  suppose
 f.c <> +infty & f.c <> -infty;
    then reconsider a = f.c as Real by XXREAL_0:14;
 (r(#)f).c = r * a by A3,EXTREAL1:13;
then  (r(#)f).c / R_EAL r = r*a/r by EXTREAL1:32
      .= a/(r/r) by XCMPLX_1:78
      .= a / 1 by A1,XCMPLX_1:60;
    hence thesis;
  end;
end;

definition
  let C be non empty set;
  let f be PartFunc of C,ExtREAL;
  deffunc F(Element of C) = -(f.$1);
  func -f -> PartFunc of C,ExtREAL means
  dom it = dom f & for c being Element of C st c in dom it holds it.c = -(f.c);
  existence
  proof
    defpred P[Element of C] means $1 in dom f;
    consider F be PartFunc of C,ExtREAL such that
A1: for c being Element of C holds c in dom F iff P[c] and
A2: for c being Element of C st c in dom F holds F.c = F(c) from SEQ_1:sch 3;
    take F;
 for x being set st x in dom F holds x in dom f
    proof
      let x be set;
      assume
A4:   x in dom F;
   dom F c= C by RELAT_1:def 18;
      then reconsider x as Element of C by A4;
   x in dom F by A4;
      hence thesis by A1;
    end;
then A7: dom F c= dom f by TARSKI:def 3;
 for x being set st x in dom f holds x in dom F
    proof
      let x be set;
      assume
A9:   x in dom f;
  dom f c= C by RELAT_1:def 18;
      then reconsider x as Element of C by A9;
  x in dom f by A9;
      hence thesis by A1;
    end;
then  dom f c= dom F by TARSKI:def 3;
    hence dom F=dom f by A7,XBOOLE_0:def 10;
    let c be Element of C;
    assume c in dom F;
    hence thesis by A2;
  end;
  uniqueness
  proof
    set X = dom f;
    thus for F,G being PartFunc of C,ExtREAL st
    (dom F=X & for c be Element of C st c in dom F holds F.c = F(c)) &
    (dom G=X & for c be Element of C st c in dom G holds G.c = F(c))
    holds F = G from SEQ_1:sch 4;
  end;
end;

definition
  func 1. -> R_eal equals
  1;
  correctness by XXREAL_0:def 1;
end;

definition
  let C be non empty set;
  let f be PartFunc of C,ExtREAL;
  let r be Real;
  deffunc F(Element of C) = (R_EAL r)/(f.$1);
  func r/f -> PartFunc of C,ExtREAL means
  :Def9:
  dom it = dom f \ f"{0.} &
  for c being Element of C st c in dom it holds it.c = (R_EAL r)/(f.c);
  existence
  proof
    defpred P[Element of C] means $1 in dom f \ f"{0.};
    consider F be PartFunc of C,ExtREAL such that
A1: for c being Element of C holds c in dom F iff P[c] and
A2: for c being Element of C st c in dom F holds F.c = F(c) from SEQ_1:sch 3;
    take F;
 for x being set st x in dom F holds x in dom f \ f"{0.}
    proof
      let x be set;
      assume
A4:   x in dom F;
   dom F c= C by RELAT_1:def 18;
      then reconsider x as Element of C by A4;
   x in dom F by A4;
      hence thesis by A1;
    end;
then A7: dom F c= dom f \ f"{0.} by TARSKI:def 3;
 for x being set st x in dom f \ f"{0.} holds x in dom F
    proof
      let x be set;
      assume
A9:   x in dom f \ f"{0.};
  dom f c= C by RELAT_1:def 18;
then   dom f \ f"{0.} c= C by XBOOLE_1:1;
      then reconsider x as Element of C by A9;
  x in dom f \ f"{0.} by A9;
      hence thesis by A1;
    end;
then  dom f \ f"{0.} c= dom F by TARSKI:def 3;
    hence dom F=dom f \ f"{0.} by A7,XBOOLE_0:def 10;
    let c be Element of C;
    assume c in dom F;
    hence thesis by A2;
  end;
  uniqueness
  proof
    set X = dom f \ f"{0.};
    thus for F,G being PartFunc of C,ExtREAL st
    (dom F=X & for c be Element of C st c in dom F holds F.c = F(c)) &
    (dom G=X & for c be Element of C st c in dom G holds G.c = F(c))
    holds F = G from SEQ_1:sch 4;
  end;
end;

theorem
  for C being non empty set, f being PartFunc of C,ExtREAL holds
  dom (1/f) = dom f \ f"{0.} &
  for c being Element of C st c in dom (1/f) holds (1/f).c = 1./(f.c)
proof
  let C be non empty set;
  let f be PartFunc of C,ExtREAL;
  thus dom (1/f) = dom f \ f"{0.} by Def9;
  thus
  for c being Element of C st c in dom (1/f) holds (1/f).c = 1./(f.c)
  proof
    let c be Element of C;
    assume c in dom (1/f);
then  (1/f).c = (R_EAL 1)/(f.c) by Def9;
    hence thesis;
  end;
end;

definition
  let C be non empty set;
  let f be PartFunc of C,ExtREAL;
  deffunc F(Element of C) = |. f.$1 .|;
  func |.f.| -> PartFunc of C,ExtREAL means
  dom it = dom f &
  for c being Element of C st c in dom it holds it.c = |. f.c .|;
  existence
  proof
    defpred P[Element of C] means $1 in dom f;
    consider F be PartFunc of C,ExtREAL such that
A1: for c being Element of C holds c in dom F iff P[c] and
A2: for c being Element of C st c in dom F holds F.c = F(c) from SEQ_1:sch 3;
    take F;
 for x being set st x in dom F holds x in dom f
    proof
      let x be set;
      assume
A4:   x in dom F;
   dom F c= C by RELAT_1:def 18;
      then reconsider x as Element of C by A4;
   x in dom F by A4;
      hence thesis by A1;
    end;
then A7: dom F c= dom f by TARSKI:def 3;
 for x being set st x in dom f holds x in dom F
    proof
      let x be set;
      assume
A9:   x in dom f;
  dom f c= C by RELAT_1:def 18;
      then reconsider x as Element of C by A9;
  x in dom f by A9;
      hence thesis by A1;
    end;
then  dom f c= dom F by TARSKI:def 3;
    hence dom F=dom f by A7,XBOOLE_0:def 10;
    let c be Element of C;
    assume c in dom F;
    hence thesis by A2;
  end;
  uniqueness
  proof
    set X = dom f;
    thus for F,G being PartFunc of C,ExtREAL st
    (dom F=X & for c be Element of C st c in dom F holds F.c = F(c)) &
    (dom G=X & for c be Element of C st c in dom G holds G.c = F(c))
    holds F = G from SEQ_1:sch 4;
  end;
end;

canceled 3;

definition
  let C be non empty set;
  let f1,f2 be PartFunc of C,ExtREAL;
  redefine func f1+f2;
  commutativity
proof
  let f1,f2 be PartFunc of C,ExtREAL;
A1: dom (f1+f2) =(dom f1 /\ dom f2)\
  ((f1"{-infty} /\ f2"{+infty}) \/ (f1"{+infty} /\ f2"{-infty})) by Def3
    .= dom (f2+f1) by Def3;
   for x being Element of C st x in dom (f1+f2) holds (f1+f2).x = (f2+f1).x
  proof
    let x be Element of C;
    assume
A3: x in dom (f1+f2);
then  (f1+f2).x = f1.x + f2.x by Def3;
    hence thesis by A1,A3,Def3;
  end;
  hence thesis by A1,PARTFUN1:34;
end;
  redefine func f1(#)f2;
  commutativity
proof
  let f1,f2 be PartFunc of C,ExtREAL;
A1: dom (f1(#)f2) = dom f1 /\ dom f2 by Def5
    .= dom (f2(#)f1) by Def5;
   for
 x being Element of C st x in dom (f1(#)f2) holds (f1(#)f2).x = (f2 (#)
  f1).x
  proof
    let x be Element of C;
    assume
A3: x in dom(f1(#)f2);
then  (f1(#)f2).x = f1.x * f2.x by Def5;
    hence thesis by A1,A3,Def5;
  end;
  hence thesis by A1,PARTFUN1:34;
end;
end;

begin  :: Level sets

theorem Th11:
  for r being Real holds ex n being Element of NAT st r <= n
proof
  let r being Real;
  per cases;
  suppose
 [/ r \] >= 0;
    then reconsider n=[/ r \] as Element of NAT by INT_1:16;
    take n;
    thus thesis by INT_1:def 5;
  end;
  suppose
A2: [/ r \] < 0;
    take 0;
    thus thesis by A2,INT_1:def 5;
  end;
end;

theorem Th12:
  for r being Real holds ex n being Element of NAT st -n <= r
proof
  let r being Real;
 [\ r /] is Element of INT by INT_1:def 2;
  then A2: [\
 r /] <= r & ex k being Element of NAT st ( [\ r /] = k or [\ r /] = -k)
  by INT_1:def 1,def 4;
  per cases;
  suppose
 [\ r /] < 0;
    hence thesis by A2;
  end;
  suppose
A4: [\ r /] >= 0;
    take 0;
    thus thesis by A4,INT_1:def 4;
  end;
end;

theorem Th13:
  for r,s being real number st r < s holds
  ex n being Element of NAT st 1/(n+1) < s-r
proof
  let r,s be real number;
  assume r < s;
then  s-r > 0 by XREAL_1:52;
  then consider t being real number such that
A3: 0 < t and
A4: t < s-r by XREAL_1:7;
  reconsider t as Real by XREAL_0:def 1;
A5: 1/t > 0 by A3,XREAL_1:141;
A6: [/ 1/t \] + 1 > 1/t by INT_1:57;
  set n = [/ 1/t \];
  reconsider n as Element of NAT by A5,A6,INT_1:16,20;
 (n+1)*t >= 1 by A3,A6,XREAL_1:83;
then  1/(n+1) <= t by XREAL_1:81;
then  1/(n+1) < s-r by A4,XXREAL_0:2;
  hence thesis;
end;

theorem Th14:
  for r,s being real number st for n being Element of NAT
  holds r-1/(n+1) <= s holds r <= s
proof
  let r,s be real number;
  assume
A1: for n being Element of NAT holds r-1/(n+1) <= s;
  assume r > s;
  then consider n being Element of NAT such that
A3: 1/(n+1) < r - s by Th13;
 s + 1/(n+1) < r by A3,XREAL_1:22;
then  s < r - 1/(n+1) by XREAL_1:22;
  hence contradiction by A1;
end;

theorem Th15:
  for a being R_eal st (for r being Real holds R_EAL r < a) holds a = +infty
proof
  let a being R_eal;
  assume
A1: for r being Real holds R_EAL r < a;
  assume not a = +infty;
then  a < +infty by XXREAL_0:4;
  then consider b being R_eal such that
A4: a < b and b < +infty and
A5: b in REAL by MEASURE5:17;
  reconsider b as Real by A5;
 a <= R_EAL b by A4;
  hence contradiction by A1;
end;

theorem Th16:
  for a being R_eal st (for r being Real holds a < R_EAL r) holds a = -infty
proof
  let a being R_eal;
  assume
A1: for r being Real holds a < R_EAL r;
  assume
A2: not a = -infty;
 -infty <= a by XXREAL_0:5;
then  -infty < a by A2,XXREAL_0:1;
  then consider b being R_eal such that
  -infty < b and
A5: b < a and
A6: b in REAL by MEASURE5:17;
  reconsider b as Real by A6;
 R_EAL b <= a by A5;
  hence contradiction by A1;
end;

reserve X for set;
reserve f for PartFunc of X,ExtREAL;
reserve S for SigmaField of X;
reserve F for Function of NAT,S;
reserve A for set;
reserve a for R_eal;
reserve r,s for Real;
reserve n,m for Element of NAT;

notation
  let f be ext-real-valued Function, a be ext-real number;
  synonym eq_dom(f,a) for Coim(f,a);
end;

definition
  canceled;
  let f be ext-real-valued Function, a be ext-real number;
  defpred P[set] means $1 in dom f & f.$1 < a;
  func less_dom(f,a) -> set means
  :Def12:
  for x being set holds x in it iff x in dom f & f.x < a;
  existence
  proof
    consider A being set such that
A1: for x being set holds x in A iff x in dom f & P[x] from XBOOLE_0:sch 1;
    take A;
    thus thesis by A1;
  end;
  uniqueness
  proof
    thus for D1,D2 be set st (for x be set holds x in D1 iff P[x]) &
    (for x be set holds x in D2 iff P[x]) holds D1 = D2 from XBOOLE_0:sch 3;
  end;
  defpred P[set] means $1 in dom f & f.$1 <= a;
  func less_eq_dom(f,a) -> set means
  :Def13:
  for x being set holds x in it iff x in dom f & f.x <= a;
  existence
  proof
    consider A being set such that
A2: for x being set holds x in A iff x in dom f & P[x] from XBOOLE_0:sch 1;
    take A;
    thus thesis by A2;
  end;
  uniqueness
  proof
    thus for D1,D2 be set st (for x be set holds x in D1 iff P[x]) &
    (for x be set holds x in D2 iff P[x]) holds D1 = D2 from XBOOLE_0:sch 3;
  end;
  defpred P[set] means $1 in dom f & a < f.$1;
  func great_dom(f,a) -> set means
  :Def14:
  for x being set holds x in it iff x in dom f & a < f.x;
  existence
  proof
    consider A being set such that
A3: for x being set holds x in A iff x in dom f & P[x] from XBOOLE_0:sch 1;
    take A;
    thus thesis by A3;
  end;
  uniqueness
  proof
    thus for D1,D2 be set st (for x be set holds x in D1 iff P[x]) &
    (for x be set holds x in D2 iff P[x]) holds D1 = D2 from XBOOLE_0:sch 3;
  end;
  defpred P[set] means $1 in dom f & a <= f.$1;
  func great_eq_dom(f,a) -> set means
  :Def15:
  for x being set holds x in it iff x in dom f & a <= f.x;
  existence
  proof
    consider A being set such that
A4: for x being set holds x in A iff x in dom f & P[x] from XBOOLE_0:sch 1;
    take A;
    thus thesis by A4;
  end;
  uniqueness
  proof
    thus for D1,D2 be set st (for x be set holds x in D1 iff P[x]) &
    (for x be set holds x in D2 iff P[x]) holds D1 = D2 from XBOOLE_0:sch 3;
  end;
  redefine func eq_dom(f,a) means
  :Def16:
  for x being set holds x in it iff x in dom f & f.x = a;
  compatibility
  proof
    let X be set;
    thus X = eq_dom(f,a) implies
    for x being set holds x in X iff x in dom f & f.x = a
    proof
      assume
A5:   X = eq_dom(f,a);
      let x being set;
      thus x in X implies x in dom f & f.x = a
      proof
        assume
A6:     x in X;
        hence x in dom f by A5,FUNCT_1:def 13;
     f.x in {a} by A5,A6,FUNCT_1:def 13;
        hence thesis by TARSKI:def 1;
      end;
      assume
A8:   x in dom f;
      assume f.x = a;
then   f.x in {a} by TARSKI:def 1;
      hence thesis by A5,A8,FUNCT_1:def 13;
    end;
    assume
A11: for x being set holds x in X iff x in dom f & f.x = a;
 for x being set holds x in X iff x in dom f & f.x in {a}
    proof
      let x be set;
      thus x in X implies x in dom f & f.x in {a}
      proof
        assume
A13:    x in X;
        hence x in dom f by A11;
    f.x = a by A11,A13;
        hence thesis by TARSKI:def 1;
      end;
      assume
A15:  x in dom f;
      assume f.x in {a};
then   f.x = a by TARSKI:def 1;
      hence thesis by A11,A15;
    end;
    hence thesis by FUNCT_1:def 13;
  end;
end;

definition
  let X be set, f be PartFunc of X,ExtREAL, a be ext-real number;
  redefine func less_dom(f,a) -> Subset of X;
  coherence
  proof
 less_dom(f,a) c= X
    proof
      let x be set;
      assume x in less_dom(f,a);
then A3:   x in dom f by Def12;
   dom f c= X by RELAT_1:def 18;
      hence thesis by A3;
    end;
    hence thesis;
  end;
  redefine func less_eq_dom(f,a) -> Subset of X;
  coherence
  proof
 less_eq_dom(f,a) c= X
    proof
      let x be set;
      assume x in less_eq_dom(f,a);
then A7:   x in dom f by Def13;
   dom f c= X by RELAT_1:def 18;
      hence thesis by A7;
    end;
    hence thesis;
  end;
  redefine func great_dom(f,a) -> Subset of X;
  coherence
  proof
 great_dom(f,a) c= X
    proof
      let x be set;
      assume x in great_dom(f,a);
then A11:  x in dom f by Def14;
  dom f c= X by RELAT_1:def 18;
      hence thesis by A11;
    end;
    hence thesis;
  end;
  redefine func great_eq_dom(f,a) -> Subset of X;
  coherence
  proof
 great_eq_dom(f,a) c= X
    proof
      let x be set;
      assume x in great_eq_dom(f,a);
then A15:  x in dom f by Def15;
  dom f c= X by RELAT_1:def 18;
      hence thesis by A15;
    end;
    hence thesis;
  end;
  redefine func eq_dom(f,a) -> Subset of X;
  coherence
  proof
 Coim(f,a) is Subset of X;
    hence thesis;
  end;
end;

canceled;

theorem Th18:
  A c= dom f implies A /\ great_eq_dom(f,a) = A\(A /\ less_dom(f,a))
proof
  assume
A1: A c= dom f;
 dom f c= X by RELAT_1:def 18;
then A3: A c= X by A1,XBOOLE_1:1;
 for x being set st x in A /\ great_eq_dom(f,a) holds x in
  A\(A /\ less_dom(f,a))
  proof
    let x be set;
    assume
A5: x in A /\ great_eq_dom(f,a);
then A6: x in A by XBOOLE_0:def 4;
 x in great_eq_dom(f,a) by A5,XBOOLE_0:def 4;
then  a <= f.x by Def15;
then  not x in less_dom(f,a) by Def12;
then  not x in (A /\ less_dom(f,a)) by XBOOLE_0:def 4;
    hence thesis by A6,XBOOLE_0:def 5;
  end;
then A11: A /\ great_eq_dom(f,a) c= A\(A /\ less_dom(f,a)) by TARSKI:def 3;
 for x being set st x in A\(A /\ less_dom(f,a)) holds x in
  A /\ great_eq_dom(f,a)
  proof
    let x be set;
    assume
A13: x in A\(A /\ less_dom(f,a));
then A14: x in A;
 not x in A /\ less_dom(f,a) by A13,XBOOLE_0:def 5;
then A16: not x in less_dom(f,a) by A13,XBOOLE_0:def 4;
    reconsider x as Element of X by A3,A14;
    reconsider y=f.x as R_eal;
 not y < a by A1,A14,A16,Def12;
then  x in great_eq_dom(f,a) by A1,A14,Def15;
    hence thesis by A13,XBOOLE_0:def 4;
  end;
then  A\(A /\ less_dom(f,a)) c= A /\ great_eq_dom(f,a) by TARSKI:def 3;
  hence thesis by A11,XBOOLE_0:def 10;
end;

theorem Th19:
  A c= dom f implies A /\ great_dom(f,a) = A\(A /\ less_eq_dom(f,a))
proof
  assume
A1: A c= dom f;
 dom f c= X by RELAT_1:def 18;
then A3: A c= X by A1,XBOOLE_1:1;
 for x being set st x in A /\ great_dom(f,a) holds x in
  A\(A /\ less_eq_dom(f,a))
  proof
    let x be set;
    assume
A5: x in A /\ great_dom(f,a);
then A6: x in A by XBOOLE_0:def 4;
 x in great_dom(f,a) by A5,XBOOLE_0:def 4;
then  a < f.x by Def14;
then  not x in less_eq_dom(f,a) by Def13;
then  not x in (A /\ less_eq_dom(f,a)) by XBOOLE_0:def 4;
    hence thesis by A6,XBOOLE_0:def 5;
  end;
then A11: A /\ great_dom(f,a) c= A\(A /\ less_eq_dom(f,a)) by TARSKI:def 3;
 for x being set st x in A\(A /\ less_eq_dom(f,a)) holds x in
  A /\ great_dom(f,a)
  proof
    let x be set;
    assume
A13: x in A\(A /\ less_eq_dom(f,a));
then A14: x in A;
 not x in A /\ less_eq_dom(f,a) by A13,XBOOLE_0:def 5;
then A16: not x in less_eq_dom(f,a) by A13,XBOOLE_0:def 4;
    reconsider x as Element of X by A3,A14;
    reconsider y=f.x as R_eal;
 not y <= a by A1,A14,A16,Def13;
then  x in great_dom(f,a) by A1,A14,Def14;
    hence thesis by A13,XBOOLE_0:def 4;
  end;
then  A\(A /\ less_eq_dom(f,a)) c= A /\ great_dom(f,a) by TARSKI:def 3;
  hence thesis by A11,XBOOLE_0:def 10;
end;

theorem Th20:
  A c= dom f implies A /\ less_eq_dom(f,a) = A\(A /\ great_dom(f,a))
proof
  assume
A1: A c= dom f;
 dom f c= X by RELAT_1:def 18;
then A3: A c= X by A1,XBOOLE_1:1;
A4: A /\ less_eq_dom(f,a) c= A\(A /\ great_dom(f,a))
  proof
    let x be set;
    assume
A5: x in A /\ less_eq_dom(f,a);
then A6: x in A by XBOOLE_0:def 4;
 x in less_eq_dom(f,a) by A5,XBOOLE_0:def 4;
then  f.x <= a by Def13;
then  not x in great_dom(f,a) by Def14;
then  not x in (A /\ great_dom(f,a)) by XBOOLE_0:def 4;
    hence thesis by A6,XBOOLE_0:def 5;
  end;
 for x being set st x in A\(A /\ great_dom(f,a)) holds x in
  A /\ less_eq_dom(f,a)
  proof
    let x be set;
    assume
A12: x in A\(A /\ great_dom(f,a));
then A13: x in A;
 not x in A /\ great_dom(f,a) by A12,XBOOLE_0:def 5;
then A15: not x in great_dom(f,a) by A12,XBOOLE_0:def 4;
    reconsider x as Element of X by A3,A13;
    reconsider y=f.x as R_eal;
 not a < y by A1,A13,A15,Def14;
then  x in less_eq_dom(f,a) by A1,A13,Def13;
    hence thesis by A12,XBOOLE_0:def 4;
  end;
then  A\(A /\ great_dom(f,a)) c= A /\ less_eq_dom(f,a) by TARSKI:def 3;
  hence thesis by A4,XBOOLE_0:def 10;
end;

theorem Th21:
  A c= dom f implies A /\ less_dom(f,a) = A\(A /\ great_eq_dom(f,a))
proof
  assume
A1: A c= dom f;
 dom f c= X by RELAT_1:def 18;
then A3: A c= X by A1,XBOOLE_1:1;
 for x being set st x in A /\ less_dom(f,a) holds x in
  A\(A /\ great_eq_dom(f,a))
  proof
    let x be set;
    assume
A5: x in A /\ less_dom(f,a);
then A6: x in A by XBOOLE_0:def 4;
 x in less_dom(f,a) by A5,XBOOLE_0:def 4;
then  f.x < a by Def12;
then  not x in great_eq_dom(f,a) by Def15;
then  not x in (A /\ great_eq_dom(f,a)) by XBOOLE_0:def 4;
    hence thesis by A6,XBOOLE_0:def 5;
  end;
then A11: A /\ less_dom(f,a) c= A\(A /\ great_eq_dom(f,a)) by TARSKI:def 3;
 for x being set st x in A\(A /\ great_eq_dom(f,a)) holds x in
  A /\ less_dom(f,a)
  proof
    let x be set;
    assume
A13: x in A\(A /\ great_eq_dom(f,a));
then A14: x in A;
 not x in A /\ great_eq_dom(f,a) by A13,XBOOLE_0:def 5;
then A16: not x in great_eq_dom(f,a) by A13,XBOOLE_0:def 4;
    reconsider x as Element of X by A3,A14;
    reconsider y=f.x as R_eal;
 not a <= y by A1,A14,A16,Def15;
then  x in less_dom(f,a) by A1,A14,Def12;
    hence thesis by A13,XBOOLE_0:def 4;
  end;
then  A\(A /\ great_eq_dom(f,a)) c= A /\ less_dom(f,a) by TARSKI:def 3;
  hence thesis by A11,XBOOLE_0:def 10;
end;

theorem
  A /\ eq_dom(f,a) = A /\ great_eq_dom(f,a) /\ less_eq_dom(f,a)
proof
 for x being set st x in A /\ eq_dom(f,a) holds
  x in A /\ great_eq_dom(f,a) /\ less_eq_dom(f,a)
  proof
    let x be set;
    assume
A2: x in A /\ eq_dom(f,a);
then A3: x in A by XBOOLE_0:def 4;
A4: x in eq_dom(f,a) by A2,XBOOLE_0:def 4;
then A5: a = f.x by Def16;
    reconsider x as Element of X by A2;
A6: x in dom f by A4,Def16;
then  x in great_eq_dom(f,a) by A5,Def15;
then A8: x in A /\ great_eq_dom(f,a) by A3,XBOOLE_0:def 4;
 x in less_eq_dom(f,a) by A5,A6,Def13;
    hence thesis by A8,XBOOLE_0:def 4;
  end;
then A10: A /\ eq_dom(f,a) c= A /\ great_eq_dom(f,a) /\ less_eq_dom(f,a) by
  TARSKI:def 3;
   for x being set st x in A /\ great_eq_dom(f,a) /\ less_eq_dom(f,a) holds
  x in A /\ eq_dom(f,a)
  proof
    let x being set;
    assume
A12: x in A /\ great_eq_dom(f,a) /\ less_eq_dom(f,a);
then A13: x in A /\ great_eq_dom(f,a) by XBOOLE_0:def 4;
A14: x in less_eq_dom(f,a) by A12,XBOOLE_0:def 4;
A15: x in A by A13,XBOOLE_0:def 4;
 x in great_eq_dom(f,a) by A13,XBOOLE_0:def 4;
then A17: a <= f.x by Def15;
A18: f.x <= a by A14,Def13;
    reconsider x as Element of X by A12;
A19: x in dom f by A14,Def13;
 a = f.x by A17,A18,XXREAL_0:1;
then  x in eq_dom(f,a) by A19,Def16;
    hence thesis by A15,XBOOLE_0:def 4;
  end;
then
 A /\ great_eq_dom(f,a) /\ less_eq_dom(f,a) c= A /\ eq_dom(f,a) by
  TARSKI:def 3;
  hence thesis by A10,XBOOLE_0:def 10;
end;

theorem
  for X, S, F, f, A, r st for n holds F.n = A /\ great_dom(f,R_EAL(r-1/(n+1)))
  holds A /\ great_eq_dom(f,R_EAL r) = meet rng F
proof
  let X,S,F,f,A,r;
  assume
A1: for n holds F.n = A /\ great_dom(f,R_EAL(r-1/(n+1)));
   for
 x being set st x in A /\ great_eq_dom(f,R_EAL r) holds x in meet rng F
  proof
    let x be set;
    assume
A3: x in A /\ great_eq_dom(f,R_EAL r);
then A4: x in A by XBOOLE_0:def 4;
A5: x in great_eq_dom(f,R_EAL r) by A3,XBOOLE_0:def 4;
 for Y being set holds Y in rng F implies x in Y
    proof
      let Y be set;
   Y in rng F implies x in Y
      proof
        assume Y in rng F;
        then consider m being Element of NAT such that
        m in dom F and
A9:     Y = F.m by PARTFUN1:26;
A10:    Y = A /\ great_dom(f,R_EAL(r-1/(m+1))) by A1,A9;
A11:    x in dom f by A5,Def15;
        reconsider x as Element of X by A3;
A12:    R_EAL r <= f.x by A5,Def15;
    (m+1)" > 0;
then     1/(m+1) > 0 by XCMPLX_1:217;
then     r < r+1/(m+1) by XREAL_1:31;
then     R_EAL(r-1/(m+1))< R_EAL r by XREAL_1:21;
then     R_EAL(r-1/(m+1)) < f.x by A12,XXREAL_0:2;
then     x in great_dom(f,R_EAL(r-1/(m+1))) by A11,Def14;
        hence thesis by A4,A10,XBOOLE_0:def 4;
      end;
      hence thesis;
    end;
    hence thesis by SETFAM_1:def 1;
  end;
then A19: A /\ great_eq_dom(f,R_EAL r) c= meet rng F by TARSKI:def 3;
 for x being set st x in meet rng F holds x in A /\ great_eq_dom(f,
  R_EAL r)
  proof
    let x be set;
    assume
A21: x in meet rng F;
A22: for m holds x in A & x in dom f & R_EAL(r-1/(m+1)) < f.x
    proof
      let m;
  m in NAT;
then   m in dom F by FUNCT_2:def 1;
then   F.m in rng F by FUNCT_1:def 5;
then   x in F.m by A21,SETFAM_1:def 1;
then A27:  x in A /\ great_dom(f,R_EAL(r-1/(m+1))) by A1;
then   x in great_dom(f,R_EAL(r-1/(m+1))) by XBOOLE_0:def 4;
      hence thesis by A27,Def14,XBOOLE_0:def 4;
    end;
    reconsider y=f.x as R_eal;
 1 in NAT;
then  1 in dom F by FUNCT_2:def 1;
then  F.1 in rng F by FUNCT_1:def 5;
then  x in F.1 by A21,SETFAM_1:def 1;
then  x in A /\ great_dom(f,R_EAL(r-1/(1+1))) by A1;
    then reconsider x as Element of X;
 R_EAL r <= y
    proof
  now per cases;
        suppose
      y=+infty;
          hence thesis by XXREAL_0:4;
        end;
        suppose
      not y=+infty;
then A38:      not +infty <= y by XXREAL_0:4;
      R_EAL(r-1/(1+1))<y by A22;
          then reconsider y1=y as Real by A38,XXREAL_0:48;
      for m holds r-1/(m+1) <= y1
          proof
            let m;
        R_EAL(r-1/(m+1)) < R_EAL y1 by A22;
            hence thesis;
          end;
          hence thesis by Th14;
        end;
      end;
      hence thesis;
    end;
then  x in great_eq_dom(f,R_EAL r) by A22,Def15;
    hence thesis by A22,XBOOLE_0:def 4;
  end;
then  meet rng F c= A /\ great_eq_dom(f,R_EAL r) by TARSKI:def 3;
  hence thesis by A19,XBOOLE_0:def 10;
end;

theorem Th24:
for r being real number st for n holds F.n = A /\ less_dom(f,R_EAL(r+1/(n+1)))
  holds A /\ less_eq_dom(f,R_EAL r) = meet rng F
proof
  let r be real number;
  assume
A1: for n holds F.n = A /\ less_dom(f,R_EAL(r+1/(n+1)));
   for x being set st x in A /\ less_eq_dom(f,R_EAL r) holds x in meet rng F
  proof
    let x be set;
    assume
A3: x in A /\ less_eq_dom(f,R_EAL r);
then A4: x in A by XBOOLE_0:def 4;
A5: x in less_eq_dom(f,R_EAL r) by A3,XBOOLE_0:def 4;
 for Y being set holds Y in rng F implies x in Y
    proof
      let Y be set;
   Y in rng F implies x in Y
      proof
        assume Y in rng F;
        then consider m being Element of NAT such that
        m in dom F and
A9:     Y = F.m by PARTFUN1:26;
A10:    Y = A /\ less_dom(f,R_EAL(r+1/(m+1))) by A1,A9;
A11:    x in dom f by A5,Def13;
        reconsider x as Element of X by A3;
A12:    f.x <= R_EAL r by A5,Def13;
    (m+1)" > 0;
then     1/(m+1) > 0 by XCMPLX_1:217;
then     R_EAL r < R_EAL(r+1/(m+1)) by XREAL_1:31;
then     f.x < R_EAL(r+1/(m+1)) by A12,XXREAL_0:2;
then     x in less_dom(f,R_EAL(r+1/(m+1))) by A11,Def12;
        hence thesis by A4,A10,XBOOLE_0:def 4;
      end;
      hence thesis;
    end;
    hence thesis by SETFAM_1:def 1;
  end;
then A18: A /\ less_eq_dom(f,R_EAL r) c= meet rng F by TARSKI:def 3;
   for
 x being set st x in meet rng F holds x in A /\ less_eq_dom(f,R_EAL r)
  proof
    let x be set;
    assume
A20: x in meet rng F;
A21: for m holds x in A & x in dom f & f.x < R_EAL(r+1/(m+1))
    proof
      let m;
  m in NAT;
then   m in dom F by FUNCT_2:def 1;
then   F.m in rng F by FUNCT_1:def 5;
then   x in F.m by A20,SETFAM_1:def 1;
then A26:  x in A /\ less_dom(f,R_EAL(r+1/(m+1))) by A1;
then   x in less_dom(f,R_EAL(r+1/(m+1))) by XBOOLE_0:def 4;
      hence thesis by A26,Def12,XBOOLE_0:def 4;
    end;
    reconsider y=f.x as R_eal;
 1 in NAT;
then  1 in dom F by FUNCT_2:def 1;
then  F.1 in rng F by FUNCT_1:def 5;
then  x in F.1 by A20,SETFAM_1:def 1;
then  x in A /\ less_dom(f,R_EAL(r+1/(1+1))) by A1;
    then reconsider x as Element of X;
 y <= R_EAL r
    proof
  now per cases;
        suppose
      y=-infty;
          hence thesis by XXREAL_0:5;
        end;
        suppose
      not y=-infty;
then A37:      not y <= -infty by XXREAL_0:6;
      y < R_EAL(r+1/(1+1)) by A21;
          then reconsider y1=y as Real by A37,XXREAL_0:48;
      for m holds y1-1/(m+1) <= r
          proof
            let m;
        R_EAL y1 < R_EAL(r+1/(m+1)) by A21;
            hence thesis by XREAL_1:22;
          end;
          hence thesis by Th14;
        end;
      end;
      hence thesis;
    end;
then  x in less_eq_dom(f,R_EAL r) by A21,Def13;
    hence thesis by A21,XBOOLE_0:def 4;
  end;
then  meet rng F c= A /\ less_eq_dom(f,R_EAL r) by TARSKI:def 3;
  hence thesis by A18,XBOOLE_0:def 10;
end;

theorem Th25:
  for r being real number st for n
  holds F.n = A /\ less_eq_dom(f,R_EAL(r-1/(n+1)))
  holds A /\ less_dom(f,R_EAL r) = union rng F
proof
  let r be real number;
  assume
A2: for n holds F.n = A /\ less_eq_dom(f,R_EAL(r-1/(n+1)));
   for x being set st x in A /\ less_dom(f,R_EAL r) holds x in union rng F
  proof
    let x be set;
    assume
A4: x in A /\ less_dom(f,R_EAL r);
then A5: x in A by XBOOLE_0:def 4;
A6: x in less_dom(f,R_EAL r) by A4,XBOOLE_0:def 4;
 ex Y being set st x in Y & Y in rng F
    proof
      reconsider x as Element of X by A4;
A8:   x in dom f by A6,Def12;
A9:   f.x < R_EAL r by A6,Def12;
  ex m st f.x <= R_EAL(r-1/(m+1))
      proof
        per cases;
        suppose
A11:      f.x = -infty;
          take 1;
          thus thesis by A11,XXREAL_0:5;
        end;
        suppose
      not f.x =-infty;
then       not f.x <= -infty by XXREAL_0:6;
          then reconsider y1=f.x as Real by A9,XXREAL_0:48;
          consider m such that
A14:      1/(m+1) < r-y1 by A9,Th13;
      y1+1/(m+1) < r by A14,XREAL_1:22;
then       f.x <= R_EAL(r-1/(m+1)) by XREAL_1:22;
          hence thesis;
        end;
      end;
      then consider m such that
A17:  f.x <= R_EAL(r-1/(m+1));
  x in less_eq_dom(f,R_EAL(r-1/(m+1))) by A8,A17,Def13;
then A19:  x in A /\ less_eq_dom(f,R_EAL(r-1/(m+1))) by A5,
      XBOOLE_0:def 4;
  m in NAT;
then A21:  m in dom F by FUNCT_2:def 1;
      take F.m;
      thus thesis by A2,A19,A21,FUNCT_1:def 5;
    end;
    hence thesis by TARSKI:def 4;
  end;
then A22: A /\ less_dom(f,R_EAL r) c= union rng F by TARSKI:def 3;
   for x being set st x in union rng F holds x in A /\ less_dom(f,R_EAL r )
  proof
    let x be set;
    assume x in union rng F;
    then consider Y being set such that
A25: x in Y and
A26: Y in rng F by TARSKI:def 4;
    consider m such that
    m in dom F and
A27: F.m = Y by A26,PARTFUN1:26;
A28: x in A /\ less_eq_dom(f,R_EAL(r-1/(m+1))) by A2,A25,A27;
then A29: x in A by XBOOLE_0:def 4;
A30: x in less_eq_dom(f,R_EAL(r-1/(m+1))) by A28,XBOOLE_0:def 4;
then A31: x in dom f by Def13;
A32: f.x <= R_EAL(r-1/(m+1)) by A30,Def13;
    reconsider x as Element of X by A25,A26;
 f.x < R_EAL r
    proof
  now
      r < r+1/(m+1) by XREAL_1:31,141;
then       r-1/(m+1) < r by XREAL_1:21;
          hence thesis by A32,XXREAL_0:2;
      end;
      hence thesis;
    end;
then  x in less_dom(f,R_EAL r) by A31,Def12;
    hence thesis by A29,XBOOLE_0:def 4;
  end;
then  union rng F c= A /\ less_dom(f,R_EAL r) by TARSKI:def 3;
  hence thesis by A22,XBOOLE_0:def 10;
end;

theorem Th26:
  for X, S, F, f, A, r st for n holds F.n = A /\
  great_eq_dom(f,R_EAL(r+1/(n+1)))
  holds A /\ great_dom(f,R_EAL r) = union rng F
proof
  let X,S,F,f,A,r;
  assume
A1: for n holds F.n = A /\ great_eq_dom(f,R_EAL(r+1/(n+1)));
   for x being set st x in A /\ great_dom(f,R_EAL r) holds x in union rng F
  proof
    let x be set;
    assume
A3: x in A /\ great_dom(f,R_EAL r);
then A4: x in A by XBOOLE_0:def 4;
A5: x in great_dom(f,R_EAL r) by A3,XBOOLE_0:def 4;
 ex Y being set st x in Y & Y in rng F
    proof
      reconsider x as Element of X by A3;
A7:   x in dom f by A5,Def14;
A8:   R_EAL r < f.x by A5,Def14;
   ex m st R_EAL(r+1/(m+1)) <= f.x
      proof
        per cases;
        suppose
A10:      f.x = +infty;
          take 1;
          thus thesis by A10,XXREAL_0:4;
        end;
        suppose
      not f.x=+infty;
then       not +infty <= f.x by XXREAL_0:4;
          then reconsider y1=f.x as Real by A8,XXREAL_0:48;
          consider m such that
A13:      1/(m+1) < y1-r by A8,Th13;
          take m;
          thus thesis by A13,XREAL_1:22;
        end;
      end;
      then consider m such that
A14:  R_EAL(r+1/(m+1)) <= f.x;
  x in great_eq_dom(f,R_EAL(r+1/(m+1))) by A7,A14,Def15;
then A16:  x in A /\ great_eq_dom(f,R_EAL(r+1/(m+1))) by A4,
      XBOOLE_0:def 4;
  m in NAT;
then A18:  m in dom F by FUNCT_2:def 1;
      take F.m;
      thus thesis by A1,A16,A18,FUNCT_1:def 5;
    end;
    hence thesis by TARSKI:def 4;
  end;
then A19: A /\ great_dom(f,R_EAL r) c= union rng F by TARSKI:def 3;
   for
 x being set st x in union rng F holds x in A /\ great_dom(f,R_EAL r )
  proof
    let x be set;
    assume x in union rng F;
    then consider Y being set such that
A22: x in Y and
A23: Y in rng F by TARSKI:def 4;
    consider m such that
    m in dom F and
A24: F.m = Y by A23,PARTFUN1:26;
A25: x in A /\ great_eq_dom(f,R_EAL(r+1/(m+1))) by A1,A22,A24;
then A26: x in A by XBOOLE_0:def 4;
A27: x in great_eq_dom(f,R_EAL(r+1/(m+1))) by A25,XBOOLE_0:def 4;
then A28: x in dom f by Def15;
A29: R_EAL(r+1/(m+1)) <= f.x by A27,Def15;
    reconsider x as Element of X by A22,A23;
 R_EAL r < f.x
    proof
  now
      r < r+1/(m+1) by XREAL_1:31,141;
          hence thesis by A29,XXREAL_0:2;
      end;
      hence thesis;
    end;
then  x in great_dom(f,R_EAL r) by A28,Def14;
    hence thesis by A26,XBOOLE_0:def 4;
  end;
then  union rng F c= A /\ great_dom(f,R_EAL r) by TARSKI:def 3;
  hence thesis by A19,XBOOLE_0:def 10;
end;

theorem Th27:
  for X, S, F, f, A st for n holds F.n = A /\ great_dom(f,R_EAL n)
  holds A /\ eq_dom(f,+infty) = meet rng F
proof
  let X,S,F,f,A;
  assume
A1: for n holds F.n = A /\ great_dom(f,R_EAL n);
 for x being set st x in A /\ eq_dom(f,+infty) holds x in meet rng F
  proof
    let x being set;
    assume
A3: x in A /\ eq_dom(f,+infty);
then A4: x in A by XBOOLE_0:def 4;
A5: x in eq_dom(f,+infty) by A3,XBOOLE_0:def 4;
 for Y being set holds Y in rng F implies x in Y
    proof
      let Y be set;
   Y in rng F implies x in Y
      proof
        assume Y in rng F;
        then consider m being Element of NAT such that
        m in dom F and
A9:     Y = F.m by PARTFUN1:26;
A10:    Y = A /\ great_dom(f,R_EAL m) by A1,A9;
        reconsider x as Element of X by A3;
A11:    f.x = +infty by A5,Def16;
    x in dom f & not +infty <= R_EAL m by A5,Def16,XXREAL_0:9;
then     x in great_dom(f,R_EAL m) by A11,Def14;
        hence thesis by A4,A10,XBOOLE_0:def 4;
      end;
      hence thesis;
    end;
    hence thesis by SETFAM_1:def 1;
  end;
then A14: A /\ eq_dom(f,+infty) c= meet rng F by TARSKI:def 3;
 for x being set st x in meet rng F holds x in A /\ eq_dom(f,+infty)
  proof
    let x be set;
    assume
A16: x in meet rng F;
A17: for m holds x in A & x in dom f & ex y being R_eal st y=f.x & y = +infty
    proof
      let m;
  m in NAT;
then   m in dom F by FUNCT_2:def 1;
then   F.m in rng F by FUNCT_1:def 5;
then   x in F.m by A16,SETFAM_1:def 1;
then A22:  x in A /\ great_dom(f,R_EAL m) by A1;
then A23:  x in great_dom(f,R_EAL m) by XBOOLE_0:def 4;
  for r holds R_EAL r < f.x
      proof
        let r;
        consider n such that
A25:    r <= n by Th11;
    n in NAT;
then     n in dom F by FUNCT_2:def 1;
then     F.n in rng F by FUNCT_1:def 5;
then     x in F.n by A16,SETFAM_1:def 1;
then     x in A /\ great_dom(f,R_EAL n) by A1;
then     x in great_dom(f,R_EAL n) by XBOOLE_0:def 4;
then     R_EAL n < f.x by Def14;
        hence thesis by A25,XXREAL_0:2;
      end;
then   f.x = +infty by Th15;
      hence thesis by A22,A23,Def14,XBOOLE_0:def 4;
    end;
 1 in NAT;
then  1 in dom F by FUNCT_2:def 1;
then  F.1 in rng F by FUNCT_1:def 5;
then  x in F.1 by A16,SETFAM_1:def 1;
then  x in A /\ great_dom(f,R_EAL 1) by A1;
    then reconsider x as Element of X;
 x in eq_dom(f,+infty) by A17,Def16;
    hence thesis by A17,XBOOLE_0:def 4;
  end;
then  meet rng F c= A /\ eq_dom(f,+infty) by TARSKI:def 3;
  hence thesis by A14,XBOOLE_0:def 10;
end;

theorem Th28:
  for X, S, F, f, A st for n holds F.n = A /\ less_dom(f,R_EAL n)
  holds A /\ less_dom(f,+infty) = union rng F
proof
  let X,S,F,f,A;
  assume
A1: for n holds F.n = A /\ less_dom(f,R_EAL n);
   for x being set st x in A /\ less_dom(f,+infty) holds x in union rng F
  proof
    let x be set;
    assume
A3: x in A /\ less_dom(f,+infty);
then A4: x in A by XBOOLE_0:def 4;
A5: x in less_dom(f,+infty) by A3,XBOOLE_0:def 4;
then A6: x in dom f by Def12;
A7: f.x < +infty by A5,Def12;
 ex n being Element of NAT st f.x < R_EAL n
    proof
      per cases;
      suppose
A9:     f.x = -infty;
        take 0;
        thus thesis by A9;
      end;
      suppose
    not f.x = -infty;
then     not f.x <= -infty by XXREAL_0:6;
        then reconsider y1=f.x as Real by A7,XXREAL_0:48;
        consider n1 being Element of NAT such that
A12:    y1 <= n1 by Th11;
A13:    n1 < n1+1 by NAT_1:13;
        reconsider m=n1+1 as Element of NAT;
        take m;
        thus thesis by A12,A13,XXREAL_0:2;
      end;
    end;
    then consider n being Element of NAT such that
A14: f.x < R_EAL n;
    reconsider x as Element of X by A3;
 x in less_dom(f,R_EAL n) by A6,A14,Def12;
then  x in A /\ less_dom(f,R_EAL n) by A4,XBOOLE_0:def 4;
then A17: x in F.n by A1;
 n in NAT;
then  n in dom F by FUNCT_2:def 1;
then  F.n in rng F by FUNCT_1:def 5;
    hence thesis by A17,TARSKI:def 4;
  end;
then A21: A /\ less_dom(f,+infty) c= union rng F by TARSKI:def 3;
   for x being set st x in union rng F holds x in A /\ less_dom(f,+infty)
  proof
    let x being set;
    assume x in union rng F;
    then consider Y being set such that
A24: x in Y and
A25: Y in rng F by TARSKI:def 4;
    consider m such that
    m in dom F and
A26: F.m = Y by A25,PARTFUN1:26;
A27: x in A /\ less_dom(f,R_EAL m) by A1,A24,A26;
then A28: x in A by XBOOLE_0:def 4;
A29: x in less_dom(f,R_EAL m) by A27,XBOOLE_0:def 4;
then A30: x in dom f by Def12;
A31: f.x < R_EAL m by A29,Def12;
    reconsider x as Element of X by A24,A25;
 f.x < +infty by A31,XXREAL_0:2,9;
then  x in less_dom(f,+infty) by A30,Def12;
    hence thesis by A28,XBOOLE_0:def 4;
  end;
then  union rng F c= A /\ less_dom(f,+infty) by TARSKI:def 3;
  hence thesis by A21,XBOOLE_0:def 10;
end;

theorem Th29:
  for X, S, F, f, A st for n holds F.n = A /\ less_dom(f,R_EAL (-n))
  holds A /\ eq_dom(f,-infty) = meet rng F
proof
  let X,S,F,f,A;
  assume
A1: for n holds F.n = A /\ less_dom(f,R_EAL (-n));
 for x being set st x in A /\ eq_dom(f,-infty) holds x in meet rng F
  proof
    let x being set;
    assume
A3: x in A /\ eq_dom(f,-infty);
then A4: x in A by XBOOLE_0:def 4;
A5: x in eq_dom(f,-infty) by A3,XBOOLE_0:def 4;
 for Y being set holds Y in rng F implies x in Y
    proof
      let Y be set;
   Y in rng F implies x in Y
      proof
        assume Y in rng F;
        then consider m being Element of NAT such that
        m in dom F and
A9:     Y = F.m by PARTFUN1:26;
A10:    Y = A /\ less_dom(f,R_EAL (-m)) by A1,A9;
        reconsider x as Element of X by A3;
A11:    f.x = -infty by A5,Def16;
            x in dom f & not R_EAL (-m) <= -infty by A5,Def16,XXREAL_0:12;
then     x in less_dom(f,R_EAL (-m)) by A11,Def12;
        hence thesis by A4,A10,XBOOLE_0:def 4;
      end;
      hence thesis;
    end;
    hence thesis by SETFAM_1:def 1;
  end;
then A14: A /\ eq_dom(f,-infty) c= meet rng F by TARSKI:def 3;
 for x being set st x in meet rng F holds x in A /\ eq_dom(f,-infty)
  proof
    let x be set;
    assume
A16: x in meet rng F;
A17: for m holds x in A & x in dom f & ex y being R_eal st y=f.x & y = -infty
    proof
      let m;
  m in NAT;
then   m in dom F by FUNCT_2:def 1;
then   F.m in rng F by FUNCT_1:def 5;
then   x in F.m by A16,SETFAM_1:def 1;
then A22:  x in A /\ less_dom(f,R_EAL (-m)) by A1;
then A23:  x in less_dom(f,R_EAL (-m)) by XBOOLE_0:def 4;
  for r holds f.x < R_EAL r
      proof
        let r;
        consider n such that
A25:    -n <= r by Th12;
    n in NAT;
then     n in dom F by FUNCT_2:def 1;
then     F.n in rng F by FUNCT_1:def 5;
then     x in F.n by A16,SETFAM_1:def 1;
then     x in A /\ less_dom(f,R_EAL (-n)) by A1;
then     x in less_dom(f,R_EAL (-n)) by XBOOLE_0:def 4;
then     f.x < R_EAL (-n) by Def12;
        hence thesis by A25,XXREAL_0:2;
      end;
then   f.x = -infty by Th16;
      hence thesis by A22,A23,Def12,XBOOLE_0:def 4;
    end;
 1 in NAT;
then  1 in dom F by FUNCT_2:def 1;
then  F.1 in rng F by FUNCT_1:def 5;
then  x in F.1 by A16,SETFAM_1:def 1;
then  x in A /\ less_dom(f,R_EAL (-1)) by A1;
    then reconsider x as Element of X;
 x in eq_dom(f,-infty) by A17,Def16;
    hence thesis by A17,XBOOLE_0:def 4;
  end;
then  meet rng F c= A /\ eq_dom(f,-infty) by TARSKI:def 3;
  hence thesis by A14,XBOOLE_0:def 10;
end;

theorem Th30:
  for X, S, F, f, A st for n holds F.n = A /\ great_dom(f,R_EAL (-n))
  holds A /\ great_dom(f,-infty) = union rng F
proof
  let X,S,F,f,A;
  assume
A1: for n holds F.n = A /\ great_dom(f,R_EAL (-n));
   for x being set st x in A /\ great_dom(f,-infty) holds x in union rng F
  proof
    let x be set;
    assume
A3: x in A /\ great_dom(f,-infty);
then A4: x in A by XBOOLE_0:def 4;
A5: x in great_dom(f,-infty) by A3,XBOOLE_0:def 4;
then A6: x in dom f by Def14;
A7: -infty < f.x by A5,Def14;
 ex n being Element of NAT st R_EAL (-n) < f.x
    proof
      per cases;
      suppose
A9:     f.x = +infty;
        take 0;
        thus thesis by A9;
      end;
      suppose
    not f.x = +infty;
then     not +infty <= f.x by XXREAL_0:4;
        then reconsider y1=f.x as Real by A7,XXREAL_0:48;
        consider n1 being Element of NAT such that
A12:    -n1 <= y1 by Th12;
    n1 < n1+1 by NAT_1:13;
then A14:    -(n1+1) < -n1 by XREAL_1:26;
        reconsider m=n1+1 as Element of NAT;
        take m;
        thus thesis by A12,A14,XXREAL_0:2;
      end;
    end;
    then consider n being Element of NAT such that
A15: R_EAL (-n) < f.x;
    reconsider x as Element of X by A3;
 x in great_dom(f,R_EAL (-n)) by A6,A15,Def14;
then  x in A /\ great_dom(f,R_EAL (-n)) by A4,XBOOLE_0:def 4;
then A18: x in F.n by A1;
 n in NAT;
then  n in dom F by FUNCT_2:def 1;
then  F.n in rng F by FUNCT_1:def 5;
    hence thesis by A18,TARSKI:def 4;
  end;
then A22: A /\ great_dom(f,-infty) c= union rng F by TARSKI:def 3;
   for x being set st x in union rng F holds x in A /\ great_dom(f,-infty )
  proof
    let x being set;
    assume x in union rng F;
    then consider Y being set such that
A25: x in Y and
A26: Y in rng F by TARSKI:def 4;
    consider m such that
    m in dom F and
A27: F.m = Y by A26,PARTFUN1:26;
A28: x in A /\ great_dom(f,R_EAL (-m)) by A1,A25,A27;
then A29: x in A by XBOOLE_0:def 4;
A30: x in great_dom(f,R_EAL (-m)) by A28,XBOOLE_0:def 4;
then A31: x in dom f by Def14;
A32: R_EAL (-m) < f.x by A30,Def14;
    reconsider x as Element of X by A25,A26;
 -infty < f.x by A32,XXREAL_0:2,12;
then  x in great_dom(f,-infty) by A31,Def14;
    hence thesis by A29,XBOOLE_0:def 4;
  end;
then  union rng F c= A /\ great_dom(f,-infty) by TARSKI:def 3;
  hence thesis by A22,XBOOLE_0:def 10;
end;

begin :: Measurable functions

definition
  let X be non empty set;
  let S be SigmaField of X;
  let f be PartFunc of X,ExtREAL;
  let A be Element of S;
  pred f is_measurable_on A means
  :Def17:
  for r being real number holds A /\ less_dom(f,R_EAL r) in S;
end;

reserve X for non empty set;
reserve x for Element of X;
reserve f,g for PartFunc of X,ExtREAL;
reserve S for SigmaField of X;
reserve A,B for Element of S;

theorem
  for X,S,f,A st A c= dom f holds f is_measurable_on A iff
  for r being real number holds A /\ great_eq_dom(f,R_EAL r) in S
proof
  let X,S,f,A;
  assume
A1: A c= dom f;
A2: f is_measurable_on A implies for r being real number holds A /\
  great_eq_dom(f,R_EAL r) in S
  proof
    assume
A3: f is_measurable_on A;
 for r being real number holds A /\ great_eq_dom(f,R_EAL r) in S
    proof
      let r be real number;
         A
 /\ less_dom(f,R_EAL r) in S & A /\ great_eq_dom(f,R_EAL r) = A\(A /\
      less_dom(f,R_EAL r)) by A1,A3,Def17,Th18;
      hence thesis by MEASURE1:20;
    end;
    hence thesis;
  end;
 (for r being real number holds A /\ great_eq_dom(f,R_EAL r) in S) implies
  f is_measurable_on A
  proof
    assume
A7: for r being real number holds A /\ great_eq_dom(f,R_EAL r) in S;
 for r being real number holds A /\ less_dom(f,R_EAL r) in S
    proof
      let r be real number;
         A
 /\ great_eq_dom(f,R_EAL r) in S & A /\ less_dom(f,R_EAL r) = A\(A /\
      great_eq_dom(f,R_EAL r)) by A1,A7,Th21;
      hence thesis by MEASURE1:20;
    end;
    hence thesis by Def17;
  end;
  hence thesis by A2;
end;

theorem Th32:
  for X,S,f,A holds f is_measurable_on A iff
  for r being real number holds A /\ less_eq_dom(f,R_EAL r) in S
proof
  let X,S,f,A;
A1: f is_measurable_on A implies for r being real number
  holds A /\ less_eq_dom(f,R_EAL r) in S
  proof
    assume
A2: f is_measurable_on A;
 for r being real number holds A /\ less_eq_dom(f,R_EAL r) in S
    proof
      let r be real number;
      defpred P[Element of NAT,set] means
      A /\ less_dom(f,R_EAL(r+1/($1+1))) = $2;
A4:   for n ex y being Element of S st P[n,y]
      proof
        let n;
        reconsider y=A /\ less_dom(f,R_EAL(r+1/(n+1))) as Element of S
        by A2,Def17;
        take y;
        thus thesis;
      end;
      consider F being Function of NAT,S such that
A5:   for n holds P[n,F.n] from FUNCT_2:sch 3(A4);
   A /\ less_eq_dom(f,R_EAL r) = meet rng F by A5,Th24;
      hence thesis;
    end;
    hence thesis;
  end;
 (for r being real number holds A /\ less_eq_dom(f,R_EAL r) in S) implies
  f is_measurable_on A
  proof
    assume
A8: for r being real number holds A /\ less_eq_dom(f,R_EAL r) in S;
 for r being real number holds A /\ less_dom(f,R_EAL r) in S
    proof
      let r be real number;
      defpred P[Element of NAT,set] means
      A /\ less_eq_dom(f,R_EAL(r-1/($1+1))) = $2;
A10:  for n ex y being Element of S st P[n,y]
      proof
        let n;
        reconsider y=A /\ less_eq_dom(f,R_EAL(r-1/(n+1))) as Element of S
        by A8;
        take y;
        thus thesis;
      end;
      consider F being Function of NAT,S such that
A11:  for n holds P[n,F.n] from FUNCT_2:sch 3(A10);
  A /\ less_dom(f,R_EAL r) = union rng F by A11,Th25;
      hence thesis;
    end;
    hence thesis by Def17;
  end;
  hence thesis by A1;
end;

theorem Th33:
  for X,S,f,A st A c= dom f holds f is_measurable_on A iff
  for r being real number holds A /\ great_dom(f,R_EAL r) in S
proof
  let X,S,f,A;
  assume
A1: A c= dom f;
A2: f is_measurable_on A implies
  for r being real number holds A /\ great_dom(f,R_EAL r) in S
  proof
    assume
A3: f is_measurable_on A;
 for r being real number holds A /\ great_dom(f,R_EAL r) in S
    proof
      let r be real number;
         A
 /\ less_eq_dom(f,R_EAL r) in S & A /\ great_dom(f,R_EAL r) = A\(A /\
      less_eq_dom(f,R_EAL r)) by A1,A3,Th19,Th32;
      hence thesis by MEASURE1:20;
    end;
    hence thesis;
  end;
 (for r being real number holds A /\ great_dom(f,R_EAL r) in S)
  implies f is_measurable_on A
  proof
    assume
A7: for r being real number holds A /\ great_dom(f,R_EAL r) in S;
 for r being real number holds A /\ less_eq_dom(f,R_EAL r) in S
    proof
      let r be real number;
         A
 /\ great_dom(f,R_EAL r) in S & A /\ less_eq_dom(f,R_EAL r) = A\(A /\
      great_dom(f,R_EAL r)) by A1,A7,Th20;
      hence thesis by MEASURE1:20;
    end;
    hence thesis by Th32;
  end;
  hence thesis by A2;
end;

theorem
  for X,S,f,A,B st B c= A & f is_measurable_on A holds f is_measurable_on B
proof
  let X,S,f,A,B;
  assume that
A1: B c= A and
A2: f is_measurable_on A;
 for r being real number holds (B /\ less_dom(f,R_EAL r)) in S
  proof
    let r be real number;
A4: A /\ less_dom(f,R_EAL r) in S by A2,Def17;
 B /\ (A /\ less_dom(f,R_EAL r)) = B /\ A /\ less_dom(f,R_EAL r) by
    XBOOLE_1:16
      .= B /\ less_dom(f,R_EAL r) by A1,XBOOLE_1:28;
    hence thesis by A4,FINSUB_1:def 2;
  end;
  hence thesis by Def17;
end;

theorem
  for X,S,f,A,B st f is_measurable_on A & f is_measurable_on B holds
  f is_measurable_on (A \/ B)
proof
  let X,S,f,A,B;
  assume
A1: f is_measurable_on A & f is_measurable_on B;
 for r being real number holds ((A \/ B) /\ less_dom(f,R_EAL r)) in S
  proof
    let r be real number;
     A
 /\ less_dom(f,R_EAL r) in S & B /\ less_dom(f,R_EAL r) in S by A1,Def17;
then  (A /\ less_dom(f,R_EAL r)) \/ (B /\ less_dom(f,R_EAL r)) in S
    by FINSUB_1:def 1;
    hence thesis by XBOOLE_1:23;
  end;
  hence thesis by Def17;
end;

theorem
  for X,S,f,A,r,s st f is_measurable_on A & A c= dom f holds
  (A /\ great_dom(f,R_EAL r) /\ less_dom(f,R_EAL s)) in S
proof
  let X,S,f,A,r,s;
  assume that
A1: f is_measurable_on A and
A2: A c= dom f;
A3: A /\ less_dom(f,R_EAL s) in S by A1,Def17;
A4: for r1 being Real holds A /\ great_eq_dom(f,R_EAL r1) in S
  proof
    let r1 be Real;
     A
 /\ less_dom(f,R_EAL r1) in S & A /\ great_eq_dom(f,R_EAL r1) = A\(A /\
    less_dom(f,R_EAL r1)) by A1,A2,Def17,Th18;
    hence thesis by MEASURE1:20;
  end;
 for r1 being Real holds A /\ great_dom(f,R_EAL r1) in S
  proof
    let r1 being Real;
    defpred P[Element of NAT,set] means
    A /\ great_eq_dom(f,R_EAL(r1+1/($1+1))) = $2;
A7: for n ex y being Element of S st P[n,y]
    proof
      let n;
      reconsider y=A /\ great_eq_dom(f,R_EAL(r1+1/(n+1))) as Element of S
      by A4;
      take y;
      thus thesis;
    end;
    consider F being Function of NAT,S such that
A8: for n holds P[n,F.n] from FUNCT_2:sch 3(A7);
 A /\ great_dom(f,R_EAL r1) = union rng F by A8,Th26;
    hence thesis;
  end;
then A10: A /\ great_dom(f,R_EAL r) in S;
 (A /\ great_dom(f,R_EAL r))/\(A /\ less_dom(f,R_EAL s))
  = (A /\ great_dom(f,R_EAL r) /\ A) /\ less_dom(f,R_EAL s) by XBOOLE_1:16
    .= (great_dom(f,R_EAL r) /\ (A /\ A)) /\
  less_dom(f,R_EAL s) by XBOOLE_1:16;
  hence thesis by A3,A10,FINSUB_1:def 2;
end;

theorem
  for X,S,f,A st f is_measurable_on A & A c= dom f holds
  A /\ eq_dom(f,+infty) in S
proof
  let X,S,f,A;
  assume
A1: f is_measurable_on A & A c= dom f;
  defpred P[Element of NAT,set]means A /\ great_dom(f,R_EAL $1) = $2;
A2: for n ex y being Element of S st P[n,y]
  proof
    let n;
    reconsider y=A /\ great_dom(f,R_EAL n) as Element of S by A1,Th33;
    take y;
    thus thesis;
  end;
  consider F being Function of NAT,S such that
A3: for n holds P[n,F.n] from FUNCT_2:sch 3(A2);
 A /\ eq_dom(f,+infty) = meet rng F by A3,Th27;
  hence thesis;
end;

theorem
  for X,S,f,A st f is_measurable_on A holds A /\ eq_dom(f,-infty) in S
proof
  let X,S,f,A;
  assume
A1: f is_measurable_on A;
  defpred P[Element of NAT,set] means A /\ less_dom(f,R_EAL (-$1)) = $2;
A2: for n ex y being Element of S st P[n,y]
  proof
    let n;
    reconsider y=A /\ less_dom(f,R_EAL (-n)) as Element of S by A1,Def17;
    take y;
    thus thesis;
  end;
  consider F being Function of NAT,S such that
A3: for n holds P[n,F.n] from FUNCT_2:sch 3(A2);
 A /\ eq_dom(f,-infty) = meet rng F by A3,Th29;
  hence thesis;
end;

theorem
  for X,S,f,A st f is_measurable_on A & A c= dom f holds
  A /\ great_dom(f,-infty) /\ less_dom(f,+infty) in S
proof
  let X,S,f,A;
  assume that
A1: f is_measurable_on A and
A2: A c= dom f;
A3: A /\ great_dom(f,-infty) in S
  proof
    defpred P[Element of NAT,set] means A /\ great_dom(f,R_EAL (-$1)) = $2;
A4: for n ex y being Element of S st P[n,y]
    proof
      let n;
      reconsider y=A /\ great_dom(f,R_EAL (-n)) as Element of S by A1,A2,Th33;
      take y;
      thus thesis;
    end;
    consider F being Function of NAT,S such that
A5: for n holds P[n,F.n] from FUNCT_2:sch 3(A4);
 A /\ great_dom(f,-infty) = union rng F by A5,Th30;
    hence thesis;
  end;
A7: A /\ less_dom(f,+infty) in S
  proof
    defpred P[Element of NAT,set] means A /\ less_dom(f,R_EAL $1) = $2;
A8: for n ex y being Element of S st P[n,y]
    proof
      let n;
      reconsider y=A /\ less_dom(f,R_EAL n) as Element of S by A1,Def17;
      take y;
      thus thesis;
    end;
    consider F being Function of NAT,S such that
A9: for n holds P[n,F.n] from FUNCT_2:sch 3(A8);
 A /\ less_dom(f,+infty) = union rng F by A9,Th28;
    hence thesis;
  end;
 (A /\ great_dom(f,-infty)) /\ (A /\ less_dom(f,+infty))
  = (A /\ great_dom(f,-infty) /\ A) /\ less_dom(f,+infty) by XBOOLE_1:16
    .= (great_dom(f,-infty) /\ (A /\ A)) /\ less_dom(f,+infty) by XBOOLE_1:16;
  hence thesis by A3,A7,FINSUB_1:def 2;
end;

theorem
  for X,S,f,g,A,r st f is_measurable_on A & g is_measurable_on A & A c= dom g
  holds A /\ less_dom(f,R_EAL r) /\ great_dom(g,R_EAL r) in S
proof
  let X,S,f,g,A,r;
  assume f is_measurable_on A & g is_measurable_on A & A c= dom g;
  then A2: A
 /\ less_dom(f,R_EAL r) in S & A /\ great_dom(g,R_EAL r) in S by Def17,
  Th33;
 (A /\ less_dom(f,R_EAL r)) /\ (A /\ great_dom(g,R_EAL r))
  =((A /\ less_dom(f,R_EAL r)) /\ A) /\ great_dom(g,R_EAL r) by XBOOLE_1:16
    .=((A /\ A) /\ less_dom(f,R_EAL r)) /\ great_dom(g,R_EAL r) by XBOOLE_1:16
    .=A /\ less_dom(f,R_EAL r) /\ great_dom(g,R_EAL r);
  hence thesis by A2,FINSUB_1:def 2;
end;

theorem
  for X,S,f,A,r st f is_measurable_on A & A c= dom f
  holds r(#)f is_measurable_on A
proof
  let X,S,f,A,r;
  assume that
A1: f is_measurable_on A and
A2: A c= dom f;
 for r1 being real number holds A /\ less_dom(r(#)f,R_EAL r1) in S
  proof
    let r1 being real number;
 now per cases;
      suppose
A5:     r<>0;
A6:     r1 in REAL by XREAL_0:def 1;
        reconsider r0=r1/r as Real;
A7:     r1=r*r0 by A5,XCMPLX_1:88;
     now per cases by A5;
          suppose
A9:         r > 0;
        for x being Element of X st x in less_dom(f,R_EAL r0)
            holds x in less_dom(r(#)f,R_EAL r1)
            proof
              let x be Element of X;
              assume
A11:          x in less_dom(f,R_EAL r0);
then           x in dom f by Def12;
then A13:          x in dom(r(#)f) by Def6;
A14:          f.x < R_EAL r0 by A11,Def12;
A15:          r1 in REAL by XREAL_0:def 1;
then           f.x < R_EAL r1 / R_EAL r by A14,EXTREAL1:39;
then A17:          f.x * R_EAL r < R_EAL r1 / R_EAL r * R_EAL
              r by A9,XXREAL_3:83;
          R_EAL r1 / R_EAL r = r1 / r by A15,EXTREAL1:32;
then A19:          R_EAL r1 / R_EAL r * R_EAL r = (r1/r)*r by
              EXTREAL1:13
                .= r1/(r/r) by XCMPLX_1:82
                .= r1/1 by A5,XCMPLX_1:60
                .= r1;
          (r(#)f).x = R_EAL r * f.x by A13,Def6;
              hence thesis by A13,A17,A19,Def12;
            end;
            then A21:        less_dom
(f,R_EAL r0) c= less_dom(r(#)f,R_EAL r1) by SUBSET_1:7;
        for x being Element of X st x in less_dom(r(#)f,R_EAL r1)
            holds x in less_dom(f,R_EAL r0)
            proof
              let x being Element of X;
              assume
A23:          x in less_dom(r(#)f,R_EAL r1);
then A24:          x in dom(r(#)f) by Def12;
          (r(#)f).x < R_EAL r1 by A23,Def12;
then           (r(#)f).x < R_EAL r * R_EAL r0 by A7,EXTREAL1:38;
              then
A27:          (r(#)f).x / R_EAL r < R_EAL r * R_EAL r0 / R_EAL r
              by A9,XXREAL_3:91;
          R_EAL r * R_EAL r0 = r * r0 by EXTREAL1:13;
then A29:          R_EAL r * R_EAL r0 / R_EAL r = (r*r0)/r by
              EXTREAL1:32
                .= r0/(r/r) by XCMPLX_1:78
                .= r0/1 by A5,XCMPLX_1:60
                .= r0;
                        x
 in dom f & f.x = (r(#)f).x / R_EAL r by A5,A24,Def6,Th6;
              hence thesis by A27,A29,Def12;
            end;
then
        less_dom(r(#)f,R_EAL r1) c= less_dom(f,R_EAL r0) by SUBSET_1:7;
then         less_dom
(f,R_EAL r0) = less_dom(r(#)f,R_EAL r1) by A21,XBOOLE_0:def 10;
            hence thesis by A1,Def17;
          end;
          suppose
A33:        r < 0;
        for x being Element of X st x in great_dom(f,R_EAL r0)
            holds x in less_dom(r(#)f,R_EAL r1)
            proof
              let x be Element of X;
              assume
A35:          x in great_dom(f,R_EAL r0);
then           x in dom f by Def14;
then A37:          x in dom(r(#)f) by Def6;
          R_EAL r0 < f.x by A35,Def14;
then           R_EAL r1 / R_EAL r < f.x by A6,EXTREAL1:39;
then A40:          f .x * R_EAL r < R_EAL r1 / R_EAL r * R_EAL r by A33,
              XXREAL_3:114;
          R_EAL r1 / R_EAL r = r1 / r by A6,EXTREAL1:32;
then A42:          R_EAL r1 / R_EAL r * R_EAL r = (r1/r)*r by
              EXTREAL1:13
                .= r1/(r/r) by XCMPLX_1:82
                .= r1/1 by A5,XCMPLX_1:60
                .= r1;
          (r(#)f).x = R_EAL r * f.x by A37,Def6;
              hence thesis by A37,A40,A42,Def12;
            end;
            then A44:        great_dom
(f,R_EAL r0) c= less_dom(r(#)f,R_EAL r1) by SUBSET_1:7;
        for x being Element of X st x in less_dom(r(#)f,R_EAL r1)
            holds x in great_dom(f,R_EAL r0)
            proof
              let x being Element of X;
              assume
A46:          x in less_dom(r(#)f,R_EAL r1);
then A47:          x in dom(r(#)f) by Def12;
          (r(#)f).x < R_EAL r1 by A46,Def12;
then           (r(#)f).x < R_EAL r * R_EAL r0 by A7,EXTREAL1:38;
              then
A50:          R_EAL r * R_EAL r0 / R_EAL r < (r(#)f).x / R_EAL r
              by A33,XXREAL_3:116;
          R_EAL r * R_EAL r0 = r * r0 by EXTREAL1:13;
then A52:          R_EAL r * R_EAL r0 / R_EAL r = (r*r0)/r by
              EXTREAL1:32
                .= r0/(r/r) by XCMPLX_1:78
                .= r0/1 by A5,XCMPLX_1:60
                .= r0;
                        x
 in dom f & f.x = (r(#)f).x / R_EAL r by A5,A47,Def6,Th6;
              hence thesis by A50,A52,Def14;
            end;
then         less_dom
(r(#)f,R_EAL r1) c= great_dom(f,R_EAL r0) by SUBSET_1:7;
then         great_dom(f,R_EAL r0) = less_dom(r(#)f,R_EAL r1) by A44,
            XBOOLE_0:def 10;
            hence thesis by A1,A2,Th33;
          end;
        end;
        hence thesis;
      end;
      suppose
A56:    r=0;
    now per cases;
          suppose
A58:        r1>0;
        for x1 being set holds x1 in A implies x1 in A /\
            less_dom(r(#)f,R_EAL r1)
            proof
              let x1 being set;
              assume
A60:          x1 in A;
              then reconsider x1 as Element of X;
          x1 in dom f by A2,A60;
then A62:          x1 in dom (r(#)f) by Def6;
              reconsider y = (r(#)f).x1 as R_eal;
          y = (R_EAL r) * f.x1 by A62,Def6
                .= 0. by A56;
then           x1 in less_dom(r(#)f,R_EAL r1) by A58,A62,Def12;
              hence thesis by A60,XBOOLE_0:def 4;
            end;
            then         A
 /\ less_dom(r(#)f,R_EAL r1) c= A & A c= A /\ less_dom(r(#)f,R_EAL r1) by
            TARSKI:def 3,XBOOLE_1:17;
then         A /\ less_dom(r(#)f,R_EAL r1) = A by XBOOLE_0:def 10;
            hence thesis;
          end;
          suppose
A67:        r1<=0;
        less_dom(r(#)f,R_EAL r1) = {}
            proof
              assume less_dom(r(#)f,R_EAL r1) <> {};
              then consider x1 being Element of X such that
A70:          x1 in less_dom(r(#)f,R_EAL r1) by SUBSET_1:10;
A71:          x1 in dom (r(#)f) by A70,Def12;
A72:          (r(#)f).x1 < R_EAL r1 by A70,Def12;
A73:          (r(#)f).x1 in rng(r(#)f) by A71,FUNCT_1:def 5;
                        for
 y being R_eal st y in rng(r(#)f) holds not y < R_EAL r1
              proof
                let y being R_eal;
                assume y in rng(r(#)f);
                then consider x such that
A76:            x in dom(r(#)f) & y = (r(#)f).x by PARTFUN1:26;
            y = (R_EAL r) * f.x by A76,Def6
                  .= 0. by A56;
                hence thesis by A67;
              end;
              hence contradiction by A72,A73;
            end;
            hence thesis by PROB_1:10;
          end;
        end;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
  hence thesis by Def17;
end;
