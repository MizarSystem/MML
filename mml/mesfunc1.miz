:: Definitions and Basic Properties of Measurable Functions
::  by Noboru Endou , Katsumi Wasaki and Yasunari Shidama
::
:: Received September 7, 2000
:: Copyright (c) 2000 Association of Mizar Users

environ

 vocabularies INT_1, RAT_1, ARYTM_1, TARSKI, FUNCT_1, FUNCT_2, RELAT_1, BOOLE,
      ARYTM_3, CARD_4, CARD_1, PARTFUN1, SUPINF_1, SEQ_1, MEASURE6, RLVECT_1,
      GROUP_1, COMPLEX1, SETFAM_1, MESFUNC1, ARYTM, PROB_1, VALUED_0, ORDINAL2;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, ORDINAL1, CARD_1, NUMBERS,
      XCMPLX_0, XREAL_0, MEASURE6, REAL_1, RELAT_1, FUNCT_1, PARTFUN1, FUNCT_2,
      NAT_1, INT_1, RAT_1, CARD_3, XXREAL_0, VALUED_0, PROB_1, SUPINF_1,
      SUPINF_2, MEASURE2, MEASURE3, EXTREAL1, RELSET_2;
 constructors WELLORD2, REAL_1, NAT_1, RAT_1, MEASURE3, MEASURE6, EXTREAL1,
      SUPINF_1, RELSET_2, PARTFUN1, EQREL_1, RELAT_2, RELSET_1;
 registrations XBOOLE_0, SUBSET_1, ORDINAL1, RELSET_1, NUMBERS, XXREAL_0,
      XREAL_0, NAT_1, INT_1, CARD_1, MEMBERED, MEASURE1, VALUED_0, XXREAL_3;
 requirements NUMERALS, BOOLE, SUBSET, REAL, ARITHM;
 definitions TARSKI, MEASURE6, RELAT_1;
 theorems MEASURE1, TARSKI, SUBSET_1, PARTFUN1, FUNCT_1, FUNCT_2, MEASURE5,
      SETFAM_1, NAT_1, INT_1, CARD_1, CARD_4, WELLORD2, RAT_1, XREAL_0,
      EXTREAL1, XBOOLE_0, XBOOLE_1, XCMPLX_1, NUMBERS, XREAL_1, FINSUB_1,
      PROB_1, XXREAL_0, ORDINAL1, CARD_3, XXREAL_3, RELAT_1;
 schemes SUBSET_1, FUNCT_2, SEQ_1, XBOOLE_0;

begin  :: Cardinal numbers of INT and RAT

reserve k for Element of NAT;
reserve r for Real;
reserve i for Integer;
reserve q for Rational;

definition
  func INT- -> Subset of REAL means
  :Def1:
  r in it iff ex k st r = - k;
  existence
  proof
    defpred P[Element of REAL] means ex k st $1 = -k;
    consider IT being Subset of REAL such that
A1: for r being Element of REAL holds r in IT iff P[r] from SUBSET_1:sch 3;
    take IT;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let D1,D2 be Subset of REAL such that
A2: r in D1 iff ex k st r = - k and
A3: r in D2 iff ex k st r = - k;
A4: for r holds r in D1 iff r in D2
    proof
      let r;
      thus r in D1 implies r in D2
      proof
        assume
A5:     r in D1;
A6:     ex k st r = - k by A2,A5;
        thus thesis by A3,A6;
      end;
      assume
A7:   r in D2;
A8:   ex k st r = - k by A3,A7;
      thus thesis by A2,A8;
    end;
    thus thesis by A4,SUBSET_1:8;
  end;
  correctness;
end;

Lm1: 0 = -0;

registration
  cluster INT- -> non empty;
  coherence by Def1,Lm1;
end;

theorem Th1:
  NAT,INT- are_equipotent
proof
  defpred P[Element of NAT,set] means $2=-$1;
A1: for x being Element of NAT ex y being Element of INT- st P[x,y]
  proof
    let x be Element of NAT;
A2: -x in INT- by Def1;
    thus thesis by A2;
  end;
  consider f being Function of NAT,INT- such that
A3: for x being Element of NAT holds P[x,f.x] from FUNCT_2:sch 3(A1);
A4: f in Funcs(NAT,INT-) by FUNCT_2:11;
A5: dom f = NAT by A4,FUNCT_2:169;
A6: for x1,x2 being set st x1 in dom f & x2 in dom f & f.x1=f.x2 holds x1=x2
  proof
    let x1,x2 be set;
    assume that
A7: x1 in dom f and
A8: x2 in dom f and
A9: f.x1=f.x2;
    reconsider x1 as Element of NAT by A4,A7,FUNCT_2:169;
    reconsider x2 as Element of NAT by A4,A8,FUNCT_2:169;
A10: f.x1 = -x1 & f.x2 = -x2 by A3;
A11: --x1=x2 by A9,A10;
    thus thesis by A11;
  end;
A12: for y being set st y in INT- holds f"{y} <> {}
  proof
    let y being set;
    assume
A13: y in INT-;
    reconsider y as Real by A13;
    consider k being Element of NAT such that
A14: y = -k by A13,Def1;
A15: f.k = -k by A3;
A16: f.k in {y} by A14,A15,TARSKI:def 1;
    thus thesis by A5,A16,FUNCT_1:def 13;
  end;
A17: f is one-to-one by A6,FUNCT_1:def 8;
A18: rng f = INT- by A12,FUNCT_2:49;
  thus thesis by A5,A17,A18,WELLORD2:def 4;
end;

theorem Th2:
  INT=INT- \/ NAT
proof
A1: for x being set st x in INT holds x in INT- \/ NAT
  proof
    let x be set;
    assume
A2: x in INT;
    consider k being Element of NAT such that
A3: x = k or x = - k by A2,INT_1:def 1;
A4: now per cases by A3;
      suppose
A5:     x = k;
        thus thesis by A5,XBOOLE_0:def 3;
      end;
      suppose
A6:     x = -k;
A7:     x in INT- by A6,Def1;
        thus thesis by A7,XBOOLE_0:def 3;
      end;
    end;
    thus thesis by A4;
  end;
A8: INT c= INT- \/ NAT by A1,TARSKI:def 3;
A9: for x being set st x in INT- \/ NAT holds x in INT
  proof
    let x be set;
    assume
A10: x in INT- \/ NAT;
A11: now per cases by A10,XBOOLE_0:def 3;
      suppose
A12:    x in INT-;
A13:    ex k being Element of NAT st x = -k by A12,Def1;
        thus thesis by A13,INT_1:def 1;
      end;
      suppose
A14:    x in NAT;
        thus thesis by A14,INT_1:def 1;
      end;
    end;
    thus thesis by A11;
  end;
A15: INT- \/ NAT c= INT by A9,TARSKI:def 3;
  thus thesis by A8,A15,XBOOLE_0:def 10;
end;

theorem Th3:
  NAT,INT are_equipotent by Th1,Th2,CARD_3:119;

definition
  redefine func INT -> Subset of REAL;
  correctness by NUMBERS:15;
end;

definition
  let n be Nat;
  func RAT_with_denominator n -> Subset of RAT means
  :Def2:
  q in it iff ex i st q = i/n;
  existence
  proof
    defpred P[Element of RAT] means ex i st $1 = i/n;
    consider X being Subset of RAT such that
A1: for r being Element of RAT holds r in X iff P[r] from SUBSET_1:sch 3;
A2: for q being Rational holds q in X iff ex i st q = i/n
    proof
      let q be Rational;
      reconsider q as Element of RAT by RAT_1:def 2;
A3:   q in X iff ex i st q = i/n by A1;
      thus thesis by A3;
    end;
    take X;
    thus thesis by A2;
  end;
  uniqueness
  proof
    let D1,D2 be Subset of RAT such that
A4: q in D1 iff ex i st q = i/n and
A5: q in D2 iff ex i st q = i/n;
A6: for q being Element of RAT holds q in D1 iff q in D2
    proof
      let q be Element of RAT;
      thus q in D1 implies q in D2
      proof
        assume
A7:     q in D1;
        reconsider q as Rational;
A8:     ex i st q = i/n by A4,A7;
        thus thesis by A5,A8;
      end;
      assume
A9:   q in D2;
      reconsider q as Rational;
A10:  ex i st q = i/n by A5,A9;
      thus thesis by A4,A10;
    end;
    thus thesis by A6,SUBSET_1:8;
  end;
end;

registration
  let n be Nat;
  cluster RAT_with_denominator(n+1) -> non empty;
  coherence
  proof
    reconsider i1=n+1 as Integer;
    reconsider q=0/i1 as Rational;
A1: q in RAT_with_denominator(n+1) by Def2;
    thus thesis by A1;
  end;
end;

theorem Th4:
  for n being Nat holds INT,RAT_with_denominator (n+1) are_equipotent
proof
  let n be Nat;
  defpred P[Element of INT,set] means $2=$1/(n+1);
A1: for x being Element of INT ex y being Element of RAT_with_denominator(n+1)
  st P[x,y]
  proof
    let x be Element of INT;
    reconsider x as Integer;
    reconsider i1=n+1 as Integer;
    set y=x/i1;
A2: y in RAT by RAT_1:def 1;
    reconsider y as Rational by A2;
A3: y in RAT_with_denominator(n+1) by Def2;
    thus thesis by A3;
  end;
  consider f being Function of INT,RAT_with_denominator(n+1) such that
A4: for x being Element of INT holds P[x,f.x] from FUNCT_2:sch 3(A1);
A5: f in Funcs(INT,RAT_with_denominator(n+1)) by FUNCT_2:11;
A6: dom f = INT by A5,FUNCT_2:169;
A7: for x1,x2 being set st x1 in dom f & x2 in dom f & f.x1=f.x2 holds x1=x2
  proof
    let x1,x2 be set;
    assume that
A8: x1 in dom f and
A9: x2 in dom f and
A10: f.x1=f.x2;
    reconsider x1 as Element of INT by A5,A8,FUNCT_2:169;
    reconsider x2 as Element of INT by A5,A9,FUNCT_2:169;
A11: f.x1 = x1/(n+1) & f.x2 = x2/(n+1) by A4;
    thus thesis by A10,A11,XCMPLX_1:53;
  end;
A12: for y being set st y in RAT_with_denominator(n+1) holds f"{y} <> {}
  proof
    let y being set;
    assume
A13: y in RAT_with_denominator(n+1);
    reconsider y as Rational by A13;
    consider i being Integer such that
A14: y = i/(n+1) by A13,Def2;
    reconsider i as Element of INT by INT_1:def 2;
A15: f.i = i/(n+1) by A4;
A16: f.i in {y} by A14,A15,TARSKI:def 1;
    thus thesis by A6,A16,FUNCT_1:def 13;
  end;
A17: f is one-to-one by A7,FUNCT_1:def 8;
A18: rng f = RAT_with_denominator(n+1) by A12,FUNCT_2:49;
  thus thesis by A6,A17,A18,WELLORD2:def 4;
end;

theorem
  NAT,RAT are_equipotent
proof
  assume
A1: not NAT,RAT are_equipotent;
  defpred P[Element of NAT,Subset of REAL] means
  $2 = RAT_with_denominator ($1+1);
A2: for n being Element of NAT ex X being Subset of REAL st P[n,X]
  proof
    let n be Element of NAT;
A3: for x being set st x in RAT_with_denominator (n+1) holds
    x in REAL by NUMBERS:12,TARSKI:def 3;
    reconsider X=RAT_with_denominator (n+1) as Subset of REAL
    by A3,TARSKI:def 3;
    take X;
    thus thesis;
  end;
  consider F being Function of NAT,bool REAL such that
A4: for n being Element of NAT holds P[n,F.n] from FUNCT_2:sch 3(A2);
A5: for x being set st x in union rng F holds x in RAT
  proof
    let x being set;
    assume
A6: x in union rng F;
    consider Y being set such that
A7: x in Y and
A8: Y in rng F by A6,TARSKI:def 4;
A9: dom F = NAT by FUNCT_2:def 1;
    consider n being set such that
A10: n in NAT and
A11: F.n = Y by A8,A9,FUNCT_1:def 5;
    reconsider n as Element of NAT by A10;
A12: Y = RAT_with_denominator (n+1) by A4,A11;
    thus thesis by A7,A12;
  end;
A13: union rng F c= RAT by A5,TARSKI:def 3;
A14: for x being set st x in RAT holds x in union rng F
  proof
    let x be set;
    assume
A15: x in RAT;
    reconsider x as Rational by A15;
A16: dom F = NAT by FUNCT_2:def 1;
A17: denominator x <> 0 by RAT_1:def 3;
    consider m being Nat such that
A18: denominator x = m + 1 by A17,NAT_1:6;
    reconsider m as Element of NAT by ORDINAL1:def 13;
A19: denominator x = m + 1 by A18;
    reconsider n = denominator x - 1 as Element of NAT by A19;
A20: x = (numerator x)/(n+1) by RAT_1:37;
A21: x in RAT_with_denominator (n+1) by A20,Def2;
A22: x in F.n by A4,A21;
A23: F.n in rng F by A16,FUNCT_1:def 5;
    thus thesis by A22,A23,TARSKI:def 4;
  end;
A24: RAT c= union rng F by A14,TARSKI:def 3;
A25: union rng F = RAT by A13,A24,XBOOLE_0:def 10;
A26: dom F = NAT by FUNCT_2:def 1;
A27: rng F is countable by A26,CARD_3:127;
A28: for Y being set st Y in rng F holds Y is countable
  proof
    let Y be set;
    assume
A29: Y in rng F;
    consider n being set such that
A30: n in dom F and
A31: F.n = Y by A29,FUNCT_1:def 5;
    reconsider n as Element of NAT by A30,FUNCT_2:def 1;
A32: Y = RAT_with_denominator (n+1) by A4,A31;
A33: INT,Y are_equipotent by A32,Th4;
A34: NAT,Y are_equipotent by A33,Th3,WELLORD2:22;
A35: card NAT = card Y by A34,CARD_1:21;
A36: card NAT c= omega by CARD_3:def 15,CARD_4:44;
    thus thesis by A35,A36,CARD_3:def 15;
  end;
A37: RAT is countable by A25,A27,A28,CARD_4:62;
A38: card RAT c= omega by A37,CARD_3:def 15;
A39: not omega in card RAT by A38;
A40: not omega c= card RAT or not omega <> card RAT by A39,CARD_1:13;
A41: card RAT in omega by A1,A40,CARD_1:14,21,84;
  thus contradiction by A41;
end;

begin  :: Basic operations on R_EAL valued functions

definition
  let C be non empty set;
  let f1,f2 be PartFunc of C,ExtREAL;
  deffunc F(Element of C) = f1.$1 + f2.$1;
  func f1+f2 -> PartFunc of C,ExtREAL means
  :Def3:
  dom it =
  (dom f1 /\ dom f2)\((f1"{-infty} /\ f2"{+infty}) \/ (f1"{+infty} /\
  f2"{-infty}))
  & for c being Element of C st c in dom it holds it.c = f1.c + f2.c;
  existence
  proof
    defpred P[Element of C] means
    $1 in (dom f1 /\ dom f2)\((f1"{-infty} /\ f2"{+infty}) \/
    (f1"{+infty} /\ f2"{-infty}));
    consider F be PartFunc of C,ExtREAL such that
A1: for c being Element of C holds c in dom F iff P[c] and
A2: for c being Element of C st c in dom F holds F.c = F(c) from SEQ_1:sch 3;
    take F;
A3: for x being set st x in dom F holds
    x in (dom f1 /\ dom f2)\((f1"{-infty} /\ f2"{+infty}) \/
    (f1"{+infty} /\ f2"{-infty}))
    proof
      let x be set;
      assume
A4:   x in dom F;
A5:   dom F c= C by RELAT_1:def 18;
      reconsider x as Element of C by A4,A5;
A6:   x in dom F by A4;
      thus thesis by A1,A6;
    end;
A7: dom F c= (dom f1 /\ dom f2)\((f1"{-infty} /\ f2"{+infty})
    \/ (f1"{+infty} /\ f2"{-infty})) by A3,TARSKI:def 3;
    A8: for x being set st x in (dom f1 /\ dom f2)\((f1"{-infty} /\ f2"{+infty
    })\/ (f1"{+infty} /\ f2"{-infty})) holds x in dom F
    proof
      let x be set;
      assume
A9:   x in (dom f1 /\ dom f2)\((f1"{-infty} /\ f2"{+infty}) \/
      (f1"{+infty} /\ f2"{-infty}));
A10:  x in dom f1 by A9,XBOOLE_0:def 4;
A11:  dom f1 c= C by RELAT_1:def 18;
      reconsider x as Element of C by A10,A11;
A12:  x in (dom f1 /\ dom f2)\((f1"{-infty} /\ f2"{+infty}) \/
      (f1"{+infty} /\ f2"{-infty})) by A9;
      thus thesis by A1,A12;
    end;
A13: (dom f1 /\ dom f2)\((f1"{-infty} /\ f2"{+infty}) \/
    (f1"{+infty} /\ f2"{-infty})) c= dom F by A8,TARSKI:def 3;
    thus dom F=(dom f1 /\ dom f2)\((f1"{-infty} /\ f2"{+infty}) \/ (f1"{+infty}
    /\ f2"{-infty})) by A7,A13,XBOOLE_0:def 10;
    let c be Element of C;
    assume
A14: c in dom F;
    thus thesis by A2,A14;
  end;
  uniqueness
  proof
    set X = (dom f1 /\ dom f2)\((f1"{-infty} /\ f2"{+infty}) \/ (f1"{+infty} /\
    f2"{-infty}));
    thus for f,g being PartFunc of C,ExtREAL st
    (dom f=X & for c be Element of C st c in dom f holds f.c = F(c)) &
    (dom g=X & for c be Element of C st c in dom g holds g.c = F(c))
    holds f = g from SEQ_1:sch 4;
  end;
  deffunc F(Element of C) = f1.$1 - f2.$1;
  func f1-f2 -> PartFunc of C,ExtREAL means
  dom it =
  (dom f1 /\ dom f2)\((f1"{+infty} /\ f2"{+infty}) \/ (f1"{-infty} /\
  f2"{-infty}))
  & for c being Element of C st c in dom it holds it.c = f1.c - f2.c;
  existence
  proof
    defpred P[Element of C] means
    $1 in (dom f1 /\ dom f2)\((f1"{+infty} /\ f2"{+infty}) \/
    (f1"{-infty} /\ f2"{-infty}));
    consider F be PartFunc of C,ExtREAL such that
A15: for c being Element of C holds c in dom F iff P[c] and
A16: for c being Element of C st c in dom F holds F.c = F(c) from SEQ_1:sch 3;
    take F;
A17: for x being set st x in dom F holds
    x in (dom f1 /\ dom f2)\((f1"{+infty} /\ f2"{+infty}) \/
    (f1"{-infty} /\ f2"{-infty}))
    proof
      let x be set;
      assume
A18:  x in dom F;
A19:  dom F c= C by RELAT_1:def 18;
      reconsider x as Element of C by A18,A19;
A20:  x in dom F by A18;
      thus thesis by A15,A20;
    end;
A21: dom F c= (dom f1 /\ dom f2)\((f1"{+infty} /\ f2"{+infty})
    \/ (f1"{-infty} /\ f2"{-infty})) by A17,TARSKI:def 3;
    A22: for
 x being set st x in (dom f1 /\ dom f2)\((f1"{+infty} /\ f2"{+infty }
    ) \/ (f1"{-infty} /\ f2"{-infty})) holds x in dom F
    proof
      let x be set;
      assume
A23:  x in (dom f1 /\ dom f2)\((f1"{+infty} /\ f2"{+infty}) \/
      (f1"{-infty} /\ f2"{-infty}));
A24:  x in dom f1 by A23,XBOOLE_0:def 4;
A25:  dom f1 c= C by RELAT_1:def 18;
      reconsider x as Element of C by A24,A25;
A26:  x in (dom f1 /\ dom f2)\((f1"{+infty} /\ f2"{+infty}) \/
      (f1"{-infty} /\ f2"{-infty})) by A23;
      thus thesis by A15,A26;
    end;
A27: (dom f1 /\ dom f2)\((f1"{+infty} /\ f2"{+infty}) \/
    (f1"{-infty} /\ f2"{-infty})) c= dom F by A22,TARSKI:def 3;
    thus dom F=(dom f1 /\ dom f2)\((f1"{+infty} /\ f2"{+infty}) \/ (f1"{-infty
    } /\ f2"{-infty})) by A21,A27,XBOOLE_0:def 10;
    let c be Element of C;
    assume
A28: c in dom F;
    thus thesis by A16,A28;
  end;
  uniqueness
  proof
    set X = (dom f1 /\ dom f2)\((f1"{+infty} /\ f2"{+infty}) \/ (f1"{-infty} /\
    f2"{-infty}));
    thus for f,g being PartFunc of C,ExtREAL st
    (dom f=X & for c be Element of C st c in dom f holds f.c = F(c)) &
    (dom g=X & for c be Element of C st c in dom g holds g.c = F(c))
    holds f = g from SEQ_1:sch 4;
  end;
  deffunc F(Element of C) = f1.$1 * f2.$1;
  func f1(#)f2 -> PartFunc of C,ExtREAL means
  :Def5:
  dom it = dom f1 /\ dom f2
  & for c being Element of C st c in dom it holds it.c = f1.c * f2.c;
  existence
  proof
    defpred P[Element of C] means $1 in dom f1 /\ dom f2;
    consider F be PartFunc of C,ExtREAL such that
A29: for c being Element of C holds c in dom F iff P[c] and
A30: for c being Element of C st c in dom F holds F.c = F(c) from SEQ_1:sch 3;
    take F;
A31: for x being set st x in dom F holds x in dom f1 /\ dom f2
    proof
      let x be set;
      assume
A32:  x in dom F;
A33:  dom F c= C by RELAT_1:def 18;
      reconsider x as Element of C by A32,A33;
A34:  x in dom F by A32;
      thus thesis by A29,A34;
    end;
A35: dom F c= dom f1 /\ dom f2 by A31,TARSKI:def 3;
A36: for x being set st x in dom f1 /\ dom f2 holds x in dom F
    proof
      let x be set;
      assume
A37:  x in dom f1 /\ dom f2;
A38:  x in dom f1 by A37,XBOOLE_0:def 4;
A39:  dom f1 c= C by RELAT_1:def 18;
      reconsider x as Element of C by A38,A39;
A40:  x in dom f1 /\ dom f2 by A37;
      thus thesis by A29,A40;
    end;
A41: dom f1 /\ dom f2 c= dom F by A36,TARSKI:def 3;
    thus dom F=dom f1 /\ dom f2 by A35,A41,XBOOLE_0:def 10;
    let c be Element of C;
    assume
A42: c in dom F;
    thus thesis by A30,A42;
  end;
  uniqueness
  proof
    set X = dom f1 /\ dom f2;
    thus for F,G being PartFunc of C,ExtREAL st
    (dom F=X & for c be Element of C st c in dom F holds F.c = F(c)) &
    (dom G=X & for c be Element of C st c in dom G holds G.c = F(c))
    holds F = G from SEQ_1:sch 4;
  end;
end;

definition
  let C be non empty set, f be PartFunc of C,ExtREAL, r be Real;
  deffunc F(Element of C) = (R_EAL r) * f.$1;
  func r(#)f -> PartFunc of C,ExtREAL means
  :Def6:
  dom it = dom f
  & for c being Element of C st c in dom it holds it.c = (R_EAL r) * f.c;
  existence
  proof
    defpred P[Element of C] means $1 in dom f;
    consider F be PartFunc of C,ExtREAL such that
A1: for c being Element of C holds c in dom F iff P[c] and
A2: for c being Element of C st c in dom F holds F.c = F(c) from SEQ_1:sch 3;
    take F;
A3: for x being set st x in dom F holds x in dom f
    proof
      let x be set;
      assume
A4:   x in dom F;
A5:   dom F c= C by RELAT_1:def 18;
      reconsider x as Element of C by A4,A5;
A6:   x in dom F by A4;
      thus thesis by A1,A6;
    end;
A7: dom F c= dom f by A3,TARSKI:def 3;
A8: for x being set st x in dom f holds x in dom F
    proof
      let x be set;
      assume
A9:   x in dom f;
A10:  dom f c= C by RELAT_1:def 18;
      reconsider x as Element of C by A9,A10;
A11:  x in dom f by A9;
      thus thesis by A1,A11;
    end;
A12: dom f c= dom F by A8,TARSKI:def 3;
    thus dom F=dom f by A7,A12,XBOOLE_0:def 10;
    let c be Element of C;
    assume
A13: c in dom F;
    thus thesis by A2,A13;
  end;
  uniqueness
  proof
    set X = dom f;
    thus for F,G being PartFunc of C,ExtREAL st
    (dom F=X & for c be Element of C st c in dom F holds F.c = F(c)) &
    (dom G=X & for c be Element of C st c in dom G holds G.c = F(c))
    holds F = G from SEQ_1:sch 4;
  end;
end;

theorem Th6:
  for C being non empty set, f being PartFunc of C,ExtREAL, r being Real
  st r <> 0 holds
  for c being Element of C st c in dom(r(#)f) holds f.c = (r(#)f).c / R_EAL r
proof
  let C being non empty set;
  let f being PartFunc of C,ExtREAL;
  let r being Real;
  assume
A1: r <> 0;
  let c be Element of C;
  assume
A2: c in dom(r(#)f);
A3: (r(#)f).c = (R_EAL r) * f.c by A2,Def6;
  per cases;
  suppose
A4: f.c = +infty;
A5: now per cases by A1;
      suppose
A6:     0. < R_EAL r;
A7:     (r(#)f).c = +infty by A3,A4,A6,XXREAL_3:def 5;
A8:     R_EAL r < +infty by XXREAL_0:9;
        thus thesis by A4,A6,A7,A8,XXREAL_3:94;
      end;
      suppose
A9:     R_EAL r < 0.;
A10:    (r(#)f).c = -infty by A3,A4,A9,XXREAL_3:def 5;
A11:    R_EAL r > -infty by XXREAL_0:12;
        thus thesis by A4,A9,A10,A11,XXREAL_3:95;
      end;
    end;
    thus thesis by A5;
  end;
  suppose
A12: f.c = -infty;
A13: now per cases by A1;
      suppose
A14:    0. < R_EAL r;
A15:    (r(#)f).c = -infty by A3,A12,A14,XXREAL_3:def 5;
A16:    R_EAL r < +infty by XXREAL_0:9;
        thus thesis by A12,A14,A15,A16,XXREAL_3:97;
      end;
      suppose
A17:    R_EAL r < 0.;
A18:    (r(#)f).c = +infty by A3,A12,A17,XXREAL_3:def 5;
A19:    R_EAL r > -infty by XXREAL_0:12;
        thus thesis by A12,A17,A18,A19,XXREAL_3:96;
      end;
    end;
    thus thesis by A13;
  end;
  suppose
A20: f.c <> +infty & f.c <> -infty;
    reconsider a = f.c as Real by A20,XXREAL_0:14;
A21: (r(#)f).c = r * a by A3,EXTREAL1:13;
A22: (r(#)f).c / R_EAL r = r*a/r by A21,EXTREAL1:32
      .= a/(r/r) by XCMPLX_1:78
      .= a / 1 by A1,XCMPLX_1:60;
    thus thesis by A22;
  end;
end;

definition
  let C be non empty set;
  let f be PartFunc of C,ExtREAL;
  deffunc F(Element of C) = -(f.$1);
  func -f -> PartFunc of C,ExtREAL means
  dom it = dom f &
  for c being Element of C st c in dom it holds it.c = -(f.c);
  existence
  proof
    defpred P[Element of C] means $1 in dom f;
    consider F be PartFunc of C,ExtREAL such that
A1: for c being Element of C holds c in dom F iff P[c] and
A2: for c being Element of C st c in dom F holds F.c = F(c) from SEQ_1:sch 3;
    take F;
A3: for x being set st x in dom F holds x in dom f
    proof
      let x be set;
      assume
A4:   x in dom F;
A5:   dom F c= C by RELAT_1:def 18;
      reconsider x as Element of C by A4,A5;
A6:   x in dom F by A4;
      thus thesis by A1,A6;
    end;
A7: dom F c= dom f by A3,TARSKI:def 3;
A8: for x being set st x in dom f holds x in dom F
    proof
      let x be set;
      assume
A9:   x in dom f;
A10:  dom f c= C by RELAT_1:def 18;
      reconsider x as Element of C by A9,A10;
A11:  x in dom f by A9;
      thus thesis by A1,A11;
    end;
A12: dom f c= dom F by A8,TARSKI:def 3;
    thus dom F=dom f by A7,A12,XBOOLE_0:def 10;
    let c be Element of C;
    assume
A13: c in dom F;
    thus thesis by A2,A13;
  end;
  uniqueness
  proof
    set X = dom f;
    thus for F,G being PartFunc of C,ExtREAL st
    (dom F=X & for c be Element of C st c in dom F holds F.c = F(c)) &
    (dom G=X & for c be Element of C st c in dom G holds G.c = F(c))
    holds F = G from SEQ_1:sch 4;
  end;
end;

definition
  func 1. -> R_eal equals
  1;
  correctness by XXREAL_0:def 1;
end;

definition
  let C be non empty set;
  let f be PartFunc of C,ExtREAL;
  let r be Real;
  deffunc F(Element of C) = (R_EAL r)/(f.$1);
  func r/f -> PartFunc of C,ExtREAL means
  :Def9:
  dom it = dom f \ f"{0.} &
  for c being Element of C st c in dom it holds it.c = (R_EAL r)/(f.c);
  existence
  proof
    defpred P[Element of C] means $1 in dom f \ f"{0.};
    consider F be PartFunc of C,ExtREAL such that
A1: for c being Element of C holds c in dom F iff P[c] and
A2: for c being Element of C st c in dom F holds F.c = F(c) from SEQ_1:sch 3;
    take F;
A3: for x being set st x in dom F holds x in dom f \ f"{0.}
    proof
      let x be set;
      assume
A4:   x in dom F;
A5:   dom F c= C by RELAT_1:def 18;
      reconsider x as Element of C by A4,A5;
A6:   x in dom F by A4;
      thus thesis by A1,A6;
    end;
A7: dom F c= dom f \ f"{0.} by A3,TARSKI:def 3;
A8: for x being set st x in dom f \ f"{0.} holds x in dom F
    proof
      let x be set;
      assume
A9:   x in dom f \ f"{0.};
A10:  dom f c= C by RELAT_1:def 18;
A11:  dom f \ f"{0.} c= C by A10,XBOOLE_1:1;
      reconsider x as Element of C by A9,A11;
A12:  x in dom f \ f"{0.} by A9;
      thus thesis by A1,A12;
    end;
A13: dom f \ f"{0.} c= dom F by A8,TARSKI:def 3;
    thus dom F=dom f \ f"{0.} by A7,A13,XBOOLE_0:def 10;
    let c be Element of C;
    assume
A14: c in dom F;
    thus thesis by A2,A14;
  end;
  uniqueness
  proof
    set X = dom f \ f"{0.};
    thus for F,G being PartFunc of C,ExtREAL st
    (dom F=X & for c be Element of C st c in dom F holds F.c = F(c)) &
    (dom G=X & for c be Element of C st c in dom G holds G.c = F(c))
    holds F = G from SEQ_1:sch 4;
  end;
end;

theorem
  for C being non empty set, f being PartFunc of C,ExtREAL holds
  dom (1/f) = dom f \ f"{0.} &
  for c being Element of C st c in dom (1/f) holds (1/f).c = 1./(f.c)
proof
  let C be non empty set;
  let f be PartFunc of C,ExtREAL;
  thus dom (1/f) = dom f \ f"{0.} by Def9;
  thus
  for c being Element of C st c in dom (1/f) holds (1/f).c = 1./(f.c)
  proof
    let c be Element of C;
    assume
A1: c in dom (1/f);
A2: (1/f).c = (R_EAL 1)/(f.c) by A1,Def9;
    thus thesis by A2;
  end;
end;

definition
  let C be non empty set;
  let f be PartFunc of C,ExtREAL;
  deffunc F(Element of C) = |. f.$1 .|;
  func |.f.| -> PartFunc of C,ExtREAL means
  dom it = dom f &
  for c being Element of C st c in dom it holds it.c = |. f.c .|;
  existence
  proof
    defpred P[Element of C] means $1 in dom f;
    consider F be PartFunc of C,ExtREAL such that
A1: for c being Element of C holds c in dom F iff P[c] and
A2: for c being Element of C st c in dom F holds F.c = F(c) from SEQ_1:sch 3;
    take F;
A3: for x being set st x in dom F holds x in dom f
    proof
      let x be set;
      assume
A4:   x in dom F;
A5:   dom F c= C by RELAT_1:def 18;
      reconsider x as Element of C by A4,A5;
A6:   x in dom F by A4;
      thus thesis by A1,A6;
    end;
A7: dom F c= dom f by A3,TARSKI:def 3;
A8: for x being set st x in dom f holds x in dom F
    proof
      let x be set;
      assume
A9:   x in dom f;
A10:  dom f c= C by RELAT_1:def 18;
      reconsider x as Element of C by A9,A10;
A11:  x in dom f by A9;
      thus thesis by A1,A11;
    end;
A12: dom f c= dom F by A8,TARSKI:def 3;
    thus dom F=dom f by A7,A12,XBOOLE_0:def 10;
    let c be Element of C;
    assume
A13: c in dom F;
    thus thesis by A2,A13;
  end;
  uniqueness
  proof
    set X = dom f;
    thus for F,G being PartFunc of C,ExtREAL st
    (dom F=X & for c be Element of C st c in dom F holds F.c = F(c)) &
    (dom G=X & for c be Element of C st c in dom G holds G.c = F(c))
    holds F = G from SEQ_1:sch 4;
  end;
end;

canceled;

theorem Th9:
  for C being non empty set, f1,f2 being PartFunc of C,ExtREAL holds
  f1 + f2 = f2 + f1
proof
  let C be non empty set;
  let f1,f2 be PartFunc of C,ExtREAL;
A1: dom (f1+f2) =(dom f1 /\ dom f2)\
  ((f1"{-infty} /\ f2"{+infty}) \/ (f1"{+infty} /\ f2"{-infty})) by Def3
    .= dom (f2+f1) by Def3;
  A2: for x being Element of C st x in dom (f1+f2) holds (f1+f2).x = (f2+f1).x
  proof
    let x be Element of C;
    assume
A3: x in dom (f1+f2);
A4: (f1+f2).x = f1.x + f2.x by A3,Def3;
    thus thesis by A1,A3,A4,Def3;
  end;
  thus thesis by A1,A2,PARTFUN1:34;
end;

theorem Th10:
  for C being non empty set, f1,f2 being PartFunc of C,ExtREAL holds
  f1 (#) f2 = f2 (#) f1
proof
  let C be non empty set;
  let f1,f2 be PartFunc of C,ExtREAL;
A1: dom (f1(#)f2) = dom f1 /\ dom f2 by Def5
    .= dom (f2(#)f1) by Def5;
  A2: for
 x being Element of C st x in dom (f1(#)f2) holds (f1(#)f2).x = (f2 (#)
  f1).x
  proof
    let x be Element of C;
    assume
A3: x in dom(f1(#)f2);
A4: (f1(#)f2).x = f1.x * f2.x by A3,Def5;
    thus thesis by A1,A3,A4,Def5;
  end;
  thus thesis by A1,A2,PARTFUN1:34;
end;

definition
  let C be non empty set;
  let f1,f2 be PartFunc of C,ExtREAL;
  redefine func f1+f2;
  commutativity by Th9;
  redefine func f1(#)f2;
  commutativity by Th10;
end;

begin  :: Level sets

theorem Th11:
  for r being Real holds ex n being Element of NAT st r <= n
proof
  let r being Real;
  per cases;
  suppose
A1: [/ r \] >= 0;
    reconsider n=[/ r \] as Element of NAT by A1,INT_1:16;
    take n;
    thus thesis by INT_1:def 5;
  end;
  suppose
A2: [/ r \] < 0;
    take 0;
    thus thesis by A2,INT_1:def 5;
  end;
end;

theorem Th12:
  for r being Real holds ex n being Element of NAT st -n <= r
proof
  let r being Real;
A1: [\ r /] is Element of INT by INT_1:def 2;
  A2: [\
 r /] <= r & ex k being Element of NAT st ( [\ r /] = k or [\ r /] = -k)
  by A1,INT_1:def 1,def 4;
  per cases;
  suppose
A3: [\ r /] < 0;
    thus thesis by A2,A3;
  end;
  suppose
A4: [\ r /] >= 0;
    take 0;
    thus thesis by A4,INT_1:def 4;
  end;
end;

theorem Th13:
  for r,s being real number st r < s holds
  ex n being Element of NAT st 1/(n+1) < s-r
proof
  let r,s be real number;
  assume
A1: r < s;
A2: s-r > 0 by A1,XREAL_1:52;
  consider t being real number such that
A3: 0 < t and
A4: t < s-r by A2,XREAL_1:7;
  reconsider t as Real by XREAL_0:def 1;
A5: 1/t > 0 by A3,XREAL_1:141;
A6: [/ 1/t \] + 1 > 1/t by INT_1:57;
  set n = [/ 1/t \];
  reconsider n as Element of NAT by A5,A6,INT_1:16,20;
A7: (n+1)*t >= 1 by A3,A6,XREAL_1:83;
A8: 1/(n+1) <= t by A7,XREAL_1:81;
A9: 1/(n+1) < s-r by A4,A8,XXREAL_0:2;
  thus thesis by A9;
end;

theorem Th14:
  for r,s being real number st for n being Element of NAT
  holds r-1/(n+1) <= s holds r <= s
proof
  let r,s be real number;
  assume
A1: for n being Element of NAT holds r-1/(n+1) <= s;
  assume
A2: r > s;
  consider n being Element of NAT such that
A3: 1/(n+1) < r - s by A2,Th13;
A4: s + 1/(n+1) < r by A3,XREAL_1:22;
A5: s < r - 1/(n+1) by A4,XREAL_1:22;
  thus contradiction by A1,A5;
end;

theorem Th15:
  for a being R_eal st (for r being Real holds R_EAL r < a) holds a = +infty
proof
  let a being R_eal;
  assume
A1: for r being Real holds R_EAL r < a;
  assume
A2: not a = +infty;
A3: a < +infty by A2,XXREAL_0:4;
  consider b being R_eal such that
A4: a < b
  and b < +infty and
A5: b in REAL by A3,MEASURE5:17;
  reconsider b as Real by A5;
A6: a <= R_EAL b by A4;
  thus contradiction by A1,A6;
end;

theorem Th16:
  for a being R_eal st (for r being Real holds a < R_EAL r) holds a = -infty
proof
  let a being R_eal;
  assume
A1: for r being Real holds a < R_EAL r;
  assume
A2: not a = -infty;
A3: -infty <= a by XXREAL_0:5;
A4: -infty < a by A2,A3,XXREAL_0:1;
  consider b being R_eal such that
  -infty < b and
A5: b < a and
A6: b in REAL by A4,MEASURE5:17;
  reconsider b as Real by A6;
A7: R_EAL b <= a by A5;
  thus contradiction by A1,A7;
end;

reserve X for set;
reserve x for Element of X;
reserve f,g for PartFunc of X,ExtREAL;
reserve S for SigmaField of X;
reserve F for Function of NAT,S;
reserve A for set;
reserve a for R_eal;
reserve r,s for Real;
reserve n,m for Element of NAT;

notation
  let f be ext-real-valued Function, a be ext-real number;
  synonym eq_dom(f,a) for Coim(f,a);
end;

definition
  canceled;
  let f be ext-real-valued Function, a be ext-real number;
  defpred P[set] means $1 in dom f & f.$1 < a;
  func less_dom(f,a) -> set means
  :Def12:
  for x being set holds x in it iff x in dom f & f.x < a;
  existence
  proof
    consider A being set such that
A1: for x being set holds x in A iff x in dom f & P[x] from XBOOLE_0:sch 1;
    take A;
    thus thesis by A1;
  end;
  uniqueness
  proof
    thus for D1,D2 be set st (for x be set holds x in D1 iff P[x]) &
    (for x be set holds x in D2 iff P[x]) holds D1 = D2 from XBOOLE_0:sch 3;
  end;
  defpred P[set] means $1 in dom f & f.$1 <= a;
  func less_eq_dom(f,a) -> set means
  :Def13:
  for x being set holds x in it iff x in dom f & f.x <= a;
  existence
  proof
    consider A being set such that
A2: for x being set holds x in A iff x in dom f & P[x] from XBOOLE_0:sch 1;
    take A;
    thus thesis by A2;
  end;
  uniqueness
  proof
    thus for D1,D2 be set st (for x be set holds x in D1 iff P[x]) &
    (for x be set holds x in D2 iff P[x]) holds D1 = D2 from XBOOLE_0:sch 3;
  end;
  defpred P[set] means $1 in dom f & a < f.$1;
  func great_dom(f,a) -> set means
  :Def14:
  for x being set holds x in it iff x in dom f & a < f.x;
  existence
  proof
    consider A being set such that
A3: for x being set holds x in A iff x in dom f & P[x] from XBOOLE_0:sch 1;
    take A;
    thus thesis by A3;
  end;
  uniqueness
  proof
    thus for D1,D2 be set st (for x be set holds x in D1 iff P[x]) &
    (for x be set holds x in D2 iff P[x]) holds D1 = D2 from XBOOLE_0:sch 3;
  end;
  defpred P[set] means $1 in dom f & a <= f.$1;
  func great_eq_dom(f,a) -> set means
  :Def15:
  for x being set holds x in it iff x in dom f & a <= f.x;
  existence
  proof
    consider A being set such that
A4: for x being set holds x in A iff x in dom f & P[x] from XBOOLE_0:sch 1;
    take A;
    thus thesis by A4;
  end;
  uniqueness
  proof
    thus for D1,D2 be set st (for x be set holds x in D1 iff P[x]) &
    (for x be set holds x in D2 iff P[x]) holds D1 = D2 from XBOOLE_0:sch 3;
  end;
  defpred P[set] means $1 in dom f & a = f.$1;
  redefine func eq_dom(f,a) means
  :Def16:
  for x being set holds x in it iff x in dom f & f.x = a;
  compatibility
  proof
    let X be set;
    thus X = eq_dom(f,a) implies
    for x being set holds x in X iff x in dom f & f.x = a
    proof
      assume
A5:   X = eq_dom(f,a);
      let x being set;
      thus x in X implies x in dom f & f.x = a
      proof
        assume
A6:     x in X;
        thus x in dom f by A5,A6,FUNCT_1:def 13;
A7:     f.x in {a} by A5,A6,FUNCT_1:def 13;
        thus thesis by A7,TARSKI:def 1;
      end;
      assume
A8:   x in dom f;
      assume
A9:   f.x = a;
A10:  f.x in {a} by A9,TARSKI:def 1;
      thus thesis by A5,A8,A10,FUNCT_1:def 13;
    end;
    assume
A11: for x being set holds x in X iff x in dom f & f.x = a;
A12: for x being set holds x in X iff x in dom f & f.x in {a}
    proof
      let x be set;
      thus x in X implies x in dom f & f.x in {a}
      proof
        assume
A13:    x in X;
        thus x in dom f by A11,A13;
A14:    f.x = a by A11,A13;
        thus thesis by A14,TARSKI:def 1;
      end;
      assume
A15:  x in dom f;
      assume
A16:  f.x in {a};
A17:  f.x = a by A16,TARSKI:def 1;
      thus thesis by A11,A15,A17;
    end;
    thus thesis by A12,FUNCT_1:def 13;
  end;
end;

definition
  let X be set, f be PartFunc of X,ExtREAL, a be ext-real number;
  redefine func less_dom(f,a) -> Subset of X;
  coherence
  proof
A1: less_dom(f,a) c= X
    proof
      let x be set;
      assume
A2:   x in less_dom(f,a);
A3:   x in dom f by A2,Def12;
A4:   dom f c= X by RELAT_1:def 18;
      thus thesis by A3,A4;
    end;
    thus thesis by A1;
  end;
  redefine func less_eq_dom(f,a) -> Subset of X;
  coherence
  proof
A5: less_eq_dom(f,a) c= X
    proof
      let x be set;
      assume
A6:   x in less_eq_dom(f,a);
A7:   x in dom f by A6,Def13;
A8:   dom f c= X by RELAT_1:def 18;
      thus thesis by A7,A8;
    end;
    thus thesis by A5;
  end;
  redefine func great_dom(f,a) -> Subset of X;
  coherence
  proof
A9: great_dom(f,a) c= X
    proof
      let x be set;
      assume
A10:  x in great_dom(f,a);
A11:  x in dom f by A10,Def14;
A12:  dom f c= X by RELAT_1:def 18;
      thus thesis by A11,A12;
    end;
    thus thesis by A9;
  end;
  redefine func great_eq_dom(f,a) -> Subset of X;
  coherence
  proof
A13: great_eq_dom(f,a) c= X
    proof
      let x be set;
      assume
A14:  x in great_eq_dom(f,a);
A15:  x in dom f by A14,Def15;
A16:  dom f c= X by RELAT_1:def 18;
      thus thesis by A15,A16;
    end;
    thus thesis by A13;
  end;
  redefine func eq_dom(f,a) -> Subset of X;
  coherence
  proof
A17: Coim(f,a) is Subset of X;
    thus thesis by A17;
  end;
end;

canceled;

theorem Th18:
  A c= dom f implies A /\ great_eq_dom(f,a) = A\(A /\ less_dom(f,a))
proof
  assume
A1: A c= dom f;
A2: dom f c= X by RELAT_1:def 18;
A3: A c= X by A1,A2,XBOOLE_1:1;
A4: for x being set st x in A /\ great_eq_dom(f,a) holds x in
  A\(A /\ less_dom(f,a))
  proof
    let x be set;
    assume
A5: x in A /\ great_eq_dom(f,a);
A6: x in A by A5,XBOOLE_0:def 4;
A7: x in great_eq_dom(f,a) by A5,XBOOLE_0:def 4;
A8: a <= f.x by A7,Def15;
A9: not x in less_dom(f,a) by A8,Def12;
A10: not x in (A /\ less_dom(f,a)) by A9,XBOOLE_0:def 4;
    thus thesis by A6,A10,XBOOLE_0:def 5;
  end;
A11: A /\ great_eq_dom(f,a) c= A\(A /\ less_dom(f,a)) by A4,TARSKI:def 3;
A12: for x being set st x in A\(A /\ less_dom(f,a)) holds x in
  A /\ great_eq_dom(f,a)
  proof
    let x be set;
    assume
A13: x in A\(A /\ less_dom(f,a));
A14: x in A by A13;
A15: not x in A /\ less_dom(f,a) by A13,XBOOLE_0:def 5;
A16: not x in less_dom(f,a) by A13,A15,XBOOLE_0:def 4;
    reconsider x as Element of X by A3,A14;
    reconsider y=f.x as R_eal;
A17: not y < a by A1,A14,A16,Def12;
A18: x in great_eq_dom(f,a) by A1,A14,A17,Def15;
    thus thesis by A13,A18,XBOOLE_0:def 4;
  end;
A19: A\(A /\ less_dom(f,a)) c= A /\ great_eq_dom(f,a) by A12,TARSKI:def 3;
  thus thesis by A11,A19,XBOOLE_0:def 10;
end;

theorem Th19:
  A c= dom f implies A /\ great_dom(f,a) = A\(A /\ less_eq_dom(f,a))
proof
  assume
A1: A c= dom f;
A2: dom f c= X by RELAT_1:def 18;
A3: A c= X by A1,A2,XBOOLE_1:1;
A4: for x being set st x in A /\ great_dom(f,a) holds x in
  A\(A /\ less_eq_dom(f,a))
  proof
    let x be set;
    assume
A5: x in A /\ great_dom(f,a);
A6: x in A by A5,XBOOLE_0:def 4;
A7: x in great_dom(f,a) by A5,XBOOLE_0:def 4;
A8: a < f.x by A7,Def14;
A9: not x in less_eq_dom(f,a) by A8,Def13;
A10: not x in (A /\ less_eq_dom(f,a)) by A9,XBOOLE_0:def 4;
    thus thesis by A6,A10,XBOOLE_0:def 5;
  end;
A11: A /\ great_dom(f,a) c= A\(A /\ less_eq_dom(f,a)) by A4,TARSKI:def 3;
A12: for x being set st x in A\(A /\ less_eq_dom(f,a)) holds x in
  A /\ great_dom(f,a)
  proof
    let x be set;
    assume
A13: x in A\(A /\ less_eq_dom(f,a));
A14: x in A by A13;
A15: not x in A /\ less_eq_dom(f,a) by A13,XBOOLE_0:def 5;
A16: not x in less_eq_dom(f,a) by A13,A15,XBOOLE_0:def 4;
    reconsider x as Element of X by A3,A14;
    reconsider y=f.x as R_eal;
A17: not y <= a by A1,A14,A16,Def13;
A18: x in great_dom(f,a) by A1,A14,A17,Def14;
    thus thesis by A13,A18,XBOOLE_0:def 4;
  end;
A19: A\(A /\ less_eq_dom(f,a)) c= A /\ great_dom(f,a) by A12,TARSKI:def 3;
  thus thesis by A11,A19,XBOOLE_0:def 10;
end;

theorem Th20:
  A c= dom f implies A /\ less_eq_dom(f,a) = A\(A /\ great_dom(f,a))
proof
  assume
A1: A c= dom f;
A2: dom f c= X by RELAT_1:def 18;
A3: A c= X by A1,A2,XBOOLE_1:1;
A4: A /\ less_eq_dom(f,a) c= A\(A /\ great_dom(f,a))
  proof
    let x be set;
    assume
A5: x in A /\ less_eq_dom(f,a);
A6: x in A by A5,XBOOLE_0:def 4;
A7: x in less_eq_dom(f,a) by A5,XBOOLE_0:def 4;
A8: f.x <= a by A7,Def13;
A9: not x in great_dom(f,a) by A8,Def14;
A10: not x in (A /\ great_dom(f,a)) by A9,XBOOLE_0:def 4;
    thus thesis by A6,A10,XBOOLE_0:def 5;
  end;
A11: for x being set st x in A\(A /\ great_dom(f,a)) holds x in
  A /\ less_eq_dom(f,a)
  proof
    let x be set;
    assume
A12: x in A\(A /\ great_dom(f,a));
A13: x in A by A12;
A14: not x in A /\ great_dom(f,a) by A12,XBOOLE_0:def 5;
A15: not x in great_dom(f,a) by A12,A14,XBOOLE_0:def 4;
    reconsider x as Element of X by A3,A13;
    reconsider y=f.x as R_eal;
A16: not a < y by A1,A13,A15,Def14;
A17: x in less_eq_dom(f,a) by A1,A13,A16,Def13;
    thus thesis by A12,A17,XBOOLE_0:def 4;
  end;
A18: A\(A /\ great_dom(f,a)) c= A /\ less_eq_dom(f,a) by A11,TARSKI:def 3;
  thus thesis by A4,A18,XBOOLE_0:def 10;
end;

theorem Th21:
  A c= dom f implies A /\ less_dom(f,a) = A\(A /\ great_eq_dom(f,a))
proof
  assume
A1: A c= dom f;
A2: dom f c= X by RELAT_1:def 18;
A3: A c= X by A1,A2,XBOOLE_1:1;
A4: for x being set st x in A /\ less_dom(f,a) holds x in
  A\(A /\ great_eq_dom(f,a))
  proof
    let x be set;
    assume
A5: x in A /\ less_dom(f,a);
A6: x in A by A5,XBOOLE_0:def 4;
A7: x in less_dom(f,a) by A5,XBOOLE_0:def 4;
A8: f.x < a by A7,Def12;
A9: not x in great_eq_dom(f,a) by A8,Def15;
A10: not x in (A /\ great_eq_dom(f,a)) by A9,XBOOLE_0:def 4;
    thus thesis by A6,A10,XBOOLE_0:def 5;
  end;
A11: A /\ less_dom(f,a) c= A\(A /\ great_eq_dom(f,a)) by A4,TARSKI:def 3;
A12: for x being set st x in A\(A /\ great_eq_dom(f,a)) holds x in
  A /\ less_dom(f,a)
  proof
    let x be set;
    assume
A13: x in A\(A /\ great_eq_dom(f,a));
A14: x in A by A13;
A15: not x in A /\ great_eq_dom(f,a) by A13,XBOOLE_0:def 5;
A16: not x in great_eq_dom(f,a) by A13,A15,XBOOLE_0:def 4;
    reconsider x as Element of X by A3,A14;
    reconsider y=f.x as R_eal;
A17: not a <= y by A1,A14,A16,Def15;
A18: x in less_dom(f,a) by A1,A14,A17,Def12;
    thus thesis by A13,A18,XBOOLE_0:def 4;
  end;
A19: A\(A /\ great_eq_dom(f,a)) c= A /\ less_dom(f,a) by A12,TARSKI:def 3;
  thus thesis by A11,A19,XBOOLE_0:def 10;
end;

theorem
  A /\ eq_dom(f,a) = A /\ great_eq_dom(f,a) /\ less_eq_dom(f,a)
proof
A1: for x being set st x in A /\ eq_dom(f,a) holds
  x in A /\ great_eq_dom(f,a) /\ less_eq_dom(f,a)
  proof
    let x be set;
    assume
A2: x in A /\ eq_dom(f,a);
A3: x in A by A2,XBOOLE_0:def 4;
A4: x in eq_dom(f,a) by A2,XBOOLE_0:def 4;
A5: a = f.x by A4,Def16;
    reconsider x as Element of X by A2;
A6: x in dom f by A4,Def16;
A7: x in great_eq_dom(f,a) by A5,A6,Def15;
A8: x in A /\ great_eq_dom(f,a) by A3,A7,XBOOLE_0:def 4;
A9: x in less_eq_dom(f,a) by A5,A6,Def13;
    thus thesis by A8,A9,XBOOLE_0:def 4;
  end;
A10: A /\ eq_dom(f,a) c= A /\ great_eq_dom(f,a) /\ less_eq_dom(f,a) by A1,
  TARSKI:def 3;
  A11: for x being set st x in A /\ great_eq_dom(f,a) /\ less_eq_dom(f,a) holds
  x in A /\ eq_dom(f,a)
  proof
    let x being set;
    assume
A12: x in A /\ great_eq_dom(f,a) /\ less_eq_dom(f,a);
A13: x in A /\ great_eq_dom(f,a) by A12,XBOOLE_0:def 4;
A14: x in less_eq_dom(f,a) by A12,XBOOLE_0:def 4;
A15: x in A by A13,XBOOLE_0:def 4;
A16: x in great_eq_dom(f,a) by A13,XBOOLE_0:def 4;
A17: a <= f.x by A16,Def15;
A18: f.x <= a by A14,Def13;
    reconsider x as Element of X by A12;
A19: x in dom f by A14,Def13;
A20: a = f.x by A17,A18,XXREAL_0:1;
A21: x in eq_dom(f,a) by A19,A20,Def16;
    thus thesis by A15,A21,XBOOLE_0:def 4;
  end;
A22: A /\ great_eq_dom(f,a) /\ less_eq_dom(f,a) c= A /\ eq_dom(f,a) by A11,
  TARSKI:def 3;
  thus thesis by A10,A22,XBOOLE_0:def 10;
end;

theorem
  for X, S, F, f, A, r st for n holds F.n = A /\ great_dom(f,R_EAL(r-1/(n+1)))
  holds A /\ great_eq_dom(f,R_EAL r) = meet rng F
proof
  let X,S,F,f,A,r;
  assume
A1: for n holds F.n = A /\ great_dom(f,R_EAL(r-1/(n+1)));
  A2: for
 x being set st x in A /\ great_eq_dom(f,R_EAL r) holds x in meet rng F
  proof
    let x be set;
    assume
A3: x in A /\ great_eq_dom(f,R_EAL r);
A4: x in A by A3,XBOOLE_0:def 4;
A5: x in great_eq_dom(f,R_EAL r) by A3,XBOOLE_0:def 4;
A6: for Y being set holds Y in rng F implies x in Y
    proof
      let Y be set;
A7:   Y in rng F implies x in Y
      proof
        assume
A8:     Y in rng F;
        consider m being Element of NAT such that
        m in dom F and
A9:     Y = F.m by A8,PARTFUN1:26;
A10:    Y = A /\ great_dom(f,R_EAL(r-1/(m+1))) by A1,A9;
A11:    x in dom f by A5,Def15;
        reconsider x as Element of X by A3;
A12:    R_EAL r <= f.x by A5,Def15;
A13:    (m+1)" > 0;
A14:    1/(m+1) > 0 by A13,XCMPLX_1:217;
A15:    r < r+1/(m+1) by A14,XREAL_1:31;
A16:    R_EAL(r-1/(m+1))< R_EAL r by A15,XREAL_1:21;
A17:    R_EAL(r-1/(m+1)) < f.x by A12,A16,XXREAL_0:2;
A18:    x in great_dom(f,R_EAL(r-1/(m+1))) by A11,A17,Def14;
        thus thesis by A4,A10,A18,XBOOLE_0:def 4;
      end;
      thus thesis by A7;
    end;
    thus thesis by A6,SETFAM_1:def 1;
  end;
A19: A /\ great_eq_dom(f,R_EAL r) c= meet rng F by A2,TARSKI:def 3;
A20: for x being set st x in meet rng F holds x in A /\ great_eq_dom(f,
  R_EAL r)
  proof
    let x be set;
    assume
A21: x in meet rng F;
A22: for m holds x in A & x in dom f & R_EAL(r-1/(m+1)) < f.x
    proof
      let m;
A23:  m in NAT;
A24:  m in dom F by A23,FUNCT_2:def 1;
A25:  F.m in rng F by A24,FUNCT_1:def 5;
A26:  x in F.m by A21,A25,SETFAM_1:def 1;
A27:  x in A /\ great_dom(f,R_EAL(r-1/(m+1))) by A1,A26;
A28:  x in great_dom(f,R_EAL(r-1/(m+1))) by A27,XBOOLE_0:def 4;
      thus thesis by A27,A28,Def14,XBOOLE_0:def 4;
    end;
    reconsider y=f.x as R_eal;
A29: 1 in NAT;
A30: 1 in dom F by A29,FUNCT_2:def 1;
A31: F.1 in rng F by A30,FUNCT_1:def 5;
A32: x in F.1 by A21,A31,SETFAM_1:def 1;
A33: x in A /\ great_dom(f,R_EAL(r-1/(1+1))) by A1,A32;
    reconsider x as Element of X by A33;
A34: R_EAL r <= y
    proof
A35:  now per cases;
        suppose
A36:      y=+infty;
          thus thesis by A36,XXREAL_0:4;
        end;
        suppose
A37:      not y=+infty;
A38:      not +infty <= y by A37,XXREAL_0:4;
A39:      R_EAL(r-1/(1+1))<y by A22;
          reconsider y1=y as Real by A38,A39,XXREAL_0:48;
A40:      for m holds r-1/(m+1) <= y1
          proof
            let m;
A41:        R_EAL(r-1/(m+1)) < R_EAL y1 by A22;
            thus thesis by A41;
          end;
          thus thesis by A40,Th14;
        end;
      end;
      thus thesis by A35;
    end;
A42: x in great_eq_dom(f,R_EAL r) by A22,A34,Def15;
    thus thesis by A22,A42,XBOOLE_0:def 4;
  end;
A43: meet rng F c= A /\ great_eq_dom(f,R_EAL r) by A20,TARSKI:def 3;
  thus thesis by A19,A43,XBOOLE_0:def 10;
end;

theorem Th24:
for r being real number st for n holds F.n = A /\ less_dom(f,R_EAL(r+1/(n+1)))
  holds A /\ less_eq_dom(f,R_EAL r) = meet rng F
proof
  let r be real number;
  assume
A1: for n holds F.n = A /\ less_dom(f,R_EAL(r+1/(n+1)));
  A2: for x being set st x in A /\ less_eq_dom(f,R_EAL r) holds x in meet rng F
  proof
    let x be set;
    assume
A3: x in A /\ less_eq_dom(f,R_EAL r);
A4: x in A by A3,XBOOLE_0:def 4;
A5: x in less_eq_dom(f,R_EAL r) by A3,XBOOLE_0:def 4;
A6: for Y being set holds Y in rng F implies x in Y
    proof
      let Y be set;
A7:   Y in rng F implies x in Y
      proof
        assume
A8:     Y in rng F;
        consider m being Element of NAT such that
        m in dom F and
A9:     Y = F.m by A8,PARTFUN1:26;
A10:    Y = A /\ less_dom(f,R_EAL(r+1/(m+1))) by A1,A9;
A11:    x in dom f by A5,Def13;
        reconsider x as Element of X by A3;
A12:    f.x <= R_EAL r by A5,Def13;
A13:    (m+1)" > 0;
A14:    1/(m+1) > 0 by A13,XCMPLX_1:217;
A15:    R_EAL r < R_EAL(r+1/(m+1)) by A14,XREAL_1:31;
A16:    f.x < R_EAL(r+1/(m+1)) by A12,A15,XXREAL_0:2;
A17:    x in less_dom(f,R_EAL(r+1/(m+1))) by A11,A16,Def12;
        thus thesis by A4,A10,A17,XBOOLE_0:def 4;
      end;
      thus thesis by A7;
    end;
    thus thesis by A6,SETFAM_1:def 1;
  end;
A18: A /\ less_eq_dom(f,R_EAL r) c= meet rng F by A2,TARSKI:def 3;
  A19: for
 x being set st x in meet rng F holds x in A /\ less_eq_dom(f,R_EAL r)
  proof
    let x be set;
    assume
A20: x in meet rng F;
A21: for m holds x in A & x in dom f & f.x < R_EAL(r+1/(m+1))
    proof
      let m;
A22:  m in NAT;
A23:  m in dom F by A22,FUNCT_2:def 1;
A24:  F.m in rng F by A23,FUNCT_1:def 5;
A25:  x in F.m by A20,A24,SETFAM_1:def 1;
A26:  x in A /\ less_dom(f,R_EAL(r+1/(m+1))) by A1,A25;
A27:  x in less_dom(f,R_EAL(r+1/(m+1))) by A26,XBOOLE_0:def 4;
      thus thesis by A26,A27,Def12,XBOOLE_0:def 4;
    end;
    reconsider y=f.x as R_eal;
A28: 1 in NAT;
A29: 1 in dom F by A28,FUNCT_2:def 1;
A30: F.1 in rng F by A29,FUNCT_1:def 5;
A31: x in F.1 by A20,A30,SETFAM_1:def 1;
A32: x in A /\ less_dom(f,R_EAL(r+1/(1+1))) by A1,A31;
    reconsider x as Element of X by A32;
A33: y <= R_EAL r
    proof
A34:  now per cases;
        suppose
A35:      y=-infty;
          thus thesis by A35,XXREAL_0:5;
        end;
        suppose
A36:      not y=-infty;
A37:      not y <= -infty by A36,XXREAL_0:6;
A38:      y < R_EAL(r+1/(1+1)) by A21;
          reconsider y1=y as Real by A37,A38,XXREAL_0:48;
A39:      for m holds y1-1/(m+1) <= r
          proof
            let m;
A40:        R_EAL y1 < R_EAL(r+1/(m+1)) by A21;
            thus thesis by A40,XREAL_1:22;
          end;
          thus thesis by A39,Th14;
        end;
      end;
      thus thesis by A34;
    end;
A41: x in less_eq_dom(f,R_EAL r) by A21,A33,Def13;
    thus thesis by A21,A41,XBOOLE_0:def 4;
  end;
A42: meet rng F c= A /\ less_eq_dom(f,R_EAL r) by A19,TARSKI:def 3;
  thus thesis by A18,A42,XBOOLE_0:def 10;
end;

theorem Th25:
  for r being real number st for n
  holds F.n = A /\ less_eq_dom(f,R_EAL(r-1/(n+1)))
  holds A /\ less_dom(f,R_EAL r) = union rng F
proof
  let r be real number;
A1: r in REAL by XREAL_0:def 1;
  assume
A2: for n holds F.n = A /\ less_eq_dom(f,R_EAL(r-1/(n+1)));
  A3: for x being set st x in A /\ less_dom(f,R_EAL r) holds x in union rng F
  proof
    let x be set;
    assume
A4: x in A /\ less_dom(f,R_EAL r);
A5: x in A by A4,XBOOLE_0:def 4;
A6: x in less_dom(f,R_EAL r) by A4,XBOOLE_0:def 4;
A7: ex Y being set st x in Y & Y in rng F
    proof
      reconsider x as Element of X by A4;
A8:   x in dom f by A6,Def12;
A9:   f.x < R_EAL r by A6,Def12;
A10:  ex m st f.x <= R_EAL(r-1/(m+1))
      proof
        per cases;
        suppose
A11:      f.x = -infty;
          take 1;
          thus thesis by A11,XXREAL_0:5;
        end;
        suppose
A12:      not f.x =-infty;
A13:      not f.x <= -infty by A12,XXREAL_0:6;
          reconsider y1=f.x as Real by A9,A13,XXREAL_0:48;
          consider m such that
A14:      1/(m+1) < r-y1 by A9,Th13;
A15:      y1+1/(m+1) < r by A14,XREAL_1:22;
A16:      f.x <= R_EAL(r-1/(m+1)) by A15,XREAL_1:22;
          thus thesis by A16;
        end;
      end;
      consider m such that
A17:  f.x <= R_EAL(r-1/(m+1)) by A10;
A18:  x in less_eq_dom(f,R_EAL(r-1/(m+1))) by A8,A17,Def13;
A19:  x in A /\ less_eq_dom(f,R_EAL(r-1/(m+1))) by A5,A18,
      XBOOLE_0:def 4;
A20:  m in NAT;
A21:  m in dom F by A20,FUNCT_2:def 1;
      take F.m;
      thus thesis by A2,A19,A21,FUNCT_1:def 5;
    end;
    thus thesis by A7,TARSKI:def 4;
  end;
A22: A /\ less_dom(f,R_EAL r) c= union rng F by A3,TARSKI:def 3;
  A23: for x being set st x in union rng F holds x in A /\ less_dom(f,R_EAL r )
  proof
    let x be set;
    assume
A24: x in union rng F;
    consider Y being set such that
A25: x in Y and
A26: Y in rng F by A24,TARSKI:def 4;
    consider m such that
    m in dom F and
A27: F.m = Y by A26,PARTFUN1:26;
A28: x in A /\ less_eq_dom(f,R_EAL(r-1/(m+1))) by A2,A25,A27;
A29: x in A by A28,XBOOLE_0:def 4;
A30: x in less_eq_dom(f,R_EAL(r-1/(m+1))) by A28,XBOOLE_0:def 4;
A31: x in dom f by A30,Def13;
A32: f.x <= R_EAL(r-1/(m+1)) by A30,Def13;
    reconsider x as Element of X by A25,A26;
A33: f.x < R_EAL r
    proof
A34:  now 
A36:      r < r+1/(m+1) by XREAL_1:31,141;
A37:      r-1/(m+1) < r by A36,XREAL_1:21;
          thus thesis by A32,A37,XXREAL_0:2;
      end;
      thus thesis by A34;
    end;
A38: x in less_dom(f,R_EAL r) by A31,A33,Def12;
    thus thesis by A29,A38,XBOOLE_0:def 4;
  end;
A39: union rng F c= A /\ less_dom(f,R_EAL r) by A23,TARSKI:def 3;
  thus thesis by A22,A39,XBOOLE_0:def 10;
end;

theorem Th26:
  for X, S, F, f, A, r st for n holds F.n = A /\
  great_eq_dom(f,R_EAL(r+1/(n+1)))
  holds A /\ great_dom(f,R_EAL r) = union rng F
proof
  let X,S,F,f,A,r;
  assume
A1: for n holds F.n = A /\ great_eq_dom(f,R_EAL(r+1/(n+1)));
  A2: for x being set st x in A /\ great_dom(f,R_EAL r) holds x in union rng F
  proof
    let x be set;
    assume
A3: x in A /\ great_dom(f,R_EAL r);
A4: x in A by A3,XBOOLE_0:def 4;
A5: x in great_dom(f,R_EAL r) by A3,XBOOLE_0:def 4;
A6: ex Y being set st x in Y & Y in rng F
    proof
      reconsider x as Element of X by A3;
A7:   x in dom f by A5,Def14;
A8:   R_EAL r < f.x by A5,Def14;
A9:   ex m st R_EAL(r+1/(m+1)) <= f.x
      proof
        per cases;
        suppose
A10:      f.x = +infty;
          take 1;
          thus thesis by A10,XXREAL_0:4;
        end;
        suppose
A11:      not f.x=+infty;
A12:      not +infty <= f.x by A11,XXREAL_0:4;
          reconsider y1=f.x as Real by A8,A12,XXREAL_0:48;
          consider m such that
A13:      1/(m+1) < y1-r by A8,Th13;
          take m;
          thus thesis by A13,XREAL_1:22;
        end;
      end;
      consider m such that
A14:  R_EAL(r+1/(m+1)) <= f.x by A9;
A15:  x in great_eq_dom(f,R_EAL(r+1/(m+1))) by A7,A14,Def15;
A16:  x in A /\ great_eq_dom(f,R_EAL(r+1/(m+1))) by A4,A15,
      XBOOLE_0:def 4;
A17:  m in NAT;
A18:  m in dom F by A17,FUNCT_2:def 1;
      take F.m;
      thus thesis by A1,A16,A18,FUNCT_1:def 5;
    end;
    thus thesis by A6,TARSKI:def 4;
  end;
A19: A /\ great_dom(f,R_EAL r) c= union rng F by A2,TARSKI:def 3;
  A20: for
 x being set st x in union rng F holds x in A /\ great_dom(f,R_EAL r )
  proof
    let x be set;
    assume
A21: x in union rng F;
    consider Y being set such that
A22: x in Y and
A23: Y in rng F by A21,TARSKI:def 4;
    consider m such that
    m in dom F and
A24: F.m = Y by A23,PARTFUN1:26;
A25: x in A /\ great_eq_dom(f,R_EAL(r+1/(m+1))) by A1,A22,A24;
A26: x in A by A25,XBOOLE_0:def 4;
A27: x in great_eq_dom(f,R_EAL(r+1/(m+1))) by A25,XBOOLE_0:def 4;
A28: x in dom f by A27,Def15;
A29: R_EAL(r+1/(m+1)) <= f.x by A27,Def15;
    reconsider x as Element of X by A22,A23;
A30: R_EAL r < f.x
    proof
A31:  now 
A33:      r < r+1/(m+1) by XREAL_1:31,141;
          thus thesis by A29,A33,XXREAL_0:2;
      end;
      thus thesis by A31;
    end;
A34: x in great_dom(f,R_EAL r) by A28,A30,Def14;
    thus thesis by A26,A34,XBOOLE_0:def 4;
  end;
A35: union rng F c= A /\ great_dom(f,R_EAL r) by A20,TARSKI:def 3;
  thus thesis by A19,A35,XBOOLE_0:def 10;
end;

theorem Th27:
  for X, S, F, f, A st for n holds F.n = A /\ great_dom(f,R_EAL n)
  holds A /\ eq_dom(f,+infty) = meet rng F
proof
  let X,S,F,f,A;
  assume
A1: for n holds F.n = A /\ great_dom(f,R_EAL n);
A2: for x being set st x in A /\ eq_dom(f,+infty) holds x in meet rng F
  proof
    let x being set;
    assume
A3: x in A /\ eq_dom(f,+infty);
A4: x in A by A3,XBOOLE_0:def 4;
A5: x in eq_dom(f,+infty) by A3,XBOOLE_0:def 4;
A6: for Y being set holds Y in rng F implies x in Y
    proof
      let Y be set;
A7:   Y in rng F implies x in Y
      proof
        assume
A8:     Y in rng F;
        consider m being Element of NAT such that
        m in dom F and
A9:     Y = F.m by A8,PARTFUN1:26;
A10:    Y = A /\ great_dom(f,R_EAL m) by A1,A9;
        reconsider x as Element of X by A3;
A11:    f.x = +infty by A5,Def16;
A12:    x in dom f & not +infty <= R_EAL m by A5,Def16,XXREAL_0:9;
A13:    x in great_dom(f,R_EAL m) by A11,A12,Def14;
        thus thesis by A4,A10,A13,XBOOLE_0:def 4;
      end;
      thus thesis by A7;
    end;
    thus thesis by A6,SETFAM_1:def 1;
  end;
A14: A /\ eq_dom(f,+infty) c= meet rng F by A2,TARSKI:def 3;
A15: for x being set st x in meet rng F holds x in A /\ eq_dom(f,+infty)
  proof
    let x be set;
    assume
A16: x in meet rng F;
A17: for m holds x in A & x in dom f & ex y being R_eal st y=f.x & y = +infty
    proof
      let m;
A18:  m in NAT;
A19:  m in dom F by A18,FUNCT_2:def 1;
A20:  F.m in rng F by A19,FUNCT_1:def 5;
A21:  x in F.m by A16,A20,SETFAM_1:def 1;
A22:  x in A /\ great_dom(f,R_EAL m) by A1,A21;
A23:  x in great_dom(f,R_EAL m) by A22,XBOOLE_0:def 4;
A24:  for r holds R_EAL r < f.x
      proof
        let r;
        consider n such that
A25:    r <= n by Th11;
A26:    n in NAT;
A27:    n in dom F by A26,FUNCT_2:def 1;
A28:    F.n in rng F by A27,FUNCT_1:def 5;
A29:    x in F.n by A16,A28,SETFAM_1:def 1;
A30:    x in A /\ great_dom(f,R_EAL n) by A1,A29;
A31:    x in great_dom(f,R_EAL n) by A30,XBOOLE_0:def 4;
A32:    R_EAL n < f.x by A31,Def14;
        thus thesis by A25,A32,XXREAL_0:2;
      end;
A33:  f.x = +infty by A24,Th15;
      thus thesis by A22,A23,A33,Def14,XBOOLE_0:def 4;
    end;
A34: 1 in NAT;
A35: 1 in dom F by A34,FUNCT_2:def 1;
A36: F.1 in rng F by A35,FUNCT_1:def 5;
A37: x in F.1 by A16,A36,SETFAM_1:def 1;
A38: x in A /\ great_dom(f,R_EAL 1) by A1,A37;
    reconsider x as Element of X by A38;
A39: x in eq_dom(f,+infty) by A17,Def16;
    thus thesis by A17,A39,XBOOLE_0:def 4;
  end;
A40: meet rng F c= A /\ eq_dom(f,+infty) by A15,TARSKI:def 3;
  thus thesis by A14,A40,XBOOLE_0:def 10;
end;

theorem Th28:
  for X, S, F, f, A st for n holds F.n = A /\ less_dom(f,R_EAL n)
  holds A /\ less_dom(f,+infty) = union rng F
proof
  let X,S,F,f,A;
  assume
A1: for n holds F.n = A /\ less_dom(f,R_EAL n);
  A2: for x being set st x in A /\ less_dom(f,+infty) holds x in union rng F
  proof
    let x be set;
    assume
A3: x in A /\ less_dom(f,+infty);
A4: x in A by A3,XBOOLE_0:def 4;
A5: x in less_dom(f,+infty) by A3,XBOOLE_0:def 4;
A6: x in dom f by A5,Def12;
A7: f.x < +infty by A5,Def12;
A8: ex n being Element of NAT st f.x < R_EAL n
    proof
      per cases;
      suppose
A9:     f.x = -infty;
        take 0;
        thus thesis by A9;
      end;
      suppose
A10:    not f.x = -infty;
A11:    not f.x <= -infty by A10,XXREAL_0:6;
        reconsider y1=f.x as Real by A7,A11,XXREAL_0:48;
        consider n1 being Element of NAT such that
A12:    y1 <= n1 by Th11;
A13:    n1 < n1+1 by NAT_1:13;
        reconsider m=n1+1 as Element of NAT;
        take m;
        thus thesis by A12,A13,XXREAL_0:2;
      end;
    end;
    consider n being Element of NAT such that
A14: f.x < R_EAL n by A8;
    reconsider x as Element of X by A3;
A15: x in less_dom(f,R_EAL n) by A6,A14,Def12;
A16: x in A /\ less_dom(f,R_EAL n) by A4,A15,XBOOLE_0:def 4;
A17: x in F.n by A1,A16;
A18: n in NAT;
A19: n in dom F by A18,FUNCT_2:def 1;
A20: F.n in rng F by A19,FUNCT_1:def 5;
    thus thesis by A17,A20,TARSKI:def 4;
  end;
A21: A /\ less_dom(f,+infty) c= union rng F by A2,TARSKI:def 3;
  A22: for x being set st x in union rng F holds x in A /\ less_dom(f,+infty)
  proof
    let x being set;
    assume
A23: x in union rng F;
    consider Y being set such that
A24: x in Y and
A25: Y in rng F by A23,TARSKI:def 4;
    consider m such that
    m in dom F and
A26: F.m = Y by A25,PARTFUN1:26;
A27: x in A /\ less_dom(f,R_EAL m) by A1,A24,A26;
A28: x in A by A27,XBOOLE_0:def 4;
A29: x in less_dom(f,R_EAL m) by A27,XBOOLE_0:def 4;
A30: x in dom f by A29,Def12;
A31: f.x < R_EAL m by A29,Def12;
    reconsider x as Element of X by A24,A25;
A32: f.x < +infty by A31,XXREAL_0:2,9;
A33: x in less_dom(f,+infty) by A30,A32,Def12;
    thus thesis by A28,A33,XBOOLE_0:def 4;
  end;
A34: union rng F c= A /\ less_dom(f,+infty) by A22,TARSKI:def 3;
  thus thesis by A21,A34,XBOOLE_0:def 10;
end;

theorem Th29:
  for X, S, F, f, A st for n holds F.n = A /\ less_dom(f,R_EAL (-n))
  holds A /\ eq_dom(f,-infty) = meet rng F
proof
  let X,S,F,f,A;
  assume
A1: for n holds F.n = A /\ less_dom(f,R_EAL (-n));
A2: for x being set st x in A /\ eq_dom(f,-infty) holds x in meet rng F
  proof
    let x being set;
    assume
A3: x in A /\ eq_dom(f,-infty);
A4: x in A by A3,XBOOLE_0:def 4;
A5: x in eq_dom(f,-infty) by A3,XBOOLE_0:def 4;
A6: for Y being set holds Y in rng F implies x in Y
    proof
      let Y be set;
A7:   Y in rng F implies x in Y
      proof
        assume
A8:     Y in rng F;
        consider m being Element of NAT such that
        m in dom F and
A9:     Y = F.m by A8,PARTFUN1:26;
A10:    Y = A /\ less_dom(f,R_EAL (-m)) by A1,A9;
        reconsider x as Element of X by A3;
A11:    f.x = -infty by A5,Def16;
        A12:    x in dom f & not R_EAL (-m) <= -infty by A5,Def16,XXREAL_0:12;
A13:    x in less_dom(f,R_EAL (-m)) by A11,A12,Def12;
        thus thesis by A4,A10,A13,XBOOLE_0:def 4;
      end;
      thus thesis by A7;
    end;
    thus thesis by A6,SETFAM_1:def 1;
  end;
A14: A /\ eq_dom(f,-infty) c= meet rng F by A2,TARSKI:def 3;
A15: for x being set st x in meet rng F holds x in A /\ eq_dom(f,-infty)
  proof
    let x be set;
    assume
A16: x in meet rng F;
A17: for m holds x in A & x in dom f & ex y being R_eal st y=f.x & y = -infty
    proof
      let m;
A18:  m in NAT;
A19:  m in dom F by A18,FUNCT_2:def 1;
A20:  F.m in rng F by A19,FUNCT_1:def 5;
A21:  x in F.m by A16,A20,SETFAM_1:def 1;
A22:  x in A /\ less_dom(f,R_EAL (-m)) by A1,A21;
A23:  x in less_dom(f,R_EAL (-m)) by A22,XBOOLE_0:def 4;
A24:  for r holds f.x < R_EAL r
      proof
        let r;
        consider n such that
A25:    -n <= r by Th12;
A26:    n in NAT;
A27:    n in dom F by A26,FUNCT_2:def 1;
A28:    F.n in rng F by A27,FUNCT_1:def 5;
A29:    x in F.n by A16,A28,SETFAM_1:def 1;
A30:    x in A /\ less_dom(f,R_EAL (-n)) by A1,A29;
A31:    x in less_dom(f,R_EAL (-n)) by A30,XBOOLE_0:def 4;
A32:    f.x < R_EAL (-n) by A31,Def12;
        thus thesis by A25,A32,XXREAL_0:2;
      end;
A33:  f.x = -infty by A24,Th16;
      thus thesis by A22,A23,A33,Def12,XBOOLE_0:def 4;
    end;
A34: 1 in NAT;
A35: 1 in dom F by A34,FUNCT_2:def 1;
A36: F.1 in rng F by A35,FUNCT_1:def 5;
A37: x in F.1 by A16,A36,SETFAM_1:def 1;
A38: x in A /\ less_dom(f,R_EAL (-1)) by A1,A37;
    reconsider x as Element of X by A38;
A39: x in eq_dom(f,-infty) by A17,Def16;
    thus thesis by A17,A39,XBOOLE_0:def 4;
  end;
A40: meet rng F c= A /\ eq_dom(f,-infty) by A15,TARSKI:def 3;
  thus thesis by A14,A40,XBOOLE_0:def 10;
end;

theorem Th30:
  for X, S, F, f, A st for n holds F.n = A /\ great_dom(f,R_EAL (-n))
  holds A /\ great_dom(f,-infty) = union rng F
proof
  let X,S,F,f,A;
  assume
A1: for n holds F.n = A /\ great_dom(f,R_EAL (-n));
  A2: for x being set st x in A /\ great_dom(f,-infty) holds x in union rng F
  proof
    let x be set;
    assume
A3: x in A /\ great_dom(f,-infty);
A4: x in A by A3,XBOOLE_0:def 4;
A5: x in great_dom(f,-infty) by A3,XBOOLE_0:def 4;
A6: x in dom f by A5,Def14;
A7: -infty < f.x by A5,Def14;
A8: ex n being Element of NAT st R_EAL (-n) < f.x
    proof
      per cases;
      suppose
A9:     f.x = +infty;
        take 0;
        thus thesis by A9;
      end;
      suppose
A10:    not f.x = +infty;
A11:    not +infty <= f.x by A10,XXREAL_0:4;
        reconsider y1=f.x as Real by A7,A11,XXREAL_0:48;
        consider n1 being Element of NAT such that
A12:    -n1 <= y1 by Th12;
A13:    n1 < n1+1 by NAT_1:13;
A14:    -(n1+1) < -n1 by A13,XREAL_1:26;
        reconsider m=n1+1 as Element of NAT;
        take m;
        thus thesis by A12,A14,XXREAL_0:2;
      end;
    end;
    consider n being Element of NAT such that
A15: R_EAL (-n) < f.x by A8;
    reconsider x as Element of X by A3;
A16: x in great_dom(f,R_EAL (-n)) by A6,A15,Def14;
A17: x in A /\ great_dom(f,R_EAL (-n)) by A4,A16,XBOOLE_0:def 4;
A18: x in F.n by A1,A17;
A19: n in NAT;
A20: n in dom F by A19,FUNCT_2:def 1;
A21: F.n in rng F by A20,FUNCT_1:def 5;
    thus thesis by A18,A21,TARSKI:def 4;
  end;
A22: A /\ great_dom(f,-infty) c= union rng F by A2,TARSKI:def 3;
  A23: for x being set st x in union rng F holds x in A /\ great_dom(f,-infty )
  proof
    let x being set;
    assume
A24: x in union rng F;
    consider Y being set such that
A25: x in Y and
A26: Y in rng F by A24,TARSKI:def 4;
    consider m such that
    m in dom F and
A27: F.m = Y by A26,PARTFUN1:26;
A28: x in A /\ great_dom(f,R_EAL (-m)) by A1,A25,A27;
A29: x in A by A28,XBOOLE_0:def 4;
A30: x in great_dom(f,R_EAL (-m)) by A28,XBOOLE_0:def 4;
A31: x in dom f by A30,Def14;
A32: R_EAL (-m) < f.x by A30,Def14;
    reconsider x as Element of X by A25,A26;
A33: -infty < f.x by A32,XXREAL_0:2,12;
A34: x in great_dom(f,-infty) by A31,A33,Def14;
    thus thesis by A29,A34,XBOOLE_0:def 4;
  end;
A35: union rng F c= A /\ great_dom(f,-infty) by A23,TARSKI:def 3;
  thus thesis by A22,A35,XBOOLE_0:def 10;
end;

begin :: Measurable functions

definition
  let X be non empty set;
  let S be SigmaField of X;
  let f be PartFunc of X,ExtREAL;
  let A be Element of S;
  pred f is_measurable_on A means
  :Def17:
  for r being real number holds A /\ less_dom(f,R_EAL r) in S;
end;

reserve X for non empty set;
reserve x for Element of X;
reserve f,g for PartFunc of X,ExtREAL;
reserve S for SigmaField of X;
reserve A,B for Element of S;

theorem
  for X,S,f,A st A c= dom f holds f is_measurable_on A iff
  for r being real number holds A /\ great_eq_dom(f,R_EAL r) in S
proof
  let X,S,f,A;
  assume
A1: A c= dom f;
A2: f is_measurable_on A implies for r being real number holds A /\
  great_eq_dom(f,R_EAL r) in S
  proof
    assume
A3: f is_measurable_on A;
A4: for r being real number
    holds A /\ great_eq_dom(f,R_EAL r) in S
    proof
      let r be real number;
      A5:   A
 /\ less_dom(f,R_EAL r) in S & A /\ great_eq_dom(f,R_EAL r) = A\(A /\
      less_dom(f,R_EAL r)) by A1,A3,Def17,Th18;
      thus thesis by A5,MEASURE1:20;
    end;
    thus thesis by A4;
  end;
A6: (for r being real number
  holds A /\ great_eq_dom(f,R_EAL r) in S) implies
  f is_measurable_on A
  proof
    assume
A7: for r being real number
    holds A /\ great_eq_dom(f,R_EAL r) in S;
A8: for r being real number holds A /\ less_dom(f,R_EAL r) in S
    proof
      let r be real number;
      A9:   A
 /\ great_eq_dom(f,R_EAL r) in S & A /\ less_dom(f,R_EAL r) = A\(A /\
      great_eq_dom(f,R_EAL r)) by A1,A7,Th21;
      thus thesis by A9,MEASURE1:20;
    end;
    thus thesis by A8,Def17;
  end;
  thus thesis by A2,A6;
end;

theorem Th32:
  for X,S,f,A holds f is_measurable_on A iff
  for r being real number holds A /\ less_eq_dom(f,R_EAL r) in S
proof
  let X,S,f,A;
A1: f is_measurable_on A implies for r being real number
  holds A /\ less_eq_dom(f,R_EAL r) in S
  proof
    assume
A2: f is_measurable_on A;
A3: for r being real number holds A /\
    less_eq_dom(f,R_EAL r) in S
    proof
      let r be real number;
      defpred P[Element of NAT,set] means
      A /\ less_dom(f,R_EAL(r+1/($1+1))) = $2;
A4:   for n ex y being Element of S st P[n,y]
      proof
        let n;
        reconsider y=A /\ less_dom(f,R_EAL(r+1/(n+1))) as Element of S
        by A2,Def17;
        take y;
        thus thesis;
      end;
      consider F being Function of NAT,S such that
A5:   for n holds P[n,F.n] from FUNCT_2:sch 3(A4);
A6:   A /\ less_eq_dom(f,R_EAL r) = meet rng F by A5,Th24;
      thus thesis by A6;
    end;
    thus thesis by A3;
  end;
A7: (for r being real number
  holds A /\ less_eq_dom(f,R_EAL r) in S) implies
  f is_measurable_on A
  proof
    assume
A8: for r being real number holds A /\
    less_eq_dom(f,R_EAL r) in S;
A9: for r being real number holds A /\ less_dom(f,R_EAL r) in S
    proof
      let r be real number;
      defpred P[Element of NAT,set] means
      A /\ less_eq_dom(f,R_EAL(r-1/($1+1))) = $2;
A10:  for n ex y being Element of S st P[n,y]
      proof
        let n;
        reconsider y=A /\ less_eq_dom(f,R_EAL(r-1/(n+1))) as Element of S
        by A8;
        take y;
        thus thesis;
      end;
      consider F being Function of NAT,S such that
A11:  for n holds P[n,F.n] from FUNCT_2:sch 3(A10);
A12:  A /\ less_dom(f,R_EAL r) = union rng F by A11,Th25;
      thus thesis by A12;
    end;
    thus thesis by A9,Def17;
  end;
  thus thesis by A1,A7;
end;

theorem Th33:
  for X,S,f,A st A c= dom f holds f is_measurable_on A iff
  for r being real number holds A /\ great_dom(f,R_EAL r) in S
proof
  let X,S,f,A;
  assume
A1: A c= dom f;
A2: f is_measurable_on A implies
  for r being real number holds A /\ great_dom(f,R_EAL r) in S
  proof
    assume
A3: f is_measurable_on A;
A4: for r being real number holds A /\ great_dom(f,R_EAL r) in S
    proof
      let r be real number;
      A5:   A
 /\ less_eq_dom(f,R_EAL r) in S & A /\ great_dom(f,R_EAL r) = A\(A /\
      less_eq_dom(f,R_EAL r)) by A1,A3,Th19,Th32;
      thus thesis by A5,MEASURE1:20;
    end;
    thus thesis by A4;
  end;
A6: (for r being real number holds A /\ great_dom(f,R_EAL r) in S)
  implies f is_measurable_on A
  proof
    assume
A7: for r being real number holds A /\
    great_dom(f,R_EAL r) in S;
A8: for r being real number
    holds A /\ less_eq_dom(f,R_EAL r) in S
    proof
      let r be real number;
      A9:   A
 /\ great_dom(f,R_EAL r) in S & A /\ less_eq_dom(f,R_EAL r) = A\(A /\
      great_dom(f,R_EAL r)) by A1,A7,Th20;
      thus thesis by A9,MEASURE1:20;
    end;
    thus thesis by A8,Th32;
  end;
  thus thesis by A2,A6;
end;

theorem
  for X,S,f,A,B st B c= A & f is_measurable_on A holds f is_measurable_on B
proof
  let X,S,f,A,B;
  assume that
A1: B c= A and
A2: f is_measurable_on A;
A3: for r being real number holds (B /\ less_dom(f,R_EAL r)) in S
  proof
    let r be real number;
A4: A /\ less_dom(f,R_EAL r) in S by A2,Def17;
A5: B /\ (A /\ less_dom(f,R_EAL r)) = B /\ A /\ less_dom(f,R_EAL r) by
    XBOOLE_1:16
      .= B /\ less_dom(f,R_EAL r) by A1,XBOOLE_1:28;
    thus thesis by A4,A5,FINSUB_1:def 2;
  end;
  thus thesis by A3,Def17;
end;

theorem
  for X,S,f,A,B st f is_measurable_on A & f is_measurable_on B holds
  f is_measurable_on (A \/ B)
proof
  let X,S,f,A,B;
  assume
A1: f is_measurable_on A & f is_measurable_on B;
A2: for r being real number
  holds ((A \/ B) /\ less_dom(f,R_EAL r)) in S
  proof
    let r be real number;
    A3: A
 /\ less_dom(f,R_EAL r) in S & B /\ less_dom(f,R_EAL r) in S by A1,Def17;
A4: (A /\ less_dom(f,R_EAL r)) \/ (B /\ less_dom(f,R_EAL r)) in S
    by A3,FINSUB_1:def 1;
    thus thesis by A4,XBOOLE_1:23;
  end;
  thus thesis by A2,Def17;
end;

theorem
  for X,S,f,A,r,s st f is_measurable_on A & A c= dom f holds
  (A /\ great_dom(f,R_EAL r) /\ less_dom(f,R_EAL s)) in S
proof
  let X,S,f,A,r,s;
  assume that
A1: f is_measurable_on A and
A2: A c= dom f;
A3: A /\ less_dom(f,R_EAL s) in S by A1,Def17;
A4: for r1 being Real holds A /\ great_eq_dom(f,R_EAL r1) in S
  proof
    let r1 be Real;
    A5: A
 /\ less_dom(f,R_EAL r1) in S & A /\ great_eq_dom(f,R_EAL r1) = A\(A /\
    less_dom(f,R_EAL r1)) by A1,A2,Def17,Th18;
    thus thesis by A5,MEASURE1:20;
  end;
A6: for r1 being Real holds A /\ great_dom(f,R_EAL r1) in S
  proof
    let r1 being Real;
    defpred P[Element of NAT,set] means
    A /\ great_eq_dom(f,R_EAL(r1+1/($1+1))) = $2;
A7: for n ex y being Element of S st P[n,y]
    proof
      let n;
      reconsider y=A /\ great_eq_dom(f,R_EAL(r1+1/(n+1))) as Element of S
      by A4;
      take y;
      thus thesis;
    end;
    consider F being Function of NAT,S such that
A8: for n holds P[n,F.n] from FUNCT_2:sch 3(A7);
A9: A /\ great_dom(f,R_EAL r1) = union rng F by A8,Th26;
    thus thesis by A9;
  end;
A10: A /\ great_dom(f,R_EAL r) in S by A6;
A11: (A /\ great_dom(f,R_EAL r))/\(A /\ less_dom(f,R_EAL s))
  = (A /\ great_dom(f,R_EAL r) /\ A) /\ less_dom(f,R_EAL s) by XBOOLE_1:16
    .= (great_dom(f,R_EAL r) /\ (A /\ A)) /\
  less_dom(f,R_EAL s) by XBOOLE_1:16;
  thus thesis by A3,A10,A11,FINSUB_1:def 2;
end;

theorem
  for X,S,f,A st f is_measurable_on A & A c= dom f holds
  A /\ eq_dom(f,+infty) in S
proof
  let X,S,f,A;
  assume
A1: f is_measurable_on A & A c= dom f;
  defpred P[Element of NAT,set]means A /\ great_dom(f,R_EAL $1) = $2;
A2: for n ex y being Element of S st P[n,y]
  proof
    let n;
    reconsider y=A /\ great_dom(f,R_EAL n) as Element of S by A1,Th33;
    take y;
    thus thesis;
  end;
  consider F being Function of NAT,S such that
A3: for n holds P[n,F.n] from FUNCT_2:sch 3(A2);
A4: A /\ eq_dom(f,+infty) = meet rng F by A3,Th27;
  thus thesis by A4;
end;

theorem
  for X,S,f,A st f is_measurable_on A holds
  A /\ eq_dom(f,-infty) in S
proof
  let X,S,f,A;
  assume
A1: f is_measurable_on A;
  defpred P[Element of NAT,set] means A /\ less_dom(f,R_EAL (-$1)) = $2;
A2: for n ex y being Element of S st P[n,y]
  proof
    let n;
    reconsider y=A /\ less_dom(f,R_EAL (-n)) as Element of S by A1,Def17;
    take y;
    thus thesis;
  end;
  consider F being Function of NAT,S such that
A3: for n holds P[n,F.n] from FUNCT_2:sch 3(A2);
A4: A /\ eq_dom(f,-infty) = meet rng F by A3,Th29;
  thus thesis by A4;
end;

theorem
  for X,S,f,A st f is_measurable_on A & A c= dom f holds
  A /\ great_dom(f,-infty) /\ less_dom(f,+infty) in S
proof
  let X,S,f,A;
  assume that
A1: f is_measurable_on A and
A2: A c= dom f;
A3: A /\ great_dom(f,-infty) in S
  proof
    defpred P[Element of NAT,set] means A /\ great_dom(f,R_EAL (-$1)) = $2;
A4: for n ex y being Element of S st P[n,y]
    proof
      let n;
      reconsider y=A /\ great_dom(f,R_EAL (-n)) as Element of S by A1,A2,Th33;
      take y;
      thus thesis;
    end;
    consider F being Function of NAT,S such that
A5: for n holds P[n,F.n] from FUNCT_2:sch 3(A4);
A6: A /\ great_dom(f,-infty) = union rng F by A5,Th30;
    thus thesis by A6;
  end;
A7: A /\ less_dom(f,+infty) in S
  proof
    defpred P[Element of NAT,set] means A /\ less_dom(f,R_EAL $1) = $2;
A8: for n ex y being Element of S st P[n,y]
    proof
      let n;
      reconsider y=A /\ less_dom(f,R_EAL n) as Element of S by A1,Def17;
      take y;
      thus thesis;
    end;
    consider F being Function of NAT,S such that
A9: for n holds P[n,F.n] from FUNCT_2:sch 3(A8);
A10: A /\ less_dom(f,+infty) = union rng F by A9,Th28;
    thus thesis by A10;
  end;
A11: (A /\ great_dom(f,-infty)) /\ (A /\ less_dom(f,+infty))
  = (A /\ great_dom(f,-infty) /\ A) /\ less_dom(f,+infty) by XBOOLE_1:16
    .= (great_dom(f,-infty) /\ (A /\ A)) /\ less_dom(f,+infty) by XBOOLE_1:16;
  thus thesis by A3,A7,A11,FINSUB_1:def 2;
end;

theorem
  for X,S,f,g,A,r st f is_measurable_on A & g is_measurable_on A & A c= dom g
  holds A /\ less_dom(f,R_EAL r) /\ great_dom(g,R_EAL r) in S
proof
  let X,S,f,g,A,r;
  assume
A1: f is_measurable_on A & g is_measurable_on A & A c= dom g;
  A2: A
 /\ less_dom(f,R_EAL r) in S & A /\ great_dom(g,R_EAL r) in S by A1,Def17,
  Th33;
A3: (A /\ less_dom(f,R_EAL r)) /\ (A /\ great_dom(g,R_EAL r))
  =((A /\ less_dom(f,R_EAL r)) /\ A) /\ great_dom(g,R_EAL r) by XBOOLE_1:16
    .=((A /\ A) /\ less_dom(f,R_EAL r)) /\ great_dom(g,R_EAL r) by XBOOLE_1:16
    .=A /\ less_dom(f,R_EAL r) /\ great_dom(g,R_EAL r);
  thus thesis by A2,A3,FINSUB_1:def 2;
end;

theorem
  for X,S,f,A,r st f is_measurable_on A & A c= dom f
  holds r(#)f is_measurable_on A
proof
  let X,S,f,A,r;
  assume that
A1: f is_measurable_on A and
A2: A c= dom f;
A3: for r1 being real number holds A /\
  less_dom(r(#)f,R_EAL r1) in S
  proof
    let r1 being real number;
A4: now per cases;
      suppose
A5:     r<>0;
A6:     r1 in REAL by XREAL_0:def 1;
        reconsider r0=r1/r as Real;
A7:     r1=r*r0 by A5,XCMPLX_1:88;
A8:     now per cases by A5;
          suppose
A9:         r > 0;
A10:        for x being Element of X st x in less_dom(f,R_EAL r0)
            holds x in less_dom(r(#)f,R_EAL r1)
            proof
              let x be Element of X;
              assume
A11:          x in less_dom(f,R_EAL r0);
A12:          x in dom f by A11,Def12;
A13:          x in dom(r(#)f) by A12,Def6;
A14:          f.x < R_EAL r0 by A11,Def12;
A15:          r1 in REAL by XREAL_0:def 1;
A16:          f.x < R_EAL r1 / R_EAL r by A14,A15,EXTREAL1:39;
A17:          f.x * R_EAL r < R_EAL r1 / R_EAL r * R_EAL
              r by A9,A16,XXREAL_3:83;
A18:          R_EAL r1 / R_EAL r = r1 / r by A15,EXTREAL1:32;
A19:          R_EAL r1 / R_EAL r * R_EAL r = (r1/r)*r by A18,
              EXTREAL1:13
                .= r1/(r/r) by XCMPLX_1:82
                .= r1/1 by A5,XCMPLX_1:60
                .= r1;
A20:          (r(#)f).x = R_EAL r * f.x by A13,Def6;
              thus thesis by A13,A17,A19,A20,Def12;
            end;
            A21:        less_dom
(f,R_EAL r0) c= less_dom(r(#)f,R_EAL r1) by A10,SUBSET_1:7;
A22:        for x being Element of X st x in less_dom(r(#)f,R_EAL r1)
            holds x in less_dom(f,R_EAL r0)
            proof
              let x being Element of X;
              assume
A23:          x in less_dom(r(#)f,R_EAL r1);
A24:          x in dom(r(#)f) by A23,Def12;
A25:          (r(#)f).x < R_EAL r1 by A23,Def12;
A26:          (r(#)f).x < R_EAL r * R_EAL r0 by A7,A25,EXTREAL1:38;
              A27:          (r(#)f).x / R_EAL r < R_EAL r * R_EAL r0 / R_EAL r
              by A9,A26,XXREAL_3:91;
A28:          R_EAL r * R_EAL r0 = r * r0 by EXTREAL1:13;
A29:          R_EAL r * R_EAL r0 / R_EAL r = (r*r0)/r by A28,
              EXTREAL1:32
                .= r0/(r/r) by XCMPLX_1:78
                .= r0/1 by A5,XCMPLX_1:60
                .= r0;
              A30:          x
 in dom f & f.x = (r(#)f).x / R_EAL r by A5,A24,Def6,Th6;
              thus thesis by A27,A29,A30,Def12;
            end;
A31:        less_dom(r(#)f,R_EAL r1) c= less_dom(f,R_EAL r0) by A22,SUBSET_1:7;
A32:        less_dom
(f,R_EAL r0) = less_dom(r(#)f,R_EAL r1) by A21,A31,XBOOLE_0:def 10;
            thus thesis by A1,A32,Def17;
          end;
          suppose
A33:        r < 0;
A34:        for x being Element of X st x in great_dom(f,R_EAL r0)
            holds x in less_dom(r(#)f,R_EAL r1)
            proof
              let x be Element of X;
              assume
A35:          x in great_dom(f,R_EAL r0);
A36:          x in dom f by A35,Def14;
A37:          x in dom(r(#)f) by A36,Def6;
A38:          R_EAL r0 < f.x by A35,Def14;
A39:          R_EAL r1 / R_EAL r < f.x by A6,A38,EXTREAL1:39;
A40:          f
              .x * R_EAL r < R_EAL r1 / R_EAL r * R_EAL r by A33,A39,
              XXREAL_3:114;
A41:          R_EAL r1 / R_EAL r = r1 / r by A6,EXTREAL1:32;
A42:          R_EAL r1 / R_EAL r * R_EAL r = (r1/r)*r by A41,
              EXTREAL1:13
                .= r1/(r/r) by XCMPLX_1:82
                .= r1/1 by A5,XCMPLX_1:60
                .= r1;
A43:          (r(#)f).x = R_EAL r * f.x by A37,Def6;
              thus thesis by A37,A40,A42,A43,Def12;
            end;
            A44:        great_dom
(f,R_EAL r0) c= less_dom(r(#)f,R_EAL r1) by A34,SUBSET_1:7;
A45:        for x being Element of X st x in less_dom(r(#)f,R_EAL r1)
            holds x in great_dom(f,R_EAL r0)
            proof
              let x being Element of X;
              assume
A46:          x in less_dom(r(#)f,R_EAL r1);
A47:          x in dom(r(#)f) by A46,Def12;
A48:          (r(#)f).x < R_EAL r1 by A46,Def12;
A49:          (r(#)f).x < R_EAL r * R_EAL r0 by A7,A48,EXTREAL1:38;
              A50:          R_EAL r * R_EAL r0 / R_EAL r < (r(#)f).x / R_EAL r
              by A33,A49,XXREAL_3:116;
A51:          R_EAL r * R_EAL r0 = r * r0 by EXTREAL1:13;
A52:          R_EAL r * R_EAL r0 / R_EAL r = (r*r0)/r by A51,
              EXTREAL1:32
                .= r0/(r/r) by XCMPLX_1:78
                .= r0/1 by A5,XCMPLX_1:60
                .= r0;
              A53:          x
 in dom f & f.x = (r(#)f).x / R_EAL r by A5,A47,Def6,Th6;
              thus thesis by A50,A52,A53,Def14;
            end;
A54:        less_dom
(r(#)f,R_EAL r1) c= great_dom(f,R_EAL r0) by A45,SUBSET_1:7;
A55:        great_dom(f,R_EAL r0) = less_dom(r(#)f,R_EAL r1) by A44,A54,
            XBOOLE_0:def 10;
            thus thesis by A1,A2,A55,Th33;
          end;
        end;
        thus thesis by A8;
      end;
      suppose
A56:    r=0;
A57:    now per cases;
          suppose
A58:        r1>0;
A59:        for x1 being set holds x1 in A implies x1 in A /\
            less_dom(r(#)f,R_EAL r1)
            proof
              let x1 being set;
              assume
A60:          x1 in A;
              reconsider x1 as Element of X by A60;
A61:          x1 in dom f by A2,A60;
A62:          x1 in dom (r(#)f) by A61,Def6;
              reconsider y = (r(#)f).x1 as R_eal;
A63:          y = (R_EAL r) * f.x1 by A62,Def6
                .= 0. by A56;
A64:          x1 in less_dom(r(#)f,R_EAL r1) by A58,A62,A63,Def12;
              thus thesis by A60,A64,XBOOLE_0:def 4;
            end;
            A65:        A
 /\ less_dom(r(#)f,R_EAL r1) c= A & A c= A /\ less_dom(r(#)f,R_EAL r1) by
            A59,TARSKI:def 3,XBOOLE_1:17;
A66:        A /\ less_dom(r(#)f,R_EAL r1) = A by A65,XBOOLE_0:def 10;
            thus thesis by A66;
          end;
          suppose
A67:        r1<=0;
A68:        less_dom(r(#)f,R_EAL r1) = {}
            proof
              assume
A69:          less_dom(r(#)f,R_EAL r1) <> {};
              consider x1 being Element of X such that
A70:          x1 in less_dom(r(#)f,R_EAL r1) by A69,SUBSET_1:10;
A71:          x1 in dom (r(#)f) by A70,Def12;
A72:          (r(#)f).x1 < R_EAL r1 by A70,Def12;
A73:          (r(#)f).x1 in rng(r(#)f) by A71,FUNCT_1:def 5;
              A74:          for
 y being R_eal st y in rng(r(#)f) holds not y < R_EAL r1
              proof
                let y being R_eal;
                assume
A75:            y in rng(r(#)f);
                consider x such that
A76:            x in dom(r(#)f) & y = (r(#)f).x by A75,PARTFUN1:26;
A77:            y = (R_EAL r) * f.x by A76,Def6
                  .= 0. by A56;
                thus thesis by A67,A77;
              end;
              thus contradiction by A72,A73,A74;
            end;
            thus thesis by A68,PROB_1:10;
          end;
        end;
        thus thesis by A57;
      end;
    end;
    thus thesis by A4;
  end;
  thus thesis by A3,Def17;
end;

