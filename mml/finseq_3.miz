:: Non-contiguous Substrings and One-to-one Finite Sequences
::  by Wojciech A. Trybulec
::
:: Received April 8, 1990
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies NUMBERS, FINSEQ_1, ORDINAL1, XBOOLE_0, ARYTM_3, CARD_1, XXREAL_0,
      ARYTM_1, NAT_1, SUBSET_1, TARSKI, RELAT_1, ORDINAL4, FUNCT_1, FINSET_1,
      FINSEQ_2, FUNCT_2, CARD_3, FUNCOP_1, FINSEQ_3, EQREL_1, ALG_1;
 notations TARSKI, XBOOLE_0, ENUMSET1, SUBSET_1, CARD_1, NUMBERS, ORDINAL1,
      XCMPLX_0, RELAT_1, FUNCT_1, FINSEQ_1, RELSET_1, PARTFUN1, FUNCT_2,
      FUNCOP_1, FINSEQ_2, FINSET_1, NAT_1, FINSEQOP, XXREAL_0, CARD_3, EQREL_1;
 constructors ENUMSET1, PARTFUN1, WELLORD2, XXREAL_0, REAL_1, NAT_1, INT_1,
      FINSEQOP, RELSET_1, CARD_3, DOMAIN_1, CARD_2, EQREL_1;
 registrations XBOOLE_0, RELAT_1, FUNCT_1, ORDINAL1, FINSET_1, XXREAL_0,
      XREAL_0, NAT_1, INT_1, FINSEQ_1, CARD_1, FINSEQ_2, FUNCOP_1, CARD_3,
      SUBSET_1, FUNCT_2, CARD_2, EQREL_1, RELSET_1;
 requirements NUMERALS, REAL, SUBSET, BOOLE, ARITHM;
 definitions FUNCT_1, TARSKI, WELLORD2, XBOOLE_0, FINSEQ_2, CARD_3, FUNCOP_1,
      EQREL_1;
 theorems CARD_1, CARD_2, ENUMSET1, FINSEQ_1, FINSEQ_2, FINSET_1, FUNCT_1,
      INT_1, NAT_1, TARSKI, WELLORD2, ZFMISC_1, RELAT_1, XREAL_1, XBOOLE_0,
      XBOOLE_1, FUNCT_2, GRFUNC_1, SUBSET_1, XXREAL_0, ORDINAL1, NUMBERS,
      CARD_3, FUNCOP_1, EQREL_1;
 schemes FINSEQ_1, FUNCT_1, NAT_1, CLASSES1, WELLORD2, RELSET_1;

begin

reserve p,q,r for FinSequence;
reserve u,v,x,y,y1,y2,z,A,D,X,Y for set;
reserve i,j,k,l,m,n for natural number;

theorem Th1:
  Seg 3 = {1,2,3}
proof
  thus Seg 3 = {1,2} \/ {2 + 1} by FINSEQ_1:4,11
    .= {1,2,3} by ENUMSET1:43;
end;

theorem Th2:
  Seg 4 = {1,2,3,4}
proof
  thus Seg 4 = {1,2,3} \/ {3 + 1} by Th1,FINSEQ_1:11
    .= {1,2,3,4} by ENUMSET1:46;
end;

theorem Th3:
  Seg 5 = {1,2,3,4,5}
proof
  thus Seg 5 = {1,2,3,4} \/ {4 + 1} by Th2,FINSEQ_1:11
    .= {1,2,3,4,5} by ENUMSET1:50;
end;

theorem Th4:
  Seg 6 = {1,2,3,4,5,6}
proof
  thus Seg 6 = {1,2,3,4,5} \/ {5 + 1} by Th3,FINSEQ_1:11
    .= {1,2,3,4,5,6} by ENUMSET1:55;
end;

theorem Th5:
  Seg 7 = {1,2,3,4,5,6,7}
proof
  thus Seg 7 = {1,2,3,4,5,6} \/ {6 + 1} by Th4,FINSEQ_1:11
    .= {1,2,3,4,5,6,7} by ENUMSET1:61;
end;

theorem
  Seg 8 = {1,2,3,4,5,6,7,8}
proof
  thus Seg 8 = {1,2,3,4,5,6,7} \/ {7 + 1} by Th5,FINSEQ_1:11
    .= {1,2,3,4,5,6,7,8} by ENUMSET1:68;
end;

theorem Th7:
  Seg k = {} iff not k in Seg k
   proof
    thus Seg k = {} implies not k in Seg k;
    assume not k in Seg k;
    then k = 0 by FINSEQ_1:5;
    hence Seg k = {};
   end;

canceled;

theorem Th9:
  not k + 1 in Seg k
proof
  assume k + 1 in Seg k;
  then
A1: k + 1 <= k by FINSEQ_1:3;
  k <= k + 1 by NAT_1:12;
  then k + 0 = k + 1 by A1,XXREAL_0:1;
  hence thesis;
end;

theorem
  k <> 0 implies k in Seg(k + n)
proof
  assume k <> 0;
  then
A1: 0 + 1 <= k by NAT_1:13;
  k <= k + n by NAT_1:12;
  hence thesis by A1,FINSEQ_1:3;
end;

theorem Th11:
  k + n in Seg k implies n = 0
proof
  assume k + n in Seg k;
  then k + n <= k + 0 by FINSEQ_1:3;
  hence thesis by XREAL_1:8;
end;

theorem
  k < n implies k + 1 in Seg n
proof
  assume k < n;
  then
A1: k + 1 <= n by NAT_1:13;
  1 <= k + 1 by NAT_1:12;
  hence thesis by A1,FINSEQ_1:3;
end;

theorem Th13:
  k in Seg n & m < k implies k - m in Seg n
proof
  assume that
A1: k in Seg n and
A2: m < k;
  consider i being Nat such that
A3: k = m + i by A2,NAT_1:10;
  reconsider x = k - m as Element of NAT by A3,ORDINAL1:def 13;
A4: now
    assume not 1 <= x;
    then x = 0 by NAT_1:14;
    hence contradiction by A2;
  end;
A5: k <= n by A1,FINSEQ_1:3;
  i <= k by A3,NAT_1:12;
  then x <= n by A3,A5,XXREAL_0:2;
  hence thesis by A4,FINSEQ_1:3;
end;

theorem
  k - n in Seg k iff n < k
proof
  thus k - n in Seg k implies n < k
  proof
    assume
A1: k - n in Seg k;
    then reconsider x = k - n as Element of NAT;
    assume not n < k;
    then k - n <= n - n by XREAL_1:11;
    then x = 0;
    hence contradiction by A1,FINSEQ_1:3;
  end;
  thus thesis by Th13,FINSEQ_1:5;
end;

theorem Th15:
  Seg k misses {k + 1}
proof
  consider x being Element of Seg k /\ {k + 1};
  assume not thesis;
  then
A1: Seg k /\ {k + 1} <> {} by XBOOLE_0:def 7;
  then
A2: x in Seg k by XBOOLE_0:def 4;
  then reconsider x as Element of NAT;
  x in {k + 1} by A1,XBOOLE_0:def 4;
  then
A3: x = k + 1 by TARSKI:def 1;
  x <= k by A2,FINSEQ_1:3;
  hence thesis by A3,XREAL_1:31;
end;

theorem
  Seg(k + 1) \ Seg k = {k + 1}
proof
A1: Seg(k + 1) = Seg k \/ {k + 1} by FINSEQ_1:11;
  Seg k misses {k + 1} by Th15;
  hence thesis by A1,XBOOLE_1:88;
end;

:: Theorem Seg(k + 1) \ {k + 1} = Seg k is
:: proved in RLVECT_1 and has a number 104.

theorem
  Seg k <> Seg(k + 1) by Th9,FINSEQ_1:6;

theorem
  Seg k = Seg(k + n) implies n = 0 by Th11,FINSEQ_1:5;

theorem Th19:
  Seg k c= Seg(k + n)
proof
  k <= k + n by NAT_1:12;
  hence thesis by FINSEQ_1:7;
end;

theorem Th20:
  Seg k, Seg n are_c=-comparable
proof
  n <= k or k <= n;
  then Seg n c= Seg k or Seg k c= Seg n by FINSEQ_1:7;
  hence thesis by XBOOLE_0:def 9;
end;

canceled;

theorem Th22:
  Seg k = {y} implies k = 1 & y = 1
proof
  assume
A1: Seg k = {y};
  now
    per cases;
    suppose
      k = 0;
      hence thesis by A1;
    end;
    suppose
      k <> 0;
      then
A2:   k in Seg k by FINSEQ_1:5;
      then 1 <= k by FINSEQ_1:3;
      then Seg 1 c= Seg k by FINSEQ_1:7;
      then Seg 1 = {y} by A1,ZFMISC_1:39;
      hence thesis by A1,A2,FINSEQ_1:4,TARSKI:def 1,ZFMISC_1:6;
    end;
  end;
  hence thesis;
end;

theorem
  Seg k = {x,y} & x <> y implies k = 2 & {x,y} = {1,2}
proof
  assume that
A1: Seg k = {x,y} and
A2: x <> y;
  now
    per cases;
    suppose
      k = 0;
      hence thesis by A1;
    end;
    suppose
A3:   k <> 0;
      now
        per cases;
        suppose
          k = 1;
          hence thesis by A1,A2,FINSEQ_1:4,ZFMISC_1:9;
        end;
        suppose
A4:       k <> 1;
          1 <= k by A3,NAT_1:14;
          then 1 < k by A4,XXREAL_0:1;
          then
A5:       1 + 1 <= k by NAT_1:13;
          then Seg 2 c= Seg k by FINSEQ_1:7;
          then
A6:       1 = x & 2 = x or 1 = x & 2 = y or 2 = x & 1 = y or 1 = y & 2 =
          y by A1,FINSEQ_1:4,ZFMISC_1:28;
          now
            k in Seg k by A1,Th7;
            then
A7:         k = 1 or k = 2 by A1,A6,TARSKI:def 2;
            assume not k <= 2;
            hence contradiction by A7;
          end;
          hence thesis by A1,A5,FINSEQ_1:4,XXREAL_0:1;
        end;
      end;
      hence thesis;
    end;
  end;
  hence thesis;
end;

theorem Th24:
  x in dom p implies x in dom(p ^ q)
proof
  dom p c= dom(p ^ q) by FINSEQ_1:39;
  hence thesis;
end;

theorem
  x in dom p implies x is Element of NAT;

theorem Th26:
  x in dom p implies x <> 0
proof
  assume x in dom p;
  then x in Seg(len p) by FINSEQ_1:def 3;
  hence thesis by FINSEQ_1:3;
end;

theorem Th27:
  n in dom p iff 1 <= n & n <= len p
proof
  thus n in dom p implies 1 <= n & n <= len p
  proof
    assume n in dom p;
    then n in Seg(len p) by FINSEQ_1:def 3;
    hence thesis by FINSEQ_1:3;
  end;
  assume that
A1: 1 <= n and
A2: n <= len p;
  n in Seg(len p) by A1,A2,FINSEQ_1:3;
  hence thesis by FINSEQ_1:def 3;
end;

theorem
  n in dom p iff n - 1 is Element of NAT & len p - n is Element of NAT
proof
  thus n in dom p implies n - 1 is Element of NAT & len p - n is Element of NAT
  proof
    assume
A1: n in dom p;
    then
A2: n <= len p by Th27;
    1 <= n by A1,Th27;
    hence thesis by A2,INT_1:18;
  end;
  assume that
A3: n - 1 is Element of NAT and
A4: len p - n is Element of NAT;
A5: 0 + n <= len p by A4,XREAL_1:21;
  0 + 1 <= n by A3,XREAL_1:21;
  hence thesis by A5,Th27;
end;

theorem Th29:
  dom<* x,y *> = Seg(2)
proof
  len<* x,y *> = 2 by FINSEQ_1:61;
  hence thesis by FINSEQ_1:def 3;
end;

theorem Th30:
  dom<* x,y,z *> = Seg(3)
proof
  len<* x,y,z *> = 3 by FINSEQ_1:62;
  hence thesis by FINSEQ_1:def 3;
end;

theorem Th31:
  len p = len q iff dom p = dom q
proof
  dom p = Seg(len p) by FINSEQ_1:def 3;
  hence thesis by FINSEQ_1:def 3;
end;

theorem Th32:
  len p <= len q iff dom p c= dom q
proof
A1: dom q = Seg(len q) by FINSEQ_1:def 3;
  dom p = Seg(len p) by FINSEQ_1:def 3;
  hence thesis by A1,FINSEQ_1:7;
end;

theorem Th33:
  x in rng p implies 1 in dom p
proof
  assume x in rng p;
  then p <> {};
  then
A1: 1 <= len p by NAT_1:14;
  dom p = Seg(len p) by FINSEQ_1:def 3;
  hence thesis by A1,FINSEQ_1:3;
end;

theorem
  rng p <> {} implies 1 in dom p
proof
  consider y being Element of rng p;
  assume rng p <> {};
  then y in rng p;
  hence thesis by Th33;
end;

canceled 3;

theorem
  {} <> <* x,y *>;

theorem
  {} <> <* x,y,z *>;

theorem Th40:
  <* x *> <> <* y,z *>
proof
  len<* x *> = 1 by FINSEQ_1:57;
  hence thesis by FINSEQ_1:61;
end;

theorem
  <* u *> <> <* x,y,z *>
proof
  len<* u *> = 1 by FINSEQ_1:57;
  hence thesis by FINSEQ_1:62;
end;

theorem
  <* u,v *> <> <* x,y,z *>
proof
  len<* u,v *> = 2 by FINSEQ_1:61;
  hence thesis by FINSEQ_1:62;
end;

theorem Th43:
  len r = len p + len q & (for k being Element of NAT st k in dom
p holds r.k = p.k) & (for k being Element of NAT st k in dom q holds r.(len p +
  k) = q.k) implies r = p ^ q
proof
  assume len r = len p + len q;
  then
A1: dom r = Seg(len p + len q) by FINSEQ_1:def 3;
  assume that
A2: for k being Element of NAT st k in dom p holds r.k = p.k and
A3: for k being Element of NAT st k in dom q holds r.(len p + k) = q.k;
A4: for k being Nat st k in dom q holds r.(len p + k) = q.k by A3;
  for k being Nat st k in dom p holds r.k = p.k by A2;
  hence thesis by A1,A4,FINSEQ_1:def 7;
end;

Lm1: A c= Seg k implies Sgm A is one-to-one
proof
  assume
A1: A c= Seg k;
  then
A2: rng(Sgm A) = A by FINSEQ_1:def 13;
  let x,y;
  assume that
A3: x in dom(Sgm A) and
A4: y in dom(Sgm A) and
A5: Sgm(A).x = Sgm(A).y and
A6: x <> y;
  Sgm(A).y in rng(Sgm A) by A4,FUNCT_1:def 5;
  then
A7: Sgm(A).y in Seg k by A1,A2;
  Sgm(A).x in rng(Sgm A) by A3,FUNCT_1:def 5;
  then Sgm(A).x in Seg k by A1,A2;
  then reconsider m = Sgm(A).x, n = Sgm(A).y as Element of NAT by A7;
  reconsider i = x, j = y as Element of NAT by A3,A4;
A8: dom(Sgm A) = Seg(len(Sgm A)) by FINSEQ_1:def 3;
  now
    per cases by A6,XXREAL_0:1;
    suppose
A9:   i < j;
A10:  j <= len(Sgm A) by A4,A8,FINSEQ_1:3;
      1 <= i by A3,A8,FINSEQ_1:3;
      then m < n by A1,A9,A10,FINSEQ_1:def 13;
      hence contradiction by A5;
    end;
    suppose
A11:  j < i;
A12:  i <= len(Sgm A) by A3,A8,FINSEQ_1:3;
      1 <= j by A4,A8,FINSEQ_1:3;
      then n < m by A1,A11,A12,FINSEQ_1:def 13;
      hence contradiction by A5;
    end;
  end;
  hence thesis;
end;

theorem Th44:
  for A being finite set st A c= Seg k holds len(Sgm A) = card A
proof
  let A be finite set;
A1: dom(Sgm A) = Seg(len(Sgm A)) by FINSEQ_1:def 3;
A2: card(Seg(len(Sgm A))) = len(Sgm A) by FINSEQ_1:78;
  assume
A3: A c= Seg k;
  then
A4: rng(Sgm A) = A by FINSEQ_1:def 13;
  Sgm A is one-to-one by A3,Lm1;
  then Seg(len(Sgm A)),A are_equipotent by A1,A4,WELLORD2:def 4;
  hence thesis by A2,CARD_1:21;
end;

theorem
  for A being finite set st A c= Seg k holds dom(Sgm A) = Seg(card A)
proof
  let A be finite set;
  assume A c= Seg k;
  then len(Sgm A) = card A by Th44;
  hence thesis by FINSEQ_1:def 3;
end;

theorem Th46:
  X c= Seg i & k < l & 1 <= n & m <= len(Sgm X) & Sgm(X).m = k &
  Sgm(X).n = l implies m < n
proof
  assume that
A1: X c= Seg i and
A2: k < l and
A3: 1 <= n and
A4: m <= len(Sgm X) and
A5: Sgm(X).m = k and
A6: Sgm(X).n = l and
A7: not m < n;
  n < m by A2,A5,A6,A7,XXREAL_0:1;
  hence thesis by A1,A2,A3,A4,A5,A6,FINSEQ_1:def 13;
end;

canceled;

theorem Th48:
  X c= Seg i & Y c= Seg j implies ((for m,n being Element of NAT
  st m in X & n in Y holds m < n) iff Sgm(X \/ Y) = Sgm(X) ^ Sgm(Y))
proof
  assume that
A1: X c= Seg i and
A2: Y c= Seg j;
  set r = Sgm(X \/ Y);
  set q = Sgm Y;
  set p = Sgm X;
  Seg i, Seg j are_c=-comparable by Th20;
  then Seg i c= Seg j or Seg j c= Seg i by XBOOLE_0:def 9;
  then
A3: X c= Seg j or Y c= Seg i by A1,A2,XBOOLE_1:1;
  then
A4: X \/ Y c= Seg i or X \/ Y c= Seg j by A1,A2,XBOOLE_1:8;
  thus (for m,n being Element of NAT st m in X & n in Y holds m < n) implies
  Sgm(X \/ Y) = Sgm(X) ^ Sgm(Y)
  proof
    reconsider X1 = X, Y1 = Y as finite set by A1,A2;
    defpred P[Element of NAT] means $1 in dom p implies r.$1 = p.$1;
    assume
A5: for m,n being Element of NAT st m in X & n in Y holds m < n;
    X /\ Y = {}
    proof
      consider x being Element of X /\ Y;
      X = rng p by A1,FINSEQ_1:def 13;
      then
A6:   X c= NAT;
      assume
A7:   not thesis;
      then x in X by XBOOLE_0:def 4;
      then reconsider m = x as Element of NAT by A6;
A8:   m in Y by A7,XBOOLE_0:def 4;
      m in X by A7,XBOOLE_0:def 4;
      hence thesis by A5,A8;
    end;
    then
A9: X misses Y by XBOOLE_0:def 7;
A10: len r = card(X1 \/ Y1) by A1,A2,A3,Th44,XBOOLE_1:8
      .= card X1 + card Y1 by A9,CARD_2:53
      .= len p + card Y1 by A1,Th44
      .= len p + len q by A2,Th44;
A11: now
      let k be Element of NAT;
      assume
A12:  P[k];
      thus P[k+1]
      proof
        assume
A15:    k + 1 in dom p;
        then
A16:    p.(k + 1) in rng p by FUNCT_1:def 5;
        then reconsider n = p.(k + 1) as Element of NAT;
A17:    n in X by A1,A16,FINSEQ_1:def 13;
        len p <= len r by A10,NAT_1:12;
        then Seg(len p) c= Seg(len r) by FINSEQ_1:7;
        then dom p c= Seg(len r) by FINSEQ_1:def 3;
        then
A18:    dom p c= dom r by FINSEQ_1:def 3;
        then
A19:    r.(k + 1) in rng r by A15,FUNCT_1:def 5;
        then reconsider m = r.(k + 1) as Element of NAT;
A20:    m in X \/ Y by A4,A19,FINSEQ_1:def 13;
        assume
A21:    r.(k + 1) <> p.(k + 1);
A22:    k + 1 in dom r by A15,A18;
        now
          per cases;
          suppose
A23:        k in dom p;
            then p.k in rng p by FUNCT_1:def 5;
            then reconsider n1 = p.k as Element of NAT;
            r.k in rng r by A18,A23,FUNCT_1:def 5;
            then reconsider m1 = r.k as Element of NAT;
            now
              per cases by A21,XXREAL_0:1;
              suppose
A24:            m < n;
A25:            1 <= k + 1 by A15,Th27;
                not m in Y by A5,A17,A24;
                then m in X by A20,XBOOLE_0:def 3;
                then m in rng p by A1,FINSEQ_1:def 13;
                then consider x such that
A26:            x in dom p and
A27:            p.x = m by FUNCT_1:def 5;
                reconsider x as Element of NAT by A26;
                x <= len p by A26,Th27;
                then
A28:            x < k + 1 by A1,A24,A27,A25,Th46;
                k + 1 in Seg(len r) by A22,FINSEQ_1:def 3;
                then
A29:            k + 1 <= len r by FINSEQ_1:3;
                k in Seg(len p) by A23,FINSEQ_1:def 3;
                then
A30:            1 <= k by FINSEQ_1:3;
                k < k + 1 by XREAL_1:31;
                then
A31:            n1 < m by A4,A12,A23,A30,A29, FINSEQ_1:def 13;
A32:            k <= len p by A23,Th27;
                1 <= x by A26,Th27;
                then k < x by A1,A27,A31,A32,Th46;
                hence contradiction by A28,NAT_1:13;
              end;
              suppose
A33:            n < m;
A34:            1 <= k + 1 by A15,Th27;
                n in X \/ Y by A17,XBOOLE_0:def 3;
                then n in rng r by A4,FINSEQ_1:def 13;
                then consider x such that
A35:            x in dom r and
A36:            r.x = n by FUNCT_1:def 5;
                reconsider x as Element of NAT by A35;
                x <= len r by A35,Th27;
                then
A37:            x < k + 1 by A1,A2,A3,A33,A36,A34,Th46,XBOOLE_1:8;
A38:            k + 1 <= len p by A15,Th27;
A39:            k < k + 1 by XREAL_1:31;
                1 <= k by A23,Th27;
                then
A40:            m1 < n by A1,A12,A23,A39,A38, FINSEQ_1:def 13;
A41:            k <= len r by A18,A23,Th27;
                1 <= x by A35,Th27;
                then k < x by A1,A2,A3,A36,A40,A41,Th46, XBOOLE_1:8;
                hence contradiction by A37,NAT_1:13;
              end;
            end;
            hence contradiction;
          end;
          suppose
A42:        not k in dom p;
A43:        k < k + 1 by XREAL_1:31;
            k < 1 or len p < k by A42,Th27;
            then
A44:        k = 0 or len p < k + 1 & k + 1 <= len p by A15,A43,Th27,
            NAT_1:14,XXREAL_0:2;
            now
              per cases by A21,XXREAL_0:1;
              suppose
A45:            m < n;
                then not m in Y by A5,A17;
                then m in X by A20,XBOOLE_0:def 3;
                then m in rng p by A1,FINSEQ_1:def 13;
                then consider x such that
A46:            x in dom p and
A47:            p.x = m by FUNCT_1:def 5;
A48:            1 <= k + 1 by A15,Th27;
                reconsider x as Element of NAT by A46;
                x <= len p by A46,Th27;
                then x < k + 1 by A1,A45,A47,A48,Th46;
                hence contradiction by A44,A46,Th26,NAT_1:14;
              end;
              suppose
A49:            n < m;
A50:            1 <= k + 1 by A15,Th27;
                n in X \/ Y by A17,XBOOLE_0:def 3;
                then n in rng r by A4,FINSEQ_1:def 13;
                then consider x such that
A51:            x in dom r and
A52:            r.x = n by FUNCT_1:def 5;
                reconsider x as Element of NAT by A51;
                x <= len r by A51,Th27;
                then x < k + 1 by A1,A2,A3,A49,A52,A50,Th46,XBOOLE_1:8;
                hence contradiction by A44,A51,Th26,NAT_1:14;
              end;
            end;
            hence contradiction;
          end;
        end;
        hence contradiction;
      end;
    end;
A53: P[0] by Th26;
A54: for k being Element of NAT holds P[k] from NAT_1:sch 1(A53,A11);
    defpred P[Element of NAT] means $1 in dom q implies r.(len p + $1) = q.$1;
A55: now
      let k be Element of NAT;
      assume
A56:  P[k];
      thus P[k+1]
      proof
        set a = len p + (k + 1);
        assume
A58:    k + 1 in dom q;
        then k + 1 <= len q by Th27;
        then
A59:    a <= len r by A10,XREAL_1:9;
A60:    1 <= k + 1 by NAT_1:12;
        then 1 <= a by NAT_1:12;
        then
A61:    a in dom r by A59,Th27;
        then
A62:    r.a in rng r by FUNCT_1:def 5;
        reconsider m = r.a as Element of NAT by A62;
A64:    now
          assume m in X;
          then m in rng p by A1,FINSEQ_1:def 13;
          then consider x such that
A65:      x in dom p and
A66:      p.x = m by FUNCT_1:def 5;
          reconsider x as Element of NAT by A65;
          x <= len p by A65,Th27;
          then
A67:      x <= len r by A10,NAT_1:12;
A68:      r is one-to-one by A1,A2,A3,Lm1,XBOOLE_1:8;
          1 <= x by A65,Th27;
          then
A69:      x in dom r by A67,Th27;
          r.x = r.a by A54,A65,A66;
          then x = a by A61,A68,A69,FUNCT_1:def 8;
          then len p + (k + 1) <= len p + 0 by A65,Th27;
          hence contradiction by XREAL_1:31;
        end;
A70:    q.(k + 1) in rng q by A58,FUNCT_1:def 5;
        then reconsider n = q.(k + 1) as Element of NAT;
A71:    n in Y by A2,A70,FINSEQ_1:def 13;
        assume
A72:    r.(len p + (k + 1)) <> q.(k + 1);
A73:    m in X \/ Y by A4,A62,FINSEQ_1:def 13;
        now
          per cases;
          suppose
A74:        k in dom q;
            then q.k in rng q by FUNCT_1:def 5;
            then reconsider n1 = q.k as Element of NAT;
            1 <= k by A74,Th27;
            then
A75:        1 <= len p + k by NAT_1:12;
A76:        k <= len q by A74,Th27;
            then len p + k <= len r by A10,XREAL_1:9;
            then len p + k in dom r by A75,Th27;
            then r.(len p + k) in rng r by FUNCT_1:def 5;
            then reconsider m1 = r.(len p + k) as Element of NAT;
            now
              per cases by A72,XXREAL_0:1;
              suppose
A77:            m < n;
A78:            1 <= k + 1 by A58,Th27;
                m in Y by A73,A64,XBOOLE_0:def 3;
                then m in rng q by A2,FINSEQ_1:def 13;
                then consider x such that
A79:            x in dom q and
A80:            q.x = m by FUNCT_1:def 5;
                reconsider x as Element of NAT by A79;
                x <= len q by A79,Th27;
                then
A81:            x < k + 1 by A2,A77,A80,A78,Th46;
                len p + k < len p + k + 1 by XREAL_1:31;
                then
A82:            n1 < m by A4,A56,A59,A74,A75, FINSEQ_1:def 13;
A83:            k <= len q by A74,Th27;
                1 <= x by A79,Th27;
                then k < x by A2,A80,A82,A83,Th46;
                hence contradiction by A81,NAT_1:13;
              end;
              suppose
A84:            n < m;
A85:            1 <= a by A60,NAT_1:12;
                n in X \/ Y by A71,XBOOLE_0:def 3;
                then n in rng r by A4,FINSEQ_1:def 13;
                then consider x such that
A86:            x in dom r and
A87:            r.x = n by FUNCT_1:def 5;
                reconsider x as Element of NAT by A86;
                x <= len r by A86,Th27;
                then
A88:            x < len p + k + 1 by A1,A2,A3,A84,A87,A85,Th46,
                XBOOLE_1:8;
A89:            k + 1 <= len q by A58,Th27;
A90:            k < k + 1 by XREAL_1:31;
                1 <= k by A74,Th27;
                then
A91:            m1 < n by A2,A56,A74,A90,A89, FINSEQ_1:def 13;
A92:            len p + k <= len r by A10,A76,XREAL_1:9;
                1 <= x by A86,Th27;
                then len p + k < x by A1,A2,A3,A87,A91,A92, Th46,
                XBOOLE_1:8;
                hence contradiction by A88,NAT_1:13;
              end;
            end;
            hence contradiction;
          end;
          suppose
A93:        not k in dom q;
A94:        k < k + 1 by XREAL_1:31;
            k < 1 or len q < k by A93,Th27;
            then
A95:        k = 0 or len q < k + 1 & k + 1 <= len q by A58,A94,
            Th27,NAT_1:14,XXREAL_0:2;
            now
              per cases by A72,XXREAL_0:1;
              suppose
A96:            m < n;
A97:            1 <= k + 1 by A58,Th27;
                m in Y by A73,A64,XBOOLE_0:def 3;
                then m in rng q by A2,FINSEQ_1:def 13;
                then consider x such that
A98:            x in dom q and
A99:            q.x = m by FUNCT_1:def 5;
                reconsider x as Element of NAT by A98;
                x <= len q by A98,Th27;
                then x < k + 1 by A2,A96,A99,A97,Th46;
                hence contradiction by A95,A98,Th26,NAT_1:14;
              end;
              suppose
A100:           n < m;
A101:           1 <= len p + 1 by NAT_1:12;
                n in X \/ Y by A71,XBOOLE_0:def 3;
                then n in rng r by A4,FINSEQ_1:def 13;
                then consider x such that
A102:           x in dom r and
A103:           r.x = n by FUNCT_1:def 5;
                reconsider x as Element of NAT by A102;
                x <= len r by A102,Th27;
                then x < len p + 1 by A1,A2,A3,A95,A100,A103,A101,Th46,
                XBOOLE_1:8;
                then
A104:           x <= len p by NAT_1:13;
                1 <= x by A102,Th27;
                then
A105:           x in dom p by A104,Th27;
                then
A106:           p.x in rng p by FUNCT_1:def 5;
                n = p.x by A54,A103,A105;
                then n in X by A1,A106,FINSEQ_1:def 13;
                hence contradiction by A5,A71;
              end;
            end;
            hence contradiction;
          end;
        end;
        hence contradiction;
      end;
    end;
A107: P[0] by Th26;
    for k being Element of NAT holds P[k] from NAT_1:sch 1(A107,A55);
    hence thesis by A10,A54,Th43;
  end;
  assume
A108: Sgm(X \/ Y) = Sgm(X) ^ Sgm(Y);
  let m,n be Element of NAT;
  assume that
A109: m in X and
A110: n in Y;
  n in rng q by A2,A110,FINSEQ_1:def 13;
  then consider y such that
A111: y in dom q and
A112: q.y = n by FUNCT_1:def 5;
  reconsider y as Element of NAT by A111;
A113: n = r.(len p + y) by A108,A111,A112,FINSEQ_1:def 7;
  y <= len q by A111,Th27;
  then len p + y <= len p + len q by XREAL_1:9;
  then
A114: len p + y <= len r by A108,FINSEQ_1:35;
  m in rng(Sgm X) by A1,A109,FINSEQ_1:def 13;
  then consider x such that
A115: x in dom p and
A116: p.x = m by FUNCT_1:def 5;
  reconsider x as Element of NAT by A115;
A117: x in Seg(len p) by A115,FINSEQ_1:def 3;
  then
A118: 1 <= x by FINSEQ_1:3;
A119: x <= len p by A117,FINSEQ_1:3;
  y <> 0 by A111,Th26;
  then
A120: x < len p + y by A119,NAT_1:16,XXREAL_0:2;
  m = r.x by A108,A115,A116,FINSEQ_1:def 7;
  hence thesis by A4,A113,A118,A120,A114,FINSEQ_1:def 13;
end;

theorem Th49:
  Sgm {} = {}
proof
  {} c= Seg 0;
  hence thesis by FINSEQ_1:72;
end;

:: The other way of the one above - FINSEQ_1:72.

theorem
  0 <> n implies Sgm{n} = <* n *>
proof
  assume 0 <> n;
  then n in Seg n by FINSEQ_1:5;
  then
A1: {n} c= Seg n by ZFMISC_1:37;
  then len(Sgm{n}) = card{n} by Th44;
  then
A2: len(Sgm{n}) = 1 by CARD_1:50;
  rng(Sgm{n}) = {n} by A1,FINSEQ_1:def 13;
  hence thesis by A2,FINSEQ_1:56;
end;

theorem
  0 < n & n < m implies Sgm{n,m} = <* n,m *>
proof
  assume that
A1: 0 < n and
A2: n < m;
A3: m in Seg m by A2,FINSEQ_1:5;
A4: n in Seg n by A1,FINSEQ_1:5;
  Seg n c= Seg m by A2,FINSEQ_1:7;
  then
A5: {n,m} c= Seg m by A3,A4,ZFMISC_1:38;
  then
A6: Sgm{n,m} is one-to-one by Lm1;
A7: len(Sgm{n,m}) = card{n,m} by A5,Th44
    .= 2 by A2,CARD_2:76;
  then
A8: dom(Sgm{n,m}) = {1,2} by FINSEQ_1:4,def 3;
  then
A9: 1 in dom(Sgm{n,m}) by TARSKI:def 2;
A10: 2 in dom(Sgm{n,m}) by A8,TARSKI:def 2;
A11: rng(Sgm{n,m}) = {n,m} by A5,FINSEQ_1:def 13;
  then
A12: Sgm{n,m}.2 in {n,m} by A10,FUNCT_1:def 5;
A13: Sgm{n,m}.1 in {n,m} by A9,A11,FUNCT_1:def 5;
  now
    per cases by A13,A12,TARSKI:def 2;
    suppose
      Sgm{n,m}.1 = n & Sgm{n,m}.2 = n;
      hence Sgm{n,m}.1 = n & Sgm{n,m}.2 = m by A9,A10,A6,FUNCT_1:def 8;
    end;
    suppose
      Sgm{n,m}.1 = m & Sgm{n,m}.2 = m;
      hence Sgm{n,m}.1 = n & Sgm{n,m}.2 = m by A9,A10,A6,FUNCT_1:def 8;
    end;
    suppose
      Sgm{n,m}.1 = n & Sgm{n,m}.2 = m;
      hence Sgm{n,m}.1 = n & Sgm{n,m}.2 = m;
    end;
    suppose
      Sgm{n,m}.1 = m & Sgm{n,m}.2 = n;
      hence Sgm{n,m}.1 = n & Sgm{n,m}.2 = m by A2,A5,A7,FINSEQ_1:def 13;
    end;
  end;
  hence thesis by A7,FINSEQ_1:61;
end;

theorem Th52:
  len(Sgm(Seg k)) = k
proof
  card(Seg k) = k by FINSEQ_1:78;
  hence thesis by Th44;
end;

Lm2: Sgm(Seg(k + 0)) | Seg k = Sgm(Seg k)
proof
  card(Seg k) = k by FINSEQ_1:78;
  then len(Sgm(Seg k)) = k by Th44;
  then dom(Sgm(Seg k)) = Seg k by FINSEQ_1:def 3;
  hence thesis by RELAT_1:97;
end;

Lm3: now
  let n;
  assume
A1: for k holds Sgm(Seg(k + n)) | Seg k = Sgm(Seg k);
  let k;
  set X = Sgm(Seg(k + (n + 1)));
  set Y = Sgm(Seg(k + 1));
A2: Y | Seg k = Sgm(Seg k)
  proof
    reconsider p = Y | Seg k as FinSequence of NAT by FINSEQ_1:23;
A3: len Y = k + 1 by Th52;
    then
A4: dom Y = Seg(k + 1) by FINSEQ_1:def 3;
A5: k <= k + 1 by NAT_1:12;
    then
A6: dom p = Seg k by A3,FINSEQ_1:21;
A7: rng Y = Seg(k + 1) by FINSEQ_1:def 13;
A8: Y.(k + 1) = k + 1
    proof
      k + 1 in dom Y by A4,FINSEQ_1:6;
      then
A9:   Y.(k + 1) in Seg(k + 1) by A7,FUNCT_1:def 5;
      then reconsider n = Y.(k + 1) as Element of NAT;
A10:  k < k + 1 by XREAL_1:31;
      k + 1 in rng Y by A7,FINSEQ_1:6;
      then consider x such that
A11:  x in dom Y and
A12:  Y.x = k + 1 by FUNCT_1:def 5;
      reconsider x as Element of NAT by A11;
      assume not thesis;
      then not Y.(k + 1) in {k + 1} by TARSKI:def 1;
      then Y.(k + 1) in Seg(k + 1) \ {k + 1} by A9,XBOOLE_0:def 5;
      then
A13:  Y.(k + 1) in Seg k by FINSEQ_1:12;
A14:  now
        assume
A15:    x = k + 1;
        n <= k by A13,FINSEQ_1:3;
        hence contradiction by A12,A15,XREAL_1:31;
      end;
      x <= k + 1 by A3,A11,Th27;
      then
A16:  x < k + 1 by A14,XXREAL_0:1;
      1 <= x by A11,Th27;
      then
A17:  k + 1 < n by A3,A12,A16,FINSEQ_1:def 13;
      n <= k by A13,FINSEQ_1:3;
      hence contradiction by A17,A10,XXREAL_0:2;
    end;
A18: Y is one-to-one by Lm1;
    rng p = rng Y \ {Y.(k + 1)}
    proof
      thus rng p c= rng Y \ {Y.(k + 1)}
      proof
        let x;
        assume
A19:    x in rng p;
A20:    now
          assume x in {k + 1};
          then
A21:      x = k + 1 by TARSKI:def 1;
A22:      k < k + 1 by XREAL_1:31;
A23:      k + 1 in dom Y by A4,FINSEQ_1:6;
A24:      Seg k c= Seg(k + 1) by Th19;
          consider y such that
A25:      y in dom p and
A26:      p.y = x by A19,FUNCT_1:def 5;
          reconsider y as Element of NAT by A25;
A27:      Y.y = p.y by A25,FUNCT_1:70;
          y <= k by A6,A25,FINSEQ_1:3;
          hence contradiction by A18,A4,A6,A8,A25,A26,A24,A27,A21,A23,A22,
          FUNCT_1:def 8;
        end;
        rng p c= rng Y by RELAT_1:99;
        hence thesis by A8,A19,A20,XBOOLE_0:def 5;
      end;
      let x;
      assume
A28:  x in rng Y \ {Y.(k + 1)};
      then x in rng Y by XBOOLE_0:def 5;
      then consider y such that
A29:  y in dom Y and
A30:  Y.y = x by FUNCT_1:def 5;
      now
        assume y in {k + 1};
        then
A31:    x = k + 1 by A8,A30,TARSKI:def 1;
        not x in {k + 1} by A8,A28,XBOOLE_0:def 5;
        hence contradiction by A31,TARSKI:def 1;
      end;
      then y in Seg(k + 1) \ {k + 1} by A4,A29,XBOOLE_0:def 5;
      then
A32:  y in dom p by A6,FINSEQ_1:12;
      then p.y = Y.y by FUNCT_1:70;
      hence thesis by A30,A32,FUNCT_1:def 5;
    end;
    then
A33: rng p = Seg k by A7,A8,FINSEQ_1:12;
    now
A34:  len p = k by A3,A5,FINSEQ_1:21;
      let i,j,l,m;
      assume that
A35:  1 <= i and
A36:  i < j and
A37:  j <= len p and
A38:  l = p.i and
A39:  m = p.j;
      i <= len p by A36,A37,XXREAL_0:2;
      then i in dom p by A6,A35,A34,FINSEQ_1:3;
      then
A40:  p.i = Y.i by FUNCT_1:70;
      1 <= j by A35,A36,XXREAL_0:2;
      then j in dom p by A6,A37,A34,FINSEQ_1:3;
      then
A41:  p.j = Y.j by FUNCT_1:70;
      len Y = k + 1 by Th52;
      then j <= len Y by A37,A34,NAT_1:12;
      hence l < m by A35,A36,A38,A39,A40,A41,FINSEQ_1:def 13;
    end;
    hence thesis by A33,FINSEQ_1:def 13;
  end;
  X = Sgm(Seg(k + 1 + n));
  then X | Seg(k + 1) = Y by A1;
  then Sgm(Seg k) = X | (Seg(k + 1) /\ Seg k) by A2,RELAT_1:100;
  hence Sgm(Seg(k + (n + 1))) | Seg k = Sgm(Seg k) by FINSEQ_1:9,NAT_1:12;
end;

Lm4: for k holds Sgm(Seg(k + n)) | Seg k = Sgm(Seg k)
proof
  defpred P[natural number] means for k holds Sgm(Seg(k + $1)) | Seg k = Sgm(
  Seg k);
A1: for k st P[k] holds P[k+1] by Lm3;
A2: P[0] by Lm2;
  for n holds P[n] from NAT_1:sch 2(A2,A1);
  hence thesis;
end;

theorem
  Sgm(Seg(k + n)) | Seg k = Sgm(Seg k) by Lm4;

Lm5: now
  let k be Nat;
  assume
A1: Sgm(Seg k) = idseq k;
A2: len(idseq(k + 1)) = k + 1 by FINSEQ_1:def 18;
  then
A3: len(Sgm(Seg(k + 1))) = len(idseq(k + 1)) by Th52;
  then
A4: dom Sgm(Seg(k + 1)) = Seg(k+1) by A2,FINSEQ_1:def 3;
  now
    let j be Nat;
    assume
A5: j in dom Sgm(Seg(k + 1));
    then
A6: j in Seg k \/ {k + 1} by A4,FINSEQ_1:11;
    now
      per cases by A6,XBOOLE_0:def 3;
      suppose
A7:     j in Seg k;
A8:     idseq(k + 1).j = j by A4,A5,FUNCT_1:35;
A9:     Sgm(Seg(k + 1)) | Seg k = Sgm(Seg k) by Lm4;
        Sgm(Seg k).j = j by A1,A7,FUNCT_1:35;
        hence Sgm(Seg(k + 1)).j = idseq(k + 1).j by A7,A8,A9,FUNCT_1:72;
      end;
      suppose
A10:    j in {k + 1};
        set Y = Sgm(Seg k);
        set X = Sgm(Seg(k + 1));
A11:    j = k + 1 by A10,TARSKI:def 1;
        then
A12:    j in Seg(k + 1) by FINSEQ_1:6;
        now
          rng X = Seg(k + 1) by FINSEQ_1:def 13;
          then
A13:      X.j in Seg(k + 1) by A5,FUNCT_1:def 5;
          then reconsider n = X.j as Element of NAT;
          assume X.j <> j;
          then not X.j in {j} by TARSKI:def 1;
          then X.j in Seg(k + 1) \ {k + 1} by A11,A13,XBOOLE_0:def 5;
          then
A14:      X.j in Seg k by FINSEQ_1:12;
A15:      X is one-to-one by Lm1;
A16:      dom X = Seg(k + 1) by A2,A3,FINSEQ_1:def 3;
A17:      k < k + 1 by XREAL_1:31;
          X | Seg k = Y by Lm4;
          then
A18:      X.n = Y.n by A14,FUNCT_1:72
            .= n by A1,A14,FUNCT_1:35;
          n <= k by A14,FINSEQ_1:3;
          hence contradiction by A11,A12,A16,A13,A15,A18,A17,FUNCT_1:def 8;
        end;
        hence Sgm(Seg(k + 1)).j = idseq(k + 1).j by A11,FINSEQ_1:6 ,FUNCT_1
        :35;
      end;
    end;
    hence Sgm(Seg(k + 1)).j = idseq(k + 1).j;
  end;
  hence Sgm(Seg(k + 1)) = idseq(k + 1) by A3,FINSEQ_2:10;
end;

theorem Th54:
  Sgm(Seg k) = idseq k
proof
  defpred P[Nat] means Sgm(Seg $1) = idseq $1;
A2: for k being Nat st P[k] holds P[k+1] by Lm5;
A3: P[0] by Th49;
  for k being Nat holds P[k] from NAT_1:sch 2(A3,A2);
  hence thesis;
end;

theorem Th55:
  p | Seg n = p iff len p <= n
proof
  thus p | Seg n = p implies len p <= n by FINSEQ_1:107;
  assume len p <= n;
  then Seg(len p) c= Seg n by FINSEQ_1:7;
  then dom p c= Seg n by FINSEQ_1:def 3;
  then
A1: dom p = dom p /\ Seg n by XBOOLE_1:28;
  for x st x in dom p holds p.x = p.x;
  hence thesis by A1,FUNCT_1:68;
end;

theorem Th56:
  idseq(n + k) | Seg n = idseq n
proof
A1: Sgm(Seg n) = idseq n by Th54;
  Sgm(Seg(n + k)) = idseq(n + k) by Th54;
  hence thesis by A1,Lm4;
end;

theorem
  idseq n | Seg m = idseq m iff m <= n
proof
  thus idseq n | Seg m = idseq m implies m <= n
  proof
    assume idseq n | Seg m = idseq m;
    then len(idseq m) <= len(idseq n) by FINSEQ_1:100;
    then m <= len(idseq n) by FINSEQ_1:def 18;
    hence thesis by FINSEQ_1:def 18;
  end;
  assume m <= n;
  then ex j being Nat st n = m + j by NAT_1:10;
  hence thesis by Th56;
end;

theorem
  idseq n | Seg m = idseq n iff n <= m
proof
  len(idseq n) = n by FINSEQ_1:def 18;
  hence thesis by Th55;
end;

theorem Th59:
  len p = k + l & q = p | Seg k implies len q = k
proof
  assume that
A1: len p = k + l and
A2: q = p | Seg k;
  k <= len p by A1,NAT_1:12;
  hence thesis by A2,FINSEQ_1:21;
end;

theorem
  len p = k + l & q = p | Seg k implies dom q = Seg k
proof
  assume that
A1: len p = k + l and
A2: q = p | Seg k;
  len q = k by A1,A2,Th59;
  hence thesis by FINSEQ_1:def 3;
end;

theorem Th61:
  len p = k + 1 & q = p | Seg k implies p = q ^ <* p.(k + 1) *>
proof
  assume that
A1: len p = k + 1 and
A2: q = p | Seg k;
A3: for l being Element of NAT holds l in dom q implies p.l = q.l by A2,
  FUNCT_1:70;
  set r = <* p.(k + 1) *>;
A4: now
    let l be Element of NAT;
    assume l in dom r;
    then l in {1} by FINSEQ_1:4,55;
    then
A5: l = 1 by TARSKI:def 1;
    hence p.(len q + l) = p.(k + 1) by A1,A2,Th59
      .= r.l by A5,FINSEQ_1:57;
  end;
  len p = len q + 1 by A1,A2,Th59
    .= len q + len<* p.(k + 1) *> by FINSEQ_1:56;
  hence thesis by A3,A4,Th43;
end;

theorem
  p | X is FinSequence iff ex k being Element of NAT st X /\ dom p = Seg k
proof
  thus p | X is FinSequence implies ex k being Element of NAT st X /\ dom p =
  Seg k
  proof
    assume p | X is FinSequence;
    then consider k being Nat such that
A1: dom(p | X) = Seg k by FINSEQ_1:def 2;
    reconsider k as Element of NAT by ORDINAL1:def 13;
    take k;
    thus thesis by A1,RELAT_1:90;
  end;
  given k being Element of NAT such that
A2: X /\ dom p = Seg k;
  dom(p | X) = Seg k by A2,RELAT_1:90;
  hence thesis by FINSEQ_1:def 2;
end;

theorem Th63:
  card((p ^ q) " A) = card(p " A) + card(q " A)
proof
  set X = (p ^ q) " A;
  set B = {len p + n where n is Element of NAT : n in q " A};
  defpred P[set,set] means ex i st $1 = i & $2 = len p + i;
A1: X = p " A \/ B
  proof
    thus X c= p " A \/ B
    proof
      let x;
      assume
A2:   x in X;
      then
A3:   x in dom(p ^ q) by FUNCT_1:def 13;
      then reconsider k = x as Element of NAT;
      now
        per cases by A3,FINSEQ_1:38;
        suppose
A4:       k in dom p;
          then (p ^ q).k = p.k by FINSEQ_1:def 7;
          then p.k in A by A2,FUNCT_1:def 13;
          then k in p " A by A4,FUNCT_1:def 13;
          hence thesis by XBOOLE_0:def 3;
        end;
        suppose
          ex m being Nat st m in dom q & k = len p + m;
          then consider m being Nat such that
A5:       m in dom q and
A6:       k = len p + m;
          q.m = (p ^ q).k by A5,A6,FINSEQ_1:def 7;
          then q.m in A by A2,FUNCT_1:def 13;
          then
A7:       m in q " A by A5,FUNCT_1:def 13;
          m in NAT by ORDINAL1:def 13;
          then k in B by A6,A7;
          hence thesis by XBOOLE_0:def 3;
        end;
      end;
      hence thesis;
    end;
    let x;
    assume
A8: x in p " A \/ B;
    now
      per cases by A8,XBOOLE_0:def 3;
      suppose
A9:     x in p " A;
        then
A10:    x in dom p by FUNCT_1:def 13;
        then reconsider k = x as Element of NAT;
        (p ^ q).k = p.k by A10,FINSEQ_1:def 7;
        then
A11:    (p ^ q).x in A by A9,FUNCT_1:def 13;
        x in dom(p ^ q) by A10,Th24;
        hence thesis by A11,FUNCT_1:def 13;
      end;
      suppose
        x in B;
        then consider n being Element of NAT such that
A12:    x = len p + n and
A13:    n in q " A;
A14:    n in dom q by A13,FUNCT_1:def 13;
        then (p ^ q).(len p + n) = q.n by FINSEQ_1:def 7;
        then
A15:    (p ^ q).x in A by A12,A13,FUNCT_1:def 13;
        x in dom(p ^ q) by A12,A14,FINSEQ_1:41;
        hence thesis by A15,FUNCT_1:def 13;
      end;
    end;
    hence thesis;
  end;
  p " A /\ B = {}
  proof
    consider x being Element of p " A /\ B;
    assume
A16: not thesis;
    then x in B by XBOOLE_0:def 4;
    then consider n being Element of NAT such that
A17: x = len p + n and
A18: n in q " A;
    len p + n in p " A by A16,A17,XBOOLE_0:def 4;
    then len p + n in dom p by FUNCT_1:def 13;
    then len p + n <= len p + 0 by Th27;
    then
A19: n = 0 by XREAL_1:8;
    n in dom q by A18,FUNCT_1:def 13;
    hence thesis by A19,Th26;
  end;
  then
A20: p " A misses B by XBOOLE_0:def 7;
  reconsider B as finite set by A1,FINSET_1:13,XBOOLE_1:7;
A21: card X = card(p " A) + card B by A1,A20,CARD_2:53;
A22: for x st x in q " A ex y st P[x,y]
  proof
    let x;
    assume x in q " A;
    then x in dom q by FUNCT_1:def 13;
    then reconsider i = x as Element of NAT;
    reconsider y = len p + i as set;
    take y;
    take i;
    thus thesis;
  end;
  consider f being Function such that
A23: dom f = q " A and
A24: for x st x in q " A holds P[x,f.x] from CLASSES1:sch 1(A22);
A25: rng f = B
  proof
    thus rng f c= B
    proof
      let x;
      assume x in rng f;
      then consider y such that
A26:  y in dom f and
A27:  f.y = x by FUNCT_1:def 5;
      consider i such that
A28:  y = i and
A29:  f.y = len p + i by A23,A24,A26;
      i is Element of NAT by ORDINAL1:def 13;
      hence thesis by A23,A26,A27,A28,A29;
    end;
    let x;
    assume x in B;
    then consider n being Element of NAT such that
A30: x = len p + n and
A31: n in q " A;
    ex i st n = i & f.n = len p + i by A24,A31;
    hence thesis by A23,A30,A31,FUNCT_1:def 5;
  end;
  f is one-to-one
  proof
    let x,y;
    assume that
A32: x in dom f and
A33: y in dom f and
A34: f.x = f.y;
A35: ex j st y = j & f.y = len p + j by A23,A24,A33;
    ex i st x = i & f.x = len p + i by A23,A24,A32;
    hence thesis by A34,A35;
  end;
  then q " A,B are_equipotent by A23,A25,WELLORD2:def 4;
  hence thesis by A21,CARD_1:21;
end;

theorem Th64:
  p " A c= (p ^ q) " A
proof
  let x;
A1: dom p c= dom(p ^ q) by FINSEQ_1:39;
  assume
A2: x in p " A;
  then
A3: x in dom p by FUNCT_1:def 13;
  then reconsider k = x as Element of NAT;
A4: p.k in A by A2,FUNCT_1:def 13;
  p.k = (p ^ q).k by A3,FINSEQ_1:def 7;
  hence thesis by A3,A1,A4,FUNCT_1:def 13;
end;

definition
  let p,A;
  func p - A -> FinSequence equals
  p * Sgm ((dom p) \ p " A);
  coherence
  proof
    now
      assume p <> {};
      then reconsider D = Seg(len p) as non empty Subset of NAT;
      Seg(len p) \ p " A c= Seg(len p) by XBOOLE_1:36;
      then rng Sgm(Seg(len p) \ p " A) c= D by FINSEQ_1:def 13;
      then reconsider q = Sgm(Seg(len p) \ p " A) as FinSequence of D by
      FINSEQ_1:def 4;
      reconsider r = p * q as FinSequence by FINSEQ_2:34;
      take rr = r;
      thus rr = p * Sgm((dom p) \ p " A) by FINSEQ_1:def 3;
    end;
    hence thesis;
  end;
end;

canceled;

theorem Th66:
  len(p - A) = len p - card(p " A)
proof
  set q = Sgm(Seg(len p) \ p " A);
A1: Seg len p = dom p by FINSEQ_1:def 3;
  Seg(len p) \ p " A c= Seg(len p) by XBOOLE_1:36;
  then rng q c= dom p by A1,FINSEQ_1:def 13;
  then
A2: dom q = dom(p - A) by A1,RELAT_1:46;
A3: dom q = Seg(len q) by FINSEQ_1:def 3;
A4: p " A c= Seg(len p)
  proof
    let x;
A5: p " A c= dom p by RELAT_1:167;
A6: dom p = Seg(len p) by FINSEQ_1:def 3;
    assume x in p " A;
    hence thesis by A5,A6;
  end;
  len q = card(Seg(len p) \ p " A) by Th44,XBOOLE_1:36;
  then len(p - A) = card(Seg(len p) \ p " A) by A2,A3,FINSEQ_1:def 3;
  hence len(p - A) = card(Seg(len p)) - card(p " A) by A4,CARD_2:63
    .= len p - card(p " A) by FINSEQ_1:78;
end;

theorem Th67:
  len(p - A) <= len p
proof
  len(p - A) = len p - card(p " A) by Th66;
  hence thesis by XREAL_1:45;
end;

theorem Th68:
  len(p - A) = len p implies A misses rng p
proof
  assume
A1: len(p - A) = len p;
A2: len(p - A) = len p - card(p " A) by Th66;
  assume A meets rng p;
  then p " A <> {} by RELAT_1:173;
  hence thesis by A1,A2;
end;

theorem
  n = len p - card(p " A) implies dom(p - A) = Seg n
proof
  assume n = len p - card(p " A);
  then len(p - A) = n by Th66;
  hence thesis by FINSEQ_1:def 3;
end;

theorem
  dom(p - A) c= dom p
proof
A1: dom(p - A) = Seg(len(p - A)) by FINSEQ_1:def 3;
A2: dom p = Seg(len p) by FINSEQ_1:def 3;
  len(p - A) <= len p by Th67;
  hence thesis by A1,A2,FINSEQ_1:7;
end;

theorem
  dom(p - A) = dom p implies A misses rng p
proof
A1: dom(p - A) = Seg(len(p - A)) by FINSEQ_1:def 3;
A2: dom p = Seg(len p) by FINSEQ_1:def 3;
  assume dom(p - A) = dom p;
  hence thesis by A1,A2,Th68,FINSEQ_1:8;
end;

theorem Th72:
  rng(p - A) = rng p \ A
proof
  set q = Sgm(Seg(len p) \ p " A);
A1: dom p = Seg(len p) by FINSEQ_1:def 3;
A2: dom p = Seg len p by FINSEQ_1:def 3;
  thus rng(p - A) c= rng p \ A
  proof
    let x;
A3: rng(p * q) c= rng p by RELAT_1:45;
    assume
A4: x in rng(p - A);
A5: now
A6:   Seg(len p) \ p " A c= Seg(len p) by XBOOLE_1:36;
      assume
A7:   x in A;
      consider y such that
A8:   y in dom(p - A) and
A9:   (p - A).y = x by A4,FUNCT_1:def 5;
      set z = q.y;
A10:  y in dom q by A2,A8,FUNCT_1:21;
      then
A11:  (p - A).y = p.z by A2,FUNCT_1:23;
      z in rng q by A10,FUNCT_1:def 5;
      then z in Seg(len p) \ p " A by A6,FINSEQ_1:def 13;
      then
A12:  not z in p " A by XBOOLE_0:def 5;
      z in dom p by A2,A8,FUNCT_1:21;
      hence contradiction by A7,A9,A11,A12,FUNCT_1:def 13;
    end;
    x in rng(p * q) by A4,FINSEQ_1:def 3;
    hence thesis by A3,A5,XBOOLE_0:def 5;
  end;
  let x;
  assume
A13: x in rng p \ A;
  then consider y such that
A14: y in dom p and
A15: p.y = x by FUNCT_1:def 5;
  Seg(len p) \ p " A c= Seg(len p) by XBOOLE_1:36;
  then
A16: rng q = Seg(len p) \ p " A by FINSEQ_1:def 13;
  not p.y in A by A13,A15,XBOOLE_0:def 5;
  then not y in p " A by FUNCT_1:def 13;
  then y in rng q by A14,A1,A16,XBOOLE_0:def 5;
  then consider z such that
A17: z in dom q and
A18: q.z = y by FUNCT_1:def 5;
A19: (p - A).z = x by A2,A15,A17,A18,FUNCT_1:23;
  z in dom(p - A) by A2,A14,A17,A18,FUNCT_1:21;
  hence thesis by A19,FUNCT_1:def 5;
end;

theorem
  rng(p - A) c= rng p
proof
  rng(p - A) = rng p \ A by Th72;
  hence thesis;
end;

theorem
  rng(p - A) = rng p implies A misses rng p
proof
  assume rng(p - A) = rng p;
  then rng p \ A = rng p by Th72;
  hence thesis by XBOOLE_1:83;
end;

theorem Th75:
  p - A = {} iff rng p c= A
proof
  thus p - A = {} implies rng p c= A
  proof
    assume that
A1: p - A = {} and
A2: not rng p c= A;
    rng p \ A <> {} by A2,XBOOLE_1:37;
    then rng(p - A) <> {} by Th72;
    hence thesis by A1;
  end;
  assume
A3: rng p c= A;
  rng(p - A) = rng p \ A by Th72;
  hence thesis by A3,XBOOLE_1:37;
end;

theorem Th76:
  p - A = p iff A misses rng p
proof
  thus p - A = p implies A misses rng p
  proof
    assume that
A1: p - A = p and
A2: not A misses rng p;
    len(p - A) <> len p by A2,Th68;
    hence contradiction by A1;
  end;
  assume A misses rng p;
  then p " A = {} by RELAT_1:173;
  then Sgm(Seg(len p) \ p " A) = idseq(len p) by Th54;
  then p * Sgm(Seg(len p) \ p " A) = p by FINSEQ_2:64;
  hence thesis by FINSEQ_1:def 3;
end;

theorem
  p - {x} = p iff not x in rng p
proof
  thus p - {x} = p implies not x in rng p
  proof
    assume p - {x} = p;
    then
A1: {x} misses rng p by Th76;
    x in {x} by TARSKI:def 1;
    hence thesis by A1,XBOOLE_0:3;
  end;
  assume
A2: not x in rng p;
  {x} misses rng p
  proof
    assume {x} meets rng p;
    then ex y st y in {x} & y in rng p by XBOOLE_0:3;
    hence thesis by A2,TARSKI:def 1;
  end;
  hence thesis by Th76;
end;

theorem
  p - {} = p
proof
  {} misses rng p by XBOOLE_1:65;
  hence thesis by Th76;
end;

theorem
  p - rng p = {} by Th75;

Lm6: <* x *> - A = <* x *> iff not x in A
proof
  thus <* x *> - A = <* x *> implies not x in A
  proof
    assume <* x *> - A = <* x *>;
    then
A1: rng<* x *> misses A by Th76;
A2: rng<* x *> = {x} by FINSEQ_1:56;
    x in {x} by TARSKI:def 1;
    hence thesis by A2,A1,XBOOLE_0:3;
  end;
  assume
A3: not x in A;
  rng<* x *> misses A
  proof
    assume rng<* x *> meets A;
    then consider y such that
A4: y in rng<* x *> and
A5: y in A by XBOOLE_0:3;
    y in {x} by A4,FINSEQ_1:56;
    hence thesis by A3,A5,TARSKI:def 1;
  end;
  hence thesis by Th76;
end;

Lm7: <* x *> - A = {} iff x in A
proof
A1: rng<* x *> = {x} by FINSEQ_1:56;
  thus <* x *> - A = {} implies x in A
  proof
    assume <* x *> - A = {};
    then rng<* x *> c= A by Th75;
    then {x} c= A by FINSEQ_1:56;
    hence thesis by ZFMISC_1:37;
  end;
  assume x in A;
  then rng<* x *> c= A by A1,ZFMISC_1:37;
  hence thesis by Th75;
end;

Lm8: (p ^ {}) - A = (p - A) ^ ({} - A)
proof
  thus (p ^ {}) - A = p - A by FINSEQ_1:47
    .= (p - A) ^ ({} - A) by FINSEQ_1:47;
end;

Lm9: (p ^ <* x *>) - A = (p - A) ^ (<* x *> - A)
proof
  set r = (p ^ <* x *>) - A;
  set q = <* x *> - A;
  set t = p ^ <* x *>;
A1: len t = len p + len<* x *> by FINSEQ_1:35
    .= len p + 1 by FINSEQ_1:57;
A2: now
    t " A c= dom t by RELAT_1:167;
    then
A3: t " A c= Seg(len t) by FINSEQ_1:def 3;
    set S = Seg(len(p ^ <* x *>)) \ (p ^ <* x *>) " A;
    set s = Sgm(Seg(len(p ^ <* x *>)) \ (p ^ <* x *>) " A);
    let k be Element of NAT;
A4: S c= Seg(len t) by XBOOLE_1:36;
    assume
A5: k in dom q;
    then
A6: not x in A by Lm7,RELAT_1:60;
    then
A7: q = <* x *> by Lm6;
    then
A8: dom q = {1} by FINSEQ_1:4,def 8;
    then
A9: k = 1 by A5,TARSKI:def 1;
A10: p " A = (p ^ <* x *>) " A
    proof
      thus p " A c= (p ^ <* x *>) " A
      proof
        let y;
        assume
A11:    y in p " A;
        then
A12:    y in dom p by FUNCT_1:def 13;
        then reconsider z = y as Element of NAT;
        p.y in A by A11,FUNCT_1:def 13;
        then
A13:    t.z in A by A12,FINSEQ_1:def 7;
        y in dom t by A12,Th24;
        hence thesis by A13,FUNCT_1:def 13;
      end;
      let y;
      assume
A14:  y in (p ^ <* x *>) " A;
      then
A15:  y in dom t by FUNCT_1:def 13;
      then reconsider z = y as Element of NAT;
A16:  t.y in A by A14,FUNCT_1:def 13;
A17:  now
        given n such that
A18:    n in dom q and
A19:    z = len p + n;
        n = 1 by A8,A18,TARSKI:def 1;
        hence contradiction by A6,A16,A19,FINSEQ_1:59;
      end;
A20:  z in dom p or ex n being Nat st n in dom q & z = len p + n by A7
      ,A15,FINSEQ_1:38;
      then t.z = p.z by A17,FINSEQ_1:def 7;
      hence thesis by A16,A20,A17,FUNCT_1:def 13;
    end;
A21: now
      assume
A22:  len t in t " A;
      t.(len t) = x by A1,FINSEQ_1:59;
      hence contradiction by A6,A22,FUNCT_1:def 13;
    end;
A23: dom s = Seg(len s) by FINSEQ_1:def 3;
    len t in Seg(len t) by A1,FINSEQ_1:6;
    then len t in S by A21,XBOOLE_0:def 5;
    then len t in rng s by A4,FINSEQ_1:def 13;
    then consider y such that
A24: y in dom s and
A25: s.y = len t by FUNCT_1:def 5;
    reconsider y as Element of NAT by A24;
A26: len(p - A) + k = len p - card(p " A) + k by Th66
      .= len p + 1 - card((p ^ <* x *>) " A) by A9,A10
      .= len p + len<* x *> - card((p ^ <* x *>) " A) by FINSEQ_1:56
      .= len(p ^ <* x *>) - card((p ^ <* x *>) " A) by FINSEQ_1:35
      .= len r by Th66;
A27: len s = card S by Th44,XBOOLE_1:36
      .= card(Seg(len t)) - card(t " A) by A3,CARD_2:63
      .= len t - card(p " A) by A10,FINSEQ_1:78
      .= len p - card(p " A) + k by A1,A9
      .= len r by A26,Th66;
    then
A28: len s = len(p - A) + 1 by A5,A8,A26,TARSKI:def 1;
    then
A29: len r in dom s by A27,A23,FINSEQ_1:6;
A30: now
A31:  s.(len s) in rng s by A27,A29,FUNCT_1:def 5;
      reconsider w = s.(len s) as Element of NAT by A31;
      w in S by A4,A31,FINSEQ_1:def 13;
      then
A32:  w in Seg(len t) by XBOOLE_0:def 5;
      assume
A33:  y <> len s;
A34:  y in Seg(len s) by A24,FINSEQ_1:def 3;
      then y <= len s by FINSEQ_1:3;
      then
A35:  y < len s by A33,XXREAL_0:1;
      1 <= y by A34,FINSEQ_1:3;
      then len t < w by A4,A25,A35,FINSEQ_1:def 13;
      hence contradiction by A32,FINSEQ_1:3;
    end;
    dom t = Seg len t by FINSEQ_1:def 3;
    hence r.(len(p - A) + k) = t.(len t) by A26,A27,A28,A23,A25,A30,
    FINSEQ_1:6,FUNCT_1:23
      .= x by A1,FINSEQ_1:59
      .= q.k by A7,A9,FINSEQ_1:def 8;
  end;
A36: now
A37: x in A implies t " A = p " A \/ {len p + 1}
    proof
      assume
A38:  x in A;
      thus t " A c= p " A \/ {len p + 1}
      proof
        let y;
        assume
A39:    y in t " A;
        then y in dom t by FUNCT_1:def 13;
        then y in Seg(len p + 1) by A1,FINSEQ_1:def 3;
        then
A40:    y in Seg(len p) \/ {len p + 1} by FINSEQ_1:11;
        now
          per cases by A40,XBOOLE_0:def 3;
          suppose
A41:        y in Seg(len p);
            then reconsider j = y as Element of NAT;
A42:        t.y in A by A39,FUNCT_1:def 13;
A43:        y in dom p by A41,FINSEQ_1:def 3;
            then p.j = t.j by FINSEQ_1:def 7;
            then y in p " A by A43,A42,FUNCT_1:def 13;
            hence thesis by XBOOLE_0:def 3;
          end;
          suppose
            y in {len p + 1};
            hence thesis by XBOOLE_0:def 3;
          end;
        end;
        hence thesis;
      end;
      let y;
      assume
A44:  y in p " A \/ {len p + 1};
      now
        per cases by A44,XBOOLE_0:def 3;
        suppose
A45:      y in p " A;
          p " A c= t " A by Th64;
          hence thesis by A45;
        end;
        suppose
          y in {len p + 1};
          then
A46:      y = len p + 1 by TARSKI:def 1;
          then y in Seg(len t) by A1,FINSEQ_1:6;
          then
A47:      y in dom t by FINSEQ_1:def 3;
          t.y in A by A38,A46,FINSEQ_1:59;
          hence thesis by A47,FUNCT_1:def 13;
        end;
      end;
      hence thesis;
    end;
    t " A c= dom t by RELAT_1:167;
    then
A48: t " A c= Seg(len t) by FINSEQ_1:def 3;
A49: not x in A implies t " A = p " A
    proof
      assume
A50:  not x in A;
      thus t " A c= p " A
      proof
        let y;
        assume
A51:    y in t " A;
        then
A52:    y in dom t by FUNCT_1:def 13;
        then reconsider l = y as Element of NAT;
A53:    now
          assume l = len p + 1;
          then t.l = x by FINSEQ_1:59;
          hence contradiction by A50,A51,FUNCT_1:def 13;
        end;
A54:    y in Seg(len t) by A52,FINSEQ_1:def 3;
        then l <= len p + 1 by A1,FINSEQ_1:3;
        then l < len p + 1 by A53,XXREAL_0:1;
        then
A55:    l <= len p by NAT_1:13;
        1 <= l by A54,FINSEQ_1:3;
        then l in Seg(len p) by A55,FINSEQ_1:3;
        then
A56:    y in dom p by FINSEQ_1:def 3;
        t.l in A by A51,FUNCT_1:def 13;
        then p.l in A by A56,FINSEQ_1:def 7;
        hence thesis by A56,FUNCT_1:def 13;
      end;
      thus thesis by Th64;
    end;
    set s2 = Sgm(Seg(len p) \ p " A);
    set s1 = Sgm(Seg(len t) \ t " A);
    let k be Element of NAT;
A57: dom p = Seg len p by FINSEQ_1:def 3;
A58: now
      {len p + 1} /\ p " A = {}
      proof
        consider z being Element of {len p + 1} /\ p " A;
A59:    p " A c= dom p by RELAT_1:167;
        assume
A60:    not thesis;
        then z in {len p + 1} by XBOOLE_0:def 4;
        then
A61:    z = len p + 1 by TARSKI:def 1;
A62:    dom p = Seg(len p) by FINSEQ_1:def 3;
        z in p " A by A60,XBOOLE_0:def 4;
        then len p + 1 <= len p by A61,A59,A62,FINSEQ_1:3;
        hence thesis by XREAL_1:31;
      end;
      then
A63:  {len p + 1} misses p " A by XBOOLE_0:def 7;
      assume t " A = p " A;
      hence Seg(len t) \ t " A = (Seg(len p) \/ {len p + 1}) \ p " A by A1,
      FINSEQ_1:11
        .= (Seg(len p) \ p " A) \/ ({len p + 1} \ p " A) by XBOOLE_1:42
        .= (Seg(len p) \ p " A) \/ {len p + 1} by A63,XBOOLE_1:83;
    end;
    Seg(len p) /\ {len p + 1} = {}
    proof
      consider z being Element of Seg(len p) /\ {len p + 1};
      assume
A64:  not thesis;
      then
A65:  z in Seg(len p) by XBOOLE_0:def 4;
      then reconsider f = z as Element of NAT;
      f in {len p + 1} by A64,XBOOLE_0:def 4;
      then
A66:  f = len p + 1 by TARSKI:def 1;
      f <= len p by A65,FINSEQ_1:3;
      hence thesis by A66,XREAL_1:31;
    end;
    then
A67: Seg(len p) misses {len p + 1} by XBOOLE_0:def 7;
A68: now
      assume t " A = p " A \/ {len p + 1};
      hence
      Seg(len t) \ t " A = (Seg(len p) \/ {len p + 1}) \ (p " A \/ {len p
      + 1}) by A1, FINSEQ_1:11
        .= (Seg(len p) \ (p " A \/ {len p + 1}) \/ ({len p + 1} \ (p " A \/
      {len p + 1}))) by XBOOLE_1:42
        .= (Seg(len p) \ (p " A \/ {len p + 1})) \/ {} by XBOOLE_1:46
        .= (Seg(len p) \ p " A) /\ (Seg(len p) \ {len p + 1}) by XBOOLE_1:53
        .= (Seg(len p) \ p " A) /\ Seg(len p) by A67,XBOOLE_1:83
        .= (Seg(len p) \ p " A) by XBOOLE_1:28,36;
    end;
    p " A c= dom p by RELAT_1:167;
    then
A69: p " A c= Seg(len p) by FINSEQ_1:def 3;
    len s2 = card(Seg(len p) \ p " A) by Th44,XBOOLE_1:36
      .= card(Seg(len p)) - card(p " A) by A69,CARD_2:63;
    then
A70: len s2 = len p - card(p " A) by FINSEQ_1:78;
    Seg(len t) \ t " A c= Seg(len t) by XBOOLE_1:36;
    then
A71: rng s1 c= Seg(len t) by FINSEQ_1:def 13;
A72: Seg(len p) \ p " A c= Seg(len p) by XBOOLE_1:36;
    then
A73: rng s2 c= Seg(len p) by FINSEQ_1:def 13;
    assume k in dom(p - A);
    then
A74: k in dom s2 by A57,FUNCT_1:21;
    then s2.k in rng s2 by FUNCT_1:def 5;
    then s2.k in Seg(len p) by A73;
    then
A75: s2.k in dom p by FINSEQ_1:def 3;
    len s1 = card(Seg(len t) \ t " A) by Th44,XBOOLE_1:36
      .= card(Seg(len t)) - card(t " A) by A48,CARD_2:63;
    then
A76: len s1 = len t - card(t " A) by FINSEQ_1:78;
A77: dom s2 c= dom s1
    proof
      <* x *> " A c= dom<* x *> by RELAT_1:167;
      then <* x *> " A c= {1} by FINSEQ_1:4,def 8;
      then
A78:  <* x *> " A = {} or <* x *> " A = {1} by ZFMISC_1:39;
      let y;
A79:  card(p " A) <= card(p " A) + 1 by NAT_1:12;
      assume
A80:  y in dom s2;
      then reconsider l = y as Element of NAT;
A81:  y in Seg(len s2) by A80,FINSEQ_1:def 3;
      then l <= len p - card(p " A) by A70,FINSEQ_1:3;
      then l + card(p " A) <= len p by XREAL_1:21;
      then
A82:  l + card(p " A) + 1 <= len t by A1,XREAL_1:9;
      card(t " A) = card(p " A) + card(<* x *> " A) by Th63;
      then l + card(t " A) <= l + (card(p " A) + 1) by A78,A79,CARD_1:47,
      50 ,XREAL_1:9;
      then l + card(t " A) <= len t by A82,XXREAL_0:2;
      then
A83:  l <= len s1 by A76,XREAL_1:21;
      1 <= l by A81,FINSEQ_1:3;
      then l in Seg(len s1) by A83,FINSEQ_1:3;
      hence thesis by FINSEQ_1:def 3;
    end;
    then s1.k in rng s1 by A74,FUNCT_1:def 5;
    then s1.k in Seg(len t) by A71;
    then reconsider l = s1.k as Element of NAT;
    dom t = Seg len t by FINSEQ_1:def 3;
    then
A84: r.k = t.l by A74,A77,FUNCT_1:23;
    len p + 1 in Seg(len p + 1) by FINSEQ_1:6;
    then
A85: {len p + 1} c= Seg(len p + 1) by ZFMISC_1:37;
A86: now
      per cases by A37,A49,A58,A68;
      suppose
        Seg(len p) \ p " A = Seg(len t) \ t " A;
        hence s1.k = s2.k;
      end;
      suppose
A87:    Seg(len t) \ t " A = (Seg(len p) \ p " A) \/ {len p + 1};
        now
          let m,n be Element of NAT;
          assume that
A88:      m in Seg(len p) \ p " A and
A89:      n in {len p + 1};
          m in Seg(len p) by A88,XBOOLE_0:def 5;
          then
A90:      m <= len p by FINSEQ_1:3;
A91:      len p < len p + 1 by XREAL_1:31;
          n = len p + 1 by A89,TARSKI:def 1;
          hence m < n by A90,A91,XXREAL_0:2;
        end;
        then s1 = s2 ^ Sgm{len p + 1} by A72,A85,A87,Th48;
        hence s1.k = s2.k by A74,FINSEQ_1:def 7;
      end;
    end;
    (p - A).k = p.(s2.k) by A57,A74,FUNCT_1:23;
    hence r.k = (p - A).k by A75,A86,A84,FINSEQ_1:def 7;
  end;
  len r = len(p ^ <* x *>) - card(t " A) by Th66
    .= len p + len<* x *> - card(t " A) by FINSEQ_1:35
    .= (len p + len<* x *>) - (card(p " A) + card(<* x *> " A)) by Th63
    .= len p - card(p " A) + len<* x *> + (- card(<* x *> " A))
    .= len(p - A) + len<* x *> + (- card(<* x *> " A)) by Th66
    .= len(p - A) + (len<* x *> - card(<* x *> " A))
    .= len(p - A) + len q by Th66;
  hence thesis by A36,A2,Th43;
end;

Lm10: now
  let q,x;
  assume
A1: for p,A holds (p ^ q) - A = (p - A) ^ (q - A);
  let p,A;
  thus (p ^ (q ^ <* x *>)) - A = (p ^ q ^ <* x *>) - A by FINSEQ_1:45
    .= ((p ^ q) - A) ^ (<* x *> - A) by Lm9
    .= (p - A) ^ (q - A) ^ (<* x *> - A) by A1
    .= (p - A) ^ ((q - A) ^ (<* x *> - A)) by FINSEQ_1:45
    .= (p - A) ^ ((q ^ <* x *>) - A) by Lm9;
end;

Lm11: for q,p,A holds (p ^ q) - A = (p - A) ^ (q - A)
proof
  defpred P[FinSequence] means (p ^ $1) - A = (p - A) ^ ($1 - A);
A1: for q,x st P[q] holds P[q^<*x*>] by Lm10;
A2: P[{}] by Lm8;
  for q holds P[q] from FINSEQ_1:sch 3(A2,A1);
  hence thesis;
end;

theorem
  (p ^ q) - A = (p - A) ^ (q - A) by Lm11;

theorem
  {} - A = {};

theorem
  <* x *> - A = <* x *> iff not x in A by Lm6;

theorem
  <* x *> - A = {} iff x in A by Lm7;

theorem Th84:
  <* x,y *> - A = {} iff x in A & y in A
proof
A1: <* x,y *> = <* x *> ^ <* y *> by FINSEQ_1:def 9;
  thus <* x,y *> - A = {} implies x in A & y in A
  proof
    assume <* x,y *> - A = {};
    then rng<* x,y *> c= A by Th75;
    then {x,y} c= A by FINSEQ_2:147;
    hence thesis by ZFMISC_1:38;
  end;
  assume that
A2: x in A and
A3: y in A;
A4: <* y *> - A = {} by A3,Lm7;
  <* x *> - A = {} by A2,Lm7;
  hence <* x,y *> - A = {} ^ {} by A4,A1,Lm11
    .= {};
end;

theorem Th85:
  x in A & not y in A implies <* x,y *> - A = <* y *>
proof
  assume that
A1: x in A and
A2: not y in A;
A3: <* y *> - A = <* y *> by A2,Lm6;
A4: <* x,y *> = <* x *> ^ <* y *> by FINSEQ_1:def 9;
  <* x *> - A = {} by A1,Lm7;
  hence <* x,y *> - A = {} ^ <* y *> by A3,A4,Lm11
    .= <* y *> by FINSEQ_1:47;
end;

theorem
  <* x,y *> - A = <* y *> & x <> y implies x in A & not y in A
proof
  assume that
A1: <* x,y *> - A = <* y *> and
A2: x <> y;
  assume
A3: not thesis;
A4: y in A implies <* y *> - A = {} by Lm7;
A5: not x in A implies <* x *> - A = <* x *> by Lm6;
A6: not y in A implies <* y *> - A = <* y *> by Lm6;
A7: x in A implies <* x *> - A = {} by Lm7;
A8: <* x *>.1 = x by FINSEQ_1:57;
  <* y *> = (<* x *> ^ <* y *>) - A by A1,FINSEQ_1:def 9
    .= (<* x *> - A) ^ (<* y *> - A) by Lm11;
  then <* y *> = {} or <* x *> = <* y *> or <* y *> = <* x,y *> by
  A7,A5,A4,
  A6,A3,FINSEQ_1:47,def 9;
  hence thesis by A2,A8,Th40,FINSEQ_1:57;
end;

theorem Th87:
  not x in A & y in A implies <* x,y *> - A = <* x *>
proof
  assume that
A1: not x in A and
A2: y in A;
A3: <* y *> - A = {} by A2,Lm7;
A4: <* x,y *> = <* x *> ^ <* y *> by FINSEQ_1:def 9;
  <* x *> - A = <* x *> by A1,Lm6;
  hence <* x,y *> - A = <* x *> ^ {} by A3,A4,Lm11
    .= <* x *> by FINSEQ_1:47;
end;

theorem
  <* x,y *> - A = <* x *> & x <> y implies not x in A & y in A
proof
  assume that
A1: <* x,y *> - A = <* x *> and
A2: x <> y;
  assume
A3: not thesis;
A4: y in A implies <* y *> - A = {} by Lm7;
A5: not x in A implies <* x *> - A = <* x *> by Lm6;
A6: not y in A implies <* y *> - A = <* y *> by Lm6;
A7: x in A implies <* x *> - A = {} by Lm7;
A8: <* x *>.1 = x by FINSEQ_1:57;
  <* x *> = (<* x *> ^ <* y *>) - A by A1,FINSEQ_1:def 9
    .= (<* x *> - A) ^ (<* y *> - A) by Lm11;
  then <* x *> = {} or <* x *> = <* y *> or <* x *> = <* x,y *> by
  A7,A5,A4,
  A6,A3,FINSEQ_1:47,def 9;
  hence thesis by A2,A8,Th40,FINSEQ_1:57;
end;

theorem
  <* x,y *> - A = <* x,y *> iff not x in A & not y in A
proof
A1: <* x,y *> = <* x *> ^ <* y *> by FINSEQ_1:def 9;
  thus <* x,y *> - A = <* x,y *> implies not x in A & not y in A
  proof
    assume
A2: <* x,y *> - A = <* x,y *>;
    assume not thesis;
    then x in A & y in A or not x in A & y in A or x in A & not y in A;
    then
    <* x,y *> - A = {} or <* x,y *> - A = <* x *> or <* x,y *> - A = <* y
    *> by Th84,Th85,Th87;
    hence thesis by A2,Th40;
  end;
  assume that
A3: not x in A and
A4: not y in A;
A5: <* y *> - A = <* y *> by A4,Lm6;
  <* x *> - A = <* x *> by A3,Lm6;
  hence thesis by A5,A1,Lm11;
end;

theorem Th90:
  len p = k + 1 & q = p | Seg k implies (p.(k + 1) in A iff p - A = q - A)
proof
  assume that
A1: len p = k + 1 and
A2: q = p | Seg k;
  thus p.(k + 1) in A implies p - A = q - A
  proof
    assume
A3: p.(k + 1) in A;
    thus p - A = (q ^ <* p.(k + 1) *>) - A by A1,A2,Th61
      .= (q - A) ^ (<* p.(k + 1) *> - A) by Lm11
      .= (q - A) ^ {} by A3,Lm7
      .= q - A by FINSEQ_1:47;
  end;
  assume that
A4: p - A = q - A and
A5: not p.(k + 1) in A;
  q - A = (q ^ <* p.(k + 1) *>) - A by A1,A2,A4,Th61
    .= (q - A) ^ (<* p.(k + 1) *> - A) by Lm11
    .= (q - A) ^ <* p.(k + 1) *> by A5,Lm6;
  hence thesis by FINSEQ_1:108;
end;

theorem Th91:
  len p = k + 1 & q = p | Seg k implies (not p.(k + 1) in A iff p
  - A = (q - A) ^ <* p.(k + 1) *>)
proof
  assume that
A1: len p = k + 1 and
A2: q = p | Seg k;
  thus not p.(k + 1) in A implies p - A = (q - A) ^ <* p.(k + 1) *>
  proof
    assume
A3: not p.(k + 1) in A;
    thus p - A = (q ^ <* p.(k + 1) *>) - A by A1,A2,Th61
      .= (q - A) ^ (<* p.(k + 1) *> - A) by Lm11
      .= (q - A) ^ <* p.(k + 1) *> by A3,Lm6;
  end;
  assume
A4: p - A = (q - A) ^ <* p.(k + 1) *>;
  assume p.(k + 1) in A;
  then q - A = (q - A) ^ <* p.(k + 1) *> by A1,A2,A4,Th90;
  hence contradiction by FINSEQ_1:108;
end;

Lm12: for l st (for p,A st len p = l for n st n in dom p for B being finite
set st B = {k where k is Element of NAT : k in dom p & k <= n & p.k in A} holds
p.n in A or (p - A).(n - card B) = p.n) holds for p,A st len p = l + 1 for n st
n in dom p for C being finite set st C = {m where m is Element of NAT : m in
dom p & m <= n & p.m in A} holds p.n in A or (p - A).(n - card C) = p.n
proof
  let l;
  assume
A1: for p,A st len p = l for n st n in dom p for B being finite set st B
= {k where k is Element of NAT : k in dom p & k <= n & p.k in A} holds p.n in A
  or (p - A).(n - card B) = p.n;
  let p,A;
  reconsider q = p | Seg l as FinSequence by FINSEQ_1:19;
  assume
A2: len p = l + 1;
  then
A3: len q = l by Th59;
  let n;
  set B = {k where k is Element of NAT: k in dom q & k <= n & q.k in A};
  B c= dom q
  proof
    let x be set;
    assume x in B;
    then
    ex k being Element of NAT st x = k & k in dom q & k <= n & q.k in A;
    hence thesis;
  end;
  then reconsider B as finite set;
  assume
A4: n in dom p;
  let C be finite set such that
A5: C = {m where m is Element of NAT: m in dom p & m <= n & p.m in A};
  assume
A6: not p.n in A;
  now
    per cases;
    suppose
A7:   p.(l + 1) in A;
A8:   n in Seg(l + 1) by A2,A4,FINSEQ_1:def 3;
      not n in {l + 1} by A6,A7,TARSKI:def 1;
      then n in Seg(l + 1) \ {l + 1} by A8,XBOOLE_0:def 5;
      then
A9:   n in Seg l by FINSEQ_1:12;
A10:  B = C
      proof
        thus B c= C
        proof
          let x;
A11:      dom q c= dom p by RELAT_1:89;
          assume x in B;
          then consider k being Element of NAT such that
A12:      x = k and
A13:      k in dom q and
A14:      k <= n and
A15:      q.k in A;
          p.k in A by A13,A15,FUNCT_1:70;
          hence thesis by A5,A12,A13,A14,A11;
        end;
        let x;
        assume x in C;
        then consider m being Element of NAT such that
A16:    x = m and
A17:    m in dom p and
A18:    m <= n and
A19:    p.m in A by A5;
        m in Seg(len p) by A17,FINSEQ_1:def 3;
        then
A20:    1 <= m by FINSEQ_1:3;
        n <= l by A9,FINSEQ_1:3;
        then m <= l by A18,XXREAL_0:2;
        then m in Seg l by A20,FINSEQ_1:3;
        then
A21:    m in dom q by A3,FINSEQ_1:def 3;
        then q.m in A by A19,FUNCT_1:70;
        hence thesis by A16,A18,A21;
      end;
A22:  q - A = p - A by A2,A7,Th90;
A23:  n in dom q by A3,A9,FINSEQ_1:def 3;
      then p.n = q.n by FUNCT_1:70;
      hence thesis by A1,A6,A3,A23,A22,A10;
    end;
    suppose
      not p.(l + 1) in A;
      then
A24:  p - A = (q - A) ^ <* p.(l + 1) *> by A2,Th91;
      now
        per cases;
        suppose
A25:      n = l + 1;
          p " A = C
          proof
            thus p " A c= C
            proof
              let x;
              assume
A26:          x in p " A;
              then
A27:          x in dom p by FUNCT_1:def 13;
              then reconsider z = x as Element of NAT;
A28:          p.x in A by A26,FUNCT_1:def 13;
              z in Seg n by A2,A25,A27,FINSEQ_1:def 3;
              then z <= n by FINSEQ_1:3;
              hence thesis by A5,A27,A28;
            end;
            let x;
            assume x in C;
            then
            ex m being Element of NAT st x = m & m in dom p & m <= n & p.
            m in A by A5;
            hence thesis by FUNCT_1:def 13;
          end;
          then
A29:      len(p - A) = n - card C by A2,A25,Th66;
          len<* p.(l + 1) *> = 1 by FINSEQ_1:56;
          then (p - A).(n - card C) = (p - A).(len(q - A) + 1) by A24,
          A29,
          FINSEQ_1:35
            .= p.(l + 1) by A24,FINSEQ_1:59;
          hence thesis by A25;
        end;
        suppose
          n <> l + 1;
          then
A30:      not n in {l + 1} by TARSKI:def 1;
          n in Seg(l + 1) by A2,A4,FINSEQ_1:def 3;
          then n in Seg(l + 1) \ {l + 1} by A30,XBOOLE_0:def 5;
          then
A31:      n in Seg l by FINSEQ_1:12;
          then 1 <= n by FINSEQ_1:3;
          then n in Seg n by FINSEQ_1:3;
          then
A32:      {n} c= Seg n by ZFMISC_1:37;
A33:      Seg n \ B c= Seg l \ q " A
          proof
            let x;
            assume
A34:        x in Seg n \ B;
            then reconsider z = x as Element of NAT;
A35:        x in Seg n by A34,XBOOLE_0:def 5;
            then
A36:        1 <= z by FINSEQ_1:3;
A37:        z <= n by A35,FINSEQ_1:3;
A38:        now
              assume
A39:          z in q " A;
              then
A40:          z in dom q by FUNCT_1:def 13;
              q.z in A by A39,FUNCT_1:def 13;
              then z in B by A37,A40;
              hence contradiction by A34,XBOOLE_0:def 5;
            end;
            n <= l by A31,FINSEQ_1:3;
            then z <= l by A37,XXREAL_0:2;
            then z in Seg l by A36,FINSEQ_1:3;
            hence thesis by A38,XBOOLE_0:def 5;
          end;
A41:      B = C
          proof
            thus B c= C
            proof
              let x;
A42:          dom q c= dom p by RELAT_1:89;
              assume x in B;
              then consider k being Element of NAT such that
A43:          x = k and
A44:          k in dom q and
A45:          k <= n and
A46:          q.k in A;
              p.k in A by A44,A46,FUNCT_1:70;
              hence thesis by A5,A43,A44,A45,A42;
            end;
            let x;
            assume x in C;
            then consider m being Element of NAT such that
A47:        x = m and
A48:        m in dom p and
A49:        m <= n and
A50:        p.m in A by A5;
            m in Seg(len p) by A48,FINSEQ_1:def 3;
            then
A51:        1 <= m by FINSEQ_1:3;
            n <= l by A31,FINSEQ_1:3;
            then m <= l by A49,XXREAL_0:2;
            then m in Seg l by A51,FINSEQ_1:3;
            then
A52:        m in dom q by A3,FINSEQ_1:def 3;
            then q.m in A by A50,FUNCT_1:70;
            hence thesis by A47,A49,A52;
          end;
          q " A c= dom q by RELAT_1:167;
          then
A53:      q " A c= Seg l by A3,FINSEQ_1:def 3;
          q " A \/ (Seg l \ q " A) = q " A \/ Seg l by XBOOLE_1:39
            .= Seg l by A53,XBOOLE_1:12;
          then card(Seg l) = card(q " A) + card(Seg l \ q " A) by CARD_2
          :53,XBOOLE_1:79;
          then
A54:      len q = card(q " A) + card(Seg(len q) \ q " A) by A3,
          FINSEQ_1:78;
          set b = card B;
          set a = n - card B;
A55:      card(Seg n) = n by FINSEQ_1:78;
A56:      n in dom q by A3,A31,FINSEQ_1:def 3;
          then
A57:      not q.n in A by A6,FUNCT_1:70;
A58:      B c= (Seg n) \ {n}
          proof
            let x;
            assume x in B;
            then consider k being Element of NAT such that
A59:        x = k and
A60:        k in dom q and
A61:        k <= n and
A62:        q.k in A;
            k in Seg(len q) by A60,FINSEQ_1:def 3;
            then 1 <= k by FINSEQ_1:3;
            then
A63:        k in Seg n by A61,FINSEQ_1:3;
            not k in {n} by A57,A62,TARSKI:def 1;
            hence thesis by A59,A63,XBOOLE_0:def 5;
          end;
          then card B <= card((Seg n) \ {n}) by NAT_1:44;
          then card B <= card(Seg n) - card{n} by A32,CARD_2:63;
          then b <= card(Seg n) - 1 by CARD_1:50;
          then b <= n - 1 by FINSEQ_1:78;
          then b + 1 <= n by XREAL_1:21;
          then
A64:      0 + b < a + b by NAT_1:13;
          then 0 <= a by XREAL_1:8;
          then reconsider a as Element of NAT by INT_1:16;
          Seg n \ {n} c= Seg n by XBOOLE_1:36;
          then a = card(Seg n \ B) by A58,A55,CARD_2:63,XBOOLE_1:1;
          then a <= len q - card(q " A) by A3,A54,A33,NAT_1:44;
          then
A65:      a <= len(q - A) by Th66;
          1 <= a by A64,NAT_1:14;
          then a in Seg(len(q - A)) by A65,FINSEQ_1:3;
          then
A66:      a in dom(q - A) by FINSEQ_1:def 3;
          p.n = q.n by A56,FUNCT_1:70;
          then (q - A).(n - card B) = p.n by A1,A6,A3,A56;
          hence thesis by A24,A41,A66,FINSEQ_1:def 7;
        end;
      end;
      hence thesis;
    end;
  end;
  hence thesis;
end;

Lm13: for l for p,A st len p = l for n st n in dom p holds for B being finite
set st B = {k where k is Element of NAT: k in dom p & k <= n & p.k in A} holds
p.n in A or (p - A).(n - card B) = p.n
proof
  defpred P[Element of NAT] means for p,A st len p = $1 for n st n in dom p
holds for B being finite set st B = {k where k is Element of NAT : k in dom p &
  k <= n & p.k in A} holds p.n in A or (p - A).(n - card B) = p.n;
A1: P[0]
  proof
    let p,A;
    assume len p = 0;
    then p = {};
    hence thesis;
  end;
A2: for k being Element of NAT st P[k] holds P[k+1] by Lm12;
  for n being Element of NAT holds P[n] from NAT_1:sch 1(A1,A2);
  hence thesis;
end;

theorem
  n in dom p implies for B being finite set st B = {k where k is Element
of NAT : k in dom p & k <= n & p.k in A} holds p.n in A or (p - A).(n - card B)
  = p.n
proof
  len p = len p;
  hence thesis by Lm13;
end;

theorem
  p is FinSequence of D implies p - A is FinSequence of D
proof
  assume p is FinSequence of D;
  then
A1: rng p c= D by FINSEQ_1:def 4;
  rng(p - A) = rng p \ A by Th72;
  then rng(p - A) c= D by A1,XBOOLE_1:1;
  hence thesis by FINSEQ_1:def 4;
end;

theorem
  p is one-to-one implies p - A is one-to-one
proof
  assume
A1: p is one-to-one;
A2: p - A = p * Sgm(Seg(len p) \ p " A) by FINSEQ_1:def 3;
  Sgm(Seg(len p) \ p " A) is one-to-one by Lm1,XBOOLE_1:36;
  hence thesis by A1,A2;
end;

theorem Th95:
  p is one-to-one implies len(p - A) = len p - card(A /\ rng p)
proof
A1: p " A c= dom p by RELAT_1:167;
  assume
A2: p is one-to-one;
  p " A,A /\ rng p are_equipotent
  proof
    deffunc F(set) = p.$1;
    consider f being Function such that
A3: dom f = p " A and
A4: for x st x in p " A holds f.x = F(x) from FUNCT_1:sch 3;
    take f;
    thus f is one-to-one
    proof
      let x,y;
      assume that
A5:   x in dom f and
A6:   y in dom f and
A7:   f.x = f.y;
A8:   f.y = p.y by A3,A4,A6;
      p.x = f.x by A3,A4,A5;
      hence thesis by A2,A1,A3,A5,A6,A7,A8,FUNCT_1:def 8;
    end;
    thus dom f = p " A by A3;
    thus rng f c= A /\ rng p
    proof
      let x;
      assume x in rng f;
      then consider y such that
A9:   y in dom f and
A10:  f.y = x by FUNCT_1:def 5;
A11:  p.y in A by A3,A9,FUNCT_1:def 13;
      y in dom p by A3,A9,FUNCT_1:def 13;
      then
A12:  p.y in rng p by FUNCT_1:def 5;
      p.y = f.y by A3,A4,A9;
      hence thesis by A10,A11,A12,XBOOLE_0:def 4;
    end;
    let x;
    assume
A13: x in A /\ rng p;
    then x in rng p by XBOOLE_0:def 4;
    then consider y such that
A14: y in dom p and
A15: p.y = x by FUNCT_1:def 5;
    p.y in A by A13,A15,XBOOLE_0:def 4;
    then
A16: y in p " A by A14,FUNCT_1:def 13;
    then f.y = x by A4,A15;
    hence thesis by A3,A16,FUNCT_1:def 5;
  end;
  then card(p " A) = card(A /\ rng p) by CARD_1:21;
  hence thesis by Th66;
end;

theorem Th96:
  for A being finite set st p is one-to-one & A c= rng p holds len
  (p - A) = len p - card A
proof
  let A be finite set;
  assume that
A1: p is one-to-one and
A2: A c= rng p;
  A /\ rng p = A by A2,XBOOLE_1:28;
  hence thesis by A1,Th95;
end;

theorem
  p is one-to-one & x in rng p implies len(p - {x}) = len p - 1
proof
  assume that
A1: p is one-to-one and
A2: x in rng p;
  {x} c= rng p by A2,ZFMISC_1:37;
  then len(p - {x}) = len p - card{x} by A1,Th96;
  hence thesis by CARD_1:50;
end;

theorem Th98:
  rng p misses rng q & p is one-to-one & q is one-to-one iff p ^ q
  is one-to-one
proof
  thus rng p misses rng q & p is one-to-one & q is one-to-one implies p ^ q is
  one-to-one
  proof
    assume that
A1: rng p misses rng q and
A2: p is one-to-one and
A3: q is one-to-one;
    let x,y;
    assume that
A4: x in dom(p ^ q) and
A5: y in dom(p ^ q) and
A6: (p ^ q).x = (p ^ q).y;
    reconsider k1 = x, k2 = y as Element of NAT by A4,A5;
    now
      per cases by A4,A5,FINSEQ_1:38;
      suppose
A7:     k1 in dom p & k2 in dom p;
        then
A8:     (p ^ q).k2 = p.k2 by FINSEQ_1:def 7;
        (p ^ q).k1 = p.k1 by A7,FINSEQ_1:def 7;
        hence thesis by A2,A6,A7,A8,FUNCT_1:def 8;
      end;
      suppose
A9:     k1 in dom p & ex n being Nat st n in dom q & k2 = len p + n;
        then consider n being Nat such that
A10:    n in dom q and
A11:    k2 = len p + n;
A12:    q.n in rng q by A10,FUNCT_1:def 5;
A13:    (p ^ q).k1 = p.k1 by A9,FINSEQ_1:def 7;
        (p ^ q).k2 = q.n by A9,A11,FINSEQ_1:def 7;
        then q.n in rng p by A6,A9,A13,FUNCT_1:def 5;
        hence thesis by A1,A12,XBOOLE_0:3;
      end;
      suppose
A14:    k2 in dom p & ex n being Nat st n in dom q & k1 = len p + n;
        then consider n being Nat such that
A15:    n in dom q and
A16:    k1 = len p + n;
A17:    q.n in rng q by A15,FUNCT_1:def 5;
A18:    (p ^ q).k2 = p.k2 by A14,FINSEQ_1:def 7;
        (p ^ q).k1 = q.n by A14,A16,FINSEQ_1:def 7;
        then q.n in rng p by A6,A14,A18,FUNCT_1:def 5;
        hence thesis by A1,A17,XBOOLE_0:3;
      end;
      suppose
A19:    (ex n being Nat st n in dom q & k1 = len p + n) & ex n being
        Nat st n in dom q & k2 = len p + n;
        then consider n2 being Nat such that
A20:    n2 in dom q and
A21:    k2 = len p + n2;
A22:    (p ^ q).k2 = q.n2 by A20,A21,FINSEQ_1:def 7;
        consider n1 being Nat such that
A23:    n1 in dom q and
A24:    k1 = len p + n1 by A19;
        (p ^ q).k1 = q.n1 by A23,A24,FINSEQ_1:def 7;
        hence thesis by A3,A6,A23,A24,A20,A21,A22,FUNCT_1:def 8;
      end;
    end;
    hence thesis;
  end;
  assume
A25: p ^ q is one-to-one;
  thus rng p misses rng q
  proof
    assume not rng p misses rng q;
    then consider x such that
A26: x in rng p and
A27: x in rng q by XBOOLE_0:3;
    consider y1 such that
A28: y1 in dom p and
A29: p.y1 = x by A26,FUNCT_1:def 5;
A30: y1 in Seg(len p) by A28,FINSEQ_1:def 3;
    consider y2 such that
A31: y2 in dom q and
A32: q.y2 = x by A27,FUNCT_1:def 5;
A33: y2 in Seg(len q) by A31,FINSEQ_1:def 3;
    reconsider y1,y2 as Element of NAT by A28,A31;
A34: len p + y2 in dom(p ^ q) by A31,FINSEQ_1:41;
A35: (p ^ q).y1 = p.y1 by A28,FINSEQ_1:def 7;
A36: (p ^ q).(len p + y2) = q.y2 by A31,FINSEQ_1:def 7;
    y1 in dom(p ^ q ) by A28,Th24;
    then
A37: y1 = len p + y2 by A25,A29,A32,A34,A35,A36,FUNCT_1:def 8;
A38: y1 = y1 + 0;
A39: len p <= len p + y2 by NAT_1:12;
    y1 <= len p by A30,FINSEQ_1:3;
    then y1 = len p by A37,A39,XXREAL_0:1;
    hence thesis by A33,A37,A38,FINSEQ_1:3;
  end;
  thus p is one-to-one
  proof
    let x,y;
    assume that
A40: x in dom p and
A41: y in dom p and
A42: p.x = p.y;
    reconsider k = x, l = y as Element of NAT by A40,A41;
A43: (p ^ q).k = p.k by A40,FINSEQ_1:def 7;
A44: (p ^ q).l = p.l by A41,FINSEQ_1:def 7;
A45: l in dom(p ^q) by A41,Th24;
    k in dom(p ^ q) by A40,Th24;
    hence thesis by A25,A42,A43,A44,A45,FUNCT_1:def 8;
  end;
  let x,y;
  assume that
A46: x in dom q and
A47: y in dom q and
A48: q.x = q.y;
  consider l being Nat such that
A49: y = l and
A50: len p + l in dom(p ^ q) by A47,FINSEQ_1:40;
A51: (p ^ q).(len p + l) = q.l by A47,A49,FINSEQ_1:def 7;
  consider k being Nat such that
A52: x = k and
A53: len p + k in dom(p ^ q) by A46,FINSEQ_1:40;
  (p ^ q).(len p + k) = q.k by A46,A52,FINSEQ_1:def 7;
  then len p + k = len p + l by A25,A48,A52,A53,A49,A50,
  A51,FUNCT_1:def 8;
  hence thesis by A52,A49;
end;

theorem
  A c= Seg k implies Sgm A is one-to-one by Lm1;

canceled 2;

theorem Th102:
  <* x *> is one-to-one
proof
  let y1,y2;
  assume that
A1: y1 in dom<* x *> and
A2: y2 in dom<* x *> and
  <* x *>.y1 = <* x *>.y2;
  y1 in {1} by A1,FINSEQ_1:4,def 8;
  then
A3: y1 = 1 by TARSKI:def 1;
  y2 in {1} by A2,FINSEQ_1:4,def 8;
  hence thesis by A3,TARSKI:def 1;
end;

theorem Th103:
  x <> y iff <* x,y *> is one-to-one
proof
A1: <* x,y *>.2 = y by FINSEQ_1:61;
  2 in {1,2} by TARSKI:def 2;
  then
A2: 2 in dom<* x,y *> by Th29,FINSEQ_1:4;
  thus x <> y implies <* x,y *> is one-to-one
  proof
    assume
A3: x <> y;
    let y1,y2;
    assume that
A4: y1 in dom<* x,y *> and
A5: y2 in dom<* x,y *> and
A6: <* x,y *>.y1 = <* x,y *>.y2;
A7: y2 in {1,2} by A5,Th29,FINSEQ_1:4;
A8: y1 in {1,2} by A4,Th29,FINSEQ_1:4;
    now
      per cases by A8,A7,TARSKI:def 2;
      suppose
        y1 = 1 & y2 = 1 or y1 = 2 & y2 = 2;
        hence thesis;
      end;
      suppose
A9:     y1 = 1 & y2 = 2;
        then <* x,y *>.y1 = x by FINSEQ_1:61;
        hence thesis by A3,A6,A9,FINSEQ_1:61;
      end;
      suppose
A10:    y1 = 2 & y2 = 1;
        then <* x,y *>.y1 = y by FINSEQ_1:61;
        hence thesis by A3,A6,A10,FINSEQ_1:61;
      end;
    end;
    hence thesis;
  end;
  assume that
A11: <* x,y *> is one-to-one and
A12: x = y;
  1 in {1,2} by TARSKI:def 2;
  then
A13: 1 in dom<* x,y *> by Th29,FINSEQ_1:4;
  <* x,y *>.1 = x by FINSEQ_1:61;
  hence thesis by A11,A12,A13,A2,A1,FUNCT_1:def 8;
end;

theorem Th104:
  x <> y & y <> z & z <> x iff <* x,y,z *> is one-to-one
proof
  set p = <* x,y,z *>;
A1: p.1 = x by FINSEQ_1:62;
A2: p.3 = z by FINSEQ_1:62;
  thus x <> y & y <> z & z <> x implies <* x,y,z *> is one-to-one
  proof
    assume that
A3: x <> y and
A4: y <> z and
A5: z <> x;
    {x,y} /\ {z} = {}
    proof
      consider y1 being Element of {x,y} /\ {z};
      assume
A6:   not thesis;
      then y1 in {x,y} by XBOOLE_0:def 4;
      then
A7:   y1 = x or y1 = y by TARSKI:def 2;
      y1 in {z} by A6,XBOOLE_0:def 4;
      hence thesis by A4,A5,A7,TARSKI:def 1;
    end;
    then {} = rng<* x,y *> /\ {z} by FINSEQ_2:147
      .= rng<* x,y *> /\ rng<* z *> by FINSEQ_1:55;
    then
A8: rng<* x,y *> misses rng<* z *> by XBOOLE_0:def 7;
A9: <* z *> is one-to-one by Th102;
    <* x,y *> is one-to-one by A3,Th103;
    then <* x,y *> ^ <* z *> is one-to-one by A8,A9,Th98;
    hence thesis by FINSEQ_1:60;
  end;
A10: p.2 = y by FINSEQ_1:62;
  1 in {1,2,3} by ENUMSET1:def 1;
  then
A11: 1 in dom p by Th1,Th30;
  3 in {1,2,3} by ENUMSET1:def 1;
  then
A12: 3 in dom p by Th1,Th30;
  2 in {1,2,3} by ENUMSET1:def 1;
  then
A13: 2 in dom p by Th1,Th30;
  assume <* x,y,z *> is one-to-one;
  hence thesis by A11,A13,A12,A1,A10,A2,FUNCT_1:def 8;
end;

theorem Th105:
  p is one-to-one & rng p = {x} implies len p = 1
proof
  assume that
A1: p is one-to-one and
A2: rng p = {x};
A3: now
    given y1,y2 such that
A4: y1 in dom p and
A5: y2 in dom p and
A6: y1 <> y2;
    p.y2 in rng p by A5,FUNCT_1:def 5;
    then
A7: p.y2 = x by A2,TARSKI:def 1;
    p.y1 in rng p by A4,FUNCT_1:def 5;
    then p.y1 = x by A2,TARSKI:def 1;
    hence contradiction by A1,A4,A5,A6,A7,FUNCT_1:def 8;
  end;
  consider y being Element of dom p;
A8: dom p <> {} by A2,RELAT_1:65;
A9: dom p = {y}
  proof
    thus dom p c= {y}
    proof
      let x;
      assume x in dom p;
      then x = y by A3;
      hence thesis by TARSKI:def 1;
    end;
    let x;
    y in dom p by A8;
    hence thesis by TARSKI:def 1;
  end;
  dom p = Seg(len p) by FINSEQ_1:def 3;
  hence thesis by A9,Th22;
end;

theorem
  p is one-to-one & rng p = {x} implies p = <* x *>
proof
  assume that
A1: p is one-to-one and
A2: rng p = {x};
  len p = 1 by A1,A2,Th105;
  hence thesis by A2,FINSEQ_1:56;
end;

theorem Th107:
  p is one-to-one & rng p = {x,y} & x <> y implies len p = 2
proof
  assume that
A1: p is one-to-one and
A2: rng p = {x,y} and
A3: x <> y;
  set q = <* x,y *>;
A4: rng q = {x,y} by FINSEQ_2:147;
A5: len q = 2 by FINSEQ_1:61;
  q is one-to-one by A3,Th103;
  hence thesis by A1,A2,A4,A5,FINSEQ_1:65;
end;

theorem
  p is one-to-one & rng p = {x,y} & x <> y implies p = <* x,y *> or p =
  <* y,x *>
proof
  assume that
A1: p is one-to-one and
A2: rng p = {x,y} and
A3: x <> y;
A4: len p = 2 by A1,A2,A3,Th107;
  then
A5: dom p = {1,2} by FINSEQ_1:4,def 3;
  then
A6: 2 in dom p by TARSKI:def 2;
  then
A7: p.2 in rng p by FUNCT_1:def 5;
A8: 1 in dom p by A5,TARSKI:def 2;
  then p.1 in rng p by FUNCT_1:def 5;
  then p.1 = x & p.2 = x or p.1 = x & p.2 = y or p.1 = y & p.2 = x or p.1 = y
  & p.2 = y by A2,A7,TARSKI:def 2;
  hence thesis by A1,A4,A8,A6,FINSEQ_1:61,FUNCT_1:def 8;
end;

theorem Th109:
  p is one-to-one & rng p = {x,y,z} & <* x,y,z *> is one-to-one
  implies len p = 3
proof
A1: len <* x,y,z *> = 3 by FINSEQ_1:62;
  rng <* x,y,z *> = {x,y,z} by FINSEQ_2:148;
  hence thesis by A1,FINSEQ_1:65;
end;

theorem
  p is one-to-one & rng p = {x,y,z} & x <> y & y <> z & x <> z implies
  len p = 3
proof
  assume that
A1: p is one-to-one and
A2: rng p = {x,y,z} and
A3: x <> y and
A4: y <> z and
A5: x <> z;
  <* x,y,z *> is one-to-one by A3,A4,A5,Th104;
  hence thesis by A1,A2,Th109;
end;

begin :: Addenda
:: from FSM_1

theorem
  for D being non empty set, df being FinSequence of D holds df is non
empty implies ex d being Element of D, df1 being FinSequence of D st d = df.1 &
  df = <*d*>^df1
proof
  let D be non empty set, df be FinSequence of D;
  deffunc F(Nat) = df.($1+1);
  assume
A1: df is non empty;
  then reconsider lend1 = len df - 1 as Element of NAT by INT_1:18,NAT_1:14;
  1 <= len df by A1,NAT_1:14;
  then 1 in dom df by Th27;
  then reconsider d = df.1 as Element of D by FINSEQ_2:13;
  take d;
  consider dta being FinSequence such that
A2: len dta = lend1 and
A3: for j being Nat st j in dom dta holds dta.j=F(j) from FINSEQ_1:sch 2;
  now
    let j be Nat;
    assume
A4: j in dom dta;
    then j in Seg len dta by FINSEQ_1:def 3;
    then j + 1 in Seg (len dta + 1) by FINSEQ_1:81;
    then j+1 in dom df by A2,FINSEQ_1:def 3;
    then df.(j+1) in D by FINSEQ_2:13;
    hence dta.j in D by A3,A4;
  end;
  then reconsider dta as FinSequence of D by FINSEQ_2:14;
  take dta;
  thus d = df.1;
  now
    thus
A5: len (<*d*>^dta) = len <*d*> + len dta by FINSEQ_1:35
      .= 1 + (len df - 1) by A2,FINSEQ_1:57
      .= len df;
    let i be Nat;
A6: dom df = Seg len df by FINSEQ_1:def 3;
    assume
A7: i in dom df;
    then
A8: 1 <= i by A6,FINSEQ_1:3;
A9: i <= len df by A6,A7,FINSEQ_1:3;
    per cases by A8,XXREAL_0:1;
    suppose
      i = 1;
      hence df.i = (<*d*>^dta).i by FINSEQ_1:58;
    end;
    suppose
A10:  i > 1;
      then consider j being Element of NAT such that
A11:  j = i-1 and
A12:  1 <= j by INT_1:78;
      i - 1 <= lend1 by A9,XREAL_1:11;
      then
A13:  j in dom dta by A2,A11,A12,Th27;
      len <*d*> = 1 by FINSEQ_1:57;
      then (<*d*>^dta).i = dta.j by A5,A9,A10,A11,FINSEQ_1:37
        .= df.(j+1) by A3,A13
        .= df.i by A11;
      hence df.i = (<*d*>^dta).i;
    end;
  end;
  hence thesis by FINSEQ_2:10;
end;

theorem
  for df being FinSequence, d being set holds i in dom df implies (<*d*>
  ^df).(i+1) = df.i
proof
  let df be FinSequence, d be set;
A1: len (<*d*>^df) = len <*d*> + len df by FINSEQ_1:35
    .= 1 + len df by FINSEQ_1:57;
  assume
A2: i in dom df;
  then i in Seg len df by FINSEQ_1:def 3;
  then i+1 in Seg len (<*d*>^df) by A1,FINSEQ_1:81;
  then i+1 in dom (<*d*>^df) by FINSEQ_1:def 3;
  then
A3: i+1 <= len (<*d*>^df) by Th27;
A4: len <*d*> = 1 by FINSEQ_1:57;
  1 <= i by A2,Th27;
  then 1 < i+1 by NAT_1:13;
  hence (<*d*>^df).(i+1) = df.(i + 1 - len <*d*>) by A4,A3,FINSEQ_1:37
    .= df.i by A4;
end;

:: from MATRIX_2, 2005.11.16, A.T.

definition
  let i;
  let p be FinSequence;
  func Del(p,i) -> FinSequence equals
  p * Sgm ((dom p) \ {i});
  coherence
  proof
    set q = Sgm((dom p) \ {i});
A1: (Seg len p) \ {i} c= Seg len p by XBOOLE_1:36;
    dom p = Seg len p by FINSEQ_1:def 3;
    then rng q c= dom p by A1,FINSEQ_1:def 13;
    then
A2: dom (p*q) = dom q by RELAT_1:46;
    dom q = Seg len q by FINSEQ_1:def 3;
    hence thesis by A2,FINSEQ_1:def 2;
  end;
end;

theorem Th113:
  for p being FinSequence holds
  (i in dom p implies ex m being Nat st len p = m + 1 & len Del(p,i) = m) &
  (not i in dom p implies Del(p,i) = p)
proof
  let p be FinSequence;
  hereby
    not i in (Seg(len p) \ {i})
    proof
      assume i in (Seg(len p) \ {i});
      then not i in {i} by XBOOLE_0:def 5;
      hence thesis by TARSKI:def 1;
    end;
    then
A1: card ((Seg(len p) \ {i}) \/ {i})=card (Seg(len p) \ {i}) +1 by
    CARD_2:54;
    assume
A2: i in dom p;
    then reconsider D9 = dom p as non empty set;
    reconsider D = rng p as non empty set by A2,FUNCT_1:12;
    reconsider r = p as Function of D9,D by FUNCT_2:3;
A3: dom p = Seg len p by FINSEQ_1:def 3;
    for x holds x in (Seg(len p) \ {i}) implies x in Seg(len p) by
    XBOOLE_0:def 5;
    then
A4: (Seg(len p) \ {i}) c= Seg len p by TARSKI:def 3;
    then rng Sgm(Seg(len p) \ {i}) c= Seg len p by FINSEQ_1:def 13;
    then reconsider q = Sgm((dom p) \ {i}) as FinSequence of D9 by
    A3,FINSEQ_1:
    def 4;
    p <> {} by A2;
    then consider m being Nat such that
A5: len p = m+1 by NAT_1:6;
    take m;
A6: len(r*q) = len q by FINSEQ_2:37;
    i in Seg len p by A2,FINSEQ_1:def 3;
    then for x holds x in {i} implies x in Seg(len p) by TARSKI:def 1;
    then {i} c= Seg len p by TARSKI:def 3;
    then card Seg(len p)=card (Seg(len p) \ {i}) +1 by A1,XBOOLE_1:45;
    then card (Seg(len p) \ {i}) +1 = m+1 by A5,FINSEQ_1:78;
    then len (Sgm(Seg(len p) \ {i}))=m by A4,Th44;
    hence len p=m + 1 & len Del(p,i) = m by A5,A6,FINSEQ_1:def 3;
  end;
  assume not i in dom p;
  then for x st x in {i} holds not x in dom p by TARSKI:def 1;
  then {i} misses dom p by XBOOLE_0:3;
  hence Del(p,i) = p * Sgm(dom p) by XBOOLE_1:83
    .= p * Sgm(Seg len p) by FINSEQ_1:def 3
    .= p * (idseq len p) by Th54
    .= p|(Seg len p) by RELAT_1:94
    .= p|(dom p) by FINSEQ_1:def 3
    .= p by RELAT_1:97;
end;

theorem
  for D being non empty set for p being FinSequence of D holds
  Del(p,i) is FinSequence of D
proof
  let D be non empty set, p be FinSequence of D;
  per cases;
  suppose
    i in dom p;
    then reconsider D9=Seg(len p) as non empty set by FINSEQ_1:def 3;
    for x holds x in (Seg(len p) \ {i}) implies x in Seg(len p) by
    XBOOLE_0:def 5;
    then Seg(len p) \ {i} c= Seg(len p) by TARSKI:def 3;
    then rng Sgm(Seg(len p) \ {i}) c= Seg(len p) by FINSEQ_1:def 13;
    then reconsider q=Sgm(Seg(len p) \ {i}) as FinSequence of D9 by
    FINSEQ_1:def 4;
    p * q = Del(p,i) by FINSEQ_1:def 3;
    hence thesis by FINSEQ_2:35;
  end;
  suppose
    not i in dom p;
    hence thesis by Th113;
  end;
end;

:: from MATRLIN, 2005.11.16, A.T.

theorem
  for p be FinSequence holds rng Del(p,i) c= rng p
proof
  let p be FinSequence;
  let x;
  assume x in rng Del(p,i);
  hence thesis by FUNCT_1:25;
end;

:: from GOBOARD1, 2005.11.16, A.T.

theorem Th116:
  n = m + 1 & i in Seg n implies len Sgm(Seg n \ {i}) = m
proof
  assume that
A1: n = m + 1 and
A2: i in Seg n;
  set X = Seg n \ {i};
  i in {i} by TARSKI:def 1;
  then not i in X by XBOOLE_0:def 5;
  then card X + 1 = card (X \/ {i}) by CARD_2:54
    .= card ((Seg n) \/ {i}) by XBOOLE_1:39
    .= card (Seg n) by A2,ZFMISC_1:46
    .= m + 1 by A1,FINSEQ_1:78;
  hence thesis by Th44,XBOOLE_1:36;
end;

reserve J for Element of NAT;

theorem Th117:
  for i,k,m,n being Nat st n=m+1 & k in Seg n & i in Seg m holds
  (1<=i & i<k implies Sgm(Seg n \ {k}).i = i) &
  (k<=i & i<=m implies Sgm(Seg n \ {k}).i = i+1)
proof
  defpred P[Nat] means for n,k,i being Nat st n = $1 + 1
& k in Seg n & i in Seg $1 holds (1<=i & i<k implies Sgm(Seg n \ {k}).i = i) &
  (k<=i & i<=$1implies Sgm(Seg n \ {k}).i = i+1);
  let i,k,m,n be Nat;
  assume that
A1: n = m + 1 and
A2: k in Seg n and
A3: i in Seg m;
A4: for k being Nat st P[k] holds P[k+1]
  proof
    let k being Nat such that
A5: P[k];
    let g,i,n be Nat;
    assume that
A6: g = k+1 + 1 and
A7: i in Seg g and
A8: n in Seg(k+1);
A9: 1<=i by A7,FINSEQ_1:3;
    set X = Seg g \ {i};
A10: len Sgm(X)=k+1 by A6,A7,Th116;
A11: 1<=n by A8,FINSEQ_1:3;
A12: i<=g by A7,FINSEQ_1:3;
A13: X c= Seg g by XBOOLE_1:36;
    then
A14: rng Sgm(X) = X by FINSEQ_1:def 13;
A15: n<=k+1 by A8,FINSEQ_1:3;
    now
      per cases;
      suppose
A16:    i=g;
        then
A17:    k+1<i by A6,NAT_1:13;
        X = Seg(k+1)
        proof
          thus X c= Seg(k+1)
          proof
            let x;
A18:        Seg g = {J: 1<=J & J<= g} by FINSEQ_1:def 1;
            assume
A19:        x in X;
            then x in Seg g by XBOOLE_0:def 5;
            then consider J such that
A20:        x=J and
A21:        1<=J and
A22:        J<=g by A18;
            not x in {i} by A19,XBOOLE_0:def 5;
            then x<>i by TARSKI:def 1;
            then J<g by A16,A20,A22,XXREAL_0:1;
            then J<=k+1 by A6,NAT_1:13;
            hence thesis by A20,A21,FINSEQ_1:3;
          end;
          let x;
          assume x in Seg(k+1);
          then x in {J: 1<=J & J<=k+1} by FINSEQ_1:def 1;
          then consider J such that
A23:      x=J and
A24:      1<=J and
A25:      J<=k+1;
          k+1<=g by A6,NAT_1:11;
          then J<=g by A25,XXREAL_0:2;
          then
A26:      J in Seg g by A24,FINSEQ_1:3;
          not J in {i} by A17,A25,TARSKI:def 1;
          hence thesis by A23,A26,XBOOLE_0:def 5;
        end;
        then Sgm X = idseq (k+1) by Th54;
        hence 1<=n & n<i implies Sgm(X).n = n by A8,FUNCT_1:34;
        thus i<=n & n<=k+1 implies Sgm(Seg g \ {i}).n = n+1 by A6,A16,NAT_1:13;
      end;
      suppose
A27:    i<>g;
        set A = {l where l is Element of NAT : 1<=l & l<=g & l<>i};
A28:    X = A
        proof
          thus X c= A
          proof
            let x;
A29:        Seg g= {J: 1<=J & J<=g } by FINSEQ_1:def 1;
            assume
A30:        x in X;
            then x in Seg g by XBOOLE_0:def 5;
            then consider m being Element of NAT such that
A31:        x=m and
A32:        1<=m and
A33:        m<=g by A29;
            not x in {i} by A30,XBOOLE_0:def 5;
            then m<>i by A31,TARSKI:def 1;
            hence thesis by A31,A32,A33;
          end;
          let x;
          assume x in A;
          then consider m being Element of NAT such that
A34:      x=m and
A35:      1<=m and
A36:      m<=g and
A37:      m<>i;
A38:      not m in {i} by A37,TARSKI:def 1;
          m in Seg g by A35,A36,FINSEQ_1:3;
          hence thesis by A34,A38,XBOOLE_0:def 5;
        end;
        1<=k+1 by A11,A15,XXREAL_0:2;
        then k+1 in dom Sgm(X) by A10,Th27;
        then Sgm(X).(k+1) in X by A14,FUNCT_1:def 5;
        then
A39:    ex J st Sgm(X).(k+1) = J & 1<=J & J<=g & J<>i by A28;
BB:     g in NAT by ORDINAL1:def 13;
        1<=g by A9,A12,XXREAL_0:2;
        then g in rng Sgm(X) by A14,A27,A28,BB;
        then consider x be Nat such that
A40:    x in dom Sgm(X) and
A41:    Sgm(X).x=g by FINSEQ_2:11;
        1<=x by A40,Th27;
        then
A42:    k+1<=x by A13,A10,A39,A41,FINSEQ_1:def 13;
A43:    i<g by A12,A27,XXREAL_0:1;
        then
A44:    i<=k+1 by A6,NAT_1:13;
A45:    x<=k+1 by A10,A40,Th27;
        now
          per cases by A15,XXREAL_0:1;
          suppose
A46:        n=k+1;
            hence 1<=n & n<i implies Sgm(X).n = n by A6,A43,NAT_1:13;
            thus i<=n & n<=k+1 implies Sgm(Seg g \ {i}).n=n+1 by A6,A41,A45,
            A42,A46, XXREAL_0:1;
          end;
          suppose
A47:        n<k+1;
            set Y = Seg(k+1) \ {i};
A48:        now
              let j1,j be Element of NAT;
              assume that
A49:          j1 in Y and
A50:          j in {g};
              j1 in Seg(k+1) by A49,XBOOLE_0:def 5;
              then
A51:          j1<=k+1 by FINSEQ_1:3;
              j=g by A50,TARSKI:def 1;
              hence j1<j by A6,A51,NAT_1:13;
            end;
A52:        X = Y \/ {g}
            proof
              thus X c= Y \/ {g}
              proof
                let x;
                assume x in X;
                then consider J such that
A53:            x=J and
A54:            1<=J and
A55:            J<=g and
A56:            J<>i by A28;
                now
                  per cases by A55,XXREAL_0:1;
                  suppose
                    J=g;
                    then x in {g} by A53,TARSKI:def 1;
                    hence thesis by XBOOLE_0:def 3;
                  end;
                  suppose
                    J<g;
                    then J<=k+1 by A6,NAT_1:13;
                    then
A57:                J in Seg(k+1) by A54,FINSEQ_1:3;
                    not J in {i} by A56,TARSKI:def 1;
                    then x in Y by A53,A57,XBOOLE_0:def 5;
                    hence thesis by XBOOLE_0:def 3;
                  end;
                end;
                hence thesis;
              end;
              let x such that
A58:          x in Y \/ {g};
              now
                per cases by A58,XBOOLE_0:def 3;
                suppose
A59:              x in Y;
A60:              Seg(k+1)={ s where s is Element of NAT :1<=s & s<=k+1}
                  by FINSEQ_1:def 1;
                  x in Seg(k+1) by A59,XBOOLE_0:def 5;
                  then consider s be Element of NAT such that
A61:              x=s and
A62:              1<=s and
A63:              s<=k+1 by A60;
                  not x in {i} by A59,XBOOLE_0:def 5;
                  then
A64:              s<>i by A61,TARSKI:def 1;
                  k+1<=g by A6,NAT_1:11;
                  then s<=g by A63,XXREAL_0:2;
                  hence thesis by A28,A61,A62,A64;
                end;
                suppose
                  x in {g};
                  then
A65:              x=g by TARSKI:def 1;
                  1<=g by A9,A12,XXREAL_0:2;
                  hence thesis by A27,A28,A65,BB;
                end;
              end;
              hence thesis;
            end;
            then {g} c= X by XBOOLE_1:7;
            then
A66:        {g} c= Seg g by A13,XBOOLE_1:1;
            Y c=X by A52,XBOOLE_1:7;
            then Y c= Seg g by A13,XBOOLE_1:1;
            then
A67:        Sgm(X) = Sgm(Y)^Sgm({g}) by A52,A48,A66,Th48;
            n<=k by A47,NAT_1:13;
            then
A68:        n in Seg k by A11,FINSEQ_1:3;
A69:        i in Seg(k+1) by A9,A44,FINSEQ_1:3;
            then len Sgm(Y) = k by Th116;
            then
A70:        n in dom Sgm(Y) by A68,FINSEQ_1:def 3;
            n<=k by A47,NAT_1:13;
            then
A71:        n in Seg k by A11,FINSEQ_1:3;
            then
A72:        1<=n & n<i implies Sgm(Seg(k+1) \ {i}).n = n by A5,A69;
            now
              assume that
A73:          1<=n and
A74:          n<i;
              n in Seg len Sgm(Y) by A71,A69,Th116;
              then n in dom Sgm(Y) by FINSEQ_1:def 3;
              hence Sgm(X).n = n by A72,A67,A73,A74,FINSEQ_1:def 7;
            end;
            hence 1<=n & n<i implies Sgm(X).n = n;
            assume that
A75:        i<=n and
            n<=k+1;
            i<=n & n<=k implies Sgm(Seg(k+1) \ {i}).n = n+1 by A5,A71,A69;
            hence Sgm(X).n = n+1 by A47,A67,A75,A70,FINSEQ_1:def 7 ,
            NAT_1:13;
          end;
        end;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
A76: P[0];
  for k being Nat holds P[k] from NAT_1:sch 2(A76,A4);
  hence thesis by A1,A2,A3;
end;

theorem Th118:
  for f being FinSequence st len f = m+1 & n in dom f holds len Del(f,n) = m
proof
  let f be FinSequence such that
A1: len f = m+1 and
A2: n in dom f;
  ex k being Nat st len f = k+1 & len Del(f,n) = k by A2,Th113;
  hence thesis by A1;
end;

theorem
  for f being FinSequence st k < n holds Del(f,n).k = f.k
proof
  let f be FinSequence;
  assume
A2: k<n;
  per cases;
  suppose that
A3: n in dom f and
B1: f <> {};
    consider m being Nat such that
A1: len f = m+1 by B1,NAT_1:6;
    now
      per cases;
      suppose
A4:     1 <= k;
        set X = dom f \ {n};
A5:     dom Sgm(X)=Seg len Sgm(X) by FINSEQ_1:def 3;
A6:     dom f=Seg len f by FINSEQ_1:def 3;
        then
A7:     len Sgm X = m by A1,A3,Th116;
        X c= Seg len f by A6,XBOOLE_1:36;
        then rng Sgm(X) = X by FINSEQ_1:def 13;
        then
A8:     dom (f*Sgm X) = dom Sgm X by RELAT_1:46,XBOOLE_1:36;
        n<=m+1 by A1,A3,Th27;
        then k<m+1 by A2,XXREAL_0:2;
        then k<=m by NAT_1:13;
        then
A9:     k in Seg m by A4,FINSEQ_1:3;
        then 1<=k & k<n implies Sgm(X).k = k by A1,A3,A6,Th117;
        hence thesis by A2,A4,A9,A5,A8,A7,FUNCT_1:22;
      end;
      suppose
A10:    not 1 <= k;
        Seg len Del (f,n) = Seg m by A1,A3,Th118;
        then dom Del (f,n) = Seg m by FINSEQ_1:def 3;
        then
A11:    not k in dom Del (f,n) by A10,FINSEQ_1:3;
        not k in dom f by A10,Th27;
        then f.k = {} by FUNCT_1:def 4;
        hence thesis by A11,FUNCT_1:def 4;
      end;
    end;
    hence thesis;
  end;
  suppose
    not n in dom f or f = {};
    hence thesis by Th113;
  end;
end;

theorem
  for f being FinSequence st len f = m+1 & n in dom f & n<=k & k<=m
  holds Del(f,n).k = f.(k+1)
proof
  let f be FinSequence;
  assume that
A1: len f = m+1 and
A2: n in dom f and
A3: n<=k and
A4: k<=m;
  set X = dom f \ {n};
A5: dom f=Seg len f by FINSEQ_1:def 3;
  then X c= Seg len f by XBOOLE_1:36;
  then rng Sgm(X) = X by FINSEQ_1:def 13;
  then
A6: dom (f*Sgm(X)) = dom Sgm(X) by RELAT_1:46,XBOOLE_1:36;
A7: len Sgm(X) = m by A1,A2,A5,Th116;
A8: dom Sgm(X)=Seg len Sgm(X) by FINSEQ_1:def 3;
  1<=n by A2,Th27;
  then 1<=k by A3,XXREAL_0:2;
  then
A9: k in Seg m by A4,FINSEQ_1:3;
  then n<=k & k<=m implies Sgm(X).k = k+1 by A1,A2,A5,Th117;
  hence thesis by A3,A4,A9,A8,A6,A7,FUNCT_1:22;
end;

:: from JORDAN3, 2005.11.16, A.T.

theorem Th121:
  for f being FinSequence st k<=n holds (f|n).k=f.k
proof
  let f be FinSequence;
  assume
A1: k<=n;
  per cases by NAT_1:14;
  suppose
A2: k = 0;
    then
A3: not k in dom f by Th27;
    not k in dom(f|n) by A2,Th27;
    hence (f|n).k = {} by FUNCT_1:def 4
      .= f.k by A3,FUNCT_1:def 4;
  end;
  suppose
    1 <= k;
    then k in Seg n by A1,FINSEQ_1:3;
    then (f|Seg n).k=f.k by FUNCT_1:72;
    hence thesis by FINSEQ_1:def 15;
  end;
end;

:: missing, 27.11.2005, A.T.

theorem
  for p,q being FinSequence st p c= q holds q|len p = p
proof
  let p,q be FinSequence such that
A1: p c= q;
A2: for k being Nat st k in dom p holds p.k = (q|len p).k
  proof
    let k be Nat;
    assume
A3: k in dom p;
    then
A4: k <= len p by Th27;
    thus p.k = q.k by A1,A3,GRFUNC_1:8
      .= (q|len p).k by A4,Th121;
  end;
  len p <= len q by A1,FINSEQ_1:84;
  then len(q|len p) = len p by FINSEQ_1:80;
  then dom(q|len p) = dom p by Th31;
  hence thesis by A2,FINSEQ_1:17;
end;

:: from MATRLIN, 2006.01.06, A.T.

theorem Th123:
  for A being set, F being FinSequence holds Sgm(F"A)^Sgm(F"(rng
  F \ A)) is Permutation of dom F
proof
  let A be set;
  let F be FinSequence;
A1: dom F = Seg len F by FINSEQ_1:def 3;
  set p = Sgm(F"A)^Sgm(F"(rng F \ A));
  A misses (rng F \ A) by XBOOLE_1:79;
  then
A2: A /\ (rng F \ A) = {} by XBOOLE_0:def 7;
A3: F"A /\ F"(rng F \ A) = F"(A /\ (rng F \ A)) by FUNCT_1:137
    .= {} by A2,RELAT_1:171;
  then
A4: F"A misses F"(rng F \ A) by XBOOLE_0:def 7;
A5: F"(rng F \ A) c= dom F by RELAT_1:167;
  then
A6: F"(rng F \ A) c= Seg len F by FINSEQ_1:def 3;
  then
A7: Sgm(F"(rng F \ A)) is one-to-one by Lm1;
A8: F"A c= dom F by RELAT_1:167;
  then
A9: F"A c= Seg len F by FINSEQ_1:def 3;
  then rng Sgm(F"A) /\ rng Sgm(F"(rng F \ A)) = F"A /\ rng Sgm(F"(rng F \ A))
  by FINSEQ_1:def 13
    .= {} by A6,A3,FINSEQ_1:def 13;
  then
A10: rng Sgm(F"A) misses rng Sgm(F"(rng F \ A)) by XBOOLE_0:def 7;
A11: rng p = rng Sgm(F"A) \/ rng Sgm(F"(rng F \ A)) by FINSEQ_1:44
    .= F"A \/ rng Sgm(F"(rng F \ A)) by A9,FINSEQ_1:def 13
    .= F"A \/ F"(rng F \ A) by A6,FINSEQ_1:def 13
    .= F"(A \/ (rng F \ A)) by RELAT_1:175
    .= F"((rng F) \/ A) by XBOOLE_1:39
    .= F"(rng F) \/ F"A by RELAT_1:175
    .= (dom F) \/ F"A by RELAT_1:169
    .= dom F by RELAT_1:167,XBOOLE_1:12;
  Sgm(F"A) is one-to-one by A9,Lm1;
  then
A12: p is one-to-one by A7,A10,Th98;
  len p = len Sgm(F"A) + len Sgm(F"(rng F \ A)) by FINSEQ_1:35
    .= card (F"A) + len Sgm(F"(rng F \ A)) by A9,Th44
    .= card (F"A) + card (F"(rng F \ A)) by A6,Th44
    .= card (F"A \/ F"(rng F \ A)) by A4,CARD_2:53
    .= len Sgm (F"A \/ F"(rng F \ A)) by A1,A8,A5,Th44,XBOOLE_1:8
    .= len Sgm (F"(A \/ (rng F \ A))) by RELAT_1:175
    .= len Sgm (F"((rng F) \/ A)) by XBOOLE_1:39
    .= len Sgm (F"(rng F) \/ F"A) by RELAT_1:175
    .= len Sgm (dom F \/ F"A) by RELAT_1:169
    .= len Sgm dom F by RELAT_1:167,XBOOLE_1:12
    .= card Seg len F by A1,Th44
    .= len F by FINSEQ_1:78;
  then dom p = dom F by Th31;
  then p is Function of dom F, dom F by A11,FUNCT_2:3;
  hence thesis by A12,A11,FUNCT_2:83;
end;

theorem
  for F being FinSequence, A be Subset of rng F
  ex p being Permutation of dom F st (F-A`)^(F-A) = F*p
proof
  let F be FinSequence;
  let A be Subset of rng F;
  set F1 = F-A`, F2 = F-A;
  A` = (rng F) \ A by SUBSET_1:def 5;
  then F"A` misses F"A by FUNCT_1:141,XBOOLE_1:79;
  then card (F"A`) + card (F"A) =  card ((F"A`) \/ (F"A)) by CARD_2:53
                               .=card (F"(A`\/A)) by RELAT_1:175
                               .= card (F"[#]rng F) by SUBSET_1:25
                               .= card (F"rng F) by SUBSET_1:def 4
                               .=card dom F by RELAT_1:169
                               .=len F by CARD_1:104;
  then A5:len F = len F -card (F"A`) + (len F -card (F"A))
               .= len F1 + (len F -card (F"A)) by Th66
               .= len F1 + len F2 by Th66;
 (rng F) \ (rng F1) = (rng F) \ (rng F \ A`) by Th72
    .= rng F \ (rng F \ (rng F \ A)) by SUBSET_1:def 5
    .= rng F \ (rng F /\ A) by XBOOLE_1:48
    .= rng F \ A by XBOOLE_1:47
    .= rng F2 by Th72;
  then reconsider
  p = Sgm(F"(rng F1))^Sgm(F"(rng F2)) as Permutation of dom F by Th123;
  reconsider F3 = F * p as FinSequence by FINSEQ_2:44;
  take p;
A8: now
A9: F"A` = F"(rng F \ A) by SUBSET_1:def 5
      .= F"(rng F) \ F"A by FUNCT_1:138
      .= dom F \ F"A by RELAT_1:169;
A10: F"(rng F) = dom F by RELAT_1:169;
    then
A11: card(F"(rng F) \ F"A`) = card (F"(rng F)) - card (F"A`) by A9,
    CARD_2:63,XBOOLE_1:36
      .= card Seg len F - card (F"A`) by A10,FINSEQ_1:def 3
      .= len F - card (F"A`) by FINSEQ_1:78
      .= len (F - A`) by Th66;
    F"(rng F1) c= dom F by RELAT_1:167;
    then
A12: F"(rng F1) c= Seg len F by FINSEQ_1:def 3;
    let k be Element of NAT such that
A13: k in dom F1;
A14: dom Sgm(F"(rng F1)) = Seg len Sgm(F"(rng F1)) by FINSEQ_1:def 3
      .= Seg card(F"(rng F1)) by A12,Th44
      .= Seg card(F"(rng F \ A`)) by Th72
      .= Seg len (F - A`) by A11,FUNCT_1:138
      .= dom F1 by FINSEQ_1:def 3;
    dom Sgm(F"(rng F1)) c= dom p by FINSEQ_1:39;
    hence (F*p).k = F.(p.k) by A13,A14,FUNCT_1:23
      .= F.(Sgm(F"(rng F1)).k) by A13,A14,FINSEQ_1:def 7
      .= (F*Sgm(F"(rng F1))).k by A13,A14,FUNCT_1:23
      .= (F*Sgm(F"(rng F \ A`))).k by Th72
      .= (F*Sgm(F"(rng F \ (rng F \ A)))).k by SUBSET_1:def 5
      .= (F*Sgm(F"(rng F /\ A))).k by XBOOLE_1:48
      .= (F*Sgm(F"(rng F) /\ F"A)).k by FUNCT_1:137
      .= (F*Sgm(dom F /\ F"A)).k by RELAT_1:169
      .= F1.k by A9,XBOOLE_1:48;
  end;
A15: now
A16: F"(rng F) = dom F by RELAT_1:169;
    F"A c= dom F by RELAT_1:167;
    then F"A c= F"(rng F) by RELAT_1:169;
    then
A17: card(F"(rng F) \ F"A) = card (F"(rng F)) - card (F"A) by CARD_2:63
      .= card Seg len F - card (F"A) by A16,FINSEQ_1:def 3
      .= len F - card (F"A) by FINSEQ_1:78
      .= len (F - A) by Th66;
    dom F = Seg len F by FINSEQ_1:def 3;
    then len Sgm(F"(rng F2)) = card(F"(rng F2)) by Th44,RELAT_1:167
      .= card(F"(rng F \ A)) by Th72
      .= len (F - A) by A17,FUNCT_1:138;
    then
A18: dom Sgm(F"(rng F2)) = dom F2 by Th31;
A19: F"(rng F) = dom F by RELAT_1:169;
    F"A` = F"(rng F \ A) by SUBSET_1:def 5
      .= F"(rng F) \ F"A by FUNCT_1:138
      .= dom F \ F"A by RELAT_1:169;
    then
A20: card(F"(rng F) \ F"A`) = card (F"(rng F)) - card (F"A`) by A19,
    CARD_2:63,XBOOLE_1:36
      .= card Seg len F - card (F"A`) by A19,FINSEQ_1:def 3
      .= len F - card (F"A`) by FINSEQ_1:78
      .= len (F - A`) by Th66;
    let k be Element of NAT such that
A21: k in dom F2;
    F"(rng F1) c= dom F by RELAT_1:167;
    then F"(rng F1) c= Seg len F by FINSEQ_1:def 3;
    then
A22: len Sgm(F"(rng F1)) = card(F"(rng F1)) by Th44
      .= card(F"(rng F \ A`)) by Th72
      .= len (F - A`) by A20,FUNCT_1:138;
    then len F1 + k in dom p by A21,A18,FINSEQ_1:41;
    hence (F*p).(len F1 + k) = F.(p.(len F1 + k)) by FUNCT_1:23
      .= F.(Sgm(F"(rng F2)).k) by A21,A18,A22,FINSEQ_1:def 7
      .= (F*Sgm(F"(rng F2))).k by A21,A18,FUNCT_1:23
      .= (F*Sgm(F"(rng F \ A))).k by Th72
      .= (F*Sgm(F"(rng F) \ F"A)).k by FUNCT_1:138
      .= F2.k by RELAT_1:169;
  end;
  len F3 = len F1 + len F2 by A5,FINSEQ_2:48;
  hence thesis by A8,A15,Th43;
end;

:: Moved from GRAPH_2 by AK on 27.12.2006

theorem
  for f being FinSubsequence holds f is FinSequence implies Seq f = f
proof
  let f be FinSubsequence;
  assume f is FinSequence;
  then reconsider ss9=f as FinSequence;
  consider k be Nat such that
A1: dom ss9 = Seg k by FINSEQ_1:def 2;
  k in NAT by ORDINAL1:def 13;
  then
A2: len ss9 <= k by A1,FINSEQ_1:def 3;
  Seq f = f* Sgm Seg k by A1,FINSEQ_1:def 14
    .= f* idseq k by Th54
    .= f by A2,FINSEQ_2:64;
  hence thesis;
end;

:: Moved from SCMBSORT by AK on 10.10.2007

theorem
  for t be FinSequence of INT holds t is FinSequence of REAL
proof
  let t be FinSequence of INT;
  now
    let i be Nat;
    assume i in dom t;
    then
A1: t.i in rng t by FUNCT_1:def 5;
    t.i in INT by A1;
    hence t.i in REAL by NUMBERS:15;
  end;
  hence thesis by FINSEQ_2:14;
end;

:: missing, 2008.02.16, A.T,

theorem
  len p < len q iff dom p c< dom q
proof
A1: len p = len q iff dom p = dom q by Th31;
  len p <= len q iff dom p c= dom q by Th32;
  hence thesis by A1,XBOOLE_0:def 8,XXREAL_0:1;
end;

:: from CATALG_1, 2009.09.08, A.T.

theorem Th7X:
  for A being set, i being Nat holds i <> 0 & A = {} iff i-tuples_on A = {}
proof
  let A be set, i be Nat;
  hereby
    assume i <> 0;
    then
A1: 0+1 <= i by NAT_1:13;
    assume that
A2: A = {} and
A3: i-tuples_on A <> {};
    reconsider B = i-tuples_on A as non empty FinSequenceSet of A by A3;
    consider p being Element of B;
    B = {s where s is Element of A*: len s = i} & p in B;
    then ex s being Element of A* st p = s & len s = i;
    then 1 in dom p by A1,Th27;
    then
X:    rng p <> {} by RELAT_1:65;
    thus contradiction by A2,X;
  end;
  assume that
A4: i-tuples_on A = {} and
A5: i = 0 or A <> {};
  len (<*>A) = 0;
  then reconsider A as non empty set by A4,A5,FINSEQ_2:153;
  i-tuples_on A is non empty;
  hence contradiction by A4;
end;

:: from AMISTD_2, 2009.09.08, A.T.

registration
  let i be Nat, D be set;
  cluster i-tuples_on D -> with_common_domain;
  coherence
  proof
    set S = i-tuples_on D;
    let f, g be Function such that
A1: f in S and
A2: g in S;
A4: ex s being Element of D* st f = s & len s = i by A1;
    ex s being Element of D* st g = s & len s = i by A2;
    hence thesis by A4,Th31;
  end;
end;

registration
  let i be Nat, D be set;
  cluster i-tuples_on D -> product-like;
  coherence
  proof
    set S = i-tuples_on D;
    per cases;
    suppose D = {} & i = 0;
      then S = { <*>D } by FINSEQ_2:112
        .= {{}};
      hence thesis by CARD_3:19;
    end;
    suppose D = {} & i <> 0;
      then
A1:   S = {} by Th7X;
      take f = 0 .--> {};
      rng f = {{}} by FUNCOP_1:14;
      then {} in rng f by TARSKI:def 1;
      hence thesis by A1,CARD_3:37;
    end;
    suppose D <> {};
      then reconsider D as non empty set;
      set S = i-tuples_on D;
      take product" S;
      S = product product" S
      proof
        thus S c= product product" S by CARD_3:94;
        let x be set;
        assume x in product product" S;
        then consider g being Function such that
A2:     x = g and
A3:     dom g = dom product" S and
A4:     for z being set st z in dom product" S holds g.z in (product" S).z
        by CARD_3:def 5;
        consider s being Element of S;
        s in S;
        then consider t being Element of D* such that
A6:     s = t and
A7:     len t = i;
A8:     dom g = DOM S by A3,CARD_3:92
          .= dom s by CARD_3:def 12;
        dom s = Seg len t by A6,FINSEQ_1:def 3;
        then
A9:     g is FinSequence by A8,FINSEQ_1:def 2;
        rng g c= D
        proof
          let y be set;
          assume y in rng g;
          then consider a being set such that
A10:      a in dom g and
A11:      g.a = y by FUNCT_1:def 5;
          g.a in (product" S).a by A3,A4,A10;
          then g.a in pi(S,a) by A3,A10,CARD_3:def 13;
          then consider f being Function such that
A12:      f in S and
A13:      g.a = f.a by CARD_3:def 6;
          consider w being Element of D* such that
A14:      f = w and len w = i by A12;
          dom g = dom w by A8,A12,A14,CARD_3:def 10;
          then
X:          w.a in rng w by A10,FUNCT_1:def 5;
          thus thesis by A11,A13,A14,X;
        end;
        then reconsider g as FinSequence of D by A9,FINSEQ_1:def 4;
A15:    g in D* by FINSEQ_1:def 11;
        len g = i by A6,A7,A8,Th31;
        hence thesis by A2,A15;
      end;
      hence thesis;
    end;
  end;
end;

begin :: Moved from ALG_1, 2010.03.17

reserve
  n for Element of NAT;

theorem
  for D1,D2 be non empty set, p be FinSequence of D1, f be Function
  of D1,D2 holds dom(f*p) = dom p & len (f*p) = len p & for n being Nat st n in
  dom (f*p) holds (f*p).n = f.(p.n)
proof
  let D1,D2 be non empty set, p be FinSequence of D1, f be Function of D1,D2;
A1: rng p c= D1 & dom f = D1 by FUNCT_2:def 1;
  hence dom(f*p) = dom p by RELAT_1:46;
  dom(f*p) = dom p by A1,RELAT_1:46;
  hence len(f*p) = len p by Th31;
  let n be Nat;
  assume n in dom (f*p);
  hence thesis by FUNCT_1:22;
end;

definition
  let D be non empty set, R be Relation of D;
  func ExtendRel(R) -> Relation of D* means
  :Def9:
  for x,y be FinSequence of D
holds [x,y] in it iff len x = len y & for n st n in dom x holds [x.n,y.n] in R;
  existence
  proof
    defpred P[set,set] means for a,b be FinSequence of D st a = $1 & b = $2
    holds len a = len b & for n st n in dom a holds [a.n,b.n] in R;
    consider P be Relation of D*,D* such that
A1: for x,y be set holds [x,y] in P iff x in D* & y in D* & P[x,y]
    from RELSET_1:sch 1;
    take P;
    let a,b be FinSequence of D;
    thus [a,b] in P implies len a = len b & for n st n in dom a holds [a.n,b.n
    ] in R by A1;
    assume len a = len b & for n st n in dom a holds [a.n,b.n] in R;
    then
A2: P[a,b];
    a in D* & b in D* by FINSEQ_1:def 11;
    hence thesis by A1,A2;
  end;
  uniqueness
  proof
    let P,Q be Relation of D*;
    assume that
A3: for x,y be FinSequence of D holds [x,y] in P iff len x = len y &
    for n st n in dom x holds [x.n,y.n] in R and
A4: for x,y be FinSequence of D holds [x,y] in Q iff len x = len y &
    for n st n in dom x holds [x.n,y.n] in R;
    for a,b be set holds [a,b] in P iff [a,b] in Q
    proof
      let a,b be set;
      thus [a,b] in P implies [a,b] in Q
      proof
        assume
A5:     [a,b] in P;
        then reconsider a1 = a, b1 = b as Element of D* by ZFMISC_1:106;
        len a1 = len b1 & for n st n in dom a1 holds [a1.n,b1.n] in R by A3,A5;
        hence thesis by A4;
      end;
      assume
A6:   [a,b] in Q;
      then reconsider a1 = a, b1 = b as Element of D* by ZFMISC_1:106;
      len a1 = len b1 & for n st n in dom a1 holds [a1.n,b1.n] in R by A4,A6;
      hence thesis by A3;
    end;
    hence thesis by RELAT_1:def 2;
  end;
end;

theorem
  for D be non empty set holds ExtendRel(id D) = id (D*)
proof
  let D be non empty set;
  set P = ExtendRel(id D), Q = id(D*);
  for a,b be set holds [a,b] in P iff [a,b] in Q
  proof
    let a,b be set;
    thus [a,b] in P implies [a,b] in Q
    proof
      assume
A1:   [a,b] in P;
      then reconsider a1 = a, b1 = b as Element of D* by ZFMISC_1:106;
A2:   now
        let n be Nat;
        assume n in dom a1;
        then [a1.n,b1.n] in id D by A1,Def9;
        hence a1.n = b1.n by RELAT_1:def 10;
      end;
      len a1 = len b1 by A1,Def9;
      then a1 = b1 by A2,FINSEQ_2:10;
      hence thesis by RELAT_1:def 10;
    end;
    assume
A3: [a,b] in Q;
    then reconsider a1 = a, b1 = b as Element of D* by ZFMISC_1:106;
A4: a1 = b1 by A3,RELAT_1:def 10;
A5: for n st n in dom a1 holds [a1.n,b1.n] in id D
    proof
      let n;
      assume n in dom a1;
      then a1.n in D by FINSEQ_2:13;
      hence thesis by A4,RELAT_1:def 10;
    end;
    len a1 = len b1 by A3,RELAT_1:def 10;
    hence thesis by A5,Def9;
  end;
  hence thesis by RELAT_1:def 2;
end;

definition
  let D be non empty set, R be Equivalence_Relation of D;
  let y be FinSequence of Class(R), x be FinSequence of D;
  pred x is_representatives_FS y means
  len x = len y & for n st n in dom x holds Class(R,x.n) = y.n;
end;

theorem
  for D be non empty set, R be Equivalence_Relation of D, y be
  FinSequence of Class(R) ex x be FinSequence of D st x is_representatives_FS y
proof
  let D be non empty set, R be Equivalence_Relation of D, y be FinSequence of
  Class(R);
  defpred P[set,set] means for u be Element of D st $2 = u holds Class(R,u) =
  y.$1;
A1: for e be set st e in dom y ex u be set st u in D & P[e,u]
  proof
    let e be set;
    assume e in dom y;
    then y.e in rng y by FUNCT_1:def 5;
    then consider a be Element of D such that
A2: y.e = Class(R,a) by EQREL_1:45;
    reconsider b = a as set;
    take b;
    thus b in D;
    let u be Element of D;
    assume b = u;
    hence thesis by A2;
  end;
  consider f being Function such that
A3: dom f = dom y & rng f c= D & for e be set st e in dom y holds P[e,f.
  e] from WELLORD2:sch 1(A1);
  dom f = Seg len y by A3,FINSEQ_1:def 3;
  then reconsider f as FinSequence by FINSEQ_1:def 2;
  reconsider f as FinSequence of D by A3,FINSEQ_1:def 4;
  take f;
  thus len f = len y by A3,Th31;
  let n;
  assume
A4: n in dom f;
  then f.n in rng f by FUNCT_1:def 5;
  then reconsider u = f.n as Element of D;
  Class(R,u) = y.n by A3,A4;
  hence thesis;
end;
