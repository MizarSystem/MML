:: Non-contiguous Substrings and One-to-one Finite Sequences
::  by Wojciech A. Trybulec
::
:: Received April 8, 1990
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies FINSEQ_1, BOOLE, ARYTM_1, ZFMISC_1, RELAT_1, INT_1, FUNCT_1,
      FINSET_1, CARD_1, TARSKI, FINSEQ_2, ORDINAL2, ARYTM, MATRIX_2, FUNCT_2,
      SUBSET_1;
 notations TARSKI, XBOOLE_0, ENUMSET1, SUBSET_1, CARD_1, NUMBERS, ORDINAL1,
      XCMPLX_0, RELAT_1, FUNCT_1, FINSEQ_1, FUNCT_2, FINSEQ_2, FINSET_1, NAT_1,
      INT_1, FINSEQOP, XXREAL_0;
 constructors ENUMSET1, PARTFUN1, WELLORD2, XXREAL_0, REAL_1, NAT_1, INT_1,
      FINSEQOP;
 registrations XBOOLE_0, RELAT_1, FUNCT_1, ORDINAL1, FINSET_1, XXREAL_0,
      XREAL_0, NAT_1, INT_1, FINSEQ_1, CARD_1;
 requirements NUMERALS, REAL, SUBSET, BOOLE, ARITHM;
 definitions FUNCT_1, TARSKI, WELLORD2, XBOOLE_0, FINSEQ_2;
 theorems CARD_1, CARD_2, ENUMSET1, FINSEQ_1, FINSEQ_2, FINSET_1, FUNCT_1,
      INT_1, NAT_1, TARSKI, TREES_1, WELLORD2, ZFMISC_1, RELAT_1, XREAL_1,
      XBOOLE_0, XBOOLE_1, FUNCT_2, GRFUNC_1, SUBSET_1, XXREAL_0, ORDINAL1,
      NUMBERS;
 schemes FINSEQ_1, FUNCT_1, NAT_1, CLASSES1;

begin

reserve p,q,r for FinSequence;
reserve u,v,x,y,y1,y2,z,A,D,X,Y for set;
reserve i,j,k,l,m,n for natural number;

theorem Th1:
  Seg 3 = {1,2,3}
proof
  thus Seg 3 = {1,2} \/ {2 + 1} by FINSEQ_1:4,11
    .= {1,2,3} by ENUMSET1:43;
end;

theorem Th2:
  Seg 4 = {1,2,3,4}
proof
  thus Seg 4 = {1,2,3} \/ {3 + 1} by Th1,FINSEQ_1:11
    .= {1,2,3,4} by ENUMSET1:46;
end;

theorem Th3:
  Seg 5 = {1,2,3,4,5}
proof
  thus Seg 5 = {1,2,3,4} \/ {4 + 1} by Th2,FINSEQ_1:11
    .= {1,2,3,4,5} by ENUMSET1:50;
end;

theorem Th4:
  Seg 6 = {1,2,3,4,5,6}
proof
  thus Seg 6 = {1,2,3,4,5} \/ {5 + 1} by Th3,FINSEQ_1:11
    .= {1,2,3,4,5,6} by ENUMSET1:55;
end;

theorem Th5:
  Seg 7 = {1,2,3,4,5,6,7}
proof
  thus Seg 7 = {1,2,3,4,5,6} \/ {6 + 1} by Th4,FINSEQ_1:11
    .= {1,2,3,4,5,6,7} by ENUMSET1:61;
end;

theorem
  Seg 8 = {1,2,3,4,5,6,7,8}
proof
  thus Seg 8 = {1,2,3,4,5,6,7} \/ {7 + 1} by Th5,FINSEQ_1:11
    .= {1,2,3,4,5,6,7,8} by ENUMSET1:68;
end;

theorem
  Seg k = {} iff not k in Seg k by FINSEQ_1:4,5;

canceled;

theorem Th9:
  not k + 1 in Seg k
proof
  assume k + 1 in Seg k;
  then k + 1 <= k & k <= k + 1 by FINSEQ_1:3,NAT_1:12;
  then k + 0 = k + 1 by XXREAL_0:1;
  hence thesis;
end;

theorem
  k <> 0 implies k in Seg(k + n)
proof
  assume
A1: k <> 0;
  0 + 1 <= k by A1,NAT_1:13;
  then 1 <= k & k <= k + n by NAT_1:12;
  hence thesis by FINSEQ_1:3;
end;

theorem Th11:
  k + n in Seg k implies n = 0
proof
  assume k + n in Seg k;
  then k + n <= k + 0 by FINSEQ_1:3;
  then n <= 0 & 0 <= n by XREAL_1:8;
  hence thesis;
end;

theorem
  k < n implies k + 1 in Seg n
proof
  assume that
A1: k < n;
A2: 1 <= k + 1 by NAT_1:12;
  k + 1 <= n by A1,NAT_1:13;
  hence thesis by A2,FINSEQ_1:3;
end;

theorem Th13:
  k in Seg n & m < k implies k - m in Seg n
proof
  assume that
A1: k in Seg n and
A2: m < k;
  consider i being Nat such that
A3: k = m + i by A2,NAT_1:10;
  reconsider x = k - m as Element of NAT by A3,ORDINAL1:def 13;
A4: now
    assume not 1 <= x;
    then x = 0 by NAT_1:14;
    hence contradiction by A2;
  end;
  i <= k & k <= n by A1,A3,FINSEQ_1:3,NAT_1:12;
  then x <= n by A3,XXREAL_0:2;
  hence thesis by A4,FINSEQ_1:3;
end;

theorem
  k - n in Seg k iff n < k
proof
  thus k - n in Seg k implies n < k
  proof
    assume
A1: k - n in Seg k;
    then reconsider x = k - n as Element of NAT;
    assume not n < k;
    then k - n <= n - n by XREAL_1:11;
    then x = 0;
    hence contradiction by A1,FINSEQ_1:3;
  end;
  thus thesis by Th13,FINSEQ_1:5;
end;

theorem Th15:
  Seg k misses {k + 1}
proof
  assume not thesis;
  then
A1: Seg k /\ {k + 1} <> {} by XBOOLE_0:def 7;
  consider x being Element of Seg k /\ {k + 1};
A2: x in Seg k by A1,XBOOLE_0:def 4;
  then reconsider x as Element of NAT;
  x in {k + 1} by A1,XBOOLE_0:def 4;
  then x = k + 1 by TARSKI:def 1;
  then x <= k & k < x by A2,FINSEQ_1:3,XREAL_1:31;
  hence thesis;
end;

theorem
  Seg(k + 1) \ Seg k = {k + 1}
proof
A1: Seg(k + 1) = Seg k \/ {k + 1} by FINSEQ_1:11;
  Seg k misses {k + 1} by Th15;
  hence thesis by A1,XBOOLE_1:88;
end;

:: Theorem Seg(k + 1) \ {k + 1} = Seg k is
:: proved in RLVECT_1 and has a number 104.

theorem
  Seg k <> Seg(k + 1) by Th9,FINSEQ_1:6;

theorem
  Seg k = Seg(k + n) implies n = 0 by Th11,FINSEQ_1:5;

theorem Th19:
  Seg k c= Seg(k + n)
proof k <= k + n by NAT_1:12;
  hence thesis by FINSEQ_1:7;
end;

theorem Th20:
  Seg k, Seg n are_c=-comparable
proof
  n <= k or k <= n;
  then Seg n c= Seg k or Seg k c= Seg n by FINSEQ_1:7;
  hence thesis by XBOOLE_0:def 9;
end;

canceled;

theorem Th22:
  Seg k = {y} implies k = 1 & y = 1
proof
  assume
A1: Seg k = {y};
  now per cases;
    suppose k = 0;
      hence thesis by A1;
    end;
    suppose k <> 0;
      then
A2:   k in Seg k by FINSEQ_1:5;
      then 1 <= k by FINSEQ_1:3;
      then Seg 1 c= Seg k & Seg 1 <> {} by FINSEQ_1:7;
      then Seg 1 = {y} by A1,ZFMISC_1:39;
      hence thesis by A1,A2,FINSEQ_1:4,TARSKI:def 1,ZFMISC_1:6;
    end;
  end;
  hence thesis;
end;

theorem
  Seg k = {x,y} & x <> y implies k = 2 & {x,y} = {1,2}
proof
  assume that
A1: Seg k = {x,y} and
A2: x <> y;
  now per cases;
    suppose k = 0;
      hence thesis by A1;
    end;
    suppose
A3:   k <> 0;
      now per cases;
        suppose k = 1;
          hence thesis by A1,A2,FINSEQ_1:4,ZFMISC_1:9;
        end;
        suppose
A4:       k <> 1;
          1 <= k by A3,NAT_1:14;
          then 1 < k by A4,XXREAL_0:1;
          then
A5:       1 + 1 <= k by NAT_1:13;
          then Seg 2 c= Seg k by FINSEQ_1:7;
          then
A6:       1 = x & 2 = x or 1 = x & 2 = y or 2 = x & 1 = y or
          1 = y & 2 = y by A1,FINSEQ_1:4,ZFMISC_1:28;
          now
            assume
A7:         not k <= 2;
            k in Seg k by A1,FINSEQ_1:4,5;
            then (k = 1 or k = 2) & 1 <= 3 & 2 <= 3 by A1,A6,TARSKI:def 2;
            hence contradiction by A7;
          end;
          hence thesis by A1,A5,FINSEQ_1:4,XXREAL_0:1;
        end;
      end;
      hence thesis;
    end;
  end;
  hence thesis;
end;

theorem Th24:
  x in dom p implies x in dom(p ^ q)
proof
  dom p c= dom(p ^ q) by FINSEQ_1:39;
  hence thesis;
end;

theorem
  x in dom p implies x is Element of NAT;

theorem Th26:
  x in dom p implies x <> 0
proof
  assume x in dom p;
  then x in Seg(len p) by FINSEQ_1:def 3;
  hence thesis by FINSEQ_1:3;
end;

theorem Th27:
  n in dom p iff 1 <= n & n <= len p
proof
  thus n in dom p implies 1 <= n & n <= len p
  proof
    assume n in dom p;
    then n in Seg(len p) by FINSEQ_1:def 3;
    hence thesis by FINSEQ_1:3;
  end;
  assume 1 <= n & n <= len p;
  then n in Seg(len p) by FINSEQ_1:3;
  hence thesis by FINSEQ_1:def 3;
end;

theorem
  n in dom p iff n - 1 is Element of NAT & len p - n is Element of NAT
proof
  thus n in dom p implies n - 1 is Element of NAT
  & len p - n is Element of NAT
  proof
    assume n in dom p;
    then 1 <= n & n <= len p & 1 is Integer & n is Integer & len p is Integer
    by Th27;
    hence thesis by INT_1:18;
  end;
  assume n - 1 is Element of NAT & len p - n is Element of NAT;
  then 0 + 1 <= n & 0 + n <= len p by XREAL_1:21;
  hence thesis by Th27;
end;

theorem Th29:
  dom<* x,y *> = Seg(2)
proof len<* x,y *> = 2 by FINSEQ_1:61;
  hence thesis by FINSEQ_1:def 3;
end;

theorem Th30:
  dom<* x,y,z *> = Seg(3)
proof len<* x,y,z *> = 3 by FINSEQ_1:62;
  hence dom<* x,y,z *> = Seg(3) by FINSEQ_1:def 3;
end;

theorem Th31:
  len p = len q iff dom p = dom q
proof
  dom p = Seg(len p) & dom q = Seg(len q) by FINSEQ_1:def 3;
  hence thesis by FINSEQ_1:8;
end;

theorem Th32:
  len p <= len q iff dom p c= dom q
proof
  dom p = Seg(len p) & dom q = Seg(len q) by FINSEQ_1:def 3;
  hence thesis by FINSEQ_1:7;
end;

theorem Th33:
  x in rng p implies 1 in dom p
proof
  assume x in rng p;
  then p <> {};
  then len p <> 0;
  then 1 <= 1 & 1 <= len p & dom p = Seg(len p) by FINSEQ_1:def 3,NAT_1:14;
  hence thesis by FINSEQ_1:3;
end;

theorem
  rng p <> {} implies 1 in dom p
proof consider y being Element of rng p;
  assume rng p <> {};
  then y in rng p;
  hence thesis by Th33;
end;

canceled 3;

theorem Th38:
  {} <> <* x,y *> by CARD_1:47,FINSEQ_1:61;

theorem
  {} <> <* x,y,z *> by CARD_1:47,FINSEQ_1:62;

theorem Th40:
  <* x *> <> <* y,z *>
proof
  len<* x *> = 1 & len<* y,z *> = 2 by FINSEQ_1:57,61;
  hence thesis;
end;

theorem
  <* u *> <> <* x,y,z *>
proof
  len<* u *> = 1 & len<* x,y,z *> = 3 by FINSEQ_1:57,62;
  hence thesis;
end;

theorem
  <* u,v *> <> <* x,y,z *>
proof
  len<* u,v *> = 2 & len<* x,y,z *> = 3 by FINSEQ_1:61,62;
  hence thesis;
end;

theorem Th43:
  len r = len p + len q &
  (for k being Element of NAT st k in dom p holds r.k = p.k) &
  (for k being Element of NAT st k in dom q
  holds r.(len p + k) = q.k) implies r = p ^ q
proof
  assume len r = len p + len q;
  then
A1: dom r = Seg(len p + len q) by FINSEQ_1:def 3;
  assume (for k being Element of NAT st k in dom p holds r.k = p.k) &
  (for k being Element of NAT st k in dom q holds r.(len p + k) = q.k);
  then (for k being Nat st k in dom p holds r.k = p.k) &
  (for k being Nat st k in dom q holds r.(len p + k) = q.k);
  hence thesis by A1,FINSEQ_1:def 7;
end;

Lm1: A c= Seg k implies Sgm A is one-to-one
proof
  assume
A1: A c= Seg k;
  let x,y;
  assume that
A2: x in dom(Sgm A) & y in dom(Sgm A) and
A3: Sgm(A).x = Sgm(A).y and
A4: x <> y;
A5: dom(Sgm A) = Seg(len(Sgm A)) by FINSEQ_1:def 3;
  reconsider i = x, j = y as Element of NAT by A2;
  Sgm(A).x in rng(Sgm A) & Sgm(A).y in rng(Sgm A) & rng(Sgm A) = A
  by A1,A2,FINSEQ_1:def 13,FUNCT_1:def 5;
  then Sgm(A).x in Seg k & Sgm(A).y in Seg k by A1;
  then reconsider m = Sgm(A).x, n = Sgm(A).y as Element of NAT;
  now per cases by A4,XXREAL_0:1;
    suppose
A6:   i < j;
      1 <= i & j <= len(Sgm A) by A2,A5,FINSEQ_1:3;
      then m < n by A1,A6,FINSEQ_1:def 13;
      hence contradiction by A3;
    end;
    suppose
A7:   j < i;
      1 <= j & i <= len(Sgm A) by A2,A5,FINSEQ_1:3;
      then n < m by A1,A7,FINSEQ_1:def 13;
      hence contradiction by A3;
    end;
  end;
  hence thesis;
end;

theorem Th44:
  for A being finite set st A c= Seg k holds len(Sgm A) = card A
proof
  let A be finite set;
  assume A c= Seg k;
  then dom(Sgm A) = Seg(len(Sgm A)) & rng(Sgm A) = A & Sgm A is one-to-one
  by Lm1,FINSEQ_1:def 3,def 13;
  then Seg(len(Sgm A)),A are_equipotent & Seg(len(Sgm A)) is finite
  by WELLORD2:def 4;
  then card(Seg(len(Sgm A))) = len(Sgm A) & card A = card(Seg(len(Sgm A)))
  by CARD_1:21,FINSEQ_1:78;
  hence thesis;
end;

theorem
  for A being finite set st A c= Seg k holds dom(Sgm A) = Seg(card A)
proof
  let A be finite set;
  assume A c= Seg k;
  then len(Sgm A) = card A & dom(Sgm A) = Seg(len(Sgm A))
  by Th44,FINSEQ_1:def 3;
  hence thesis;
end;

theorem Th46:
  X c= Seg i & k < l & 1 <= n & m <= len(Sgm X) &
  Sgm(X).m = k & Sgm(X).n = l implies m < n
proof
  assume that
A1: X c= Seg i and
A2: k < l and
A3: 1 <= n and
A4: m <= len(Sgm X) and
A5: Sgm(X).m = k & Sgm(X).n = l and
A6: not m < n;
  n < m by A2,A5,A6,XXREAL_0:1;
  hence thesis by A1,A2,A3,A4,A5,FINSEQ_1:def 13;
end;

canceled;

theorem Th48:
  X c= Seg i & Y c= Seg j implies
  ((for m,n being Element of NAT st m in X & n in Y holds m < n) iff
  Sgm(X \/ Y) = Sgm(X) ^ Sgm(Y))
proof
  assume that
A1: X c= Seg i and
A2: Y c= Seg j;
  Seg i, Seg j are_c=-comparable by Th20;
  then Seg i c= Seg j or Seg j c= Seg i by XBOOLE_0:def 9;
  then
A3: X c= Seg j or Y c= Seg i by A1,A2,XBOOLE_1:1;
  then
A4: X \/ Y c= Seg i or X \/ Y c= Seg j by A1,A2,XBOOLE_1:8;
  set p = Sgm X;
  set q = Sgm Y;
  set r = Sgm(X \/ Y);
  thus (for m,n being Element of NAT st m in X & n in Y holds m < n) implies
  Sgm(X \/ Y) = Sgm(X) ^ Sgm(Y)
  proof
    assume
A5: for m,n being Element of NAT st m in X & n in Y holds m < n;
    X /\ Y = {}
    proof
      assume
A6:   not thesis;
      consider x being Element of X /\ Y;
      X = rng p & Y = rng q by A1,A2,FINSEQ_1:def 13;
      then X c= NAT & Y c= NAT & x in X by A6,FINSEQ_1:def 4,XBOOLE_0:def 4;
      then reconsider m = x as Element of NAT;
      m in X & m in Y by A6,XBOOLE_0:def 4;
      hence thesis by A5;
    end;
    then
A7: X misses Y by XBOOLE_0:def 7;
    reconsider X1 = X, Y1 = Y as finite set by A1,A2;
A8: len r = card(X1 \/ Y1) by A1,A2,A3,Th44,XBOOLE_1:8
      .= card X1 + card Y1 by A7,CARD_2:53
      .= len p + card Y1 by A1,Th44
      .= len p + len q by A2,Th44;
    defpred P[Element of NAT] means $1 in dom p implies r.$1 = p.$1;
A9: P[0] by Th26;
A10: now
      let k be Element of NAT;
      assume
A11:  P[k];
      thus P[k+1]
      proof
        assume
A12:    k + 1 in dom p;
        assume
A13:    r.(k + 1) <> p.(k + 1);
A14:    p.(k + 1) in rng p & rng p c= NAT by A12,FINSEQ_1:def 4,FUNCT_1:def 5;
        then reconsider n = p.(k + 1) as Element of NAT;
        len p <= len r by A8,NAT_1:12;
        then Seg(len p) c= Seg(len r) by FINSEQ_1:7;
        then dom p c= Seg(len r) by FINSEQ_1:def 3;
        then
A15:    dom p c= dom r by FINSEQ_1:def 3;
        then
A16:    k + 1 in dom r by A12;
A17:    r.(k + 1) in rng r & rng r c= NAT by A12,A15,FINSEQ_1:def 4
,FUNCT_1:def 5;
        then reconsider m = r.(k + 1) as Element of NAT;
A18:    n in X & m in X \/ Y by A1,A4,A14,A17,FINSEQ_1:def 13;
        now per cases;
          suppose
A19:        k in dom p;
            then p.k in rng p by FUNCT_1:def 5;
            then reconsider n1 = p.k as Element of NAT by A14;
            r.k in rng r by A15,A19,FUNCT_1:def 5;
            then reconsider m1 = r.k as Element of NAT by A17;
            now per cases by A13,XXREAL_0:1;
              suppose
A20:            m < n;
                then not m in Y by A5,A18;
                then m in X by A18,XBOOLE_0:def 3;
                then m in rng p by A1,FINSEQ_1:def 13;
                then consider x such that
A21:            x in dom p and
A22:            p.x = m by FUNCT_1:def 5;
                reconsider x as Element of NAT by A21;
                k in Seg(len p) & k + 1 in
                Seg(len r) by A16,A19,FINSEQ_1:def 3;
                then 1 <= k & k < k + 1 & k + 1 <= len r
                by FINSEQ_1:3,XREAL_1:31;
                then
A23:            n1 < m by A4,A11,A19,FINSEQ_1:def 13;
                1 <= x & k <= len p by A19,A21,Th27;
                then
A24:            k < x by A1,A22,A23,Th46;
                1 <= k + 1 & x <= len p by A12,A21,Th27;
                then x < k + 1 by A1,A20,A22,Th46;
                hence contradiction by A24,NAT_1:13;
              end;
              suppose
A25:            n < m;
                n in X \/ Y by A18,XBOOLE_0:def 3;
                then n in rng r by A4,FINSEQ_1:def 13;
                then consider x such that
A26:            x in dom r and
A27:            r.x = n by FUNCT_1:def 5;
                reconsider x as Element of NAT by A26;
                1 <= k & k < k + 1 & k + 1 <= len p
                by A12,A19,Th27,XREAL_1:31;
                then
A28:            m1 < n by A1,A11,A19,FINSEQ_1:def 13;
                1 <= x & k <= len r by A15,A19,A26,Th27;
                then
A29:            k < x by A1,A2,A3,A27,A28,Th46,XBOOLE_1:8;
                1 <= k + 1 & x <= len r by A12,A26,Th27;
                then x < k + 1 by A1,A2,A3,A25,A27,Th46,XBOOLE_1:8;
                hence contradiction by A29,NAT_1:13;
              end;
            end;
            hence contradiction;
          end;
          suppose not k in dom p;
            then k < 1 or len p < k by Th27;
            then (k = 0 or len p < k) & k < k + 1 by NAT_1:14,XREAL_1:31;
            then
A30:        k = 0 or (len p < k + 1 & k + 1 <= len p) by A12,Th27,XXREAL_0:2;
            now per cases by A13,XXREAL_0:1;
              suppose
A31:            m < n;
                then not m in Y by A5,A18;
                then m in X by A18,XBOOLE_0:def 3;
                then m in rng p by A1,FINSEQ_1:def 13;
                then consider x such that
A32:            x in dom p and
A33:            p.x = m by FUNCT_1:def 5;
                reconsider x as Element of NAT by A32;
                1 <= k + 1 & x <= len p by A12,A32,Th27;
                then x < k + 1 by A1,A31,A33,Th46;
                hence contradiction by A30,A32,Th26,NAT_1:14;
              end;
              suppose
A34:            n < m;
                n in X \/ Y by A18,XBOOLE_0:def 3;
                then n in rng r by A4,FINSEQ_1:def 13;
                then consider x such that
A35:            x in dom r and
A36:            r.x = n by FUNCT_1:def 5;
                reconsider x as Element of NAT by A35;
                1 <= k + 1 & x <= len r by A12,A35,Th27;
                then x < k + 1 by A1,A2,A3,A34,A36,Th46,XBOOLE_1:8;
                hence contradiction by A30,A35,Th26,NAT_1:14;
              end;
            end;
            hence contradiction;
          end;
        end;
        hence contradiction;
      end;
    end;
A37: for k being Element of NAT holds P[k] from NAT_1:sch 1(A9,A10);
    defpred P[Element of NAT] means $1 in dom q implies r.(len p + $1) = q.$1;
A38: P[0] by Th26;
A39: now
      let k be Element of NAT;
      assume
A40:  P[k];
      thus P[k+1]
      proof
        assume
A41:    k + 1 in dom q;
        assume
A42:    r.(len p + (k + 1)) <> q.(k + 1);
        set a = len p + (k + 1);
A43:    q.(k + 1) in rng q & rng q c= NAT by A41,FINSEQ_1:def 4,FUNCT_1:def 5;
        then reconsider n = q.(k + 1) as Element of NAT;
        len p <= len p & k + 1 <= len q by A41,Th27;
        then
A44:    a <= len r by A8,XREAL_1:9;
A45:    1 <= k + 1 & k + 1 <= a by NAT_1:12;
        then 1 <= a by XXREAL_0:2;
        then
A46:    a in dom r by A44,Th27;
        then
A47:    r.a in rng r & rng r c= NAT by FINSEQ_1:def 4,FUNCT_1:def 5;
        then reconsider m = r.a as Element of NAT;
A48:    n in Y & m in X \/ Y by A2,A4,A43,A47,FINSEQ_1:def 13;
A49:    now
          assume m in X;
          then m in rng p by A1,FINSEQ_1:def 13;
          then consider x such that
A50:      x in dom p and
A51:      p.x = m by FUNCT_1:def 5;
          reconsider x as Element of NAT by A50;
A52:      r.x = r.a & r is one-to-one by A4,A37,A50,A51,Lm1;
          1 <= x & x <= len p & len p <= len r by A8,A50,Th27,NAT_1:12;
          then 1 <= x & x <= len r by XXREAL_0:2;
          then x in dom r by Th27;
          then x = a by A46,A52,FUNCT_1:def 8;
          then len p + (k + 1) <= len p + 0 by A50,Th27;
          hence contradiction by XREAL_1:31;
        end;
        now per cases;
          suppose
A53:        k in dom q;
            then q.k in rng q by FUNCT_1:def 5;
            then reconsider n1 = q.k as Element of NAT by A43;
A54:        1 <= k & k <= len q & k <= len p + k by A53,Th27,NAT_1:12;
            then
A55:        1 <= len p + k & len p + k <= len r by A8,XREAL_1:9,XXREAL_0:2;
            then len p + k in dom r by Th27;
            then r.(len p + k) in rng r by FUNCT_1:def 5;
            then reconsider m1 = r.(len p + k) as Element of NAT by A47;
            now per cases by A42,XXREAL_0:1;
              suppose
A56:            m < n;
                m in Y by A48,A49,XBOOLE_0:def 3;
                then m in rng q by A2,FINSEQ_1:def 13;
                then consider x such that
A57:            x in dom q and
A58:            q.x = m by FUNCT_1:def 5;
                reconsider x as Element of NAT by A57;
                len p + k < len p + k + 1 by XREAL_1:31;
                then
A59:            n1 < m by A4,A40,A44,A53,A55,FINSEQ_1:def 13;
                1 <= x & k <= len q by A53,A57,Th27;
                then
A60:            k < x by A2,A58,A59,Th46;
                1 <= k + 1 & x <= len q by A41,A57,Th27;
                then x < k + 1 by A2,A56,A58,Th46;
                hence contradiction by A60,NAT_1:13;
              end;
              suppose
A61:            n < m;
                n in X \/ Y by A48,XBOOLE_0:def 3;
                then n in rng r by A4,FINSEQ_1:def 13;
                then consider x such that
A62:            x in dom r and
A63:            r.x = n by FUNCT_1:def 5;
                reconsider x as Element of NAT by A62;
                1 <= k & k < k + 1 & k + 1 <= len q
                by A41,A53,Th27,XREAL_1:31;
                then
A64:            m1 < n by A2,A40,A53,FINSEQ_1:def 13;
                1 <= x & len p + k <= len r by A8,A54,A62,Th27,XREAL_1:9;
                then
A65:            len p + k < x by A1,A2,A3,A63,A64,Th46,XBOOLE_1:8;
                1 <= a & x <= len r by A45,A62,Th27,XXREAL_0:2;
                then x < len p + k + 1 by A1,A2,A3,A61,A63,Th46,XBOOLE_1:8;
                hence contradiction by A65,NAT_1:13;
              end;
            end;
            hence contradiction;
          end;
          suppose not k in dom q;
            then k < 1 or len q < k by Th27;
            then (k = 0 or len q < k) & k < k + 1 by NAT_1:14,XREAL_1:31;
            then
A66:        k = 0 or (len q < k + 1 & k + 1 <= len q) by A41,Th27,XXREAL_0:2;
            now per cases by A42,XXREAL_0:1;
              suppose
A67:            m < n;
                m in Y by A48,A49,XBOOLE_0:def 3;
                then m in rng q by A2,FINSEQ_1:def 13;
                then consider x such that
A68:            x in dom q and
A69:            q.x = m by FUNCT_1:def 5;
                reconsider x as Element of NAT by A68;
                1 <= k + 1 & x <= len q by A41,A68,Th27;
                then x < k + 1 by A2,A67,A69,Th46;
                hence contradiction by A66,A68,Th26,NAT_1:14;
              end;
              suppose
A70:            n < m;
                n in X \/ Y by A48,XBOOLE_0:def 3;
                then n in rng r by A4,FINSEQ_1:def 13;
                then consider x such that
A71:            x in dom r and
A72:            r.x = n by FUNCT_1:def 5;
                reconsider x as Element of NAT by A71;
                1 <= len p + 1 & x <= len r by A71,Th27,NAT_1:12;
                then x < len p + 1 by A1,A2,A3,A66,A70,A72,Th46,XBOOLE_1:8;
                then 1 <= x & x <= len p by A71,Th27,NAT_1:13;
                then x in dom p by Th27;
                then n = p.x & p.x in rng p by A37,A72,FUNCT_1:def 5;
                then n in X by A1,FINSEQ_1:def 13;
                hence contradiction by A5,A48;
              end;
            end;
            hence contradiction;
          end;
        end;
        hence contradiction;
      end;
    end;
    for k being Element of NAT holds P[k] from NAT_1:sch 1(A38,A39);
    hence thesis by A8,A37,Th43;
  end;
  assume
A73: Sgm(X \/ Y) = Sgm(X) ^ Sgm(Y);
  let m,n be Element of NAT;
  assume that
A74: m in X and
A75: n in Y;
  m in rng(Sgm X) by A1,A74,FINSEQ_1:def 13;
  then consider x such that
A76: x in dom p and
A77: p.x = m by FUNCT_1:def 5;
  reconsider x as Element of NAT by A76;
  n in rng q by A2,A75,FINSEQ_1:def 13;
  then consider y such that
A78: y in dom q and
A79: q.y = n by FUNCT_1:def 5;
  reconsider y as Element of NAT by A78;
A80: m = r.x & n = r.(len p + y) by A73,A76,A77,A78,A79,FINSEQ_1:def 7;
  x in Seg(len p) by A76,FINSEQ_1:def 3;
  then
A81: 1 <= x & x <= len p by FINSEQ_1:3;
  y <> 0 by A78,Th26;
  then
A82: x < len p + y by A81,NAT_1:16,XXREAL_0:2;
  y <= len q by A78,Th27;
  then len p + y <= len p + len q by XREAL_1:9;
  then len p + y <= len r by A73,FINSEQ_1:35;
  hence thesis by A4,A80,A81,A82,FINSEQ_1:def 13;
end;

theorem Th49:
  Sgm {} = {}
proof
  {} c= Seg 0;
  hence thesis by FINSEQ_1:72;
end;

:: The other way of the one above - FINSEQ_1:72.

theorem
  0 <> n implies Sgm{n} = <* n *>
proof
  assume 0 <> n;
  then n in Seg n by FINSEQ_1:5;
  then {n} c= Seg n by ZFMISC_1:37;
  then rng(Sgm{n}) = {n} & len(Sgm{n}) = card{n} by Th44,FINSEQ_1:def 13;
  then rng(Sgm{n}) = {n} & len(Sgm{n}) = 1 by CARD_1:50;
  hence thesis by FINSEQ_1:56;
end;

theorem
  0 < n & n < m implies Sgm{n,m} = <* n,m *>
proof
  assume that
A1: 0 < n and
A2: n < m;
  Seg n c= Seg m & m in Seg m & n in Seg n by A1,A2,FINSEQ_1:5,7;
  then
A3: {n,m} c= Seg m by ZFMISC_1:38;
  then
A4: len(Sgm{n,m}) = card{n,m} by Th44
    .= 2 by A2,CARD_2:76;
  then dom(Sgm{n,m}) = {1,2} by FINSEQ_1:4,def 3;
  then
A5: 1 in dom(Sgm{n,m}) & 2 in dom(Sgm{n,m}) & rng(Sgm{n,m}) = {n,m}
  by A3,FINSEQ_1:def 13,TARSKI:def 2;
  then
A6: Sgm{n,m}.1 in {n,m} & Sgm{n,m}.2 in {n,m} by FUNCT_1:def 5;
A7: Sgm{n,m} is one-to-one by A3,Lm1;
  now per cases by A6,TARSKI:def 2;
    suppose Sgm{n,m}.1 = n & Sgm{n,m}.2 = n;
      hence Sgm{n,m}.1 = n & Sgm{n,m}.2 = m by A5,A7,FUNCT_1:def 8;
    end;
    suppose Sgm{n,m}.1 = m & Sgm{n,m}.2 = m;
      hence Sgm{n,m}.1 = n & Sgm{n,m}.2 = m by A5,A7,FUNCT_1:def 8;
    end;
    suppose Sgm{n,m}.1 = n & Sgm{n,m}.2 = m;
      hence Sgm{n,m}.1 = n & Sgm{n,m}.2 = m;
    end;
    suppose Sgm{n,m}.1 = m & Sgm{n,m}.2 = n;
      hence Sgm{n,m}.1 = n & Sgm{n,m}.2 = m by A2,A3,A4,FINSEQ_1:def 13;
    end;
  end;
  hence thesis by A4,FINSEQ_1:61;
end;

theorem Th52:
  len(Sgm(Seg k)) = k
proof
  Seg k c= Seg k & card(Seg k) = k by FINSEQ_1:78;
  hence thesis by Th44;
end;

Lm2: Sgm(Seg(k + 0)) | Seg k = Sgm(Seg k)
proof k + 0 = k & Seg k c= Seg k & card(Seg k) = k by FINSEQ_1:78;
  then len(Sgm(Seg k)) = k by Th44;
  then dom(Sgm(Seg k)) = Seg k by FINSEQ_1:def 3;
  hence thesis by RELAT_1:97;
end;

Lm3: now
  let n;
  assume
A1: for k holds Sgm(Seg(k + n)) | Seg k = Sgm(Seg k);
  let k;
  set X = Sgm(Seg(k + (n + 1)));
  set Y = Sgm(Seg(k + 1));
  X = Sgm(Seg(k + 1 + n));
  then
A2: X | Seg(k + 1) = Y by A1;
  Y | Seg k = Sgm(Seg k)
  proof reconsider p = Y | Seg k as FinSequence of NAT by FINSEQ_1:23;
A3: rng Y = Seg(k + 1) by FINSEQ_1:def 13;
A4: Y is one-to-one by Lm1;
A5: len Y = k + 1 by Th52;
    then
A6: dom Y = Seg(k + 1) & k <= k + 1 by FINSEQ_1:def 3,NAT_1:12;
    then
A7: dom p = Seg k by A5,FINSEQ_1:21;
A8: Y.(k + 1) = k + 1
    proof
      assume
A9:   not thesis;
      k + 1 in dom Y by A6,FINSEQ_1:6;
      then
A10:  Y.(k + 1) in Seg(k + 1) & not Y.(k + 1) in {k + 1}
      by A3,A9,FUNCT_1:def 5,TARSKI:def 1;
      then Y.(k + 1) in Seg(k + 1) \ {k + 1} by XBOOLE_0:def 5;
      then
A11:  Y.(k + 1) in Seg k by FINSEQ_1:12;
      reconsider n = Y.(k + 1) as Element of NAT by A10;
      k + 1 in rng Y by A3,FINSEQ_1:6;
      then consider x such that
A12:  x in dom Y and
A13:  Y.x = k + 1 by FUNCT_1:def 5;
      reconsider x as Element of NAT by A12;
A14:  1 <= x & x <= k + 1 by A5,A12,Th27;
      now
        assume x = k + 1;
        then n = k + 1 & n <= k & k < k + 1 by A11,A13,FINSEQ_1:3,XREAL_1:31;
        hence contradiction;
      end;
      then x < k + 1 by A14,XXREAL_0:1;
      then k + 1 < n & n <= k & k < k + 1
      by A5,A11,A13,A14,FINSEQ_1:3,def 13,XREAL_1:31;
      hence contradiction by XXREAL_0:2;
    end;
    rng p = rng Y \ {Y.(k + 1)}
    proof
      thus rng p c= rng Y \ {Y.(k + 1)}
      proof
        let x;
        assume
A15:    x in rng p;
A16:    rng p c= rng Y by RELAT_1:99;
        now
          assume
A17:      x in {k + 1};
          consider y such that
A18:      y in dom p and
A19:      p.y = x by A15,FUNCT_1:def 5;
          reconsider y as Element of NAT by A18;
          Seg k c= Seg(k + 1) by Th19;
          then
A20:      y in dom Y & Y.y = p.y & x = k + 1 & k + 1 in dom Y
          by A6,A7,A17,A18,FINSEQ_1:6,FUNCT_1:70,TARSKI:def 1;
          y <= k & k < k + 1 by A7,A18,FINSEQ_1:3,XREAL_1:31;
          hence contradiction by A4,A8,A19,A20,FUNCT_1:def 8;
        end;
        hence thesis by A8,A15,A16,XBOOLE_0:def 5;
      end;
      let x;
      assume
A21:  x in rng Y \ {Y.(k + 1)};
      then consider y such that
A22:  y in dom Y and
A23:  Y.y = x by FUNCT_1:def 5;
      now
        assume y in {k + 1};
        then x = k + 1 & not x in
        {k + 1} by A8,A21,A23,TARSKI:def 1,XBOOLE_0:def 5;
        hence contradiction by TARSKI:def 1;
      end;
      then y in Seg(k + 1) \ {k + 1} by A6,A22,XBOOLE_0:def 5;
      then
A24:  y in dom p by A7,FINSEQ_1:12;
      then p.y = Y.y by FUNCT_1:70;
      hence thesis by A23,A24,FUNCT_1:def 5;
    end;
    then
A25: rng p = Seg k by A3,A8,FINSEQ_1:12;
    now
      let i,j,l,m;
      assume that
A26:  1 <= i & i < j & j <= len p and
A27:  l = p.i & m = p.j;
A28:  len p = k & i <= len p & 1 <= j by A5,A6,A26,FINSEQ_1:21,XXREAL_0:2;
      then i in dom p & j in dom p & len Y = k + 1 & k <= k + 1
      by A7,A26,Th52,FINSEQ_1:3,NAT_1:12;
      then p.i = Y.i & p.j = Y.j & j <= len Y by A26,A28,FUNCT_1:70,XXREAL_0:2;
      hence l < m by A26,A27,FINSEQ_1:def 13;
    end;
    hence thesis by A25,FINSEQ_1:def 13;
  end;
  then Sgm(Seg k) = X | (Seg(k + 1) /\ Seg k) & k <= k + 1
  by A2,NAT_1:12,RELAT_1:100;
  hence Sgm(Seg(k + (n + 1))) | Seg k = Sgm(Seg k) by FINSEQ_1:9;
end;

Lm4: for k holds Sgm(Seg(k + n)) | Seg k = Sgm(Seg k)
proof
  defpred P[natural number] means
  for k holds Sgm(Seg(k + $1)) | Seg k = Sgm(Seg k);
A1: P[0] by Lm2;
A2: for k st P[k] holds P[k+1] by Lm3;
  for n holds P[n] from NAT_1:sch 2(A1,A2);
  hence thesis;
end;

theorem
  Sgm(Seg(k + n)) | Seg k = Sgm(Seg k) by Lm4;

Lm5: now
  let k be Nat;
  assume
A1: Sgm(Seg k) = idseq k;
A2: len(idseq(k + 1)) = k + 1 by FINSEQ_2:55;
  then
A3: len(Sgm(Seg(k + 1))) = len(idseq(k + 1)) by Th52;
then
X: dom Sgm(Seg(k + 1)) = Seg(k+1)  by A2,FINSEQ_1:def 3;
  now
    let j be Nat;
    assume
A4: j in dom Sgm(Seg(k + 1));
    then
A5: j in Seg k \/ {k + 1} by X,FINSEQ_1:11;
    now per cases by A5,XBOOLE_0:def 3;
      suppose
A6:     j in Seg k;
        then Sgm(Seg k).j = j & (idseq k qua FinSequence).j = j &
        idseq(k + 1).j = j & Sgm(Seg(k + 1)) | Seg k = Sgm(Seg k) &
        dom(Sgm(Seg(k + 1))) = Seg(k + 1) by A1,A4,Lm4,X,FUNCT_1:35;
        hence Sgm(Seg(k + 1)).j = idseq(k + 1).j by A6,FUNCT_1:72;
      end;
      suppose j in {k + 1};
        then
A7:     j = k + 1 by TARSKI:def 1;
        then
A8:     j in Seg(k + 1) by FINSEQ_1:6;
        set X = Sgm(Seg(k + 1));
        set Y = Sgm(Seg k);
        now
          assume X.j <> j;
          then
A9:       not X.j in {j} & Seg(k + 1) c= Seg(k + 1) by TARSKI:def 1;
A10:      rng X = Seg(k + 1) & dom X = Seg(k + 1)
          by A2,A3,FINSEQ_1:def 3,def 13;
          then
A11:      X.j in Seg(k + 1) by A4,FUNCT_1:def 5;
          then X.j in Seg(k + 1) \ {k + 1} by A7,A9,XBOOLE_0:def 5;
          then
A12:      X.j in Seg k by FINSEQ_1:12;
          reconsider n = X.j as Element of NAT by A11;
A13:      X is one-to-one by Lm1;
          X | Seg k = Y by Lm4;
          then
A14:      X.n = Y.n by A12,FUNCT_1:72
            .= n by A1,A12,FUNCT_1:35;
          n <= k & k < k + 1 by A12,FINSEQ_1:3,XREAL_1:31;
          hence contradiction by A7,A8,A10,A11,A13,A14,FUNCT_1:def 8;
        end;
        hence Sgm(Seg(k + 1)).j = idseq(k + 1).j by A7,FINSEQ_1:6,FUNCT_1:35;
      end;
    end;
    hence Sgm(Seg(k + 1)).j = idseq(k + 1).j;
  end;
  hence Sgm(Seg(k + 1)) = idseq(k + 1) by A3,FINSEQ_2:10;
end;

theorem Th54:
  Sgm(Seg k) = idseq k
proof
  defpred P[Element of NAT] means Sgm(Seg $1) = idseq $1;
A1: P[0] by Th49,FINSEQ_2:58;
A2: for k being Element of NAT st P[k] holds P[k+1] by Lm5;
A3: for k being Element of NAT holds P[k] from NAT_1:sch 1(A1,A2);
  k is Element of NAT by ORDINAL1:def 13;
  hence thesis by A3;
end;

theorem Th55:
  p | Seg n = p iff len p <= n
proof
  n is Element of NAT by ORDINAL1:def 13;
  hence p | Seg n = p implies len p <= n by TREES_1:1;
  assume len p <= n;
  then Seg(len p) c= Seg n by FINSEQ_1:7;
  then dom p c= Seg n by FINSEQ_1:def 3;
  then
A1: dom p = dom p /\ Seg n by XBOOLE_1:28;
  for x st x in dom p holds p.x = p.x;
  hence thesis by A1,FUNCT_1:68;
end;

theorem Th56:
  idseq(n + k) | Seg n = idseq n
proof Sgm(Seg(n + k)) = idseq(n + k) & Sgm(Seg n) = idseq n &
  Sgm(Seg(n + k)) | Seg n = Sgm(Seg n) by Lm4,Th54;
  hence thesis;
end;

theorem
  idseq n | Seg m = idseq m iff m <= n
proof
A1: n is Element of NAT & m is Element of NAT by ORDINAL1:def 13;
  thus idseq n | Seg m = idseq m implies m <= n
  proof
    assume idseq n | Seg m = idseq m;
    then len(idseq m) <= len(idseq n) by A1,TREES_1:2;
    then m <= len(idseq n) by FINSEQ_2:55;
    hence thesis by FINSEQ_2:55;
  end;
  assume m <= n;
  then ex j being Nat st n = m + j by NAT_1:10;
  hence idseq n | Seg m = idseq m by Th56;
end;

theorem
  idseq n | Seg m = idseq n iff n <= m
proof len(idseq n) = n by FINSEQ_2:55;
  hence thesis by Th55;
end;

theorem Th59:
  len p = k + l & q = p | Seg k implies len q = k
proof
  assume that
A1: len p = k + l and
A2: q = p | Seg k;
  k <= len p by A1,NAT_1:12;
  hence thesis by A2,FINSEQ_1:21;
end;

theorem
  len p = k + l & q = p | Seg k implies dom q = Seg k
proof
  assume len p = k + l & q = p | Seg k;
  then len q = k by Th59;
  hence thesis by FINSEQ_1:def 3;
end;

theorem Th61:
  len p = k + 1 & q = p | Seg k implies p = q ^ <* p.(k + 1) *>
proof
  assume that
A1: len p = k + 1 and
A2: q = p | Seg k;
A3: len p = len q + 1 by A1,A2,Th59
    .= len q + len<* p.(k + 1) *> by FINSEQ_1:56;
  set r = <* p.(k + 1) *>;
A4: for l being Element of NAT holds l in dom q implies p.l = q.l
  by A2,FUNCT_1:70;
  now
    let l be Element of NAT;
    assume l in dom r;
    then l in {1} by FINSEQ_1:4,55;
    then
A5: l = 1 by TARSKI:def 1;
    hence p.(len q + l) = p.(k + 1) by A1,A2,Th59
      .= r.l by A5,FINSEQ_1:57;
  end;
  hence thesis by A3,A4,Th43;
end;

theorem
  p | X is FinSequence iff ex k being Element of NAT st X /\ dom p = Seg k
proof
  thus p | X is FinSequence implies
  ex k being Element of NAT st X /\ dom p = Seg k
  proof
    assume p | X is FinSequence;
    then consider k being Nat such that
A1: dom(p | X) = Seg k by FINSEQ_1:def 2;
    reconsider k as Element of NAT by ORDINAL1:def 13;
    take k;
    thus X /\ dom p = Seg k by A1,RELAT_1:90;
  end;
  given k being Element of NAT such that
A2: X /\ dom p = Seg k;
  dom(p | X) = Seg k by A2,RELAT_1:90;
  hence thesis by FINSEQ_1:def 2;
end;

theorem Th63:
  card((p ^ q) " A) = card(p " A) + card(q " A)
proof
  set X = (p ^ q) " A;
  set B = {len p + n where n is Element of NAT : n in q " A};
A1: X = p " A \/ B
  proof
    thus X c= p " A \/ B
    proof
      let x;
      assume
A2:   x in X;
      then
A3:   x in dom(p ^ q) by FUNCT_1:def 13;
      then reconsider k = x as Element of NAT;
      now per cases by A3,FINSEQ_1:38;
        suppose
A4:       k in dom p;
          then (p ^ q).k = p.k by FINSEQ_1:def 7;
          then p.k in A by A2,FUNCT_1:def 13;
          then k in p " A by A4,FUNCT_1:def 13;
          hence thesis by XBOOLE_0:def 3;
        end;
        suppose ex m being Nat st m in dom q & k = len p + m;
          then consider m being Nat such that
A5:       m in dom q and
A6:       k = len p + m;
A7:       m in NAT by ORDINAL1:def 13;
          q.m = (p ^ q).k by A5,A6,FINSEQ_1:def 7;
          then q.m in A by A2,FUNCT_1:def 13;
          then m in q " A by A5,FUNCT_1:def 13;
          then k in B by A6,A7;
          hence thesis by XBOOLE_0:def 3;
        end;
      end;
      hence thesis;
    end;
    let x;
    assume
A8: x in p " A \/ B;
    now per cases by A8,XBOOLE_0:def 3;
      suppose
A9:     x in p " A;
        then
A10:    x in dom p by FUNCT_1:def 13;
        then
A11:    x in dom(p ^ q) by Th24;
        reconsider k = x as Element of NAT by A10;
        (p ^ q).k = p.k by A10,FINSEQ_1:def 7;
        then (p ^ q).x in A by A9,FUNCT_1:def 13;
        hence thesis by A11,FUNCT_1:def 13;
      end;
      suppose x in B;
        then consider n being Element of NAT such that
A12:    x = len p + n and
A13:    n in q " A;
A14:    n in dom q by A13,FUNCT_1:def 13;
        then
A15:    x in dom(p ^ q) by A12,FINSEQ_1:41;
        (p ^ q).(len p + n) = q.n by A14,FINSEQ_1:def 7;
        then (p ^ q).x in A by A12,A13,FUNCT_1:def 13;
        hence thesis by A15,FUNCT_1:def 13;
      end;
    end;
    hence thesis;
  end;
  p " A /\ B = {}
  proof
    assume
A16: not thesis;
    consider x being Element of p " A /\ B;
    x in B by A16,XBOOLE_0:def 4;
    then consider n being Element of NAT such that
A17: x = len p + n and
A18: n in q " A;
    len p + n in p " A by A16,A17,XBOOLE_0:def 4;
    then len p + n in dom p by FUNCT_1:def 13;
    then len p + n <= len p + 0 by Th27;
    then n <= 0 & 0 <= n by XREAL_1:8;
    then n = 0 & n in dom q by A18,FUNCT_1:def 13;
    hence thesis by Th26;
  end;
  then
A19: p " A misses B by XBOOLE_0:def 7;
  reconsider B as finite set by A1,FINSET_1:13,XBOOLE_1:7;
A20: card X = card(p " A) + card B by A1,A19,CARD_2:53;
  defpred P[set,set] means ex i st $1 = i & $2 = len p + i;
A22: for x st x in q " A ex y st P[x,y]
  proof
    let x;
    assume x in q " A;
    then x in dom q by FUNCT_1:def 13;
    then reconsider i = x as Element of NAT;
    reconsider y = len p + i as set;
    take y;
    take i;
    thus thesis;
  end;
  consider f being Function such that
A23: dom f = q " A and
A24: for x st x in q " A holds P[x,f.x] from CLASSES1:sch 1(A22);
A25: rng f = B
  proof
    thus rng f c= B
    proof
      let x;
      assume x in rng f;
      then consider y such that
A26:  y in dom f and
A27:  f.y = x by FUNCT_1:def 5;
      consider i such that
A28:  y = i & f.y = len p + i by A23,A24,A26;
      i is Element of NAT by ORDINAL1:def 13;
      hence thesis by A23,A26,A27,A28;
    end;
    let x;
    assume x in B;
    then consider n being Element of NAT such that
A29: x = len p + n and
A30: n in q " A;
    ex i st n = i & f.n = len p + i by A24,A30;
    hence thesis by A23,A29,A30,FUNCT_1:def 5;
  end;
  f is one-to-one
  proof
    let x,y;
    assume that
A31: x in dom f and
A32: y in dom f and
A33: f.x = f.y;
A34: ex i st x = i & f.x = len p + i by A23,A24,A31;
    ex j st y = j & f.y = len p + j by A23,A24,A32;
    hence thesis by A33,A34;
  end;
  then q " A,B are_equipotent by A23,A25,WELLORD2:def 4;
  hence thesis by A20,CARD_1:21;
end;

theorem Th64:
  p " A c= (p ^ q) " A
proof
  let x;
  assume
A1: x in p " A;
  then
A2: x in dom p by FUNCT_1:def 13;
  then reconsider k = x as Element of NAT;
  p.k = (p ^ q).k & dom p c= dom(p ^ q) & p.k in A
  by A1,A2,FINSEQ_1:39,def 7,FUNCT_1:def 13;
  hence thesis by A2,FUNCT_1:def 13;
end;

definition
  let p,A;
  func p - A -> FinSequence equals

  p * Sgm ((dom p) \ p " A);
  coherence
  proof
    now
      assume p <> {};
      then len p <> 0;
      then reconsider D = Seg(len p) as non empty Subset of NAT;
      Seg(len p) \ p " A c= Seg(len p) by XBOOLE_1:36;
      then rng Sgm(Seg(len p) \ p " A) c= D by FINSEQ_1:def 13;
      then reconsider q = Sgm(Seg(len p) \ p " A) as FinSequence of D
      by FINSEQ_1:def 4;
      reconsider r = p * q as FinSequence by FINSEQ_2:34;
      take rr = r;
      thus rr = p * Sgm((dom p) \ p " A) by FINSEQ_1:def 3;
    end;
    hence thesis;
  end;
end;

canceled;

theorem Th66:
  len(p - A) = len p - card(p " A)
proof
  set q = Sgm(Seg(len p) \ p " A);
A1: Seg len p = dom p by FINSEQ_1:def 3;
A2: Seg(len p) \ p " A c= Seg(len p) by XBOOLE_1:36;
  then rng q c= dom p & p - A = p * q by A1,FINSEQ_1:def 13;
  then dom q = dom(p - A) & len q = card(Seg(len p) \ p " A) &
  dom q = Seg(len q) by A2,Th44,FINSEQ_1:def 3,RELAT_1:46;
  then
A3: len(p - A) = card(Seg(len p) \ p " A) by FINSEQ_1:def 3;
  p " A c= Seg(len p)
  proof
    let x;
    assume
A4: x in p " A;
    p " A c= dom p & dom p = Seg(len p) by FINSEQ_1:def 3,RELAT_1:167;
    hence thesis by A4;
  end;
  hence len(p - A) = card(Seg(len p)) - card(p " A) by A3,CARD_2:63
    .= len p - card(p " A) by FINSEQ_1:78;
end;

theorem Th67:
  len(p - A) <= len p
proof len(p - A) = len p - card(p " A) by Th66;
  hence thesis by XREAL_1:45;
end;

theorem Th68:
  len(p - A) = len p implies A misses rng p
proof
  assume
A1: len(p - A) = len p;
  assume A meets rng p;
  then p " A <> {} by RELAT_1:173;
  then not p " A,{} are_equipotent & p " A c= dom p &
  dom p = Seg(len p) & Seg(len p ) is finite
  by CARD_1:46,FINSEQ_1:def 3,RELAT_1:167;
  then 0 <> card(p " A) & 0 <= card(p " A) by CARD_1:21,47;
  then 0 < card(p " A) & len(p - A) = len p - card(p " A) by Th66;
  hence thesis by A1;
end;

theorem
  n = len p - card(p " A) implies dom(p - A) = Seg n
proof
  assume n = len p - card(p " A);
  then len(p - A) = n by Th66;
  hence thesis by FINSEQ_1:def 3;
end;

theorem
  dom(p - A) c= dom p
proof dom(p - A) = Seg(len(p - A)) & dom p = Seg(len p) & len(p - A) <= len p
  by Th67,FINSEQ_1:def 3;
  hence thesis by FINSEQ_1:7;
end;

theorem
  dom(p - A) = dom p implies A misses rng p
proof
  assume
A1: dom(p - A) = dom p;
  dom(p - A) = Seg(len(p - A)) & dom p = Seg(len p) by FINSEQ_1:def 3;
  hence thesis by A1,Th68,FINSEQ_1:8;
end;

theorem Th72:
  rng(p - A) = rng p \ A
proof
  set q = Sgm(Seg(len p) \ p " A);
A1: dom p = Seg len p by FINSEQ_1:def 3;
  thus rng(p - A) c= rng p \ A
  proof
    let x;
    assume
A2: x in rng(p - A);
    then
A3: x in rng(p * q) & rng(p * q) c= rng p by FINSEQ_1:def 3,RELAT_1:45;
    now
      assume
A4:   x in A;
      consider y such that
A5:   y in dom(p - A) and
A6:   (p - A).y = x by A2,FUNCT_1:def 5;
      set z = q.y;
A7:   y in dom q by A1,A5,FUNCT_1:21;
      then
A8:   z in rng q by FUNCT_1:def 5;
A9:   z in dom p by A1,A5,FUNCT_1:21;
A10:  (p - A).y = p.z by A1,A7,FUNCT_1:23;
      Seg(len p) \ p " A c= Seg(len p) by XBOOLE_1:36;
      then z in Seg(len p) \ p " A by A8,FINSEQ_1:def 13;
      then not z in p " A by XBOOLE_0:def 5;
      hence contradiction by A4,A6,A9,A10,FUNCT_1:def 13;
    end;
    hence thesis by A3,XBOOLE_0:def 5;
  end;
  let x;
  assume
A11: x in rng p \ A;
  then consider y such that
A12: y in dom p and
A13: p.y = x by FUNCT_1:def 5;
  not p.y in A by A11,A13,XBOOLE_0:def 5;
  then
A14: not y in p " A by FUNCT_1:def 13;
  Seg(len p) \ p " A c= Seg(len p) by XBOOLE_1:36;
  then dom p = Seg(len p) & rng q = Seg(len p) \ p " A
  by FINSEQ_1:def 3,def 13;
  then y in rng q by A12,A14,XBOOLE_0:def 5;
  then consider z such that
A15: z in dom q and
A16: q.z = y by FUNCT_1:def 5;
A17: z in dom(p - A) by A1,A12,A15,A16,FUNCT_1:21;
  (p - A).z = x by A1,A13,A15,A16,FUNCT_1:23;
  hence thesis by A17,FUNCT_1:def 5;
end;

theorem
  rng(p - A) c= rng p
proof rng(p - A) = rng p \ A by Th72;
  hence thesis;
end;

theorem
  rng(p - A) = rng p implies A misses rng p
proof
  assume rng(p - A) = rng p;
  then rng p \ A = rng p by Th72;
  hence thesis by XBOOLE_1:83;
end;

theorem Th75:
  p - A = {} iff rng p c= A
proof
  thus p - A = {} implies rng p c= A
  proof
    assume that
A1: p - A = {} and
A2: not rng p c= A;
    rng p \ A <> {} by A2,XBOOLE_1:37;
    then rng(p - A) <> {} by Th72;
    hence thesis by A1;
  end;
  assume rng p c= A;
  then rng p \ A = {} & rng(p - A) = rng p \ A by Th72,XBOOLE_1:37;
  hence thesis;
end;

theorem Th76:
  p - A = p iff A misses rng p
proof
  thus p - A = p implies A misses rng p
  proof
    assume that
A1: p - A = p and
A2: not A misses rng p;
    len(p - A) <> len p by A2,Th68;
    hence contradiction by A1;
  end;
  assume A misses rng p;
  then p " A = {} by RELAT_1:173;
  then Sgm(Seg(len p) \ p " A) = idseq(len p) & len p <= len p by Th54;
  then p * Sgm(Seg(len p) \ p " A) = p by FINSEQ_2:64;
  hence thesis by FINSEQ_1:def 3;
end;

theorem
  p - {x} = p iff not x in rng p
proof
  thus p - {x} = p implies not x in rng p
  proof
    assume p - {x} = p;
    then x in {x} & {x} misses rng p by Th76,TARSKI:def 1;
    hence thesis by XBOOLE_0:3;
  end;
  assume
A1: not x in rng p;
  {x} misses rng p
  proof
    assume {x} meets rng p;
    then consider y such that
A2: y in {x} & y in rng p by XBOOLE_0:3;
    thus thesis by A1,A2,TARSKI:def 1;
  end;
  hence thesis by Th76;
end;

theorem
  p - {} = p
proof {} misses rng p by XBOOLE_1:65;
  hence thesis by Th76;
end;

theorem
  p - rng p = {} by Th75;

Lm6: <* x *> - A = <* x *> iff not x in A
proof
  thus <* x *> - A = <* x *> implies not x in A
  proof
    assume <* x *> - A = <* x *>;
    then x in {x} & rng<* x *> = {x} & rng<* x *> misses A
    by Th76,FINSEQ_1:56,TARSKI:def 1;
    hence thesis by XBOOLE_0:3;
  end;
  assume
A1: not x in A;
  rng<* x *> misses A
  proof
    assume rng<* x *> meets A;
    then consider y such that
A2: y in rng<* x *> & y in A by XBOOLE_0:3;
    y in {x} by A2,FINSEQ_1:56;
    hence thesis by A1,A2,TARSKI:def 1;
  end;
  hence thesis by Th76;
end;

Lm7: <* x *> - A = {} iff x in A
proof
  thus <* x *> - A = {} implies x in A
  proof
    assume <* x *> - A = {};
    then rng<* x *> c= A by Th75;
    then {x} c= A by FINSEQ_1:56;
    hence thesis by ZFMISC_1:37;
  end;
  assume
A1: x in A;
  rng<* x *> = {x} by FINSEQ_1:56;
  then rng<* x *> c= A by A1,ZFMISC_1:37;
  hence thesis by Th75;
end;

Lm8: (p ^ {}) - A = (p - A) ^ ({} - A)
proof
  thus (p ^ {}) - A = p - A by FINSEQ_1:47
    .= (p - A) ^ ({} - A) by FINSEQ_1:47;
end;

Lm9: (p ^ <* x *>) - A = (p - A) ^ (<* x *> - A)
proof
  set r = (p ^ <* x *>) - A;
  set q = <* x *> - A;
  set t = p ^ <* x *>;
A1: len t = len p + len<* x *> by FINSEQ_1:35
    .= len p + 1 by FINSEQ_1:57;
A2: len r = len(p ^ <* x *>) - card(t " A) by Th66
    .= len p + len<* x *> - card(t " A) by FINSEQ_1:35
    .= (len p + len<* x *>) - (card(p " A) + card(<* x *> " A)) by Th63
    .= len p - card(p " A) + len<* x *> + (- card(<* x *> " A))
    .= len(p - A) + len<* x *> + (- card(<* x *> " A)) by Th66
    .= len(p - A) + (len<* x *> - card(<* x *> " A))
    .= len(p - A) + len q by Th66;
A3: now
    let k be Element of NAT;
    assume
A4: k in dom(p - A);
    set s1 = Sgm(Seg(len t) \ t " A);
    set s2 = Sgm(Seg(len p) \ p " A);
A5: dom t = Seg len t by FINSEQ_1:def 3;
A6: dom p = Seg len p by FINSEQ_1:def 3;
    then
A7: k in dom s2 by A4,FUNCT_1:21;
    t " A c= dom t by RELAT_1:167;
    then
A8: t " A c= Seg(len t) by FINSEQ_1:def 3;
    len s1 = card(Seg(len t) \ t " A) by Th44,XBOOLE_1:36
      .= card(Seg(len t)) - card(t " A) by A8,CARD_2:63;
    then
A9: len s1 = len t - card(t " A) by FINSEQ_1:78;
    p " A c= dom p by RELAT_1:167;
    then
A10: p " A c= Seg(len p) by FINSEQ_1:def 3;
    len s2 = card(Seg(len p) \ p " A) by Th44,XBOOLE_1:36
      .= card(Seg(len p)) - card(p " A) by A10,CARD_2:63;
    then
A11: len s2 = len p - card(p " A) by FINSEQ_1:78;
A12: dom s2 c= dom s1
    proof
      let y;
      assume
A13:  y in dom s2;
      then reconsider l = y as Element of NAT;
A14:  y in Seg(len s2) by A13,FINSEQ_1:def 3;
      then
A15:  1 <= l & l <= len s2 by FINSEQ_1:3;
      l <= len p - card(p " A) by A11,A14,FINSEQ_1:3;
      then l + card(p " A) <= len p by XREAL_1:21;
      then
A16:  l + card(p " A) + 1 <= len t by A1,XREAL_1:9;
A17:  card(t " A) = card(p " A) + card(<* x *> " A) by Th63;
      <* x *> " A c= dom<* x *> by RELAT_1:167;
      then <* x *> " A c= {1} by FINSEQ_1:4,def 8;
      then <* x *> " A = {} or <* x *> " A = {1} by ZFMISC_1:39;
      then (card(t " A) = card(p " A) or
      card(t " A) = card(p " A) + 1) & card(t " A) <= card(t " A) &
      card(p " A) <= card(p " A) + 1 by A17,CARD_1:47,50,NAT_1:12;
      then l + card(t " A) <= l + (card(p " A) + 1) by XREAL_1:9;
      then l + card(t " A) <= len t by A16,XXREAL_0:2;
      then l <= len s1 by A9,XREAL_1:21;
      then l in Seg(len s1) by A15,FINSEQ_1:3;
      hence thesis by FINSEQ_1:def 3;
    end;
A18: Seg(len p) \ p " A c= Seg(len p) by XBOOLE_1:36;
    then s2.k in
    rng s2 & rng s2 c= Seg(len p) by A7,FINSEQ_1:def 13,FUNCT_1:def 5;
    then s2.k in Seg(len p);
    then
A20: s2.k in dom p by FINSEQ_1:def 3;
    Seg(len t) \ t " A c= Seg(len t) by XBOOLE_1:36;
    then s1.k in rng s1 & rng s1 c= Seg(len t) by A7,A12,FINSEQ_1:def 13
,FUNCT_1:def 5;
    then s1.k in Seg(len t);
    then reconsider l = s1.k as Element of NAT;
A21: x in A implies t " A = p " A \/ {len p + 1}
    proof
      assume
A22:  x in A;
      thus t " A c= p " A \/ {len p + 1}
      proof
        let y;
        assume
A23:    y in t " A;
        then y in dom t by FUNCT_1:def 13;
        then y in Seg(len p + 1) by A1,FINSEQ_1:def 3;
        then
A24:    y in Seg(len p) \/ {len p + 1} by FINSEQ_1:11;
        now per cases by A24,XBOOLE_0:def 3;
          suppose
A25:        y in Seg(len p);
            then
A26:        y in dom p by FINSEQ_1:def 3;
            reconsider j = y as Element of NAT by A25;
            p.j = t.j & t.y in A by A23,A26,FINSEQ_1:def 7,FUNCT_1:def 13;
            then y in p " A by A26,FUNCT_1:def 13;
            hence thesis by XBOOLE_0:def 3;
          end;
          suppose y in {len p + 1};
            hence thesis by XBOOLE_0:def 3;
          end;
        end;
        hence thesis;
      end;
      let y;
      assume
A27:  y in p " A \/ {len p + 1};
      now per cases by A27,XBOOLE_0:def 3;
        suppose
A28:      y in p " A;
          p " A c= t " A by Th64;
          hence thesis by A28;
        end;
        suppose y in {len p + 1};
          then y = len p + 1 by TARSKI:def 1;
          then y in Seg(len t) & t.y in A by A1,A22,FINSEQ_1:6,59;
          then y in dom t & t.y in A by FINSEQ_1:def 3;
          hence thesis by FUNCT_1:def 13;
        end;
      end;
      hence thesis;
    end;
A29: not x in A implies t " A = p " A
    proof
      assume
A30:  not x in A;
      thus t " A c= p " A
      proof
        let y;
        assume
A31:    y in t " A;
        then
A32:    y in dom t by FUNCT_1:def 13;
        then reconsider l = y as Element of NAT;
        y in Seg(len t) by A32,FINSEQ_1:def 3;
        then
A33:    1 <= l & l <= len p + 1 by A1,FINSEQ_1:3;
        now
          assume l = len p + 1;
          then t.l = x & l in t " A by A31,FINSEQ_1:59;
          hence contradiction by A30,FUNCT_1:def 13;
        end;
        then l < len p + 1 by A33,XXREAL_0:1;
        then l <= len p by NAT_1:13;
        then l in Seg(len p) by A33,FINSEQ_1:3;
        then y in dom p & t.l in A by A31,FINSEQ_1:def 3,FUNCT_1:def 13;
        then y in dom p & p.l in A by FINSEQ_1:def 7;
        hence thesis by FUNCT_1:def 13;
      end;
      thus thesis by Th64;
    end;
A34: now
      assume
A35:  t " A = p " A;
      {len p + 1} /\ p " A = {}
      proof
        assume
A36:    not thesis;
        consider z being Element of {len p + 1} /\ p " A;
        z in {len p + 1} by A36,XBOOLE_0:def 4;
        then
A37:    z = len p + 1 by TARSKI:def 1;
        p " A c= dom p & dom p = Seg(len p) & z in p " A
        by A36,FINSEQ_1:def 3,RELAT_1:167,XBOOLE_0:def 4;
        then len p + 1 <= len p & len p < len p + 1
        by A37,FINSEQ_1:3,XREAL_1:31;
        hence thesis;
      end;
      then
A38:  {len p + 1} misses p " A by XBOOLE_0:def 7;
      thus Seg(len t) \ t " A = (Seg(len p) \/
      {len p + 1}) \ p " A by A1,A35,FINSEQ_1:11
        .= (Seg(len p) \ p " A) \/ ({len p + 1} \ p " A) by XBOOLE_1:42
        .= (Seg(len p) \ p " A) \/ {len p + 1} by A38,XBOOLE_1:83;
    end;
    Seg(len p) /\ {len p + 1} = {}
    proof
      assume
A39:  not thesis;
      consider z being Element of Seg(len p) /\ {len p + 1};
A40:  z in Seg(len p) by A39,XBOOLE_0:def 4;
      then reconsider f = z as Element of NAT;
      f in {len p + 1} by A39,XBOOLE_0:def 4;
      then f = len p + 1 & f <= len p by A40,FINSEQ_1:3,TARSKI:def 1;
      hence thesis by XREAL_1:31;
    end;
    then
A41: Seg(len p) misses {len p + 1} by XBOOLE_0:def 7;
A42: now
      assume t " A = p " A \/ {len p + 1};
      hence Seg(len t) \ t " A =
      (Seg(len p) \/ {len p + 1}) \ (p " A \/ {len p + 1}) by A1,FINSEQ_1:11
        .= (Seg(len p) \ (p " A \/ {len p + 1}) \/
      ({len p + 1} \ (p " A \/ {len p + 1}))) by XBOOLE_1:42
        .= (Seg(len p) \ (p " A \/ {len p + 1})) \/ {} by XBOOLE_1:46
        .= (Seg(len p) \ p " A) /\ (Seg(len p) \ {len p + 1}) by XBOOLE_1:53
        .= (Seg(len p) \ p " A) /\ Seg(len p) by A41,XBOOLE_1:83
        .= (Seg(len p) \ p " A) by XBOOLE_1:28,36;
    end;
    len p + 1 in Seg(len p + 1) by FINSEQ_1:6;
    then
A43: {len p + 1} c= Seg(len p + 1) by ZFMISC_1:37;
A44: now per cases by A21,A29,A34,A42;
      suppose Seg(len p) \ p " A = Seg(len t) \ t " A;
        hence s1.k = s2.k;
      end;
      suppose
A45:    Seg(len t) \ t " A = (Seg(len p) \ p " A) \/ {len p + 1};
        now
          let m,n be Element of NAT;
          assume that
A46:      m in Seg(len p) \ p " A and
A47:      n in {len p + 1};
          m in Seg(len p) by A46,XBOOLE_0:def 5;
          then m <= len p & n = len p + 1 & len p < len p + 1
          by A47,FINSEQ_1:3,TARSKI:def 1,XREAL_1:31;
          hence m < n by XXREAL_0:2;
        end;
        then s1 = s2 ^ Sgm{len p + 1} by A18,A43,A45,Th48;
        hence s1.k = s2.k by A7,FINSEQ_1:def 7;
      end;
    end;
A48: r.k = t.l by A5,A7,A12,FUNCT_1:23;
    (p - A).k = p.(s2.k) by A6,A7,FUNCT_1:23;
    hence r.k = (p - A).k by A20,A44,A48,FINSEQ_1:def 7;
  end;
  now
    let k be Element of NAT;
    assume
A49: k in dom q;
A50: not x in A by Lm7,A49,RELAT_1:60;
    then
A51: q = <* x *> by Lm6;
    then
A52: dom q = {1} by FINSEQ_1:4,def 8;
    then
A53: k = 1 by A49,TARSKI:def 1;
A54: p " A = (p ^ <* x *>) " A
    proof
      thus p " A c= (p ^ <* x *>) " A
      proof
        let y;
        assume y in p " A;
        then
A55:    y in dom p & p.y in A by FUNCT_1:def 13;
        then reconsider z = y as Element of NAT;
        y in dom t & t.z in A by A55,Th24,FINSEQ_1:def 7;
        hence thesis by FUNCT_1:def 13;
      end;
      let y;
      assume y in (p ^ <* x *>) " A;
      then
A56:  y in dom t & t.y in A by FUNCT_1:def 13;
      then reconsider z = y as Element of NAT;
A57:  z in dom p or ex n being Nat st n in dom q & z = len p + n
      by A51,A56,FINSEQ_1:38;
A58:  now
        given n such that
A59:    n in dom q and
A60:    z = len p + n;
        n = 1 by A52,A59,TARSKI:def 1;
        hence contradiction by A50,A56,A60,FINSEQ_1:59;
      end;
      then t.z = p.z by A57,FINSEQ_1:def 7;
      hence thesis by A56,A57,A58,FUNCT_1:def 13;
    end;
A61: len(p - A) + k = len p - card(p " A) + k by Th66
      .= len p + 1 - card((p ^ <* x *>) " A) by A53,A54
      .= len p + len<* x *> - card((p ^ <* x *>) " A) by FINSEQ_1:56
      .= len(p ^ <* x *>) - card((p ^ <* x *>) " A) by FINSEQ_1:35
      .= len r by Th66;
    set s = Sgm(Seg(len(p ^ <* x *>)) \ (p ^ <* x *>) " A);
    set S = Seg(len(p ^ <* x *>)) \ (p ^ <* x *>) " A;
    t " A c= dom t by RELAT_1:167;
    then
A62: t " A c= Seg(len t) & Seg(len t) is finite by FINSEQ_1:def 3;
A63: S c= Seg(len t) by XBOOLE_1:36;
A64: len s = card S by Th44,XBOOLE_1:36
      .= card(Seg(len t)) - card(t " A) by A62,CARD_2:63
      .= len t - card(p " A) by A54,FINSEQ_1:78
      .= len p - card(p " A) + k by A1,A53
      .= len r by A61,Th66;
    then
A65: len s = len(p - A) + 1 & dom s = Seg(len s) by A49,A52,A61,FINSEQ_1:def 3
,TARSKI:def 1;
    then
A66: len r in dom s by A64,FINSEQ_1:6;
A67: dom t = Seg len t by FINSEQ_1:def 3;
A68: len t in Seg(len t) by A1,FINSEQ_1:6;
    now
      assume
A69:  len t in t " A;
      t.(len t) = x by A1,FINSEQ_1:59;
      hence contradiction by A50,A69,FUNCT_1:def 13;
    end;
    then len t in S by A68,XBOOLE_0:def 5;
    then len t in rng s by A63,FINSEQ_1:def 13;
    then consider y such that
A70: y in dom s and
A71: s.y = len t by FUNCT_1:def 5;
    reconsider y as Element of NAT by A70;
    now
      assume
A72:  y <> len s;
A73:  y in Seg(len s) by A70,FINSEQ_1:def 3;
      then y <= len s by FINSEQ_1:3;
      then
A74:  1 <= y & y < len s & len s <= len s by A72,A73,FINSEQ_1:3,XXREAL_0:1;
A75:  s.(len s) in rng s & rng s c= NAT by A64,A66,FINSEQ_1:def 4,FUNCT_1:def 5
;
      then reconsider w = s.(len s) as Element of NAT;
A76:  len t < w by A63,A71,A74,FINSEQ_1:def 13;
      w in S by A63,A75,FINSEQ_1:def 13;
      then w in Seg(len t) by XBOOLE_0:def 5;
      hence contradiction by A76,FINSEQ_1:3;
    end;
    hence r.(len(p - A) + k) = t.(len t)
    by A61,A64,A65,A67,A71,FINSEQ_1:6,FUNCT_1:23
      .= x by A1,FINSEQ_1:59
      .= q.k by A51,A53,FINSEQ_1:def 8;
  end;
  hence thesis by A2,A3,Th43;
end;

Lm10: now
  let q,x;
  assume
A1: for p,A holds (p ^ q) - A = (p - A) ^ (q - A);
  let p,A;
  thus (p ^ (q ^ <* x *>)) - A = (p ^ q ^ <* x *>) - A by FINSEQ_1:45
    .= ((p ^ q) - A) ^ (<* x *> - A) by Lm9
    .= (p - A) ^ (q - A) ^ (<* x *> - A) by A1
    .= (p - A) ^ ((q - A) ^ (<* x *> - A)) by FINSEQ_1:45
    .= (p - A) ^ ((q ^ <* x *>) - A) by Lm9;
end;

Lm11: for q,p,A holds (p ^ q) - A = (p - A) ^ (q - A)
proof
  defpred P[FinSequence] means (p ^ $1) - A = (p - A) ^ ($1 - A);
A1: P[{}] by Lm8;
A2: for q,x st P[q] holds P[q^<*x*>] by Lm10;
  for q holds P[q] from FINSEQ_1:sch 3(A1,A2);
  hence thesis;
end;

theorem
  (p ^ q) - A = (p - A) ^ (q - A) by Lm11;

theorem
  {} - A = {};

theorem
  <* x *> - A = <* x *> iff not x in A by Lm6;

theorem
  <* x *> - A = {} iff x in A by Lm7;

theorem Th84:
  <* x,y *> - A = {} iff x in A & y in A
proof
  thus <* x,y *> - A = {} implies x in A & y in A
  proof
    assume <* x,y *> - A = {};
    then rng<* x,y *> c= A by Th75;
    then {x,y} c= A by FINSEQ_2:147;
    hence thesis by ZFMISC_1:38;
  end;
  assume x in A & y in A;
  then <* x *> - A = {} & <* y *> - A = {} & <* x,y *> = <* x *> ^ <* y *>
  by Lm7,FINSEQ_1:def 9;
  hence <* x,y *> - A = {} ^ {} by Lm11
    .= {} by FINSEQ_1:47;
end;

theorem Th85:
  x in A & not y in A implies <* x,y *> - A = <* y *>
proof
  assume x in A & not y in A;
  then <* x *> - A = {} & <* y *> - A = <* y *> &
  <* x,y *> = <* x *> ^ <* y *> by Lm6,Lm7,FINSEQ_1:def 9;
  hence <* x,y *> - A = {} ^ <* y *> by Lm11
    .= <* y *> by FINSEQ_1:47;
end;

theorem
  <* x,y *> - A = <* y *> & x <> y implies x in A & not y in A
proof
  assume that
A1: <* x,y *> - A = <* y *> and
A2: x <> y;
A3: <* y *> = (<* x *> ^ <* y *>) - A by A1,FINSEQ_1:def 9
    .= (<* x *> - A) ^ (<* y *> - A) by Lm11;
A4: (x in A implies <* x *> - A = {}) &
  (not x in A implies <* x *> - A = <* x *>) &
  (y in A implies <* y *> - A = {}) &
  (not y in A implies <* y *> - A = <* y *>) by Lm6,Lm7;
  assume not thesis;
  then <* y *> = {} or <* x *> = <* y *> or <* y *> = <* x,y *>
  by A3,A4,FINSEQ_1:47,def 9;
  then <* x *> = <* y *> & <* x *>.1 = x & <* y *>.1 = y by Th40,FINSEQ_1:57;
  hence thesis by A2;
end;

theorem Th87:
  not x in A & y in A implies <* x,y *> - A = <* x *>
proof
  assume not x in A & y in A;
  then <* x *> - A = <* x *> & <* y *> - A = {} &
  <* x,y *> = <* x *> ^ <* y *> by Lm6,Lm7,FINSEQ_1:def 9;
  hence <* x,y *> - A = <* x *> ^ {} by Lm11
    .= <* x *> by FINSEQ_1:47;
end;

theorem
  <* x,y *> - A = <* x *> & x <> y implies not x in A & y in A
proof
  assume that
A1: <* x,y *> - A = <* x *> and
A2: x <> y;
A3: <* x *> = (<* x *> ^ <* y *>) - A by A1,FINSEQ_1:def 9
    .= (<* x *> - A) ^ (<* y *> - A) by Lm11;
A4: (x in A implies <* x *> - A = {}) &
  (not x in A implies <* x *> - A = <* x *>) &
  (y in A implies <* y *> - A = {}) &
  (not y in A implies <* y *> - A = <* y *>) by Lm6,Lm7;
  assume not thesis;
  then <* x *> = {} or <* x *> = <* y *> or <* x *> = <* x,y *>
  by A3,A4,FINSEQ_1:47,def 9;
  then <* x *> = <* y *> & <* x *>.1 = x & <* y *>.1 = y by Th40,FINSEQ_1:57;
  hence thesis by A2;
end;

theorem
  <* x,y *> - A = <* x,y *> iff not x in A & not y in A
proof
  thus <* x,y *> - A = <* x,y *> implies not x in A & not y in A
  proof
    assume
A1: <* x,y *> - A = <* x,y *>;
    assume not thesis;
    then x in A & y in A or not x in A & y in A or x in A & not y in A;
    then <* x,y *> - A = {} or <* x,y *> - A = <* x *> or
    <* x,y *> - A = <* y *> by Th84,Th85,Th87;
    hence thesis by A1,Th38,Th40;
  end;
  assume not x in A & not y in A;
  then <* x *> - A = <* x *> & <* y *> - A = <* y *> &
  <* x,y *> = <* x *> ^ <* y *> by Lm6,FINSEQ_1:def 9;
  hence <* x,y *> - A = <* x,y *> by Lm11;
end;

theorem Th90:
  len p = k + 1 & q = p | Seg k implies (p.(k + 1) in A iff p - A = q - A)
proof
  assume that
A1: len p = k + 1 and
A2: q = p | Seg k;
  thus p.(k + 1) in A implies p - A = q - A
  proof
    assume
A3: p.(k + 1) in A;
    thus p - A = (q ^ <* p.(k + 1) *>) - A by A1,A2,Th61
      .= (q - A) ^ (<* p.(k + 1) *> - A) by Lm11
      .= (q - A) ^ {} by A3,Lm7
      .= q - A by FINSEQ_1:47;
  end;
  assume that
A4: p - A = q - A and
A5: not p.(k + 1) in A;
  q - A = (q ^ <* p.(k + 1) *>) - A by A1,A2,A4,Th61
    .= (q - A) ^ (<* p.(k + 1) *> - A) by Lm11
    .= (q - A) ^ <* p.(k + 1) *> by A5,Lm6;
  hence thesis by TREES_1:5;
end;

theorem Th91:
  len p = k + 1 & q = p | Seg k implies
  (not p.(k + 1) in A iff p - A = (q - A) ^ <* p.(k + 1) *>)
proof
  assume
A1: len p = k + 1 & q = p | Seg k;
  thus not p.(k + 1) in A implies p - A = (q - A) ^ <* p.(k + 1) *>
  proof
    assume
A2: not p.(k + 1) in A;
    thus p - A = (q ^ <* p.(k + 1) *>) - A by A1,Th61
      .= (q - A) ^ (<* p.(k + 1) *> - A) by Lm11
      .= (q - A) ^ <* p.(k + 1) *> by A2,Lm6;
  end;
  assume
A3: p - A = (q - A) ^ <* p.(k + 1) *>;
  assume p.(k + 1) in A;
  then q - A = (q - A) ^ <* p.(k + 1) *> by A1,A3,Th90;
  hence contradiction by TREES_1:5;
end;


Lm13: for l st (for p,A st len p = l for n st n in dom p
for B being finite set st
B = {k where k is Element of NAT : k in dom p & k <= n & p.k in A}
holds p.n in A or (p - A).(n - card B) = p.n) holds (for p,A st len p = l + 1
for n st n in dom p for C being finite set st
C = {m where m is Element of NAT : m in dom p & m <= n & p.m in A}
holds p.n in A or (p - A).(n - card C) = p.n)
proof
  let l;
  assume
A1: for p,A st len p = l for n st n in dom p for B being finite set st
  B = {k where k is Element of NAT : k in dom p & k <= n & p.k in A}
  holds p.n in A or (p - A).(n - card B) = p.n;
  let p,A;
  assume
A2: len p = l + 1;
  let n;
  assume
A3: n in dom p;
  let C be finite set such that
A4: C = {m where m is Element of NAT: m in dom p & m <= n & p.m in A};
  assume
A5: not p.n in A;
  reconsider q = p | Seg l as FinSequence by FINSEQ_1:19;
A6: len q = l by A2,Th59;
  set B = {k where k is Element of NAT: k in dom q & k <= n & q.k in A};
  B c= dom q
  proof
    let x be set;
    assume x in B;
    then ex k being Element of NAT st x = k & k in dom q & k <= n & q.k in A;
    hence thesis;
  end;
  then reconsider B as finite set;
  now per cases;
    suppose
A7:   p.(l + 1) in A;
      then not n in {l + 1} & n in Seg(l + 1) by A2,A3,A5,FINSEQ_1:def 3
,TARSKI:def 1;
      then n in Seg(l + 1) \ {l + 1} by XBOOLE_0:def 5;
      then
A8:   n in Seg l by FINSEQ_1:12;
      then
A9:   n in dom q by A6,FINSEQ_1:def 3;
      then
A10:  p.n = q.n by FUNCT_1:70;
A11:  q - A = p - A by A2,A7,Th90;
      B = C
      proof
        thus B c= C
        proof
          let x;
          assume x in B;
          then consider k being Element of NAT such that
A12:      x = k and
A13:      k in dom q and
A14:      k <= n and
A15:      q.k in A;
A16:      p.k in A by A13,A15,FUNCT_1:70;
          dom q c= dom p by RELAT_1:89;
          hence thesis by A4,A12,A13,A14,A16;
        end;
        let x;
        assume x in C;
        then consider m being Element of NAT such that
A17:    x = m and
A18:    m in dom p and
A19:    m <= n and
A20:    p.m in A by A4;
        m in Seg(len p) by A18,FINSEQ_1:def 3;
        then 1 <= m & n <= l by A8,FINSEQ_1:3;
        then 1 <= m & m <= l by A19,XXREAL_0:2;
        then m in Seg l by FINSEQ_1:3;
        then
A21:    m in dom q by A6,FINSEQ_1:def 3;
        then q.m in A by A20,FUNCT_1:70;
        hence thesis by A17,A19,A21;
      end;
      hence thesis by A1,A5,A6,A9,A10,A11;
    end;
    suppose not p.(l + 1) in A;
      then
A22:  p - A = (q - A) ^ <* p.(l + 1) *> by A2,Th91;
      now per cases;
        suppose
A23:      n = l + 1;
A24:      len<* p.(l + 1) *> = 1 by FINSEQ_1:56;
          p " A = C
          proof
            thus p " A c= C
            proof
              let x;
              assume x in p " A;
              then
A25:          x in dom p & p.x in A by FUNCT_1:def 13;
              then reconsider z = x as Element of NAT;
              z in Seg n by A2,A23,A25,FINSEQ_1:def 3;
              then z <= n by FINSEQ_1:3;
              hence thesis by A4,A25;
            end;
            let x;
            assume x in C;
            then ex m being Element of NAT st x = m & m in dom p &
            m <= n & p.m in A by A4;
            hence thesis by FUNCT_1:def 13;
          end;
          then len(p - A) = n - card C by A2,A23,Th66;
          then (p - A).(n - card C) = (p - A).(len(q - A) + 1) by A22,A24,
FINSEQ_1:35
            .= p.(l + 1) by A22,FINSEQ_1:59;
          hence thesis by A23;
        end;
        suppose n <> l + 1;
          then not n in {l + 1} & n in Seg(l + 1) by A2,A3,FINSEQ_1:def 3
,TARSKI:def 1;
          then n in Seg(l + 1) \ {l + 1} by XBOOLE_0:def 5;
          then
A26:      n in Seg l by FINSEQ_1:12;
          then
A27:      n in dom q by A6,FINSEQ_1:def 3;
          then
A28:      p.n = q.n by FUNCT_1:70;
A29:      not q.n in A by A5,A27,FUNCT_1:70;
A30:      (q - A).(n - card B) = p.n by A1,A5,A6,A27,A28;
A31:      B = C
          proof
            thus B c= C
            proof
              let x;
              assume x in B;
              then consider k being Element of NAT such that
A32:          x = k and
A33:          k in dom q and
A34:          k <= n and
A35:          q.k in A;
A36:          p.k in A by A33,A35,FUNCT_1:70;
              dom q c= dom p by RELAT_1:89;
              hence thesis by A4,A32,A33,A34,A36;
            end;
            let x;
            assume x in C;
            then consider m being Element of NAT such that
A37:        x = m and
A38:        m in dom p and
A39:        m <= n and
A40:        p.m in A by A4;
            m in Seg(len p) by A38,FINSEQ_1:def 3;
            then 1 <= m & n <= l by A26,FINSEQ_1:3;
            then 1 <= m & m <= l by A39,XXREAL_0:2;
            then m in Seg l by FINSEQ_1:3;
            then
A41:        m in dom q by A6,FINSEQ_1:def 3;
            then q.m in A by A40,FUNCT_1:70;
            hence thesis by A37,A39,A41;
          end;
          set a = n - card B;
          set b = card B;
A42:      B c= (Seg n) \ {n}
          proof
            let x;
            assume x in B;
            then consider k being Element of NAT such that
A43:        x = k and
A44:        k in dom q and
A45:        k <= n and
A46:        q.k in A;
            k in Seg(len q) by A44,FINSEQ_1:def 3;
            then 1 <= k by FINSEQ_1:3;
            then
A47:        k in Seg n by A45,FINSEQ_1:3;
            not k in {n} by A29,A46,TARSKI:def 1;
            hence thesis by A43,A47,XBOOLE_0:def 5;
          end;
          1 <= n & n <= n by A26,FINSEQ_1:3;
          then n in Seg n by FINSEQ_1:3;
          then
A48:      {n} c= Seg n by ZFMISC_1:37;
          card B <= card((Seg n) \ {n}) by A42,NAT_1:44;
          then card B <= card(Seg n) - card{n} by A48,CARD_2:63;
          then b <= card(Seg n) - 1 by CARD_1:50;
          then b <= n - 1 by FINSEQ_1:78;
          then b + 1 <= n by XREAL_1:21;
          then
A49:      0 + b < a + b by NAT_1:13;
          then 0 <= a by XREAL_1:8;
          then reconsider a as Element of NAT by INT_1:16;
A50:      1 <= a by A49,NAT_1:14;
          q " A c= dom q by RELAT_1:167;
          then
A51:      q " A c= Seg l by A6,FINSEQ_1:def 3;
          q " A \/ (Seg l \ q " A) = q " A \/ Seg l by XBOOLE_1:39
            .= Seg l by A51,XBOOLE_1:12;
          then card(Seg l) = card(q " A) + card(Seg l \ q " A)
          by CARD_2:53,XBOOLE_1:79;
          then
A52:      len q = card(q " A) + card(Seg(len q) \ q " A) by A6,FINSEQ_1:78;
          Seg n \ {n} c= Seg n by XBOOLE_1:36;
          then B c= Seg n & card(Seg n) = n by A42,FINSEQ_1:78,XBOOLE_1:1;
          then
A53:      a = card(Seg n \ B) by CARD_2:63;
          Seg n \ B c= Seg l \ q " A
          proof
            let x;
            assume
A54:        x in Seg n \ B;
            then
A55:        x in Seg n by XBOOLE_0:def 5;
            reconsider z = x as Element of NAT by A54;
A56:        z <= n & n <= l by A26,A55,FINSEQ_1:3;
            then 1 <= z & z <= l by A55,FINSEQ_1:3,XXREAL_0:2;
            then
A57:        z in Seg l by FINSEQ_1:3;
            now
              assume z in q " A;
              then q.z in A & z in dom q by FUNCT_1:def 13;
              then z in B & not z in B by A54,A56,XBOOLE_0:def 5;
              hence contradiction;
            end;
            hence thesis by A57,XBOOLE_0:def 5;
          end;
          then a <= len q - card(q " A) by A6,A52,A53,NAT_1:44;
          then a <= len(q - A) by Th66;
          then a in Seg(len(q - A)) by A50,FINSEQ_1:3;
          then a in dom(q - A) by FINSEQ_1:def 3;
          hence thesis by A22,A30,A31,FINSEQ_1:def 7;
        end;
      end;
      hence thesis;
    end;
  end;
  hence thesis;
end;

Lm14: for l for p,A st len p = l for n st n in dom p holds
for B being finite set st
B = {k where k is Element of NAT: k in dom p & k <= n & p.k in A}
holds p.n in A or (p - A).(n - card B) = p.n
proof
  defpred P[Element of NAT] means for p,A st len p = $1
  for n st n in dom p holds for B being finite set st
  B = {k where k is Element of NAT : k in dom p & k <= n & p.k in A}
  holds p.n in A or (p - A).(n - card B) = p.n;
A1: P[0]
 proof
  let p,A;
  assume len p = 0;
   then p = {};
  hence thesis;
 end;
A2: for k being Element of NAT st P[k] holds P[k+1] by Lm13;
  for n being Element of NAT holds P[n] from NAT_1:sch 1(A1,A2);
  hence thesis;
end;

theorem
  n in dom p implies for B being finite set st
  B = {k where k is Element of NAT : k in dom p & k <= n & p.k in A}
  holds p.n in A or (p - A).(n - card B) = p.n
proof len p = len p;
  hence thesis by Lm14;
end;

theorem
  p is FinSequence of D implies p - A is FinSequence of D
proof
  assume p is FinSequence of D;
  then rng p c= D & rng(p - A) = rng p \ A & rng p \ A c= rng p by Th72,
FINSEQ_1:def 4;
  then rng(p - A) c= D by XBOOLE_1:1;
  hence thesis by FINSEQ_1:def 4;
end;

theorem Th94:
  p is one-to-one implies p - A is one-to-one
proof
  assume
A1: p is one-to-one;
  Seg(len p) \ p " A c= Seg(len p) by XBOOLE_1:36;
  then p - A = p * Sgm(Seg(len p) \ p " A) &
  Sgm(Seg(len p) \ p " A) is one-to-one by Lm1,FINSEQ_1:def 3;
  hence thesis by A1;
end;

theorem Th95:
  p is one-to-one implies len(p - A) = len p - card(A /\ rng p)
proof
  assume
A1: p is one-to-one;
A2: dom p = Seg(len p) & p " A c= dom p & Seg(len p) is finite
  by FINSEQ_1:def 3,RELAT_1:167;
  p " A,A /\ rng p are_equipotent
  proof
    deffunc F(set) = p.$1;
    consider f being Function such that
A3: dom f = p " A and
A4: for x st x in p " A holds f.x = F(x) from FUNCT_1:sch 3;
    take f;
    thus f is one-to-one
    proof
      let x,y;
      assume that
A5:   x in dom f & y in dom f and
A6:   f.x = f.y;
      x in dom p & y in dom p & p.x = f.x & f.y = p.y by A2,A3,A4,A5;
      hence thesis by A1,A6,FUNCT_1:def 8;
    end;
    thus dom f = p " A by A3;
    thus rng f c= A /\ rng p
    proof
      let x;
      assume x in rng f;
      then consider y such that
A7:   y in dom f and
A8:   f.y = x by FUNCT_1:def 5;
A9:   p.y in A by A3,A7,FUNCT_1:def 13;
      y in dom p by A3,A7,FUNCT_1:def 13;
      then p.y in rng p & p.y = f.y by A3,A4,A7,FUNCT_1:def 5;
      hence thesis by A8,A9,XBOOLE_0:def 4;
    end;
    let x;
    assume
A10: x in A /\ rng p;
    then x in rng p by XBOOLE_0:def 4;
    then consider y such that
A11: y in dom p and
A12: p.y = x by FUNCT_1:def 5;
    p.y in A by A10,A12,XBOOLE_0:def 4;
    then
A13: y in p " A by A11,FUNCT_1:def 13;
    then f.y = x by A4,A12;
    hence thesis by A3,A13,FUNCT_1:def 5;
  end;
  then card(p " A) = card(A /\ rng p) by CARD_1:21;
  hence thesis by Th66;
end;

theorem Th96:
  for A being finite set st p is one-to-one & A c= rng p
  holds len(p - A) = len p - card A
proof
  let A be finite set;
  assume that
A1: p is one-to-one and
A2: A c= rng p;
  A /\ rng p = A by A2,XBOOLE_1:28;
  hence thesis by A1,Th95;
end;

theorem
  p is one-to-one & x in rng p implies len(p - {x}) = len p - 1
proof
  assume that
A1: p is one-to-one and
A2: x in rng p;
  {x} c= rng p by A2,ZFMISC_1:37;
  then len(p - {x}) = len p - card{x} by A1,Th96;
  hence thesis by CARD_1:50;
end;

theorem Th98:
  rng p misses rng q & p is one-to-one & q is one-to-one iff
  p ^ q is one-to-one
proof
  thus rng p misses rng q & p is one-to-one & q is one-to-one implies
  p ^ q is one-to-one
  proof
    assume that
A1: rng p misses rng q and
A2: p is one-to-one and
A3: q is one-to-one;
    let x,y;
    assume that
A4: x in dom(p ^ q) and
A5: y in dom(p ^ q) and
A6: (p ^ q).x = (p ^ q).y;
    reconsider k1 = x, k2 = y as Element of NAT by A4,A5;
    now per cases by A4,A5,FINSEQ_1:38;
      suppose
A7:     k1 in dom p & k2 in dom p;
        then (p ^ q).k1 = p.k1 & (p ^ q).k2 = p.k2 by FINSEQ_1:def 7;
        hence thesis by A2,A6,A7,FUNCT_1:def 8;
      end;
      suppose
A8:     k1 in dom p & ex n being Nat st n in dom q & k2 = len p + n;
        then consider n being Nat such that
A9:     n in dom q and
A10:    k2 = len p + n;
        (p ^ q).k1 = p.k1 & (p ^ q).k2 = q.n by A8,A10,FINSEQ_1:def 7;
        then q.n in rng p & q.n in rng q by A6,A8,A9,FUNCT_1:def 5;
        hence thesis by A1,XBOOLE_0:3;
      end;
      suppose
A11:    k2 in dom p & ex n being Nat st n in dom q & k1 = len p + n;
        then consider n being Nat such that
A12:    n in dom q and
A13:    k1 = len p + n;
        (p ^ q).k2 = p.k2 & (p ^ q).k1 = q.n by A11,A13,FINSEQ_1:def 7;
        then q.n in rng p & q.n in rng q by A6,A11,A12,FUNCT_1:def 5;
        hence thesis by A1,XBOOLE_0:3;
      end;
      suppose
A14:    (ex n being Nat st n in dom q & k1 = len p + n) &
        ex n being Nat st n in dom q & k2 = len p + n;
        then consider n1 being Nat such that
A15:    n1 in dom q and
A16:    k1 = len p + n1;
        consider n2 being Nat such that
A17:    n2 in dom q and
A18:    k2 = len p + n2 by A14;
        (p ^ q).k1 = q.n1 & (p ^ q).k2 = q.n2 by A15,A16,A17,A18,FINSEQ_1:def 7
;
        hence thesis by A3,A6,A15,A16,A17,A18,FUNCT_1:def 8;
      end;
    end;
    hence thesis;
  end;
  assume
A19: p ^ q is one-to-one;
  thus rng p misses rng q
  proof
    assume not rng p misses rng q;
    then consider x such that
A20: x in rng p and
A21: x in rng q by XBOOLE_0:3;
    consider y1 such that
A22: y1 in dom p and
A23: p.y1 = x by A20,FUNCT_1:def 5;
    consider y2 such that
A24: y2 in dom q and
A25: q.y2 = x by A21,FUNCT_1:def 5;
A26: y1 in Seg(len p) & y2 in Seg(len q) by A22,A24,FINSEQ_1:def 3;
    reconsider y1,y2 as Element of NAT by A22,A24;
A27: len p + y2 in dom(p ^ q) by A24,FINSEQ_1:41;
    (p ^ q).y1 = p.y1 & (p ^ q).(len p + y2) = q.y2 & y1 in dom(p ^ q )
    by A22,A24,Th24,FINSEQ_1:def 7;
    then
A28: y1 = len p + y2 by A19,A23,A25,A27,FUNCT_1:def 8;
    y1 <= len p & len p <= len p + y2 by A26,FINSEQ_1:3,NAT_1:12;
    then y1 = len p & y1 = y1 + 0 by A28,XXREAL_0:1;
    hence thesis by A26,A28,FINSEQ_1:3;
  end;
  thus p is one-to-one
  proof
    let x,y;
    assume that
A29: x in dom p & y in dom p and
A30: p.x = p.y;
    reconsider k = x, l = y as Element of NAT by A29;
    (p ^ q).k = p.k & (p ^ q).l = p.l & k in dom(p ^ q) & l in dom(p ^q)
    by A29,Th24,FINSEQ_1:def 7;
    hence thesis by A19,A30,FUNCT_1:def 8;
  end;
  let x,y;
  assume that
A31: x in dom q and
A32: y in dom q and
A33: q.x = q.y;
  consider k being Nat such that
A34: x = k and
A35: len p + k in dom(p ^ q) by A31,FINSEQ_1:40;
  consider l being Nat such that
A36: y = l and
A37: len p + l in dom(p ^ q) by A32,FINSEQ_1:40;
  (p ^ q).(len p + k) = q.k & (p ^ q).(len p + l) = q.l by A31,A32,A34,A36,
FINSEQ_1:def 7;
  then len p + k = len p + l by A19,A33,A34,A35,A36,A37,FUNCT_1:def 8;
  hence thesis by A34,A36;
end;

theorem
  A c= Seg k implies Sgm A is one-to-one by Lm1;

canceled 2;

theorem Th102:
  <* x *> is one-to-one
proof
  let y1,y2;
  assume y1 in dom<* x *> & y2 in dom<* x *> & <* x *>.y1 = <* x *>.y2;
  then y1 in {1} & y2 in {1} by FINSEQ_1:4,def 8;
  then y1 = 1 & y2 = 1 by TARSKI:def 1;
  hence thesis;
end;

theorem Th103:
  x <> y iff <* x,y *> is one-to-one
proof
  thus x <> y implies <* x,y *> is one-to-one
  proof
    assume
A1: x <> y;
    let y1,y2;
    assume that
A2: y1 in dom<* x,y *> & y2 in dom<* x,y *> and
A3: <* x,y *>.y1 = <* x,y *>.y2;
A4: y1 in {1,2} & y2 in {1,2} by A2,Th29,FINSEQ_1:4;
    now per cases by A4,TARSKI:def 2;
      suppose y1 = 1 & y2 = 1 or y1 = 2 & y2 = 2;
        hence thesis;
      end;
      suppose y1 = 1 & y2 = 2;
        then <* x,y *>.y1 = x & <* x,y *>.y2 = y by FINSEQ_1:61;
        hence thesis by A1,A3;
      end;
      suppose y1 = 2 & y2 = 1;
        then <* x,y *>.y1 = y & <* x,y *>.y2 = x by FINSEQ_1:61;
        hence thesis by A1,A3;
      end;
    end;
    hence thesis;
  end;
  assume
A5: <* x,y *> is one-to-one & x = y;
  1 in {1,2} & 2 in {1,2} by TARSKI:def 2;
  then 1 in dom<* x,y *> & 2 in dom<* x,y *> & 1 <> 2 & <* x,y *>.1 = x &
  <* x,y *>.2 = y by Th29,FINSEQ_1:4,61;
  hence thesis by A5,FUNCT_1:def 8;
end;

theorem Th104:
  x <> y & y <> z & z <> x iff <* x,y,z *> is one-to-one
proof
  thus x <> y & y <> z & z <> x implies <* x,y,z *> is one-to-one
  proof
    assume that
A1: x <> y and
A2: y <> z & z <> x;
    {x,y} /\ {z} = {}
    proof
      assume
A3:   not thesis;
      consider y1 being Element of {x,y} /\ {z};
      y1 in {x,y} & y1 in {z} by A3,XBOOLE_0:def 4;
      then (y1 = x or y1 = y) & y1 = z by TARSKI:def 1,def 2;
      hence thesis by A2;
    end;
    then {} = rng<* x,y *> /\ {z} by FINSEQ_2:147
      .= rng<* x,y *> /\ rng<* z *> by FINSEQ_1:55;
    then
A4: rng<* x,y *> misses rng<* z *> by XBOOLE_0:def 7;
    <* x,y *> is one-to-one & <* z *> is one-to-one by A1,Th102,Th103;
    then <* x,y *> ^ <* z *> is one-to-one by A4,Th98;
    hence thesis by FINSEQ_1:60;
  end;
  set p = <* x,y,z *>;
  assume
A5: <* x,y,z *> is one-to-one;
  1 in {1,2,3} & 2 in {1,2,3} & 3 in {1,2,3} by ENUMSET1:def 1;
  then 1 in dom p & 2 in dom p & 3 in dom p & 1 <> 2 & 1 <> 3 & 2 <> 3 &
  p.1 = x & p.2 = y & p.3 = z by Th1,Th30,FINSEQ_1:62;
  hence thesis by A5,FUNCT_1:def 8;
end;

theorem Th105:
  p is one-to-one & rng p = {x} implies len p = 1
proof
  assume that
A1: p is one-to-one and
A2: rng p = {x};
A3: now
    given y1,y2 such that
A4: y1 in dom p & y2 in dom p and
A5: y1 <> y2;
    p.y1 in rng p & p.y2 in rng p by A4,FUNCT_1:def 5;
    then p.y1 = x & p.y2 = x by A2,TARSKI:def 1;
    hence contradiction by A1,A4,A5,FUNCT_1:def 8;
  end;
A6: dom p <> {} by A2,RELAT_1:65;
  consider y being Element of dom p;
A7: dom p = {y}
  proof
    thus dom p c= {y}
    proof
      let x;
      assume x in dom p;
      then x = y by A3;
      hence thesis by TARSKI:def 1;
    end;
    let x;
    y in dom p by A6;
    hence thesis by TARSKI:def 1;
  end;
  dom p = Seg(len p) by FINSEQ_1:def 3;
  hence len p = 1 by A7,Th22;
end;

theorem
  p is one-to-one & rng p = {x} implies p = <* x *>
proof
  assume that
A1: p is one-to-one and
A2: rng p = {x};
  len p = 1 by A1,A2,Th105;
  hence thesis by A2,FINSEQ_1:56;
end;

theorem Th107:
  p is one-to-one & rng p = {x,y} & x <> y implies len p = 2
proof
  assume that
A1: p is one-to-one & rng p = {x,y} and
A2: x <> y;
  set q = <* x,y *>;
  rng q = {x,y} & q is one-to-one & len q = 2 by A2,Th103,FINSEQ_1:61
,FINSEQ_2:147;
  hence thesis by A1,FINSEQ_1:65;
end;

theorem
  p is one-to-one & rng p = {x,y} & x <> y implies
  p = <* x,y *> or p = <* y,x *>
proof
  assume that
A1: p is one-to-one and
A2: rng p = {x,y} and
A3: x <> y;
A4: len p = 2 by A1,A2,A3,Th107;
  then dom p = {1,2} by FINSEQ_1:4,def 3;
  then
A5: 1 in dom p & 2 in dom p by TARSKI:def 2;
  then p.1 in rng p & p.2 in rng p & 1 <> 2 by FUNCT_1:def 5;
  then (p.1 = x & p.2 = x or p.1 = x & p.2 = y or
  p.1 = y & p.2 = x or p.1 = y & p.2 = y) &
  p.1 <> p.2 by A1,A2,A5,FUNCT_1:def 8,TARSKI:def 2;
  hence thesis by A4,FINSEQ_1:61;
end;

theorem Th109:
  p is one-to-one & rng p = {x,y,z} & <* x,y,z *> is one-to-one implies
  len p = 3
proof rng <* x,y,z *> = {x,y,z} & len <* x,y,z *> = 3 by FINSEQ_1:62
,FINSEQ_2:148;
  hence thesis by FINSEQ_1:65;
end;

theorem
  p is one-to-one & rng p = {x,y,z} & x <> y & y <> z & x <> z implies
  len p = 3
proof
  assume that
A1: p is one-to-one & rng p = {x,y,z} and
A2: x <> y & y <> z & x <> z;
  <* x,y,z *> is one-to-one by A2,Th104;
  hence thesis by A1,Th109;
end;

begin :: Addenda

:: from FSM_1

theorem
  for D being non empty set, df being FinSequence of D holds
  df is non empty implies ex d being Element of D, df1 being FinSequence of D
  st d = df.1 & df = <*d*>^df1
proof
  let D be non empty set, df be FinSequence of D;
  assume df is non empty;
  then
A1: len df <> 0;
  then 1 <= len df by NAT_1:14;
  then 1 in dom df by Th27;
  then reconsider d = df.1 as Element of D by FINSEQ_2:13;
  take d;
  reconsider lend1 = len df - 1 as Element of NAT by A1,INT_1:18,NAT_1:14;
  deffunc F(Nat) = df.($1+1);
  consider dta being FinSequence such that
A2: len dta = lend1 and
A3: for j being Nat st j in dom dta holds dta.j=F(j) from FINSEQ_1:sch 2;
  now
    let j be Nat;
    assume
A4:   j in dom dta;
    then j in Seg len dta by FINSEQ_1:def 3;
    then j + 1 in Seg (len dta + 1) by FINSEQ_1:81;
    then j+1 in dom df by A2,FINSEQ_1:def 3;
    then df.(j+1) in D by FINSEQ_2:13;
    hence dta.j in D by A3,A4;
  end;
  then reconsider dta as FinSequence of D by FINSEQ_2:14;
  take dta; thus d = df.1;
  now thus
A6: len (<*d*>^dta) = len <*d*> + len dta by FINSEQ_1:35
      .= 1 + (len df - 1) by A2,FINSEQ_1:57
      .= len df;
X: dom df = Seg len df by FINSEQ_1:def 3;
    let i be Nat;
    assume i in dom df;
    then
A7: 1 <= i & i <= len df by X,FINSEQ_1:3;
    per cases by A7,XXREAL_0:1;
    suppose i = 1;
      hence df.i = (<*d*>^dta).i by FINSEQ_1:58;
    end;
    suppose
A8:   i > 1;
      then consider j being Element of NAT such that
A9:   j = i-1 & 1 <= j by INT_1:78;
      i - 1 <= lend1 by A7,XREAL_1:11;
      then
A10:   j in dom dta by A9,A2,Th27;
      len <*d*> = 1 by FINSEQ_1:57;
      then (<*d*>^dta).i = dta.j by A6,A7,A8,A9,FINSEQ_1:37
        .= df.(j+1) by A3,A10
        .= df.i by A9;
      hence df.i = (<*d*>^dta).i;
    end;
  end;
  hence thesis by FINSEQ_2:10;
end;

theorem
  for df being FinSequence, d being set holds
  i in dom df implies (<*d*>^df).(i+1) = df.i
proof
  let df be FinSequence, d be set;
  assume
A1: i in dom df;
  then
A2: i in Seg len df by FINSEQ_1:def 3;
  len (<*d*>^df) = len <*d*> + len df by FINSEQ_1:35
    .= 1 + len df by FINSEQ_1:57;
  then i+1 in Seg len (<*d*>^df) by A2,FINSEQ_1:81;
  then
A3: i+1 in dom (<*d*>^df) by FINSEQ_1:def 3;
A4: len <*d*> = 1 by FINSEQ_1:57;
  1 <= i by A1,Th27;
  then
A5: 1 < i+1 by NAT_1:13;
  i+1 <= len (<*d*>^df) by A3,Th27;
  hence (<*d*>^df).(i+1) = df.(i + 1 - len <*d*>) by A4,A5,FINSEQ_1:37
    .= df.i by A4;
end;

:: from MATRIX_2, 2005.11.16, A.T.

definition
  let i;
  let p be FinSequence;
  func Del(p,i) -> FinSequence equals

  p * Sgm ((dom p) \ {i});
  coherence
  proof
    set q = Sgm((dom p) \ {i});
A1: dom q = Seg len q by FINSEQ_1:def 3;
A2: dom p = Seg len p by FINSEQ_1:def 3;
    (Seg len p) \ {i} c= Seg len p by XBOOLE_1:36;
    then rng q c= dom p by A2,FINSEQ_1:def 13;
    then dom (p*q) = dom q by RELAT_1:46;
    hence p*q is FinSequence by A1,FINSEQ_1:def 2;
  end;
end;

theorem Th113:
  for p being FinSequence holds (i in dom p implies
  ex m being Nat st len p = m + 1 & len Del(p,i) = m) &
  (not i in dom p implies Del(p,i) = p)
proof
  let p be FinSequence;
  hereby
    assume
A1: i in dom p;
    then p <> {};
    then
A2: len p <> 0;
A3: i in Seg len p by A1,FINSEQ_1:def 3;
    consider m being Nat such that
A4: len p = m+1 by A2,NAT_1:6;
A5: (Seg(len p) \ {i}) c= Seg len p
    proof
      for x holds x in (Seg(len p) \ {i}) implies x in Seg(len p)
      by XBOOLE_0:def 5;
      hence thesis by TARSKI:def 3;
    end;
    then
A6: rng Sgm(Seg(len p) \ {i}) c= Seg len p by FINSEQ_1:def 13;
    reconsider D' = dom p as non empty set by A1;
    dom p = Seg len p by FINSEQ_1:def 3;
    then reconsider q = Sgm((dom p) \ {i}) as FinSequence of D'
    by A6,FINSEQ_1:def 4;
    reconsider D = rng p as non empty set by A1,FUNCT_1:12;
    reconsider r = p as Function of D',D by FUNCT_2:3;
A7: len(r*q) = len q by FINSEQ_2:37;
A8: len (Sgm(Seg(len p) \ {i}))=m
    proof
      not i in (Seg(len p) \ {i})
      proof
        assume i in (Seg(len p) \ {i});
        then i in Seg(len p) & not i in {i} by XBOOLE_0:def 5;
        hence thesis by TARSKI:def 1;
      end;
      then
A9:   card ((Seg(len p) \ {i}) \/ {i})=card (Seg(len p) \ {i}) +1
      by CARD_2:54;
      {i} c= Seg len p
      proof
        for x holds x in {i} implies x in Seg(len p) by A3,TARSKI:def 1;
        hence thesis by TARSKI:def 3;
      end;
      then card Seg(len p)=card (Seg(len p) \ {i}) +1 by A9,XBOOLE_1:45;
      then card (Seg(len p) \ {i}) +1 = m+1 by A4,FINSEQ_1:78;
      hence thesis by A5,Th44;
    end;
    take m;
    thus len p=m + 1 & len Del(p,i) = m by A4,A7,A8,FINSEQ_1:def 3;
  end;
  assume not i in dom p;
  then for x st x in {i} holds not x in dom p by TARSKI:def 1;
  then {i} misses dom p by XBOOLE_0:3;
  hence Del(p,i) = p * Sgm(dom p) by XBOOLE_1:83
    .= p * Sgm(Seg len p) by FINSEQ_1:def 3
    .= p * (idseq len p) by Th54
    .= p|(Seg len p) by RELAT_1:94
    .= p|(dom p) by FINSEQ_1:def 3
    .= p by RELAT_1:97;
end;

theorem
  for D being non empty set
  for p being FinSequence of D holds Del(p,i) is FinSequence of D
proof
  let D be non empty set, p be FinSequence of D;
  per cases;
  suppose
A1: i in dom p;
    Seg(len p) \ {i} c= Seg(len p)
    proof
      for x holds x in (Seg(len p) \ {i}) implies x in Seg(len p)
      by XBOOLE_0:def 5;
      hence thesis by TARSKI:def 3;
    end;
    then
A2: rng Sgm(Seg(len p) \ {i}) c= Seg(len p) by FINSEQ_1:def 13;
    reconsider D'=Seg(len p) as non empty set by A1,FINSEQ_1:def 3;
    reconsider q=Sgm(Seg(len p) \ {i}) as FinSequence of D'
    by A2,FINSEQ_1:def 4;
    p * q = Del(p,i) by FINSEQ_1:def 3;
    hence thesis by FINSEQ_2:35;
  end;
  suppose not i in dom p;
    hence thesis by Th113;
  end;
end;

:: from MATRLIN, 2005.11.16, A.T.

theorem
  for p be FinSequence holds rng Del(p,i) c= rng p
proof
  let p be FinSequence;
  let x;
  assume x in rng Del(p,i);
  hence x in rng p by FUNCT_1:25;
end;

:: from GOBOARD1, 2005.11.16, A.T.

theorem Th116:
  n = m + 1 & i in Seg n implies len Sgm(Seg n \ {i}) = m
proof
  assume
A1: n = m + 1 & i in Seg n;
  set X = Seg n \ {i};
A2: X c= Seg n & i in {i} by TARSKI:def 1,XBOOLE_1:36;
  then not i in X & Seg n is finite by XBOOLE_0:def 5;
  then card X + 1 = card (X \/ {i}) by CARD_2:54
    .= card ((Seg n) \/ {i}) by XBOOLE_1:39
    .= card (Seg n) by A1,ZFMISC_1:46
    .= m + 1 by A1,FINSEQ_1:78;
  hence thesis by A2,Th44;
end;

reserve J for Element of NAT;

theorem Th117:
  for i,k,m,n being Element of NAT st n=m+1 & k in Seg n & i in Seg m
  holds (1<=i & i<k implies Sgm(Seg n \ {k}).i = i) &
  (k<=i & i<=m implies Sgm(Seg n \ {k}).i = i+1)
proof
  let i,k,m,n be Element of NAT;
  defpred P[Element of NAT] means
  for n,k,i being Element of NAT st n = $1 + 1 & k in Seg n & i in Seg $1
  holds (1<=i & i<k implies Sgm(Seg n \ {k}).i = i) &
  (k<=i & i<=$1implies Sgm(Seg n \ {k}).i = i+1);
A1: P[0];
A2: for k being Element of NAT st P[k] holds P[k+1]
  proof
    let k being Element of NAT such that
A3: P[k];
    let g,i,n be Element of NAT;
    assume that
A4: g = k+1 + 1 and
A5: i in Seg g and
A6: n in Seg(k+1);
    set X = Seg g \ {i};
A7: X c= Seg g by XBOOLE_1:36;
    then
A8: rng Sgm(X) = X by FINSEQ_1:def 13;
A9: len Sgm(X)=k+1 by A4,A5,Th116;
A10: 1<=i & i<=g & 1<=n & n<=k+1 by A5,A6,FINSEQ_1:3;
    now per cases;
      suppose
A11:    i=g;
        then
A12:    k+1<i by A4,NAT_1:13;
        X = Seg(k+1)
        proof
          thus X c= Seg(k+1)
          proof
            let x;
            assume x in X;
            then
A13:        x in Seg g & not x in {i} & Seg g = {J: 1<=J & J<= g}
            by FINSEQ_1:def 1,XBOOLE_0:def 5;
            then consider J such that
A14:        x=J & 1<=J & J<=g;
            x<>i by A13,TARSKI:def 1;
            then J<g by A11,A14,XXREAL_0:1;
            then J<=k+1 by A4,NAT_1:13;
            hence thesis by A14,FINSEQ_1:3;
          end;
          let x;
          assume x in Seg(k+1);
          then x in {J: 1<=J & J<=k+1} by FINSEQ_1:def 1;
          then consider J such that
A15:      x=J & 1<=J & J<=k+1;
          k+1<=g by A4,NAT_1:11;
          then J<=g by A15,XXREAL_0:2;
          then
A16:      J in Seg g by A15,FINSEQ_1:3;
          not J in {i} by A12,A15,TARSKI:def 1;
          hence thesis by A15,A16,XBOOLE_0:def 5;
        end;
        then Sgm X = idseq (k+1) by Th54;
        hence 1<=n & n<i implies Sgm(X).n = n by A6,FUNCT_1:34;
        thus i<=n & n<=k+1 implies Sgm(Seg g \ {i}).n = n+1 by A4,A11,NAT_1:13;
      end;
      suppose
A17:    i<>g;
        then
A18:    i<g by A10,XXREAL_0:1;
        then
A19:    i<=k+1 by A4,NAT_1:13;
        1<=k+1 by A10,XXREAL_0:2;
        then k+1 in dom Sgm(X) by A9,Th27;
        then
A20:    Sgm(X).(k+1) in X by A8,FUNCT_1:def 5;
        set A = {l where l is Element of NAT : 1<=l & l<=g & l<>i};
A21:    X = A
        proof
          thus X c= A
          proof
            let x;
            assume x in X;
            then
A22:        x in Seg g & not x in {i} & Seg g= {J: 1<=J & J<=g }
            by FINSEQ_1:def 1,XBOOLE_0:def 5;
            then consider m being Element of NAT such that
A23:        x=m & 1<=m & m<=g;
            m<>i by A22,A23,TARSKI:def 1;
            hence x in A by A23;
          end;
          let x;
          assume x in A;
          then consider m being Element of NAT such that
A24:      x=m & 1<=m & m<=g & m<>i;
          m in Seg g & not m in {i} by A24,FINSEQ_1:3,TARSKI:def 1;
          hence thesis by A24,XBOOLE_0:def 5;
        end;
        then
A25:    ex J st Sgm(X).(k+1) = J & 1<=J & J<=g & J<>i by A20;
        1<=g & g<=g & g<>i by A10,A17,XXREAL_0:2;
        then g in rng Sgm(X) by A8,A21;
        then consider x be Nat such that
A26:    x in dom Sgm(X) & Sgm(X).x=g by FINSEQ_2:11;
A27:    1<=x & x<=k+1 & k+1<=len Sgm(X) by A9,A26,Th27;
        then
A28:    k+1<=x by A7,A25,A26,FINSEQ_1:def 13;
        now per cases by A10,XXREAL_0:1;
          suppose
A29:        n=k+1;
            hence 1<=n & n<i implies Sgm(X).n = n by A4,A18,NAT_1:13;

thus i<=n & n<=k+1 implies Sgm(Seg g \ {i}).n=n+1 by A4,A26,A27,A28,A29,
XXREAL_0:1;
          end;
          suppose
A30:        n<k+1;
            then n<=k by NAT_1:13;
            then
A31:        n in Seg k & i in Seg(k+1) by A10,A19,FINSEQ_1:3;
            then
A32:        (1<=n & n<i implies Sgm(Seg(k+1) \ {i}).n = n) &
            (i<=n & n<=k implies Sgm(Seg(k+1) \ {i}).n = n+1) by A3;
            set Y = Seg(k+1) \ {i};
A33:        X = Y \/ {g}
            proof
              thus X c= Y \/ {g}
              proof
                let x;
                assume x in X;
                then consider J such that
A34:            x=J & 1<=J & J<=g & J<>i by A21;
                now per cases by A34,XXREAL_0:1;
                  suppose J=g;
                    then x in {g} by A34,TARSKI:def 1;
                    hence thesis by XBOOLE_0:def 3;
                  end;
                  suppose J<g;
                    then J<=k+1 by A4,NAT_1:13;

then J in Seg(k+1) & not J in {i} by A34,FINSEQ_1:3,TARSKI:def 1;
                    then x in Y by A34,XBOOLE_0:def 5;
                    hence thesis by XBOOLE_0:def 3;
                  end;
                end;
                hence thesis;
              end;
              let x such that
A35:          x in Y \/ {g};
              now per cases by A35,XBOOLE_0:def 3;
                suppose x in Y;
                  then
A36:              x in Seg(k+1) & not x in {i} &
                  Seg(k+1)={ s where s is Element of NAT :1<=s & s<=k+1}
                  by FINSEQ_1:def 1,XBOOLE_0:def 5;
                  then consider s be Element of NAT such that
A37:              x=s & 1<=s & s<=k+1;
                  k+1<=g by A4,NAT_1:11;
                  then s<=g & s<>i by A36,A37,TARSKI:def 1,XXREAL_0:2;
                  hence x in X by A21,A37;
                end;
                suppose x in {g};
                  then x=g & 1<=g by A10,TARSKI:def 1,XXREAL_0:2;
                  hence thesis by A17,A21;
                end;
              end;
              hence thesis;
            end;
A38:        now
              let j1,j be Element of NAT;
              assume j1 in Y & j in {g};
              then
A39:          j1 in Seg(k+1) & j=g by TARSKI:def 1,XBOOLE_0:def 5;
              then j1<=k+1 by FINSEQ_1:3;
              hence j1<j by A4,A39,NAT_1:13;
            end;
            Y c=X & {g} c= X by A33,XBOOLE_1:7;
            then Y c= Seg g & {g} c= Seg g by A7,XBOOLE_1:1;
            then
A40:        Sgm(X) = Sgm(Y)^Sgm({g}) by A33,A38,Th48;
A41:        len Sgm(Y) = k by A31,Th116;
            now
              assume
A42:          1<=n & n<i;
              n in Seg len Sgm(Y) by A31,Th116;
              then n in dom Sgm(Y) by FINSEQ_1:def 3;
              hence Sgm(X).n = n by A32,A40,A42,FINSEQ_1:def 7;
            end;
            hence 1<=n & n<i implies Sgm(X).n = n;
            assume
A43:        i<=n & n<=k+1;
            n<=k by A30,NAT_1:13;
            then n in Seg k by A10,FINSEQ_1:3;
            then n in dom Sgm(Y) by A41,FINSEQ_1:def 3;
            hence Sgm(X).n = n+1 by A30,A32,A40,A43,FINSEQ_1:def 7,NAT_1:13;
          end;
        end;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
A44: for k being Element of NAT holds P[k] from NAT_1:sch 1(A1,A2);
  assume n = m + 1 & k in Seg n & i in Seg m;
  hence thesis by A44;
end;

theorem Th118:
  for f being FinSequence, n,m st len f = m+1 & n in dom f holds len Del(f,n)=m
proof
  let f be FinSequence, n,m such that
A1: len f = m+1 & n in dom f;
  ex k being Nat st len f = k+1 & len Del(f,n) = k by A1,Th113;
  hence thesis by A1;
end;

theorem
  for f being FinSequence,n,m,k being Element of NAT st len f = m+1 & k<n
  holds Del(f,n).k = f.k
proof
  let f be FinSequence,n,m,k be Element of NAT;
  assume
A1: len f = m+1 & k<n;
  per cases;
  suppose
A2: n in dom f;
    now per cases;
      suppose
A3:     1 <= k;
        n<=m+1 by A1,A2,Th27;
        then k<m+1 by A1,XXREAL_0:2;
        then k<=m by NAT_1:13;
        then
A4:     k in Seg m by A3,FINSEQ_1:3;
        set X = dom f \ {n};
A5:     dom f=Seg len f & dom Sgm(X)=Seg len Sgm(X) by FINSEQ_1:def 3;
        then
A6:     Del(f,n) = f*Sgm(X) & X c= Seg len f by XBOOLE_1:36;
        then rng Sgm(X) = X by FINSEQ_1:def 13;
        then
A7:     dom (f*Sgm X) = dom Sgm X & len Sgm X = m
        by A1,A2,A5,A6,Th116,RELAT_1:46;
        1<=k & k<n implies Sgm(X).k = k by A1,A2,A4,A5,Th117;
        hence thesis by A1,A3,A4,A5,A7,FUNCT_1:22;
      end;
      suppose
A8:     not 1 <= k;
        then not k in dom f by Th27;
        then
A9:     f.k = {} by FUNCT_1:def 4;
        Seg len Del (f,n) = Seg m by A1,A2,Th118;
        then dom Del (f,n) = Seg m by FINSEQ_1:def 3;
        then not k in dom Del (f,n) by A8,FINSEQ_1:3;
        hence thesis by A9,FUNCT_1:def 4;
      end;
    end;
    hence thesis;
  end;
  suppose not n in dom f;
    hence thesis by Th113;
  end;
end;

theorem
  for f being FinSequence,n,m,k being Element of NAT
  st len f = m+1 & n in dom f & n<=k & k<=m holds Del(f,n).k = f.(k+1)
proof
  let f be FinSequence,n,m,k be Element of NAT;
  assume
A1: len f = m+1 & n in dom f & n<=k & k<=m;
  then 1<=n by Th27;
  then 1<=k by A1,XXREAL_0:2;
  then
A2: k in Seg m by A1,FINSEQ_1:3;
  set X = dom f \ {n};
A3: dom f=Seg len f & dom Sgm(X)=Seg len Sgm(X) by FINSEQ_1:def 3;
  then
A4: Del(f,n) = f*Sgm(X) & X c= Seg len f by XBOOLE_1:36;
  then rng Sgm(X) = X by FINSEQ_1:def 13;
  then
A5: dom (f*Sgm(X)) = dom Sgm(X) & len Sgm(X) = m by A1,A3,A4,Th116,RELAT_1:46;
  n<=k & k<=m implies Sgm(X).k = k+1 by A1,A2,A3,Th117;
  hence thesis by A1,A2,A3,A5,FUNCT_1:22;
end;

:: from JORDAN3, 2005.11.16, A.T.

theorem Th121:
  for f being FinSequence st k<=n holds (f|n).k=f.k
proof
  let f be FinSequence;
  assume
A1: k<=n;
  per cases by NAT_1:14;
  suppose
A2: k = 0;
    then
A3: not k in dom f by Th27;
    not k in dom(f|n) by A2,Th27;
    hence (f|n).k = {} by FUNCT_1:def 4
      .= f.k by A3,FUNCT_1:def 4;
  end;
  suppose 1 <= k;
    then k in Seg n by A1,FINSEQ_1:3;
    then (f|Seg n).k=f.k by FUNCT_1:72;
    hence (f|n).k=f.k by FINSEQ_1:def 15;
  end;
end;

:: missing, 27.11.2005, A.T.

theorem
  for p,q being FinSequence st p c= q holds q|len p = p
proof
  let p,q be FinSequence such that
A1: p c= q;
  len p <= len q by A1,FINSEQ_1:84;
  then len(q|len p) = len p by FINSEQ_1:80;
  then
A2: dom(q|len p) = dom p by Th31;
  for k being Nat st k in dom p holds p.k = (q|len p).k
  proof
    let k be Nat;
    assume
A3: k in dom p;
    then
A4: k <= len p by Th27;
    thus p.k = q.k by A1,A3,GRFUNC_1:8
      .= (q|len p).k by A4,Th121;
  end;
  hence q|len p = p by A2,FINSEQ_1:17;
end;

:: from MATRLIN, 2006.01.06, A.T.

theorem Th123:
  for A being set, F being FinSequence
  holds Sgm(F"A)^Sgm(F"(rng F \ A)) is Permutation of dom F
proof
  let A be set;
  let F be FinSequence;
A1: dom F = Seg len F by FINSEQ_1:def 3;
  set p = Sgm(F"A)^Sgm(F"(rng F \ A));
A2: F"A c= dom F by RELAT_1:167;
  then
A3: F"A c= Seg len F by FINSEQ_1:def 3;
A4: F"(rng F \ A) c= dom F by RELAT_1:167;
  then
A5: F"(rng F \ A) c= Seg len F by FINSEQ_1:def 3;
A6: Sgm(F"A) is one-to-one by A3,Lm1;
A7: Sgm(F"(rng F \ A)) is one-to-one by A5,Lm1;
  A misses (rng F \ A) by XBOOLE_1:79;
  then
A8: A /\ (rng F \ A) = {} by XBOOLE_0:def 7;
A9: F"A /\ F"(rng F \ A) = F"(A /\ (rng F \ A)) by FUNCT_1:137
    .= {} by A8,RELAT_1:171;
  then
A10: F"A misses F"(rng F \ A) by XBOOLE_0:def 7;
  rng Sgm(F"A) /\ rng Sgm(F"(rng F \ A)) = F"A /\ rng Sgm(F"(rng F \ A))
  by A3,FINSEQ_1:def 13
    .= {} by A5,A9,FINSEQ_1:def 13;
  then rng Sgm(F"A) misses rng Sgm(F"(rng F \ A)) by XBOOLE_0:def 7;
  then
A11: p is one-to-one by A6,A7,Th98;
A12: rng p = rng Sgm(F"A) \/ rng Sgm(F"(rng F \ A)) by FINSEQ_1:44
    .= F"A \/ rng Sgm(F"(rng F \ A)) by A3,FINSEQ_1:def 13
    .= F"A \/ F"(rng F \ A) by A5,FINSEQ_1:def 13
    .= F"(A \/ (rng F \ A)) by RELAT_1:175
    .= F"((rng F) \/ A) by XBOOLE_1:39
    .= F"(rng F) \/ F"A by RELAT_1:175
    .= (dom F) \/ F"A by RELAT_1:169
    .= dom F by RELAT_1:167,XBOOLE_1:12;
  len p = len Sgm(F"A) + len Sgm(F"(rng F \ A)) by FINSEQ_1:35
    .= card (F"A) + len Sgm(F"(rng F \ A)) by A3,Th44
    .= card (F"A) + card (F"(rng F \ A)) by A5,Th44
    .= card (F"A \/ F"(rng F \ A)) by A10,CARD_2:53
    .= len Sgm (F"A \/ F"(rng F \ A)) by A1,A2,A4,Th44,XBOOLE_1:8
    .= len Sgm (F"(A \/ (rng F \ A))) by RELAT_1:175
    .= len Sgm (F"((rng F) \/ A)) by XBOOLE_1:39
    .= len Sgm (F"(rng F) \/ F"A) by RELAT_1:175
    .= len Sgm (dom F \/ F"A) by RELAT_1:169
    .= len Sgm dom F by RELAT_1:167,XBOOLE_1:12
    .= card Seg len F by A1,Th44
    .= len F by FINSEQ_1:78;
  then dom p = dom F by Th31;
  then p is Function of dom F, dom F by A12,FUNCT_2:3;
  hence thesis by A11,A12,FUNCT_2:83;
end;

theorem
  for F being FinSequence, A be Subset of rng F st F is one-to-one
  ex p being Permutation of dom F st (F-A`)^(F-A) = F*p
proof
  let F be FinSequence;
  let A be Subset of rng F such that
A1: F is one-to-one;
  set F1 = F-A`, F2 = F-A;
  (rng F) \ (rng F1) = (rng F) \ (rng F \ A`) by Th72
    .= rng F \ (rng F \ (rng F \ A)) by SUBSET_1:def 5
    .= rng F \ (rng F /\ A) by XBOOLE_1:48
    .= rng F \ A by XBOOLE_1:47
    .= rng F2 by Th72;
  then reconsider p = Sgm(F"(rng F1))^Sgm(F"(rng F2))
  as Permutation of dom F by Th123;
  take p;
A2: F1 is one-to-one & F2 is one-to-one by A1,Th94;
A3: A misses (rng F \ A) by XBOOLE_1:79;
A4: rng(F-A`) = rng F \ A` by Th72;
  then rng F1 /\ rng F2 = (rng F \ A`) /\ (rng F \ A) by Th72
    .= (rng F \ (rng F \ A)) /\ (rng F \ A) by SUBSET_1:def 5
    .= (rng F /\ A) /\ (rng F \ A) by XBOOLE_1:48
    .= A /\ (rng F \ A) by XBOOLE_1:28
    .= {} by A3,XBOOLE_0:def 7;
  then
A5: rng F1 misses rng F2 by XBOOLE_0:def 7;
A6: rng (F1^F2) = rng F1 \/ rng F2 by FINSEQ_1:44
    .= (rng F \ A`) \/ (rng F \ A) by A4,Th72
    .= (rng F \ (rng F \ A)) \/ (rng F \ A) by SUBSET_1:def 5
    .= (rng F /\ A) \/ (rng F \ A) by XBOOLE_1:48
    .= A \/ (rng F \ A) by XBOOLE_1:28
    .= A \/ rng F by XBOOLE_1:39
    .= rng F by XBOOLE_1:12;
A7: F1^F2 is one-to-one by A2,A5,Th98;
  reconsider F3 = F * p as FinSequence by FINSEQ_2:44;
A8: len F3 = len F by FINSEQ_2:48
    .= len (F1^F2) by A1,A6,A7,FINSEQ_1:65
    .= len F1 + len F2 by FINSEQ_1:35;
A9: now
    let k be Element of NAT such that
A10: k in dom F1;
A11: F"A` = F"(rng F \ A) by SUBSET_1:def 5
      .= F"(rng F) \ F"A by FUNCT_1:138
      .= dom F \ F"A by RELAT_1:169;
A12: F"(rng F) = dom F by RELAT_1:169;
    then
A13: card(F"(rng F) \ F"A`) = card (F"(rng F)) - card (F"A`)
    by A11,CARD_2:63,XBOOLE_1:36
      .= card Seg len F - card (F"A`)
    by A12,FINSEQ_1:def 3
      .= len F - card (F"A`) by FINSEQ_1:78
      .= len (F - A`) by Th66;
    F"(rng F1) c= dom F by RELAT_1:167;
    then
A14: F"(rng F1) c= Seg len F by FINSEQ_1:def 3;
A15: dom Sgm(F"(rng F1)) = Seg len Sgm(F"(rng F1)) by FINSEQ_1:def 3
      .= Seg card(F"(rng F1)) by A14,Th44
      .= Seg card(F"(rng F \ A`)) by Th72
      .= Seg len (F - A`) by A13,FUNCT_1:138
      .= dom F1 by FINSEQ_1:def 3;
    dom Sgm(F"(rng F1)) c= dom p by FINSEQ_1:39;
    hence (F*p).k = F.(p.k) by A10,A15,FUNCT_1:23
      .= F.(Sgm(F"(rng F1)).k) by A10,A15,FINSEQ_1:def 7
      .= (F*Sgm(F"(rng F1))).k by A10,A15,FUNCT_1:23
      .= (F*Sgm(F"(rng F \ A`))).k by Th72
      .= (F*Sgm(F"(rng F \ (rng F \ A)))).k by SUBSET_1:def 5
      .= (F*Sgm(F"(rng F /\ A))).k by XBOOLE_1:48
      .= (F*Sgm(F"(rng F) /\ F"A)).k by FUNCT_1:137
      .= (F*Sgm(dom F /\ F"A)).k by RELAT_1:169
      .= F1.k by A11,XBOOLE_1:48;
  end;
  now
    let k be Element of NAT such that
A16: k in dom F2;
A17: F"(rng F) = dom F by RELAT_1:169;
    F"A c= dom F by RELAT_1:167;
    then F"A c= F"(rng F) by RELAT_1:169;
    then
A18: card(F"(rng F) \ F"A) = card (F"(rng F)) - card (F"A) by CARD_2:63
      .= card Seg len F - card (F"A) by A17,FINSEQ_1:def 3
      .= len F - card (F"A) by FINSEQ_1:78
      .= len (F - A) by Th66;
    dom F = Seg len F by FINSEQ_1:def 3;
    then len Sgm(F"(rng F2)) = card(F"(rng F2)) by Th44,RELAT_1:167
      .= card(F"(rng F \ A)) by Th72
      .= len (F - A) by A18,FUNCT_1:138;
    then
A19: dom Sgm(F"(rng F2)) = dom F2 by Th31;
A20: F"A` = F"(rng F \ A) by SUBSET_1:def 5
      .= F"(rng F) \ F"A by FUNCT_1:138
      .= dom F \ F"A by RELAT_1:169;
A21: F"(rng F) = dom F by RELAT_1:169;
    then
A22: card(F"(rng F) \ F"A`) = card (F"(rng F)) - card (F"A`)
    by A20,CARD_2:63,XBOOLE_1:36
      .= card Seg len F - card (F"A`)
    by A21,FINSEQ_1:def 3
      .= len F - card (F"A`) by FINSEQ_1:78
      .= len (F - A`) by Th66;
    F"(rng F1) c= dom F by RELAT_1:167;
    then F"(rng F1) c= Seg len F by FINSEQ_1:def 3;
    then
A23: len Sgm(F"(rng F1)) = card(F"(rng F1)) by Th44
      .= card(F"(rng F \ A`)) by Th72
      .= len (F - A`) by A22,FUNCT_1:138;
    then len F1 + k in dom p by A16,A19,FINSEQ_1:41;
    hence (F*p).(len F1 + k) = F.(p.(len F1 + k)) by FUNCT_1:23
      .= F.(Sgm(F"(rng F2)).k) by A16,A19,A23,FINSEQ_1:def 7
      .= (F*Sgm(F"(rng F2))).k by A16,A19,FUNCT_1:23
      .= (F*Sgm(F"(rng F \ A))).k by Th72
      .= (F*Sgm(F"(rng F) \ F"A)).k by FUNCT_1:138
      .= F2.k by RELAT_1:169;
  end;
  hence thesis by A8,A9,Th43;
end;

:: Moved from GRAPH_2 by AK on 27.12.2006

theorem
  for f being FinSubsequence holds f is FinSequence implies Seq f = f
proof
  let f be FinSubsequence;
  assume f is FinSequence;
  then reconsider ss'=f as FinSequence;
  consider k be Nat such that
A1: dom ss' = Seg k by FINSEQ_1:def 2;
  k in NAT by ORDINAL1:def 13;
  then
A2: len ss' <= k by A1,FINSEQ_1:def 3;
  Seq f = f* Sgm Seg k by A1,FINSEQ_1:def 14
    .= f* idseq k by Th54
    .= f by A2,FINSEQ_2:64;
  hence thesis;
end;

:: Moved from SCMBSORT by AK on 10.10.2007

theorem
  for t be FinSequence of INT holds t is FinSequence of REAL
proof
  let t be FinSequence of INT;
  now
    let i be Nat;
    assume i in dom t;
    then
A1: t.i in rng t by FUNCT_1:def 5;
    rng t c= INT by FINSEQ_1:def 4;
    then t.i in INT by A1;
    hence t.i in REAL by NUMBERS:15;
  end;
  hence thesis by FINSEQ_2:14;
end;

:: missing, 2008.02.16, A.T,

theorem
  len p < len q iff dom p c< dom q
proof
A: len p <= len q iff dom p c= dom q by Th32;
   len p = len q iff dom p = dom q by Th31;
 hence thesis by A,XBOOLE_0:def 8,XXREAL_0:1;
end;
