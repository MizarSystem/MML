:: The Basic Properties of { \bf SCM } over Ring
::  by Artur Korni{\l}owicz
::
:: Received November 29, 1998
:: Copyright (c) 1998 Association of Mizar Users

environ

 vocabularies NUMBERS, SUBSET_1, CARD_1, XBOOLE_0, STRUCT_0, SCMFSA7B,
      FUNCSDOM, AMI_3, AMI_1, AMI_2, FUNCT_7, TARSKI, RELAT_1, ZFMISC_1, FSM_1,
      FUNCT_1, CAT_1, FINSEQ_1, GRAPHSP, FUNCT_2, CARD_3, ORDINAL1, ARYTM_3,
      ARYTM_1, SUPINF_2, FUNCOP_1, SCMRING1, GLIB_000, FUNCT_4,
      ALGSTR_0, RECDEF_2, COMPOS_1, AMISTD_2;
 notations TARSKI, XBOOLE_0, ENUMSET1, ZFMISC_1, SUBSET_1, RELAT_1, FUNCT_1,
      CARD_1, FUNCT_2, RECDEF_2,
      STRUCT_0, ALGSTR_0, FUNCSDOM, MCART_1, CARD_3, FINSEQ_1,
      FUNCOP_1, FUNCT_4, FUNCT_7, COMPOS_1, EXTPRO_1,
      AMI_1,
      AMI_2, AMI_3, SCMRING1;
 constructors XXREAL_0, FINSEQ_4, REALSET1, CAT_2, AMI_3, SCMRING1, PRE_POLY,
 RECDEF_2, AMI_1;
 registrations XBOOLE_0, SETFAM_1, ORDINAL1, RELSET_1, FUNCOP_1, FINSEQ_1,
      CARD_3, STRUCT_0, AMI_1, AMI_3, SCMRING1, AMI_2, FUNCT_1, FUNCT_2,
      ALGSTR_0, ALGSTR_1, GCD_1, COMPOS_1, EXTPRO_1;
 requirements NUMERALS, REAL, SUBSET, BOOLE, ARITHM;
 definitions STRUCT_0, AMI_3, FUNCOP_1, SCMRING1, COMPOS_1, EXTPRO_1,
      AMI_1, CARD_1, NAT_1, AMI_2;
 theorems AMI_1, AMI_2, AMI_3, AMI_5, CARD_3, FUNCOP_1, ENUMSET1, FUNCT_4,
      MCART_1, SCMRING1, TARSKI, ZFMISC_1, XBOOLE_0, XBOOLE_1, FUNCT_7,
      ARYTM_3, NAT_1, FINSEQ_1, FUNCT_1, PARTFUN1, PBOOLE, RELAT_1, RECDEF_2,
      COMPOS_1, EXTPRO_1;

begin  :: { \bf SCM } over ring

reserve I for Element of Segm 8,
  S for non empty 1-sorted,
  t for Element of S,
  x for set,
  k for Element of NAT;

registration
  cluster strict trivial Ring;
  existence
  proof
    consider R being strict trivial non empty doubleLoopStr;
    take R;
    thus thesis;
  end;
end;

definition
  let R be good Ring;
  func SCM R -> strict AMI-Struct over { the carrier of R } means
  :Def1:
  the carrier of it = SCM-Memory & the Instruction-Counter of it = NAT &
  the Instructions of it = SCM-Instr R & the haltF of it = [0,{},{}] &
  the Object-Kind of it = SCM-OK R & the
  Execution of it = SCM-Exec R;
  existence
  proof
    take AMI-Struct (#SCM-Memory,In(NAT,SCM-Memory),
      SCM-Instr R, In([0,{},{}],SCM-Instr R),
      SCM-OK R, SCM-Exec R#);
     [0,{},{}] in SCM-Instr R by SCMRING1:22;
    hence thesis by AMI_2:30,FUNCT_7:def 1;
  end;
  uniqueness;
end;

registration
  let R be good Ring;
  cluster SCM R -> non empty stored-program standard-ins;
  coherence
  proof
    thus the carrier of SCM R is non empty by Def1;
    NAT c= the carrier of SCM;
    hence NAT c= the carrier of SCM R by Def1;
    consider X being non empty set such that
WW:   proj2 SCM-Instr R c= X* by FINSEQ_1:106;
    take X;
S:   SCM-Instr R c= [:NAT,NAT*,proj2 SCM-Instr R:] by SCMRING1:21;
     [:NAT,NAT*,proj2 SCM-Instr R:] c= [:NAT,NAT*,X*:] by WW,MCART_1:77;
     then SCM-Instr R c= [:NAT,NAT*,X*:] by S,XBOOLE_1:1;
    hence the Instructions of SCM R c= [:NAT,NAT*,X*:] by Def1;
  end;
end;

definition
  let R be good Ring;
  mode Data-Location of R -> Object of SCM R means
:Def2: it in (the carrier
    of SCM R) \ (NAT \/ {NAT});
  existence
  proof
    [1,1] in Data-Locations SCM by AMI_2:33,AMI_3:72;
    then reconsider a = [1,1] as Object of SCM R by Def1;
    take a;
A1: now
      assume a in {NAT};
      then a = NAT by TARSKI:def 1;
      hence contradiction by ARYTM_3:36;
    end;
    not [1,1] in NAT by ARYTM_3:38;
    then not a in NAT \/ {NAT} by A1,XBOOLE_0:def 3;
    hence thesis by XBOOLE_0:def 5;
  end;
end;

reserve R for good Ring,
  r for Element of R,
  a, b, c, d1, d2 for Data-Location
  of R,
  i1 for Element of NAT;

theorem Th1:
  x is Data-Location of R iff x in Data-Locations SCM
proof
A1: the carrier of SCM R = SCM-Memory by Def1;
A2: the carrier of SCM R = {IC SCM} \/ Data-Locations SCM \/ NAT
  by Def1,AMI_5:23;
  hereby
    assume
A3: x is Data-Location of R;
    then x in (the carrier of SCM R) \ (NAT \/ {NAT}) by Def2;
    then
A4: not x in NAT \/ {NAT} by XBOOLE_0:def 5;
    then not x in NAT by XBOOLE_0:def 3;
    then
A5: x in {IC SCM} \/ Data-Locations SCM by A2,A3,XBOOLE_0:def 3;
    not x in {NAT} by A4,XBOOLE_0:def 3;
    hence x in Data-Locations SCM by A5,AMI_3:4,XBOOLE_0:def 3;
  end;
  assume
A6: x in Data-Locations SCM;
  then x is Data-Location by AMI_3:72,def 2;
  then x <> NAT by AMI_3:4,AMI_5:20;
  then
A7: not x in {NAT} by TARSKI:def 1;
  not x in NAT by A6,AMI_2:29,AMI_3:72,XBOOLE_0:3;
  then not x in NAT \/ {NAT} by A7,XBOOLE_0:def 3;
  then x in (the carrier of SCM R) \ (NAT \/ {NAT}) by A1,A6,XBOOLE_0:def 5;
  hence thesis by Def2;
end;

definition
  let R be good Ring, s be State of SCM R, a be Data-Location of R;
  redefine func s.a -> Element of R;
  coherence
  proof
  the Object-Kind of SCM R = SCM-OK R by Def1;
    then reconsider S = s as SCM-State of R by PBOOLE:155;
    a is Element of Data-Locations SCM by Th1;
    then reconsider a as Element of SCM-Data-Loc by AMI_3:72;
    S.a in the carrier of R;
    hence thesis;
  end;
end;

canceled;

theorem Th3:
  [0,{},{}] is Instruction of SCM R by Def1;

theorem Th4:
  x in {1,2,3,4} implies [x,{},<*d1,d2*>] in SCM-Instr S
proof
  reconsider D1 = d1, D2 = d2 as Element of Data-Locations SCM by Th1;
  assume
A1: x in {1,2,3,4};
  then x=1 or x=2 or x=3 or x=4 by ENUMSET1:def 2;
  then reconsider x as Element of Segm 8 by NAT_1:45;
  [x,{},<*D1,D2*>] in { [I,{},<*a,b*>]
    where a,b is Element of Data-Locations SCM: I
  in { 1,2,3,4 } } by A1;
  then [x,{},<*D1,D2*>] in { [0,{},{}] } \/ { [I,{},<*a,b*>]
  where a,b is Element of
  Data-Locations SCM: I in { 1,2,3,4 } } by XBOOLE_0:def 3;
  then [x,{},<*D1,D2*>] in { [0,{},{}] } \/ { [I,{},<*a,b*>]
  where a,b is Element of
Data-Locations SCM: I in { 1,2,3,4 } } \/ { [6,<*i*>,{}]
where i is Element of NAT: not
  contradiction } by XBOOLE_0:def 3;
  then [x,{},<*D1,D2*>] in { [0,{},{}] } \/ { [I,{},<*a,b*>]
  where a,b is Element of
Data-Locations SCM: I in { 1,2,3,4 } } \/ { [6,<*i*>,{}]
where i is Element of NAT: not
  contradiction } \/ { [7,<*i*>,<*a*>] where i is Element of NAT,
  a is Element of
  Data-Locations SCM: not contradiction } by XBOOLE_0:def 3;
  hence thesis by AMI_3:72,XBOOLE_0:def 3;
end;

theorem Th5:
  [5,{},<*d1,t*>] in SCM-Instr S
proof
  reconsider D1 = d1 as Element of Data-Locations SCM by Th1;
  [5,{},<*D1,t*>] in { [5,{},<*i,a*>]
    where i is Element of Data-Locations SCM, a is
  Element of S : not contradiction };
  hence thesis by AMI_3:72,XBOOLE_0:def 3;
end;

theorem Th6:
  [6,<*i1*>,{}] in SCM-Instr S
proof
  reconsider I1 = i1 as Element of NAT;
  [6,<*I1*>,{}] in {[6,<*i*>,{}] where i is Element of NAT: not contradiction};
  then [6,<*I1*>,{}] in { [0,{},{}] } \/ { [I,{},<*a,b*>]
  where a,b is Element of
  Data-Locations SCM: I in { 1,2,3,4 } } \/ {[6,<*i*>,{}]
  where i is Element of NAT: not
  contradiction} by XBOOLE_0:def 3;
  then [6,<*I1*>,{}] in { [0,{},{}] } \/ { [I,{},<*a,b*>]
  where a,b is Element of
  Data-Locations SCM: I in { 1,2,3,4 } } \/ {[6,<*i*>,{}]
  where i is Element of NAT: not
  contradiction} \/ { [7,<*i*>,<*a*>] where i is Element of NAT,
  a is Element of
  Data-Locations SCM: not contradiction } by XBOOLE_0:def 3;
  hence thesis by AMI_3:72,XBOOLE_0:def 3;
end;

theorem Th7:
  [7,<*i1*>,<*d1*>] in SCM-Instr S
proof
  reconsider D1 = d1 as Element of Data-Locations SCM by Th1;
  reconsider I1 = i1 as Element of NAT;
  [7,<*I1*>,<*D1*>] in { [7,<*i*>,<*a*>]
  where i is Element of NAT, a is Element
  of Data-Locations SCM: not contradiction };
  then [7,<*I1*>,<*D1*>] in { [0,{},{}] } \/ { [I,{},<*a,b*>]
  where a,b is Element of
  Data-Locations SCM: I in { 1,2,3,4 } } \/ {[6,<*i*>,{}]
  where i is Element of NAT: not
  contradiction} \/ { [7,<*i*>,<*a*>]
  where i is Element of NAT, a is Element of
  Data-Locations SCM: not contradiction } by XBOOLE_0:def 3;
  hence thesis by AMI_3:72,XBOOLE_0:def 3;
end;

definition
  let R be good Ring, a, b be Data-Location of R;
  func a := b -> Instruction of SCM R equals
  [1,{},<*a,b*>];
  coherence
  proof
    1 in { 1,2,3,4} by ENUMSET1:def 2;
    then [1,{},<*a,b*>] in SCM-Instr R by Th4;
    hence thesis by Def1;
  end;
  func AddTo(a,b) -> Instruction of SCM R equals
  [2,{},<*a,b*>];
  coherence
  proof
    2 in { 1,2,3,4} by ENUMSET1:def 2;
    then [2,{},<*a,b*>] in SCM-Instr R by Th4;
    hence thesis by Def1;
  end;
  func SubFrom(a,b) -> Instruction of SCM R equals
  [3,{},<*a,b*>];
  coherence
  proof
    3 in { 1,2,3,4} by ENUMSET1:def 2;
    then [3,{},<*a,b*>] in SCM-Instr R by Th4;
    hence thesis by Def1;
  end;
  func MultBy(a,b) -> Instruction of SCM R equals
  [4,{},<*a,b*>];
  coherence
  proof
    4 in { 1,2,3,4} by ENUMSET1:def 2;
    then [4,{},<*a,b*>] in SCM-Instr R by Th4;
    hence thesis by Def1;
  end;
end;

definition
  let R be good Ring, a be Data-Location of R, r be Element of R;
  func a := r -> Instruction of SCM R equals
  [5,{},<*a,r*>];
  coherence
  proof
    [5,{},<*a,r*>] in SCM-Instr R by Th5;
    hence thesis by Def1;
  end;
end;

definition
  let R be good Ring, l be Element of NAT;
  func goto(l,R) -> Instruction of SCM R equals
  [6,<*l*>,{}];
  coherence
  proof
    [6,<*l*>,{}] in SCM-Instr R by Th6;
    hence thesis by Def1;
  end;
end;

definition
  let R be good Ring, l be Element of NAT, a be Data-Location
  of R;
  func a=0_goto l -> Instruction of SCM R equals
  [7,<*l*>,<*a*>];
  coherence
  proof
    [7,<*l*>,<*a*>] in SCM-Instr R by Th7;
    hence thesis by Def1;
  end;
end;

theorem Th8:
  for I being set holds I is Instruction of SCM R iff I = [0,{},{}] or
(ex a,b st I = a:=b) or (ex a,b st I = AddTo(a,b)) or (ex a,b st I = SubFrom(a,
b)) or (ex a,b st I = MultBy(a,b)) or (ex i1 st I = goto(i1,R)) or
 (ex a,i1 st I =
  a=0_goto i1) or ex a,r st I = a:=r
proof
  let J be set;
A1: the Instructions of SCM R = SCM-Instr R by Def1;
  thus J is Instruction of SCM R implies J = [0,{},{}] or
   (ex a,b st J = a:=b) or
  (ex a,b st J = AddTo(a,b)) or (ex a,b st J = SubFrom(a,b)) or (ex a,b st J =
MultBy(a,b)) or (ex i1 st J = goto(i1,R)) or
 (ex a,i1 st J = a=0_goto i1) or ex a,
  r st J = a:=r
  proof
    assume J is Instruction of SCM R;
    then
    J in { [0,{},{}] } \/ { [I,{},<*a,b*>]
    where I is Element of Segm 8, a, b is
Element of Data-Locations SCM: I in { 1,2,3,4 } } \/ { [6,<*i*>,{}]
where i is Element
of NAT: not contradiction } \/ { [7,<*i*>,<*a*>]
where i is Element of NAT, a is
Element of Data-Locations SCM: not contradiction } or
 J in { [5,{},<*a,r*>] where a is
    Element of Data-Locations SCM, r is Element of R: not contradiction }
    by A1,AMI_3:72,XBOOLE_0:def 3;
    then
    J in { [0,{},{}] } \/ { [I,{},<*a,b*>]
    where I is Element of Segm 8, a, b is
Element of Data-Locations SCM: I in { 1,2,3,4 } } \/ { [6,<*i*>,{}]
where i is Element
of NAT: not contradiction } or J in { [7,<*i*>,<*a*>]
where i is Element of NAT, a
is Element of Data-Locations SCM: not contradiction } or J in { [5,{},<*a,r*>]
where a
    is Element of Data-Locations SCM, r is Element of R: not contradiction }
by XBOOLE_0:def 3;
    then
A2: J in { [0,{},{}] } \/ { [I,{},<*a,b*>]
where I is Element of Segm 8, a, b is
    Element of Data-Locations SCM: I in { 1,2,3,4 } } or J in { [6,<*i*>,{}]
    where i is
Element of NAT: not contradiction } or J in { [7,<*i*>,<*a*>]
where i is Element of
NAT, a is Element of Data-Locations SCM: not contradiction }
 or J in { [5,{},<*a,r*>]
where a is Element of Data-Locations SCM, r is Element of R:
 not contradiction } by XBOOLE_0:def 3;
    per cases by A2,XBOOLE_0:def 3;
    suppose
      J in { [0,{},{}] };
      hence thesis by TARSKI:def 1;
    end;
    suppose
      J in { [6,<*i*>,{}] where i is Element of NAT: not contradiction };
      then consider i being Element of NAT such that
A3:   J = [6,<*i*>,{}] and
      not contradiction;
      reconsider i as Element of NAT;
      J = goto(i,R) by A3;
      hence thesis;
    end;
    suppose
      J in { [7,<*i*>,<*a*>] where i is Element of NAT, a is Element of
      Data-Locations SCM: not contradiction };
      then consider
      i being Element of NAT, a being Element of Data-Locations SCM such
      that
A4:   J = [7,<*i*>,<*a*>] and
      not contradiction;
      reconsider A = a as Data-Location of R by Th1,AMI_3:72;
      reconsider I = i as Element of NAT;
      J = A=0_goto I by A4;
      hence thesis;
    end;
    suppose
      J in { [5,{},<*a,r*>] where a is Element of Data-Locations SCM, r is
      Element of R: not contradiction };
      then consider
      a being Element of Data-Locations SCM, r being Element of R such that
A5:   J = [5,{},<*a,r*>] and
      not contradiction;
      reconsider A = a as Data-Location of R by Th1,AMI_3:72;
      J = A:=r by A5;
      hence thesis;
    end;
    suppose
      J in { [I,{},<*a,b*>] where I is Element of Segm 8, a, b is
      Element of Data-Locations SCM: I in { 1,2,3,4 } };
      then consider
      I being Element of Segm 8, a, b being Element of Data-Locations SCM
      such that
A6:   J = [I,{},<*a,b*>] & I in { 1,2,3,4 };
      reconsider A = a, B = b as Data-Location of R by Th1,AMI_3:72;
      J = A:=B or J = AddTo(A,B) or J = SubFrom(A,B) or J = MultBy(A,B)
      by A6,ENUMSET1:def 2;
      hence thesis;
    end;
  end;
  thus thesis by A1,SCMRING1:22;
end;

reserve s for State of SCM R;

registration
  let R;
  cluster SCM R -> proper-halt IC-Ins-separated;
  coherence
  proof
    the haltF of SCM R = [0,{},{}] by Def1;
    then JumpPart halt SCM R is empty by RECDEF_2:def 2;
    then halt SCM R is ins-loc-free by COMPOS_1:def 37;
   hence SCM R is proper-halt by COMPOS_1:def 39;
    thus ObjectKind IC SCM R = (SCM-OK R).IC SCM R by Def1
      .= (SCM-OK R).NAT by Def1
      .= NAT by AMI_2:30,SCMRING1:def 3;
  end;
end;

theorem
  IC SCM R = NAT by Def1;

theorem
  for S being SCM-State of R st S = s holds IC s = IC S by Def1;

canceled;

theorem Th12:
  for I being Instruction of SCM R for i being Element of
  SCM-Instr R st i = I for S being SCM-State of R st S = s holds Exec(I,s) =
  SCM-Exec-Res(i,S)
proof
  let I be Instruction of SCM R, i be Element of SCM-Instr R such that
A1: i = I;
  let S be SCM-State of R;
  assume S = s;
  hence Exec(I,s) = ((SCM-Exec R).i qua Element of Funcs(product SCM-OK R,
  product SCM-OK R)).S by A1,Def1
    .= SCM-Exec-Res(i,S) by SCMRING1:def 15;
end;

begin :: Users guide

theorem Th13:
  Exec(a := b, s).IC SCM R = succ IC s & Exec(a := b, s).a = s.b &
  for c st c <> a holds Exec(a := b, s).c = s.c
proof
A1: a is Element of Data-Locations SCM by Th1;
Y: the Object-Kind of SCM R = SCM-OK R by Def1;
  reconsider S = s as SCM-State of R by Y,PBOOLE:155;
  reconsider I = a := b as Element of SCM-Instr R by Def1;
  set S1 = SCM-Chg(S, I address_1,S.(I address_2));
  reconsider i = 1 as Element of Segm 8 by NAT_1:45;
A2: IC s = IC S by Def1;
A3: b is Element of Data-Locations SCM by Th1;
A4: Exec(a := b, s) = SCM-Exec-Res(I,S) by Th12
    .= (SCM-Chg(S1, succ IC S)) by A1,A3,AMI_3:72,SCMRING1:def 14;
A5: I = [i,{},<*a,b*>];
  then
A6: I address_1 = a by A1,A3,AMI_3:72,SCMRING1:17;
A7: I address_2 = b by A5,A1,A3,AMI_3:72,SCMRING1:17;
  thus Exec(a := b, s).IC SCM R = Exec(a := b, s).NAT by Def1
    .= succ IC s by A2,A4,SCMRING1:10;
  thus Exec(a := b, s).a = S1.a by A1,A4,AMI_3:72,SCMRING1:11
    .= s.b by A6,A7,SCMRING1:14;
  let c;
  assume
A8: c <> a;
A9: c is Element of Data-Locations SCM by Th1;
  hence Exec(a := b, s).c = S1.c by A4,AMI_3:72,SCMRING1:11
    .= s.c by A6,A8,A9,AMI_3:72,SCMRING1:15;
end;

theorem Th14:
  Exec(AddTo(a,b), s).IC SCM R = succ IC s & Exec(AddTo(a,b), s).a
  = s.a + s.b & for c st c <> a holds Exec(AddTo(a,b), s).c = s.c
proof
A1: a is Element of Data-Locations SCM by Th1;
Y: the Object-Kind of SCM R = SCM-OK R by Def1;
  reconsider S = s as SCM-State of R by Y,PBOOLE:155;
  reconsider I = AddTo(a,b) as Element of SCM-Instr R by Def1;
  set S1 = SCM-Chg(S, I address_1,S.(I address_1)+S.(I address_2));
  reconsider i = 2 as Element of Segm 8 by NAT_1:45;
A2: IC s = IC S by Def1;
A3: b is Element of Data-Locations SCM by Th1;
A4: Exec(AddTo(a,b), s) = SCM-Exec-Res(I,S) by Th12
    .= (SCM-Chg(S1, succ IC S)) by A1,A3,AMI_3:72,SCMRING1:def 14;
A5: I = [i,{},<*a,b*>];
  then
A6: I address_1 = a by A1,A3,AMI_3:72,SCMRING1:17;
A7: I address_2 = b by A5,A1,A3,AMI_3:72,SCMRING1:17;
  thus Exec(AddTo(a,b), s).IC SCM R = Exec(AddTo(a,b), s).NAT by Def1
    .= succ IC s by A2,A4,SCMRING1:10;
  thus Exec(AddTo(a,b), s).a = S1.a by A1,A4,AMI_3:72,SCMRING1:11
    .= s.a + s.b by A6,A7,SCMRING1:14;
  let c;
  assume
A8: c <> a;
A9: c is Element of Data-Locations SCM by Th1;
  hence Exec(AddTo(a,b), s).c = S1.c by A4,AMI_3:72,SCMRING1:11
    .= s.c by A6,A8,A9,AMI_3:72,SCMRING1:15;
end;

theorem Th15:
  Exec(SubFrom(a,b), s).IC SCM R = succ IC s & Exec(SubFrom(a,b),
  s).a = s.a - s.b & for c st c <> a holds Exec(SubFrom(a,b), s).c = s.c
proof
A1: a is Element of Data-Locations SCM by Th1;
Y: the Object-Kind of SCM R = SCM-OK R by Def1;
  reconsider S = s as SCM-State of R by Y,PBOOLE:155;
  reconsider I = SubFrom(a,b) as Element of SCM-Instr R by Def1;
  set S1 = SCM-Chg(S, I address_1,S.(I address_1)-S.(I address_2));
  reconsider i = 3 as Element of Segm 8 by NAT_1:45;
A2: IC s = IC S by Def1;
A3: b is Element of Data-Locations SCM by Th1;
A4: Exec(SubFrom(a,b), s) = SCM-Exec-Res(I,S) by Th12
    .= (SCM-Chg(S1, succ IC S)) by A1,A3,AMI_3:72,SCMRING1:def 14;
A5: I = [i,{},<*a,b*>];
  then
A6: I address_1 = a by A1,A3,AMI_3:72,SCMRING1:17;
A7: I address_2 = b by A5,A1,A3,AMI_3:72,SCMRING1:17;
  thus Exec(SubFrom(a,b), s).IC SCM R = Exec(SubFrom(a,b), s).NAT by Def1
    .= succ IC s by A2,A4,SCMRING1:10;
  thus Exec(SubFrom(a,b), s).a = S1.a by A1,A4,AMI_3:72,SCMRING1:11
    .= s.a - s.b by A6,A7,SCMRING1:14;
  let c;
  assume
A8: c <> a;
A9: c is Element of Data-Locations SCM by Th1;
  hence Exec(SubFrom(a,b), s).c = S1.c by A4,AMI_3:72,SCMRING1:11
    .= s.c by A6,A8,A9,AMI_3:72,SCMRING1:15;
end;

theorem Th16:
  Exec(MultBy(a,b), s).IC SCM R = succ IC s & Exec(MultBy(a,b), s)
  .a = s.a * s.b & for c st c <> a holds Exec(MultBy(a,b), s).c = s.c
proof
A1: a is Element of Data-Locations SCM by Th1;
Y: the Object-Kind of SCM R = SCM-OK R by Def1;
  reconsider S = s as SCM-State of R by Y,PBOOLE:155;
  reconsider I = MultBy(a,b) as Element of SCM-Instr R by Def1;
  set S1 = SCM-Chg(S, I address_1,S.(I address_1)*S.(I address_2));
  reconsider i = 4 as Element of Segm 8 by NAT_1:45;
A2: IC s = IC S by Def1;
A3: b is Element of Data-Locations SCM by Th1;
A4: Exec(MultBy(a,b), s) = SCM-Exec-Res(I,S) by Th12
    .= (SCM-Chg(S1, succ IC S)) by A1,A3,AMI_3:72,SCMRING1:def 14;
A5: I = [i,{},<*a,b*>];
  then
A6: I address_1 = a by A1,A3,AMI_3:72,SCMRING1:17;
A7: I address_2 = b by A5,A1,A3,AMI_3:72,SCMRING1:17;
  thus Exec(MultBy(a,b), s).IC SCM R = Exec(MultBy(a,b), s).NAT by Def1
    .= succ IC s by A2,A4,SCMRING1:10;
  thus Exec(MultBy(a,b), s).a = S1.a by A1,A4,AMI_3:72,SCMRING1:11
    .= s.a * s.b by A6,A7,SCMRING1:14;
  let c;
  assume
A8: c <> a;
A9: c is Element of Data-Locations SCM by Th1;
  hence Exec(MultBy(a,b), s).c = S1.c by A4,AMI_3:72,SCMRING1:11
    .= s.c by A6,A8,A9,AMI_3:72,SCMRING1:15;
end;

theorem
  Exec(goto(i1,R), s).IC SCM R = i1 & Exec(goto(i1,R), s).c = s.c
proof
Y: the Object-Kind of SCM R = SCM-OK R by Def1;
  reconsider S = s as SCM-State of R by Y,PBOOLE:155;
  reconsider i = 6 as Element of Segm 8 by NAT_1:45;
  reconsider I = goto(i1,R) as Element of SCM-Instr R by Def1;
  I = [i,<*i1*>,{}];
  then
A2: I jump_address = i1 by SCMRING1:18;
A3: Exec(goto(i1,R), s) = SCM-Exec-Res(I,S) by Th12
    .= (SCM-Chg(S,I jump_address)) by SCMRING1:def 14;
  thus Exec(goto(i1,R), s).IC SCM R = Exec(goto(i1,R), s).NAT by Def1
    .= i1 by A3,A2,SCMRING1:10;
  c is Element of Data-Locations SCM by Th1;
  hence thesis by A3,AMI_3:72,SCMRING1:11;
end;

theorem Th18:
  (s.a = 0.R implies Exec(a =0_goto i1, s).IC SCM R = i1) & (s.a
<> 0.R implies Exec(a =0_goto i1, s).IC SCM R = succ IC s) & Exec(a =0_goto i1,
  s).c = s.c
proof
Y: the Object-Kind of SCM R = SCM-OK R by Def1;
  reconsider S = s as SCM-State of R by Y,PBOOLE:155;
  reconsider I = a =0_goto i1 as Element of SCM-Instr R by Def1;
  reconsider i = 7 as Element of Segm 8 by NAT_1:45;
A1: a is Element of Data-Locations SCM & i1 is Element of NAT by Th1;
A2: Exec(a =0_goto i1, s) = SCM-Exec-Res(I,S) by Th12
    .= SCM-Chg(S,IFEQ(S.(I cond_address),0.R,I cjump_address,succ IC S)) by A1,
AMI_3:72,SCMRING1:def 14;
A3: I = [i,<*i1*>,<*a*>];
  thus s.a = 0.R implies Exec(a =0_goto i1, s).IC SCM R = i1
  proof
    assume s.a = 0.R;
    then
A4: S.(I cond_address)=0.R by A3,A1,AMI_3:72,SCMRING1:19;
    thus Exec(a =0_goto i1, s).IC SCM R = Exec(a =0_goto i1, s).NAT by Def1
      .= IFEQ(S.(I cond_address),0.R,I cjump_address,succ IC S) by A2,
SCMRING1:10
      .= I cjump_address by A4,FUNCOP_1:def 8
      .= i1 by A3,A1,AMI_3:72,SCMRING1:19;
  end;
A5: IC s = IC S by Def1;
  thus s.a <> 0.R implies Exec(a =0_goto i1, s).IC SCM R = succ IC s
  proof
    assume s.a <> 0.R;
    then
A6: S.(I cond_address) <> 0.R by A3,A1,AMI_3:72,SCMRING1:19;
    thus Exec(a =0_goto i1, s).IC SCM R = Exec(a =0_goto i1, s).NAT by Def1
      .= IFEQ(S.(I cond_address),0.R,I cjump_address,succ IC S) by A2,
SCMRING1:10
      .= succ IC s by A5,A6,FUNCOP_1:def 8;
  end;
  c is Element of Data-Locations SCM by Th1;
  hence thesis by A2,AMI_3:72,SCMRING1:11;
end;

theorem Th19:
  Exec(a := r, s).IC SCM R = succ IC s & Exec(a := r, s).a = r &
  for c st c <> a holds Exec(a := r, s).c = s.c
proof
A1: a is Element of Data-Locations SCM by Th1;
Y: the Object-Kind of SCM R = SCM-OK R by Def1;
  reconsider S = s as SCM-State of R by Y,PBOOLE:155;
  reconsider I = a := r as Element of SCM-Instr R by Def1;
  set S1 = SCM-Chg(S, I const_address, I const_value);
  reconsider i = 5 as Element of Segm 8 by NAT_1:45;
A2: IC s = IC S by Def1;
A3: I = [ i,{}, <*a, r*>];
  then
A4: I const_address = a by A1,AMI_3:72,SCMRING1:20;
A5: I const_value = r by A3,A1,AMI_3:72,SCMRING1:20;
A6: Exec(a := r, s) = SCM-Exec-Res(I,S) by Th12
    .= (SCM-Chg(S1, succ IC S)) by A1,AMI_3:72,SCMRING1:def 14;
  thus Exec(a := r, s).IC SCM R = Exec(a := r, s).NAT by Def1
    .= succ IC s by A2,A6,SCMRING1:10;
  thus Exec(a := r, s).a = S1.a by A1,A6,AMI_3:72,SCMRING1:11
    .= r by A4,A5,SCMRING1:14;
  let c;
  assume
A7: c <> a;
A8: c is Element of Data-Locations SCM by Th1;
  hence Exec(a := r, s).c = S1.c by A6,AMI_3:72,SCMRING1:11
    .= s.c by A4,A7,A8,AMI_3:72,SCMRING1:15;
end;

begin  :: Halt instruction

theorem Th20:
  for I being Instruction of SCM R st ex s st Exec(I,s).IC SCM R =
  succ IC s holds I is non halting
proof
  let I be Instruction of SCM R;
  given s such that
A1: Exec(I, s).IC SCM R = succ IC s;
Y: the Object-Kind of SCM R = SCM-OK R by Def1;
  reconsider t = s as SCM-State of R by Y,PBOOLE:155;
  IC t = t.NAT;
  then reconsider w = t.NAT as Element of NAT;
A2: Exec(I,s).IC SCM R = succ w by A1,Def1;
  assume
A3: I is halting;
  IC t = IC s by Def1;
  then Exec(I,s).IC SCM R = t.NAT by A3,EXTPRO_1:def 3;
  hence contradiction by A2;
end;

theorem Th21:
  for I being Instruction of SCM R st I = [0,{},{}] holds I is
  halting
proof
  let I be Instruction of SCM R such that
A1: I = [0,{},{}];
A2: I`3_3 = {} by A1,RECDEF_2:def 3;
  then
A3: ( not(ex mk, ml being Element of Data-Locations SCM st
 I = [ 1,{}, <*mk, ml*>]))&
  not( ex mk, ml being Element of Data-Locations SCM st I =
   [ 2, {}, <*mk, ml*>]) by RECDEF_2:def 3;
A4: not(ex mk being Element of Data-Locations SCM, r being Element of R
 st I = [ 5,{}, <*mk,r*>]) by A2,RECDEF_2:def 3;
 I`2_3 = {} by A1,RECDEF_2:def 2;
  then
A5: ( not(ex mk being Element of NAT st I = [ 6, <*mk*>,{}]))&
not(ex mk being
Element of NAT, ml being Element of Data-Locations SCM st
 I = [ 7,<*mk*>,<*ml*>]) by RECDEF_2:def 2;
  reconsider L = I as Element of SCM-Instr R by A1,SCMRING1:22;
  let s be State of SCM R;
Y: the Object-Kind of SCM R = SCM-OK R by Def1;
  reconsider t = s as SCM-State of R by Y,PBOOLE:155;
A6: ( not(ex mk, ml being Element of Data-Locations SCM
st I = [ 3,{}, <*mk, ml*>]))&
  not( ex mk, ml being Element of Data-Locations SCM
  st I = [ 4,{}, <*mk, ml*>]) by A2,RECDEF_2:def 3;
  thus Exec(I,s) = SCM-Exec-Res(L,t) by Th12
    .= s by A3,A6,A5,A4,AMI_3:72,SCMRING1:def 14;
end;

Lm1: a := b is non halting
proof
  consider s;
  Exec(a := b,s).IC SCM R = succ IC s by Th13;
  hence thesis by Th20;
end;

Lm2: AddTo(a,b) is non halting
proof
  consider s;
  Exec(AddTo(a,b),s).IC SCM R = succ IC s by Th14;
  hence thesis by Th20;
end;

Lm3: SubFrom(a,b) is non halting
proof
  consider s;
  Exec(SubFrom(a,b),s).IC SCM R = succ IC s by Th15;
  hence thesis by Th20;
end;

Lm4: MultBy(a,b) is non halting
proof
  consider s;
  Exec(MultBy(a,b),s).IC SCM R = succ IC s by Th16;
  hence thesis by Th20;
end;

Lm5: goto(i1,R) is non halting
proof
  reconsider i5 = i1 as Element of NAT;
  consider s being SCM-State of R;
  set t = s +* (NAT .--> succ i1);
  set f = the Object-Kind of SCM R;
A1: {NAT} c= SCM-Memory by AMI_2:30,ZFMISC_1:37;
A3: dom t = dom s \/ dom (NAT .--> succ i1) by FUNCT_4:def 1
    .= SCM-Memory \/ dom (NAT .--> succ i1) by PARTFUN1:def 4
    .= SCM-Memory \/ {NAT} by FUNCOP_1:19
    .= SCM-Memory by A1,XBOOLE_1:12;
A5: f = SCM-OK R by Def1;
A6: dom(NAT .--> succ i1) = {NAT} by FUNCOP_1:19;
  then NAT in dom (NAT .--> succ i1) by TARSKI:def 1;
  then
A7: t.NAT = (NAT .--> succ i1).NAT by FUNCT_4:14
    .= succ i5 by FUNCOP_1:87;
YY:  dom t = the carrier of SCM R by A3,Def1
      .= dom f by PARTFUN1:def 4;
XX: for x being set st x in dom t holds t.x in f.x
  proof
    let x be set such that
A9: x in dom t;
    per cases;
    suppose
A10:  x = NAT;
      then f.x = NAT by A5,AMI_2:30,SCMRING1:2;
      hence thesis by A7,A10;
    end;
    suppose
      x <> NAT;
      then not x in dom (NAT .--> succ i1) by A6,TARSKI:def 1;
      then t.x = s.x by FUNCT_4:12;
      hence thesis by A5,A9,YY,CARD_3:18;
    end;
  end;
Y: the Object-Kind of SCM R = SCM-OK R by Def1;
  dom t = the carrier of SCM R by A3,Def1;
  then reconsider t as PartState of SCM R by XX,FUNCT_1:def 20,RELAT_1:def 18;
  dom t = the carrier of SCM R by A3,Def1;
  then reconsider t as State of SCM R by PARTFUN1:def 4;
  reconsider w = t as SCM-State of R by Y,PBOOLE:155;
  dom(NAT .--> i1) = {NAT} by FUNCOP_1:19;
  then NAT in dom (NAT .--> i1) by TARSKI:def 1;
  then
A11: (w +* (NAT .--> i1)).NAT = (NAT .--> i1).NAT by FUNCT_4:14
    .= i1 by FUNCOP_1:87;
  reconsider V = goto(i1,R) as Element of SCM-Instr R by Def1;
  assume
A12: goto(i1,R) is halting;
A13: 6 is Element of Segm 8 by NAT_1:45;
  w +* (NAT .--> i1) = SCM-Chg(w,i5)
    .= SCM-Chg(w,V jump_address) by A13,SCMRING1:18
    .= SCM-Exec-Res(V,w) by SCMRING1:def 14
    .= Exec(goto(i1,R),t) by Th12
    .= t by A12,EXTPRO_1:def 3;
  hence contradiction by A7,A11;
end;

Lm6: a =0_goto i1 is non halting
proof
  reconsider i5 = i1 as Element of NAT;
  consider s being SCM-State of R;
  set t = s +* (NAT .--> succ i1);
  set f = the Object-Kind of SCM R;
  reconsider V = a =0_goto i1 as Element of SCM-Instr R by Def1;
A2: {NAT} c= SCM-Memory by AMI_2:30,ZFMISC_1:37;
A3: dom t = dom s \/ dom (NAT .--> succ i1) by FUNCT_4:def 1
    .= SCM-Memory \/ dom (NAT .--> succ i1) by PARTFUN1:def 4
    .= SCM-Memory \/ {NAT} by FUNCOP_1:19
    .= SCM-Memory by A2,XBOOLE_1:12;
A4: f = SCM-OK R by Def1;
A5: dom(NAT .--> succ i1) = {NAT} by FUNCOP_1:19;
  then NAT in dom (NAT .--> succ i1) by TARSKI:def 1;
  then
A6: t.NAT = (NAT .--> succ i1).NAT by FUNCT_4:14
    .= succ i5 by FUNCOP_1:87;
YY:  dom t = the carrier of SCM R by A3,Def1
      .= dom f by PARTFUN1:def 4;
X: for x being set st x in dom t holds t.x in f.x
  proof
    let x be set such that
A8: x in dom t;
    per cases;
    suppose
A9:   x = NAT;
      then f.x = NAT by A4,AMI_2:30,SCMRING1:2;
      hence thesis by A6,A9;
    end;
    suppose
      x <> NAT;
      then not x in dom (NAT .--> succ i1) by A5,TARSKI:def 1;
      then t.x = s.x by FUNCT_4:12;
      hence thesis by A4,A8,YY,CARD_3:18;
    end;
  end;
  dom t = the carrier of SCM R by A3,Def1;
  then reconsider t as PartState of SCM R by X,FUNCT_1:def 20,RELAT_1:def 18;
  dom t = the carrier of SCM R by A3,Def1;
  then reconsider t as State of SCM R by PARTFUN1:def 4;
Y: the Object-Kind of SCM R = SCM-OK R by Def1;
  reconsider w = t as SCM-State of R by Y,PBOOLE:155;
  dom(NAT .--> i1) = {NAT} by FUNCOP_1:19;
  then NAT in dom (NAT .--> i1) by TARSKI:def 1;
  then
A10: (w +* (NAT .--> i1)).NAT = (NAT .--> i1).NAT by FUNCT_4:14
    .= i1 by FUNCOP_1:87;
A11: 7 is Element of Segm 8 by NAT_1:45;
A13: a is Element of Data-Locations SCM by Th1;
  assume
A14: a =0_goto i1 is halting;
  per cases;
  suppose
A15: w.(V cond_address) <> 0.R;
    IC w = w.NAT;
    then reconsider e = w.NAT as Element of NAT;
A16: IC t = IC w by Def1;
    then
A17: Exec(a =0_goto i1,t).IC SCM R = w.NAT by A14,EXTPRO_1:def 3;
    a is Element of Data-Locations SCM by Th1;
    then t.a <> 0.R by A11,A15,AMI_3:72,SCMRING1:19;
    then Exec(a =0_goto i1,t).IC SCM R = succ e by A16,Th18;
    hence contradiction by A17;
  end;
  suppose
A18: w.(V cond_address) = 0.R;
    w +* (NAT .--> i1) = SCM-Chg(w,i5)
      .= SCM-Chg(w,V cjump_address) by A11,A13,AMI_3:72,SCMRING1:19

      .= SCM-Chg(w,IFEQ(w.(V cond_address),0.R,V cjump_address,succ IC w))
    by A18,FUNCOP_1:def 8

      .= SCM-Exec-Res(V,w) by A13,AMI_3:72,SCMRING1:def 14
      .= Exec(a =0_goto i1,t) by Th12
      .= t by A14,EXTPRO_1:def 3;
    hence contradiction by A6,A10;
  end;
end;

Lm7: a := r is non halting
proof
  consider s;
  Exec(a := r,s).IC SCM R = succ IC s by Th19;
  hence thesis by Th20;
end;

registration
  let R, a, b;
  cluster a:=b -> non halting;
  coherence by Lm1;
  cluster AddTo(a,b) -> non halting;
  coherence by Lm2;
  cluster SubFrom(a,b) -> non halting;
  coherence by Lm3;
  cluster MultBy(a,b) -> non halting;
  coherence by Lm4;
end;

registration
  let R, i1;
  cluster goto(i1,R) -> non halting;
  coherence by Lm5;
end;

registration
  let R, a, i1;
  cluster a =0_goto i1 -> non halting;
  coherence by Lm6;
end;

registration
  let R, a, r;
  cluster a:=r -> non halting;
  coherence by Lm7;
end;

Lm8: for W being Instruction of SCM R st W is halting holds W = [0,{},{}]
proof
  set I = [0,{},{}];
  let W be Instruction of SCM R such that
A1: W is halting;
  assume
A2: I <> W;
  per cases by Th8;
  suppose
    W = [0,{},{}];
    hence thesis by A2;
  end;
  suppose
    ex a,b st W = a := b;
    hence thesis by A1;
  end;
  suppose
    ex a,b st W = AddTo(a,b);
    hence thesis by A1;
  end;
  suppose
    ex a,b st W = SubFrom(a,b);
    hence thesis by A1;
  end;
  suppose
    ex a,b st W = MultBy(a,b);
    hence thesis by A1;
  end;
  suppose
    ex i1 st W = goto(i1,R);
    hence thesis by A1;
  end;
  suppose
    ex a,i1 st W = a =0_goto i1;
    hence thesis by A1;
  end;
  suppose
    ex a,r st W = a := r;
    hence thesis by A1;
  end;
end;

registration
  let R;
  cluster SCM R -> halting definite steady-programmed realistic;
  coherence
  proof
    thus SCM R is halting
    proof
     the haltF of SCM R = [0,{},{}] by Def1;
    hence the haltF of SCM R is halting by Th21;
    end;
    thus SCM R is definite
    proof
      let l be Element of NAT;
      reconsider L = l as Element of NAT;
      thus (the Object-Kind of SCM R).l = (SCM-OK R).L by Def1
        .= SCM-Instr R by SCMRING1:6
        .= the Instructions of SCM R by Def1;
    end;
    thus SCM R is steady-programmed
    proof
      let s be State of SCM R, j be Instruction of SCM R, l be
      Element of NAT;
Y: the Object-Kind of SCM R = SCM-OK R by Def1;
      reconsider S = s as SCM-State of R by Y,PBOOLE:155;
      reconsider c = j as Element of SCM-Instr R by Def1;
      reconsider l9 = l as Element of NAT;
      c in { [0,{},{}] } \/ { [I,{},<*a,b*>] where a,b is Element of
Data-Locations SCM: I in { 1,2,3,4 } } \/ { [6,<*i*>,{}]
where i is Element of NAT: not
contradiction } \/ { [7,<*i*>,<*a*>] where i is Element of NAT, a is Element of
Data-Locations SCM: not contradiction } or c in { [5,{},<*a,r*>]
where a is Element of
      Data-Locations SCM, r is Element of R: not contradiction }
      by AMI_3:72,XBOOLE_0:def 3;
      then c in { [0,{},{}] } \/ { [I,{},<*a,b*>] where a,b is Element of
Data-Locations SCM: I in { 1,2,3,4 } } \/ { [6,<*i*>,{}]
where i is Element of NAT: not
contradiction } or c in { [7,<*i*>,<*a*>]
where i is Element of NAT, a is Element
of Data-Locations SCM: not contradiction } or c in { [5,{},<*a,r*>]
where a is Element
of Data-Locations SCM, r is Element of R: not contradiction }
by XBOOLE_0:def 3;
      then
A1:   c in { [0,{},{}] } \/ { [I,{},<*a,b*>] where a,b is Element of
Data-Locations SCM: I in { 1,2,3,4 } } or c in { [6,<*i*>,{}]
where i is Element of NAT:
      not contradiction } or c in { [7,<*i*>,<*a*>]
      where i is Element of NAT, a is
Element of Data-Locations SCM: not contradiction } or c in { [5,{},<*a,r*>]
where a is
Element of Data-Locations SCM, r is Element of R: not contradiction } by
XBOOLE_0:def 3;
A2:   now
        per cases by A1,XBOOLE_0:def 3;
        case
          c in { [0,{},{}] };
          then
X:           c = [0,{},{}] by TARSKI:def 1;
          then
A3:       c`3_3 = {} by RECDEF_2:def 3;
          then
A4:       ( not(ex mk, ml being Element of Data-Locations SCM
st c = [ 3,{}, <*mk,
ml*>]))&
not( ex mk, ml being Element of Data-Locations SCM st c = [ 4,{}, <*mk, ml*>])
          by RECDEF_2:def 3;
A5:       not(ex mk being Element of Data-Locations SCM, r being Element of R
          st c = [ 5,{}, <*mk,r*>]) by A3,RECDEF_2:def 3;
       c`2_3 = {} by X,RECDEF_2:def 2;
       then
A6:       ( not(ex mk being Element of NAT
 st c = [ 6, <*mk*>,{}]))& not(ex
mk being Element of NAT, ml being Element of Data-Locations SCM st
 c = [ 7, <*mk*>,<*ml*>]) by RECDEF_2:def 2;
          ( not(ex mk, ml being Element of Data-Locations SCM
           st c = [ 1,{}, <*mk,
ml*>]))& not( ex mk, ml being Element of Data-Locations SCM
st c = [ 2,{}, <*mk, ml*>])
          by A3,RECDEF_2:def 3;
          hence SCM-Exec-Res(c,S).l9 = S.l9
          by A4,A6,A5,AMI_3:72,SCMRING1:def 14;
        end;
        case
          c in { [6,<*i*>,{}] where i is Element of NAT: not
          contradiction };
          then ex i being Element of NAT st c = [6,<*i*>,{}];
          hence SCM-Exec-Res(c,S).l9 = SCM-Chg(S,c jump_address).l9 by
SCMRING1:def 14
            .= S.l9 by SCMRING1:12;
        end;
        case
          c in { [7,<*i*>,<*a*>] where i is Element of NAT, a is Element
          of Data-Locations SCM: not contradiction };
          then ex i being Element of NAT, a being
          Element of Data-Locations SCM st c
          = [7,<*i*>,<*a*>] & not contradiction;
          hence SCM-Exec-Res(c,S).l9 = SCM-Chg(S,IFEQ(S.(c cond_address),0.R,c
          cjump_address,succ IC S)).l9 by AMI_3:72,SCMRING1:def 14
            .= S.l9 by SCMRING1:12;
        end;
        case
          c in { [I,{},<*a,b*>] where I is Element of Segm 8, a,b is
          Element of Data-Locations SCM: I in { 1,2,3,4 } };
          then consider I being Element of Segm 8, a, b being Element of
          Data-Locations SCM such that
A7:       c = [I,{},<*a,b*>] and
A8:       I in {1,2,3,4};
          now
            per cases by A8,ENUMSET1:def 2;
            case
              I = 1;
              hence SCM-Exec-Res(c,S).l9 = SCM-Chg(SCM-Chg(S, c address_1,S.(c
              address_2)), succ IC S).l9 by A7,AMI_3:72,SCMRING1:def 14
                .= SCM-Chg(S, c address_1,S.(c address_2)).l9 by SCMRING1:12
                .= S.l9 by SCMRING1:16;
            end;
            case
              I = 2;
              hence SCM-Exec-Res(c,S).l9 = SCM-Chg(SCM-Chg(S,c address_1, S.(c
              address_1)+S.(c address_2)),succ IC S).l9
              by A7,AMI_3:72,SCMRING1:def 14
                .= SCM-Chg(S,c address_1,S.(c address_1)+S.(c address_2)).l9
              by SCMRING1:12
                .= S.l9 by SCMRING1:16;
            end;
            case
              I = 3;
              hence SCM-Exec-Res(c,S).l9 = SCM-Chg(SCM-Chg(S,c address_1, S.(c
              address_1)-S.(c address_2)),succ IC S).l9
              by A7,AMI_3:72,SCMRING1:def 14
                .= SCM-Chg(S,c address_1,S.(c address_1)-S.(c address_2)).l9
              by SCMRING1:12
                .= S.l9 by SCMRING1:16;
            end;
            case
              I = 4;
              hence SCM-Exec-Res(c,S).l9 = SCM-Chg(SCM-Chg(S,c address_1, S.(c
              address_1)*S.(c address_2)),succ IC S).l9
              by A7,AMI_3:72,SCMRING1:def 14
                .= SCM-Chg(S,c address_1,S.(c address_1)*S.(c address_2)).l9
              by SCMRING1:12
                .= S.l9 by SCMRING1:16;
            end;
          end;
          hence SCM-Exec-Res(c,S).l9 = S.l9;
        end;
        case
          c in { [5,{},<*a,r*>] where a is Element of Data-Locations SCM, r is
          Element of R: not contradiction };
          then ex a being Element of Data-Locations SCM,
          r being Element of R st c =
          [5,{},<*a,r*>] & not contradiction;
          hence SCM-Exec-Res(c,S).l9 = SCM-Chg(SCM-Chg(S, c const_address, c
          const_value), succ IC S).l9 by AMI_3:72,SCMRING1:def 14
            .= SCM-Chg(S, c const_address, c const_value).l9 by SCMRING1:12
            .= S.l9 by SCMRING1:16;
        end;
      end;
      Exec(j,s) = (SCM-Exec R).c.S by Def1
        .= SCM-Exec-Res(c,S) by SCMRING1:def 15;
      hence thesis by A2;
    end;
    assume
A9: the Instruction-Counter of SCM R in NAT;
    the Instruction-Counter of SCM R = NAT by Def1;
    hence contradiction by A9;
  end;
end;

canceled 7;

theorem Th29:
  for I being Instruction of SCM R st I is halting holds I = halt
  SCM R
proof
  let I be Instruction of SCM R;
  assume I is halting;
  then I = [0,{},{}] by Lm8;
  hence thesis by Lm8;
end;

theorem
  halt SCM R = [0,{},{}]
proof
  reconsider I = [0,{},{}] as Instruction of SCM R by Th3;
  I is halting by Th21;
  hence thesis by Th29;
end;

theorem
  Data-Locations SCM R = Data-Locations SCM
proof
  Data-Locations SCM misses {NAT} by AMI_2:27,AMI_3:72,ZFMISC_1:56;
  then
A1: Data-Locations SCM misses {NAT} \/ NAT by AMI_2:29,AMI_3:72,XBOOLE_1:70;
  thus Data-Locations SCM R = SCM-Memory \ ({IC SCM R} \/ NAT) by Def1
    .= SCM-Memory \ ({NAT} \/ NAT) by Def1
    .= Data-Locations SCM \/ ({NAT} \/ NAT) \ ({NAT} \/ NAT)
        by AMI_3:72,XBOOLE_1:4
    .= Data-Locations SCM \ ({NAT} \/ NAT) by XBOOLE_1:40
    .= Data-Locations SCM by A1,XBOOLE_1:83;
end;

