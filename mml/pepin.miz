:: Public-Key Cryptography and Pepin's Test for the Primality of
:: Fermat Numbers
::  by Yoshinori Fujisawa , Yasushi Fuwa and Hidetaka Shimizu
::
:: Received December 21, 1998
:: Copyright (c) 1998 Association of Mizar Users

environ

 vocabularies EULER_1, NAT_1, SQUARE_1, ARYTM_3, INT_1, FINSET_1, ARYTM_1,
      FILTER_0, MATRIX_2, CARD_1, POWER, PEPIN, RELAT_1, GROUP_1, ARYTM,
      ORDINAL2, FINSOP_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, CARD_1, NUMBERS, XCMPLX_0,
      XREAL_0, INT_1, INT_2, NAT_1, NAT_D, SETWOP_2, POWER, BINARITH, ABIAN,
      SQUARE_1, NEWTON, EULER_1, FINSET_1, XXREAL_0;
 constructors SQUARE_1, NAT_1, NAT_D, SETWOP_2, NEWTON, SERIES_1, BINARITH,
      EULER_1, ABIAN, VALUED_1, XXREAL_2;
 registrations ORDINAL1, FINSET_1, FINSUB_1, XXREAL_0, XREAL_0, NAT_1, INT_1,
      NEWTON, ABIAN, SERIES_1, XBOOLE_0, POWER, CARD_1;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, INT_1, SQUARE_1;
 theorems REAL_2, NAT_1, NAT_2, INT_1, INT_2, FINSEQ_1, CARD_2,
      FINSET_1, EULER_1, EULER_2, BINARITH, PREPOWER, PRE_FF, POWER, NEWTON,
      XCMPLX_0, XCMPLX_1, XREAL_1, XXREAL_0, ORDINAL1, NAT_D;
 schemes NAT_1;

begin :: Some selected Basic Theorems

reserve d,i,j,k,m,n,p,q,x,n1,n2,k1,k2 for Nat,
  a,b,c,i1,i2,i3,i5 for Integer;

Lm1: for n,x,y be Nat holds
n > 1 & x >= 1 & y >= 0 & 1 = (x*y*n + x + y) implies x = 1 & y = 0
proof
  let n,x,y be Nat;
  assume
A1: n > 1 & x >= 1 & y >= 0 & 1 = (x*y*n + x + y);
  now per cases by A1,XXREAL_0:1;
    suppose
A2:   x > 1;
      now per cases;
        suppose
A3:       y > 0;
          then x*y > 1*y by A2,XREAL_1:70;
          then (x*y)*n > 1*(x*y) by A1,XREAL_1:70;
          then x*y*n + x > 0 + x by XREAL_1:8;
          then
A4:       x*y*n + x > 1 by A2,XXREAL_0:2;
          y + 1 > 0 + 1 by A3,XREAL_1:8;
          hence thesis by A1,A4,XREAL_1:8;
        end;
        suppose y = 0;
          hence thesis by A1;
        end;
      end;
      hence thesis;
    end;
    suppose
A5:   x = 1;
      now per cases;
        suppose
A6:       y > 0;
          then x*y*n > 0 by A1,A5,XREAL_1:70;
          then
A7:       x*y*n + x > 0 + 1 by A5,XREAL_1:8;
          y + 1 > 0 + 1 by A6,XREAL_1:8;
          hence thesis by A1,A7,XREAL_1:8;
        end;
        suppose y = 0;
          hence thesis by A5;
        end;
      end;
      hence thesis;
    end;
  end;
  hence thesis;
end;

Lm2: 2 |^ (2 |^ n) > 1
proof
  defpred P[Nat] means 2 |^ (2 |^ $1) > 1;
  2 |^ 0 = 1 by NEWTON:9;
  then 2 |^ (2 |^ 0) = 2 by NEWTON:10;
  then
A1: P[0];
A2: for k holds P[k] implies P[k+1]
  proof
    let k;
    assume
A3: 2 |^ (2 |^ k) > 1;
A4: 2 |^ (k + 1) = (2 |^ k)*2 by NEWTON:11;
    2 |^ (2 |^ k) < (2 |^ (2 |^ k)) |^ 2 by A3,PREPOWER:21;
    then 1 < (2 |^ (2 |^ k)) |^ 2 by A3,XXREAL_0:2;
    hence thesis by A4,NEWTON:14;
  end;
  for n holds P[n] from NAT_1:sch 2(A1,A2);
  hence thesis;
end;

Lm3: n <> 0 implies n - 1 >= 0
proof
  assume n <> 0;
  then n >= 1 by NAT_1:14;
  then n-1 >= 1-1 by XREAL_1:11;
  hence thesis;
end;

Lm4: n <> 0 implies n -'1 + 1 = (n + 1) -' 1
proof
  assume n <> 0;
  then n >= 1 by NAT_1:14;
  then n - 1 >= 1 - 1 by XREAL_1:11;
  then n -' 1 + 1 = n + -1 + 1 by BINARITH:def 3
    .= n + 1 - 1;
  hence thesis by BINARITH:def 3;
end;

theorem
  for i being Nat holds i,i+1 are_relative_prime
proof
  let k be Nat;
  k hcf (k+1) = (1+k*1) hcf k
    .= 1 hcf k by EULER_1:9
    .= 1 by NEWTON:64;
  hence thesis by INT_2:def 4;
end;

theorem Th2:
  for p being Nat holds
  p is prime implies m,p are_relative_prime or m hcf p = p
proof
  let p be Nat;
  assume
A1: p is prime;
  m hcf p divides p by NAT_D:def 5;
  then m hcf p = 1 or m hcf p = p by A1,INT_2:def 5;
  hence thesis by INT_2:def 4;
end;

theorem Th3:
  for k,m,n being Nat holds
  k divides n*m & n,k are_relative_prime implies k divides m
proof
  let k,m,n be Nat;
  assume
A1: k divides n*m & n,k are_relative_prime;
  reconsider k,m,n as Element of NAT by ORDINAL1:def 13;
  n hcf k = 1 by A1,INT_2:def 4;
  then
A2: (n*m hcf k*m) = m by EULER_1:6;
  k divides k*m by NAT_D:9;
  hence thesis by A1,A2,NEWTON:63;
end;

theorem Th4:
  n divides m & k divides m & n,k are_relative_prime implies (n*k) divides m
proof
  assume
A1: n divides m & k divides m & n,k are_relative_prime;
  then consider t1 be Nat such that
A2: m = n*t1 by NAT_D:def 3;
  k divides t1 by A1,A2,Th3;
  then consider t2 be Nat such that
A3: t1 = k*t2 by NAT_D:def 3;
  m = (n*k)*t2 by A2,A3;
  hence thesis by NAT_D:def 3;
end;

registration
  let n be Nat;
  cluster n^2 -> natural;
  coherence;
end;

definition
  let n be Element of NAT;
  redefine func n^2 -> Element of NAT;
  coherence by ORDINAL1:def 13;
end;

theorem
  c > 1 implies 1 mod c = 1
proof
  assume
A1: c > 1;
  then 1 div c = 0 by PRE_FF:4;
  then 1 mod c = 1 - 0*c by A1,INT_1:def 8
    .= 1;
  hence thesis;
end;

theorem Th6:
  for i,n be Nat st i <> 0 holds i divides n iff n mod i = 0
proof
  let i,n be Nat;
  assume
A1: i <> 0;
A2: i divides n implies n mod i = 0
  proof
    assume i divides n;
    then consider t being Nat such that
A3: n = i*t by NAT_D:def 3;
    thus thesis by A3,NAT_D:13;
  end;
  n mod i = 0 implies i divides n
  proof
    assume n mod i = 0;
    then consider t being Nat such that
A4: n = i*t + 0 & 0 < i by A1,NAT_D:def 2;
    thus thesis by A4,NAT_D:def 3;
  end;
  hence thesis by A2;
end;

theorem Th7:
  for m,n being Nat holds m <> 0 & m divides n mod m implies m divides n
proof
  let m,n be Nat;
  assume
A1: m <> 0 & m divides n mod m;
  then consider x be Nat such that
A2: n mod m = m * x by NAT_D:def 3;
A3: m > 0 by A1;
  n mod m + m * (n div m) = m * (x + (n div m)) by A2;
  then n = m * (x + (n div m)) by A3,NAT_D:2;
  hence thesis by NAT_D:def 3;
end;

theorem
  for m,n,k being Nat holds 0 < n & m mod n = k implies n divides (m - k)
proof
  let m,n,k be Nat;
  assume 0 < n & m mod n = k;
  then
A1: m = n*(m div n) + k by NAT_D:2;
  take m div n;
  thus thesis by A1;
end;

theorem
  i*p <> 0 & p is prime & k mod i*p < p implies k mod i*p = k mod p
proof
  assume
A1: i*p <> 0 & p is prime & k mod i*p < p;
  set T = k mod i*p;
  consider n being Nat such that
A2: k = (i*p)*n + T & T < i*p by A1,NAT_D:def 2;
  k = p*(i*n) + T by A2;
  then k mod p = T mod p by NAT_D:21
    .= T by A1,NAT_D:24;
  hence thesis;
end;

theorem Th10:
  (a*p + 1) mod p = 1 mod p
proof
  per cases;
  suppose
A1: p <> 0;
    reconsider p as Integer;
    (a*p + 1) mod p = (a*p + 1) - ((a*p + 1) div p)*p by A1,INT_1:def 8
      .= (a*p + 1) - ([\(a*p/p)+(1/p)/])*p by XCMPLX_1:63
      .= (a*p + 1) - ([\(a)+(1/p)/])*p by A1,XCMPLX_1:90
      .= (a*p + 1) - (a+[\(1/p)/])*p by INT_1:51
      .= 1 - (1 div p)*p;
    hence thesis by A1,INT_1:def 8;
  end;
  suppose p = 0;
    hence (a*p + 1) mod p = 1 mod p;
  end;
end;

theorem Th11:
  1 < m & (n*k) mod m = k mod m & k,m are_relative_prime implies n mod m = 1
proof
  assume
A1: 1 < m & (n*k) mod m = k mod m & k,m are_relative_prime;
  then consider t1 being Nat such that
A2: n*k = m*t1 + ((n*k) mod m) & ((n*k) mod m) < m by NAT_D:def 2;
  consider t2 being Nat such that
A3: k = m*t2 + (k mod m) & (k mod m) < m by A1,NAT_D:def 2;
  n*k - 1*k = m*(t1 - t2) by A1,A2,A3;
  then
A4: m divides k*(n - 1) by INT_1:def 9;
  reconsider n,m as Integer;
  m divides (n - 1) by A1,A4,INT_2:40;
  then consider tt being Integer such that
A5: n - 1 = m*tt by INT_1:def 9;
  n = m*tt + 1 by A5;
  then n mod m = (1 qua Integer) mod m by EULER_1:13
    .= 1 by A1,NAT_D:14;
  hence thesis;
end;

theorem Th12:
  (p |^ k) mod m = ((p mod m) |^ k) mod m
proof
  defpred P[Nat] means (p |^ $1) mod m = ((p mod m) |^ $1) mod m;
A1: P[0]
  proof
    (p |^ 0) mod m = 1 mod m by NEWTON:9;
    hence thesis by NEWTON:9;
  end;
A2: for n st P[n] holds P[n+1]
  proof
    let n;
    assume
A3: P[n];
    (p |^ (n+1)) mod m = ((p |^ n)*(p |^ 1)) mod m by NEWTON:13
      .= ((p |^ n)*p) mod m by NEWTON:10
      .= (((p |^ n) mod m)*(p mod m)) mod m by EULER_2:11
      .= (((p mod m) |^ n)*(p mod m)) mod m by A3,EULER_2:10
      .= ((p mod m) |^ (n + 1)) mod m by NEWTON:11;
    hence thesis;
  end;
  for n holds P[n] from NAT_1:sch 2(A1,A2);
  hence thesis;
end;

Lm5: k*(2 |^ (n+1)) div 2 = k*(2 |^ n)
proof
  k*(2 |^ (n+1)) = k*(2 |^ n*2) by NEWTON:11
    .= k*2 |^ n*2;
  hence thesis by NAT_D:20;
end;

Lm6: k <= n implies m |^ k divides m |^ n
proof
  assume k <= n;
  then consider t being Nat such that
A1: n = k + t by NAT_1:10;
  m |^ n = (m |^ k)*(m |^ t) by A1,NEWTON:13;
  hence thesis by NAT_D:def 3;
end;

Lm7: 2 |^ 2 = 4
proof
  2 |^ 2 = 2 |^ (1+1)
    .= 2 |^ 1 * 2 by NEWTON:11
    .= 2 * 2 by NEWTON:10;
  hence thesis;
end;

Lm8: 2 |^ 3 = 8
proof
  2 |^ 3 = 2 |^ (2+1)
    .= 4 * 2 by Lm7,NEWTON:11;
  hence thesis;
end;

Lm9: 2 |^ 4 = 16
proof
  2 |^ 4 = 2 |^ (2+2)
    .= 4 * 4 by Lm7,NEWTON:13;
  hence thesis;
end;

Lm10: 2 |^ 8 = 256
proof
  2 |^ 8 = 2 |^ (4+4)
    .= 16*16 by Lm9,NEWTON:13;
  hence thesis;
end;

theorem
  i <> 0 implies i^2 mod (i+1) = 1
proof
  assume
A1: i <> 0;
  then i > 0;
  then
A2: i+1 > 0+1 by XREAL_1:8;
  i >= 1 by A1,NAT_1:14;
  then i-1 >= 1-1 by XREAL_1:11;
  then reconsider I = i-1 as Element of NAT by INT_1:16;
  reconsider II = (i+1)*I as Element of NAT;
  i^2 mod (i+1) = (II + 1) mod (i+1)
    .= (II mod (i+1) + 1) mod (i+1) by NAT_D:22
    .= (0 + 1) mod (i+1) by NAT_D:13
    .= 1 by A2,NAT_D:24;
  hence thesis;
end;

theorem
  k^2 < j & i mod j = k implies i^2 mod j = k^2
proof
  assume
A1: k^2 < j & i mod j = k;
  then consider n being Nat such that
A2: i = j*n + k & k < j by NAT_D:def 2;
  i^2 = j*((n*j*n) + (k*n) + (n*k)) + k^2 by A2;
  then i^2 mod j = k^2 mod j by NAT_D:21;
  hence thesis by A1,NAT_D:24;
end;

theorem
  p is prime & i mod p = -1 implies i^2 mod p = 1
proof
  assume
A1: p is prime & i mod p = -1;
  then
A2: p > 1 by INT_2:def 5;
  p <> 0 by A1,INT_2:def 5;
  then i mod p = i - (i div p)*p by INT_1:def 8;
  then i = (i div p)*p - 1 by A1;
  then
A3: i^2 = ((((i div p)*p) - 2)*(i div p))*p + 1;
  i^2 mod p = 1 mod p by A3,Th10
    .= 1 by A2,NAT_D:24;
  hence thesis;
end;

theorem Th16:
  n is even implies n + 1 is odd
proof
  assume n is even;
  then n mod 2 = 0 by NAT_2:23;
  then consider k being Nat such that
A1: n = 2*k + 0 & 0 < 2 by NAT_D:def 2;
  (n + 1) mod 2 = 1 mod 2 by A1,NAT_D:21
    .= 1 by NAT_D:14;
  hence thesis by NAT_2:24;
end;

theorem
  for p being Nat holds p > 2 & p is prime implies p is odd
proof
  let p be Nat;
  assume
A1: p > 2 & p is prime;
  assume p is even;
  then p mod 2 = 0 by NAT_2:23;
  then consider k being Nat such that
A2: p = 2*k + 0 & 0 < 2 by NAT_D:def 2;
  2 divides p by A2,NAT_D:def 3;
  hence contradiction by A1,INT_2:def 5;
end;

theorem
  n > 0 implies 2 to_power(n) is even
proof
  assume n > 0;
  then 2 to_power(n) mod 2 = 0 by NAT_2:19;
  hence thesis by NAT_2:23;
end;

theorem
  i is odd & j is odd implies i*j is odd;

theorem
  i is odd implies i |^ k is odd
proof
  assume
A1: i is odd;
A2: i |^ 0 = 1 by NEWTON:9;
  defpred P[Nat] means i |^ $1 is odd;
  1 mod 2 = 1 by NAT_D:24;
  then
A3: P[0] by A2,NAT_2:24;
A4: for n holds P[n] implies P[n+1]
  proof
    let n;
    assume
A5: i |^ n is odd;
    i |^ (n+1) = i |^ n * i by NEWTON:11;
    hence thesis by A1,A5;
  end;
  for n holds P[n] from NAT_1:sch 2(A3,A4);
  hence thesis;
end;

theorem Th21:
  k > 0 & i is even implies i |^ k is even
proof
  assume
A1: k > 0 & i is even;
  defpred P[Nat] means $1 > 0 & i is even implies i |^ $1 is even;
A2: P[0];
A3: for n holds P[n] implies P[n+1]
  proof
    let n;
    assume
 P[n];
    P[n+1]
    proof
      now per cases;
        suppose n = 0;
          hence thesis by NEWTON:10;
        end;
        suppose n <> 0;
          (i |^ n)*i is even by A1;
          hence thesis by NEWTON:11;
        end;
      end;
      hence thesis;
    end;
    hence thesis;
  end;
  for n holds P[n] from NAT_1:sch 2(A2,A3);
  hence thesis by A1;
end;

theorem
  2 divides n iff n is even
proof
A1: 2 divides n implies n is even
  proof
    assume 2 divides n;
    then consider k being Nat such that
A2: n = 2*k by NAT_D:def 3;
    thus thesis by A2;
  end;
  n is even implies 2 divides n
  proof
    assume n is even;
    then n mod 2 = 0 by NAT_2:23;
    then consider k being Nat such that
A3: n = 2*k + 0 & 0 < 2 by NAT_D:def 2;
    thus thesis by A3,NAT_D:def 3;
  end;
  hence thesis by A1;
end;

theorem
  m*n is even implies m is even or n is even;

theorem Th24:
  n |^ 2 = n^2
proof
  n |^ 2 = n |^ (1 + 1)
    .= n |^ 1*n |^ 1 by NEWTON:13
    .= n*n |^ 1 by NEWTON:10
    .= n^2 by NEWTON:10;
  hence thesis;
end;

canceled;

theorem Th26:
  m > 1 & n > 0 implies m |^ n > 1
proof
  assume
A1: m > 1 & n > 0;
  defpred P[Nat] means $1 > 0 implies m |^ $1 > 1;
A2: P[0];
A3: for n holds P[n] implies P[n+1]
  proof
    let n;
    assume
A4: P[n];
A5: m |^ (n+1) = (m |^ n)*(m |^ 1) by NEWTON:13
      .= (m |^ n)*m by NEWTON:10;
    P[n+1]
    proof
      now per cases;
        suppose n = 0;
          hence thesis by A1,NEWTON:10;
        end;
        suppose n <> 0;
          then (m |^ n)*m > 1*m by A1,A4,XREAL_1:70;
          hence thesis by A1,A5,XXREAL_0:2;
        end;
      end;
      hence thesis;
    end;
    hence thesis;
  end;
  for n holds P[n] from NAT_1:sch 2(A2,A3);
  hence thesis by A1;
end;

Lm12: (2 |^ (2 |^ n))^2 = (2 |^ (2 |^ (n + 1)))
proof
  defpred P[Nat] means (2 |^ (2 |^ $1))^2 = (2 |^ (2 |^ ($1 + 1)));
A1: P[0]
  proof
A2: 2 |^ 0 = 1 by NEWTON:9;
    2 |^ (0 + 1) = 2 by NEWTON:10;
    then 2 |^ (2 |^ (0 + 1)) = 2 to_power 2
      .= 2^2 by POWER:53;
    hence thesis by A2,NEWTON:10;
  end;
A3: for k st P[k] holds P[k+1]
  proof
    let x;
    assume
A4: (2 |^ (2 |^ x))^2 = (2 |^ (2 |^ (x + 1)));
    (2 |^ (2 |^ (x + 1)))^2 = (2 |^ ((2 |^ x)*2))^2 by NEWTON:11
      .= ((2 |^ (2 |^ x)) |^ 2)^2 by NEWTON:14
      .= ((2 |^ (2 |^ x))^2)^2 by Th24
      .= (2 |^ (2 |^ (x + 1))) |^ 2 by A4,Th24
      .= 2 |^ ((2 |^ (x + 1))*2) by NEWTON:14
      .= 2 |^ (2 |^ ((x + 1) + 1)) by NEWTON:11;
    hence thesis;
  end;
  for k holds P[k] from NAT_1:sch 2(A1,A3);
  hence thesis;
end;

theorem Th27:
  n <> 0 & p <> 0 implies n |^ p = n*(n |^ (p -'1))
proof
  assume
A1: n <> 0 & p <> 0;
  defpred P[Nat] means n <> 0 & $1 <> 0 implies n |^ $1 = n*(n |^ ($1 -' 1));
A2: P[0];
A3: for m holds P[m] implies P[m+1]
  proof
    let m;
    assume P[m];
    now per cases;
      suppose
A4:     m = 0;
        n*(n |^ ((0+1) -' 1)) = n*(n |^ 0) by BINARITH:51
          .= n*1 by NEWTON:9;
        hence thesis by A4,NEWTON:10;
      end;
      suppose
A5:     m <> 0;
        n |^ (m+1) = (n |^ m) * n by NEWTON:11
          .= (n |^ (m -' 1 + 1))*n by A5,BINARITH:53,NAT_1:14
          .= n*(n |^ ((m + 1) -' 1)) by A5,Lm4;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
  for m holds P[m] from NAT_1:sch 2(A2,A3);
  hence thesis by A1;
end;

theorem Th28:
  for n,m st m mod 2 = 0 holds (n |^ (m div 2))^2 = n |^ m
proof
  let n,m;
  assume
A1: m mod 2 = 0;
  (n |^ (m div 2))^2 = n |^ ((m div 2) + (m div 2)) by NEWTON:13
    .= n |^ ((m + m) div 2) by A1,NAT_D:19
    .= n |^ ((2*m) div 2)
    .= n |^ m by NAT_D:18;
  hence thesis;
end;

theorem Th29:
  n <> 0 & 1 <= k implies (n |^ k) div n = n |^ (k -'1)
proof
  assume
A1: n <> 0 & 1 <= k;
  then
A2: k - 1 >= 1 - 1 by XREAL_1:11;
  k = k - 1 + 1
    .= (k -'1) + 1 by A2,BINARITH:def 3;
  then n |^ k = n*(n |^ (k -'1)) by NEWTON:11;
  hence thesis by A1,NAT_D:18;
end;

theorem
  2 |^ (n + 1) = (2 |^ n)+(2 |^ n)
proof
  defpred P[Nat] means 2 |^ ($1+1) = (2 |^ $1)+(2 |^ $1);
A1: P[0]
  proof
    2 |^ (0+1) = 1 + 1 by NEWTON:10
      .= (2 |^ 0)+1 by NEWTON:9
      .= (2 |^ 0)+(2 |^ 0) by NEWTON:9;
    hence thesis;
  end;
A2: for m st P[m] holds P[m+1]
  proof
    let m;
    assume
A3: 2 |^ (m+1) = (2 |^ m)+(2 |^ m);
    (2 |^ (m+1))+(2 |^ (m+1)) = 2*(2 |^ m)+(2 |^ (m+1)) by NEWTON:11
      .= 2*(2 |^ m)+2*(2 |^ m) by NEWTON:11
      .= 2*((2 |^ m)+(2 |^ m))
      .= 2 |^ ((m+1)+1) by A3,NEWTON:11;
    hence thesis;
  end;
  for n holds P[n] from NAT_1:sch 2(A1,A2);
  hence thesis;
end;

theorem Th31:
  k > 1 & k |^ n = k |^ m implies n = m
proof
  assume
A1: k > 1 & k |^ n = k |^ m;
  now per cases;
    suppose n = m;
      hence thesis;
    end;
    suppose n <> m;
      then k to_power m <> k to_power n by A1,POWER:57;
      hence thesis by A1;
    end;
  end;
  hence thesis;
end;

Lm13: k > n & x > 1 implies x |^ k > x |^ n
proof
  assume that
A1: k>n and
A2: x > 1;
  consider m be Nat such that
A3: k = n + m by A1,NAT_1:10;
  m + n - n > n - n by A1,A3,XREAL_1:11;
  then m + 1 - 1 > 1 - 1;
  then m + 1 > 1 by XREAL_1:11;
  then
A4: m >= 1 by NAT_1:13;
A5: x |^ k = x |^ n * x |^ m by A3,NEWTON:13;
A6: x |^ n >= 1 by A2,PREPOWER:19;
  1 |^ m = 1 by NEWTON:15;
  then x |^ m > 1 by A2,A4,PREPOWER:18;
  hence thesis by A5,A6,REAL_2:144;
end;

Lm14: 2 |^ m divides 2 |^ n implies m <= n
proof
  not(m <= n) implies not(2 |^ m divides 2 |^ n)
  proof
    assume
A1: not(m <= n);
    not (2 |^ m divides 2 |^ n)
    proof
      assume
A2:   2 |^ m divides 2 |^ n;
      (2 |^ n) div (2 |^ m) = 0 by A1,Lm13,NAT_D:27;
      then 2 |^ n > 2 |^ m * ((2 |^ n) div (2 |^ m)) by PREPOWER:13;
      hence contradiction by A2,NAT_D:3;
    end;
    hence thesis;
  end;
  hence thesis;
end;

theorem
  m <= n iff 2 |^ m divides 2 |^ n by Lm6,Lm14;

theorem Th33:
  p is prime & i divides p |^ n implies i = 1
  or (ex k being Element of NAT st i = p*k)
proof
  assume
A1: p is prime;
  then
A2: p <> 0 by INT_2:def 5;
  defpred P[Nat] means
  i divides p |^ $1 implies i = 1 or (ex k being Element of NAT st i = p*k);
A3: P[0]
  proof
    now
      assume i divides p |^ 0;
      then i divides 1 by NEWTON:9;
      then consider t being Nat such that
A4:   1 = i*t by NAT_D:def 3;
      thus thesis by A4,NAT_1:15;
    end;
    hence thesis;
  end;
A5: for n holds P[n] implies P[n+1]
  proof
    let n;
    assume
A6: P[n];
    now
      assume i divides p |^ (n+1);
      then consider u1 being Nat such that
A7:   p |^ (n+1) = i*u1 by NAT_D:def 3;
      p*(p |^ n) = i*u1 by A7,NEWTON:11;
      then
A8:   p divides i*u1 by NAT_D:def 3;
      now per cases by A1,A8,NEWTON:98;
        suppose p divides i;
          then consider w1 being Nat such that
A9:       i = p*w1 by NAT_D:def 3;
          w1 in NAT by ORDINAL1:def 13;
          hence thesis by A9;
        end;
        suppose p divides u1;
          then consider w2 being Nat such that
A10:      u1 = p*w2 by NAT_D:def 3;
          p*(p |^ n) = p*(i*w2) by A7,A10,NEWTON:11;
          then p |^ n = p*(i*w2) div p by A2,NAT_D:18;
          then p |^ n = i*w2 by A2,NAT_D:18;
          then consider w3 being Element of NAT such that
A11:      i = p*w3 or i = 1 by A6,NAT_D:def 3;
          thus thesis by A11;
        end;
      end;
      hence thesis;
    end;
    hence thesis;
  end;
  for n holds P[n] from NAT_1:sch 2(A3,A5);
  hence thesis;
end;

theorem Th34:
  for n st n <> 0 & p is prime & n < p |^ (k+1) holds
  n divides p |^ (k+1) iff n divides p |^ k
proof
  let n;
  assume
A1: n <> 0 & p is prime & n < p |^ (k+1);
  then
A2: p <> 0 by INT_2:def 5;
A3: n divides p |^ (k+1) implies n divides p |^ k
  proof
    assume
A4: n divides p |^ (k+1);
    now per cases by A1,A4,Th33;
      suppose n = 1;
        hence thesis by NAT_D:6;
      end;
      suppose ex i being Element of NAT st n = p*i;
        then consider i being Element of NAT such that
A5:     n = p*i;
        consider u being Nat such that
A6:     p |^ (k+1) = (p*i)*u by A4,A5,NAT_D:def 3;
        p*(p |^ k) = p*(i*u) by A6,NEWTON:11;
        then
A7:     p |^ k = p*(i*u) div p by A2,NAT_D:18;
        then
A8:     p |^ k = i*u by A2,NAT_D:18;
        then
A9:     u divides p |^ k by NAT_D:def 3;
        u <> 1 by A1,A5,A8,NEWTON:11;
        then consider j being Element of NAT such that
A10:    u = p*j by A1,A9,Th33;
        p |^ k = n*j by A2,A5,A7,A10,NAT_D:18;
        hence thesis by NAT_D:def 3;
      end;
    end;
    hence thesis;
  end;
  n divides p |^ k implies n divides p |^ (k+1)
  proof
    assume n divides p |^ k;
    then n divides (p |^ k)*p by NAT_D:9;
    hence thesis by NEWTON:11;
  end;
  hence thesis by A3;
end;

theorem Th35:
  for k holds p is prime & d divides (p |^ k) & d <> 0
  implies ex t being Element of NAT st d = p |^ t & t <= k
proof
  let n;
  assume
A1: p is prime & d divides (p |^ n) & d <> 0;
  then
A2: p > 0 by INT_2:def 5;
  defpred P[Nat] means d divides (p |^ $1) implies
  ex t being Element of NAT st d = p |^ t & t <= $1;
A3: P[0]
  proof
    assume
A4: d divides (p |^ 0);
    d = p |^ 0
    proof
      consider t being Nat such that
A5:   p |^ 0 = d*t by A4,NAT_D:def 3;
      d = 1 by A5,NAT_1:15,NEWTON:9;
      hence thesis by NEWTON:9;
    end;
    hence thesis;
  end;
A6: for n st P[n] holds P[n+1]
  proof
    let n;
    assume
A7: P[n];
    now
      assume
A8:   d divides (p |^ (n+1));
      (p |^ (n+1)) > 0 by A2,PREPOWER:13;
      then
A9:   d <= p |^ (n+1) by A8,NAT_D:7;
      now per cases by A9,XXREAL_0:1;
        suppose d < p |^ (n+1);
          then consider t being Element of NAT such that
A10:      d = p |^ t & t <= n by A1,A7,A8,Th34;
          thus thesis by A10,NAT_1:12;
        end;
        suppose d = p |^ (n+1);
          hence thesis;
        end;
      end;
      hence thesis;
    end;
    hence thesis;
  end;
  for n holds P[n] from NAT_1:sch 2(A3,A6);
  hence thesis by A1;
end;

theorem Th36:
  p > 1 & i mod p = 1 implies (i |^ n) mod p = 1
proof
  assume
A1: p > 1 & i mod p = 1;
  defpred P[Nat] means (i |^ $1) mod p = 1;
  1 mod p = 1 by A1,NAT_D:24;
  then
A2: P[0] by NEWTON:9;
A3: for k holds P[k] implies P[k+1]
  proof
    let k;
    assume
A4: (i |^ k) mod p = 1;
    (i |^ (k + 1)) mod p = ((i |^ k)*i) mod p by NEWTON:11
      .= (((i |^ k) mod p)*i) mod p by EULER_2:10
      .= 1 by A1,A4;
    hence thesis;
  end;
  for n holds P[n] from NAT_1:sch 2(A2,A3);
  hence thesis;
end;

theorem Th37:
  for m,n being natural number holds m > 0 implies (n |^ m) mod n = 0
proof
  let m,n be natural number;
  assume
A1: m > 0;
  defpred P[natural number] means $1 > 0 implies (n |^ $1) mod n = 0;
A2: P[0];
A3: for m being natural number holds P[m] implies P[m+1]
  proof
    let m be natural number;
    assume P[m];
    P[m+1]
    proof
      reconsider m,n as Element of NAT by ORDINAL1:def 13;
      n*(n |^ m) mod n = ((n mod n)*(n |^ m)) mod n by EULER_2:10
        .= (0*(n |^ m)) mod n by NAT_D:25
        .= 0 by NAT_D:26;
      hence thesis by NEWTON:11;
    end;
    hence thesis;
  end;
  for n being natural number holds P[n] from NAT_1:sch 2(A2,A3);
  hence thesis by A1;
end;

theorem Th38:
  for p being Nat holds p is prime & n,p are_relative_prime
  implies (n |^ (p -'1)) mod p = 1
proof
  let p be Nat;
  assume
A1: p is prime & n,p are_relative_prime;
  then
A2: p > 1 by INT_2:def 5;
A3: p <> 0 by A1,INT_2:def 5;
A4: n <> 0
  proof
    assume n = 0;
    then n hcf p = p by NEWTON:65;
    then n hcf p > 1 by A1,INT_2:def 5;
    hence contradiction by A1,INT_2:def 4;
  end;
  then (n |^ p) mod p = n mod p by A1,EULER_2:34;
  then
A5: (n |^ (p -' 1))*n mod p = n mod p by A3,A4,Th27;
  (n |^ (p -'1)) <> 0 by A4,PREPOWER:12;
  hence thesis by A1,A2,A4,A5,EULER_2:27;
end;

theorem Th39:
  p is prime & d > 1 & d divides (p |^ k) & not d divides ((p |^ k) div p)
  implies d = p |^ k
proof
  assume
A1: p is prime & d > 1 & d divides (p |^ k) & not d divides ((p |^ k) div p);
  then
A2: p <> 0 by INT_2:def 5;
A3: k <> 0
  proof
    assume k = 0;
    then p |^ k = 1 by NEWTON:9;
    hence contradiction by A1,NAT_D:7;
  end;
  then k >= 1 by NAT_1:14;
  then
A4: k - 1 >= 1 - 1 by XREAL_1:11;
  consider t being Element of NAT such that
A5: d = p |^ t & t <= k by A1,Th35;
  not t < k
  proof
    assume t < k;
    then t < k + -1 + 1;
    then t < k -'1 + 1 by A4,BINARITH:def 3;
    then t <= k -'1 by NAT_1:13;
    then d divides p |^ (k -'1) by A5,Lm6;
    hence contradiction by A1,A2,A3,Th29,NAT_1:14;
  end;
  hence thesis by A5,XXREAL_0:1;
end;

definition
  let a be Integer;
  redefine func a^2 -> Element of NAT;
  coherence by INT_1:16,XREAL_1:65;
end;

theorem Th40:
  for n st n > 1 holds m mod n = 1 iff m,1 are_congruent_mod n
proof
  let n;
  assume
A1: n > 1;
A2: m mod n = 1 implies m,1 are_congruent_mod n
  proof
    assume m mod n = 1;
    then consider k being Nat such that
A3: m = n*k + 1 & 1 < n by NAT_D:def 2;
    n*k = m - 1 by A3;
    hence thesis by INT_1:def 3;
  end;
  m,1 are_congruent_mod n implies m mod n = 1
  proof
    assume m,1 are_congruent_mod n;
    then consider t being Integer such that
A4: n*t = m - 1 by INT_1:def 3;
    reconsider m,n as Integer;
    m mod n = (n*t + 1) mod n by A4
      .= 1 mod n by EULER_1:13;
    hence thesis by A1,NAT_D:14;
  end;
  hence thesis by A2;
end;

Lm15: n > 1 & m > 1 & m,1 are_congruent_mod n implies m mod n = 1
proof
  assume
A1: n > 1 & m > 1 & m,1 are_congruent_mod n;
  then consider d being Integer such that
A2: n*d = m - 1 by INT_1:def 3;
  m - 1 > 1 - 1 by A1,XREAL_1:11;
  then d > 0 by A2;
  then reconsider d as Element of NAT by INT_1:16;
  m = n*d + 1 by A2;
  then m mod n = 1 mod n by NAT_D:21
    .= 1 by A1,NAT_D:14;
  hence thesis;
end;

canceled 2;

theorem Th43:
  i1,i2 are_congruent_mod i5 & i1,i3 are_congruent_mod i5
  implies i2,i3 are_congruent_mod i5
proof
  assume i1,i2 are_congruent_mod i5 & i1,i3 are_congruent_mod i5;
  then
A1: i2,i1 are_congruent_mod i5 & i3,i1 are_congruent_mod i5 by INT_1:35;
  then consider t1 being Integer such that
A2: i5*t1 = i2 - i1 by INT_1:def 3;
  consider t2 being Integer such that
A3: i5*t2 = i3 - i1 by A1,INT_1:def 3;
  i2 - i3 = i5*(t1 - t2) by A2,A3;
  hence thesis by INT_1:def 3;
end;

theorem Th44:
  3 is prime
proof
  for n being natural number holds n divides 3 implies n = 1 or n = 3
  proof
    let n be natural number;
    assume
A1: n divides 3;
    then
A2: n <> 0 by INT_2:3;
    n <= 3 by A1,NAT_D:7;
    then n < 2 + 1 or n = 3 by XXREAL_0:1;
    then n <= 2 or n = 3 by NAT_1:13;
    then n < 1 + 1 or n = 2 or n = 3 by XXREAL_0:1;
    then n <= 1 or n = 2 or n = 3 by NAT_1:13;
    then
A3: n < 1 or n = 1 or n = 2 or n = 3 by XXREAL_0:1;
    n <> 2
    proof
      assume
A4:   n = 2;
      3 mod 2 = (2*1 + 1) mod 2
        .= 1 mod 2 by NAT_D:21
        .= 1 by NAT_D:24;
      hence contradiction by A1,A4,Th6;
    end;
    hence thesis by A2,A3,NAT_1:14;
  end;
  hence thesis by INT_2:def 5;
end;

theorem Th45:
  n <> 0 implies Euler n <> 0
proof
  assume
A1: n <> 0;
  assume
A2: Euler n = 0;
  set X = {k where k is Element of NAT :
  n,k are_relative_prime & k >= 1 & k <= n};
A3: Card X = 0 by A2,EULER_1:def 1;
  X c= finSeg n
  proof
    let x be set;
    assume x in X;
    then consider xx being Element of NAT such that
A4: x = xx and n,xx are_relative_prime and
A5: xx >= 1 & xx <= n;
    thus thesis by A4,A5,FINSEQ_1:3;
  end;
  then reconsider X as finite set;
  ex k st k in X
  proof
A6: 1 in X
    proof
      n hcf 1 = 1 by NEWTON:64;
      then
A7:   n,1 are_relative_prime by INT_2:def 4;
      1 <= n by A1,NAT_1:14;
      hence thesis by A7;
    end;
    take 1;
    thus thesis by A6;
  end;
  hence contradiction by A3;
end;

theorem
  n <> 0 implies -n < n;

canceled;

theorem Th48:
  n <> 0 implies n div n = 1
proof
  assume n <> 0;
  then n*1 div n = 1 by NAT_D:18;
  hence thesis;
end;

begin :: Public Key Cryptography

definition
  let k,m,n;
  func Crypto(m,n,k) -> Element of NAT equals

  (m |^ k) mod n;
  coherence;
end;

theorem
  p is prime & q is prime & p <> q & n = p*q &
  k1,Euler(n) are_relative_prime & (k1*k2) mod Euler(n) = 1
  implies for m be Element of NAT st m < n
  holds Crypto(Crypto(m,n,k1),n,k2) = m
proof
  assume
A1: p is prime & q is prime & p <> q & n = p*q &
  k1,Euler(n) are_relative_prime & (k1*k2) mod Euler(n) = 1;
  let m be Element of NAT;
  assume
A2: m < n;
A3: p <> 0 & q <> 0 by A1,INT_2:def 5;
  then reconsider p1 = p-1,q1 = q-1 as Element of NAT by Lm3,INT_1:16;
A4: p > 1 & q > 1 by A1,INT_2:def 5;
A5: n <> 0 by A1,A3,XCMPLX_1:6;
A6: p > 0 & q > 0 by A1,INT_2:def 5;
  Euler n <> 0 by A5,Th45;
  then
A7: (Euler n) > 0;
A8: Euler(n)= Euler(p)*Euler(q) by A1,A4,EULER_1:22,INT_2:47
    .= (p-1)*Euler(q) by A1,EULER_1:21
    .= (p-1)*(q-1) by A1,EULER_1:21;
  not(p1=1 & q1=1) by A1;
  then
A9: p1*q1 <> 1 by NAT_1:15;
A10: k1 <> 0
  proof
    assume k1 = 0;
    then k1 hcf Euler n = Euler n by NEWTON:65;
    hence contradiction by A1,A8,A9,INT_2:def 4;
  end;
A11: k2 <> 0 by A1,NAT_D:26;
A12: n > 1 by A1,A4,REAL_2:137;
  now per cases;
    suppose
A13:  m = 0;
      then m |^ k1 = 0 by A10,NAT_1:14,NEWTON:16;
      then m |^ k1 mod n = 0 by NAT_D:26;
      then Crypto(m,n,k1) |^ k2 = 0 by A11,NAT_1:14,NEWTON:16;
      hence thesis by A13,NAT_D:26;
    end;
    suppose
A14:  m = 1;
      then m |^ k1 = 1 by NEWTON:15;
      then m |^ k1 mod n = 1 by A12,NAT_D:14;
      then Crypto(m,n,k1) |^ k2 = 1 by NEWTON:15;
      hence thesis by A12,A14,NAT_D:14;
    end;
    suppose
A15:  m <> 0 & m <> 1;
A16:  for t being Element of NAT holds m |^ (t*p1*q1 + 1) mod n = m
      proof
        let t be Element of NAT;
        now
          m |^ (t*p1*q1) >= 1 by A15,NAT_1:14,PREPOWER:19;
          then (m |^ (t*p1*q1)) - 1 >= 1 - 1 by XREAL_1:11;
          then reconsider a = m |^ (t*p1*q1) -1 as Element of NAT by INT_1:16;
A17:      q divides (m*a)
          proof
            now per cases;
              suppose m hcf q = 1;
                then
A18:            m,q are_relative_prime by INT_2:def 4;
                m |^ (t*p1) <> 0 by A15,PREPOWER:12;
                then ((m |^ (t*p1)) |^ (q1+1)) mod q = (m |^ (t*p1)) mod q
                by A1,A15,A18,EULER_2:32,34;
                then (((m |^ (t*p1)) |^ q1)*(m |^ (t*p1))) mod q
                = (m |^ (t*p1)) mod q by NEWTON:11;
                then ((m |^ (t*p1)) |^ q1) mod q = 1 by A4,A15,A18,Th11,
                EULER_2:32;
                then (m |^ (t*p1*q1)) mod q = 1 by NEWTON:14;
                then (m |^ (t*p1*q1)) = q*((m |^ (t*p1*q1)) div q) + 1
                by A6,NAT_D:2;
                then q divides a by NAT_D:def 3;
                hence thesis by NAT_D:9;
              end;
              suppose m hcf q <> 1;
                then not m,q are_relative_prime by INT_2:def 4;
                then m hcf q = q by A1,Th2;
                then q divides m by NAT_D:def 5;
                hence thesis by NAT_D:9;
              end;
            end;
            hence thesis;
          end;
          p divides (m*a)
          proof
            now per cases;
              suppose m hcf p = 1;
                then
A19:            m,p are_relative_prime by INT_2:def 4;
                m |^ (t*q1) <> 0 by A15,PREPOWER:12;
                then ((m |^ (t*q1)) |^ (p1+1)) mod p = (m |^ (t*q1)) mod p
                by A1,A15,A19,EULER_2:32,34;
                then (((m |^ (t*q1)) |^ p1)*(m |^ (t*q1))) mod p
                = (m |^ (t*q1)) mod p by NEWTON:11;
                then ((m |^ (t*q1)) |^ p1) mod p = 1 by A4,A15,A19,Th11,
                EULER_2:32;
                then (m |^ (t*q1*p1)) mod p = 1 by NEWTON:14;
                then (m |^ (t*p1*q1))
                = p*((m |^ (t*p1*q1)) div p) + 1 by A6,NAT_D:2;
                then p divides a by NAT_D:def 3;
                hence thesis by NAT_D:9;
              end;
              suppose m hcf p <> 1;
                then not m,p are_relative_prime by INT_2:def 4;
                then m hcf p = p by A1,Th2;
                then p divides m by NAT_D:def 5;
                hence thesis by NAT_D:9;
              end;
            end;
            hence thesis;
          end;
          then (p*q) divides (m*a) by A1,A17,Th4,INT_2:47;
          then consider k be Nat such that
A20:      (m*a) = (p*q)*k by NAT_D:def 3;
          m*(m |^ (t*p1*q1) -1 ) = m*(m |^ (t*p1*q1)) - m*1
            .= m |^ (t*p1*q1 + 1) - m by NEWTON:11;
          then m |^ (t*p1*q1 + 1) - (m - m) = n*k + m by A1,A20,XCMPLX_1:37;
          hence thesis by A2,NAT_D:def 2;
        end;
        hence thesis;
      end;
      reconsider t = (k1*k2) div Euler(n) as Element of NAT;
      k1*k2 = p1*q1*t + 1 by A1,A7,A8,NAT_D:2
        .= t*p1*q1 + 1;
      then m |^ (k1*k2) mod n = m by A16;
      then ((m |^ k1) |^ k2) mod n = m by NEWTON:14;
      hence thesis by Th12;
    end;
  end;
  hence thesis;
end;

begin :: Order

definition
  let i,p;
  assume
A1: p > 1 & i,p are_relative_prime;
  func order(i,p) -> Element of NAT means
  :Def2:
  it > 0 & (i |^ it) mod p = 1 & for k st k > 0 & (i |^ k) mod p = 1
  holds 0 < it & it <= k;
  existence
  proof
    set A = {k where k is Element of NAT:k > 0 & (i |^ k) mod p = 1};
A2: i <> 0
    proof
      assume i = 0;
      then i hcf p = p by NEWTON:65;
      hence contradiction by A1,INT_2:def 4;
    end;
A3: A is non empty set
    proof
A4:   (i |^ Euler p) mod p = 1 by A1,A2,EULER_2:33;
      Euler p <> 0 by A1,Th45;
      then Euler p > 0;
      then Euler p in A by A4;
      hence thesis;
    end;
    A c= NAT
    proof
      let a be set;
      assume a in A;
      then consider k being Element of NAT such that
A5:   k = a & k > 0 & (i |^ k) mod p = 1;
      thus thesis by A5;
    end;
    then reconsider A as non empty Subset of NAT by A3;
    consider a being Element of A;
    defpred P[set] means $1 in A;
    a is Element of NAT;
    then
A6: ex kk being Nat st P[kk];
    consider kk being Nat such that
A7: P[kk] and
A8: for n being Nat st P[n] holds kk <= n from NAT_1:sch 5(A6);
    consider k being Element of NAT such that
A9: k = kk & k > 0 & (i |^ k) mod p = 1 by A7;
    take kk;
    for k st k > 0 & (i |^ k) mod p = 1 holds kk <= k
    proof
      let k;
A10:  k in NAT by ORDINAL1:def 13;
      assume k > 0 & (i |^ k) mod p = 1;
      then k in A by A10;
      hence thesis by A8;
    end;
    hence thesis by A9;
  end;
  uniqueness
  proof
    let k1,k2 be Element of NAT such that
A11: k1 > 0 & (i |^ k1) mod p = 1 & for k st k > 0 & (i |^ k) mod p = 1
    holds 0 < k1 & k1 <= k and
A12: k2 > 0 & (i |^ k2) mod p = 1 & for k st k > 0 & (i |^ k) mod p = 1
    holds 0 < k2 & k2 <= k;
    k1 <= k2 & k2 <= k1 by A11,A12;
    hence thesis by XXREAL_0:1;
  end;
end;

theorem
  p > 1 implies order(1,p) = 1
proof
  assume
A1: p > 1;
  assume
A2: order(1,p) <> 1;
  p hcf 1 = 1 by NEWTON:64;
  then
A3: 1,p are_relative_prime by INT_2:def 4;
  (1 |^ 1) mod p = 1 mod p by NEWTON:10
    .= 1 by A1,NAT_D:24;
  then order(1,p) <= 1 by A1,A3,Def2;
  then order(1,p) < 1 or order(1,p) = 1 by XXREAL_0:1;
  then order(1,p) = 0 or order(1,p) = 1 by NAT_1:14;
  hence contradiction by A1,A2,A3,Def2;
end;

canceled;

theorem Th52:
  p > 1 & n > 0 & (i |^ n) mod p = 1 & i,p are_relative_prime
  implies order(i,p) divides n
proof
  assume
A1: p > 1 & n > 0 & (i |^ n) mod p = 1 & i,p are_relative_prime;
  then
A2: (i |^ order(i,p)) mod p = 1 by Def2;
A3: order(i,p) <> 0 by A1,Def2;
  n mod order(i,p) = 0
  proof
    assume
A4: n mod order(i,p) <> 0;
    set I = n mod order(i,p);
    consider t being Nat such that
A5: n = order(i,p)*t + I & I < order(i,p) by A3,NAT_D:def 2;
    reconsider t as Element of NAT by ORDINAL1:def 13;
    (i |^ (order(i,p)*t))*(i |^ I) mod p = 1 by A1,A5,NEWTON:13;
    then ((i |^ order(i,p)) |^ t)*(i |^ I) mod p = 1 by NEWTON:14;
    then ((((i |^ order(i,p)) |^ t) mod p)*(i |^ I)) mod p = 1 by EULER_2:10;
    then
A6: (1*(i |^ I)) mod p = 1 by A1,A2,Th36;
    I > 0 by A4;
    hence contradiction by A1,A5,A6,Def2;
  end;
  hence thesis by A3,Th6;
end;

theorem Th53:
  p > 1 & i,p are_relative_prime & order(i,p) divides n implies
  (i |^ n) mod p = 1
proof
  assume
A1: p > 1 & i,p are_relative_prime & order(i,p) divides n;
  then consider t being Nat such that
A2: n = order(i,p)*t by NAT_D:def 3;
  reconsider t as Element of NAT by ORDINAL1:def 13;
  (i |^ n) mod p = ((i |^ order(i,p)) |^ t) mod p by A2,NEWTON:14
    .= (((i |^ order(i,p)) mod p) |^ t ) mod p by Th12
    .= ((1) |^ t) mod p by A1,Def2
    .= 1 mod p by NEWTON:15
    .= 1 by A1,NAT_D:24;
  hence thesis;
end;

theorem Th54:
  p is prime & i,p are_relative_prime implies order(i,p) divides (p -'1)
proof
  assume
A1: p is prime & i,p are_relative_prime;
  then
A2: (i |^ (p -'1)) mod p = 1 by Th38;
A3: p > 1 by A1,INT_2:def 5;
  then p - 1 > 1 - 1 by XREAL_1:11;
  then p -'1 > 0 by BINARITH:def 3;
  hence thesis by A1,A2,A3,Th52;
end;

begin :: Fermat Number

definition
  let n be Nat;
  func Fermat(n) -> Element of NAT equals

  2 |^ (2 |^ n) + 1;
  correctness;
end;

theorem Th55:
  Fermat(0) = 3
proof
  Fermat(0) = 2 |^ 1 + 1 by NEWTON:9
    .= 2 + 1 by NEWTON:10;
  hence thesis;
end;

theorem Th56:
  Fermat(1) = 5
proof
  Fermat(1) = 2 |^ (1 + 1) + 1 by NEWTON:10
    .= 2 |^ 1 * 2 + 1 by NEWTON:11
    .= 2 * 2 + 1 by NEWTON:10;
  hence thesis;
end;

theorem Th57:
  Fermat(2) = 17
proof
  thus Fermat(2) = 2 |^ (2 |^ (1 + 1)) + 1
    .= 2 |^ (2 |^ 1 * 2) + 1 by NEWTON:11
    .= 2 |^ (2 * 2) + 1 by NEWTON:10
    .= 2 |^ (3 + 1) + 1
    .= 2 |^ (2 + 1) * 2 + 1 by NEWTON:11
    .= 2 |^ (1 + 1) * 2 * 2 + 1 by NEWTON:11
    .= 2 |^ 1 * 2 * 2 * 2 + 1 by NEWTON:11
    .= 2 * 2 * 2 * 2 + 1 by NEWTON:10
    .= 17;
end;

theorem Th58:
  Fermat(3) = 257
proof
  thus Fermat(3) = 2 |^ (4 + 4) + 1 by Lm8
    .= 2 |^ 4 * 2 |^ 4 + 1 by NEWTON:13
    .= 257 by Lm9;
end;

theorem Th59:
  Fermat(4) = 256*256+1
proof
  thus Fermat(4) = 2 |^ (8 + 8) + 1 by Lm9
    .= 2 |^ 8 * 2 |^ 8 + 1 by NEWTON:13
    .= (2 |^ 4 * 2 |^ 4)*2 |^ (4 + 4) + 1 by NEWTON:13
    .= 256*256+1 by Lm9,NEWTON:13;
end;

theorem Th60:
  Fermat(n) > 2
proof
  set 2N = 2 |^ (2 |^ n);
  2N > 1 by Lm2;
  then 2N + 1 > 1 + 1 by XREAL_1:8;
  hence thesis;
end;

Lm16: Fermat(n) > 1
proof
  Fermat(n) <> 1 by Th60;
  then Fermat(n) > 1 or Fermat(n) < 1 by XXREAL_0:1;
  hence thesis by NAT_1:14;
end;

Lm17: (Fermat(n) -'1) mod 2 = 0
proof
A1: Fermat(n) -'1 = 2 |^ (2 |^ n) by BINARITH:39;
  2 mod 2 = 0 by NAT_D:25;
  then 2 is even by NAT_2:23;
  then (Fermat(n) -'1) is even by A1,Th21,PREPOWER:13;
  hence thesis by NAT_2:23;
end;

Lm18: Fermat(n)-'1 > 0
proof
  Fermat(n) > 1 by Lm16;
  then Fermat(n) - 1 > 1 - 1 by XREAL_1:11;
  hence thesis by BINARITH:def 3;
end;

Lm19: Fermat(n) mod (2 |^ (2 |^ n)) = 1
proof
  set 2N = 2 |^ (2 |^ n);
  Fermat(n) mod 2N = (2N*1 + 1) mod 2N
    .= 1 mod 2N by NAT_D:21
    .= 1 by Lm2,NAT_D:14;
  hence thesis;
end;

Lm20: Fermat(n) is odd
proof
A1: Fermat(n) - 1 = Fermat(n) -'1 by BINARITH:def 3;
  (Fermat(n) -'1) mod 2 = 0 by Lm17;
  then (Fermat(n) -'1) is even by NAT_2:23;
  hence thesis by A1,Th16;
end;

theorem Th61:
  p is prime & p > 2 & p divides Fermat(n) implies
  ex k being Element of NAT st p = k*(2 |^ (n + 1)) + 1
proof
  assume
A1: p is prime & p > 2 & p divides Fermat(n);
  set 2N = 2 |^ (2 |^ n);
A2: p > 1 by A1,INT_2:def 5;
  set t = Fermat(n) div p;
A3: p*t = 2N + 1 by A1,NAT_D:3;
A4: 2N > 1 by Th26,PREPOWER:13;
  then (2N)*(2N) > 1*(2N) by XREAL_1:70;
  then
A5: (2N)^2 > 1 by A4,XXREAL_0:2;
A6: (-1),(-1) are_congruent_mod p by INT_1:32;
  p*t - 0 = p*t;
  then p*t,0 are_congruent_mod p by INT_1:def 3;
  then
A7: (p*t + (-1)),(0 + (-1)) are_congruent_mod p by A6,INT_1:37;
  then (2N)^2,(-1)^2 are_congruent_mod p by A3,INT_1:39;
  then (2N)^2 mod p = 1 by A2,A5,Lm15;
  then
A8: (2 |^ (2 |^ (n + 1))) mod p = 1 by Lm12;
A9: 2N mod p <> 1
  proof
    assume 2N mod p = 1;
    then 2N,1 are_congruent_mod p by A2,Th40;
    then 1,2N are_congruent_mod p by INT_1:35;
    then 1,(-1) are_congruent_mod p by A3,A7,INT_1:36;
    then p divides (1 - -1) by INT_2:19;
    hence contradiction by A1,NAT_D:7;
  end;
A10: 2,p are_relative_prime by A1,INT_2:44,47;
  then
A11: order(2,p) divides (2 |^ (n + 1)) by A2,A8,Th52,PREPOWER:13;
  order(2,p) <> 0 by A2,A10,Def2;
  then order(2,p) >= 1 by NAT_1:14;
  then
A12: order(2,p) = 1 or order(2,p) > 1 by XXREAL_0:1;
  2 |^ (n + 1) div 2 = 2*(2 |^ n) div 2 by NEWTON:11
    .= 2 |^ n by NAT_D:18;

then not order(2,p) divides (2 |^ (n + 1)) div 2 by A1,A2,A9,Th53,INT_2:44,47;
  then order(2,p) = 2 |^ (n + 1) by A11,A12,Th39,INT_2:44,NAT_D:6;
  then (2 |^ (n + 1)) divides (p -'1) by A1,Th54,INT_2:44,47;
  then consider t2 being Nat such that
A13: (p -'1) = (2 |^ (n + 1))*t2 by NAT_D:def 3;
  reconsider t2 as Element of NAT by ORDINAL1:def 13;
  p - 1 > 1 - 1 by A2,XREAL_1:11;
  then p - 1 = (2 |^ (n + 1))*t2 by A13,BINARITH:def 3;
  then p = t2*(2 |^ (n + 1)) + 1;
  hence thesis;
end;

theorem Th62:
  n <> 0 implies 3,Fermat(n) are_relative_prime
proof
  assume
A1: n <> 0;
  Fermat(n) hcf 3 <> 3
  proof
    assume Fermat(n) hcf 3 = 3;
    then 3 divides Fermat(n) by NAT_D:def 5;
    then consider k being Element of NAT such that
A2: 3 = k*(2 |^ (n+1)) + 1 by Th44,Th61;
    1 = k*(2 |^ (n+1)) div 2 by A2,NAT_2:5;
    then 1 = k*(2 |^ n) by Lm5;
    then k = 1 & 2 |^ n = 1 by NAT_1:15;
    then 2 |^ n = 2 |^ 0 by NEWTON:9;
    hence contradiction by A1,Th31;
  end;
  hence thesis by Th2,Th44;
end;

begin :: Pepin's Test

theorem Th63:
  (3 |^ ((Fermat(n)-'1) div 2)),(-1) are_congruent_mod Fermat(n)
  implies Fermat(n) is prime
proof
  per cases;
  suppose n = 0;
    hence thesis by Th44,Th55;
  end;
  suppose
A1: n > 0;
    assume
A2: (3 |^ ((Fermat(n)-'1) div 2)),(-1) are_congruent_mod Fermat(n);
    assume
A3: not Fermat(n) is prime;
    Fermat(n) > 2 by Th60;
    then consider p being Element of NAT such that
A4: p is prime and
A5: p divides Fermat(n) by INT_2:48;
    consider i being Nat such that
A6: Fermat(n) = p*i by A5,NAT_D:def 3;
    reconsider i as Element of NAT by ORDINAL1:def 13;
A7: (3 |^ ((Fermat(n)-'1) div 2)),(-1) are_congruent_mod p by A2,A6,INT_1:41;
A8: p <> 3
    proof
      assume
A9:   p = 3;
      1 hcf i = 1 by NEWTON:64;
      then 3*1 hcf 3*i = 3 by EULER_1:6;
      then not 3,Fermat(n) are_relative_prime by A6,A9,INT_2:def 4;
      hence contradiction by A1,Th62;
    end;
    then
A10: 3,p are_relative_prime by A4,Th44,INT_2:47;
A11: 3 |^ (Fermat(n)-'1) > 1 by Lm18,Th26;
A12: 1 < p by A4,INT_2:def 5;
A13: (Fermat(n) -'1) mod 2 = 0 by Lm17;
A14: p > 2
    proof
      assume p <= 2;
      then p = 2 or p < 1 + 1 by XXREAL_0:1;
      then
A15:  p = 2 or p <= 1 by NAT_1:13;
      Fermat(n) is odd by Lm20;
      then
A16:  Fermat(n) mod p = 1 by A4,A15,INT_2:def 5,NAT_2:24;
      p <> 0 by A4,INT_2:def 5;
      hence contradiction by A5,A16,Th6;
    end;
    (3 |^ ((Fermat(n)-'1) div 2))^2,(-1)^2 are_congruent_mod Fermat(n)
    by A2,INT_1:39;
    then (3 |^ (Fermat(n)-'1)),(-1)^2 are_congruent_mod Fermat(n) by A13,Th28;
    then
A17: (3 |^ (Fermat(n)-'1)) mod p = 1 by A6,A11,A12,Lm15,INT_1:41;
    set d = order(3,p);
A18: d divides (Fermat(n)-'1) by A10,A12,A17,Lm18,Th52;
A19: d divides (p-'1) by A4,A8,Th44,Th54,INT_2:47;
A20: d <> 0 by A10,A12,Def2;
A21: not d divides ((Fermat(n) -'1) div 2)
    proof
      assume d divides ((Fermat(n) -'1) div 2);

then (3 |^ ((Fermat(n) -'1) div 2)) mod p = 1 by A4,A8,A12,Th44,Th53,INT_2:47;
      then (3 |^ ((Fermat(n) -'1) div 2)),1 are_congruent_mod p by A12,Th40;
      then 1,-1 are_congruent_mod p by A7,Th43;
      then p divides (1 - -1) by INT_2:19;
      hence contradiction by A14,NAT_D:7;
    end;
    set 2N = 2 |^ (2 |^ n);
A22: 2N > 1 by Lm2;
A23: 2N <> 0 by Lm2;
A24: Fermat(n) - 1 >= 0;
    then
A25: d divides 2N by A18,BINARITH:def 3;
A26: not d divides (2N div 2) by A21,A24,BINARITH:def 3;
    d > 1
    proof
      assume
A27:  d <= 1;
      now per cases by A27,XXREAL_0:1;
        suppose d < 1;
          hence thesis by A20,NAT_1:14;
        end;
        suppose d = 1;
          hence thesis by A21,NAT_D:6;
        end;
      end;
      hence thesis;
    end;
    then d = 2N by A25,A26,Th39,INT_2:44
      .= Fermat(n) -'1 by A24,BINARITH:def 3;
    then
A28: d = 2N by A24,BINARITH:def 3;
    consider x being Nat such that
A29: p -'1 = d*x by A19,NAT_D:def 3;
    p - 1 > 1 - 1 by A12,XREAL_1:11;
    then
A30: p - 1 = x*d by A29,BINARITH:def 3;
    then
A31: p = x*2N + 1 by A28;
    then
A32: p mod 2N = 1 mod 2N by NAT_D:21
      .= 1 by Lm2,NAT_D:24;
A33: x >= 1
    proof
      assume x < 1;
      then p = 0*2N + 1 by A31,NAT_1:14;
      hence contradiction by A4,INT_2:def 5;
    end;
    p*i mod 2N = 1 by A6,Lm19;
    then (1*i) mod 2N = 1 by A32,EULER_2:10;
    then consider y being Nat such that
A34: i = 2N*y + 1 & 1 < 2N by NAT_D:def 2;
    reconsider y as Element of NAT by ORDINAL1:def 13;
    Fermat(n) = (x*2N + 1)*(y*2N + 1) by A6,A28,A30,A34
      .= 2N*(x*y*2N + x + y) + 1;
    then 1 = 2N*(x*y*2N + x + y) div 2N by A23,Th48
      .= (x*y*2N + x + y) by A23,NAT_D:18;
    then p = 1*2N + 1 by A22,A31,A33,Lm1
      .= Fermat(n);
    hence contradiction by A3,A4;
  end;
end;

:: LEMMA ::

Lm21: 3 |^ 2 = 9
proof
  3 |^ 2 = 3 |^ (1+1)
    .= (3 |^ 1)*(3 |^ 1) by NEWTON:13
    .= 3*(3 |^ 1) by NEWTON:10
    .= 3*3 by NEWTON:10;
  hence thesis;
end;

Lm22: 3 |^ 4 = 81
proof
  3 |^ 4 = 3 |^ (2+2)
    .= (3 |^ 2)* (3 |^ 2) by NEWTON:13;
  hence thesis by Lm21;
end;

Lm23: 3 |^ 8 = 6561
proof
  3 |^ 8 = 3 |^ (4+4)
    .= (3 |^ 4)* (3 |^ 4) by NEWTON:13;
  hence thesis by Lm22;
end;

Lm24: 3 |^ 16 = 6561*6561
proof
  3 |^ 16 = 3 |^ (8+8)
    .= (3 |^ 8)* (3 |^ 8) by NEWTON:13;
  hence thesis by Lm23;
end;

Lm25: Fermat(1) divides (3 |^ (4*i + 2)) + 1
proof
  defpred P[Nat] means Fermat(1) divides (3 |^ (4*$1 + 2)) + 1;
A1: P[0]
  proof
    3 |^ (4*0+2) + 1 = 5 * 2 by Lm21;
    hence thesis by Th56,NAT_D:def 3;
  end;
A2: for n holds P[n] implies P[n+1]
  proof
    let n;
    assume Fermat(1) divides (3 |^ (4*n + 2)) + 1;
    then consider m being Nat such that
A3: (3 |^ (4*n + 2)) + 1 = Fermat(1)*m by NAT_D:def 3;
A4: (m*81)-16 > 0
    proof
      3 |^ (4*n) >= 3 |^ 0 by PREPOWER:107;
      then 3 |^ (4*n) >= 1 by NEWTON:9;
      then (3 |^ (4*n)) * 3 |^ 2 > 0 * 3 |^ 2 by Lm21,XREAL_1:70;
      then (3 |^ (4*n + 2)) > 0 by NEWTON:13;
      then (3 |^ (4*n + 2)) + 1 > 0 + 1 by XREAL_1:8;
      then Fermat(1)*m*81 > 1*81 by A3,XREAL_1:70;
      then Fermat(1)*m*81/Fermat(1) > 1*81/Fermat(1) by XREAL_1:76;
      then m*Fermat(1)*81*Fermat(1)" > 1*81/Fermat(1) by XCMPLX_0:def 9;
      then m*81*Fermat(1)"*Fermat(1) > 1*81/Fermat(1);
      then m*81/Fermat(1)*Fermat(1) > 1*81/Fermat(1) by XCMPLX_0:def 9;
      then m*81 > 81/5 by Th56,XCMPLX_1:88;
      then m*81 > 16 by XXREAL_0:2;
      then m*81 + (- 16) > 16 + (- 16) by XREAL_1:8;
      hence thesis;
    end;
    (3 |^ (4*(n+1) + 2)) + 1 = (3 |^ (4*n + 2 + 4)) + 1
      .= ((Fermat(1)*m -1)*(3 |^ 4)) + 1 by A3,NEWTON:13
      .= Fermat(1)*((m*81)-16) by Lm22,Th56
      .= Fermat(1)*((m*81)-'16) by A4,BINARITH:def 3;
    hence thesis by NAT_D:def 3;
  end;
  for n holds P[n] from NAT_1:sch 2(A1,A2);
  hence thesis;
end;

Lm26: n = 1 implies
(3 |^ ((Fermat(n)-'1) div 2)),(-1) are_congruent_mod Fermat(n)
proof
  assume
A1: n = 1;
A2: 5 -'1 = 5 - 1 by BINARITH:def 3
    .= 4 + 0;
A3: 2 to_power 1 = 2 |^ 1
    .= 2 by NEWTON:10;
A4: 2 -'1 = 2 - 1 by BINARITH:def 3
    .= 1 + 0;
  2 to_power 2 = 2 |^ (1+1)
    .= 2 |^ 1*2 by NEWTON:11
    .= 2*2 by NEWTON:10;
  then 4 div 2 = (2 to_power (2-'1)) by A3,NAT_2:18
    .= 2 |^ 1 by A4
    .= 2 by NEWTON:10;
  then (Fermat(1) -'1) div 2 = 4*0 + 2 by A2,Th56;
  then Fermat(1) divides (3 |^ ((Fermat(n)-'1) div 2)) + 1 by A1,Lm25;
  then Fermat(1) divides ((3 |^ ((Fermat(n)-'1) div 2 )) - (-1));
  hence thesis by A1,INT_2:19;
end;

Lm27: Fermat(2) divides (3 |^ (16*n + 8)) + 1
proof
  defpred P[Nat] means Fermat(2) divides (3 |^ (16*$1 + 8)) + 1;
A1: P[0]
  proof
    3 |^ (16*0 + 8) + 1 = 17 * 386 by Lm23;
    hence thesis by Th57,NAT_D:def 3;
  end;
A2: for n holds P[n] implies P[n+1]
  proof
    let n;
    assume Fermat(2) divides (3 |^ (16*n + 8)) + 1;
    then consider m being Nat such that
A3: (3 |^ (16*n + 8)) + 1 = Fermat(2)*m by NAT_D:def 3;
A4: m*6561*6561-386*6560 > 0
    proof
      3 |^ (16*n) >= 3 |^ 0 by PREPOWER:107;
      then 3 |^ (16*n) >= 1 by NEWTON:9;
      then (3 |^ (16*n)) * 3 |^ 8 > 0 * 3 |^ 8 by Lm23,XREAL_1:70;
      then (3 |^ (16*n + 8)) > 0 by NEWTON:13;
      then (3 |^ (16*n + 8)) + 1 > 0 + 1 by XREAL_1:8;
      then Fermat(2)*m*6561 > 1*6561 by A3,XREAL_1:70;
      then Fermat(2)*m*6561*6561 > 6561*6561 by XREAL_1:70;
      then Fermat(2)*m*6561*6561 > 6562*6560 by XXREAL_0:2;
      then Fermat(2)*m*6561*6561/Fermat(2) > 6560*6562/Fermat(2)
      by XREAL_1:76;
      then m*6561*Fermat(2)*6561*Fermat(2)"
      > 6560*6562/Fermat(2) by XCMPLX_0:def 9;
      then m*6561*6561*Fermat(2)"*Fermat(2) > 6560*6562/Fermat(2);
      then m*6561*6561/Fermat(2)*Fermat(2)
      > 6560*6562/Fermat(2) by XCMPLX_0:def 9;
      then m*6561*6561 > 6560*6562/17 by Th57,XCMPLX_1:88;
      then m*6561*6561-386*6560 > 386*6560-386*6560 by XREAL_1:11;
      hence thesis;
    end;
    (3 |^ (16*(n+1) + 8)) + 1 = (3 |^ (16*n + 8 + 16)) + 1
      .= ((Fermat(2)*m -1)*(3 |^ 16)) + 1 by A3,NEWTON:13
      .= Fermat(2)*((m*6561*6561)-386*6560) by Lm24,Th57
      .= Fermat(2)*((m*6561*6561)-'386*6560) by A4,BINARITH:def 3;
    hence thesis by NAT_D:def 3;
  end;
  for n holds P[n] from NAT_1:sch 2(A1,A2);
  hence thesis;
end;

Lm28: 3 |^ 1 mod Fermat(3) = 3
proof
A1: 3 = 0*257 + 3;
  thus 3 |^ 1 mod Fermat(3) = 3 mod Fermat(3) by NEWTON:10
    .= 3 by A1,Th58,NAT_D:def 2;
end;

Lm29: 3 |^ 2 mod Fermat(3) = 9
proof
A1: 9 = 0*257 + 9;
  thus 3 |^ 2 mod Fermat(3) = (3 |^ (1+1)) mod Fermat(3)
    .= (3 |^ 1*3 |^ 1) mod Fermat(3) by NEWTON:13
    .= (((3 |^ 1) mod Fermat(3))*((3 |^ 1) mod Fermat(3)))
  mod Fermat(3) by EULER_2:11
    .= 9 by A1,Lm28,Th58,NAT_D:def 2;
end;

Lm30: 3 |^ 4 mod Fermat(3) = 81
proof
A1: 81 = 0*257 + 81;
  thus 3 |^ 4 mod Fermat(3) = (3 |^ (2+2)) mod Fermat(3)
    .= (3 |^ 2*3 |^ 2) mod Fermat(3) by NEWTON:13
    .= (((3 |^ 2) mod Fermat(3))*((3 |^ 2) mod Fermat(3)))
  mod Fermat(3) by EULER_2:11
    .= 81 by A1,Lm29,Th58,NAT_D:def 2;
end;

Lm31: 3 |^ 8 mod Fermat(3) = 136
proof
A1: 6561 = 25*257 + 136;
  thus 3 |^ 8 mod Fermat(3) = (3 |^ (4+4)) mod Fermat(3)
    .= (3 |^ 4*3 |^ 4) mod Fermat(3) by NEWTON:13
    .= (((3 |^ 4) mod Fermat(3))*((3 |^ 4) mod Fermat(3)))
  mod Fermat(3) by EULER_2:11
    .= 136 by A1,Lm30,Th58,NAT_D:def 2;
end;

Lm32: 3 |^ 16 mod Fermat(3) = 83*3
proof
A1: 18496 = 71*257 + 249;
  thus 3 |^ 16 mod Fermat(3) = (3 |^ (8+8)) mod Fermat(3)
    .= (3 |^ 8*3 |^ 8) mod Fermat(3) by NEWTON:13
    .= (136*136) mod Fermat(3) by Lm31,EULER_2:11
    .= 83*3 by A1,Th58,NAT_D:def 2;
end;

Lm33: 3 |^ 32 mod Fermat(3) = 64
proof
A1: 83*3*83*3 = 241*257 + 64;
  3 |^ 32 mod Fermat(3) = (3 |^ (16+16)) mod Fermat(3)
    .= (3 |^ 16*3 |^ 16) mod Fermat(3) by NEWTON:13
    .= (83*3*(83*3)) mod Fermat(3) by Lm32,EULER_2:11
    .= 64 by A1,Th58,NAT_D:def 2;
  hence thesis;
end;

Lm34: 3 |^ 64 mod Fermat(3) = 241
proof
A1: 4096 = 15*257 + 241;
  thus 3 |^ 64 mod Fermat(3) = (3 |^ (32+32)) mod Fermat(3)
    .= (3 |^ 32*3 |^ 32) mod Fermat(3) by NEWTON:13
    .= (64*64) mod Fermat(3) by Lm33,EULER_2:11
    .= 241 by A1,Th58,NAT_D:def 2;
end;

Lm35: 3 |^ 128 mod Fermat(3) = 256
proof
A1: 241*241 = 225*257 + 256;
  thus 3 |^ 128 mod Fermat(3) = (3 |^ (64+64)) mod Fermat(3)
    .= (3 |^ 64*3 |^ 64) mod Fermat(3) by NEWTON:13
    .= (241*241) mod Fermat(3) by Lm34,EULER_2:11
    .= 256 by A1,Th58,NAT_D:def 2;
end;

Lm36: 3 |^ 1 mod Fermat(4) = 3
proof
A1: 3 = 0*(256*256+1) + 3;
  thus 3 |^ 1 mod Fermat(4) = 3 mod Fermat(4) by NEWTON:10
    .= 3 by A1,Th59,NAT_D:def 2;
end;

Lm37: 3 |^ 2 mod Fermat(4) = 9
proof
A1: 9 = 0*(256*256+1) + 9;
  thus 3 |^ 2 mod Fermat(4) = (3 |^ (1+1)) mod Fermat(4)
    .= (3 |^ 1*3 |^ 1) mod Fermat(4) by NEWTON:13
    .= (3*3) mod Fermat(4) by Lm36,EULER_2:11
    .= 9 by A1,Th59,NAT_D:def 2;
end;

Lm38: 3 |^ 4 mod Fermat(4) = 81
proof
A1: 81 = 0*(256*256+1) + 81;
  thus 3 |^ 4 mod Fermat(4) = (3 |^ (2+2)) mod Fermat(4)
    .= (3 |^ 2*3 |^ 2) mod Fermat(4) by NEWTON:13
    .= (9*9) mod Fermat(4) by Lm37,EULER_2:11
    .= 81 by A1,Th59,NAT_D:def 2;
end;

Lm39: 3 |^ 8 mod Fermat(4) = 6561
proof
A1: 6561 = 0*(256*256+1) + 6561;
  thus 3 |^ 8 mod Fermat(4) = (3 |^ (4+4)) mod Fermat(4)
    .= (3 |^ 4*3 |^ 4) mod Fermat(4) by NEWTON:13
    .= (81*81) mod Fermat(4) by Lm38,EULER_2:11
    .= 6561 by A1,Th59,NAT_D:def 2;
end;

Lm40: 3 |^ 16 mod Fermat(4) = 164*332+1
proof
A1: 6561*6561 = 656*(256*256+1) + (164*332+1);
  3 |^ 16 mod Fermat(4) = (3 |^ (8+8)) mod Fermat(4)
    .= (3 |^ 8*3 |^ 8) mod Fermat(4) by NEWTON:13
    .= (6561*6561) mod Fermat(4) by Lm39,EULER_2:11
    .= 164*332+1 by A1,Th59,NAT_D:def 2;
  hence thesis;
end;

Lm41: 3 |^ 32 mod Fermat(4) = 123*503
proof
A1: (164*332+1)*(164*332+1) = 263*172*(256*256+1) + 123*503;
  3 |^ 32 mod Fermat(4) = (3 |^ (16+16)) mod Fermat(4)
    .= (3 |^ 16*3 |^ 16) mod Fermat(4) by NEWTON:13
    .= ((164*332+1)*(164*332+1)) mod Fermat(4) by Lm40,EULER_2:11
    .= 123*503 by A1,Th59,NAT_D:def 2;
  hence thesis;
end;

Lm42: 3 |^ 64 mod Fermat(4) = 14*1367+1
proof
A1: (123*503)*(123*503) = (325+(241*241))*(256*256+1)+(14*1367+1);
  3 |^ 64 mod Fermat(4) = (3 |^ (32+32)) mod Fermat(4)
    .= (3 |^ 32*3 |^ 32) mod Fermat(4) by NEWTON:13
    .= ((123*503)*(123*503)) mod Fermat(4) by Lm41,EULER_2:11
    .= 14*1367+1 by A1,Th59,NAT_D:def 2;
  hence thesis;
end;

Lm43: 3 |^ 128 mod Fermat(4) = 52*289
proof
A1: (14*1367+1)*(14*1367+1) = 5589*(256*256+1) + 52*289;
  3 |^ 128 mod Fermat(4) = (3 |^ (64+64)) mod Fermat(4)
    .= (3 |^ 64*3 |^ 64) mod Fermat(4) by NEWTON:13
    .= ((14*1367+1)*(14*1367+1)) mod Fermat(4) by Lm42,EULER_2:11
    .= 52*289 by A1,Th59,NAT_D:def 2;
  hence thesis;
end;

Lm44: 3 |^ 256 mod Fermat(4) = 282
proof
A1: (52*289)*(52*289) = 3446*(256*256+1) + 282;
  3 |^ 256 mod Fermat(4) = (3 |^ (128+128)) mod Fermat(4)
    .= (3 |^ 128*3 |^ 128) mod Fermat(4) by NEWTON:13
    .= ((52*289)*(52*289)) mod Fermat(4) by Lm43,EULER_2:11
    .= 282 by A1,Th59,NAT_D:def 2;
  hence thesis;
end;

Lm45: 3 |^ (256*2) mod Fermat(4) = 71*197
proof
A1: 282*282 = 1*(256*256+1) + 71*197;
  3 |^ (256*2) = (3 |^ 256)*(3 |^ 256)
  proof
    (3 |^ 256)*(3 |^ 256) = 3 |^ (256+256) by NEWTON:13
      .= 3 |^ 512;
    hence thesis;
  end;
  then 3 |^ (256*2) mod Fermat(4) = (282*282) mod Fermat(4) by Lm44,EULER_2:11
    .= 71*197 by A1,Th59,NAT_D:def 2;
  hence thesis;
end;

Lm46: 3 |^ (256*4) mod Fermat(4) = 32*257
proof
A1: (71*197)*(71*197) = 2985*(256*256+1) + 32*257;
  3 |^ (256*4) = (3 |^ (256*2))*(3 |^ (256*2))
  proof
    (3 |^ (256*2))*(3 |^ (256*2)) = 3 |^ ((256*2)+(256*2)) by NEWTON:13
      .= 3 |^ (1024);
    hence thesis;
  end;
  then 3 |^ (256*4) mod Fermat(4)
  = ((71*197)*(71*197)) mod Fermat(4) by Lm45,EULER_2:11
    .= 32*257 by A1,Th59,NAT_D:def 2;
  hence thesis;
end;

Lm47: 3 |^ (256*8) mod Fermat(4) = 81*809
proof
A1: (32*257)*(32*257) = 1031*(256*256+1) + 81*809;
  3 |^ (256*8) = (3 |^ (256*4))*(3 |^ (256*4))
  proof
    (3 |^ (256*4))*(3 |^ (256*4)) = 3 |^ ((256*4)+(256*4)) by NEWTON:13
      .= 3 |^ (2048);
    hence thesis;
  end;
  then 3 |^ (256*8) mod Fermat(4) = ((32*257)*(32*257)) mod (256*256+1)
  by Lm46,Th59,EULER_2:11
    .= 81*809 by A1,NAT_D:def 2;
  hence thesis;
end;

Lm48: 3 |^ (256*16) mod Fermat(4) = 64
proof
A1: (81*809)*(81*809) = (256*255+241)*(256*256+1) + 64;
  3 |^ (256*16) = (3 |^ (256*8))*(3 |^ (256*8))
  proof
    (3 |^ (256*8))*(3 |^ (256*8)) = 3 |^ ((256*8)+(256*8)) by NEWTON:13
      .= 3 |^ (4096);
    hence thesis;
  end;
  then 3 |^ (256*16) mod Fermat(4)
  = ((81*809)*(81*809)) mod Fermat(4) by Lm47,EULER_2:11
    .= 64 by A1,Th59,NAT_D:def 2;
  hence thesis;
end;

Lm49: 3 |^ (256*32) mod Fermat(4) = 256*16
proof
A1: 64*64 = 0*(256*256+1) + 256*16;
  3 |^ (256*32) = (3 |^ (256*16))*(3 |^ (256*16))
  proof
    (3 |^ (256*16))*(3 |^ (256*16)) = 3 |^ ((256*16)+(256*16)) by NEWTON:13
      .= 3 |^ (8192);
    hence thesis;
  end;
  then 3 |^ (256*32) mod Fermat(4) = (64*64) mod Fermat(4) by Lm48,EULER_2:11
    .= 256*16 by A1,Th59,NAT_D:def 2;
  hence thesis;
end;

Lm50: 3 |^ (256*64) mod Fermat(4) = 673*97
proof
A1: (256*16)*(256*16) = 255*(256*256+1) + 673*97;
  3 |^ (256*64) = (3 |^ (256*32))*(3 |^ (256*32))
  proof
    (3 |^ (256*32))*(3 |^ (256*32)) = 3 |^ ((256*32)+(256*32)) by NEWTON:13
      .= 3 |^ (256*(32+32));
    hence thesis;
  end;
  then 3 |^ (256*64) mod Fermat(4)
  = ((256*16)*(256*16)) mod Fermat(4) by Lm49,EULER_2:11
    .= 673*97 by A1,Th59,NAT_D:def 2;
  hence thesis;
end;

Lm51: 3 |^ (256*128) mod Fermat(4) = 256*256
proof
A1: (255*256+1)*(255*256+1) = 255*255*(256*256+1) + 256*256;
  3 |^ (256*128) = (3 |^ (256*64))*(3 |^ (256*64))
  proof
    (3 |^ (256*64))*(3 |^ (256*64)) = 3 |^ ((256*64)+(256*64)) by NEWTON:13
      .= 3 |^ (256*(64+64));
    hence thesis;
  end;
  then 3 |^ (256*128) mod Fermat(4)
  = ((673*97)*(673*97)) mod Fermat(4) by Lm50,EULER_2:11
    .= 256*256 by A1,Th59,NAT_D:def 2;
  hence thesis;
end;

Lm52: Fermat(3) divides (3 |^ (32*0 + 128)) + 1
proof
A1: 1 mod Fermat(3) = 1
  proof
    1 = 257*0 + 1;
    hence thesis by Th58,NAT_D:def 2;
  end;
A2: 257 mod Fermat(3) = 0
  proof
    257 = 257*1 + 0;
    hence thesis by Th58,NAT_D:def 2;
  end;
  ((3 |^ (32*0 + 128)) + 1) mod Fermat(3)
  = (256 + 1) mod Fermat(3) by A1,Lm35,EULER_2:8
    .= 0 by A2;
  then Fermat(3) divides ((3 |^ (32*0 + 128)) + 1) mod Fermat(3) by NAT_D:6;
  hence thesis by Th7;
end;

Lm53: Fermat(4) divides (3 |^ (64*0 + 256*128)) + 1
proof
A1: 1 mod Fermat(4) = 1
  proof
    1 = (256*256+1)*0 + 1;
    hence thesis by Th59,NAT_D:def 2;
  end;
  (256*256+1) mod Fermat(4) = 0
  proof
    (256*256+1) = (256*256+1)*1 + 0;
    hence thesis by Th59,NAT_D:def 2;
  end;
  then (3 |^ (64*0 + 256*128) + 1) mod Fermat(4) = 0 by A1,Lm51,EULER_2:8;
  then Fermat(4) divides (3 |^ (64*0 + 256*128) + 1) mod Fermat(4) by NAT_D:6;
  hence thesis by Th7;
end;

theorem
  5 is prime
proof
  (3 |^ ((Fermat(1)-'1) div 2)),(-1) are_congruent_mod Fermat(1) by Lm26;
  hence thesis by Th56,Th63;
end;

theorem
  17 is prime
proof
A1: 17 -'1 = 17 - 1 by BINARITH:def 3
    .= 16 + 0;
A2: 2 to_power 4 = 16 by Lm9;
A3: 4 -'1 = 4 - 1 by BINARITH:def 3
    .= 3 + 0;
  2 to_power 1 = 2 |^ 1
    .= 2 by NEWTON:10;
  then 16 div 2 = (2 to_power (4-'1)) by A2,NAT_2:18
    .= 8 by A3,Lm8;
  then (Fermat(2) -'1) div 2 = 16*0 + 8 by A1,Th57;
  then Fermat(2) divides (3 |^ ((Fermat(2)-'1) div 2)) + 1 by Lm27;
  then Fermat(2) divides ((3 |^ ((Fermat(2)-'1) div 2 )) - (-1));
  then (3 |^ ((Fermat(2)-'1) div 2)),
  (-1) are_congruent_mod Fermat(2) by INT_2:19;
  hence thesis by Th57,Th63;
end;

theorem
  257 is prime
proof
A1: 257 -'1 = 257 - 1 by BINARITH:def 3
    .= 256 + 0;
A2: 2 to_power 8 = 2 |^ (4+4)
    .= 16*16 by Lm9,NEWTON:13
    .=256;
A3: 8 -'1 = 8 - 1 by BINARITH:def 3
    .= 7 + 0;
  2 to_power 1 = 2 |^ 1
    .= 2 by NEWTON:10;
  then 256 div 2 = (2 to_power (8-'1)) by A2,NAT_2:18
    .= 2 |^ (4+3) by A3
    .= 16*8 by Lm8,Lm9,NEWTON:13
    .= 128;
  then Fermat(3) divides ((3 |^ ((Fermat(3)-'1) div 2 )) - (-1))
  by A1,Lm52,Th58;
  then (3 |^ ((Fermat(3)-'1) div 2)),
  (-1) are_congruent_mod Fermat(3) by INT_2:19;
  hence thesis by Th58,Th63;
end;

theorem
  256*256+1 is prime
proof
A1: 256*256 + 1 -'1 = 256*256 + 1 - 1 by BINARITH:def 3
    .= 256*256 + 0;
A2: 2 to_power 16 = 2 |^ (8+8)
    .= 256*256 by Lm10,NEWTON:13;
A3: 16 -'1 = 16 - 1 by BINARITH:def 3
    .= 15 + 0;
  2 to_power 1 = 2 |^ 1
    .= 2 by NEWTON:10;
  then 256*256 div 2 = (2 to_power (16-'1)) by A2,NAT_2:18
    .= 2 |^ (8+7) by A3
    .= 256*2 |^ (4+3) by Lm10,NEWTON:13
    .= 256*(16*8) by Lm8,Lm9,NEWTON:13
    .= 256*128;
  then Fermat(4) divides
  ((3 |^ ((Fermat(4)-'1) div 2 )) - (-1)) by A1,Lm53,Th59;
  then (3 |^ ((Fermat(4)-'1) div 2)),
  (-1) are_congruent_mod Fermat(4) by INT_2:19;
  hence thesis by Th59,Th63;
end;

:: Moved from JORDAN1A, AK, 21.02.2006

theorem Th68:
  for i,j being natural number holds
  j > 0 & i mod j = 0 implies i div j = i / j
proof
  let i,j be natural number;
  assume
A1: j > 0;
  assume i mod j = 0;
  then consider t being Nat such that
A2: i = j * t + 0 and
A3: 0 < j by A1,NAT_D:def 2;
  thus i div j = t by A2,A3,NAT_D:def 1
    .= i / j by A1,A2,XCMPLX_1:90;
end;

theorem
  for i,n being natural number holds n > 0 implies i |^ n div i = i |^ n / i
proof
  let i,n be natural number;
  assume
A1: n > 0;
  per cases;
  suppose
A2: i > 0;
    i |^ n mod i = 0 by A1,Th37;
    hence i |^ n div i = i |^ n / i by A2,Th68;
  end;
  suppose
A3: i = 0;
    n >= 0+1 by A1,NAT_1:13;
    then i |^ n = 0 by A3,NEWTON:16;
    hence i |^ n div i = i |^ n / i by A3,NAT_D:def 1;
  end;
end;

reserve r for real number;

theorem Th70:
  for n being natural number holds 0 < n & 1 < r implies 1 < r|^n
proof
  let n be natural number;
  assume that
A1: 0 < n and
A2: r > 1;
  defpred P[natural number] means 0 < $1 implies 1 < r|^$1;
A3: P[0];
A4: for k being natural number holds P[k] implies P[k+1]
  proof
    let k be natural number;
    assume that
A5: P[k] and 0 < k+1;
    per cases;
    suppose
A6:   k > 0;
A7:   r|^(k+1) = (r|^k)*r by NEWTON:11;
      1 * r <= (r|^k)*r by A2,A5,A6,XREAL_1:66;
      hence thesis by A2,A7,XXREAL_0:2;
    end;
    suppose k = 0;
      hence thesis by A2,NEWTON:10;
    end;
  end;
  for k being natural number holds P[k] from NAT_1:sch 2(A3,A4);
  hence thesis by A1;
end;

theorem
  for m,n being natural number holds r > 1 & m > n implies r|^m > r|^n
proof
  let m,n be natural number;
  assume that
A1: r > 1 and
A2: m > n;
  reconsider m,n as Element of NAT by ORDINAL1:def 13;
  (m-'n)+n = m by A2,BINARITH:53;
  then
A3: r|^m = r|^(m-'n)*r|^n by NEWTON:13;
  m-'n <> 0 by A2,BINARITH:54;
  then m-'n > 0;
  then
A4: r|^(m-'n) > 1 by A1,Th70;
  r|^n > 0 by A1,NEWTON:102;
  hence thesis by A3,A4,REAL_2:144;
end;

