:: The Continuous Functions on Normed Linear Spaces
::  by Takaya Nishiyama, Keiji Ohkubo and Yasunari Shidama
::
:: Received April 6, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies PRE_TOPC, FCONT_1, SUBSET_1, RLVECT_1, FUNCT_1, ARYTM, ARYTM_1,
      ARYTM_3, RELAT_1, ABSVALUE, ORDINAL2, SEQ_1, SEQ_2, SEQM_3, COMPTS_1,
      RCOMP_1, BOOLE, FINSEQ_4, SEQ_4, PARTFUN1, PARTFUN2, NORMSP_1, FRECHET2,
      COMPLEX1, ALGSTR_0;
 notations TARSKI, XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, RELSET_1, PARTFUN1,
      PARTFUN2, FUNCT_2, PRE_TOPC, STRUCT_0, ALGSTR_0, XREAL_0, ORDINAL1,
      NUMBERS, REAL_1, RLVECT_1, SEQ_1, SEQ_2, SEQM_3, SEQ_4, COMPLEX1,
      RCOMP_1, VFUNCT_1, NORMSP_1, BHSP_1, BHSP_3, LOPBAN_3, XXREAL_0,
      RECDEF_1;
 constructors PARTFUN1, REAL_1, COMPLEX1, SEQ_2, SEQ_4, RCOMP_1, PARTFUN2,
      BHSP_3, VFUNCT_1, LOPBAN_3, SEQ_1, RECDEF_1;
 registrations XBOOLE_0, FUNCT_1, ORDINAL1, RELSET_1, NUMBERS, NAT_1, MEMBERED,
      SEQM_3, STRUCT_0, RLVECT_1, VALUED_0;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;
 definitions TARSKI, SEQM_3, BHSP_3, RLVECT_1, RELAT_1;
 theorems TARSKI, XBOOLE_0, XBOOLE_1, SUBSET_1, RLVECT_1, XCMPLX_0, XCMPLX_1,
      ZFMISC_1, RCOMP_1, XREAL_0, SEQ_2, NAT_1, RELAT_1, FUNCT_1, VFUNCT_1,
      LOPBAN_1, FUNCT_2, SEQ_4, NORMSP_1, LOPBAN_3, PARTFUN1, PARTFUN2, BHSP_3,
      XREAL_1, COMPLEX1, XXREAL_0, BHSP_1;
 schemes SEQ_1, FUNCT_2;

begin :: Continuous Functions on Normed Linear Spaces

reserve n,m for Element of NAT;
reserve x,X,X1 for set;
reserve s,g,r,p for Real;
reserve S,T for RealNormSpace;
reserve f,f1,f2 for PartFunc of S,T;
reserve s1,s2 for sequence of S;
reserve x0,x1,x2 for Point of S;
reserve Y for Subset of S;

theorem
  for S being non empty addLoopStr
  for seq1,seq2 be sequence of S holds seq1 - seq2 = seq1 +- seq2
proof
  let S be non empty addLoopStr;
  let seq1, seq2 be sequence of S;
  for n holds (seq1 - seq2).n = (seq1 +- seq2).n
  proof
    let n;
    thus (seq1 - seq2).n = seq1.n - seq2.n by NORMSP_1:def 6
      .= seq1.n + (-seq2).n by BHSP_1:def 10
      .= (seq1+-seq2).n by NORMSP_1:def 5;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th2:
  for seq be sequence of S holds -seq=(-1)*seq
proof
  let seq be sequence of S;
  now
    let n;
    thus ((-1)*seq).n =(-1)*seq.n by NORMSP_1:def 8
      .=-seq.n by RLVECT_1:29
      .=(-seq).n by BHSP_1:def 10;
  end;
  hence thesis by FUNCT_2:113;
end;

definition
  canceled;
  let S,T;
  let f be PartFunc of S,T;
  func ||.f .|| -> PartFunc of the carrier of S,REAL means
  :Def2:
  dom it = dom f & for c being Point of S st c in dom it holds
  it.c = ||.f/.c.||;
  existence
  proof
    defpred X[set] means $1 in dom f;
    deffunc U(Element of S) = ||.f/.$1 .||;
    consider F be PartFunc of the carrier of S,REAL such that
A1: for c being Element of S holds c in dom F iff X[c] and
A2: for c being Element of S st c in dom F holds F.c = U(c) from SEQ_1:sch 3;
    reconsider F as PartFunc of the carrier of S,REAL;
    take F;
    thus dom F = dom f by A1,SUBSET_1:8;
    thus thesis by A2;
  end;
  uniqueness
  proof
    deffunc U(Element of S) = ||. f/.$1 .||;
    for f1, f2 be PartFunc of the carrier of S,REAL st (dom f1 = dom f &
    for c being Element of S st c in dom f1 holds f1.c = U(c)) &
    (dom f2 = dom f & for c being Element of S st c in dom f2 holds
    f2.c = U(c)) holds f1 = f2 from SEQ_1:sch 4;
    hence thesis;
  end;
end;

definition
  let S, x0;
  mode Neighbourhood of x0 -> Subset of S means
    :Def3:
    ex g be Real st 0<g & {y where y is Point of S: ||.y-x0 .|| < g} c= it;
  existence
  proof
    set N= {y where y is Point of S : ||.y-x0 .|| < 1};
A1: N c= the carrier of S
    proof
      let x be set;
      assume x in {y where y is Point of S : ||.y-x0 .|| < 1};
      then consider y be Point of S such that
A2:   x=y and ||.y-x0 .|| < 1;
      thus x in the carrier of S by A2;
    end;
    take N;
    thus thesis by A1;
  end;
end;

theorem Th3:
  for g be Real st 0<g holds
  {y where y is Point of S: ||.y-x0 .|| < g} is Neighbourhood of x0
proof
  let g be Real such that
A1: g >0;
  set N= {y where y is Point of S : ||.y-x0 .|| < g};
  N c= the carrier of S
  proof
    let x be set;
    assume x in {y where y is Point of S : ||.y-x0 .|| < g};
    then consider y be Point of S such that
A2: x=y and ||.y-x0 .|| < g;
    thus x in the carrier of S by A2;
  end;
  hence thesis by A1,Def3;
end;

theorem Th4:
  for N being Neighbourhood of x0 holds x0 in N
proof
  let N be Neighbourhood of x0;
  consider g be Real such that
A1: 0<g and
A2: {z where z is Point of S : ||.z-x0 .|| < g} c= N by Def3;
  ||.x0-x0 .|| = ||.0.S.|| by RLVECT_1:28
    .=0 by NORMSP_1:5;
  then x0 in {z where z is Point of S : ||.z-x0 .|| < g} by A1;
  hence thesis by A2;
end;

definition
  let S;
  let X be Subset of S;
  attr X is compact means
  :Def4:
  for s1 be sequence of S st rng s1 c= X
  ex s2 be sequence of S st
  s2 is subsequence of s1 & s2 is convergent & lim s2 in X;
end;

definition
  let S;
  let X be Subset of S;
  attr X is closed means

  for s1 be sequence of S st
  rng s1 c= X & s1 is convergent holds lim s1 in X;
end;

definition
  let S;
  let X be Subset of S;
  attr X is open means

  X` is closed;
end;

definition
  let S,T;
  let f;
  let seq be sequence of S;
  assume
A1: rng seq c= dom f;
  func f*seq -> sequence of T equals
  :Def7:
  (f qua Function)*seq;
  coherence
  proof
    dom seq = NAT by FUNCT_2:def 1;
    then
A2: dom ((f qua Function)*seq) = NAT by A1,RELAT_1:46;
    rng ((f qua Function)*seq) c= rng f by RELAT_1:45;
    hence (f qua Function)*seq is sequence of T by A2,FUNCT_2:4,XBOOLE_1:1;
  end;
end;

definition
  let S;
  let f be PartFunc of the carrier of S,REAL;
  let seq be sequence of S;
  assume
A1: rng seq c= dom f;
  func f*seq -> Real_Sequence equals
  :Def8:
  (f qua Function)*seq;
  coherence
  proof
    dom seq = NAT by FUNCT_2:def 1;
    then
A2: dom ((f qua Function)*seq) = NAT by A1,RELAT_1:46;
    rng ((f qua Function)*seq) c= rng f by RELAT_1:45;
    hence (f qua Function)*seq is Real_Sequence by A2,FUNCT_2:4,XBOOLE_1:1;
  end;
end;

definition
  let S,T;
  let f,x0;
  pred f is_continuous_in x0 means
  :Def9:
  x0 in dom f &
  for s1 st rng s1 c= dom f & s1 is convergent & lim s1 = x0 holds
  f*s1 is convergent & f/.x0 = lim (f*s1);
end;

definition
  let S;
  let f be PartFunc of the carrier of S,REAL;
  let x0;
  pred f is_continuous_in x0 means
  :Def10:
  x0 in dom f &
  for s1 st rng s1 c= dom f & s1 is convergent & lim s1 = x0 holds
  f*s1 is convergent & f/.x0 = lim (f*s1);
end;

theorem Th5:
  for seq be sequence of S, h be PartFunc of S,T st
  rng seq c= dom h holds seq.n in dom h
proof
  let seq be sequence of S;
  let h be PartFunc of S,T;
  assume
A1: rng seq c= dom h;
  n in NAT;
  then n in dom seq by FUNCT_2:def 1;
  then n in dom ((h qua Function)*seq) by A1,RELAT_1:46;
  hence seq.n in dom h by FUNCT_1:21;
end;

theorem Th6:
  for seq be sequence of S, x be set holds x in rng seq iff ex n st x = seq.n
proof
  let seq be sequence of S;
  let x be set;
  thus x in rng seq implies ex n st x = seq.n
  proof
    assume x in rng seq;
    then consider y be set such that
A1: y in dom seq & x = seq.y by FUNCT_1:def 5;
    reconsider m=y as Element of NAT by A1;
    take m;
    thus thesis by A1;
  end;
  given n such that
A2: x = seq.n;
  n in NAT;
  then n in dom seq by FUNCT_2:def 1;
  hence thesis by A2,FUNCT_1:def 5;
end;

theorem Th7:
  for seq,seq1 be sequence of S st seq1 is subsequence of seq holds
  rng seq1 c= rng seq
proof
  let seq be sequence of S;
  let seq1 be sequence of S;
  assume seq1 is subsequence of seq;
  then consider Ns be increasing Seq_of_Nat such that
A1: seq1 = seq * Ns by BHSP_3:def 4;
  let x;
  assume x in rng seq1;
  then consider n such that
A2: x = (seq *Ns).n by A1,Th6;
  x = seq.(Ns.n) by A2,LOPBAN_3:7;
  hence x in rng seq by Th6;
end;

theorem Th8:
  for f,s1 st rng s1 c= dom f for n holds (f*s1).n=f/.(s1.n)
proof
  let f,s1 such that
A1: rng s1 c= dom f;
  let n;
  dom s1 = NAT by FUNCT_2:def 1;
  then s1.n in rng s1 by FUNCT_1:12;
  hence f/.(s1.n) = (f qua Function).(s1.n) by A1,PARTFUN1:def 8
    .=((f qua Function)*s1).n by FUNCT_2:21
    .=(f*s1).n by A1,Def7;
end;

theorem Th9:
  for f be PartFunc of the carrier of S,REAL for s1 st rng s1 c= dom f
  for n holds (f*s1).n=f/.(s1.n)
proof
  let f be PartFunc of the carrier of S,REAL;
  let s1 such that
A1: rng s1 c= dom f;
  let n;
  dom s1 = NAT by FUNCT_2:def 1;
  then s1.n in rng s1 by FUNCT_1:12;
  hence f/.(s1.n) = (f qua Function).(s1.n) by A1,PARTFUN1:def 8
    .=((f qua Function)*s1).n by FUNCT_2:21
    .=(f*s1).n by A1,Def8;
end;

theorem Th10:
  for h be PartFunc of S,T for seq be sequence of S
  for Ns be increasing Seq_of_Nat st
  rng seq c= dom h holds (h*seq)*Ns = h * (seq*Ns)
proof
  let h be PartFunc of S,T;
  let seq be sequence of S;
  let Ns be increasing Seq_of_Nat;
  assume
A1: rng seq c= dom h;
  (seq * Ns) is subsequence of seq by BHSP_3:def 4;
  then
A2: rng (seq*Ns) c= rng seq by Th7;
  thus (h*seq)*Ns = ((h qua Function)*seq)*Ns by A1,Def7
    .= (h qua Function)*(seq*Ns) by RELAT_1:55
    .= h*(seq*Ns) by A1,A2,Def7,XBOOLE_1:1;
end;

theorem Th11:
  for h be PartFunc of the carrier of S,REAL for seq be sequence of S
  for Ns be increasing Seq_of_Nat st
  rng seq c= dom h holds (h*seq)*Ns = h * (seq*Ns)
proof
  let h be PartFunc of the carrier of S,REAL;
  let seq be sequence of S;
  let Ns be increasing Seq_of_Nat;
  assume
A1: rng seq c= dom h;
  (seq * Ns) is subsequence of seq by BHSP_3:def 4;
  then
A2: rng (seq*Ns) c= rng seq by Th7;
  thus (h*seq)*Ns = ((h qua Function)*seq)*Ns by A1,Def8
    .= (h qua Function)*(seq*Ns) by RELAT_1:55
    .= h*(seq*Ns) by A1,A2,Def8,XBOOLE_1:1;
end;

theorem Th12:
  for h be PartFunc of S,T for seq1,seq2 be sequence of S st
  rng seq1 c= dom h & seq2 is subsequence of seq1 holds
  h*seq2 is subsequence of h*seq1
proof
  let h be PartFunc of S,T;
  let seq1,seq2 be sequence of S;
  assume
A1: rng seq1 c= dom h & seq2 is subsequence of seq1;
  then consider Ns be increasing Seq_of_Nat such that
A2: seq2=seq1*Ns by BHSP_3:def 4;
  take Ns; thus thesis by A1,A2,Th10;
end;

theorem Th13:
  for h be PartFunc of the carrier of S,REAL for seq1,seq2 be sequence of S st
  rng seq1 c= dom h & seq2 is subsequence of seq1 holds
  h*seq2 is subsequence of h*seq1
proof
  let h be PartFunc of the carrier of S,REAL;
  let seq1,seq2 be sequence of S;
  assume
A1: rng seq1 c= dom h & seq2 is subsequence of seq1;
  then consider Ns be increasing Seq_of_Nat such that
A2: seq2=seq1*Ns by BHSP_3:def 4;
  take Ns; thus thesis by A1,A2,Th11;
end;

theorem Th14:
  f is_continuous_in x0 iff x0 in dom f & for r st 0<r ex s st 0<s & for x1 st
  x1 in dom f & ||. x1- x0 .|| <s holds ||. f/.x1-f/.x0 .||<r
proof
  thus f is_continuous_in x0 implies x0 in dom f & for r st 0<r ex s st 0<s &
  for x1 st x1 in dom f & ||. x1- x0 .|| <s holds ||. f/.x1- f/.x0 .|| <r
  proof
    assume
A1: f is_continuous_in x0;
    hence x0 in dom f by Def9;
    given r such that
A2: 0<r & for s holds not 0<s or ex x1 st x1 in dom f &
    ||. x1- x0 .|| <s & not ||. f/.x1- f/.x0 .|| <r;
    defpred P[Element of NAT ,Point of S] means
    $2 in dom f & ||. $2-x0 .|| < 1/($1+1) & not ||.f/.$2-f/.x0 .||<r;
A3: for n ex p be Point of S st P[n,p]
    proof
      let n;
      0<n+1 by NAT_1:3;
      then 0<(n+1)" by XREAL_1:124;
      then 0<1/(n+1) by XCMPLX_1:217;
      then consider p be Point of S such that
A4:   p in dom f & ||.p-x0 .|| < 1/(n+1) & not ||.f/.p-f/.x0 .|| <r by A2;
      take p;
      thus thesis by A4;
    end;
    consider s1 being Function of NAT,the carrier of S such that
A5: for n being Element of NAT holds P[n,s1.n] from FUNCT_2:sch 3(A3);
    reconsider s1 as sequence of S;
A6: rng s1 c= dom f
    proof
      let v be set such that
A7:   v in rng s1;
      dom s1= NAT by FUNCT_2:def 1;
      then ex n be set st n in NAT & v=s1.n by A7,FUNCT_1:def 5;
      hence thesis by A5;
    end;
A8: now
      let n;
      not ||.f/.(s1.n)-f/.x0 .||<r by A5;
      hence not ||.(f*s1).n-f/.x0 .||<r by A6,Th8;
    end;
A9: now
      let s be Real;
      assume 0<s;
      then
A10:  0<s" by XREAL_1:124;
      consider n such that
A11:  s"<n by SEQ_4:10;
      take k=n;
      let m such that
A12:  k<=m;
      s"+0 <n+1 by A11,XREAL_1:10;
      then 1/(n+1)<1/s" by A10,XREAL_1:78;
      then
A13:  1/(n+1)<s by XCMPLX_1:218;
      k+1<=m+1 by A12,XREAL_1:8;
      then 1/(m+1)<=1/(k+1) by NAT_1:3,XREAL_1:120;
      then
A14:  1/(m+1)<s by A13,XXREAL_0:2;
      ||. s1.m-x0 .|| <1/(m+1) by A5;
      hence ||. s1.m-x0 .|| <s by A14,XXREAL_0:2;
    end;
    then
A15: s1 is convergent by NORMSP_1:def 9;
    then lim s1=x0 by A9,NORMSP_1:def 11;
    then f*s1 is convergent & f/.x0=lim(f*s1) by A1,A6,A15,Def9;
    then consider n such that
A16: for m st n<=m holds ||.(f*s1).m-f/.x0.||<r by A2,NORMSP_1:def 11;
    ||.(f*s1).n-f/.x0.||<r by A16;
    hence contradiction by A8;
  end;
  assume
A17: x0 in dom f & for r st 0<r ex s st 0<s &
  for x1 st x1 in dom f & ||. x1- x0 .|| <s holds ||. f/.x1- f/.x0 .|| <r;
  now
    let s1 such that
A18: rng s1 c= dom f & s1 is convergent & lim s1 = x0;
A19: now
      let p be Real;
      assume 0<p;
      then consider s such that
A20:  0<s & for x1 st x1 in dom f & ||. x1- x0 .|| <s holds
      ||. f/.x1- f/.x0 .|| <p by A17;
      consider n such that
A21:  for m st n<=m holds ||. s1.m-x0 .|| <s by A18,A20,NORMSP_1:def 11;
      take k=n;
      let m such that
A22:  k<=m;
      dom s1 = NAT by FUNCT_2:def 1;
      then
A23:  s1.m in rng s1 by FUNCT_1:12;
      ||. s1.m-x0 .|| <s by A21,A22;
      then ||.f/.(s1.m)-f/.x0.||<p by A18,A20,A23;
      hence ||.(f*s1).m - f/.x0.||<p by A18,Th8;
    end;
    then f*s1 is convergent by NORMSP_1:def 9;
    hence f*s1 is convergent & f/.x0 = lim (f*s1) by A19,NORMSP_1:def 11;
  end;
  hence thesis by A17,Def9;
end;

theorem Th15:
  for f be PartFunc of the carrier of S,REAL holds
  (f is_continuous_in x0 iff x0 in dom f & for r st 0<r ex s st 0<s &
  for x1 st x1 in dom f & ||. x1- x0 .|| <s holds abs( f/.x1-f/.x0 )<r )
proof
  let f be PartFunc of the carrier of S,REAL;
  thus f is_continuous_in x0 implies x0 in dom f & for r st 0<r ex s st 0<s &
  for x1 st x1 in dom f & ||. x1- x0 .|| <s holds abs( f/.x1- f/.x0 ) <r
  proof
    assume
A1: f is_continuous_in x0;
    hence x0 in dom f by Def10;
    given r such that
A2: 0<r & for s holds not 0<s or ex x1 st x1 in dom f &
    ||. x1- x0 .|| <s & not abs( f/.x1- f/.x0 ) <r;
    defpred P[Element of NAT ,Point of S] means
    $2 in dom f & ||. $2-x0 .|| < 1/($1+1) & not abs(f/.$2-f/.x0 )<r;
A3: for n ex p be Point of S st P[n,p]
    proof
      let n;
      0<n+1 by NAT_1:3;
      then 0<(n+1)" by XREAL_1:124;
      then 0<1/(n+1) by XCMPLX_1:217;
      then consider p be Point of S such that
A4:   p in dom f & ||.p-x0 .|| < 1/(n+1) & abs(f/.p-f/.x0 ) >= r by A2;
      take p;
      thus thesis by A4;
    end;
    consider s1 being Function of NAT,the carrier of S such that
A5: for n being Element of NAT holds P[n,s1.n] from FUNCT_2:sch 3(A3);
    reconsider s1 as sequence of S;
A6: rng s1 c= dom f
    proof
      let v be set such that
A7:   v in rng s1;
      dom s1= NAT by FUNCT_2:def 1;
      then ex n be set st n in NAT & v=s1.n by A7,FUNCT_1:def 5;
      hence thesis by A5;
    end;
A8: now
      let n;
      abs(f/.(s1.n)-f/.x0 )>=r by A5;
      hence abs( (f*s1).n-f/.x0 ) >= r by A6,Th9;
    end;
A9: now
      let s be Real;
      assume 0<s;
      then
A10:  0<s" by XREAL_1:124;
      consider n such that
A11:  s"<n by SEQ_4:10;
      take k=n;
      let m such that
A12:  k<=m;
      s"+0 <n+1 by A11,XREAL_1:10;
      then 1/(n+1)<1/s" by A10,XREAL_1:78;
      then
A13:  1/(n+1)<s by XCMPLX_1:218;
      k+1<=m+1 by A12,XREAL_1:8;
      then 1/(m+1)<=1/(k+1) by NAT_1:3,XREAL_1:120;
      then
A14:  1/(m+1)<s by A13,XXREAL_0:2;
      ||. s1.m-x0 .|| <1/(m+1) by A5;
      hence ||. s1.m-x0 .|| <s by A14,XXREAL_0:2;
    end;
    then
A15: s1 is convergent by NORMSP_1:def 9;
    then lim s1=x0 by A9,NORMSP_1:def 11;
    then f*s1 is convergent & f/.x0=lim(f*s1) by A1,A6,A15,Def10;
    then consider n such that
A16: for m st n<=m holds abs( (f*s1).m-f/.x0)<r by A2,SEQ_2:def 7;
    abs((f*s1).n-f/.x0) <r by A16;
    hence contradiction by A8;
  end;
  assume
A17: x0 in dom f & for r st 0<r ex s st 0<s & for x1 st x1 in dom f &
  ||. x1- x0 .|| <s holds abs( f/.x1- f/.x0 ) <r;
  now
    let s1 such that
A18: rng s1 c= dom f & s1 is convergent & lim s1 = x0;
A19: now
      let p be real number;
      reconsider pp=p as Real by XREAL_0:def 1;
      assume 0<p;
      then consider s such that
A20:  0<s & for x1 st x1 in dom f & ||. x1- x0 .|| <s holds
      abs( f/.x1- f/.x0 ) <pp by A17;
      consider n such that
A21:  for m st n<=m holds ||. s1.m-x0 .|| <s by A18,A20,NORMSP_1:def 11;
      take k=n;
      let m such that
A22:  k<=m;
      dom s1 = NAT by FUNCT_2:def 1;
      then
A23:  s1.m in rng s1 by FUNCT_1:12;
      ||. s1.m-x0 .|| <s by A21,A22;
      then abs(f/.(s1.m)-f/.x0) <p by A18,A20,A23;
      hence abs( (f*s1).m - f/.x0) <p by A18,Th9;
    end;
    then f*s1 is convergent by SEQ_2:def 6;
    hence f*s1 is convergent & f/.x0 = lim (f*s1) by A19,SEQ_2:def 7;
  end;
  hence thesis by A17,Def10;
end;

theorem Th16:
  for f,x0 holds f is_continuous_in x0 iff x0 in dom f &
  for N1 being Neighbourhood of f/.x0 ex N being Neighbourhood of x0 st
  for x1 st x1 in dom f & x1 in N holds f/.x1 in N1
proof
  let f,x0;
  thus f is_continuous_in x0 implies x0 in dom f &
  for N1 being Neighbourhood of f/.x0 ex N being Neighbourhood of x0 st
  for x1 st x1 in dom f & x1 in N holds f/.x1 in N1
  proof
    assume
A1: f is_continuous_in x0;
    hence x0 in dom f by Def9;
    let N1 be Neighbourhood of f/.x0;
    consider r such that
A2: 0<r & {y where y is Point of T : ||.y-f/.x0 .|| < r} c= N1 by Def3;
    consider s such that
A3: 0<s & for x1 st x1 in dom f & ||. x1- x0 .|| <s holds
    ||. f/.x1- f/.x0 .|| <r by A1,A2,Th14;
    reconsider N={z where z is Point of S : ||.z-x0 .|| < s}
    as Neighbourhood of x0 by A3,Th3;
    take N;
    let x1;
    assume
A4: x1 in dom f & x1 in N;
    then consider z be Point of S such that
A5: x1=z and
A6: ||.z-x0 .|| < s;
    ||. f/.x1- f/.x0 .|| <r by A3,A4,A5,A6;
    then f/.x1 in {y where y is Point of T : ||.y-f/.x0 .|| < r};
    hence thesis by A2;
  end;
  assume
A7: x0 in dom f & for N1 being Neighbourhood of f/.x0
  ex N being Neighbourhood of x0 st
  for x1 st x1 in dom f & x1 in N holds f/.x1 in N1;
  now
    let r;
    assume 0<r;
    then reconsider N1 = {y where y is Point of T : ||.y-f/.x0 .|| < r}
    as Neighbourhood of f/.x0 by Th3;
    consider N2 being Neighbourhood of x0 such that
A8: for x1 st x1 in dom f & x1 in N2 holds f/.x1 in N1 by A7;
    consider s such that
A9: 0<s & {z where z is Point of S : ||.z-x0 .|| < s} c= N2 by Def3;
    take s;
    for x1 st x1 in dom f & ||. x1- x0 .|| <s holds ||. f/.x1- f/.x0 .|| <r
    proof
      let x1;
      assume
A10:  x1 in dom f & ||. x1- x0 .|| <s;
      then x1 in {z where z is Point of S : ||.z-x0 .|| < s};
      then f/.x1 in N1 by A8,A9,A10;
      then consider y be Point of T such that
A11:  f/.x1=y and
A12:  ||.y-f/.x0 .|| < r;
      thus thesis by A11,A12;
    end;
    hence 0<s & for x1 st x1 in dom f & ||. x1- x0 .||
    <s holds ||. f/.x1- f/.x0 .|| <r by A9;
  end;
  hence thesis by A7,Th14;
end;

theorem Th17:
  for f,x0 holds f is_continuous_in x0 iff x0 in dom f &
  for N1 being Neighbourhood of f/.x0
  ex N being Neighbourhood of x0 st f.:N c= N1
proof
  let f,x0;
  thus f is_continuous_in x0 implies x0 in dom f &
  for N1 being Neighbourhood of f/.x0
  ex N being Neighbourhood of x0 st f.:N c= N1
  proof
    assume
A1: f is_continuous_in x0;
    hence x0 in dom f by Def9;
    let N1 be Neighbourhood of f/.x0;
    consider N being Neighbourhood of x0 such that
A2: for x1 st x1 in dom f & x1 in N holds f/.x1 in N1 by A1,Th16;
    take N;
    now
      let r be set;
      assume r in f.:N;
      then consider x be Point of S such that
A3:   x in dom f and
A4:   x in N & r=f.x by PARTFUN2:78;
      x in dom f & x in N & r=f/.x by A3,A4,PARTFUN1:def 8;
      hence r in N1 by A2;
    end;
    hence thesis by TARSKI:def 3;
  end;
  assume
A5: x0 in dom f & for N1 being Neighbourhood of f/.x0
  ex N being Neighbourhood of x0 st f.:N c= N1;
  now
    let N1 be Neighbourhood of f/.x0;
    consider N being Neighbourhood of x0 such that
A6: f.:N c= N1 by A5;
    take N;
    let x1;
    assume
A7: x1 in dom f & x1 in N;
    then f.x1 in f.:N by FUNCT_1:def 12;
    then f.x1 in N1 by A6;
    hence f/.x1 in N1 by A7,PARTFUN1:def 8;
  end;
  hence thesis by A5,Th16;
end;

theorem
  x0 in dom f & (ex N be Neighbourhood of x0 st
  dom f /\ N = {x0}) implies f is_continuous_in x0
proof
  assume
A1: x0 in dom f;
  given N be Neighbourhood of x0 such that
A2: dom f /\ N = {x0};
  now
    let N1 be Neighbourhood of f/.x0;
    take N;
A3: f.:N = Im(f,x0) by A2,RELAT_1:145
      .= {f.x0} by A1,FUNCT_1:117
      .= {f/.x0} by A1,PARTFUN1:def 8;
    f/.x0 in N1 by Th4;
    hence f.:N c= N1 by A3,ZFMISC_1:37;
  end;
  hence thesis by A1,Th17;
end;

theorem Th19:
  for h1,h2 be PartFunc of S,T for seq be sequence of S
  st rng seq c= dom h1 /\ dom h2
  holds (h1+h2)*seq=h1*seq+h2*seq & (h1-h2)*seq=h1*seq-h2*seq
proof
  let h1,h2 be PartFunc of S,T;
  let seq be sequence of S;
  assume
A1: rng seq c= dom h1 /\ dom h2;
A2: dom h1 /\ dom h2 c= dom h1 & dom h1 /\ dom h2 c= dom h2 by XBOOLE_1:17;
A3: rng seq c= dom (h1 + h2) by A1,VFUNCT_1:def 1;
A4: rng seq c= dom (h1 - h2) by A1,VFUNCT_1:def 2;
  now
    let n;
A5: seq.n in dom (h1 + h2) by A3,Th5;
    thus ((h1+h2)*seq).n = (h1+h2)/.(seq.n) by A3,Th8
      .= h1/.(seq.n) + h2/.(seq.n) by A5,VFUNCT_1:def 1
      .= (h1*seq).n + h2/.(seq.n) by A1,A2,Th8,XBOOLE_1:1
      .= (h1*seq).n + (h2*seq).n by A1,A2,Th8,XBOOLE_1:1;
  end;
  hence (h1+h2)*seq=h1*seq+h2*seq by NORMSP_1:def 5;
  now
    let n;
A6: seq.n in dom (h1 - h2) by A4,Th5;
    thus ((h1-h2)*seq).n = (h1-h2)/.(seq.n) by A4,Th8
      .= h1/.(seq.n) - h2/.(seq.n) by A6,VFUNCT_1:def 2
      .= (h1*seq).n - h2/.(seq.n) by A1,A2,Th8,XBOOLE_1:1
      .= (h1*seq).n - (h2*seq).n by A1,A2,Th8,XBOOLE_1:1;
  end;
  hence (h1-h2)*seq=h1*seq-h2*seq by NORMSP_1:def 6;
end;

theorem Th20:
  for h be PartFunc of S,T for seq be sequence of S for r be Real
  st rng seq c= dom h holds (r(#)h)*seq = r*(h*seq)
proof
  let h be PartFunc of S,T;
  let seq be sequence of S;
  let r be Real;
  assume
A1: rng seq c= dom h;
  then
A2: rng seq c= dom (r(#)h) by VFUNCT_1:def 4;
  now
    let n;
A3: seq.n in dom (r(#)h) by A2,Th5;
    thus ((r(#)h)*seq).n = (r(#)h)/.(seq.n) by A2,Th8
      .= r * (h/.(seq.n)) by A3,VFUNCT_1:def 4
      .= r * (h*seq).n by A1,Th8;
  end;
  hence thesis by NORMSP_1:def 8;
end;

theorem Th21:
  for h be PartFunc of S,T for seq be sequence of S st rng seq c= dom h holds
  ||.h*seq .|| = ||.h.||*seq & -(h*seq) = (-h)*seq
proof
  let h be PartFunc of S,T;
  let seq be sequence of S;
  assume
A1: rng seq c= dom h;
  then
A2: rng seq c= dom ||.h.|| by Def2;
  now
    let n;
    seq.n in rng seq by Th6;
    then seq.n in dom h by A1;
    then
A3: seq.n in dom ||.h.|| by Def2;
    thus ||.h*seq.||.n = ||.(h*seq).n.|| by NORMSP_1:def 10
      .= ||.h/.(seq.n).|| by A1,Th8
      .= ||.h.||.(seq.n) by A3,Def2
      .= ||.h.||/.(seq.n) by A3,PARTFUN1:def 8
      .= (||.h.||*seq).n by A2,Th9;
  end;
  hence ||.h*seq.|| = (||.h.||)*seq by FUNCT_2:113;
  thus -(h*seq) =(-1)*(h*seq) by Th2
    .= ((-1)(#)h)*seq by A1,Th20
    .= (-h)*seq by VFUNCT_1:29;
end;

theorem
  f1 is_continuous_in x0 & f2 is_continuous_in x0 implies
  f1+f2 is_continuous_in x0 & f1-f2 is_continuous_in x0
proof
  assume
A1: f1 is_continuous_in x0 & f2 is_continuous_in x0;
  then
A2: x0 in dom f1 &
  for s1 st rng s1 c= dom f1 & s1 is convergent & lim s1 = x0 holds
  f1*s1 is convergent & f1/.x0 = lim (f1*s1) by Def9;
A3: x0 in dom f2 &
  for s1 st rng s1 c= dom f2 & s1 is convergent & lim s1 = x0 holds
  f2*s1 is convergent & f2/.x0 = lim (f2*s1) by A1,Def9;
  now
    x0 in dom f1 /\ dom f2 by A2,A3,XBOOLE_0:def 3;
    hence
A4: x0 in dom (f1+f2) by VFUNCT_1:def 1;
    let s1;
    assume
A5: rng s1 c= dom(f1+f2) & s1 is convergent & lim s1=x0;
    then
A6: rng s1 c= dom f1 /\ dom f2 by VFUNCT_1:def 1;
    dom (f1+f2) = dom f1 /\ dom f2 by VFUNCT_1:def 1;
    then dom (f1+f2) c= dom f1 by XBOOLE_1:17;
    then rng s1 c= dom f1 by A5,XBOOLE_1:1;
    then
A7: f1*s1 is convergent & f1/.x0 = lim (f1*s1) by A1,A5,Def9;
    dom (f1+f2) = dom f1 /\ dom f2 by VFUNCT_1:def 1;
    then dom (f1+f2) c= dom f2 by XBOOLE_1:17;
    then rng s1 c= dom f2 by A5,XBOOLE_1:1;
    then
A8: f2*s1 is convergent & f2/.x0 = lim (f2*s1) by A1,A5,Def9;
    then (f1*s1)+(f2*s1) is convergent by A7,NORMSP_1:34;
    hence (f1+f2)*s1 is convergent by A6,Th19;
    thus (f1+f2)/.x0 = f1/.x0 + f2/.x0 by A4,VFUNCT_1:def 1
      .= lim (f1*s1 + f2*s1) by A7,A8,NORMSP_1:42
      .= lim ((f1+f2)*s1) by A6,Th19;
  end;
  hence f1+f2 is_continuous_in x0 by Def9;
  now x0 in dom f1 /\ dom f2 by A2,A3,XBOOLE_0:def 3;
    hence
A9: x0 in dom (f1-f2) by VFUNCT_1:def 2;
    let s1;
    assume
A10: rng s1 c= dom(f1-f2) & s1 is convergent & lim s1=x0;
    then
A11: rng s1 c= dom f1 /\ dom f2 by VFUNCT_1:def 2;
    dom (f1-f2) = dom f1 /\ dom f2 by VFUNCT_1:def 2;
    then dom (f1-f2) c= dom f1 by XBOOLE_1:17;
    then rng s1 c= dom f1 by A10,XBOOLE_1:1;
    then
A12: f1*s1 is convergent & f1/.x0 = lim (f1*s1) by A1,A10,Def9;
    dom (f1-f2) = dom f1 /\ dom f2 by VFUNCT_1:def 2;
    then dom (f1-f2) c= dom f2 by XBOOLE_1:17;
    then rng s1 c= dom f2 by A10,XBOOLE_1:1;
    then
A13: f2*s1 is convergent & f2/.x0 = lim (f2*s1) by A1,A10,Def9;
    then f1*s1-f2*s1 is convergent by A12,NORMSP_1:35;
    hence (f1-f2)*s1 is convergent by A11,Th19;
    thus (f1-f2)/.x0 = f1/.x0 - f2/.x0 by A9,VFUNCT_1:def 2
      .= lim (f1*s1 - f2*s1) by A12,A13,NORMSP_1:43
      .= lim ((f1-f2)*s1) by A11,Th19;
  end;
  hence f1-f2 is_continuous_in x0 by Def9;
end;

theorem Th23:
  f is_continuous_in x0 implies r(#)f is_continuous_in x0
proof
  assume
A1: f is_continuous_in x0;
  then x0 in dom f &
  for s1 st rng s1 c= dom f & s1 is convergent & lim s1 = x0 holds
  f*s1 is convergent & f/.x0 = lim (f*s1) by Def9;
  hence
A2: x0 in dom (r(#)f) by VFUNCT_1:def 4;
  let s1;
  assume
A3: rng s1 c= dom(r(#)f) & s1 is convergent & lim s1=x0;
  then
A4: rng s1 c= dom f by VFUNCT_1:def 4;
  then
A5: f*s1 is convergent & f/.x0 = lim (f*s1) by A1,A3,Def9;
  then r*(f*s1) is convergent by NORMSP_1:37;
  hence (r(#)f)*s1 is convergent by A4,Th20;
  thus (r(#)f)/.x0 = r*f/.x0 by A2,VFUNCT_1:def 4
    .= lim (r*(f*s1)) by A5,NORMSP_1:45
    .= lim ((r(#)f)*s1) by A4,Th20;
end;

theorem
  f is_continuous_in x0 implies ||. f .|| is_continuous_in x0 &
  -f is_continuous_in x0
proof
  assume
A1: f is_continuous_in x0;
  then
A2: x0 in dom f &
  for s1 st rng s1 c= dom f & s1 is convergent & lim s1 = x0 holds
  f*s1 is convergent & f/.x0 = lim (f*s1) by Def9;
  now thus
A3: x0 in dom (||.f .||) by A2,Def2;
    let s1;
    assume
A4: rng s1 c= dom(||.f .||) & s1 is convergent & lim s1=x0;
    then
A5: rng s1 c= dom f by Def2;
    then
A6: f*s1 is convergent & f/.x0 = lim (f*s1) by A1,A4,Def9;
    then ||.f*s1.|| is convergent by NORMSP_1:39;
    hence ||.f.||*s1 is convergent by A5,Th21; thus
    ||.f.||/.x0 =||.f.||.x0 by A3,PARTFUN1:def 8
      .= ||.f/.x0 .|| by A3,Def2
      .= lim ||.f*s1 .|| by A6,LOPBAN_1:47
      .= lim (||.f.|| *s1) by A5,Th21;
  end;
  hence ||.f .|| is_continuous_in x0 by Def10;
  -f=(-1)(#)f by VFUNCT_1:29;
  hence thesis by A1,Th23;
end;

definition
  let S,T;
  let f,X;
  pred f is_continuous_on X means
  :Def11:
  X c= dom f & for x0 st x0 in X holds f|X is_continuous_in x0;
end;

definition
  let S;
  let f be PartFunc of the carrier of S, REAL;
  let X;
  pred f is_continuous_on X means
  :Def12:
  X c= dom f & for x0 st x0 in X holds f|X is_continuous_in x0;
end;

theorem Th25:
  for X,f holds f is_continuous_on X iff X c= dom f &
  for s1 st rng s1 c= X & s1 is convergent & lim s1 in X holds
  f*s1 is convergent & f/.(lim s1) = lim (f*s1)
proof
  let X,f;
  thus f is_continuous_on X implies X c= dom f &
  for s1 st rng s1 c= X & s1 is convergent & lim s1 in X holds
  f*s1 is convergent & f/.(lim s1) = lim (f*s1)
  proof
    assume
A1: f is_continuous_on X;
    then
A2: X c= dom f & for x1 st x1 in X holds f|X is_continuous_in x1 by Def11;
    now
      let s1 such that
A3:   rng s1 c= X & s1 is convergent & lim s1 in X;
A4:   dom (f|X) = dom f /\ X by PARTFUN2:32
        .= X by A2,XBOOLE_1:28;
A5:   f|X is_continuous_in (lim s1) by A1,A3,Def11;
      now
        let n;
        dom s1 = NAT by FUNCT_2:def 1;
        then
A6:     s1.n in rng s1 by FUNCT_1:12;
A7:     rng s1 c= dom f by A2,A3,XBOOLE_1:1; thus
        ((f|X)*s1).n = (f|X)/.(s1.n) by A3,A4,Th8
          .= f/.(s1.n) by A3,A4,A6,PARTFUN2:32
          .= (f*s1).n by A7,Th8;
      end;
      then
A8:   (f|X)*s1 = f*s1 by FUNCT_2:113;
      f/.(lim s1) = (f|X)/.(lim s1) by A3,A4,PARTFUN2:32
        .= lim (f*s1) by A3,A4,A5,A8,Def9;
      hence f*s1 is convergent & f/.(lim s1) = lim (f*s1) by A3,A4,A5,A8,Def9;
    end;
    hence thesis by A1,Def11;
  end;
  assume
A9: X c= dom f & for s1 st rng s1 c= X & s1 is convergent & lim s1 in X holds
  f*s1 is convergent & f/.(lim s1) = lim (f*s1);
  now
    let x1 such that
A10: x1 in X;
A11: dom (f|X) = dom f /\ X by PARTFUN2:32
      .= X by A9,XBOOLE_1:28;
    now
      let s1 such that
A12:  rng s1 c= dom (f|X) & s1 is convergent & lim s1=x1;
      now
        let n;
        dom s1 = NAT by FUNCT_2:def 1;
        then
A13:    s1.n in rng s1 by FUNCT_1:12;
A14:    rng s1 c= dom f by A9,A11,A12,XBOOLE_1:1; thus
        ((f|X)*s1).n = (f|X)/.(s1.n) by A12,Th8
          .= f/.(s1.n) by A12,A13,PARTFUN2:32
          .= (f*s1).n by A14,Th8;
      end;
      then
A15:  (f|X)*s1 = f*s1 by FUNCT_2:113;
      (f|X)/.(lim s1) = f/.(lim s1) by A10,A11,A12,PARTFUN2:32
        .= lim ((f|X)*s1) by A9,A10,A11,A12,A15;

hence (f|X)*s1 is convergent & (f|X)/.x1 = lim ((f|X)*s1) by A9,A10,A11,A12,A15
      ;
    end;
    hence f|X is_continuous_in x1 by A10,A11,Def9;
  end;
  hence thesis by A9,Def11;
end;

theorem Th26:
  f is_continuous_on X iff X c= dom f &
  for x0,r st x0 in X & 0<r ex s st 0<s & for x1 st x1 in X &
  ||. x1- x0 .|| < s holds ||. f/.x1 - f/.x0 .|| < r
proof
  thus f is_continuous_on X implies X c= dom f &
  for x0,r st x0 in X & 0<r ex s st 0<s & for x1 st x1 in X &
  ||. x1- x0 .|| < s holds ||. f/.x1 - f/.x0 .|| < r
  proof
    assume
A1: f is_continuous_on X;
    then
A2: X c= dom f & for x1 st x1 in X holds f|X is_continuous_in x1
    by Def11; thus X c= dom f by A1,Def11;
    let x0,r;
    assume
A3: x0 in X & 0<r;
    then f|X is_continuous_in x0 by A1,Def11;
    then consider s such that
A4: 0<s & for x1 st x1 in dom(f|X) & ||. x1- x0 .|| <s holds
    ||. (f|X)/.x1-(f|X)/.x0 .||<r by A3,Th14;
A5: dom (f|X) = dom f /\ X by PARTFUN2:32
      .= X by A2,XBOOLE_1:28;
    take s;
    thus 0<s by A4;
    let x1;
    assume
A6: x1 in X & ||. x1- x0 .|| <s;
    then ||. f/.x1 - f/.x0 .|| = ||. (f|X)/.x1 - f/.x0 .|| by A5,PARTFUN2:32
      .= ||. (f|X)/.x1 - (f|X)/.x0 .|| by A3,A5,PARTFUN2:32;
    hence thesis by A4,A5,A6;
  end;
  assume that
A7: X c= dom f and
A8: for x0,r st x0 in X & 0<r ex s st 0<s & for x1 st x1 in X &
  ||. x1- x0 .|| < s holds ||. f/.x1 - f/.x0 .|| < r;
A9: dom (f|X) = dom f /\ X by PARTFUN2:32
    .= X by A7,XBOOLE_1:28;
  now
    let x0 such that
A10: x0 in X;
    for r st 0<r ex s st 0<s
    & for x1 st x1 in dom(f|X) & ||. x1- x0 .|| <s holds
    ||. (f|X)/.x1-(f|X)/.x0 .||<r
    proof
      let r;
      assume 0<r;
      then consider s such that
A11:  0<s & for x1 st x1 in X &
      ||. x1- x0 .|| < s holds ||. f/.x1 - f/.x0 .|| < r by A8,A10;
      take s;
      thus 0<s by A11;
      let x1 such that
A12:  x1 in dom(f|X) & ||. x1- x0 .|| <s;
      ||. (f|X)/.x1-(f|X)/.x0.|| = ||. (f|X)/.x1 - f/.x0.||
      by A9,A10,PARTFUN2:32
        .= ||. f/.x1 - f/.x0 .|| by A12,PARTFUN2:32;
      hence thesis by A9,A11,A12;
    end;
    hence f|X is_continuous_in x0 by A9,A10,Th14;
  end;
  hence thesis by A7,Def11;
end;

theorem
  for f be PartFunc of the carrier of S, REAL holds
  ( f is_continuous_on X iff X c= dom f &
  for x0,r st x0 in X & 0<r ex s st 0<s & for x1 st x1 in X &
  ||. x1- x0 .|| < s holds abs( f/.x1 - f/.x0 ) < r )
proof
  let f be PartFunc of the carrier of S, REAL;
  thus f is_continuous_on X implies X c= dom f &
  for x0,r st x0 in X & 0<r ex s st 0<s & for x1 st x1 in X &
  ||. x1- x0 .|| < s holds abs( f/.x1 - f/.x0 ) < r
  proof
    assume
A1: f is_continuous_on X;
    then
A2: X c= dom f & for x1 st x1 in X holds f|X is_continuous_in x1
    by Def12; thus X c= dom f by A1,Def12;
    let x0,r;
    assume
A3: x0 in X & 0<r;
    then f|X is_continuous_in x0 by A1,Def12;
    then consider s such that
A4: 0<s & for x1 st x1 in dom(f|X) & ||. x1- x0 .|| <s holds
    abs( (f|X)/.x1-(f|X)/.x0 )<r by A3,Th15;
A5: dom (f|X) = dom f /\ X by PARTFUN2:32
      .= X by A2,XBOOLE_1:28;
    take s;
    thus 0<s by A4;
    let x1;
    assume
A6: x1 in X & ||. x1- x0 .|| <s;
    then abs( f/.x1 - f/.x0 ) = abs( (f|X)/.x1 - f/.x0 ) by A5,PARTFUN2:32
      .= abs( (f|X)/.x1 - (f|X)/.x0 ) by A3,A5,PARTFUN2:32;
    hence thesis by A4,A5,A6;
  end;
  assume that
A7: X c= dom f and
A8: for x0,r st x0 in X & 0<r ex s st 0<s & for x1 st x1 in X &
  ||. x1- x0 .|| < s holds abs( f/.x1 - f/.x0 ) < r;
A9: dom (f|X) = dom f /\ X by PARTFUN2:32
    .= X by A7,XBOOLE_1:28;
  now
    let x0 such that
A10: x0 in X;
    for r st 0<r ex s st 0<s
    & for x1 st x1 in dom(f|X) & ||. x1- x0 .|| <s holds
    abs( (f|X)/.x1-(f|X)/.x0 )<r
    proof
      let r;
      assume 0<r;
      then consider s such that
A11:  0<s & for x1 st x1 in X &
      ||. x1- x0 .|| < s holds abs( f/.x1 - f/.x0 ) < r by A8,A10;
      take s;
      thus 0<s by A11;
      let x1 such that
A12:  x1 in dom(f|X) & ||. x1- x0 .|| <s;
      abs( (f|X)/.x1-(f|X)/.x0 ) = abs( (f|X)/.x1 - f/.x0 )
      by A9,A10,PARTFUN2:32
        .= abs( f/.x1 - f/.x0 ) by A12,PARTFUN2:32;
      hence thesis by A9,A11,A12;
    end;
    hence f|X is_continuous_in x0 by A9,A10,Th15;
  end;
  hence thesis by A7,Def12;
end;

theorem
  f is_continuous_on X iff f|X is_continuous_on X
proof
  thus f is_continuous_on X implies f|X is_continuous_on X
  proof
    assume
A1: f is_continuous_on X;
    then X c= dom f & for r be Point of S st r in X
    holds f|X is_continuous_in r by Def11;
    then X c= dom f /\ X by XBOOLE_1:28;
    hence X c= dom (f|X) by RELAT_1:90;
    let r be Point of S;
    assume r in X;
    then f|X is_continuous_in r by A1,Def11;
    hence (f|X)|X is_continuous_in r by RELAT_1:101;
  end;
  assume
A2: f|X is_continuous_on X;
  then
A3: X c= dom(f|X) & for r be Point of S st r in X
  holds (f|X)|X is_continuous_in r by Def11;
A4: dom f /\ X c= dom f by XBOOLE_1:17;
  X c= dom f /\ X by A3,RELAT_1:90;
  hence X c= dom f by A4,XBOOLE_1:1;
  let r be Point of S;
  assume r in X;
  then (f|X)|X is_continuous_in r by A2,Def11;
  hence f|X is_continuous_in r by RELAT_1:101;
end;

theorem Th29:
  for f be PartFunc of the carrier of S,REAL holds
  (f is_continuous_on X iff f|X is_continuous_on X)
proof
  let f be PartFunc of the carrier of S,REAL;
  thus f is_continuous_on X implies f|X is_continuous_on X
  proof
    assume
A1: f is_continuous_on X;
    then X c= dom f & for r be Point of S st r in X
    holds f|X is_continuous_in r by Def12;
    then X c= dom f /\ X by XBOOLE_1:28;
    hence X c= dom (f|X) by RELAT_1:90;
    let r be Point of S;
    assume r in X;
    then f|X is_continuous_in r by A1,Def12;
    hence (f|X)|X is_continuous_in r by RELAT_1:101;
  end;
  assume
A2: f|X is_continuous_on X;
  then
A3: X c= dom(f|X) & for r be Point of S st r in X
  holds (f|X)|X is_continuous_in r by Def12;
A4: dom f /\ X c= dom f by XBOOLE_1:17;
  X c= dom f /\ X by A3,RELAT_1:90;
  hence X c= dom f by A4,XBOOLE_1:1;
  let r be Point of S;
  assume r in X;
  then (f|X)|X is_continuous_in r by A2,Def12;
  hence f|X is_continuous_in r by RELAT_1:101;
end;

theorem Th30:
  f is_continuous_on X & X1 c= X implies f is_continuous_on X1
proof
  assume
A1: f is_continuous_on X & X1 c= X;
  then X c= dom f by Def11;
  hence
A2: X1 c= dom f by A1,XBOOLE_1:1;
  let r be Point of S;
  assume
A3: r in X1;
  then
A4: f|X is_continuous_in r by A1,Def11;
  thus f|X1 is_continuous_in r
  proof r in dom f /\ X1 by A2,A3,XBOOLE_0:def 3;
    hence
A5: r in dom (f|X1) by RELAT_1:90;
    let s1 such that
A6: rng s1 c= dom (f|X1) & s1 is convergent & lim s1 = r;
    dom f /\ X1 c= dom f /\ X by A1,XBOOLE_1:26;
    then dom (f|X1) c= dom f /\ X by RELAT_1:90;
    then
A7: dom (f|X1) c= dom (f|X) by RELAT_1:90;
    then
A8: rng s1 c= dom (f|X) by A6,XBOOLE_1:1;
    then
A9: (f|X)*s1 is convergent & (f|X)/.r = lim ((f|X)*s1) by A4,A6,Def9;
A10: (f|X)/.r = f/.r by A5,A7,PARTFUN2:32
      .= (f|X1)/.r by A5,PARTFUN2:32;
    now
      let n;
      dom s1 = NAT by FUNCT_2:def 1;
      then
A11:  s1.n in rng s1 by FUNCT_1:12;
      thus ((f|X)*s1).n = (f|X)/.(s1.n) by A6,A7,Th8,XBOOLE_1:1
        .= f/.(s1.n) by A8,A11,PARTFUN2:32
        .= (f|X1)/.(s1.n) by A6,A11,PARTFUN2:32
        .= ((f|X1)*s1).n by A6,Th8;
    end;
    hence (f|X1)*s1 is convergent & (f|X1)/.r
    = lim ((f|X1)*s1) by A9,A10,FUNCT_2:113;
  end;
end;

theorem
  x0 in dom f implies f is_continuous_on {x0}
proof
  assume
A1: x0 in dom f;
  thus {x0} c= dom f
  proof
    let x;
    assume x in {x0};
    hence x in dom f by A1,TARSKI:def 1;
  end;
  let p be Point of S such that
A2: p in {x0};
  thus f|{x0} is_continuous_in p
  proof
A3: p=x0 by A2,TARSKI:def 1;
    p in dom f by A1,A2,TARSKI:def 1;
    then p in dom f /\ {x0} by A2,XBOOLE_0:def 3;
    hence p in dom (f|{x0}) by RELAT_1:90;
    let s1;
    assume
A4: rng s1 c= dom(f|{x0}) & s1 is convergent & lim s1=p;
    then
A5: rng s1 c= dom f /\ {x0} by RELAT_1:90;
    dom f /\ {x0} c= {x0} by XBOOLE_1:17;
    then
A6: rng s1 c= {x0} by A5,XBOOLE_1:1;
A7: now
      let n;
      dom s1 = NAT by FUNCT_2:def 1;
      then s1.n in rng s1 by FUNCT_1:12;
      hence s1.n = x0 by A6,TARSKI:def 1;
    end;
A8: now
      let g be Real such that
A9:   0<g;
      take n=0;
      let m such that n<=m;
      ||. ((f|{x0})*s1).m - (f|{x0})/.p.||
      = ||. (f|{x0})/.(s1.m) - (f|{x0})/.x0 .|| by A3,A4,Th8
        .= ||. (f|{x0})/.x0 - (f|{x0})/.x0.|| by A7
        .= ||. 0.T .|| by RLVECT_1:28
        .= 0 by NORMSP_1:5;
      hence ||. ((f|{x0})*s1).m - (f|{x0})/.p .|| < g by A9;
    end;
    hence (f|{x0})*s1 is convergent by NORMSP_1:def 9;
    hence (f|{x0})/.p = lim ((f|{x0})*s1) by A8,NORMSP_1:def 11;
  end;
end;

theorem Th32:
  for X,f1,f2 st f1 is_continuous_on X & f2 is_continuous_on X holds
  f1+f2 is_continuous_on X & f1-f2 is_continuous_on X
proof
  let X,f1,f2;
  assume that
A1: f1 is_continuous_on X and
A2: f2 is_continuous_on X;
A3: X c= dom f1 by A1,Th25;
  X c= dom f2 by A2,Th25;
  then
A4: X c= dom f1 /\ dom f2 by A3,XBOOLE_1:19;
  then
A5: X c= dom (f1+f2) by VFUNCT_1:def 1;
A6: X c= dom (f1-f2) by A4,VFUNCT_1:def 2;
  now
    let s1;
    assume
A7: rng s1 c= X & s1 is convergent & lim s1 in X;
    then
A8: rng s1 c= dom f1 /\ dom f2 by A4,XBOOLE_1:1;
A9: f1*s1 is convergent & f1/.(lim s1) = lim (f1*s1) by A1,A7,Th25;
A10: f2*s1 is convergent & f2/.(lim s1) = lim (f2*s1) by A2,A7,Th25;
    then
A11: (f1*s1)+(f2*s1) is convergent by A9,NORMSP_1:34;
    (f1+f2)/.(lim s1) = lim (f1*s1) + lim (f2*s1)
    by A5,A7,A9,A10,VFUNCT_1:def 1
      .= lim (f1*s1 + f2*s1) by A9,A10,NORMSP_1:42
      .= lim ((f1+f2)*s1) by A8,Th19;

hence (f1+f2)*s1 is convergent & (f1+f2)/.(lim s1)=lim((f1+f2)*s1) by A8,A11,
    Th19;
  end;
  hence f1+f2 is_continuous_on X by A5,Th25;
  now
    let s1;
    assume
A12: rng s1 c= X & s1 is convergent & lim s1 in X;
    then
A13: rng s1 c= dom f1 /\ dom f2 by A4,XBOOLE_1:1;
A14: f1*s1 is convergent & f1/.(lim s1) = lim (f1*s1) by A1,A12,Th25;
A15: f2*s1 is convergent & f2/.(lim s1) = lim (f2*s1) by A2,A12,Th25;
    then
A16: (f1*s1)-(f2*s1) is convergent by A14,NORMSP_1:35;
    (f1-f2)/.(lim s1) = lim (f1*s1) - lim (f2*s1)
    by A6,A12,A14,A15,VFUNCT_1:def 2
      .= lim (f1*s1 - f2*s1) by A14,A15,NORMSP_1:43
      .= lim ((f1-f2)*s1) by A13,Th19;
    hence (f1-f2)*s1 is convergent
    & (f1-f2)/.(lim s1)=lim((f1-f2)*s1) by A13,A16,Th19;
  end;
  hence f1-f2 is_continuous_on X by A6,Th25;
end;

theorem
  for X,X1,f1,f2 st f1 is_continuous_on X & f2 is_continuous_on X1 holds
  f1+f2 is_continuous_on X /\ X1 & f1-f2 is_continuous_on X /\ X1
proof
  let X,X1,f1,f2;
  assume f1 is_continuous_on X & f2 is_continuous_on X1;
  then f1 is_continuous_on X /\ X1 & f2 is_continuous_on X /\ X1 by Th30,
  XBOOLE_1:17;
  hence thesis by Th32;
end;

theorem Th34:
  for r,X,f st f is_continuous_on X holds r(#)f is_continuous_on X
proof
  let r,X,f;
  assume
A1: f is_continuous_on X;
  then
A2: X c= dom f by Th25;
  then
A3: X c= dom(r(#)f) by VFUNCT_1:def 4;
  now
    let s1;
    assume
A4: rng s1 c= X & s1 is convergent & lim s1 in X;
    then
A5: f*s1 is convergent & f/.(lim s1) = lim (f*s1) by A1,Th25;
    then
A6: r*(f*s1) is convergent by NORMSP_1:37;
    (r(#)f)/.(lim s1) = r * lim (f*s1) by A3,A4,A5,VFUNCT_1:def 4
      .= lim (r*(f*s1)) by A5,NORMSP_1:45
      .= lim ((r(#)f)*s1) by A2,A4,Th20,XBOOLE_1:1;

hence (r(#)f)*s1 is convergent & (r(#)f)/.(lim s1)=lim((r(#)f)*s1) by A2,A4,A6,
    Th20,XBOOLE_1:1;
  end;
  hence thesis by A3,Th25;
end;

theorem Th35:
  f is_continuous_on X implies
  ||.f.|| is_continuous_on X & -f is_continuous_on X
proof
  assume
A1: f is_continuous_on X;
  thus ||.f.|| is_continuous_on X
  proof
A2: X c= dom f by A1,Def11;
    hence
A3: X c= dom (||.f.|| ) by Def2;
    let r be Point of S;
    assume
A4: r in X;
    then
A5: f|X is_continuous_in r by A1,Def11;
    thus (||.f.|| )|X is_continuous_in r
    proof
A6:   r in dom (||.f.|| ) /\ X by A3,A4,XBOOLE_0:def 3;
      hence r in dom ((||.f.|| )|X) by RELAT_1:90;
      let s1;
      assume
A7:   rng s1 c= dom ((||.f.|| )|X) & s1 is convergent & lim s1 = r;
      then rng s1 c= dom (||.f.|| ) /\ X by RELAT_1:90;
      then rng s1 c= dom f /\ X by Def2;
      then
A8:   rng s1 c= dom (f|X) by RELAT_1:90;
      then
A9:   (f|X)*s1 is convergent & (f|X)/.r = lim ((f|X)*s1) by A5,A7,Def9;
      now
        let n;
        dom s1 = NAT by FUNCT_2:def 1;
        then
A10:    s1.n in rng s1 by FUNCT_1:12;
        then s1.n in dom (f|X) by A8;
        then s1.n in dom f /\ X by RELAT_1:90;
        then
A11:    s1.n in dom f & s1.n in X by XBOOLE_0:def 3;
        then
A12:    s1.n in dom (||.f.|| ) & s1.n in X by Def2;
        thus (||.(f|X)*s1.||).n = ||. ((f|X)*s1).n .|| by NORMSP_1:def 10
          .=||.(f|X)/.(s1.n).|| by A8,Th8
          .=||.f/.(s1.n).|| by A8,A10,PARTFUN2:32
          .=(||.f.|| ).(s1.n) by A12,Def2
          .=((||.f.|| )|X).(s1.n) by A11,FUNCT_1:72
          .=((||.f.|| )|X)/.(s1.n) by A7,A10,PARTFUN1:def 8
          .=(((||.f.|| )|X)*s1).n by A7,Th9;
      end;
      then
A13:  ||.(f|X)*s1.|| = ((||.f.|| )|X)*s1 by FUNCT_2:113;
      hence ((||.f.|| )|X)*s1 is convergent by A9,NORMSP_1:39;
      ||.(f|X)/.r.|| = ||.f/.r.|| by A2,A4,PARTFUN2:35
        .= (||.f.|| ).r by A3,A4,Def2
        .= (||.f.|| )/.r by A3,A4,PARTFUN1:def 8
        .= ((||.f.|| )|X)/.r by A6,PARTFUN2:34;
      hence ((||.f.|| )|X)/.r = lim (((||.f.|| )|X)*s1) by A9,A13,LOPBAN_1:24;
    end;
  end;
  (-1)(#)f is_continuous_on X by A1,Th34;
  hence thesis by VFUNCT_1:29;
end;

theorem
  f is total & (for x1,x2 holds f/.(x1+x2) = f/.x1 + f/.x2) &
  (ex x0 st f is_continuous_in x0) implies f is_continuous_on the carrier of S
proof
  assume that
A1: f is total and
A2: for x1,x2 holds f/.(x1+x2) = f/.x1 + f/.x2;
  given x0 such that
A3: f is_continuous_in x0;
A4: dom f = the carrier of S by A1,PARTFUN1:def 4;
  f/.x0 + 0.T =f/.x0 by RLVECT_1:10
    .= f/.(x0+0.S) by RLVECT_1:10
    .= f/.x0+f/.0.S by A2;
  then
A5: f/.0.S = 0.T by RLVECT_1:21;
A6: now
    let x1;
    0.T = f/.(x1+-x1) by A5,RLVECT_1:16
      .= f/.x1+f/.(-x1) by A2;
    hence -(f/.x1)=f/.(-x1) by RLVECT_1:19;
  end;
A7: now
    let x1,x2;
    thus f/.(x1-x2)= f/.x1 + f/.(-x2) by A2
      .= f/.x1 - f/.x2 by A6;
  end;
  now
    let x1;
    let r be Real;
    assume x1 in the carrier of S & r>0;
    then consider s such that
A8: 0<s & for x1 st x1 in dom f & ||. x1- x0 .|| <s holds
    ||. f/.x1- f/.x0 .|| <r by A3,Th14;
    take s; thus s>0 by A8;
    let x2 such that
A9: x2 in the carrier of S & ||. x2-x1 .||<s;
    set y=x1-x0;
A10: y+x0 =x1-(x0-x0) by RLVECT_1:43
      .=x1- 0.S by RLVECT_1:28
      .=x1 by RLVECT_1:26;
    then
A11: ||.f/.x2-f/.x1.|| = ||.f/.x2-(f/.y+f/.x0).|| by A2
      .= ||.f/.x2-f/.y-f/.x0 .|| by RLVECT_1:41
      .= ||.f/.(x2-y)-f/.x0.|| by A7;
    ||.x2-y-x0.||=||.x2-x1.|| by A10,RLVECT_1:41;
    hence ||.f/.x2-f/.x1.||<r by A4,A8,A9,A11;
  end;
  hence thesis by A4,Th26;
end;

theorem Th37:
  for f st dom f is compact & f is_continuous_on (dom f)
  holds (rng f) is compact
proof
  let f;
  assume that
A1: dom f is compact and
A2: f is_continuous_on (dom f);
  now
    let s1 be sequence of T such that
A3: rng s1 c= rng f;
    defpred P[set,set] means $2 in dom f & f/.$2=s1.$1;
A4: for n ex p be Point of S st P[n,p]
    proof
      let n;
      dom s1 = NAT by FUNCT_2:def 1;
      then s1.n in rng s1 by FUNCT_1:12;
      then consider p be Point of S such that
A5:   p in dom f & s1.n =f.p by A3,PARTFUN1:26;
      take p; thus thesis by A5,PARTFUN1:def 8;
    end;
    consider q1 be sequence of S such that
A6: for n holds P[n,q1.n] from FUNCT_2:sch 3(A4);
    now
      let x;
      assume x in rng q1;
      then ex n st x = q1.n by Th6;
      hence x in dom f by A6;
    end;
    then
A7: rng q1 c= dom f by TARSKI:def 3;
    now
      let n;
      q1.n in dom f & f/.(q1.n)=s1.n by A6;
      hence (f*q1).n= s1.n by A7,Th8;
    end;
    then
A8: f*q1=s1 by FUNCT_2:113;
    consider s2 such that
A9: s2 is subsequence of q1 & s2 is convergent & (lim s2) in dom f
    by A1,A7,Def4;
    f|(dom f) is_continuous_in (lim s2) by A2,A9,Def11;
    then
A10: f is_continuous_in (lim s2) by RELAT_1:97;
    rng s2 c= rng q1 by A9,Th7;
    then rng s2 c= dom f by A7,XBOOLE_1:1;
    then
A11: f*s2 is convergent & f/.(lim s2) = lim (f*s2) by A9,A10,Def9;
    take q2 = f*s2;
    thus q2 is subsequence of s1 & q2 is convergent &
    (lim q2) in rng f by A7,A8,A9,A11,Th12,PARTFUN2:4;
  end;
  hence thesis by Def4;
end;

theorem Th38:
  for f be PartFunc of the carrier of S, REAL
  st dom f is compact & f is_continuous_on dom f holds rng f is compact
proof
  let f be PartFunc of the carrier of S, REAL;
  assume that
A1: dom f is compact and
A2: f is_continuous_on dom f;
  now
    let s1 be Real_Sequence such that
A3: rng s1 c= rng f;
    defpred P[set,set] means $2 in dom f & f/.$2=s1.$1;
A4: for n ex p be Point of S st P[n,p]
    proof
      let n;
      dom s1 = NAT by FUNCT_2:def 1;
      then s1.n in rng s1 by FUNCT_1:12;
      then consider p be Point of S such that
A5:   p in dom f & s1.n =f.p by A3,PARTFUN1:26;
      take p; thus thesis by A5,PARTFUN1:def 8;
    end;
    consider q1 be sequence of S such that
A6: for n holds P[n,q1.n] from FUNCT_2:sch 3(A4);
    now
      let x;
      assume x in rng q1;
      then ex n st x = q1.n by Th6;
      hence x in dom f by A6;
    end;
    then
A7: rng q1 c= dom f by TARSKI:def 3;
    now
      let n;
      q1.n in dom f & f/.(q1.n)=s1.n by A6;
      hence (f*q1).n= s1.n by A7,Th9;
    end;
    then
A8: f*q1=s1 by FUNCT_2:113;
    consider s2 such that
A9: s2 is subsequence of q1 & s2 is convergent & (lim s2) in dom f
    by A1,A7,Def4;
    f|(dom f) is_continuous_in (lim s2) by A2,A9,Def12;
    then
A10: f is_continuous_in (lim s2) by RELAT_1:97;
    rng s2 c= rng q1 by A9,Th7;
    then rng s2 c= dom f by A7,XBOOLE_1:1;
    then
A11: f*s2 is convergent & f/.(lim s2) = lim (f*s2) by A9,A10,Def10;
    take q2 = f*s2;
    thus q2 is subsequence of s1 & q2 is convergent &
    (lim q2) in rng f by A7,A8,A9,A11,Th13,PARTFUN2:4;
  end;
  hence thesis by RCOMP_1:def 3;
end;

theorem
  Y c= dom f & Y is compact & f is_continuous_on Y implies (f.:Y) is compact
proof
  assume that
A1: Y c= dom f and
A2: Y is compact and
A3: f is_continuous_on Y;
A4: dom (f|Y) = dom f /\ Y by RELAT_1:90
    .= Y by A1,XBOOLE_1:28;
  f|Y is_continuous_on Y
  proof thus Y c= dom (f|Y) by A4;
    let r be Point of S;
    assume r in Y;
    then f|Y is_continuous_in r by A3,Def11;
    hence (f|Y)|Y is_continuous_in r by RELAT_1:101;
  end;
  then rng (f|Y) is compact by A2,A4,Th37;
  hence thesis by RELAT_1:148;
end;

theorem Th40:
  for f be PartFunc of the carrier of S,REAL
  st dom f<>{} & (dom f) is compact & f is_continuous_on (dom f)
  ex x1,x2 st x1 in dom f & x2 in dom f & f/.x1 = upper_bound (rng f) &
  f/.x2 = lower_bound (rng f)
proof
  let f be PartFunc of the carrier of S,REAL such that
A1: dom f <> {} and
A2: dom f is compact and
A3: f is_continuous_on (dom f);
A4: rng f <> {} by A1,RELAT_1:65;
  rng f is compact by A2,A3,Th38;
  then
A5: upper_bound (rng f) in rng f &
  lower_bound (rng f) in rng f by A4,RCOMP_1:32;
  then consider x being Element of the carrier of S such that
A6: x in dom f & upper_bound (rng f) = f.x by PARTFUN1:26;
  consider y being Element of the carrier of S such that
A7: y in dom f & lower_bound (rng f ) = f.y by A5,PARTFUN1:26;
  take x;
  take y;
  thus thesis by A6,A7,PARTFUN1:def 8;
end;

theorem Th41:
  for f st dom f<>{} & (dom f) is compact & f is_continuous_on (dom f)
  ex x1,x2 st x1 in dom f & x2 in dom f
  & ||.f.||/.x1 = upper_bound (rng ||.f.||) &
  ||.f.||/.x2 = lower_bound (rng ||.f.||)
proof
  let f such that
A1: dom f <> {} and
A2: dom f is compact and
A3: f is_continuous_on (dom f);
A4: dom ||.f.|| is compact by A2,Def2;
A5: dom f = dom ||.f.|| by Def2;
  then
A6: ||.f.|| is_continuous_on (dom ||.f.||) by A3,Th35;
A7: rng ||.f.|| <> {} by A1,A5,RELAT_1:65;
  rng ||.f.|| is compact by A4,A6,Th38;
  then
A8: upper_bound (rng ||.f.||) in rng ||.f.|| &
  lower_bound (rng ||.f.||) in rng ||.f.|| by A7,RCOMP_1:32;
  then consider x being Element of the carrier of S such that
A9: x in dom ||.f.|| & upper_bound (rng ||.f.||) = ||.f.||.x by PARTFUN1:26;
  consider y being Element of the carrier of S such that
A10: y in dom ||.f.|| &
  lower_bound (rng ||.f.||) = ||.f.||.y by A8,PARTFUN1:26;
  take x;
  take y;
  thus thesis by A9,A10,Def2,PARTFUN1:def 8;
end;

theorem Th42:
  (||.f.||)|X = ||.(f|X).||
proof
A1: dom ((||.f.||)|X) = dom (||.f.||) /\ X by RELAT_1:90
    .= dom f /\ X by Def2
    .= dom (f|X) by RELAT_1:90
    .= dom ( ||.(f|X).||) by Def2;
  now
    let c be Point of S;
    assume
A2: c in dom ((||.f.||)|X);
    then c in dom (||.f.||) /\ X by RELAT_1:90;
    then
A3: c in dom (||.f.||) & c in X by XBOOLE_0:def 3;
A4: c in dom (f|X) by A1,A2,Def2;
    thus ((||.f.||)|X).c = (||.f.||).c by A2,FUNCT_1:70
      .= ||.f/.c.|| by A3,Def2
      .= ||.(f|X)/.c.|| by A4,PARTFUN2:32
      .= ||.(f|X).||.c by A1,A2,Def2;
  end;
  hence thesis by A1,PARTFUN1:34;
end;

theorem
  for f,Y st Y<>{} & Y c= dom f & Y is compact & f is_continuous_on Y
  ex x1,x2 st x1 in Y & x2 in Y &

||.f.||/.x1 = upper_bound (||.f.||.:Y) & ||.f.||/.x2 = lower_bound (||.f.||.:Y)
proof
  let f,Y such that
A1: Y <> {} and
A2: Y c= dom f and
A3: Y is compact and
A4: f is_continuous_on Y;
A5: dom (f|Y) = dom f /\ Y by RELAT_1:90
    .= Y by A2,XBOOLE_1:28;
  f|Y is_continuous_on Y
  proof thus Y c= dom (f|Y) by A5;
    let r be Point of S;
    assume r in Y;
    then f|Y is_continuous_in r by A4,Def11;
    hence (f|Y)|Y is_continuous_in r by RELAT_1:101;
  end;
  then consider x1,x2 such that
A6: x1 in dom (f|Y) & x2 in dom (f|Y) &
  ||.(f|Y).||/.x1 = upper_bound (rng ||.(f|Y).||)
  & ||.(f|Y).||/.x2 = lower_bound (rng ||.(f|Y).||) by A1,A3,A5,Th41;
A7: dom f = dom ||.f.|| by Def2;
  take x1,x2; thus x1 in Y & x2 in Y by A5,A6;
A8: x1 in dom ||.(f|Y).|| & x2 in dom ||.(f|Y).|| by A6,Def2;
  then
A9: ||.(f|Y).||/.x1 =||.(f|Y).||.x1 by PARTFUN1:def 8
    .=||. (f|Y)/.x1.|| by A8,Def2
    .=||. f/.x1.|| by A6,PARTFUN2:32
    .=||. f.||.x1 by A2,A5,A6,A7,Def2
    .=||. f.||/.x1 by A2,A5,A6,A7,PARTFUN1:def 8;
A10: ||.(f|Y).||/.x2 =||.(f|Y).||.x2 by A8,PARTFUN1:def 8
    .=||. (f|Y)/.x2.|| by A8,Def2
    .=||. f/.x2.|| by A6,PARTFUN2:32
    .=||. f.||.x2 by A2,A5,A6,A7,Def2
    .=||. f.||/.x2 by A2,A5,A6,A7,PARTFUN1:def 8;
  ||.f.||.:Y =rng ( ||.f.|| | Y) by RELAT_1:148
    .=rng( ||.(f|Y).|| ) by Th42;
  hence thesis by A6,A9,A10;
end;

theorem
  for f be PartFunc of the carrier of S,REAL
  for Y st Y<>{} & Y c= dom f & Y is compact & f is_continuous_on Y
  ex x1,x2 st x1 in Y & x2 in Y &
  f/.x1 = upper_bound (f.:Y) & f/.x2 = lower_bound (f.:Y)
proof
  let f be PartFunc of the carrier of S,REAL;
  let Y such that
A1: Y <> {} and
A2: Y c= dom f and
A3: Y is compact and
A4: f is_continuous_on Y;
A5: dom (f|Y) = dom f /\ Y by RELAT_1:90
    .= Y by A2,XBOOLE_1:28;
  f|Y is_continuous_on Y
  proof thus Y c= dom (f|Y) by A5;
    let r be Point of S;
    assume r in Y;
    then f|Y is_continuous_in r by A4,Def12;
    hence (f|Y)|Y is_continuous_in r by RELAT_1:101;
  end;
  then consider x1,x2 such that
A6: x1 in dom (f|Y) & x2 in dom (f|Y) & (f|Y)/.x1 = upper_bound (rng (f|Y))
  & (f|Y)/.x2 = lower_bound (rng (f|Y)) by A1,A3,A5,Th40;
  take x1,x2; thus x1 in Y & x2 in Y by A5,A6;
A7: (f|Y)/.x1 =f/.x1 by A6,PARTFUN2:32;
  (f|Y)/.x2 =f/.x2 by A6,PARTFUN2:32;
  hence thesis by A6,A7,RELAT_1:148;
end;

definition
  let S,T;
  let X,f;
  pred f is_Lipschitzian_on X means
  :Def13:
  X c= dom f & ex r st 0<r & for x1,x2 st x1 in X & x2 in X holds
  ||.f/.x1-f/.x2.||<=r*||.x1-x2.||;
end;

definition
  let S;
  let X;
  let f be PartFunc of the carrier of S,REAL;
  pred f is_Lipschitzian_on X means
  :Def14:
  X c= dom f & ex r st 0<r & for x1,x2 st x1 in X & x2 in X holds
  abs(f/.x1-f/.x2)<=r*||.x1-x2.||;
end;

theorem Th45:
  f is_Lipschitzian_on X & X1 c= X implies f is_Lipschitzian_on X1
proof
  assume
A1: f is_Lipschitzian_on X & X1 c= X;
  then X c= dom f by Def13;
  hence X1 c= dom f by A1,XBOOLE_1:1;
  consider s such that
A2: 0<s & for x1,x2 st x1 in X & x2 in X
  holds ||. f/.x1-f/.x2.||<=s*||. x1-x2.|| by A1,Def13;
  take s; thus 0<s by A2;
  let x1,x2;
  assume x1 in X1 & x2 in X1;
  hence thesis by A1,A2;
end;

theorem
  f1 is_Lipschitzian_on X & f2 is_Lipschitzian_on X1 implies
  f1+f2 is_Lipschitzian_on X /\ X1
proof
  assume
A1: f1 is_Lipschitzian_on X & f2 is_Lipschitzian_on X1;
  then
A2: f1 is_Lipschitzian_on X /\ X1 by Th45,XBOOLE_1:17;
  then
A3: X /\ X1 c= dom f1 by Def13;
A4: f2 is_Lipschitzian_on X /\ X1 by A1,Th45,XBOOLE_1:17;
  then X /\ X1 c= dom f2 by Def13;
  then X /\ X1 c= dom f1 /\ dom f2 by A3,XBOOLE_1:19;
  hence
A5: X /\ X1 c= dom (f1+f2) by VFUNCT_1:def 1;
  consider s such that
A6: 0<s & for x1,x2 st x1 in X /\ X1 & x2 in X /\ X1 holds
  ||. f1/.x1-f1/.x2.||<=s*||. x1-x2 .|| by A2,Def13;
  consider g such that
A7: 0<g & for x1,x2 st x1 in X /\ X1 & x2 in X /\ X1 holds
  ||. f2/.x1-f2/.x2 .||<=g*||. x1-x2 .|| by A4,Def13;
  take p=s+g;
  0+0<s+g by A6,A7,XREAL_1:10;
  hence 0<p;
  let x1,x2;
  assume
A8: x1 in X/\X1 & x2 in X/\X1;
  then ||. (f1+f2)/.x1-(f1+f2)/.x2 .||
  = ||. f1/.x1 + f2/.x1-(f1+f2)/.x2.|| by A5,VFUNCT_1:def 1
    .= ||. f1/.x1 + f2/.x1 - (f1/.x2+f2/.x2) .|| by A5,A8,VFUNCT_1:def 1
    .= ||. f1/.x1 + (f2/.x1 - (f1/.x2+f2/.x2)).|| by RLVECT_1:def 6
    .= ||. f1/.x1 + (f2/.x1 - f1/.x2-f2/.x2).|| by RLVECT_1:41
    .= ||. f1/.x1 + (-f1/.x2 + f2/.x1-f2/.x2).||
    .= ||. f1/.x1 + (-f1/.x2 + (f2/.x1-f2/.x2)).|| by RLVECT_1:def 6
    .= ||. f1/.x1 - f1/.x2 + (f2/.x1-f2/.x2).|| by RLVECT_1:def 6;
  then
A9: ||. (f1+f2)/.x1-(f1+f2)/.x2.||
  <=||. f1/.x1-f1/.x2.||+||. f2/.x1-f2/.x2.|| by NORMSP_1:def 2;
A10: ||. f1/.x1-f1/.x2.||<=s*||. x1-x2.|| by A6,A8;
  ||. f2/.x1-f2/.x2.||<=g*||. x1-x2.|| by A7,A8;
  then ||. f1/.x1-f1/.x2.||+||. f2/.x1-f2/.x2.||<=
  s*||. x1-x2.||+g*||. x1-x2.|| by A10,XREAL_1:9;
  hence thesis by A9,XXREAL_0:2;
end;

theorem
  f1 is_Lipschitzian_on X & f2 is_Lipschitzian_on X1 implies
  f1-f2 is_Lipschitzian_on X /\ X1
proof
  assume
A1: f1 is_Lipschitzian_on X & f2 is_Lipschitzian_on X1;
  then
A2: f1 is_Lipschitzian_on X /\ X1 by Th45,XBOOLE_1:17;
  then
A3: X /\ X1 c= dom f1 by Def13;
A4: f2 is_Lipschitzian_on X /\ X1 by A1,Th45,XBOOLE_1:17;
  then X /\ X1 c= dom f2 by Def13;
  then X /\ X1 c= dom f1 /\ dom f2 by A3,XBOOLE_1:19;
  hence
A5: X /\ X1 c= dom (f1-f2) by VFUNCT_1:def 2;
  consider s such that
A6: 0<s & for x1,x2 st x1 in X /\ X1 & x2 in X /\ X1 holds
  ||. f1/.x1-f1/.x2.||<=s*||. x1-x2.|| by A2,Def13;
  consider g such that
A7: 0<g & for x1,x2 st x1 in X /\ X1 & x2 in X /\ X1 holds
  ||. f2/.x1-f2/.x2.||<=g*||. x1-x2.|| by A4,Def13;
  take p=s+g;
  0+0<s+g by A6,A7,XREAL_1:10;
  hence 0<p;
  let x1,x2;
  assume
A8: x1 in X/\X1 & x2 in X/\X1;
  then ||. (f1-f2)/.x1-(f1-f2)/.x2.||
  = ||. f1/.x1 - f2/.x1-(f1-f2)/.x2.|| by A5,VFUNCT_1:def 2
    .= ||. f1/.x1 - f2/.x1 - (f1/.x2-f2/.x2).|| by A5,A8,VFUNCT_1:def 2
    .= ||. f1/.x1 - (f2/.x1 + (f1/.x2-f2/.x2)).|| by RLVECT_1:41
    .= ||. f1/.x1 - (f1/.x2 + f2/.x1-f2/.x2).|| by RLVECT_1:def 6
    .= ||. f1/.x1 - (f1/.x2 + (f2/.x1-f2/.x2)).|| by RLVECT_1:def 6
    .= ||. f1/.x1 - f1/.x2 - (f2/.x1-f2/.x2).|| by RLVECT_1:41;
  then
A9: ||. (f1-f2)/.x1-(f1-f2)/.x2.|| <=||. f1/.x1-f1/.x2.||+||. f2/.x1-f2
  /.x2.|| by NORMSP_1:7;
A10: ||. f1/.x1-f1/.x2.||<=s*||. x1-x2.|| by A6,A8;
  ||. f2/.x1-f2/.x2.||<=g*||. x1-x2.|| by A7,A8;
  then ||. f1/.x1-f1/.x2.||+||. f2/.x1-f2/.x2.||<=
  s*||. x1-x2.||+g*||. x1-x2.|| by A10,XREAL_1:9;
  hence thesis by A9,XXREAL_0:2;
end;

theorem Th48:
  f is_Lipschitzian_on X implies p(#)f is_Lipschitzian_on X
proof
  assume
A1: f is_Lipschitzian_on X;
  then X c= dom f by Def13;
  hence
A2: X c= dom (p(#)f) by VFUNCT_1:def 4;
  consider s such that
A3: 0<s & for x1,x2 st x1 in X & x2 in X holds
  ||. f/.x1-f/.x2.||<=s*||. x1-x2.|| by A1,Def13;
  now per cases;
    suppose
A4:   p=0;
      take s; thus 0<s by A3;
      let x1,x2;
      assume
A5:   x1 in X & x2 in X;
      then
A6:   ||. (p(#)f)/.x1-(p(#)f)/.x2.||
      = ||. p*(f/.x1)-(p(#)f)/.x2.|| by A2,VFUNCT_1:def 4
        .= ||. 0.T-(p(#)f)/.x2.|| by A4,RLVECT_1:23
        .= ||. 0.T - p*(f/.x2).|| by A2,A5,VFUNCT_1:def 4
        .= ||. 0.T-0.T.|| by A4,RLVECT_1:23
        .= ||. 0.T.|| by RLVECT_1:26
        .= 0 by NORMSP_1:5;
      0<=||. x1-x2.|| by NORMSP_1:8;
      then s*0<=s*||. x1-x2.|| by A3,XREAL_1:66;
      hence ||. (p(#)f)/.x1-(p(#)f)/.x2.|| <=s*||. x1-x2.|| by A6;
    end;
    suppose p<>0;
      then 0<abs(p) by COMPLEX1:133;
      then
A7:   0*s<abs(p)*s by A3,XREAL_1:70;
      take g = abs(p) *s; thus 0<g by A7;
      let x1,x2;
      assume
A8:   x1 in X & x2 in X;
      then
A9:   ||. (p(#)f)/.x1-(p(#)f)/.x2.||
      = ||. p*(f/.x1)-(p(#)f)/.x2.|| by A2,VFUNCT_1:def 4
        .= ||. p*(f/.x1) - p*(f/.x2).|| by A2,A8,VFUNCT_1:def 4
        .= ||. p*(f/.x1 - f/.x2).|| by RLVECT_1:48
        .= abs(p)*||. f/.x1 - f/.x2.|| by NORMSP_1:def 2;
      0<=abs(p) by COMPLEX1:132;
      then abs(p)*||. f/.x1-f/.x2.||
      <=abs(p)*(s*||. x1-x2.||) by A3,A8,XREAL_1:66;
      hence ||. (p(#)f)/.x1-(p(#)f)/.x2.|| <= g*||. x1-x2.|| by A9;
    end;
  end;
  hence thesis;
end;

theorem
  f is_Lipschitzian_on X implies -f is_Lipschitzian_on X &
  ||.f.|| is_Lipschitzian_on X
proof
  assume
A1: f is_Lipschitzian_on X;
  -f=(-1)(#)f by VFUNCT_1:29;
  hence -f is_Lipschitzian_on X by A1,Th48;
  X c= dom f by A1,Def13;
  hence
A2: X c= dom (||.f.|| ) by Def2;
  consider s such that
A3: 0<s & for x1,x2 st x1 in X & x2 in X holds
  ||. f/.x1-f/.x2.||<=s*||. x1-x2.|| by A1,Def13;
  take s; thus 0<s by A3;
  let x1,x2;
  assume
A4: x1 in X & x2 in X;
  then abs( (||.f.|| )/.x1-(||.f.|| )/.x2 )
  =abs( (||.f.|| ).x1-(||.f.|| )/.x2 ) by A2,PARTFUN1:def 8
    .=abs( (||.f.|| ).x1-(||.f.|| ).x2 ) by A2,A4,PARTFUN1:def 8
    .= abs( ||. f/.x1.||-(||.f.|| ).x2 ) by A2,A4,Def2
    .= abs(||. f/.x1.|| - ||. f/.x2 .||) by A2,A4,Def2;
  then
A5: abs( (||.f.|| )/.x1-(||.f.|| )/.x2 ) <= ||. f/.x1-f/.x2.|| by NORMSP_1:13;
  ||. f/.x1-f/.x2 .||<=s*||. x1-x2.|| by A3,A4;
  hence thesis by A5,XXREAL_0:2;
end;

theorem Th50:
  X c= dom f & f is_constant_on X implies f is_Lipschitzian_on X
proof
  assume
A1: X c= dom f & f is_constant_on X;
  now
    let x1,x2;
    assume
A2: x1 in X & x2 in X;
    then
A3: x1 in X /\ dom f & x2 in X /\ dom f by A1,XBOOLE_0:def 3;
    f/.x1= f.x1 by A1,A2,PARTFUN1:def 8
      .= f.x2 by A1,A3,PARTFUN2:77
      .= f/.x2 by A1,A2,PARTFUN1:def 8;
    then ||. f/.x1-f/.x2.|| = ||. 0.T .|| by RLVECT_1:28
      .= 0 by NORMSP_1:5;
    hence ||. f/.x1-f/.x2.|| <= 1*||. x1-x2 .|| by NORMSP_1:8;
  end;
  hence thesis by A1,Def13;
end;

theorem
  id Y is_Lipschitzian_on Y
proof
  thus Y c= dom(id Y) by RELAT_1:71;
  reconsider r=1 as Real;
  take r;
  thus r>0;
  let x1,x2;
  assume
A1: x1 in Y & x2 in Y;
  then ||. (id Y)/.x1-(id Y)/.x2 .|| = ||. x1-(id Y)/.x2 .|| by PARTFUN2:12
    .= r*||. x1-x2 .|| by A1,PARTFUN2:12;
  hence thesis;
end;

theorem Th52:
  f is_Lipschitzian_on X implies f is_continuous_on X
proof
  assume
A1: f is_Lipschitzian_on X;
  then consider r such that
A2: 0<r & for x1,x2 st x1 in X & x2 in X holds
  ||. f/.x1-f/.x2.||<=r*||. x1-x2.|| by Def13;
A3: X c= dom f by A1,Def13;
  then
A4: dom (f|X) = X by RELAT_1:91;
  now
    let x0 such that
A5: x0 in X;
    now
      let g such that
A6:   0<g;
      set s=g/r;
      take s'=s;
A7:   0<r" by A2,XREAL_1:124;
A8:   s'=g*r" by XCMPLX_0:def 9;
      now
        let x1;
        assume
A9:     x1 in dom (f|X) & ||. x1- x0 .|| <s;
        then r*||. x1- x0 .|| <(g/r)*r by A2,XREAL_1:70;
        then
A10:    r*||. x1- x0 .|| <g by A2,XCMPLX_1:88;
        ||. f/.x1- f/.x0 .|| <=r*||. x1- x0 .|| by A2,A4,A5,A9;
        then ||. f/.x1- f/.x0 .|| <g by A10,XXREAL_0:2;
        then ||. (f|X)/.x1-f/.x0 .||<g by A9,PARTFUN2:32;
        hence ||. (f|X)/.x1-(f|X)/.x0 .||<g by A4,A5,PARTFUN2:32;
      end;
      hence 0<s' & for x1 st x1 in dom (f|X) & ||. x1- x0 .|| <s' holds
      ||. (f|X)/.x1-(f|X)/.x0.||<g by A6,A7,A8,XREAL_1:131;
    end;
    hence f|X is_continuous_in x0 by A4,A5,Th14;
  end;
  hence thesis by A3,Def11;
end;

theorem Th53:
  for f be PartFunc of the carrier of S, REAL st f is_Lipschitzian_on X
  holds f is_continuous_on X
proof
  let f be PartFunc of the carrier of S, REAL;
  assume
A1: f is_Lipschitzian_on X;
  then consider r such that
A2: 0<r & for x1,x2 st x1 in X & x2 in X holds
  abs(f/.x1-f/.x2)<=r*||. x1-x2.|| by Def14;
A3: X c= dom f by A1,Def14;
  then
A4: dom (f|X) = X by RELAT_1:91;
  now
    let x0 such that
A5: x0 in X;
    now
      let g such that
A6:   0<g;
      set s=g/r;
      take s'=s;
A7:   0<r" by A2,XREAL_1:124;
A8:   s'=g*r" by XCMPLX_0:def 9;
      now
        let x1;
        assume
A9:     x1 in dom (f|X) & ||. x1- x0 .|| <s;
        then r*||. x1- x0 .|| <(g/r)*r by A2,XREAL_1:70;
        then
A10:    r*||. x1- x0 .|| <g by A2,XCMPLX_1:88;
        abs( f/.x1- f/.x0 ) <=r*||. x1- x0 .|| by A2,A4,A5,A9;
        then abs( f/.x1- f/.x0 ) <g by A10,XXREAL_0:2;
        then abs( (f|X)/.x1-f/.x0 )<g by A9,PARTFUN2:32;
        hence abs( (f|X)/.x1-(f|X)/.x0 )<g by A4,A5,PARTFUN2:32;
      end;
      hence 0<s' & for x1 st x1 in dom (f|X) & ||. x1- x0 .|| <s' holds
      abs( (f|X)/.x1-(f|X)/.x0)<g by A6,A7,A8,XREAL_1:131;
    end;
    hence f|X is_continuous_in x0 by A4,A5,Th15;
  end;
  hence thesis by A3,Def12;
end;

theorem
  for f st (ex r be Point of T st rng f = {r})
  holds f is_continuous_on (dom f)
proof
  let f;
  given r be Point of T such that
A1: rng f = {r};
  now
    let x1,x2;
    assume
A2: x1 in dom f & x2 in dom f;
    then f.x1 in rng f & f.x2 in rng f by FUNCT_1:def 5;
    then f/.x1 in rng f & f/.x2 in rng f by A2,PARTFUN1:def 8;
    then f/.x1=r & f/.x2=r by A1,TARSKI:def 1;
    then ||. f/.x1-f/.x2.|| = ||. 0.T .|| by RLVECT_1:28
      .= 0 by NORMSP_1:5;
    hence ||. f/.x1-f/.x2.|| <= 1*||. x1-x2.|| by NORMSP_1:8;
  end;
  then f is_Lipschitzian_on dom f by Def13;
  hence thesis by Th52;
end;

theorem
  X c= dom f & f is_constant_on X implies f is_continuous_on X by Th50,Th52;

theorem Th56:
  for f be PartFunc of S,S st (for x0 st x0 in dom f holds f/.x0 = x0) holds
  f is_continuous_on dom f
proof
  let f be PartFunc of S,S such that
A1: for x0 st x0 in dom f holds f/.x0 = x0;
  now
    let x1,x2;
    assume x1 in dom f & x2 in dom f;
    then f/.x1 = x1 & f/.x2 = x2 by A1;
    hence ||. f/.x1-f/.x2.|| <= 1*||. x1-x2.||;
  end;
  then f is_Lipschitzian_on dom f by Def13;
  hence thesis by Th52;
end;

theorem
  for f be PartFunc of S,S st f = id dom f holds f is_continuous_on dom f
proof
  let f be PartFunc of S,S;
  assume
A1: f = id dom f;
  now
    let x0 such that
A2: x0 in dom f;
    thus f/.x0=f.x0 by A2,PARTFUN1:def 8
      .= x0 by A1,A2,FUNCT_1:34;
  end;
  hence thesis by Th56;
end;

theorem
  for f be PartFunc of S,S st
  Y c= dom f & f|Y = id Y holds f is_continuous_on Y
proof
  let f be PartFunc of S,S;
  assume
A1: Y c= dom f & f|Y = id Y;
  now
    let x1,x2;
    assume
A2: x1 in Y & x2 in Y;
    then x1 in dom f /\ Y & x2 in dom f /\ Y by A1,XBOOLE_0:def 3;
    then
A3: x1 in dom (f|Y) & x2 in dom (f|Y) by RELAT_1:90;
    (f|Y).x1 = x1 & (f|Y).x2 = x2 by A1,A2,FUNCT_1:34;
    then f.x1 = x1 & f.x2 = x2 by A3,FUNCT_1:68;
    then f/.x1 = x1 & f/.x2 = x2 by A1,A2,PARTFUN1:def 8;
    hence ||. f/.x1-f/.x2.|| <= 1*||. x1-x2.||;
  end;
  then f is_Lipschitzian_on Y by A1,Def13;
  hence thesis by Th52;
end;

theorem
  for f be PartFunc of S,S for r be Real for p be Point of S holds
  (X c= dom f & (for x0 st x0 in X holds f/.x0 = r*x0+p) implies
  f is_continuous_on X )
proof
  let f be PartFunc of S,S;
  let r be Real;
  let p be Point of S;
  assume that
A1: X c= dom f and
A2: for x0 st x0 in X holds f/.x0 = r*x0+p;
  now
    0+0<abs(r)+1 by COMPLEX1:132,XREAL_1:10;
    hence 0<abs(r)+1;
    let x1,x2 be Point of S;
    assume x1 in X & x2 in X;
    then f/.x1 = r*x1+p & f/.x2 = r*x2+p by A2;
    then
A3: ||. f/.x1-f/.x2.|| = ||. r*x1+(p-(p+r*x2)).|| by RLVECT_1:def 6
      .= ||. r*x1+(p-p-r*x2).|| by RLVECT_1:41
      .= ||. r*x1+(0.S-r*x2).|| by RLVECT_1:28
      .= ||. r*x1-r*x2.|| by RLVECT_1:27
      .= ||. r*(x1-x2).|| by RLVECT_1:48
      .= abs(r)*||. x1-x2.|| by NORMSP_1:def 2;
    0<=||. x1-x2.|| by NORMSP_1:8;
    then ||. f/.x1-f/.x2.||
    + 0 <= abs(r)*||. x1-x2.|| + 1*||. x1-x2.|| by A3,XREAL_1:9;
    hence ||. f/.x1-f/.x2.|| <= (abs(r)+1)*||. x1-x2.||;
  end;
  then f is_Lipschitzian_on X by A1,Def13;
  hence thesis by Th52;
end;

theorem Th60:
  for f be PartFunc of the carrier of S,REAL st
  (for x0 st x0 in dom f holds f/.x0 = ||. x0.|| ) holds
  f is_continuous_on (dom f)
proof
  let f be PartFunc of the carrier of S,REAL;
  assume
A1: for x0 st x0 in dom f holds f/.x0 = ||. x0 .||;
  now
    let x1,x2;
    assume x1 in dom f & x2 in dom f;
    then f/.x1 = ||. x1 .|| & f/.x2 = ||. x2.|| by A1;
    hence abs(f/.x1-f/.x2) <= 1*||. x1-x2.|| by NORMSP_1:13;
  end;
  then f is_Lipschitzian_on dom f by Def14;
  hence thesis by Th53;
end;

theorem
  for f be PartFunc of the carrier of S,REAL st
  X c= dom f & (for x0 st x0 in X holds f/.x0 = ||. x0.||) holds
  f is_continuous_on X
proof
  let f be PartFunc of the carrier of S,REAL;
  assume
A1: X c= dom f & (for x0 st x0 in X holds f/.x0 = ||. x0 .||);
  then X=dom f /\ X by XBOOLE_1:28;
  then
A2: X=dom(f|X) by RELAT_1:90;
  now
    let x0;
    assume
A3: x0 in dom(f|X);
    hence (f|X)/.x0=f/.x0 by PARTFUN2:32
      .=||. x0 .|| by A1,A2,A3;
  end;
  then f|X is_continuous_on X by A2,Th60;
  hence thesis by Th29;
end;

