:: Euler's Function
::  by Yoshinori Fujisawa and Yasushi Fuwa
::
:: Received December 10, 1997
:: Copyright (c) 1997 Association of Mizar Users

environ

 vocabularies INT_1, INT_2, ARYTM_3, ABSVALUE, NAT_1, ARYTM_1, ORDINAL2, ARYTM,
      FILTER_0, FINSET_1, CARD_1, BOOLE, FUNCT_1, SGRAPH1, RELAT_1, FINSEQ_1,
      EULER_1, NAT_LAT;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0,
      CARD_1, INT_1, NAT_1, NAT_D, INT_2, FINSEQ_1, FINSET_1, RELAT_1, FUNCT_1,
      FUNCT_2, BINOP_1, XXREAL_0, NEWTON;
 constructors BINOP_1, XXREAL_0, REAL_1, NAT_1, NAT_D, NEWTON;
 registrations XBOOLE_0, SUBSET_1, ORDINAL1, RELSET_1, ARYTM_3, FINSET_1,
      XXREAL_0, XREAL_0, NAT_1, INT_1, CARD_1, FINSEQ_1, NEWTON;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, FUNCT_1, FUNCT_2, XBOOLE_0, BINOP_1;
 theorems TARSKI, INT_2, AXIOMS, FINSEQ_1, CARD_1, FINSET_1, NAT_1, ABSVALUE,
      CARD_2, REAL_1, ZFMISC_1, INT_1, FUNCT_1, FUNCT_2, PRE_FF, DOMAIN_1,
      RELSET_1, XBOOLE_0, XBOOLE_1, XCMPLX_1, NEWTON, XREAL_1, COMPLEX1,
      XXREAL_0, ORDINAL1, NAT_D;
 schemes NAT_1, FUNCT_7;

begin :: Preliminary

reserve a,b,c,k,l,m,n for Element of NAT,
        i,j,x,y for Integer;

Lm1:k gcd n = k hcf n
proof
  abs(k) = k & abs(n) = n by ABSVALUE:def 1;
  hence thesis by INT_2:def 3;
end;

Lm2:a,(b qua Integer) are_relative_prime iff
                                    a,b are_relative_prime
proof
  thus a,(b qua Integer) are_relative_prime implies
                                    a,b are_relative_prime
  proof
    assume a,(b qua Integer) are_relative_prime;
    then a gcd b = 1 by INT_2:def 4;
    then a hcf b = 1 by Lm1;
    hence thesis by INT_2:def 6;
  end;
  assume a,b are_relative_prime;
  then a hcf b = 1 by INT_2:def 6;
  then a gcd b = 1 by Lm1;
  hence thesis by INT_2:def 4;
end;

Lm3:k <> 0 & ([\ j/k /]) + 1 >= j/k implies k >= j - (([\ j/k /])*k)
proof
  assume A1:k <> 0 & ([\ j/k /]) + 1 >= j/k;
     ([\ j/k /]) + 1 + -1 >= j/k + -1 by A1,XREAL_1:8;
  then (([\ j/k /]))*k >= ((j/k)-1)*k by XREAL_1:66;
  then -(((j/k) - 1)*k) >= -(([\ j/k /])*k) by XREAL_1:26;
  then j + -(((j/k) - 1)*k) >= j + -(([\ j/k /])*k) by XREAL_1:8;
  then j - ((j/k)*k - 1*k) >= j - (([\ j/k /])*k);
  then j -(j - k) >= j - (([\ j/k /])*k) by A1,XCMPLX_1:88;
  hence k >= j - (([\ j/k /])*k);
end;

theorem Th1:
  for k, n being natural number holds k in n iff k < n
proof
  let k, n be natural number;
A1:k in NAT & n in NAT by ORDINAL1:def 13;
  thus k in n implies k < n
  proof
    assume k in n;
    then k in {l : l < n} by AXIOMS:30;
    then ex j being Element of NAT st k = j & j < n;
    hence k < n;
  end;
  assume k < n;
  then k in {l: l < n} by A1;
  hence thesis by AXIOMS:30;
end;

Lm4:not 1 is prime by INT_2:def 5;

theorem Th2:
  n,n are_relative_prime iff n = 1
proof
     n hcf n = n;
  hence thesis by INT_2:def 6;
end;

theorem Th3:
  k <> 0 & k < n & n is prime implies k,n are_relative_prime
proof
  assume A1:k <> 0 & k < n & n is prime;
  A2:(k hcf n) divides n by NAT_D:def 5;
  per cases by A1,A2,INT_2:def 5;
    suppose k hcf n = 1;
      hence k,n are_relative_prime by INT_2:def 6;
    end;
    suppose k hcf n = n;
      then A3:n divides k by NAT_D:def 5;
         0 < k by A1;
  hence thesis by A1,A3,NAT_D:7;
end;
end;

theorem Th4:
  n is prime &
    k in
     {kk where kk is Element of NAT:
         n,kk are_relative_prime & kk >= 1 & kk <= n}
                                 iff n is prime & k in n & not k in {0}
proof
  set X = {kk where kk is Element of NAT :
   n,kk are_relative_prime & kk >= 1 & kk <= n};
  thus n is prime & k in X implies n is prime & k in n & not k in {0}
  proof
    assume A1:n is prime & k in X;
    then A2:ex kk being Element of NAT st
             kk = k & n,kk are_relative_prime & kk >= 1 & kk <= n;
    then k <> n by A1,Lm4,Th2;
    then A3:k < n by A2,REAL_1:def 5;
       not k = 0 by A2;
    hence thesis by A1,A3,Th1,TARSKI:def 1;
  end;
  assume A4:n is prime & k in n & not k in {0};
  then A5:k < n by Th1;
  A6:k <> 0 by A4,TARSKI:def 1;
  then A7:k >= 1 by NAT_1:14;
     k,n are_relative_prime by A4,A5,A6,Th3;
  then (k hcf n) = 1 by INT_2:def 6;
  then n,k are_relative_prime by INT_2:def 6;
  hence thesis by A4,A5,A7;
end;

theorem Th5:
  for A being finite set, x being set st x in A holds
                            card(A \ {x}) = card A - card{x}
proof
  let A be finite set,x be set; assume x in A;
  then {x} c= A by ZFMISC_1:37;
  hence thesis by CARD_2:63;
end;

theorem Th6:
  a hcf b = 1 implies for c holds a*c hcf b*c = c
proof
  assume A1:a hcf b = 1;
  A2:a = abs(a) & b = abs(b) by ABSVALUE:def 1;
  reconsider a' = a , b' = b as Integer;
     a' gcd b' = 1 by A1,A2,INT_2:def 3;
  then A3:a',b' are_relative_prime by INT_2:def 4;
  let m be Element of NAT;
  A4:a*m = abs(a*m) & b*m = abs(b*m) by ABSVALUE:def 1;
    abs(a*m) = abs(a')*abs(m) & abs(b*m) = abs(b')*abs(m) by COMPLEX1:151;
  then a*m hcf b*m = a'*abs(m) gcd b'*abs(m) by A2,A4,INT_2:def 3
             .= abs(abs(m)) by A3,INT_2:39
             .= m by ABSVALUE:def 1;
  hence thesis;
end;

theorem Th7:
  a <> 0 & b <> 0 & c <> 0 & a*c hcf b*c = c
                            implies a,b are_relative_prime
proof
  assume A1:a <> 0 & b <> 0 & c <> 0 & a*c hcf b*c = c;
  then A2:a*c <> 0*c & b*c <> 0*c by XCMPLX_1:5;
  consider a1,b1 being Integer such that
    A3:a*c = (a*c gcd b*c)*a1 and
    A4:b*c = (a*c gcd b*c)*b1 and
    A5:a1,b1 are_relative_prime by A2,INT_2:38;
     a*c = c*a1 & b*c = c*b1 by A1,A3,A4,Lm1;
  then a = a1 & b = b1 by A1,XCMPLX_1:5;
  hence thesis by A5,Lm2;
end;

theorem Th8:
  a hcf b = 1 implies (a + b) hcf b = 1
proof
  assume A1:a hcf b = 1;
  set n = (a + b) hcf b;
  A2:n divides (a + b) & n divides b by NAT_D:def 5;
  then n divides a by NAT_D:10;
  then n divides a hcf b by A2,NEWTON:63;
  then A3: n <= 1 + 0 & n >= 0 by A1,NAT_D:7;
  per cases by A3,NAT_1:9;
    suppose n = 1;
    hence (a + b) hcf b = 1;
    end;
    suppose n = 0;
    then (a + b) = 0 & b = 0 by INT_2:5;
  hence thesis by A1;
end;
end;

theorem Th9:
  for a, b, c be Nat holds (a + b*c) hcf b = a hcf b
proof
  let a, b, c be Nat;
  defpred P[Nat] means (a + b*$1) hcf b = a hcf b;
  A1:P[0];
  A2:for k being Nat st P[k] holds P[k + 1]
  proof
    let kk be Nat;
    assume A3:(a + b*kk) hcf b = a hcf b;
       now per cases;
      suppose b = 0;
        hence (a + b*(kk + 1)) hcf b = a hcf b;
      end;
      suppose A4:b > 0;
        A5:a hcf b divides (a + b*kk) by A3,NAT_D:def 5;
        then A6:(a + b*kk)
               = (a hcf b)*((a + b*kk) div (a hcf b)) by NAT_D:3;
        set T = a hcf b;
        a in NAT & b in NAT by ORDINAL1:def 13;
        then
        A7:a hcf b > 0 by A4,NEWTON:71;
           now per cases;
          suppose (a + b*kk) = 0;
            then A8:a = 0 & b*kk = 0 & a hcf b = 0 hcf b by NAT_1:7;
               (a + b*(kk + 1)) hcf b = a hcf b by A8,NEWTON:65;
            hence (a + b*(kk + 1)) hcf b = a hcf b;
          end;
          suppose (a + b*kk) > 0;
            then A9:((a + b*kk) div (a hcf b)) <> 0 by A6;
            set T1 = ((a + b*kk) div (a hcf b));
               a hcf b divides b by NAT_D:def 5;
            then A10:b = (a hcf b)*(b div (a hcf b)) by NAT_D:3;
            then A11:(b div (a hcf b)) <> 0 by A4;
            set T2 = (b div (a hcf b));
               T1*T hcf T2*T = T by A3,A5,A10,NAT_D:3;
            then T1,T2 are_relative_prime by A7,A9,A11,Th7;
            then T1 hcf T2 = 1 by INT_2:def 6;
            then A12:(T1 + T2) hcf T2 = 1 by Th8;
               (a + b*(kk + 1)) hcf b = ((a + b*kk) + b) hcf b
                                  .= (T*T1 + T*T2) hcf T*T2 by A5,A10,NAT_D:3
                                  .= (T*(T1 + T2)) hcf T*T2
                                  .= a hcf b by A12,Th6;
          hence (a + b*(kk + 1)) hcf b = a hcf b;
        end;
        end;
        hence (a + b*(kk + 1)) hcf b = a hcf b;
    end;
    end;
    hence thesis;
  end;
  for c be Nat holds P[c] from NAT_1:sch 2(A1,A2);
  hence thesis;
end;

theorem Th10:
  m,n are_relative_prime implies
    ex k st (ex i0,j0 being Integer st k = i0*m + j0*n & k > 0) &
      for l st (ex i,j being Integer st l = i*m + j*n & l > 0)
                                                       holds k <= l
proof
  defpred P[Integer] means
     ex i0,j0 being Integer st $1 = i0*m + j0*n & $1>0;
  assume A1:m,n are_relative_prime;
  m > 0 or n > 0
  proof
    assume not(m > 0 or n > 0);
    then m = 0 & n = 0;
    then m hcf n <> 1;
    hence contradiction by A1,INT_2:def 6;
  end;
  then m + n <> 0 by NAT_1:7;
  then 1*m + 1*n > 0;
  then A2:ex k st P[k];
  consider k such that
A3: P[k] and
A4: for l st P[l] holds k<=l from NAT_1:sch 5(A2);
  thus thesis by A3,A4;
end;

theorem Th11:
  m,n are_relative_prime implies for k holds ex i,j st i*m + j*n = k
proof
  assume A1:m,n are_relative_prime;
  let k;
      consider a such that
  A2: ex i0,j0 being Integer st a = i0*m + j0*n & a>0 and
  A3: for c st (ex i,j st c = i*m + j*n & c>0)
                                          holds a <= c by A1,Th10;
      consider i0,j0 being Integer such that
  A4: a = i0*m + j0*n and
  A5: a > 0 by A2;
  A6: for c st (ex i,j st c = i*m + j*n & c > 0) holds c mod a = 0
      proof
        let b;
        assume A7: ex i,j st b = i*m + j*n & b > 0;
        assume A8: b mod a <> 0;
        consider i,j such that
    A9: b=i*m + j*n & b>0 by A7;
        set t1 = i - ((b div a)*i0);
        set t2 = j - ((b div a)*j0);
        b mod a + a*(b div a) - a*(b div a)
               = b - a*(b div a) by A5,NAT_D:2;
        then A10: b mod a + (a*(b div a) - a*(b div a)) =
               b - a*(b div a);
   A11: b mod a >= 0;
        reconsider c = t1*m + t2*n as Element of NAT by A4,A9,A10;
        c < a by A5,A4,A9,A10,NAT_D:1;
        hence contradiction by A3,A4,A8,A9,A10,A11;
      end;
      reconsider 1'=1, 0'=0 as Integer;
      A12: a divides m
      proof
        per cases;
          suppose m = 0;
            hence thesis by NAT_D:6;
          end;
          suppose A13: m > 0;
            reconsider c = 1'*m + 0'*n as Element of NAT;
               c > 0 by A13;
            then m mod a = 0 by A6;
            then m = a*(m div a) + 0 by A2,NAT_D:2;
            hence thesis by NAT_D:3;
        end;
        end;
         a divides n
      proof
        per cases;
          suppose n = 0;
            hence thesis by NAT_D:6;
          end;
          suppose A14: n > 0;
            reconsider c = 0'*m + 1'*n as Element of NAT;
               c > 0 by A14;
            then n mod a = 0 by A6;
            then n = a*(n div a) + 0 by A2,NAT_D:2;
            hence thesis by NAT_D:3;
      end;
      end;
      then a divides (m hcf n) by A12,NAT_D:def 5;
      then a divides 1 by A1,INT_2:def 6;
      then ex b be Nat st 1 = a*b by NAT_D:def 3;
      then i0*m + j0*n = 1 by A4,NAT_1:15;
      then k = k*(i0*m + j0*n)
            .= (k*i0)*m + k*(j0*n);
      then k = (k*i0)*m + (k*j0)*n;
    hence thesis;
end;

theorem Th12:
  for A,B being non empty finite set holds
    (ex f being Function of A, B st f is one-to-one onto)
                                        implies Card A = Card B
proof
    let A,B be non empty finite set;
    given f being Function of A, B such that
      A1: f is one-to-one and
      A2: f is onto;
    A3: A = dom f & rng f c= B by FUNCT_2:def 1,RELSET_1:12;
    then Card A <=` Card B by A1,CARD_1:26;
    then A4: card A <= card B by CARD_2:57;
       B c= rng f by A2,FUNCT_2:def 3;
    then Card B <=` Card A by A3,CARD_1:28;
    then card B <= card A by CARD_2:57;
    hence thesis by A4,XXREAL_0:1;
end;

theorem Th13:
  for i,k,n being Integer holds (i + k*n) mod n = i mod n
proof
  let i,k,n be Integer;
  per cases;
  suppose A1: n <> 0;
  then (i + k*n) mod n = (i + k*n) - ((i + k*n) div n)*n by INT_1:def 8
                 .= (i + k*n) - ([\(i + k*n)/n/])*n by INT_1:def 7
                 .= (i + k*n) - ([\i/n + (k*n)/n/])*n by XCMPLX_1:63
                 .= (i + k*n) - ([\i/n + k/(n/n)/])*n by XCMPLX_1:78
                 .= (i + k*n) - ([\i/n + k/1/])*n by A1,XCMPLX_1:60
                 .= (i + k*n) - ([\i/n/] + k)*n by INT_1:51
                 .= i - [\i/n/]*n
                 .= i - (i div n)*n by INT_1:def 7
                 .= i mod n by A1,INT_1:def 8;
  hence thesis;
  end;
  suppose n = 0;
  hence (i + k*n) mod n = i mod n;
end;
end;

theorem Th14:
  a <> 0 & b <> 0 & c <> 0 & c divides a*b & a,c are_relative_prime implies c
 divides b
proof
  assume A1:a <> 0 & b <> 0 & c <> 0 & c divides a*b & a,c are_relative_prime;
  then a hcf c = 1 by INT_2:def 6;
  then A2:(a*b hcf c*b) = b by Th6;
     c divides c*b by NAT_D:9;
  hence thesis by A1,A2,NEWTON:63;
end;

theorem Th15:
  a <> 0 & b <> 0 & c <> 0 &
    a,c are_relative_prime & b,c are_relative_prime
                             implies a*b,c are_relative_prime
proof
  assume A1:a <> 0 & b <> 0 & c <> 0 &
         a,c are_relative_prime & b,c are_relative_prime;
  then A2:(a hcf c) = 1 & (b hcf c) = 1 by INT_2:def 6;
  A3: a > 0 & b > 0 & c > 0 by A1;
  then A4:(a*b) hcf c > 0 & a*b hcf c <> 0 by NEWTON:71;
  A5:(a*b hcf c) divides a*b & (a*b hcf c) divides c by NAT_D:def 5;
  A6:((a*b hcf c) hcf a) divides (a*b hcf c) & ((a*b hcf c) hcf a) divides
a by NAT_D:def 5;
  then ((a*b hcf c) hcf a) divides c by A5,NAT_D:4;
  then A7:((a*b hcf c) hcf a) divides 1 by A2,A6,NEWTON:63;
  A8:((a*b hcf c) hcf a) <> 0 by A3,NEWTON:71;
     ((a*b hcf c) hcf a) >= 0 & ((a*b hcf c) hcf a) <= 0 + 1
                                              by A7,NAT_D:7;
  then ((a*b hcf c) hcf a) = 1 by A8,NAT_1:9;
  then a,(a*b hcf c) are_relative_prime by INT_2:def 6;
  then (a*b hcf c) divides b by A1,A4,A5,Th14;
  then (a*b hcf c) divides 1 by A2,A5,NEWTON:63;
  then a*b hcf c >= 0 & a*b hcf c <= 0 + 1 by NAT_D:7;
  then a*b hcf c = 1 by A4,NAT_1:9;
  hence thesis by INT_2:def 6;
end;

theorem Th16:
  x <> 0 & y <> 0 & i > 0 implies i*x gcd i*y = i*(x gcd y)
proof
  assume A1:x <> 0 & y <> 0 & i > 0;
  then consider a2,b2 being Integer such that
       A2:x = (x gcd y)*a2 and
       A3:y = (x gcd y)*b2 and
       A4:a2,b2 are_relative_prime by INT_2:38;
  A5:i*x = (i*(x gcd y))*a2 by A2;
     i*y = (i*(x gcd y))*b2 by A3;
  then A6:i*x gcd i*y = abs(i*(x gcd y)) by A4,A5,INT_2:39
                 .= abs(i)*abs((x gcd y)) by COMPLEX1:151;
  A7:i = abs(i) by A1,ABSVALUE:def 1;
     x gcd y is Element of NAT by INT_2:29;
  then x gcd y >= 0;
  hence thesis by A6,A7,ABSVALUE:def 1;
end;

theorem Th17:
  for x st a <> 0 & b <> 0 holds (a + x*b) gcd b = a gcd b
proof
  let xx be Integer;
  assume a <> 0 & b <> 0;
  then A1:a > 0 & b > 0;
  set i = a hcf b;
  A2:a = abs(a) & b = abs(b) by ABSVALUE:def 1;
  then A3:i = a gcd b by INT_2:def 3;
  A4:i divides a & i divides b by NAT_D:def 5;
  then A5:a = i*(a div i) & b = i*(b div i) by NAT_D:3;
  A6:i divides abs(a + xx*b)
  proof
    per cases;
      suppose a + xx*b < 0;
        then A7:abs((a + xx*b)) = -(a + xx*b) by ABSVALUE:def 1;
           -(a + xx*b) = i * (-((a div i) + (xx*(b div i)))) by A5;
        then i divides -(a + xx*b) by INT_1:def 9;
      hence thesis by A7;
      end;
      suppose (a + xx*b) >= 0;
        then A8:abs((a + xx*b)) = a + xx*b by ABSVALUE:def 1;
           a + xx*b = i*((a div i) + (xx*(b div i))) by A5;
        then i divides (a + xx*b) by INT_1:def 9;
    hence thesis by A8;
  end;
  end;
  for m be Nat st m divides abs((a + xx*b)) & m divides abs(b) holds
  m divides i
  proof
    let mm be Nat;
    assume A9:mm divides abs((a + xx*b)) & mm divides abs(b);
    then consider n being Nat such that
           A10:b = mm*n by A2,NAT_D:def 3;
       reconsider mm as Element of NAT by ORDINAL1:def 13;
       now per cases;
      suppose (a + xx*b) >= 0;
        then A11:abs((a + xx*b)) = (a + xx*b) by ABSVALUE:def 1;
           now per cases;
          suppose mm > 0;
            mm divides (a + xx*b) by A9,A11;
            then consider t being Integer such that
                 A12:(a + xx*b) = mm*t by INT_1:def 9;
            A13:a = mm*t - mm*(xx*n) by A10,A12,XCMPLX_1:26
                   .= mm*(t - (xx*n));
            then (t - (xx*n)) >= 0 by A1,XREAL_1:136;
            then reconsider tt = (t - (xx*n)) as Element of NAT by INT_1:16;
               a = mm*tt by A13;
          hence mm divides a by NAT_D:def 3;
          end;
          suppose A14:mm = 0;
            then abs((a + xx*b)) = 0 by A9,INT_2:3;
          hence mm divides a by A10,A14,ABSVALUE:7;
        end;
        end;
      hence mm divides a;
      end;
      suppose (a + xx*b) < 0;
        then A15:abs((a + xx*b)) = -(a + xx*b) by ABSVALUE:def 1;
           now per cases;
          suppose mm > 0;
            mm divides -(a + xx*b) by A9,A15;
            then consider t being Integer such that
                   A16:-(a + xx*b) = mm*t by INT_1:def 9;
            A17:a = -mm*t - mm*(xx*n) by A10,A16,XCMPLX_1:26
                        .= mm*(-t - (xx*n));
            then (-t - (xx*n)) >= 0 by A1,XREAL_1:136;
            then reconsider tt = (-t - (xx*n)) as Element of NAT by INT_1:16;
               a = mm*tt by A17;
          hence mm divides a by NAT_D:def 3;
          end;
          suppose A18:mm = 0;
            then abs((a + xx*b)) = 0 by A9,INT_2:3;
          hence mm divides a by A10,A18,ABSVALUE:7;
        end;
        end;
      hence mm divides a;
    end;
    end;
    hence thesis by A2,A9,NEWTON:63;
  end;
  then abs((a + xx*b)) hcf abs(b) = i by A2,A4,A6,NAT_D:def 5;
  hence thesis by A3,INT_2:def 3;
end;

begin :: Euler's function

definition
  let n be Element of NAT;
    func Euler n -> Element of NAT
    equals
    Card {k where k is Element of NAT :
     n,k are_relative_prime & k >= 1 & k <= n};
coherence
  proof
    set X = {k where k is Element of NAT :
     n,k are_relative_prime & k >= 1 & k <= n};
       X c= Seg n
    proof
      let x be set;
      assume x in X;
      then ex k be Element of NAT st
           k = x & n,k are_relative_prime & k >= 1 & k <= n;
      hence x in Seg n by FINSEQ_1:3;
    end;
    then reconsider X as finite set by FINSET_1:13;
       card X is Element of NAT;
    hence thesis;
  end;
end;

set X = {k where k is Element of NAT :
 1,k are_relative_prime & k >= 1 & k <= 1};
   for x being set holds x in X iff x = 1
proof
  let x be set;
  thus x in X implies x = 1
  proof
    assume x in X;
    then ex k be Element of NAT st k = x &
     1,k are_relative_prime & k >= 1 & k <= 1;
    hence x = 1 by XXREAL_0:1;
  end;
     1 hcf 1 = 1;
  then 1,1 are_relative_prime by INT_2:def 6;
  hence thesis;
end;
then Lm5:Card{1} = Euler 1 by TARSKI:def 1;

theorem
     Euler 1 = 1 by Lm5,CARD_1:79;

set X = {k where k is Element of NAT :
 2,k are_relative_prime & k >= 1 & k <= 2};
   for x being set holds x in X iff x = 1
proof
  let x be set;
  thus x in X implies x = 1
  proof
    assume x in X;
    then consider k be Element of NAT such that
         A1:k = x and
         A2:2,k are_relative_prime and
         A3:k >= 1 & k <= 2;
    A4:k = 1 or k = 2 by A3,NAT_1:27;
       2 hcf 2 = 2;
    hence thesis by A1,A2,A4,INT_2:def 6;
  end;
     2 hcf 1 = 1 by NEWTON:64;
  then 2,1 are_relative_prime by INT_2:def 6;
  hence thesis;
end;
then Lm6:Card{1} = Euler 2 by TARSKI:def 1;

theorem
     Euler 2 = 1 by Lm6,CARD_1:79;

theorem Th20:
  n > 1 implies Euler n <= n - 1
proof
  assume A1:n > 1;
  set X={kk where kk is Element of NAT :
   n,kk are_relative_prime & kk >= 1 & kk <= n};
     X c= Seg n
  proof
    let x be set;
    assume x in X;
    then consider k be Element of NAT such that
      A2:k = x & n,k are_relative_prime & k >= 1 & k <= n;
    thus thesis by A2,FINSEQ_1:3;
  end;
  then reconsider X as finite set by FINSET_1:13;
     X c= n \ {0}
  proof
    let x be set;
    assume x in X;
    then consider k be Element of NAT such that
      A3:k = x & n,k are_relative_prime & k >= 1 & k <= n;
       k <> 0 by A3;
    then A4: not k in {0} by TARSKI:def 1;
       not n,n are_relative_prime by A1,Th2;
    then k < n by A3,REAL_1:def 5;
    then k in {l: l < n};
    then k in n by AXIOMS:30;
    hence thesis by A3,A4,XBOOLE_0:def 4;
  end;
  then A5:card X <= card (n \ {0}) by CARD_1:80;
     0 < n by A1;
  then 0 in {l: l < n};
  then 0 in n by AXIOMS:30;
  then A6:card(n \ {0}) = card n - card {0} by Th5;
     Card n = n & Card {0} = 1 by CARD_1:66,79;
  hence thesis by A5,A6;
end;

theorem
     n is prime implies Euler n = n - 1
proof
  assume A1:n is prime;
  set X={kk where kk is Element of NAT :
   n,kk are_relative_prime & kk >= 1 & kk <= n};
     X c= Seg n
  proof
    let x be set;
    assume x in X;
    then consider k be Element of NAT such that
           A2: k = x & n,k are_relative_prime & k >= 1 & k <= n;
    thus thesis by A2,FINSEQ_1:3;
  end;
  then reconsider X as finite set by FINSET_1:13;
  A3: n < 1 or n = 1 or n > 1 by XXREAL_0:1;
  A4:n <> 0
  proof
    assume n = 0;
    then n in SetPrimenumber 2 by A1,NEWTON:def 7;
    hence contradiction;
  end;
  A5: Euler n <= n - 1 by A1,A3,Th20,INT_2:def 5;
     n - 1 <= Euler n
  proof
       n \ {0} c= X
    proof
      let x be set;
      assume x in n \ {0};
      then A6:x in n & not x in {0} by XBOOLE_0:def 4;
      then x in {k: k< n} by AXIOMS:30;
      then ex k st k = x & k < n;
      hence x in X by A1,A6,Th4;
    end;
    then card (n \ {0}) <= card X by CARD_1:80;
    then A7: card (n \ {0}) <= Euler n;
       0 < n by A4;
    then 0 in {l: l < n};
    then A8:0 in n by AXIOMS:30;
       Card n = n & Card {0} = 1 by CARD_1:66,79;
    hence thesis by A7,A8,Th5;
  end;
  hence thesis by A5,XXREAL_0:1;
end;

theorem
     m > 1 & n > 1 & m,n are_relative_prime
                      implies Euler (m*n) = Euler m * Euler n
proof
  assume A1:m > 1 & n > 1 & m,n are_relative_prime;
  then A2:m > 0 & n > 0;
  A3:m*n <> 1 by A1,NAT_1:15;
  A4:m*n <> 0 by A2,XCMPLX_1:6;
  then A5:m*n >= 1 by NAT_1:14;
  then A6:m*n > 0;
  set A={i where i is Element of NAT:n,i are_relative_prime & i >= 1 & i <= n};
  set B={i where i is Element of NAT:m,i are_relative_prime & i >= 1 & i <= m};
  set C={i where i is Element of NAT:ex x,y being Integer st
             i = (m*y + n*x) mod m*n & m*n,i are_relative_prime &
                                                y <= n & x <= m & i >= 0};
  set X={i where i is Element of NAT:
  m*n,i are_relative_prime & i >= 1 & i <= m*n};
  A7:A c= Seg n
  proof
    let x be set;
    assume x in A;
    then consider i being Element of NAT such that
           A8:i = x and n,i are_relative_prime and
           A9:i >= 1 & i <= n;
    thus x in Seg n by A8,A9,FINSEQ_1:3;
  end;
  then reconsider A as finite set by FINSET_1:13;
  A10:B c= Seg m
  proof
    let x be set;
    assume x in B;
    then consider i being Element of NAT such that
           A11:i = x and m,i are_relative_prime and
           A12:i >= 1 & i <= m;
    thus x in Seg m by A11,A12,FINSEQ_1:3;
  end;
  then reconsider B as finite set by FINSET_1:13;
  A13:C c= Seg (m*n) \/ {0}
  proof
    let z be set;
    assume z in C;
    then consider i being Element of NAT such that
           A14:i = z and
           A15:ex x,y being Integer st i = (m*y + n*x) mod m*n &
                m*n,i are_relative_prime & y <= n & x <= m & i >= 0;
    consider x,y being Integer such that
      A16:i = (m*y + n*x) mod m*n and
         m*n,i are_relative_prime and
      y <= n & x <= m & i >= 0 by A15;
    per cases;
      suppose i > 0;
        then A17:i >= 1 by NAT_1:14;
           (m*y + n*x) mod m*n <= m*n
        proof
          set i1 = (m*y + n*x);
          set i2 = m*n;
          A18:(m*y + n*x) mod m*n = i1 - (i1 div i2)*i2 by A4,INT_1:def 8
                                 .= i1 - ([\ i1/i2 /])*i2 by INT_1:def 7;
             ([\ i1/i2 /]) + 1 >= i1/i2 by INT_1:52;
          hence (m*y + n*x) mod m*n <= m*n by A4,A18,Lm3;
        end;
        then z in Seg (m*n) by A14,A16,A17,FINSEQ_1:3;
      hence z in Seg (m*n) \/ {0} by XBOOLE_0:def 2;
      end;
      suppose A19:i = 0;
           0 in {0} by TARSKI:def 1;
    hence z in Seg (m*n) \/ {0} by A14,A19,XBOOLE_0:def 2;
  end;
  end;
  then reconsider C as finite set by FINSET_1:13;
     C = X
  proof
    thus C c= X
    proof
      let z be set;
      assume z in C;
      then consider i being Element of NAT such that
             A20:i = z and
             A21:ex x,y being Integer st i = (m*y + n*x) mod m*n &
              m*n,i are_relative_prime & y <= n & x <= m & i >= 0;
      consider x,y being Integer such that
        A22:i = (m*y + n*x) mod m*n and
        A23:m*n,i are_relative_prime and
           y <= n & x <= m & i >= 0 by A21;
      A24:(m*y + n*x) mod m*n <= m*n
      proof
        set i1 = (m*y + n*x);
        set i2 = m*n;
        A25:(m*y + n*x) mod m*n = i1 - (i1 div i2)*i2 by A4,INT_1:def 8
                               .= i1 - ([\ i1/i2 /])*i2 by INT_1:def 7;
           ([\ i1/i2 /]) + 1 >= i1/i2 by INT_1:52;
        hence (m*y + n*x) mod m*n <= m*n by A4,A25,Lm3;
      end;
      per cases;
        suppose i > 0;
          then i >= 1 by NAT_1:14;
        hence z in X by A20,A22,A23,A24;
        end;
        suppose i = 0;
          then m*n hcf 0 = 1 by A23,INT_2:def 6;
      hence z in X by A3,NEWTON:65;
    end;
    end;
    let z be set;
    assume z in X;
    then consider i being Element of NAT such that
           A26:i=z and
           A27:m*n,i are_relative_prime and
           A28:i >= 1 & i <= m*n;
       i <> m*n
    proof
      assume i = m*n;
      then (m*n) hcf (m*n) = 1 by A27,INT_2:def 6;
      hence contradiction by A1,NAT_1:15;
    end;
    then A29:i < m*n by A28,REAL_1:def 5;
    consider y0,x0 being Integer such that
      A30:y0*m + x0*n = i by A1,Th11;
    consider x1,y1 being Integer such that
      A31:x1 = x0 mod m and
      A32:y1 = y0 mod n;
    A33:x0 mod m = x0 - (x0 div m)*m by A2,INT_1:def 8;
    set j = x0 div m;
    A34:x1 = x0 - (x0 div m)*m by A2,A31,INT_1:def 8
           .= x0 - ([\ x0/m /])*m by INT_1:def 7;
       ([\ x0/m /]) + 1 >= x0/m by INT_1:52;
    then A35:x1 <= m by A2,A34,Lm3;
    A36:y0 mod n = y0- (y0 div n)*n by A2,INT_1:def 8;
    set k = y0 div n;
    A37:y1 = y0 - (y0 div n)*n by A2,A32,INT_1:def 8
           .= y0 - ([\ y0/n /])*n by INT_1:def 7;
       ([\ y0/n /]) + 1 >= y0/n by INT_1:52;
    then A38:y1 <= n by A2,A37,Lm3;
    A39: m*y0 + n*x0 = m*y1 + n*x1 + (m*n)*(k+j) by A31,A32,A33,A36;
       (m*y0 + n*x0) mod (m*n)
        = (m*y0 + n*x0) - ((m*y0 + n*x0) div (m*n))*(m*n) by A4,INT_1:def 8
       .= (m*y0 + n*x0) - 0*(m*n) by A29,A30,PRE_FF:4
       .= m*y0 + n*x0;
    then m*y0 + n*x0 = (m*y1 + n*x1) mod (m*n) by A39,Th13;
    hence z in C by A26,A27,A30,A35,A38;
  end;
  then A40:Euler (m*n) = Card C;
  A41:a = 1 implies a in A
  proof
    assume A42:a = 1;
    then n hcf a = 1 by NEWTON:64;
    then n,a are_relative_prime by INT_2:def 6;
    hence thesis by A1,A42;
  end;
  A43:b = 1 implies b in B
  proof
    assume A44:b = 1;
    then m hcf b = 1 by NEWTON:64;
    then m,b are_relative_prime by INT_2:def 6;
    hence thesis by A1,A44;
  end;
  A45:y = 1 & x = 1 implies (m*y + n*x) mod m*n in C
  proof
    assume A46:y = 1 & x = 1;
    then reconsider y'= y,x'= x as Element of NAT;
    (m*y' + n*x') mod m*n = (m*y + n*x) mod m*n;
    then consider t being Element of NAT such that
      A47:t = (m*y + n*x) mod m*n;
    A48:m hcf n = 1 by A1,INT_2:def 6;
    then (m + n) hcf n = 1 by Th8;
    then A49:n,(m + n) are_relative_prime by INT_2:def 6;
       (m + n) hcf m = 1 by A48,Th8;
    then A50:m,(m + n) are_relative_prime by INT_2:def 6;
       m + n <> 0 by A2,NAT_1:7;
    then m*n,(m + n) are_relative_prime by A2,A49,A50,Th15;
    then A51: m*n hcf (m + n) = 1 by INT_2:def 6;
    consider d being Nat such that
      A52:(m + n) = (m*n)*d + ((m + n) mod m*n) and
         ((m + n) mod m*n) < (m*n) by A4,NAT_D:def 2;
       (m + n) mod m*n hcf m*n = 1 by A51,A52,Th9;
    then m*n,t are_relative_prime by A46,A47,INT_2:def 6;
    hence thesis by A1,A46,A47;
  end;
   {0} c= NAT by ZFMISC_1:37;
  then Seg (m*n) \/ {0} c= NAT by XBOOLE_1:8;
  then reconsider A,B,C as non empty finite Subset of NAT
                                 by A7,A10,A13,A41,A43,A45,XBOOLE_1:1;
  deffunc F(Element of A,Element of B) = (m*$1+n*$2) mod (m*n);
  A53:for y being Element of A, x being Element of B holds F(y,x) in C
  proof
    let y be Element of A, x be Element of B;
       y in A;
    then consider i being Element of NAT such that
           A54:i = y and
           A55:n,i are_relative_prime and
           A56:i >= 1 and
           A57:i <= n;
     x in B;
    then consider j being Element of NAT such that
           A58:j = x and
           A59:m,j are_relative_prime and
           A60:j >= 1 and
           A61:j <= m;
    A62:i <> 0 & j <> 0 by A56,A60;
    reconsider ii = i,jj = j as Integer;
    m*ii + n*jj mod m*n = m*i + n*j mod m*n;
    then consider q being Element of NAT such that
      A63:q = (m*ii + n*jj) mod m*n;
       n hcf i = 1 by A55,INT_2:def 6;
    then i,n are_relative_prime by INT_2:def 6;
    then i*m,n are_relative_prime by A1,A62,Th15;
    then A64:i*m hcf n = 1 by INT_2:def 6;
       m hcf j = 1 by A59,INT_2:def 6;
    then A65:j,m are_relative_prime by INT_2:def 6;
       m hcf n = 1 by A1,INT_2:def 6;
    then n,m are_relative_prime by INT_2:def 6;
    then j*n,m are_relative_prime by A2,A62,A65,Th15;
    then A66:j*n hcf m = 1 by INT_2:def 6;
       (m*i + n*j) hcf n = 1 by A64,Th9;
    then A67:n,(m*i + n*j) are_relative_prime by INT_2:def 6;
       (m*i + n*j) hcf m = 1 by A66,Th9;
    then A68:m,(m*i + n*j) are_relative_prime by INT_2:def 6;
       i*m <> 0*m & j*n <> 0*n by A2,A62,XCMPLX_1:5;
    then (m*i + n*j) <> 0 by NAT_1:7;
    then n*m,(m*i + n*j) are_relative_prime by A2,A67,A68,Th15;
    then A69:n*m hcf (m*i + n*j) = 1 by INT_2:def 6;
    consider t being Nat such that
      A70:(m*i + n*j) = (n*m)*t + ((m*i + n*j) mod n*m) and
         ((m*i + n*j) mod n*m) < (n*m) by A4,NAT_D:def 2;
    set l = n*m;
       ((m*i + n*j) mod l) hcf l = 1 by A69,A70,Th9;
    then l,q are_relative_prime by A63,INT_2:def 6;
    hence thesis by A54,A57,A58,A61,A63;
  end;
  consider f being Function of [:A,B:], C such that
    A71:for y being Element of A,
    x being Element of B holds f.(y,x) = F(y,x) from FUNCT_7:sch 1(A53);
  A72:f is one-to-one
  proof
    let x,y be set;
    A73:dom f = [:A,B:] by FUNCT_2:def 1;
    assume x in dom f;
    then consider xx1 being Element of A,
                  xx2 being Element of B such that
           A74:x = [xx1,xx2] by A73,DOMAIN_1:9;
       xx1 in A;
    then consider x1 being Element of NAT such that
           A75:xx1 = x1 and
              n,x1 are_relative_prime and
           A76:x1 >= 1 and
           A77:x1 <= n;
       xx2 in B;
    then consider x2 being Element of NAT such that
           A78:xx2 = x2 and
              m,x2 are_relative_prime and
           A79:x2 >= 1 and
           A80:x2 <= m;
    assume y in dom f;
    then consider yy1 being Element of A,
                  yy2 being Element of B such that
           A81:y = [yy1,yy2] by A73,DOMAIN_1:9;
       yy1 in A;
    then consider y1 being Element of NAT such that
           A82:yy1 = y1 and
              n,y1 are_relative_prime and
           A83:y1 >= 1 and
           A84:y1 <= n;
       yy2 in B;
    then consider y2 being Element of NAT such that
           A85:yy2 = y2 and
              m,y2 are_relative_prime and
           A86:y2 >= 1 and
           A87:y2 <= m;
    assume A88:f.x = f.y;
    A89:(x1*m+x2*n) mod (m*n) = f.(xx1,xx2) by A71,A75,A78
                    .= f.(yy1,yy2) by A88,A81,A74
                    .= (y1*m+y2*n) mod (m*n) by A71,A82,A85;
    reconsider y1,y2,x1,x2 as Element of NAT;
    set k = (x1*m+x2*n) mod (m*n);
    A90:y1*m + y2*n = k + ((y1*m + y2*n) div (m*n))*(m*n) by A6,A89,NAT_D:2;
    A91:(x1*m + x2*n)-(y1*m + y2*n) = m*(x1 - y1) + n*(x2 - y2);
    A92:k + ((x1*m + x2*n) div (m*n))*(m*n) -
                  (k + ((y1*m + y2*n) div (m*n))*(m*n))
             = (m*n)*(((x1*m + x2*n) div (m*n)) -
                  ((y1*m + y2*n) div (m*n)));
    set w = (((x1*m + x2*n) div (m*n)) - ((y1*m + y2*n) div (m*n)));
       (m hcf n) = 1 by A1,INT_2:def 6;
    then A93:n,m are_relative_prime by INT_2:def 6;
    consider p being Integer such that
      A94:p = m;
    A95:n,p are_relative_prime by A93,A94,Lm2;
    A96:m*(x1 - y1) + n*(x2 - y2) = (m*n)*w by A6,A90,A91,A92,NAT_D:2;
    then n*(x2 - y2) = m*((n*w) - (x1 - y1));
    then p divides n*(x2 - y2) by A94,INT_1:def 9;
    then A97:m divides (x2 - y2) by A94,A95,INT_2:40;
       x2 - y2 = 0
    proof
      per cases;
        suppose A98:0<=(x2 - y2);
          consider k being Integer such that
            A99:k = x2 - y2;
          reconsider k as Element of NAT by A98,A99,INT_1:16;
          A100:m divides k by A97,A99;
             k = 0
          proof
            assume k <> 0;
            then A101:0 < k;
               -y2 <= -1 by A86,XREAL_1:26;
            then A102:x2 + -y2 <= m + -1 by A80,XREAL_1:9;
            set b = m + -1;
             b is Element of NAT by A98,A102,INT_1:16;
            then k < b + 1 by A99,A102,NAT_1:13;
            hence contradiction by A100,A101,NAT_D:7;
          end;
        hence thesis by A99;
        end;
        suppose A103:(x2 - y2)<=0;
          consider k being Integer such that
            A104:k = -(x2 - y2);
          A105:m divides k by A97,A104,INT_2:14;
          A106:-0 <= k by A103,A104,XREAL_1:26;
          then reconsider k as Element of NAT by INT_1:16;
          A107:m divides k by A105;
             k = 0
          proof
            assume k <> 0;
            then A108:0 < k;
               -x2 <= -1 by A79,XREAL_1:26;
            then A109: - x2 + y2 <= m + -1 by A87,XREAL_1:9;
            set b = m + -1;
             b is Element of NAT by A104,A106,A109,INT_1:16;
            then k < b + 1 by A104,A109,NAT_1:13;
            hence contradiction by A107,A108,NAT_D:7;
          end;
        then 0 -(x2 - y2) = 0 by A104;
      hence thesis;
    end;
    end;
    then A110:x2 = y2;
    consider p being Integer such that
      A111:p = n;
    A112:m,p are_relative_prime by A1,A111,Lm2;
       m*(x1 - y1) = n*((m*w) - (x2 - y2)) by A96;
    then p divides m*(x1 - y1) by A111,INT_1:def 9;
    then A113:n divides (x1 - y1) by A111,A112,INT_2:40;
       x1 - y1 = 0
    proof
      per cases;
        suppose A114:0<=(x1 - y1);
          consider k being Integer such that
            A115:k = x1 - y1;
          reconsider k as Element of NAT by A114,A115,INT_1:16;
          A116:n divides k by A113,A115;
             k = 0
          proof
            assume k <> 0;
            then A117:0 < k;
               -y1 <= -1 by A83,XREAL_1:26;
            then A118:x1 + -y1 <= n + -1 by A77,XREAL_1:9;
            set b = n + -1;
             b is Element of NAT by A114,A118,INT_1:16;
            then k < b + 1 by A115,A118,NAT_1:13;
            hence contradiction by A116,A117,NAT_D:7;
          end;
        hence thesis by A115;
        end;
        suppose A119:(x1 - y1)<=0;
          consider k being Integer such that
            A120:k = -(x1 - y1);
          A121:n divides k by A113,A120,INT_2:14;
          A122:-0 <= k by A119,A120,XREAL_1:26;
          then reconsider k as Element of NAT by INT_1:16;
          A123:n divides k by A121;
             k = 0
          proof
            assume k <> 0;
            then A124:0 < k;
               -x1 <= -1 by A76,XREAL_1:26;
            then A125: - x1 + y1 <= n + -1 by A84,XREAL_1:9;
            set b = n + -1;
             b is Element of NAT by A120,A122,A125,INT_1:16;
            then k < b + 1 by A120,A125,NAT_1:13;
          hence contradiction by A123,A124,NAT_D:7;
        end;
        then 0 -(x1 - y1) = 0 by A120;
        hence thesis;
    end;
    end;
    hence x = y by A74,A75,A78,A81,A82,A85,A110;
  end;
     f is onto
  proof
    thus rng f c= C by RELSET_1:12;
    let z be set;
    assume z in C;
    then consider i being Element of NAT such that
           A126:i = z and
           A127:ex x0,y0 being Integer st i = (m*y0 + n*x0) mod m*n &
                m*n,i are_relative_prime & y0 <= n & x0 <= m & i >= 0;
    consider x0,y0 being Integer such that
      A128:i = (m*y0 + n*x0) mod m*n and
      A129:m*n,i are_relative_prime and
      y0 <= n & x0 <= m & i >= 0 by A127;
    consider x,y being Integer such that
      A130:x = x0 mod m and
      A131:y = y0 mod n;
    A132:x <= m & y <= n by A2,A130,A131,NEWTON:79;
    A133:x >= 0 & y >= 0 by A130,A131,NEWTON:78;
    A134:y >= 0 by A131,NEWTON:78;
    A135:(m*y + n*x) mod (m*n) = (m*y0 + n*x0) mod (m*n)
    proof
      A136: x0 mod m = x0 - (x0 div m)*m by A2,INT_1:def 8;
      set j = x0 div m;
      A137: y0 mod n = y0- (y0 div n)*n by A2,INT_1:def 8;
      set k = y0 div n;
       m*y0 + n*x0
                    = m*y + n*x + (m*n)*(k+j) by A130,A131,A136,A137;
      hence thesis by Th13;
    end;
    A138:m,x0 are_relative_prime
    proof
      set p = m gcd x0;
      A139:p is Element of NAT by INT_2:29;
         p divides m by INT_2:31;
      then consider i1 being Integer such that
             A140:m = p*i1 by INT_1:def 9;
         p <> 0 & i1 <> 0 by A1,A140;
      then A141:p > 0 by A139;
         p divides x0 by INT_2:32;
      then consider i2 being Integer such that
        A142:x0 = p*i2 by INT_1:def 9;
      A143:m*n hcf i = 1 by A129,INT_2:def 6;
         m*n = abs(m*n) by ABSVALUE:def 1;
      then A144:abs(m*n) hcf abs(i) = 1 by A143,ABSVALUE:def 1;
      A145:(m*y0 + n*x0) mod m*n
               = ((p*i1)*y0 + n*(p*i2)) - (((p*i1)*y0 + n*(p*i2))
                   div((p*i1)*n))*((p*i1)*n) by A4,A140,A142,INT_1:def 8;
      set k = ((p*i1)*y0 + n*(p*i2))div((p*i1)*n);
      A146:((p*i1)*y0 + n*(p*i2)) - k*((p*i1)*n)
                         = p*((i1*y0 + n*i2) - ((i1*k)*n));
      set j = (i1*y0 + n*i2) - ((i1*k)*n);
         not j < 0
      proof
        assume j < 0;
        then p*j < 0*j by A141,XREAL_1:71;
        hence contradiction by A4,A128,A140,A142,A146,INT_1:def 8;
      end;
      then reconsider p,j as Element of NAT by INT_1:16,INT_2:29;
      A147:m*n = p*(i1*n) by A140;
      set jj = i1*n;
        not jj < 0
      proof
        assume jj < 0;
        then p*jj < 0*jj by A141,XREAL_1:71;
        hence contradiction by A5,A140;
      end;
      then reconsider jj as Element of NAT by INT_1:16;
         now per cases by A2,A147,XCMPLX_1:6;
        suppose A148:p <> 0 & jj <> 0 & j <> 0;
          then A149:p > 0;
             p*jj gcd p*j = 1 by A128,A140,A144,A145,INT_2:def 3;
          then A150:p*(jj gcd j) = 1 by A148,A149,Th16;
             jj gcd j is Element of NAT by INT_2:29;
        hence p = 1 by A150,NAT_1:15;
        end;
        suppose p <> 0 & jj <> 0 & j = 0;
          then p*jj hcf 0 = 1 by A128,A129,A140,A145,A146,INT_2:def 6;
          then p*jj = 1 by NEWTON:65;
        hence p = 1 by NAT_1:15;
      end;
      end;
      hence thesis by INT_2:def 4;
    end;
    A151:n,y0 are_relative_prime
    proof
      set p = n gcd y0;
      A152:p is Element of NAT by INT_2:29;
         p divides n by INT_2:31;
      then consider i1 being Integer such that
             A153:n = p*i1 by INT_1:def 9;
         p <> 0 & i1 <> 0 by A1,A153;
      then A154:p > 0 by A152;
         p divides y0 by INT_2:32;
      then consider i2 being Integer such that
        A155:y0 = p*i2 by INT_1:def 9;
      A156:m*n hcf i = 1 by A129,INT_2:def 6;
         m*n = abs(m*n) by ABSVALUE:def 1;
      then A157:abs(m*n) hcf abs(i) = 1 by A156,ABSVALUE:def 1;
      A158:(m*y0 + n*x0) mod m*n
        = (m*(p*i2) + (p*i1)*x0) - ((m*(p*i2) + (p*i1)*x0)
                    div (m*(p*i1)))*(m*(p*i1)) by A4,A153,A155,INT_1:def 8;
      set k = (m*(p*i2) + (p*i1)*x0) div (m*(p*i1));
      A159:(m*(p*i2) + (p*i1)*x0) - k*(m*(p*i1))
                         = p*((m*i2 + i1*x0) - ((i1*k)*m));
      set j = (m*i2 + i1*x0) - ((i1*k)*m);
         j >= 0
      proof
        assume j < 0;
        then p*j < 0*j by A154,XREAL_1:71;
        hence contradiction by A4,A128,A153,A155,A159,INT_1:def 8;
      end;
      then reconsider p,j as Element of NAT by INT_1:16,INT_2:29;
      A160:m*n = p*(i1*m) by A153;
      set jj = i1*m;
        not jj < 0
      proof
        assume jj < 0;
        then p*jj < 0*jj by A154,XREAL_1:71;
        hence contradiction by A5,A153;
      end;
      then reconsider jj as Element of NAT by INT_1:16;
         now per cases by A2,A160,XCMPLX_1:6;
        suppose A161:p <> 0 & jj <> 0 & j <> 0;
          then A162:p > 0;
             p*jj gcd p*j = 1 by A128,A153,A157,A158,INT_2:def 3;
          then A163:p*(jj gcd j) = 1 by A161,A162,Th16;
             jj gcd j is Element of NAT by INT_2:29;
        hence p = 1 by A163,NAT_1:15;
        end;
        suppose p <> 0 & jj <> 0 & j = 0;
          then p*jj hcf 0 = 1 by A128,A129,A153,A158,A159,INT_2:def 6;
          then p*jj = 1 by NEWTON:65;
        hence p = 1 by NAT_1:15;
      end;
      end;
      hence thesis by INT_2:def 4;
    end;
    reconsider x,y as Element of NAT by A133,INT_1:16;
    A164:x <> 0
    proof
      assume x = 0;
      then A165:x0 - (x0 div m)*m = 0 by A2,A130,INT_1:def 8;
      set j = x0 div m;
      A166:(m*y0 + n*x0) mod m*n =
 m*(y0 + (n*j)) - ((m*(y0 + (n*j))) div m*n)*(m*n)
             by  A165,A4,INT_1:def 8
           .= m*((y0 + (n*j)) - (n*((m*(y0 + (n*j))) div m*n)));
      set jj = (y0 + (n*j)) - (n*((m*(y0 + (n*j))) div m*n));
      reconsider g = m*jj as Element of NAT by A128,A166;
      A167:g > 0 or g = 0;
      A168:not jj < 0
      proof
        assume jj < 0;
        then m*jj < 0*jj by A2,XREAL_1:71;
        hence contradiction by A167;
      end;
      A169:m*n hcf i = 1 by A129,INT_2:def 6;
         m*n = abs(m*n) by ABSVALUE:def 1;
      then abs(m*n) hcf abs(i) = 1 by A169,ABSVALUE:def 1;
      then A170:m*n gcd m*jj = 1 by A128,A166,INT_2:def 3;
      reconsider jj as Element of NAT by A168,INT_1:16;
        jj = 0
      proof
        assume not(jj = 0);
        then A171:m*(n gcd jj) = 1 by A2,A170,Th16;
           n gcd jj is Element of NAT by INT_2:29;
        hence contradiction by A1,A171,NAT_1:15;
      end;
      then m*n hcf 0 = 1 by A128,A129,A166,INT_2:def 6;
      hence contradiction by A3,NEWTON:65;
    end;
    then A172:x >= 0 + 1 by A133,INT_1:20;
    A173:y <> 0
    proof
      assume y = 0;
      then A174:y0 - (y0 div n)*n = 0 by A2,A131,INT_1:def 8;
      set j = y0 div n;
      A175:(m*y0 + n*x0) mod m*n =
 n*((m*j) + x0) - ((n*((m*j) + x0)) div m*n)*(m*n)
             by  A174,A4,INT_1:def 8
           .= n*(((m*j) + x0) - (m*((n*((m*j) + x0)) div m*n)));
      set jj = ((m*j) + x0) - (m*((n*((m*j) + x0)) div m*n));
      reconsider g = n*jj as Element of NAT by A128,A175;
      A176:g > 0 or g = 0;
      A177:not jj < 0
      proof
        assume jj < 0;
        then n*jj < 0*jj by A2,XREAL_1:71;
        hence contradiction by A176;
      end;
      A178:m*n hcf i = 1 by A129,INT_2:def 6;
         m*n = abs(m*n) by ABSVALUE:def 1;
      then abs(m*n) hcf abs(i) = 1 by A178,ABSVALUE:def 1;
      then A179:m*n gcd n*jj = 1 by A128,A175,INT_2:def 3;
      reconsider jj as Element of NAT by A177,INT_1:16;
        jj = 0
      proof
        assume not(jj = 0);
        then A180:n*(m gcd jj) = 1 by A2,A179,Th16;
           m gcd jj is Element of NAT by INT_2:29;
        hence contradiction by A1,A180,NAT_1:15;
      end;
      then m*n hcf 0 = 1 by A128,A129,A175,INT_2:def 6;
      hence contradiction by A3,NEWTON:65;
   end;
   then A181:y >= 0 + 1 by A134,INT_1:20;
   A182:m,x are_relative_prime
   proof
     A183:x = x0 - (x0 div m)*m by A2,A130,INT_1:def 8;
        x0 gcd m = x + (x0 div m)*m gcd m by A183;
     then A184:x0 gcd m = x gcd m by A2,A164,Th17;
        x0 gcd m = 1 by A138,INT_2:def 4;
     then m hcf x = 1 by A184,Lm1;
     hence thesis by INT_2:def 6;
   end;
   A185:n,y are_relative_prime
   proof
     A186:y = y0 - (y0 div n)*n by A2,A131,INT_1:def 8;
        y0 gcd n = y + (y0 div n)*n gcd n by A186;
     then A187:y0 gcd n = y gcd n by A2,A173,Th17;
        y0 gcd n = 1 by A151,INT_2:def 4;
     then n hcf y = 1 by A187,Lm1;
     hence thesis by INT_2:def 6;
   end;
      x in B by A132,A172,A182;
   then reconsider x as Element of B;
      y in A by A132,A181,A185;
   then reconsider y as Element of A;
   A188:i = (m*y + n*x) mod (m*n) by A128,A135;
   A189:dom f = [:A,B:] by FUNCT_2:def 1;
   consider w being set such that
     A190:w = [y,x];
   A191:w in dom f by A189,A190,ZFMISC_1:106;
      i = f.(y,x) by A188,A71;
   hence z in rng f by A126,A191,FUNCT_1:def 5,A190;
 end;
 then Card[:A,B:] = Card C by A72,Th12;
hence thesis by A40,CARD_2:65;
end;
