:: Euler's Function
::  by Yoshinori Fujisawa and Yasushi Fuwa
::
:: Received December 10, 1997
:: Copyright (c) 1997 Association of Mizar Users

environ

 vocabularies INT_1, INT_2, ARYTM_3, ABSVALUE, NAT_1, ARYTM_1, ORDINAL2, ARYTM,
      FILTER_0, FINSET_1, CARD_1, BOOLE, FUNCT_1, SGRAPH1, RELAT_1, FINSEQ_1,
      EULER_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, ORDINAL2, ORDINAL1, NUMBERS,
      XCMPLX_0, XREAL_0, CARD_1, INT_1, NAT_1, INT_2, FINSEQ_1, FINSET_1,
      RELAT_1, FUNCT_1, FUNCT_2;
 constructors NAT_1, FUNCT_2, INT_2, REAL_1, DOMAIN_1, MEMBERED, FINSEQ_1;
 registrations SUBSET_1, FINSET_1, INT_1, CARD_1, XREAL_0, FINSEQ_1, RELSET_1,
      ARYTM_3, ORDINAL2;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, FUNCT_1, FUNCT_2, XBOOLE_0;
 theorems TARSKI, INT_2, AXIOMS, FINSEQ_1, CARD_1, FINSET_1, CQC_THE1, NAT_1,
      ABSVALUE, CARD_2, REAL_1, ZFMISC_1, INT_1, FUNCT_1, FUNCT_2, SQUARE_1,
      PRE_FF, DOMAIN_1, RELSET_1, ORDINAL2, XBOOLE_0, XBOOLE_1, XCMPLX_1,
      NEWTON;
 schemes NAT_1, FUNCT_7;

begin :: Preliminary

reserve a,b,c,k,l,m,n for Nat,
        i,j,x,y for Integer;

Lm1:0 <= k & 0 <= n implies k gcd n = k hcf n
proof
  assume 0 <= k & 0 <= n;
  then abs(k) = k & abs(n) = n by ABSVALUE:def 1;
  hence thesis by INT_2:def 3;
end;

Lm2:a,(b qua Integer) are_relative_prime iff
                                    a,b are_relative_prime
proof
  thus a,(b qua Integer) are_relative_prime implies
                                    a,b are_relative_prime
  proof
    assume a,(b qua Integer) are_relative_prime;
    then A1:a gcd b = 1 by INT_2:def 4;
       a >= 0 & b >= 0 by NAT_1:18;
    then a hcf b = 1 by A1,Lm1;
    hence thesis by INT_2:def 6;
  end;
  assume a,b are_relative_prime;
  then A2:a hcf b = 1 by INT_2:def 6;
     a >= 0 & b >= 0 by NAT_1:18;
  then a gcd b = 1 by A2,Lm1;
  hence thesis by INT_2:def 4;
end;

Lm3:i > 0 & a = i & k > 0 implies i mod k = a mod k
proof
  assume A1:i > 0 & a = i & k > 0;
  then A2: i mod k = a - (i div k)*k by INT_1:def 8;
     0/k < i/k by A1,REAL_1:73;
  then 0 <= i/k & 0 <> i/k & i/k <= [\ i/k /]+1 by INT_1:52;
  then 0 + 1 + -1 <= [\ i/k /]+1 + -1 by INT_1:20;
  then 0 <= i div k by INT_1:def 7;
  then reconsider t = i div k as Nat by INT_1:16;
     i mod k >= 0 by A1,NEWTON:78;
  then reconsider tt = i mod k as Nat by INT_1:16;
  A3:a = t*k + tt by A2;
     tt < k by A1,NEWTON:79;
  hence thesis by A3,NAT_1:def 2;
end;

Lm4:
  a = i & m > 0 & m divides i iff a = i & m > 0 & m divides a
proof
  thus a = i & m > 0 & m divides i implies a = i & m > 0 & m divides a
  proof
   assume A1: a = i & m > 0 & m divides i;
    then consider j such that
      A2: i = m*j by INT_1:def 9;
       j >= 0
    proof
      assume not j >= 0;
      then m*j < 0*j by A1,REAL_1:71;
      hence contradiction by A1,A2,NAT_1:18;
    end;
    then reconsider j as Nat by INT_1:16;
       a = m*j by A1,A2;
    hence thesis by A1,NAT_1:def 3;
  end;
  assume A3: a = i & m > 0 & m divides a;
  then ex j being Nat st a = m*j by NAT_1:def 3;
  hence thesis by A3,INT_1:def 9;
end;

Lm5:k <> 0 & ([\ j/k /]) + 1 >= j/k implies k >= j - (([\ j/k /])*k)
proof
  assume A1:k <> 0 & ([\ j/k /]) + 1 >= j/k;
  A2:k >= 0 by NAT_1:18;
     ([\ j/k /]) + 1 + -1 >= j/k + -1 by A1,AXIOMS:24;
  then (([\ j/k /]))*k >= ((j/k)-1)*k by A2,AXIOMS:25;
  then -(((j/k) - 1)*k) >= -(([\ j/k /])*k) by REAL_1:50;
  then j + -(((j/k) - 1)*k) >= j + -(([\ j/k /])*k) by AXIOMS:24;
  then j - ((j/k)*k - 1*k) >= j - (([\ j/k /])*k);
  then j -(j - k) >= j - (([\ j/k /])*k) by A1,XCMPLX_1:88;
  hence k >= j - (([\ j/k /])*k);
end;

theorem Th1:
  for k, n being natural number holds k in n iff k < n
proof
  let k, n be natural number;
A1:k in NAT & n in NAT by ORDINAL2:def 21;
  thus k in n implies k < n
  proof
    assume k in n;
    then k in {l : l < n} by AXIOMS:30;
    then ex j being Nat st k = j & j < n;
    hence k < n;
  end;
  assume k < n;
  then k in {l: l < n} by A1;
  hence thesis by AXIOMS:30;
end;

Lm6:not 1 is prime by INT_2:def 5;

theorem Th2:
  n,n are_relative_prime iff n = 1
proof
     n hcf n = n;
  hence thesis by INT_2:def 6;
end;

theorem Th3:
  k <> 0 & k < n & n is prime implies k,n are_relative_prime
proof
  assume A1:k <> 0 & k < n & n is prime;
  A2:(k hcf n) divides n by NAT_1:def 5;
  per cases by A1,A2,INT_2:def 5;
    suppose k hcf n = 1;
      hence k,n are_relative_prime by INT_2:def 6;
    end;
    suppose k hcf n = n;
      then A3:n divides k by NAT_1:def 5;
         0 < k by A1,NAT_1:19;
  hence thesis by A1,A3,NAT_1:54;
end;
end;

theorem Th4:
  n is prime &
    k in {kk where kk is Nat:n,kk are_relative_prime & kk >= 1 & kk <= n}
                                 iff n is prime & k in n & not k in {0}
proof
  set X = {kk where kk is Nat : n,kk are_relative_prime & kk >= 1 & kk <= n};
  thus n is prime & k in X implies n is prime & k in n & not k in {0}
  proof
    assume A1:n is prime & k in X;
    then A2:ex kk being Nat st
             kk = k & n,kk are_relative_prime & kk >= 1 & kk <= n;
    then k <> n by A1,Lm6,Th2;
    then A3:k < n by A2,REAL_1:def 5;
       not k = 0 by A2;
    hence thesis by A1,A3,Th1,TARSKI:def 1;
  end;
  assume A4:n is prime & k in n & not k in {0};
  then A5:k < n by Th1;
  A6:k <> 0 by A4,TARSKI:def 1;
  then A7:k >= 1 by NAT_1:39;
     k,n are_relative_prime by A4,A5,A6,Th3;
  then (k hcf n) = 1 by INT_2:def 6;
  then n,k are_relative_prime by INT_2:def 6;
  hence thesis by A4,A5,A7;
end;

theorem Th5:
  for A being finite set, x being set st x in A holds
                            card(A \ {x}) = card A - card{x}
proof
  let A be finite set,x be set; assume x in A;
  then {x} c= A by ZFMISC_1:37;
  hence thesis by CARD_2:63;
end;

theorem Th6:
  a hcf b = 1 implies for c holds a*c hcf b*c = c
proof
  assume A1:a hcf b = 1;
     a >= 0 & b >= 0 by NAT_1:18;
  then A2:a = abs(a) & b = abs(b) by ABSVALUE:def 1;
  reconsider a' = a , b' = b as Integer;
     a' gcd b' = 1 by A1,A2,INT_2:def 3;
  then A3:a',b' are_relative_prime by INT_2:def 4;
  let m be Nat;
  A4:m >= 0 by NAT_1:18;
     a*m >= 0 & b*m >= 0 by NAT_1:18;
  then A5:a*m = abs(a*m) & b*m = abs(b*m) by ABSVALUE:def 1;
    abs(a*m) = abs(a')*abs(m) & abs(b*m) = abs(b')*abs(m) by ABSVALUE:10;
  then a*m hcf b*m = a'*abs(m) gcd b'*abs(m) by A2,A5,INT_2:def 3
             .= abs(abs(m)) by A3,INT_2:39
             .= m by A4,ABSVALUE:def 1;
  hence thesis;
end;

theorem Th7:
  a <> 0 & b <> 0 & c <> 0 & a*c hcf b*c = c
                            implies a,b are_relative_prime
proof
  assume A1:a <> 0 & b <> 0 & c <> 0 & a*c hcf b*c = c;
  then A2:a*c <> 0*c & b*c <> 0*c by XCMPLX_1:5;
  A3:a*c >= 0 & b*c >= 0 by NAT_1:18;
  consider a1,b1 being Integer such that
    A4:a*c = (a*c gcd b*c)*a1 and
    A5:b*c = (a*c gcd b*c)*b1 and
    A6:a1,b1 are_relative_prime by A2,INT_2:38;
     a*c = c*a1 & b*c = c*b1 by A1,A3,A4,A5,Lm1;
  then a = a1 & b = b1 by A1,XCMPLX_1:5;
  hence thesis by A6,Lm2;
end;

theorem Th8:
  a hcf b = 1 implies (a + b) hcf b = 1
proof
  assume A1:a hcf b = 1;
  set n = (a + b) hcf b;
  A2:n divides (a + b) & n divides b by NAT_1:def 5;
  then n divides a by NAT_1:57;
  then n divides a hcf b by A2,NEWTON:63;
  then A3: n <= 1 + 0 & n >= 0 by A1,NAT_1:18,54;
  per cases by A3,NAT_1:27;
    suppose n = 1;
    hence (a + b) hcf b = 1;
    end;
    suppose n = 0;
    then (a + b) = 0 & b = 0 by INT_2:5;
  hence thesis by A1;
end;
end;

theorem Th9:
  for c holds (a + b*c) hcf b = a hcf b
proof
  defpred P[Nat] means (a + b*$1) hcf b = a hcf b;
  A1:P[0];
  A2:for k being Nat st P[k] holds P[k + 1]
  proof
    let kk be Nat;
    assume A3:(a + b*kk) hcf b = a hcf b;
       now per cases by NAT_1:19;
      suppose b = 0;
        hence (a + b*(kk + 1)) hcf b = a hcf b;
      end;
      suppose A4:b > 0;
        A5:a hcf b divides (a + b*kk) by A3,NAT_1:def 5;
        then A6:(a + b*kk)
               = (a hcf b)*((a + b*kk) div (a hcf b)) by NAT_1:49;
        set T = a hcf b;
        A7:a hcf b > 0 by A4,NEWTON:72;
           now per cases by NAT_1:19;
          suppose (a + b*kk) = 0;
            then A8:a = 0 & b*kk = 0 & a hcf b = 0 hcf b by NAT_1:23;
               (a + b*(kk + 1)) hcf b = (a + (b*kk + b*1)) hcf b
                                  .= a hcf b by A8,NEWTON:65;
            hence (a + b*(kk + 1)) hcf b = a hcf b;
          end;
          suppose (a + b*kk) > 0;
            then A9:((a + b*kk) div (a hcf b)) <> 0 by A6;
            set T1 = ((a + b*kk) div (a hcf b));
               a hcf b divides b by NAT_1:def 5;
            then A10:b = (a hcf b)*(b div (a hcf b)) by NAT_1:49;
            then A11:(b div (a hcf b)) <> 0 by A4;
            set T2 = (b div (a hcf b));
               T1*T hcf T2*T = T by A3,A5,A10,NAT_1:49;
            then T1,T2 are_relative_prime by A7,A9,A11,Th7;
            then T1 hcf T2 = 1 by INT_2:def 6;
            then A12:(T1 + T2) hcf T2 = 1 by Th8;
               (a + b*(kk + 1)) hcf b = ((a + b*kk) + b) hcf b
                                  .= (T*T1 + T*T2) hcf T*T2 by A5,A10,NAT_1:49
                                  .= (T*(T1 + T2)) hcf T*T2
                                  .= a hcf b by A12,Th6;
          hence (a + b*(kk + 1)) hcf b = a hcf b;
        end;
        end;
        hence (a + b*(kk + 1)) hcf b = a hcf b;
    end;
    end;
    hence thesis;
  end;
   for c holds P[c] from NAT_1:sch 1(A1,A2);
  hence thesis;
end;

theorem Th10:
  m,n are_relative_prime implies
    ex k st (ex i0,j0 being Integer st k = i0*m + j0*n & k > 0) &
      for l st (ex i,j being Integer st l = i*m + j*n & l > 0)
                                                       holds k <= l
proof
  defpred P[Integer] means
     ex i0,j0 being Integer st $1 = i0*m + j0*n & $1>0;
  assume A1:m,n are_relative_prime;
     m > 0 or n > 0
  proof
    assume not(m > 0 or n > 0);
    then m = 0 & n = 0 by NAT_1:19;
    then m hcf n <> 1;
    hence contradiction by A1,INT_2:def 6;
  end;
  then m + n <> 0 by NAT_1:23;
  then 1*m + 1*n > 0 by NAT_1:19;
  then A2:ex k st P[k];
  consider k such that
    A3: P[k] and
    A4: for l st P[l] holds k<=l from NAT_1:sch 5(A2);
  thus thesis by A3,A4;
end;

theorem Th11:
  m,n are_relative_prime implies for k holds ex i,j st i*m + j*n = k
proof
  assume A1:m,n are_relative_prime;
  let k;
      consider a such that
        A2: (ex i0,j0 being Integer st a = i0*m + j0*n & a>0) and
        A3: for c st (ex i,j st c = i*m + j*n & c>0)
                                          holds a <= c by A1,Th10;
      consider i0,j0  being Integer such that
        A4: a = i0*m + j0*n and
        A5: a > 0 by A2;
      A6: for c st (ex i,j st c = i*m + j*n & c > 0)
                                                 holds c mod a = 0
      proof
        let b;
        assume A7: ex i,j st b = i*m + j*n & b > 0;
        assume A8: b mod a <> 0;
        consider i,j such that
          A9: b=i*m + j*n & b>0 by A7;
        set t1 = (i - ((b div a)*i0));
        set t2 = (j - ((b div a)*j0));
           b mod a + a*(b div a) - a*(b div a)
                                   = b - a*(b div a) by A5,NAT_1:47;
        then A10: b mod a + (a*(b div a) - a*(b div a)) =
                    b - a*(b div a) by XCMPLX_1:29;
         A11: b mod a >= 0 by NAT_1:18;
        reconsider c = t1*m + t2*n as Nat by A4,A9,A10;
           c < a by A2,A4,A9,A10,NAT_1:46;
        hence contradiction by A3,A4,A8,A9,A10,A11;
      end;
      reconsider 1'=1, 0'=0 as Integer;
      A12: a divides m
      proof
        per cases by NAT_1:19;
          suppose m = 0;
            hence thesis by NAT_1:53;
          end;
          suppose A13: m > 0;
            reconsider c = 1'*m + 0'*n as Nat;
               c > 0 by A13;
            then m mod a = 0 by A6;
            then m = a*(m div a) + 0 by A2,NAT_1:47;
            hence thesis by NAT_1:49;
        end;
        end;
         a divides n
      proof
        per cases by NAT_1:19;
          suppose n = 0;
            hence thesis by NAT_1:53;
          end;
          suppose A14: n > 0;
            reconsider c = 0'*m + 1'*n as Nat;
               c > 0 by A14;
            then n mod a = 0 by A6;
            then n = a*(n div a) + 0 by A2,NAT_1:47;
            hence thesis by NAT_1:49;
      end;
      end;
      then a divides (m hcf n) by A12,NAT_1:def 5;
      then a divides 1 by A1,INT_2:def 6;
      then ex b st 1 = a*b by NAT_1:def 3;
      then i0*m + j0*n = 1 by A4,NAT_1:40;
      then k = k*(i0*m + j0*n)
            .= (k*i0)*m + k*(j0*n);
      then k = (k*i0)*m + (k*j0)*n by XCMPLX_1:4;
    hence thesis;
end;

theorem Th12:
  for A,B being non empty finite set holds
    (ex f being Function of A, B st f is one-to-one onto)
                                        implies Card A = Card B
proof
    let A,B be non empty finite set;
    given f being Function of A, B such that
      A1: f is one-to-one and
      A2: f is onto;
    A3: A = dom f & rng f c= B by FUNCT_2:def 1,RELSET_1:12;
    then Card A <=` Card B by A1,CARD_1:26;
    then A4: card A <= card B by CARD_2:57;
       B c= rng f by A2,FUNCT_2:def 3;
    then Card B <=` Card A by A3,CARD_1:28;
    then card B <= card A by CARD_2:57;
    hence thesis by A4,AXIOMS:21;
end;

theorem Th13:
  for i,k,n being Integer holds (i + k*n) mod n = i mod n
proof
  let i,k,n be Integer;
  per cases;
  suppose A1: n <> 0;
  then (i + k*n) mod n = (i + k*n) - ((i + k*n) div n)*n by INT_1:def 8
                 .= (i + k*n) - ([\(i + k*n)/n/])*n by INT_1:def 7
                 .= (i + k*n) - ([\i/n + (k*n)/n/])*n by XCMPLX_1:63
                 .= (i + k*n) - ([\i/n + k/(n/n)/])*n by XCMPLX_1:78
                 .= (i + k*n) - ([\i/n + k/1/])*n by A1,XCMPLX_1:60
                 .= (i + k*n) - ([\i/n/] + k)*n by INT_1:51
                 .= i - [\i/n/]*n
                 .= i - (i div n)*n by INT_1:def 7
                 .= i mod n by A1,INT_1:def 8;
  hence thesis;
  end;
  suppose n = 0;
  hence (i + k*n) mod n = i mod n;
end;
end;

theorem Th14:
  a <> 0 & b <> 0 & c <> 0 & c divides a*b & a,c are_relative_prime implies c
 divides b
proof
  assume A1:a <> 0 & b <> 0 & c <> 0 & c divides a*b & a,c are_relative_prime;
  then a hcf c = 1 by INT_2:def 6;
  then A2:(a*b hcf c*b) = b by Th6;
     c divides c*b by NAT_1:56;
  hence thesis by A1,A2,NEWTON:63;
end;

theorem Th15:
  a <> 0 & b <> 0 & c <> 0 &
    a,c are_relative_prime & b,c are_relative_prime
                             implies a*b,c are_relative_prime
proof
  assume A1:a <> 0 & b <> 0 & c <> 0 &
         a,c are_relative_prime & b,c are_relative_prime;
  then A2:(a hcf c) = 1 & (b hcf c) = 1 by INT_2:def 6;
  A3: a > 0 & b > 0 & c > 0 by A1,NAT_1:19;
  then A4:(a*b) hcf c > 0 & a*b hcf c <> 0 by NEWTON:72;
  A5:(a*b hcf c) divides a*b & (a*b hcf c) divides c by NAT_1:def 5;
  A6:((a*b hcf c) hcf a) divides (a*b hcf c) & ((a*b hcf c) hcf a) divides
a by NAT_1:def 5;
  then ((a*b hcf c) hcf a) divides c by A5,NAT_1:51;
  then A7:((a*b hcf c) hcf a) divides 1 by A2,A6,NEWTON:63;
  A8:((a*b hcf c) hcf a) <> 0 by A3,NEWTON:72;
     ((a*b hcf c) hcf a) >= 0 & ((a*b hcf c) hcf a) <= 0 + 1
                                              by A7,NAT_1:18,54;
  then ((a*b hcf c) hcf a) = 1 by A8,NAT_1:27;
  then a,(a*b hcf c) are_relative_prime by INT_2:def 6;
  then (a*b hcf c) divides b by A1,A4,A5,Th14;
  then (a*b hcf c) divides 1 by A2,A5,NEWTON:63;
  then a*b hcf c >= 0 & a*b hcf c <= 0 + 1 by NAT_1:18,54;
  then a*b hcf c = 1 by A4,NAT_1:27;
  hence thesis by INT_2:def 6;
end;

theorem Th16:
  x <> 0 & y <> 0 & i > 0 implies i*x gcd i*y = i*(x gcd y)
proof
  assume A1:x <> 0 & y <> 0 & i > 0;
  then consider a2,b2 being Integer such that
       A2:x = (x gcd y)*a2 and
       A3:y = (x gcd y)*b2 and
       A4:a2,b2 are_relative_prime by INT_2:38;
  A5:i*x = (i*(x gcd y))*a2 by A2;
     i*y = (i*(x gcd y))*b2 by A3;
  then A6:i*x gcd i*y = abs(i*(x gcd y)) by A4,A5,INT_2:39
                 .= abs(i)*abs((x gcd y)) by ABSVALUE:10;
  A7:i = abs(i) by A1,ABSVALUE:def 1;
     x gcd y is Nat by INT_2:29;
  then x gcd y >= 0 by NAT_1:18;
  hence thesis by A6,A7,ABSVALUE:def 1;
end;

theorem Th17:
  for x st a <> 0 & b <> 0 holds (a + x*b) gcd b = a gcd b
proof
  let xx be Integer;
  assume a <> 0 & b <> 0;
  then A1:a > 0 & b > 0 by NAT_1:19;
  set i = a hcf b;
     0 <= a & 0 <= b by NAT_1:18;
  then A2:a = abs(a) & b = abs(b) by ABSVALUE:def 1;
  then A3:i = a gcd b by INT_2:def 3;
  A4:i divides a & i divides b by NAT_1:def 5;
  then A5:a = i*(a div i) & b = i*(b div i) by NAT_1:49;
  A6: i > 0 by A1,NEWTON:72;
  A7:i divides abs(a + xx*b)
  proof
    per cases;
      suppose a + xx*b < 0;
        then A8:abs((a + xx*b)) = -(a + xx*b) by ABSVALUE:def 1;
           -(a + xx*b) = i * (-((a div i) + (xx*(b div i)))) by A5;
        then i divides -(a + xx*b) by INT_1:def 9;
      hence thesis by A6,A8,Lm4;
      end;
      suppose (a + xx*b) >= 0;
        then A9:abs((a + xx*b)) = a + xx*b by ABSVALUE:def 1;
           a + xx*b = i*((a div i) + (xx*(b div i))) by A5;
        then i divides (a + xx*b) by INT_1:def 9;
    hence thesis by A6,A9,Lm4;
  end;
  end;
     for m st m divides abs((a + xx*b)) & m divides abs(b) holds m divides i
  proof
    let mm be Nat;
    assume A10:mm divides abs((a + xx*b)) & mm divides abs(b);
    then consider n being Nat such that
           A11:b = mm*n by A2,NAT_1:def 3;
       now per cases;
      suppose (a + xx*b) >= 0;
        then A12:abs((a + xx*b)) = (a + xx*b) by ABSVALUE:def 1;
           now per cases by NAT_1:19;
          suppose A13:mm > 0;
            then mm divides (a + xx*b) by A10,A12,Lm4;
            then consider t being Integer such that
                 A14:(a + xx*b) = mm*t by INT_1:def 9;
            A15:a = mm*t - mm*(xx*n) by A11,A14,XCMPLX_1:26
                   .= mm*(t - (xx*n));
            then (t - (xx*n)) >= 0 by A1,A13,SQUARE_1:26;
            then reconsider tt = (t - (xx*n)) as Nat by INT_1:16;
               a = mm*tt by A15;
          hence mm divides a by NAT_1:def 3;
          end;
          suppose A16:mm = 0;
            then abs((a + xx*b)) = 0 by A10,INT_2:3;
          hence mm divides a by A11,A16,ABSVALUE:7;
        end;
        end;
      hence mm divides a;
      end;
      suppose (a + xx*b) < 0;
        then A17:abs((a + xx*b)) = -(a + xx*b) by ABSVALUE:def 1;
           now per cases by NAT_1:19;
          suppose A18:mm > 0;
            then mm divides -(a + xx*b) by A10,A17,Lm4;
            then consider t being Integer such that
                   A19:-(a + xx*b) = mm*t by INT_1:def 9;
            A20:a = -mm*t - mm*(xx*n) by A11,A19,XCMPLX_1:26
                        .= mm*(-t - (xx*n));
            then (-t - (xx*n)) >= 0 by A1,A18,SQUARE_1:26;
            then reconsider tt = (-t - (xx*n)) as Nat by INT_1:16;
               a = mm*tt by A20;
          hence mm divides a by NAT_1:def 3;
          end;
          suppose A21:mm = 0;
            then abs((a + xx*b)) = 0 by A10,INT_2:3;
          hence mm divides a by A11,A21,ABSVALUE:7;
        end;
        end;
      hence mm divides a;
    end;
    end;
    hence thesis by A2,A10,NEWTON:63;
  end;
  then abs((a + xx*b)) hcf abs(b) = i by A2,A4,A7,NAT_1:def 5;
  hence thesis by A3,INT_2:def 3;
end;

begin :: Euler's function

definition
  let n be Nat;
    func Euler n -> Nat
    equals
   :Def1: Card {k where k is Nat : n,k are_relative_prime & k >= 1 & k <= n};
coherence
  proof
    set X = {k where k is Nat : n,k are_relative_prime & k >= 1 & k <= n};
       X c= Seg n
    proof
      let x be set;
      assume x in X;
      then ex k be Nat st
           k = x & n,k are_relative_prime & k >= 1 & k <= n;
      hence x in Seg n by FINSEQ_1:3;
    end;
    then reconsider X as finite set by FINSET_1:13;
       card X is Nat;
    hence thesis;
  end;
end;

set X = {k where k is Nat : 1,k are_relative_prime & k >= 1 & k <= 1};
   for x being set holds x in X iff x = 1
proof
  let x be set;
  thus x in X implies x = 1
  proof
    assume x in X;
    then ex k be Nat st k = x & 1,k are_relative_prime & k >= 1 & k <= 1;
    hence x = 1 by AXIOMS:21;
  end;
     1 hcf 1 = 1;
  then 1,1 are_relative_prime by INT_2:def 6;
  hence thesis;
end;
then Lm7:Card{1} = Card X by TARSKI:def 1
               .= Euler 1 by Def1;

theorem
     Euler 1 = 1 by Lm7,CARD_1:79;

set X = {k where k is Nat : 2,k are_relative_prime & k >= 1 & k <= 2};
   for x being set holds x in X iff x = 1
proof
  let x be set;
  thus x in X implies x = 1
  proof
    assume x in X;
    then consider k be Nat such that
         A1:k = x and
         A2:2,k are_relative_prime and
         A3:k >= 1 & k <= 2;
    A4:k = 1 or k = 2 by A3,CQC_THE1:3;
       2 hcf 2 = 2;
    hence thesis by A1,A2,A4,INT_2:def 6;
  end;
     2 hcf 1 = 1 by NEWTON:64;
  then 2,1 are_relative_prime by INT_2:def 6;
  hence thesis;
end;
then Lm8:Card{1} = Card X by TARSKI:def 1
               .= Euler 2 by Def1;

theorem
     Euler 2 = 1 by Lm8,CARD_1:79;

theorem Th20:
  n > 1 implies Euler n <= n - 1
proof
  assume A1:n > 1;
  set X={kk where kk is Nat : n,kk are_relative_prime & kk >= 1 & kk <= n};
     X c= Seg n
  proof
    let x be set;
    assume x in X;
    then consider k be Nat such that
      A2:k = x & n,k are_relative_prime & k >= 1 & k <= n;
    thus thesis by A2,FINSEQ_1:3;
  end;
  then reconsider X as finite set by FINSET_1:13;
     X c= n \ {0}
  proof
    let x be set;
    assume x in X;
    then consider k be Nat such that
      A3:k = x & n,k are_relative_prime & k >= 1 & k <= n;
       k <> 0 by A3;
    then A4: not k in {0} by TARSKI:def 1;
       not n,n are_relative_prime by A1,Th2;
    then k < n by A3,REAL_1:def 5;
    then k in {l: l < n};
    then k in n by AXIOMS:30;
    hence thesis by A3,A4,XBOOLE_0:def 4;
  end;
  then A5:card X <= card (n \ {0}) by CARD_1:80;
     0 < n by A1;
  then 0 in {l: l < n};
  then 0 in n by AXIOMS:30;
  then A6:card(n \ {0}) = card n - card {0} by Th5;
     Card n = n & Card {0} = 1 by CARD_1:66,79;
  hence thesis by A5,A6,Def1;
end;

theorem
     n is prime implies Euler n = n - 1
proof
  assume A1:n is prime;
  set X={kk where kk is Nat : n,kk are_relative_prime & kk >= 1 & kk <= n};
     X c= Seg n
  proof
    let x be set;
    assume x in X;
    then consider k be Nat such that
           A2: k = x & n,k are_relative_prime & k >= 1 & k <= n;
    thus thesis by A2,FINSEQ_1:3;
  end;
  then reconsider X as finite set by FINSET_1:13;
  A3: n < 1 or n = 1 or n > 1 by AXIOMS:21;
  A4:n <> 0
  proof
    assume n = 0;
    then n in { k: k<2 & k is prime} by A1;
    hence contradiction by NEWTON:88;
  end;
  A5: Euler n <= n - 1 by A1,A3,Th20,INT_2:def 5;
     n - 1 <= Euler n
  proof
       n \ {0} c= X
    proof
      let x be set;
      assume x in n \ {0};
      then A6:x in n & not x in {0} by XBOOLE_0:def 4;
      then x in {k: k< n} by AXIOMS:30;
      then ex k st k = x & k < n;
      hence x in X by A1,A6,Th4;
    end;
    then card (n \ {0}) <= card X by CARD_1:80;
    then A7: card (n \ {0}) <= Euler n by Def1;
       0 < n by A4,NAT_1:19;
    then 0 in {l: l < n};
    then A8:0 in n by AXIOMS:30;
       Card n = n & Card {0} = 1 by CARD_1:66,79;
    hence thesis by A7,A8,Th5;
  end;
  hence thesis by A5,AXIOMS:21;
end;

theorem
     m > 1 & n > 1 & m,n are_relative_prime
                      implies Euler (m*n) = Euler m * Euler n
proof
  assume A1:m > 1 & n > 1 & m,n are_relative_prime;
  then A2:m > 0 & n > 0;
  A3:m*n <> 1 by A1,NAT_1:40;
  A4:m*n <> 0 by A2,XCMPLX_1:6;
  then A5:m*n >= 1 by NAT_1:39;
  then A6:m*n > 0;
  set A={i where i is Nat:n,i are_relative_prime & i >= 1 & i <= n};
  set B={i where i is Nat:m,i are_relative_prime & i >= 1 & i <= m};
  set C={i where i is Nat:ex x,y being Integer st
             i = (m*y + n*x) mod m*n & m*n,i are_relative_prime &
                                                y <= n & x <= m & i >= 0};
  set X={i where i is Nat:m*n,i are_relative_prime & i >= 1 & i <= m*n};
  A7:A c= Seg n
  proof
    let x be set;
    assume x in A;
    then consider i being Nat such that
           A8:i = x and n,i are_relative_prime and
           A9:i >= 1 & i <= n;
    thus x in Seg n by A8,A9,FINSEQ_1:3;
  end;
  then reconsider A as finite set by FINSET_1:13;
  A10:B c= Seg m
  proof
    let x be set;
    assume x in B;
    then consider i being Nat such that
           A11:i = x and m,i are_relative_prime and
           A12:i >= 1 & i <= m;
    thus x in Seg m by A11,A12,FINSEQ_1:3;
  end;
  then reconsider B as finite set by FINSET_1:13;
  A13:C c= Seg (m*n) \/ {0}
  proof
    let z be set;
    assume z in C;
    then consider i being Nat such that
           A14:i = z and
           A15:ex x,y being Integer st i = (m*y + n*x) mod m*n &
                m*n,i are_relative_prime & y <= n & x <= m & i >= 0;
    consider x,y being Integer such that
      A16:i = (m*y + n*x) mod m*n and
         m*n,i are_relative_prime and
      A17:y <= n & x <= m & i >= 0 by A15;
    per cases by A17;
      suppose i > 0;
        then A18:i >= 1 by NAT_1:39;
           (m*y + n*x) mod m*n <= m*n
        proof
          set i1 = (m*y + n*x);
          set i2 = m*n;
          A19:(m*y + n*x) mod m*n = i1 - (i1 div i2)*i2 by A4,INT_1:def 8
                                 .= i1 - ([\ i1/i2 /])*i2 by INT_1:def 7;
             ([\ i1/i2 /]) + 1 >= i1/i2 by INT_1:52;
          hence (m*y + n*x) mod m*n <= m*n by A4,A19,Lm5;
        end;
        then z in Seg (m*n) by A14,A16,A18,FINSEQ_1:3;
      hence z in Seg (m*n) \/ {0} by XBOOLE_0:def 2;
      end;
      suppose A20:i = 0;
           0 in {0} by TARSKI:def 1;
    hence z in Seg (m*n) \/ {0} by A14,A20,XBOOLE_0:def 2;
  end;
  end;
  then reconsider C as finite set by FINSET_1:13;
  A21:Euler m = Card B by Def1;
  A22:Euler n = Card A by Def1;
     C = X
  proof
    thus C c= X
    proof
      let z be set;
      assume z in C;
      then consider i being Nat such that
             A23:i = z and
             A24:ex x,y being Integer st i = (m*y + n*x) mod m*n &
              m*n,i are_relative_prime & y <= n & x <= m & i >= 0;
      consider x,y being Integer such that
        A25:i = (m*y + n*x) mod m*n and
        A26:m*n,i are_relative_prime and
           y <= n & x <= m & i >= 0 by A24;
      A27:(m*y + n*x) mod m*n <= m*n
      proof
        set i1 = (m*y + n*x);
        set i2 = m*n;
        A28:(m*y + n*x) mod m*n = i1 - (i1 div i2)*i2 by A4,INT_1:def 8
                               .= i1 - ([\ i1/i2 /])*i2 by INT_1:def 7;
           ([\ i1/i2 /]) + 1 >= i1/i2 by INT_1:52;
        hence (m*y + n*x) mod m*n <= m*n by A4,A28,Lm5;
      end;
      per cases by NAT_1:19;
        suppose i > 0;
          then i >= 1 by NAT_1:39;
        hence z in X by A23,A25,A26,A27;
        end;
        suppose i = 0;
          then m*n hcf 0 = 1 by A26,INT_2:def 6;
      hence z in X by A3,NEWTON:65;
    end;
    end;
    let z be set;
    assume z in X;
    then consider i being Nat such that
           A29:i=z and
           A30:m*n,i are_relative_prime and
           A31:i >= 1 & i <= m*n;
       i <> m*n
    proof
      assume i = m*n;
      then (m*n) hcf (m*n) = 1 by A30,INT_2:def 6;
      hence contradiction by A1,NAT_1:40;
    end;
    then A32:i < m*n by A31,REAL_1:def 5;
    consider y0,x0 being Integer such that
      A33:y0*m + x0*n = i by A1,Th11;
    consider x1,y1 being Integer such that
      A34:x1 = x0 mod m and
      A35:y1 = y0 mod n;
    A36:x0 mod m = x0 - (x0 div m)*m by A2,INT_1:def 8;
    set j = x0 div m;
    A37:x1 = x0 - (x0 div m)*m by A2,A34,INT_1:def 8
           .= x0 - ([\ x0/m /])*m by INT_1:def 7;
       ([\ x0/m /]) + 1 >= x0/m by INT_1:52;
    then A38:x1 <= m by A2,A37,Lm5;
    A39:y0 mod n = y0- (y0 div n)*n by A2,INT_1:def 8;
    set k = y0 div n;
    A40:y1 = y0 - (y0 div n)*n by A2,A35,INT_1:def 8
           .= y0 - ([\ y0/n /])*n by INT_1:def 7;
       ([\ y0/n /]) + 1 >= y0/n by INT_1:52;
    then A41:y1 <= n by A2,A40,Lm5;
    A42: m*y0 + n*x0 = m*y1 + n*x1 + (m*n)*(k+j) by A34,A35,A36,A39;
    A43:0 <= (m*y0 + n*x0) by A33,NAT_1:18;
    A44:i >= 0 by NAT_1:18;
       (m*y0 + n*x0) mod (m*n)
        = (m*y0 + n*x0) - ((m*y0 + n*x0) div (m*n))*(m*n) by A4,INT_1:def 8
       .= (m*y0 + n*x0) - 0*(m*n) by A32,A33,A43,PRE_FF:4
       .= m*y0 + n*x0;
    then m*y0 + n*x0 = (m*y1 + n*x1) mod (m*n) by A42,Th13;
    hence z in C by A29,A30,A33,A38,A41,A44;
  end;
  then A45:Euler (m*n) = Card C by Def1;
  A46:a = 1 implies a in A
  proof
    assume A47:a = 1;
    then n hcf a = 1 by NEWTON:64;
    then n,a are_relative_prime by INT_2:def 6;
    hence thesis by A1,A47;
  end;
  A48:b = 1 implies b in B
  proof
    assume A49:b = 1;
    then m hcf b = 1 by NEWTON:64;
    then m,b are_relative_prime by INT_2:def 6;
    hence thesis by A1,A49;
  end;
  A50:y = 1 & x = 1 implies (m*y + n*x) mod m*n in C
  proof
    assume A51:y = 1 & x = 1;
    then reconsider y'= y,x'= x as Nat;
       m*y' + n*x' <> 0 by A2,A51,NAT_1:23;
    then m*y' + n*x' > 0 by NAT_1:19;
    then A52:(m*y' + n*x') mod m*n = (m*y + n*x) mod m*n by A6,Lm3;
    then consider t being Nat such that
      A53:t = (m*y + n*x) mod m*n;
    A54:m hcf n = 1 by A1,INT_2:def 6;
    then (m + n) hcf n = 1 by Th8;
    then A55:n,(m + n) are_relative_prime by INT_2:def 6;
       (m + n) hcf m = 1 by A54,Th8;
    then A56:m,(m + n) are_relative_prime by INT_2:def 6;
       m + n <> 0 by A2,NAT_1:23;
    then m*n,(m + n) are_relative_prime by A2,A55,A56,Th15;
    then A57: m*n hcf (m + n) = 1 by INT_2:def 6;
    consider d being Nat such that
      A58:(m + n) = (m*n)*d + ((m + n) mod m*n) and
         ((m + n) mod m*n) < (m*n) by A4,NAT_1:def 2;
       (m + n) mod m*n hcf m*n = 1 by A57,A58,Th9;
    then A59:m*n,t are_relative_prime by A51,A52,A53,INT_2:def 6;
       t >= 0 by NAT_1:18;
    hence thesis by A1,A51,A53,A59;
  end;
   {0} c= NAT by ZFMISC_1:37;
  then Seg (m*n) \/ {0} c= NAT by XBOOLE_1:8;
  then reconsider A,B,C as non empty finite Subset of NAT
                                 by A7,A10,A13,A46,A48,A50,XBOOLE_1:1;
  deffunc F(Element of A,Element of B) = (m*$1+n*$2) mod (m*n);
  A60:for y being Element of A, x being Element of B holds F(y,x) in C
  proof
    let y be Element of A, x be Element of B;
       y in A;
    then consider i being Nat such that
           A61:i = y and
           A62:n,i are_relative_prime and
           A63:i >= 1 and
           A64:i <= n;
     x in B;
    then consider j being Nat such that
           A65:j = x and
           A66:m,j are_relative_prime and
           A67:j >= 1 and
           A68:j <= m;
    A69:i <> 0 & j <> 0 by A63,A67;
    reconsider ii = i,jj = j as Integer;
       m*i <> 0*i & n*j <> 0*j by A2,A69,XCMPLX_1:5;
    then m*i + n*j <> 0 by NAT_1:23;
    then m*i + n*j > 0 by NAT_1:19;
    then A70:m*ii + n*jj mod m*n = m*i + n*j mod m*n by A6,Lm3;
    then consider q being Nat such that
      A71:q = (m*ii + n*jj) mod m*n;
       n hcf i = 1 by A62,INT_2:def 6;
    then i,n are_relative_prime by INT_2:def 6;
    then i*m,n are_relative_prime by A1,A69,Th15;
    then A72:i*m hcf n = 1 by INT_2:def 6;
       m hcf j = 1 by A66,INT_2:def 6;
    then A73:j,m are_relative_prime by INT_2:def 6;
       m hcf n = 1 by A1,INT_2:def 6;
    then n,m are_relative_prime by INT_2:def 6;
    then j*n,m are_relative_prime by A2,A69,A73,Th15;
    then A74:j*n hcf m = 1 by INT_2:def 6;
       (m*i + n*j) hcf n = 1 by A72,Th9;
    then A75:n,(m*i + n*j) are_relative_prime by INT_2:def 6;
       (m*i + n*j) hcf m = 1 by A74,Th9;
    then A76:m,(m*i + n*j) are_relative_prime by INT_2:def 6;
       i*m <> 0*m & j*n <> 0*n by A2,A69,XCMPLX_1:5;
    then (m*i + n*j) <> 0 by NAT_1:23;
    then n*m,(m*i + n*j) are_relative_prime by A2,A75,A76,Th15;
    then A77:n*m hcf (m*i + n*j) = 1 by INT_2:def 6;
    consider t being Nat such that
      A78:(m*i + n*j) = (n*m)*t + ((m*i + n*j) mod n*m) and
         ((m*i + n*j) mod n*m) < (n*m) by A4,NAT_1:def 2;
    set l = n*m;
       ((m*i + n*j) mod l) hcf l = 1 by A77,A78,Th9;
    then A79:l,q are_relative_prime by A70,A71,INT_2:def 6;
       q >= 0 by NAT_1:18;
    hence thesis by A61,A64,A65,A68,A70,A71,A79;
  end;
  consider f being Function of [:A,B:], C such that
    A80:for y being Element of A,
    x being Element of B holds f.[y,x] = F(y,x) from FUNCT_7:sch 1(A60);
  A81:f is one-to-one
  proof
    let x,y be set;
    A82:dom f = [:A,B:] by FUNCT_2:def 1;
    assume x in dom f;
    then consider xx1 being Element of A,
                  xx2 being Element of B such that
           A83:x = [xx1,xx2] by A82,DOMAIN_1:9;
       xx1 in A;
    then consider x1 being Nat such that
           A84:xx1 = x1 and
              n,x1 are_relative_prime and
           A85:x1 >= 1 and
           A86:x1 <= n;
       xx2 in B;
    then consider x2 being Nat such that
           A87:xx2 = x2 and
              m,x2 are_relative_prime and
           A88:x2 >= 1 and
           A89:x2 <= m;
    assume y in dom f;
    then consider yy1 being Element of A,
                  yy2 being Element of B such that
           A90:y = [yy1,yy2] by A82,DOMAIN_1:9;
       yy1 in A;
    then consider y1 being Nat such that
           A91:yy1 = y1 and
              n,y1 are_relative_prime and
           A92:y1 >= 1 and
           A93:y1 <= n;
       yy2 in B;
    then consider y2 being Nat such that
           A94:yy2 = y2 and
              m,y2 are_relative_prime and
           A95:y2 >= 1 and
           A96:y2 <= m;
    assume A97:f.x = f.y;
    A98:(x1*m+x2*n) mod (m*n) = f.x by A80,A83,A84,A87
                    .= (y1*m+y2*n) mod (m*n) by A80,A90,A91,A94,A97;
    reconsider y1,y2,x1,x2 as Element of NAT;
    set k = (x1*m+x2*n) mod (m*n);
    A99:y1*m + y2*n = k + ((y1*m + y2*n) div (m*n))*(m*n) by A6,A98,NAT_1:47;
    A100:(x1*m + x2*n)-(y1*m + y2*n) = m*(x1 - y1) + n*(x2 - y2);
    A101:k + ((x1*m + x2*n) div (m*n))*(m*n) -
                  (k + ((y1*m + y2*n) div (m*n))*(m*n))
             = (m*n)*(((x1*m + x2*n) div (m*n)) -
                  ((y1*m + y2*n) div (m*n)));
    set w = (((x1*m + x2*n) div (m*n)) - ((y1*m + y2*n) div (m*n)));
       (m hcf n) = 1 by A1,INT_2:def 6;
    then A102:n,m are_relative_prime by INT_2:def 6;
    consider p being Integer such that
      A103:p = m;
    A104:n,p are_relative_prime by A102,A103,Lm2;
    A105:m*(x1 - y1) + n*(x2 - y2) = (m*n)*w by A6,A99,A100,A101,NAT_1:47;
    then n*(x2 - y2) = m*(n*w) - m*(x1 - y1) by XCMPLX_1:26
               .= m*((n*w) - (x1 - y1));
    then p divides n*(x2 - y2) by A103,INT_1:def 9;
    then A106:m divides (x2 - y2) by A103,A104,INT_2:40;
       x2 - y2 = 0
    proof
      per cases;
        suppose A107:0<=(x2 - y2);
          consider k being Integer such that
            A108:k = x2 - y2;
          reconsider k as Nat by A107,A108,INT_1:16;
          A109:m divides k by A2,A106,A108,Lm4;
             k = 0
          proof
            assume k <> 0;
            then A110:0 < k by NAT_1:19;
               -y2 <= -1 by A95,REAL_1:50;
            then A111:x2 + -y2 <= m + -1 by A89,REAL_1:55;
            set b = m + -1;
             b is Nat by A107,A111,INT_1:16;
            then k < b + 1 by A108,A111,NAT_1:38;
            hence contradiction by A109,A110,NAT_1:54;
          end;
        hence thesis by A108;
        end;
        suppose A112:(x2 - y2)<=0;
          consider k being Integer such that
            A113:k = -(x2 - y2);
          A114:m divides k by A106,A113,INT_2:14;
          A115:-0 <= k by A112,A113,REAL_1:50;
          then reconsider k as Nat by INT_1:16;
          A116:m divides k by A2,A114,Lm4;
             k = 0
          proof
            assume k <> 0;
            then A117:0 < k by NAT_1:19;
               -x2 <= -1 by A88,REAL_1:50;
            then A118: - x2 + y2 <= m + -1 by A96,REAL_1:55;
            set b = m + -1;
             b is Nat by A113,A115,A118,INT_1:16;
            then k < b + 1 by A113,A118,NAT_1:38;
            hence contradiction by A116,A117,NAT_1:54;
          end;
        then 0 -(x2 - y2) = 0 by A113,XCMPLX_1:150;
      hence thesis by XCMPLX_1:16;
    end;
    end;
    then A119:x2 = y2 by XCMPLX_1:15;
    consider p being Integer such that
      A120:p = n;
    A121:m,p are_relative_prime by A1,A120,Lm2;
       m*(x1 - y1) = n*(m*w) - n*(x2 - y2) by A105,XCMPLX_1:26
               .= n*((m*w) - (x2 - y2));
    then p divides m*(x1 - y1) by A120,INT_1:def 9;
    then A122:n divides (x1 - y1) by A120,A121,INT_2:40;
       x1 - y1 = 0
    proof
      per cases;
        suppose A123:0<=(x1 - y1);
          consider k being Integer such that
            A124:k = x1 - y1;
          reconsider k as Nat by A123,A124,INT_1:16;
          A125:n divides k by A2,A122,A124,Lm4;
             k = 0
          proof
            assume k <> 0;
            then A126:0 < k by NAT_1:19;
               -y1 <= -1 by A92,REAL_1:50;
            then A127:x1 + -y1 <= n + -1 by A86,REAL_1:55;
            set b = n + -1;
             b is Nat by A123,A127,INT_1:16;
            then k < b + 1 by A124,A127,NAT_1:38;
            hence contradiction by A125,A126,NAT_1:54;
          end;
        hence thesis by A124;
        end;
        suppose A128:(x1 - y1)<=0;
          consider k being Integer such that
            A129:k = -(x1 - y1);
          A130:n divides k by A122,A129,INT_2:14;
          A131:-0 <= k by A128,A129,REAL_1:50;
          then reconsider k as Nat by INT_1:16;
          A132:n divides k by A2,A130,Lm4;
             k = 0
          proof
            assume k <> 0;
            then A133:0 < k by NAT_1:19;
               -x1 <= -1 by A85,REAL_1:50;
            then A134: - x1 + y1 <= n + -1 by A93,REAL_1:55;
            set b = n + -1;
             b is Nat by A129,A131,A134,INT_1:16;
            then k < b + 1 by A129,A134,NAT_1:38;
          hence contradiction by A132,A133,NAT_1:54;
        end;
        then 0 -(x1 - y1) = 0 by A129,XCMPLX_1:150;
        hence thesis by XCMPLX_1:16;
    end;
    end;
    hence x = y by A83,A84,A87,A90,A91,A94,A119,XCMPLX_1:15;
  end;
     f is onto
  proof
    thus rng f c= C by RELSET_1:12;
    let z be set;
    assume z in C;
    then consider i being Nat such that
           A135:i = z and
           A136:ex x0,y0 being Integer st i = (m*y0 + n*x0) mod m*n &
                m*n,i are_relative_prime & y0 <= n & x0 <= m & i >= 0;
    consider x0,y0 being Integer such that
      A137:i = (m*y0 + n*x0) mod m*n and
      A138:m*n,i are_relative_prime and
      A139:y0 <= n & x0 <= m & i >= 0 by A136;
    consider x,y being Integer such that
      A140:x = x0 mod m and
      A141:y = y0 mod n;
    A142:x <= m & y <= n by A2,A140,A141,NEWTON:79;
    A143:x >= 0 & y >= 0 by A2,A140,A141,NEWTON:78;
    A144:y >= 0 by A2,A141,NEWTON:78;
    A145:(m*y + n*x) mod (m*n) = (m*y0 + n*x0) mod (m*n)
    proof
      A146: x0 mod m = x0 - (x0 div m)*m by A2,INT_1:def 8;
      set j = x0 div m;
      A147: y0 mod n = y0- (y0 div n)*n by A2,INT_1:def 8;
      set k = y0 div n;
       m*y0 + n*x0
                    = m*y + n*x + (m*n)*(k+j) by A140,A141,A146,A147;
      hence thesis by Th13;
    end;
    A148:m,x0 are_relative_prime
    proof
      set p = m gcd x0;
      A149:p is Nat by INT_2:29;
         p divides m by INT_2:31;
      then consider i1 being Integer such that
             A150:m = p*i1 by INT_1:def 9;
         p <> 0 & i1 <> 0 by A1,A150;
      then A151:p > 0 by A149,NAT_1:19;
         p divides x0 by INT_2:32;
      then consider i2 being Integer such that
        A152:x0 = p*i2 by INT_1:def 9;
      A153:m*n hcf i = 1 by A138,INT_2:def 6;
         m*n = abs(m*n) by A6,ABSVALUE:def 1;
      then A154:abs(m*n) hcf abs(i) = 1 by A139,A153,ABSVALUE:def 1;
      A155:(m*y0 + n*x0) mod m*n
               = ((p*i1)*y0 + n*(p*i2)) - (((p*i1)*y0 + n*(p*i2))
                   div((p*i1)*n))*((p*i1)*n) by A4,A150,A152,INT_1:def 8;
      set k = ((p*i1)*y0 + n*(p*i2))div((p*i1)*n);
      A156:((p*i1)*y0 + n*(p*i2)) - k*((p*i1)*n)
                         = p*((i1*y0 + n*i2) - ((i1*k)*n));
      set j = (i1*y0 + n*i2) - ((i1*k)*n);
         not j < 0
      proof
        assume j < 0;
        then p*j < 0*j by A151,REAL_1:71;
        hence contradiction by A4,A137,A139,A150,A152,A156,INT_1:def 8;
      end;
      then reconsider p,j as Nat by INT_1:16,INT_2:29;
      A157:m*n = p*(i1*n) by A150;
      set jj = i1*n;
        not jj < 0
      proof
        assume jj < 0;
        then p*jj < 0*jj by A151,REAL_1:71;
        hence contradiction by A5,A150;
      end;
      then reconsider jj as Nat by INT_1:16;
         now per cases by A2,A157,XCMPLX_1:6;
        suppose A158:p <> 0 & jj <> 0 & j <> 0;
          then A159:p > 0 by NAT_1:19;
             p*jj gcd p*j = 1 by A137,A150,A154,A155,INT_2:def 3;
          then A160:p*(jj gcd j) = 1 by A158,A159,Th16;
             jj gcd j is Nat by INT_2:29;
        hence p = 1 by A160,NAT_1:40;
        end;
        suppose p <> 0 & jj <> 0 & j = 0;
          then p*jj hcf 0 = 1 by A137,A138,A150,A155,A156,INT_2:def 6;
          then p*jj = 1 by NEWTON:65;
        hence p = 1 by NAT_1:40;
      end;
      end;
      hence thesis by INT_2:def 4;
    end;
    A161:n,y0 are_relative_prime
    proof
      set p = n gcd y0;
      A162:p is Nat by INT_2:29;
         p divides n by INT_2:31;
      then consider i1 being Integer such that
             A163:n = p*i1 by INT_1:def 9;
         p <> 0 & i1 <> 0 by A1,A163;
      then A164:p > 0 by A162,NAT_1:19;
         p divides y0 by INT_2:32;
      then consider i2 being Integer such that
        A165:y0 = p*i2 by INT_1:def 9;
      A166:m*n hcf i = 1 by A138,INT_2:def 6;
         m*n = abs(m*n) by A6,ABSVALUE:def 1;
      then A167:abs(m*n) hcf abs(i) = 1 by A139,A166,ABSVALUE:def 1;
      A168:(m*y0 + n*x0) mod m*n
        = (m*(p*i2) + (p*i1)*x0) - ((m*(p*i2) + (p*i1)*x0)
                    div (m*(p*i1)))*(m*(p*i1)) by A4,A163,A165,INT_1:def 8;
      set k = (m*(p*i2) + (p*i1)*x0) div (m*(p*i1));
      A169:(m*(p*i2) + (p*i1)*x0) - k*(m*(p*i1))
                         = p*((m*i2 + i1*x0) - ((i1*k)*m));
      set j = (m*i2 + i1*x0) - ((i1*k)*m);
         j >= 0
      proof
        assume j < 0;
        then p*j < 0*j by A164,REAL_1:71;
        hence contradiction by A4,A137,A139,A163,A165,A169,INT_1:def 8;
      end;
      then reconsider p,j as Nat by INT_1:16,INT_2:29;
      A170:m*n = p*(i1*m) by A163;
      set jj = i1*m;
        not jj < 0
      proof
        assume jj < 0;
        then p*jj < 0*jj by A164,REAL_1:71;
        hence contradiction by A5,A163;
      end;
      then reconsider jj as Nat by INT_1:16;
         now per cases by A2,A170,XCMPLX_1:6;
        suppose A171:p <> 0 & jj <> 0 & j <> 0;
          then A172:p > 0 by NAT_1:19;
             p*jj gcd p*j = 1 by A137,A163,A167,A168,INT_2:def 3;
          then A173:p*(jj gcd j) = 1 by A171,A172,Th16;
             jj gcd j is Nat by INT_2:29;
        hence p = 1 by A173,NAT_1:40;
        end;
        suppose p <> 0 & jj <> 0 & j = 0;
          then p*jj hcf 0 = 1 by A137,A138,A163,A168,A169,INT_2:def 6;
          then p*jj = 1 by NEWTON:65;
        hence p = 1 by NAT_1:40;
      end;
      end;
      hence thesis by INT_2:def 4;
    end;
    reconsider x,y as Nat by A143,INT_1:16;
    A174:x <> 0
    proof
      assume x = 0;
      then A175:x0 - (x0 div m)*m = 0 by A2,A140,INT_1:def 8;
      set j = x0 div m;
      A176:(m*y0 + n*x0) mod m*n = (m*y0 + n*(j*m)) mod m*n by A175,XCMPLX_1:15
           .= m*(y0 + (n*j)) - ((m*(y0 + (n*j))) div m*n)*(m*n)
             by A4,INT_1:def 8
           .= m*((y0 + (n*j)) - (n*((m*(y0 + (n*j))) div m*n)));
      set jj = (y0 + (n*j)) - (n*((m*(y0 + (n*j))) div m*n));
      reconsider g = m*jj as Nat by A137,A176;
      A177:g > 0 or g = 0 by NAT_1:19;
      A178:not jj < 0
      proof
        assume jj < 0;
        then m*jj < 0*jj by A2,REAL_1:71;
        hence contradiction by A177;
      end;
      A179:m*n hcf i = 1 by A138,INT_2:def 6;
         m*n = abs(m*n) by A6,ABSVALUE:def 1;
      then abs(m*n) hcf abs(i) = 1 by A139,A179,ABSVALUE:def 1;
      then A180:m*n gcd m*jj = 1 by A137,A176,INT_2:def 3;
      reconsider jj as Nat by A178,INT_1:16;
        jj = 0
      proof
        assume not(jj = 0);
        then A181:m*(n gcd jj) = 1 by A2,A180,Th16;
           n gcd jj is Nat by INT_2:29;
        hence contradiction by A1,A181,NAT_1:40;
      end;
      then m*n hcf 0 = 1 by A137,A138,A176,INT_2:def 6;
      hence contradiction by A3,NEWTON:65;
    end;
    then A182:x >= 0 + 1 by A143,INT_1:20;
    A183:y <> 0
    proof
      assume y = 0;
      then A184:y0 - (y0 div n)*n = 0 by A2,A141,INT_1:def 8;
      set j = y0 div n;
      A185:(m*y0 + n*x0) mod m*n = (m*(j*n) + n*x0) mod m*n by A184,XCMPLX_1:15
           .= n*((m*j) + x0) - ((n*((m*j) + x0)) div m*n)*(m*n)
             by A4,INT_1:def 8
           .= n*(((m*j) + x0) - (m*((n*((m*j) + x0)) div m*n)));
      set jj = ((m*j) + x0) - (m*((n*((m*j) + x0)) div m*n));
      reconsider g = n*jj as Nat by A137,A185;
      A186:g > 0 or g = 0 by NAT_1:19;
      A187:not jj < 0
      proof
        assume jj < 0;
        then n*jj < 0*jj by A2,REAL_1:71;
        hence contradiction by A186;
      end;
      A188:m*n hcf i = 1 by A138,INT_2:def 6;
         m*n = abs(m*n) by A6,ABSVALUE:def 1;
      then abs(m*n) hcf abs(i) = 1 by A139,A188,ABSVALUE:def 1;
      then A189:m*n gcd n*jj = 1 by A137,A185,INT_2:def 3;
      reconsider jj as Nat by A187,INT_1:16;
        jj = 0
      proof
        assume not(jj = 0);
        then A190:n*(m gcd jj) = 1 by A2,A189,Th16;
           m gcd jj is Nat by INT_2:29;
        hence contradiction by A1,A190,NAT_1:40;
      end;
      then m*n hcf 0 = 1 by A137,A138,A185,INT_2:def 6;
      hence contradiction by A3,NEWTON:65;
   end;
   then A191:y >= 0 + 1 by A144,INT_1:20;
   A192:m,x are_relative_prime
   proof
     A193:x = x0 - (x0 div m)*m by A2,A140,INT_1:def 8;
     A194:m >= 0 & x >= 0 by NAT_1:18;
        x0 gcd m = x + (x0 div m)*m gcd m by A193;
     then A195:x0 gcd m = x gcd m by A2,A174,Th17;
        x0 gcd m = 1 by A148,INT_2:def 4;
     then m hcf x = 1 by A194,A195,Lm1;
     hence thesis by INT_2:def 6;
   end;
   A196:n,y are_relative_prime
   proof
     A197:y = y0 - (y0 div n)*n by A2,A141,INT_1:def 8;
     A198:n >= 0 & y >= 0 by NAT_1:18;
        y0 gcd n = y + (y0 div n)*n gcd n by A197;
     then A199:y0 gcd n = y gcd n by A2,A183,Th17;
        y0 gcd n = 1 by A161,INT_2:def 4;
     then n hcf y = 1 by A198,A199,Lm1;
     hence thesis by INT_2:def 6;
   end;
      x in B by A142,A182,A192;
   then reconsider x as Element of B;
      y in A by A142,A191,A196;
   then reconsider y as Element of A;
   A200:n*x > n*0 by A2,A143,A174,REAL_1:70;
      m*y > m*0 by A2,A144,A183,REAL_1:70;
   then (m*y + n*x) > 0+0 by A200,REAL_1:67;
   then A201:i = (m*y + n*x) mod (m*n) by A6,A137,A145,Lm3;
   A202:dom f = [:A,B:] by FUNCT_2:def 1;
   consider w being set such that
     A203:w = [y,x];
   A204:w in dom f by A202,A203,ZFMISC_1:106;
      i = f.w by A80,A201,A203;
   hence z in rng f by A135,A204,FUNCT_1:def 5;
 end;
 then Card[:A,B:] = Card C by A81,Th12;
hence thesis by A21,A22,A45,CARD_2:65;
end;
