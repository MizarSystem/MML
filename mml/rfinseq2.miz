:: Sorting Operators for Finite Sequences
::  by Yatsuka Nakamura
::
:: Received October 17, 2003
:: Copyright (c) 2003 Association of Mizar Users

environ

 vocabularies RELAT_1, CARD_1, FUNCT_1, BOOLE, FUNCT_2, FINSEQ_1, ARYTM_1,
      SQUARE_1, PROB_1, RFINSEQ, RFINSEQ2, CQC_LANG;
 notations TARSKI, XBOOLE_0, NUMBERS, XREAL_0, RELAT_1, FUNCT_1, FUNCT_2,
      FINSEQ_1, CARD_1, REAL_1, NAT_1, SQUARE_1, SEQ_1, RVSUM_1, CQC_LANG,
      RFINSEQ, INTEGRA2;
 constructors REAL_1, NAT_1, SQUARE_1, SEQ_1, CQC_LANG, RFINSEQ, INTEGRA2,
      BINOP_2;
 registrations NUMBERS, FUNCT_1, RELSET_1, FINSEQ_1, XREAL_0, FUNCT_2, REAL_1,
      NAT_1, RFINSEQ, INTEGRA2, MEMBERED;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions INTEGRA2;
 theorems AXIOMS, TARSKI, FINSEQ_1, FINSEQ_2, FINSEQ_3, REAL_1, NAT_1, FUNCT_1,
      FUNCT_2, SQUARE_1, RVSUM_1, RELAT_1, XCMPLX_1, CQC_LANG, EUCLID_2,
      RFINSEQ, XBOOLE_0, INTEGRA2;
 schemes NAT_1;

begin

reserve n,m for Nat;

definition let f be FinSequence of REAL;
func max_p f -> Nat means
 :Def1: (len f=0 implies it=0) &
 (len f>0 implies
 it in dom f &
 (for i being Nat,r1,r2 being Real st i in dom f & r1=f.i & r2=f.it
 holds r1<=r2) & (for j being Nat st j in dom f & f.j=f.it holds it<=j));
 existence
 proof
  A1: dom f=Seg len f by FINSEQ_1:def 3;
  per cases;
  suppose len f=0;
  hence ex n being Nat st
 (len f=0 implies n=0) & (len f>0 implies n in dom f &
 (for i be Nat,r1,r2 be Real st i in dom f & r1=f.i & r2=f.n
 holds r1<=r2) & (for j being Nat st j in dom f & f.j=f.n holds n<=j));
  end;
  suppose A2: len f<>0;
  defpred P[Nat] means (ex n being Nat st ($1<>0 implies n<=$1 &
   n in dom f) &
   (for i being Nat,r1,r2 being Real st i<=$1 & i in dom f
   & r1=f.i & r2=f.n holds r1<=r2) &
   (for j being Nat st j<=$1 & j in dom f & f.j=f.n holds n<=j));
    A3: for i being Nat,r1,r2 being Real st i<=0 & i in dom f
     & r1=f.i & r2=f.1 holds r1<=r2 by A1,FINSEQ_1:3;
      for j being Nat st j<=0 & j in dom f & f.j=f.1 holds 1<=j by FINSEQ_3:27;
    then A5: P[0] by A3;
   A6: for k being Nat st P[k] holds P[k+1]
   proof let k be Nat;
    assume P[k];
     then consider n1 being Nat such that
     A7: (k<>0 implies n1<=k & n1 in dom f) &
      (for i being Nat,r1,r2 being Real st i<=k & i in dom f
      & r1=f.i & r2=f.n1
      holds r1<=r2) & (for j being Nat st j<=k & j in dom f & f.j=f.n1
      holds n1<=j);
      per cases;
      suppose A8: k=0;
        A9: dom f=Seg len f by FINSEQ_1:def 3;
          len f>0 by A2,NAT_1:19;
        then len f>=0+1 by NAT_1:38;
        then A10: 1<=1 & 1 in dom f by A9,FINSEQ_1:3;
        A11: for i being Nat,r1,r2 being Real st i<=1 & i in dom f
        & r1=f.i & r2=f.1 holds r1<=r2
        proof let i be Nat,r1,r2 be Real;
         assume A12: i<=1 & i in dom f & r1=f.i & r2=f.1;
           then 1<=i & i<=len f by FINSEQ_3:27;
         hence r1<=r2 by A12,AXIOMS:21;
        end;
          for j being Nat st j<=1 & j in dom f & f.j=f.1
          holds 1<=j by A9,FINSEQ_1:3;
       hence P[k+1] by A8,A10,A11;
      end;
      suppose A13: k<>0;
         now per cases;
       case A14: f.n1>=f.(k+1);
      A15: n1<=k+1 by A7,A13,NAT_1:38;
      A16: k+1<>0 implies n1<=k+1 & n1 in dom f by A7,A13,NAT_1:38;
      A17: for i being Nat,r1,r2 being Real st i<=k+1 & i in dom f
       & r1=f.i & r2=f.n1
      holds r1<=r2
      proof let i be Nat,r1,r2 be Real;
       assume A18: i<=k+1 & i in dom f
       & r1=f.i & r2=f.n1;
        per cases;
        suppose i<k+1; then i<=k by NAT_1:38;
         hence r1<=r2 by A7,A18;
        end;
        suppose i>=k+1;
         hence r1<=r2 by A14,A18,AXIOMS:21;
      end;
      end;
        for j being Nat st j<=k+1 & j in dom f & f.j=f.n1
       holds n1<=j
       proof let j be Nat;
        assume A19: j<=k+1 & j in dom f & f.j=f.n1;
          now per cases;
        case j<k+1; then j<=k by NAT_1:38;
         hence n1<=j by A7,A19;
        end;
        case j>=k+1;
         hence n1<=j by A15,A19,AXIOMS:21;
        end;
        end;
        hence n1<=j;
       end;
      hence P[k+1] by A16,A17;
      end;
      case A20: f.n1<f.(k+1);
         now per cases;
       case A21: k+1>len f;
        then A22: k>=len f by NAT_1:38;
          1<=n1 & n1<=len f by A1,A7,A13,FINSEQ_1:3;
        then A23: k+1<>0 implies n1<=k+1 & n1 in dom f by A7,A21,AXIOMS:22;
        A24: for i being Nat,r1,r2 being Real st i<=k+1 & i in dom f
           & r1=f.i & r2=f.n1 holds r1<=r2
         proof let i be Nat,r1,r2 be Real;
          assume A25: i<=k+1 & i in dom f
           & r1=f.i & r2=f.n1;
           then 1<=i & i<=len f by A1,FINSEQ_1:3;
           then i<=k by A22,AXIOMS:22;
          hence r1<=r2 by A7,A25;
         end;
           for j being Nat st j<=k+1 & j in dom f & f.j=f.n1
           holds n1<=j
          proof let j be Nat;
           assume A26: j<=k+1 & j in dom f & f.j=f.n1;
            per cases;
            suppose j<k+1;
              then j<=k by NAT_1:38;
             hence n1<=j by A7,A26;
            end;
            suppose j>=k+1;
             then k<j by NAT_1:38;
             hence n1<=j by A7,A13,AXIOMS:22;
          end;
          end;
       hence P[k+1] by A23,A24;
       end;
       case A27: k+1<=len f;
A28:     1<=1+k by NAT_1:37;
       set n2=k+1;
      A29: (k+1<>0 implies n2<=k+1 &
      n2 in dom f) by A1,A27,A28,FINSEQ_1:3;
     A30: for i being Nat,r1,r2 being Real st i<=k+1 & i in dom f
       & r1=f.i & r2=f.n2 holds r1<=r2
      proof let i be Nat,r1,r2 be Real;
       assume A31: i<=k+1 & i in dom f & r1=f.i & r2=f.n2;
        per cases;
        suppose i<k+1;
          then A32: i<=k by NAT_1:38;
          reconsider r3=f.n1 as Real;
            r1<=r3 by A7,A31,A32;
         hence r1<=r2 by A20,A31,AXIOMS:22;
        end;
        suppose i>=k+1;
         hence r1<=r2 by A31,AXIOMS:21;
      end;
      end;
        for j being Nat st j<=k+1 & j in dom f & f.j=f.n2 holds n2<=j
       proof let j be Nat;
        assume A33: j<=k+1 & j in dom f & f.j=f.n2;
        per cases;
        suppose j<k+1;
          then j<=k by NAT_1:38;
         hence n2<=j by A7,A20,A33;
        end;
        suppose j>=k+1;
         hence n2<=j;
       end;
       end;
       hence P[k+1] by A29,A30;
      end;
      end;
    hence P[k+1];
    end;
    end;
    hence P[k+1];
   end;
   end;
     for k being Nat holds P[k] from NAT_1:sch 1(A5,A6);
   then consider n1 being Nat such that
   A34: (len f<>0 implies n1<=len f &
   n1 in dom f) &
   (for i being Nat,r1,r2 being Real st i<=len f & i in dom f
   & r1=f.i & r2=f.n1
 holds r1<=r2) & (for j being Nat st j<=len f & j in dom f & f.j=f.n1
      holds n1<=j);
   A35: for i being Nat,r1,r2 being Real st i in dom f & r1=f.i & r2=f.n1
    holds r1<=r2
   proof let i be Nat,r1,r2 be Real;
    assume A36: i in dom f & r1=f.i & r2=f.n1;
     then 1<=i & i<=len f by FINSEQ_3:27;
    hence r1<=r2 by A34,A36;
   end;
     for j being Nat st j in dom f & f.j=f.n1 holds n1<=j
   proof let j be Nat;
    assume A37: j in dom f & f.j=f.n1;
     then 1<=j & j<=len f by FINSEQ_3:27;
    hence n1<=j by A34,A37;
   end;
  hence ex n being Nat st
 (len f=0 implies n=0) &
 (len f>0 implies n in dom f &
 (for i being Nat,r1,r2 being Real st i in dom f & r1=f.i & r2=f.n
 holds r1<=r2) & (for j being Nat st j in dom f & f.j=f.n holds n<=j))
              by A2,A34,A35;
 end;
 end;
 uniqueness
 proof
 thus (for m1,m2 being Nat st ((len f=0 implies m1=0) &
 (len f>0 implies
 m1 in dom f &
 (for i being Nat,r1,r2 being Real st i in dom f & r1=f.i & r2=f.m1
 holds r1<=r2) & (for j being Nat st j in dom f & f.j=f.m1 holds m1<=j)))
 &
  ((len f=0 implies m2=0) &
 (len f>0 implies m2 in dom f &
 (for i being Nat,r1,r2 being Real st i in dom f & r1=f.i & r2=f.m2
 holds r1<=r2) & (for j being Nat st j in dom f & f.j=f.m2 holds m2<=j)))
 holds m1=m2)
 proof let m1,m2 be Nat;
  assume A38: ((len f=0 implies m1=0) &
   (len f>0 implies
   m1 in dom f &
   (for i being Nat,r1,r2 being Real st i in dom f & r1=f.i & r2=f.m1
   holds r1<=r2) & (for j being Nat st j in dom f & f.j=f.m1 holds m1<=j)))
   &
    ((len f=0 implies m2=0) &
   (len f>0 implies m2 in dom f &
   (for i being Nat,r1,r2 being Real st i in dom f & r1=f.i & r2=f.m2
   holds r1<=r2) & (for j being Nat st j in dom f & f.j=f.m2 holds m2<=j)));
     then A39: f.m2<=f.m1 by NAT_1:19;
       f.m1<=f.m2 by A38,NAT_1:19;
     then A40: f.m1=f.m2 by A39,AXIOMS:21;
     then A41: m1<=m2 by A38,NAT_1:19;
       m2<=m1 by A38,A40,NAT_1:19;
    hence m1=m2 by A41,AXIOMS:21;
 end;
 end;
end;

definition let f be FinSequence of REAL;
func min_p f -> Nat means
 :Def2: (len f=0 implies it=0) &
 (len f>0 implies
 it in dom f &
 (for i being Nat,r1,r2 being Real st i in dom f & r1=f.i & r2=f.it
 holds r1>=r2) & (for j being Nat st j in dom f & f.j=f.it holds it<=j));
 existence
 proof
  A1: dom f=Seg len f by FINSEQ_1:def 3;
    now per cases;
  case len f=0;
  hence ex n being Nat st
 (len f=0 implies n=0) & (len f>0 implies n in dom f &
 (for i be Nat,r1,r2 be Real st i in dom f & r1=f.i & r2=f.n
 holds r1>=r2) & (for j being Nat st j in dom f & f.j=f.n holds n<=j));
  end;
  case A2: len f<>0;
  defpred P[Nat] means (ex n being Nat st ($1<>0 implies n<=$1 &
   n in dom f) &
   (for i being Nat,r1,r2 being Real st i<=$1 & i in dom f
   & r1=f.i & r2=f.n
 holds r1>=r2) & (for j being Nat st j<=$1 & j in dom f & f.j=f.n
      holds n<=j));
    A3: for i being Nat,r1,r2 being Real st i<=0 & i in dom f
     & r1=f.i & r2=f.1
     holds r1>=r2 by A1,FINSEQ_1:3;
      for j being Nat st j<=0 & j in dom f & f.j=f.1
      holds 1<=j by FINSEQ_3:27;
   then A5: P[0] by A3;
   A6: for k being Nat st P[k] holds P[k+1]
   proof let k be Nat;
    assume P[k];
     then consider n1 being Nat such that
     A7: (k<>0 implies n1<=k &
      n1 in dom f) &
      (for i being Nat,r1,r2 being Real st i<=k & i in dom f
      & r1=f.i & r2=f.n1
      holds r1>=r2) & (for j being Nat st j<=k & j in dom f & f.j=f.n1
      holds n1<=j);
        now per cases;
      case A8: k=0;
        A9: dom f=Seg len f by FINSEQ_1:def 3;
          len f>0 by A2,NAT_1:19;
        then len f>=0+1 by NAT_1:38;
        then A10: 1<=1 & 1 in dom f by A9,FINSEQ_1:3;
        A11: for i being Nat,r1,r2 being Real st i<=1 & i in dom f
        & r1=f.i & r2=f.1 holds r1>=r2
        proof let i be Nat,r1,r2 be Real;
         assume A12: i<=1 & i in dom f
           & r1=f.i & r2=f.1;
           then 1<=i & i<=len f by FINSEQ_3:27;
         hence r1>=r2 by A12,AXIOMS:21;
        end;
          for j being Nat st j<=1 & j in dom f & f.j=f.1
          holds 1<=j by A9,FINSEQ_1:3;
       hence P[k+1] by A8,A10,A11;
      end;
      case A13: k<>0;
         now per cases;
       case A14: f.n1<=f.(k+1);
      A15: n1<=k+1 by A7,A13,NAT_1:38;
      A16: k+1<>0 implies n1<=k+1 & n1 in dom f by A7,A13,NAT_1:38;
      A17: for i being Nat,r1,r2 being Real st i<=k+1 & i in dom f
       & r1=f.i & r2=f.n1 holds r1>=r2
      proof let i be Nat,r1,r2 be Real;
       assume A18: i<=k+1 & i in dom f & r1=f.i & r2=f.n1;
        per cases;
        suppose i<k+1; then i<=k by NAT_1:38;
         hence r1>=r2 by A7,A18;
        end;
        suppose i>=k+1;
         hence r1>=r2 by A14,A18,AXIOMS:21;
      end;
      end;
        for j being Nat st j<=k+1 & j in dom f & f.j=f.n1 holds n1<=j
       proof let j be Nat;
        assume A19: j<=k+1 & j in dom f & f.j=f.n1;
        per cases;
        suppose j<k+1; then j<=k by NAT_1:38;
         hence n1<=j by A7,A19;
        end;
        suppose j>=k+1;
         hence n1<=j by A15,A19,AXIOMS:21;
       end;
       end;
      hence P[k+1] by A16,A17;
      end;
      case A20: f.n1>f.(k+1);
         now per cases;
       case A21: k+1>len f;
        then A22: k>=len f by NAT_1:38;
          1<=n1 & n1<=len f by A1,A7,A13,FINSEQ_1:3;
        then A23: k+1<>0 implies n1<=k+1 & n1 in dom f by A7,A21,AXIOMS:22;
        A24: for i being Nat,r1,r2 being Real st i<=k+1 & i in dom f
           & r1=f.i & r2=f.n1 holds r1>=r2
         proof let i be Nat,r1,r2 be Real;
          assume A25: i<=k+1 & i in dom f
           & r1=f.i & r2=f.n1;
           then 1<=i & i<=len f by A1,FINSEQ_1:3;
           then i<=k by A22,AXIOMS:22;
          hence r1>=r2 by A7,A25;
         end;
           for j being Nat st j<=k+1 & j in dom f & f.j=f.n1
           holds n1<=j
          proof let j be Nat;
           assume A26: j<=k+1 & j in dom f & f.j=f.n1;
            per cases;
            suppose j<k+1;
             then j<=k by NAT_1:38;
             hence n1<=j by A7,A26;
            end;
            suppose j>=k+1;
             then k<j by NAT_1:38;
             hence n1<=j by A7,A13,AXIOMS:22;
          end;
          end;
       hence P[k+1] by A23,A24;
       end;
       case A27: k+1<=len f;
A28:     1<=1+k by NAT_1:37;
       set n2=k+1;
      A29: (k+1<>0 implies n2<=k+1 &
      n2 in dom f) by A1,A27,A28,FINSEQ_1:3;
     A30: for i being Nat,r1,r2 being Real st i<=k+1 & i in dom f
       & r1=f.i & r2=f.n2 holds r1>=r2
      proof let i be Nat,r1,r2 be Real;
       assume A31: i<=k+1 & i in dom f & r1=f.i & r2=f.n2;
        per cases;
        suppose i<k+1;
          then A32: i<=k by NAT_1:38;
          reconsider r3=f.n1 as Real;
            r1>=r3 by A7,A31,A32;
         hence r1>=r2 by A20,A31,AXIOMS:22;
        end;
        suppose i>=k+1;
         hence r1>=r2 by A31,AXIOMS:21;
      end;
      end;
        for j being Nat st j<=k+1 & j in dom f & f.j=f.n2
       holds n2<=j
       proof let j be Nat;
        assume A33: j<=k+1 & j in dom f & f.j=f.n2;
        per cases;
        suppose j<k+1;
          then j<=k by NAT_1:38;
         hence n2<=j by A7,A20,A33;
        end;
        suppose j>=k+1;
         hence n2<=j;
       end;
       end;
       hence P[k+1] by A29,A30;
       end;
       end;
       hence P[k+1];
      end;
      end;
    hence P[k+1];
    end;
    end;
    hence P[k+1];
   end;
     for k being Nat holds P[k] from NAT_1:sch 1(A5,A6);
   then consider n1 being Nat such that
   A34: (len f<>0 implies n1<=len f & n1 in dom f) &
   (for i being Nat,r1,r2 being Real st i<=len f & i in dom f
   & r1=f.i & r2=f.n1
 holds r1>=r2) & (for j being Nat st j<=len f & j in dom f & f.j=f.n1
      holds n1<=j);
   A35: for i being Nat,r1,r2 being Real st i in dom f & r1=f.i & r2=f.n1
    holds r1>=r2
   proof let i be Nat,r1,r2 be Real;
    assume A36: i in dom f & r1=f.i & r2=f.n1;
     then 1<=i & i<=len f by FINSEQ_3:27;
    hence r1>=r2 by A34,A36;
   end;
     for j being Nat st j in dom f & f.j=f.n1 holds n1<=j
   proof let j be Nat;
    assume A37: j in dom f & f.j=f.n1;
     then 1<=j & j<=len f by FINSEQ_3:27;
    hence n1<=j by A34,A37;
   end;
  hence ex n being Nat st (len f=0 implies n=0) &
 (len f>0 implies n in dom f &
 (for i being Nat,r1,r2 being Real st i in dom f & r1=f.i & r2=f.n
 holds r1>=r2) & (for j being Nat st j in dom f & f.j=f.n holds n<=j))
              by A2,A34,A35;
  end;
  end;
  hence ex n being Nat st
 (len f=0 implies n=0) & (len f>0 implies n in dom f &
 (for i be Nat,r1,r2 be Real st i in dom f & r1=f.i & r2=f.n
 holds r1>=r2) & (for j being Nat st j in dom f & f.j=f.n holds n<=j));
 end;
 uniqueness
 proof
 thus (for m1,m2 being Nat st ((len f=0 implies m1=0) &
 (len f>0 implies
 m1 in dom f &
 (for i being Nat,r1,r2 being Real st i in dom f & r1=f.i & r2=f.m1
 holds r1>=r2) & (for j being Nat st j in dom f & f.j=f.m1 holds m1<=j)))
 &
  ((len f=0 implies m2=0) &
 (len f>0 implies
 m2 in dom f &
 (for i being Nat,r1,r2 being Real st i in dom f & r1=f.i & r2=f.m2
 holds r1>=r2) & (for j being Nat st j in dom f & f.j=f.m2 holds m2<=j)))
 holds m1=m2)
 proof let m1,m2 be Nat;
  assume A38: ((len f=0 implies m1=0) &
   (len f>0 implies
   m1 in dom f &
   (for i being Nat,r1,r2 being Real st i in dom f & r1=f.i & r2=f.m1
   holds r1>=r2) & (for j being Nat st j in dom f & f.j=f.m1 holds m1<=j)))
   &
    ((len f=0 implies m2=0) &
   (len f>0 implies m2 in dom f &
   (for i being Nat,r1,r2 being Real st i in dom f & r1=f.i & r2=f.m2
   holds r1>=r2) & (for j being Nat st j in dom f & f.j=f.m2 holds m2<=j)));
     then A39: f.m2>=f.m1 by NAT_1:19;
       f.m1>=f.m2 by A38,NAT_1:19;
     then A40: f.m1=f.m2 by A39,AXIOMS:21;
     then A41: m1>=m2 by A38,NAT_1:19;
       m2>=m1 by A38,A40,NAT_1:19;
    hence m1=m2 by A41,AXIOMS:21;
 end;
 end;
end;

definition let f be FinSequence of REAL;
func max f -> Real equals
:Def3: f.(max_p f);
correctness;
func min f -> Real equals
:Def4: f.(min_p f);
correctness;
end;

theorem Th1: for f being FinSequence of REAL,i being Nat
 st 1<=i & i<=len f holds f.i<=f.(max_p f) & f.i<=max f
proof let f be FinSequence of REAL,i be Nat;
 assume A1: 1<=i & i<=len f;
  then A2: i in dom f by FINSEQ_3:27;
    0<len f by A1;
 hence f.i<=f.(max_p f) by A2,Def1;
 hence f.i<=max f by Def3;
end;

theorem Th2: for f being FinSequence of REAL,i being Nat
 st 1<=i & i<=len f holds f.i>=f.(min_p f) & f.i>=min f
proof let f be FinSequence of REAL,i be Nat;
 assume A1: 1<=i & i<=len f;
  then A2: i in dom f by FINSEQ_3:27;
    0<len f by A1;
 hence f.i>=f.(min_p f) by A2,Def2;
 hence f.i>=min f by Def4;
end;

theorem   for f being FinSequence of REAL,r being Real
 st f=<*r*> holds max_p f=1 & max f=r
proof let f be FinSequence of REAL,r be Real;
 assume f=<*r*>;
  then A1: len f=1 & f.1=r by FINSEQ_1:57;
  then (max_p f) in dom f by Def1;
  then 1<= (max_p f) & (max_p f)<=len f by FINSEQ_3:27;
  then max_p f=1 by A1,AXIOMS:21;
 hence max_p f=1 & max f=r by A1,Def3;
end;

theorem   for f being FinSequence of REAL,r being Real
 st f=<*r*> holds min_p f=1 & min f=r
proof let f be FinSequence of REAL,r be Real;
 assume f=<*r*>;
  then A1: len f=1 & f.1=r by FINSEQ_1:57;
  then (min_p f) in dom f by Def2;
  then 1<= (min_p f) & (min_p f)<=len f by FINSEQ_3:27;
  then min_p f=1 by A1,AXIOMS:21;
 hence min_p f=1 & min f=r by A1,Def4;
end;

theorem   for f being FinSequence of REAL,r1,r2 being Real
 st f=<*r1,r2*> holds max f=max(r1,r2) & max_p f=IFEQ(r1,max(r1,r2),1,2)
proof let f be FinSequence of REAL,r1,r2 be Real;
 assume A1: f=<*r1,r2*>;
  then A2: len f=2 & f.1=r1 & f.2=r2 by FINSEQ_1:61;
  then (max_p f) in dom f by Def1;
  then A3: 1<= (max_p f) & (max_p f)<=len f by FINSEQ_3:27;
  A4: f.1<=f.(max_p f) & f.1<=max f by A2,Th1;
  A5: f.2<=f.(max_p f) & f.2<=max f by A2,Th1;
    now per cases;
  case r1>=r2;
   then A6: max(r1,r2)<=max f by A2,A4,SQUARE_1:def 2;
   A7: max f=f.(max_p f) by Def3;
     now per cases;
   case max_p f<len f; then max_p f <1+1 by A1,FINSEQ_1:61;
     then max_p f <=1 by NAT_1:38;
     then A8: max_p f=1 by A3,AXIOMS:21;
      then max f <=max(r1,r2) by A2,A7,SQUARE_1:46;
     then max f=max(r1,r2) by A6,AXIOMS:21;
    hence max f=max(r1,r2) & max_p f=IFEQ(r1,max(r1,r2),1,2)
                    by A2,A7,A8,CQC_LANG:def 1;
   end;
   case max_p f>=len f;
     then A9: max_p f=2 by A2,A3,AXIOMS:21;
       1 in dom f by A2,FINSEQ_3:27;
     then A10: r1<>r2 by A2,A9,Def1;
       max f <=max(r1,r2) by A2,A7,A9,SQUARE_1:46;
     then max f=max(r1,r2) by A6,AXIOMS:21;
    hence max f=max(r1,r2) & max_p f=IFEQ(r1,max(r1,r2),1,2)
                      by A2,A7,A9,A10,CQC_LANG:def 1;
   end;
   end;
   hence max f=max(r1,r2) & max_p f=IFEQ(r1,max(r1,r2),1,2);
  end;
  case r1<r2;
   then A11: max(r1,r2)<=max f by A2,A5,SQUARE_1:def 2;
   A12: max f=f.(max_p f) by Def3;
     now per cases;
   case max_p f<len f; then max_p f <1+1 by A1,FINSEQ_1:61;
     then max_p f <=1 by NAT_1:38;
     then A13: max_p f=1 by A3,AXIOMS:21;
      then max f <=max(r1,r2) by A2,A12,SQUARE_1:46;
     then max f=max(r1,r2) by A11,AXIOMS:21;
    hence max f=max(r1,r2) & max_p f=IFEQ(r1,max(r1,r2),1,2)
                       by A2,A12,A13,CQC_LANG:def 1;
   end;
   case max_p f>=len f;
     then A14: max_p f=2 by A2,A3,AXIOMS:21;
       1 in dom f by A2,FINSEQ_3:27;
     then A15: r1<>r2 by A2,A14,Def1;
       max f <=max(r1,r2) by A2,A12,A14,SQUARE_1:46;
     then max f=max(r1,r2) by A11,AXIOMS:21;
    hence max f=max(r1,r2) & max_p f=IFEQ(r1,max(r1,r2),1,2)
                        by A2,A12,A14,A15,CQC_LANG:def 1;
   end;
   end;
   hence max f=max(r1,r2) & max_p f=IFEQ(r1,max(r1,r2),1,2);
  end;
  end;
 hence thesis;
end;

theorem   for f being FinSequence of REAL,r1,r2 being Real
 st f=<*r1,r2*> holds min f=min(r1,r2) & min_p f=IFEQ(r1,min(r1,r2),1,2)
proof let f be FinSequence of REAL,r1,r2 be Real;
 assume A1: f=<*r1,r2*>;
  then A2: len f=2 & f.1=r1 & f.2=r2 by FINSEQ_1:61;
  then (min_p f) in dom f by Def2;
  then A3: 1<= (min_p f) & (min_p f)<=len f by FINSEQ_3:27;
  A4: f.1>=f.(min_p f) & f.1>=min f by A2,Th2;
  A5: f.2>=f.(min_p f) & f.2>=min f by A2,Th2;
  per cases;
  suppose r1<=r2;
   then A6: min(r1,r2)>=min f by A2,A4,SQUARE_1:def 1;
   A7: min f=f.(min_p f) by Def4;
     now per cases;
   case min_p f<len f; then min_p f <1+1 by A1,FINSEQ_1:61;
     then min_p f <=1 by NAT_1:38;
     then A8: min_p f=1 by A3,AXIOMS:21;
      then min f >=min(r1,r2) by A2,A7,SQUARE_1:35;
     then min f=min(r1,r2) by A6,AXIOMS:21;
    hence min f=min(r1,r2) & min_p f=IFEQ(r1,min(r1,r2),1,2)
                              by A2,A7,A8,CQC_LANG:def 1;
   end;
   case min_p f>=len f;
     then A9: min_p f=2 by A2,A3,AXIOMS:21;
       1 in dom f by A2,FINSEQ_3:27;
     then A10: r1<>r2 by A2,A9,Def2;
       min f >=min(r1,r2) by A2,A7,A9,SQUARE_1:35;
     then min f=min(r1,r2) by A6,AXIOMS:21;
    hence min f=min(r1,r2) & min_p f=IFEQ(r1,min(r1,r2),1,2)
                            by A2,A7,A9,A10,CQC_LANG:def 1;
   end;
   end;
   hence min f=min(r1,r2) & min_p f=IFEQ(r1,min(r1,r2),1,2);
  end;
  suppose r1>r2;
   then A11: min(r1,r2)>=min f by A2,A5,SQUARE_1:def 1;
   A12: min f=f.(min_p f) by Def4;
     now per cases;
   case min_p f<len f; then min_p f <1+1 by A1,FINSEQ_1:61;
     then min_p f <=1 by NAT_1:38;
     then A13: min_p f=1 by A3,AXIOMS:21;
      then min f >=min(r1,r2) by A2,A12,SQUARE_1:35;
     then min f=min(r1,r2) by A11,AXIOMS:21;
    hence min f=min(r1,r2) & min_p f=IFEQ(r1,min(r1,r2),1,2)
                            by A2,A12,A13,CQC_LANG:def 1;
   end;
   case min_p f>=len f;
     then A14: min_p f=2 by A2,A3,AXIOMS:21;
       1 in dom f by A2,FINSEQ_3:27;
     then A15: r1<>r2 by A2,A14,Def2;
       min f >=min(r1,r2) by A2,A12,A14,SQUARE_1:35;
     then min f=min(r1,r2) by A11,AXIOMS:21;
    hence min f=min(r1,r2) & min_p f=IFEQ(r1,min(r1,r2),1,2)
                              by A2,A12,A14,A15,CQC_LANG:def 1;
   end;
   end;
   hence thesis;
end;
end;

theorem   for f1,f2 being FinSequence of REAL st len f1=len f2 & len f1>0
holds max (f1+f2)<=(max f1) +(max f2)
proof let f1,f2 be FinSequence of REAL;
 assume A1: len f1=len f2 & len f1>0;
  then A2: len (f1+f2)=len f1 by EUCLID_2:6;
    then A3: max_p (f1+f2) in dom (f1+f2) by A1,Def1;
    then A4: 1<=max_p (f1+f2) & max_p (f1+f2)<=len (f1+f2) by FINSEQ_3:27;
    A5: max (f1+f2)=(f1+f2).(max_p (f1+f2)) by Def3
    .=f1.(max_p (f1+f2)) + f2.(max_p (f1+f2)) by A3,RVSUM_1:26;
    A6: max_p (f1+f2) in Seg len f1 by A2,A4,FINSEQ_1:3;
    then max_p (f1+f2) in dom f1 by FINSEQ_1:def 3;
    then A7: f1.(max_p (f1+f2))<=f1.(max_p f1) by A1,Def1;
    A8: f1.(max_p f1)=max f1 by Def3;
      max_p (f1+f2) in dom f2 by A1,A6,FINSEQ_1:def 3;
    then A9: f2.(max_p (f1+f2))<=f2.(max_p f2) by A1,Def1;
      f2.(max_p f2)=max f2 by Def3;
   hence max (f1+f2)<=(max f1) +(max f2) by A5,A7,A8,A9,REAL_1:55;
 end;

theorem   for f1,f2 being FinSequence of REAL st len f1=len f2 & len f1>0
holds min (f1+f2)>=(min f1) +(min f2)
proof let f1,f2 be FinSequence of REAL;
 assume A1: len f1=len f2 & len f1>0;
  then A2: len (f1+f2)=len f1 by EUCLID_2:6;
  then A3: min_p (f1+f2) in dom (f1+f2) by A1,Def2;
  then A4: 1<=min_p (f1+f2) & min_p (f1+f2)<=len (f1+f2) by FINSEQ_3:27;
  A5: min (f1+f2)=(f1+f2).(min_p (f1+f2)) by Def4
  .=f1.(min_p (f1+f2)) + f2.(min_p (f1+f2)) by A3,RVSUM_1:26;
  A6: min_p (f1+f2) in Seg len f1 by A2,A4,FINSEQ_1:3;
  then min_p (f1+f2) in dom f1 by FINSEQ_1:def 3;
  then A7: f1.(min_p (f1+f2))>=f1.(min_p f1) by A1,Def2;
  A8: f1.(min_p f1)=min f1 by Def4;
    min_p (f1+f2) in dom f2 by A1,A6,FINSEQ_1:def 3;
  then A9: f2.(min_p (f1+f2))>=f2.(min_p f2) by A1,Def2;
    f2.(min_p f2)=min f2 by Def4;
 hence min (f1+f2)>=(min f1) +(min f2) by A5,A7,A8,A9,REAL_1:55;
 end;

theorem   for f being FinSequence of REAL, a being Real st len f>0 & a>0
holds max (a*f)=a*(max f) & max_p (a*f)=max_p f
proof let f be FinSequence of REAL, a be Real;
 assume A1: len f>0 & a>0;
  A2: len (a*f)=len f by EUCLID_2:8;
  A3: dom (a*f)=dom f by RVSUM_1:65;
  A4: max_p (a*f) in dom (a*f) by A1,A2,Def1;
  then A5: 1<=max_p (a*f) & max_p (a*f)<=len (a*f) by FINSEQ_3:27;
  A6: max (a*f)=(a*f).(max_p (a*f)) by Def3
  .=a*(f.(max_p (a*f))) by RVSUM_1:66;
    max_p (a*f) in Seg len f by A2,A5,FINSEQ_1:3;
  then A7: max_p (a*f) in dom f by FINSEQ_1:def 3;
  then A8: f.(max_p (a*f))<=f.(max_p f) by A1,Def1;
  A9: a*(f.(max_p (f)))=(a*f).(max_p (f)) by RVSUM_1:66;
  A10: a*(f.(max_p (a*f)))=(a*f).(max_p (a*f)) by RVSUM_1:66;
  A11: max_p (f) in dom (a*f) by A1,A3,Def1;
  then (a*f).(max_p (f))<=(a*f).(max_p (a*f)) by A1,A2,Def1;
  then f.(max_p (f))<=f.(max_p (a*f)) by A1,A9,A10,REAL_1:70;
  then A12: f.(max_p (f))=f.(max_p (a*f)) by A8,AXIOMS:21;
    max_p (f) in dom (a*f) by A1,A3,Def1;
  then A13: (a*f).(max_p (a*f))>=(a*f).(max_p f) by A1,A2,Def1;
    f.(max_p (f))>= f.(max_p (a*f)) by A1,A7,Def1;
  then a*(f.(max_p (f)))>=a*(f.(max_p (a*f))) by A1,AXIOMS:25;
  then A14: (a*f).(max_p (a*f))=(a*f).(max_p f) by A9,A10,A13,AXIOMS:21;
  A15: f.(max_p f)=max f by Def3;
  A16: max_p (a*f)>=max_p f by A1,A3,A4,A12,Def1;
    max_p (a*f)<=max_p f by A1,A2,A11,A14,Def1;
 hence max (a*f)=a*(max f) & max_p (a*f)=max_p f
                    by A6,A15,A16,AXIOMS:21;
end;

theorem   for f being FinSequence of REAL, a being Real st
 len f>0 & a>0
holds min (a*f)=a*(min f) & min_p (a*f)=min_p f
proof let f be FinSequence of REAL, a be Real;
 assume A1: len f>0 & a>0;
  A2: len (a*f)=len f by EUCLID_2:8;
  A3: dom (a*f)=dom f by RVSUM_1:65;
  A4: min_p (a*f) in dom (a*f) by A1,A2,Def2;
  then A5: 1<=min_p (a*f) & min_p (a*f)<=len (a*f) by FINSEQ_3:27;
  A6: min (a*f)=(a*f).(min_p (a*f)) by Def4
  .=a*(f.(min_p (a*f))) by RVSUM_1:66;
  A7: min_p (a*f) in dom f by A2,A5,FINSEQ_3:27;
  then A8: f.(min_p (a*f))>=f.(min_p f) by A1,Def2;
  A9: a*(f.(min_p (f)))=(a*f).(min_p (f)) by RVSUM_1:66;
  A10: a*(f.(min_p (a*f)))=(a*f).(min_p (a*f)) by RVSUM_1:66;
  A11: min_p (f) in dom (a*f) by A1,A3,Def2;
  then (a*f).(min_p (f))>=(a*f).(min_p (a*f)) by A1,A2,Def2;
  then f.(min_p (f))>=f.(min_p (a*f)) by A1,A9,A10,REAL_1:70;
  then A12: f.(min_p (f))=f.(min_p (a*f)) by A8,AXIOMS:21;
    min_p (f) in dom (a*f) by A1,A3,Def2;
  then A13: (a*f).(min_p (a*f))<=(a*f).(min_p f) by A1,A2,Def2;
    f.(min_p (f))<= f.(min_p (a*f)) by A1,A7,Def2;
  then a*(f.(min_p (f)))<=a*(f.(min_p (a*f))) by A1,AXIOMS:25;
  then A14: (a*f).(min_p (a*f))=(a*f).(min_p f) by A9,A10,A13,AXIOMS:21;
  A15: f.(min_p f)=min f by Def4;
  A16: min_p (a*f)>=min_p f by A1,A3,A4,A12,Def2;
    min_p (a*f)<=min_p f by A1,A2,A11,A14,Def2;
 hence min (a*f)=a*(min f) & min_p (a*f)=min_p f
                    by A6,A15,A16,AXIOMS:21;
end;

theorem   for f being FinSequence of REAL st
 len f>0
holds max (-f)=-(min f) & max_p (-f)=min_p f
proof let f be FinSequence of REAL;
 assume A1: len f>0;
  A2: len (-f)=len f by EUCLID_2:5;
  A3: dom (-f)=dom f by RVSUM_1:34;
  A4: max_p (-f) in dom (-f) by A1,A2,Def1;
  then A5: 1<=max_p (-f) & max_p (-f)<=len (-f) by FINSEQ_3:27;
  A6: max (-f)=(-f).(max_p (-f)) by Def3
  .=-(f.(max_p (-f))) by RVSUM_1:35;
  A7: min_p (f) in dom (-f) by A1,A3,Def2;
  A8: -(f.(min_p (f)))=(-f).(min_p (f)) by RVSUM_1:35;
  A9: -(f.(max_p (-f)))=(-f).(max_p (-f)) by RVSUM_1:35;
    max_p (-f) in Seg len f by A2,A5,FINSEQ_1:3;
  then A10: max_p (-f) in dom f by FINSEQ_1:def 3;
    (-f).(max_p (-f))>=(-f).(min_p f) by A1,A2,A7,Def1;
  then A11: f.(max_p (-f))<=f.(min_p f) by A8,A9,REAL_1:50;
    f.(min_p (f))<=f.(max_p (-f)) by A1,A10,Def2;
  then A12: f.(min_p (f))=f.(max_p (-f)) by A11,AXIOMS:21;
    min_p (f) in dom (-f) by A1,A3,Def2;
  then A13: (-f).(max_p (-f))>=(-f).(min_p f) by A1,A2,Def1;
    f.(min_p (f))<= f.(max_p (-f)) by A1,A10,Def2;
  then -(f.(min_p (f)))>=-(f.(max_p (-f))) by REAL_1:50;
  then A14: (-f).(max_p (-f))=(-f).(min_p f) by A8,A9,A13,AXIOMS:21;
  A15: f.(min_p f)=min f by Def4;
  A16: max_p (-f)>=min_p f by A1,A3,A4,A12,Def2;
    max_p (-f)<=min_p f by A1,A2,A7,A14,Def1;
 hence max (-f)=-(min f) & max_p (-f)=min_p f by A6,A15,A16,AXIOMS:21;
end;

theorem   for f being FinSequence of REAL st len f>0
holds min (-f)=-(max f) & min_p (-f)=max_p f
proof let f be FinSequence of REAL;
 assume A1: len f>0;
  A2: len (-f)=len f by EUCLID_2:5;
  A3: dom (-f)=dom f by RVSUM_1:34;
  A4: min_p (-f) in dom (-f) by A1,A2,Def2;
  then A5: 1<=min_p (-f) & min_p (-f)<=len (-f) by FINSEQ_3:27;
  A6: min (-f)=(-f).(min_p (-f)) by Def4
  .=-(f.(min_p (-f))) by RVSUM_1:35;
  A7: max_p (f) in dom (-f) by A1,A3,Def1;
  A8: -(f.(max_p (f)))=(-f).(max_p (f)) by RVSUM_1:35;
  A9: -(f.(min_p (-f)))=(-f).(min_p (-f)) by RVSUM_1:35;
    min_p (-f) in Seg len f by A2,A5,FINSEQ_1:3;
  then A10: min_p (-f) in dom f by FINSEQ_1:def 3;
    (-f).(min_p (-f))<=(-f).(max_p f) by A1,A2,A7,Def2;
  then A11: f.(min_p (-f))>=f.(max_p f) by A8,A9,REAL_1:50;
    f.(max_p (f))>=f.(min_p (-f)) by A1,A10,Def1;
  then A12: f.(max_p (f))=f.(min_p (-f)) by A11,AXIOMS:21;
    max_p (f) in dom (-f) by A1,A3,Def1;
  then A13: (-f).(min_p (-f))<=(-f).(max_p f) by A1,A2,Def2;
    f.(max_p (f))>= f.(min_p (-f)) by A1,A10,Def1;
  then -(f.(max_p (f)))<=-(f.(min_p (-f))) by REAL_1:50;
  then A14: (-f).(min_p (-f))=(-f).(max_p f) by A8,A9,A13,AXIOMS:21;
  A15: f.(max_p f)=max f by Def3;
  A16: min_p (-f)>=max_p f by A1,A3,A4,A12,Def1;
    min_p (-f)<=max_p f by A1,A2,A7,A14,Def2;
 hence min (-f)=-(max f) & min_p (-f)=max_p f by A6,A15,A16,AXIOMS:21;
end;

theorem   for f being FinSequence of REAL,n being Nat st
 1<=n & n<len f
holds max (f/^n)<= max f & min (f/^n)>= min f
proof let f be FinSequence of REAL,n be Nat;
 assume A1: 1<=n & n<len f;
  then A2: 0<len f;
    A3: len (f/^n)=len f -n by A1,RFINSEQ:def 2;
    then A4: len (f/^n)>0 by A1,SQUARE_1:11;
    then A5: (max_p (f/^n)) in dom (f/^n) by Def1;
    then A6: 1<= (max_p (f/^n)) & (max_p (f/^n))<=len (f/^n) by FINSEQ_3:27;
    then A7: (max_p (f/^n))+n<=len f-n+n by A3,REAL_1:55;
    A8: 1+n<=(max_p (f/^n))+n by A6,REAL_1:55;
      1<=1+n by NAT_1:37;
    then 1<= ((max_p (f/^n))+n) & ((max_p (f/^n))+n)<=len f
                      by A7,A8,AXIOMS:22;
    then A9: ((max_p (f/^n))+n) in dom f by FINSEQ_3:27;
      f.( (max_p (f/^n))+n)=(f/^n).(max_p (f/^n)) by A1,A5,RFINSEQ:def 2
                        .=max (f/^n) by Def3;
    then A10: max (f/^n)<= f.(max_p f) by A2,A9,Def1;
    A11: (min_p (f/^n)) in dom (f/^n) by A4,Def2;
    then A12: 1<= (min_p (f/^n)) & (min_p (f/^n))<=len (f/^n) by FINSEQ_3:27;
    then A13: (min_p (f/^n))+n<=len f-n+n by A3,REAL_1:55;
    A14: 1+n<=(min_p (f/^n))+n by A12,REAL_1:55;
      1<=1+n by NAT_1:37;
    then 1<= ((min_p (f/^n))+n) & ((min_p (f/^n))+n)<=len f
                      by A13,A14,AXIOMS:22;
    then A15: ((min_p (f/^n))+n) in dom f by FINSEQ_3:27;
      f.( (min_p (f/^n))+n)=(f/^n).(min_p (f/^n)) by A1,A11,RFINSEQ:def 2
                        .=min (f/^n) by Def4;
    then min (f/^n)>= f.(min_p f) by A2,A15,Def2;
 hence max (f/^n)<= max f & min (f/^n)>= min f by A10,Def3,Def4;
end;

 Lm1: for f,g being FinSequence of REAL st
f,g are_fiberwise_equipotent holds max f<=max g
proof let f,g be FinSequence of REAL;
 assume A1: f,g are_fiberwise_equipotent;
  then consider P being Permutation of dom g such that
  A2: f = g*P by RFINSEQ:17;
  A3: len f=len g & dom f=dom g by A1,RFINSEQ:16;
  per cases;
  suppose A4: len f>0;
    then A5: max_p f in dom (g*P) by A2,Def1;
    then A6: (g*P).(max_p f)=g.(P.(max_p f)) by FUNCT_1:22;
    A7: dom g=Seg len g by FINSEQ_1:def 3;
    A8: 0+1<=len f by A4,NAT_1:38;
    A9: rng P=dom g by FUNCT_2:def 3;
      dom g=Seg len g by FINSEQ_1:def 3;
    then dom g <>{} by A3,A8,FINSEQ_1:3;
    then dom P=dom g by FUNCT_2:def 1;
    then A10: P.(max_p f) in rng P by A2,A3,A5,FUNCT_1:def 5;
    then reconsider n=P.(max_p f) as Nat by A9;
      1<=n & n<=len g by A7,A9,A10,FINSEQ_1:3;
    then g.n <= max g by Th1;
   hence max f<=max g by A2,A6,Def3;
  end;
  suppose len f<=0;
    then A11: len f=0 by NAT_1:18;
    then f={} by FINSEQ_1:25;
   hence max f<=max g by A3,A11,FINSEQ_1:25;
end;
end;

theorem Th14: for f,g being FinSequence of REAL st
f,g are_fiberwise_equipotent holds max f=max g
proof let f,g be FinSequence of REAL;
 assume A1: f,g are_fiberwise_equipotent;
  then A2: max f <= max g by Lm1;
    max g <= max f by A1,Lm1;
 hence max f=max g by A2,AXIOMS:21;
end;

Lm2: for f,g being FinSequence of REAL st
f,g are_fiberwise_equipotent holds min f>=min g
proof let f,g be FinSequence of REAL;
 assume A1: f,g are_fiberwise_equipotent;
  then consider P being Permutation of dom g such that
  A2: f = g*P by RFINSEQ:17;
  A3: len f=len g & dom f=dom g by A1,RFINSEQ:16;
  per cases;
  suppose A4: len f>0;
    then A5: min_p f in dom (g*P) by A2,Def2;
    then A6: (g*P).(min_p f)=g.(P.(min_p f)) by FUNCT_1:22;
    A7: min_p f in dom g by A1,A2,A5,RFINSEQ:16;
    A8: dom g=Seg len g by FINSEQ_1:def 3;
    A9: 0+1<=len f by A4,NAT_1:38;
    A10: rng P=dom g by FUNCT_2:def 3;
      dom g=Seg len g by FINSEQ_1:def 3;
     then dom g <>{} by A3,A9,FINSEQ_1:3;
    then dom P=dom g by FUNCT_2:def 1;
    then A11: P.(min_p f) in rng P by A7,FUNCT_1:def 5;
    then reconsider n=P.(min_p f) as Nat by A10;
      1<=n & n<=len g by A8,A10,A11,FINSEQ_1:3;
    then g.n >= min g by Th2;
   hence min f>=min g by A2,A6,Def4;
  end;
  suppose len f<=0;
    then A12: len f=0 by NAT_1:18;
    then f={} by FINSEQ_1:25;
   hence min f>=min g by A3,A12,FINSEQ_1:25;
end;
end;

theorem Th15: for f,g being FinSequence of REAL st
f,g are_fiberwise_equipotent holds min f=min g
proof let f,g be FinSequence of REAL;
 assume A1: f,g are_fiberwise_equipotent;
  then A2: min f >= min g by Lm2;
    min g >= min f by A1,Lm2;
 hence min f=min g by A2,AXIOMS:21;
end;

definition let f be FinSequence of REAL;
func sort_d f -> non-increasing FinSequence of REAL means
::Descend Sorting or Rearrangement of FinSequences
:Def5: f,it are_fiberwise_equipotent;
existence by RFINSEQ:35;
uniqueness
proof
 thus for f1,f2 being non-increasing FinSequence of REAL st
  f,f1 are_fiberwise_equipotent & f,f2 are_fiberwise_equipotent
  holds f1=f2
  proof let f1,f2 be non-increasing FinSequence of REAL;
   assume f,f1 are_fiberwise_equipotent & f,f2 are_fiberwise_equipotent;
   then f1,f2 are_fiberwise_equipotent by RFINSEQ:2;
   hence f1=f2 by RFINSEQ:36;
  end;
end;
end;

theorem Th16:
for R be FinSequence of REAL
  st len R = 0 or len R = 1 holds R is non-decreasing
 proof let R be FinSequence of REAL; assume
  A1: len R = 0 or len R = 1;
     per cases by A1;
   suppose len R = 0;
    then R = <*>REAL by FINSEQ_1:32;
    then for n st n in dom R & n+1 in dom R holds R.n<=R.(n+1) by FINSEQ_1:26;
    hence thesis by INTEGRA2:def 1;
   end;
   suppose len R = 1;
    then A2: dom R = {1} by FINSEQ_1:4,def 3;
         now let n; assume n in dom R & n+1 in dom R;
      then n = 1 & n+1 = 1 by A2,TARSKI:def 1;
      hence R.n<=R.(n+1);
     end;
    hence thesis by INTEGRA2:def 1;
 end;
 end;

theorem Th17:
for R be FinSequence of REAL holds
                      R is non-decreasing
                              iff
            for n,m be Nat st n in dom R & m in dom R & n<m holds R.n<=R.m
proof let R be FinSequence of REAL;
  thus R is non-decreasing implies
   for n,m st n in dom R & m in dom R & n<m holds R.n<=R.m
   proof assume
    A1: R is non-decreasing;
    defpred P[Nat] means $1 in dom R implies
      for n st n in dom R & n<$1 holds R.n<=R.$1;
         Seg len R = dom R by FINSEQ_1:def 3;
    then A2: P[0] by FINSEQ_1:3;
    A3: for m st P[m] holds P[m+1]
     proof let m; assume
      A4: P[m]; assume
      A5: m+1 in dom R;
      let n; assume
      A6: n in dom R & n<m+1;
      then A7: 1<=m+1 & m+1<=len R & 1<=n & n<=len R & n<=m & m<=m+1
        by A5,FINSEQ_3:27,NAT_1:38;
      then A8: 1<=m & m<=len R by AXIOMS:22;
      then A9: m in dom R by FINSEQ_3:27;
      set t = R.m, r = R.n, s = R.(m+1);
           now per cases;
       case n = m; hence r<=s by A1,A5,A6,INTEGRA2:def 1;
       end;
       case n <> m;
        then n<m by A7,REAL_1:def 5;
        then A10: r<=t by A4,A6,A8,FINSEQ_3:27;
            t<=s by A1,A5,A9,INTEGRA2:def 1;
        hence r<=s by A10,AXIOMS:22;
       end;
       end;
      hence r<=s;
     end;
        for m holds P[m] from NAT_1:sch 1(A2,A3);
    hence thesis;
   end; assume
  A11: for n,m st n in dom R & m in dom R & n<m holds R.n<=R.m;
  let n; assume
  A12: n in dom R & n+1 in dom R;
      n<n+1 by NAT_1:38;
  hence thesis by A11,A12;
 end;

Lm3:
for f,g be non-decreasing FinSequence of REAL, n st
  len f = n+1 & len f = len g & f,g are_fiberwise_equipotent holds
  f.(len f) = g.(len g) & f|n, g|n are_fiberwise_equipotent
 proof let f,g be non-decreasing FinSequence of REAL, n; assume
   A1: len f = n+1 & len f = len g & f,g are_fiberwise_equipotent;
       0<n+1 by NAT_1:19;
   then 0+1<=n+1 by NAT_1:38;
   then A2: n+1 in dom f & n+1 in dom g by A1,FINSEQ_3:27;
   set r = f.(n+1), s = g.(n+1);
   A3: now assume A4: r <> s;
     A5: rng f = rng g by A1,RFINSEQ:1;
          now per cases by A4,REAL_1:def 5;
      case A6: r<s;
           s in rng f by A2,A5,FUNCT_1:def 5;
       then consider m such that
       A7: m in dom f & f.m = s by FINSEQ_2:11;
       A8: 1<=m & m<=len f by A7,FINSEQ_3:27;
            now per cases;
        case m = len f;
         hence contradiction by A1,A6,A7;
        end;
        case m <> len f;
         then m<n+1 by A1,A8,REAL_1:def 5;
         hence contradiction by A2,A6,A7,Th17;
        end;
        end;
       hence contradiction;
      end;
      case A9: r>s;
           r in rng g by A2,A5,FUNCT_1:def 5;
       then consider m such that
       A10: m in dom g & g.m = r by FINSEQ_2:11;
       A11: 1<=m & m<=len g by A10,FINSEQ_3:27;
            now per cases;
        case m = len g;
         hence contradiction by A1,A9,A10;
        end;
        case m <> len g;
         then m<n+1 by A1,A11,REAL_1:def 5;
         hence contradiction by A2,A9,A10,Th17;
        end;
        end;
       hence contradiction;
      end;
      end;
     hence contradiction;
    end;
   hence f.(len f) = g.(len g) by A1;
   A12: f = (f|n)^<*r*> & g = (g|n)^<*r*> by A1,A3,RFINSEQ:20;
        now let x be set;
         card((f|n)"{x}) + card(<*r*>"{x}) = card (f"{x}) by A12,FINSEQ_3:63
     .= card(g"{x}) by A1,RFINSEQ:def 1
     .= card((g|n)"{x}) + card(<*r*>"{x}) by A12,FINSEQ_3:63;
    hence card((f|n)"{x}) = card((g|n)"{x}) by XCMPLX_1:2;
    end;
   hence thesis by RFINSEQ:def 1;
 end;

theorem Th18:
for R be non-decreasing FinSequence of REAL, n be Nat holds
  R|n is non-decreasing FinSequence of REAL
 proof let f be non-decreasing FinSequence of REAL, n;
  set fn = f|n;
        now per cases;
    case A1: n = 0; then n<=len f by NAT_1:18;
     then len fn = 0 by A1,FINSEQ_1:80;
     hence thesis by Th16;
    end;
    case n <> 0;
     then 0<n by NAT_1:19;
     then A2: 0+1<=n by NAT_1:38;
          now per cases;
      case len f<=n; hence thesis by FINSEQ_1:79;
      end;
      case n<len f;
       then A3: n in dom f & len fn = n by A2,FINSEQ_1:80,FINSEQ_3:27;
            now let m; assume
         A4: m in dom fn & m+1 in dom fn;
         dom f = Seg len f & dom fn = Seg len fn by FINSEQ_1:def 3;
         then f.m = fn.m & f.(m+1) = fn.(m+1) & m in dom f & m+1 in dom f
           by A3,A4,RFINSEQ:19;
         hence fn.m<=fn.(m+1) by INTEGRA2:def 1;
        end;
       hence thesis by INTEGRA2:def 1;
      end;
      end;
     hence thesis;
    end;
    end;
  hence thesis;
 end;

Lm4:
for n holds for g1,g2 be non-decreasing FinSequence of REAL st
  n = len g1 & g1,g2 are_fiberwise_equipotent holds g1 = g2
 proof
  defpred P[Nat] means
   for g1,g2 be non-decreasing FinSequence of REAL st $1 = len g1 &
     g1,g2 are_fiberwise_equipotent holds g1 = g2;
   A1: P[0]
    proof
     let g1,g2 be non-decreasing FinSequence of REAL; assume that
     A2: len g1 = 0 and
     A3: g1,g2 are_fiberwise_equipotent;
         len g2 = len g1 by A3,RFINSEQ:16;
     then g1 = <*>REAL & g2 = <*>REAL by A2,FINSEQ_1:32;
     hence thesis;
    end;
   A4: for n st P[n] holds P[n+1]
    proof let n; assume
     A5: P[n];
     let g1,g2 be non-decreasing FinSequence of REAL; assume that
     A6: len g1 = n+1 and
     A7: g1,g2 are_fiberwise_equipotent;
     A8: len g2 = len g1 by A7,RFINSEQ:16;
     then A9: g1.(len g1) = g2.(len g2) & g1|n, g2|n are_fiberwise_equipotent
      by A6,A7,Lm3;
         n<=len g1 by A6,NAT_1:29;
     then A10: len(g1|n) = n & len(g2|n) = n by A8,FINSEQ_1:80;
     reconsider g1n = g1|n, g2n = g2|n as non-decreasing FinSequence of REAL
       by Th18;
     set r = g1.(n+1);
     A11: g1n = g2n by A5,A9,A10;
         (g1|n)^<*r*> = g1 & (g2|n)^<*r*> = g2 by A6,A8,A9,RFINSEQ:20;
     hence thesis by A11;
    end;
      for m holds P[m] from NAT_1:sch 1(A1,A4);
   hence thesis;
 end;

theorem Th19:
  for R1,R2 be non-decreasing FinSequence of REAL st
  R1,R2 are_fiberwise_equipotent holds R1 = R2
 proof let g1,g2 be non-decreasing FinSequence of REAL; assume
  A1: g1,g2 are_fiberwise_equipotent;
      len g1 = len g1;
  hence thesis by A1,Lm4;
 end;

definition let f be FinSequence of REAL;
func sort_a f -> non-decreasing FinSequence of REAL means
::Ascend Sorting or Rearrangement of FinSequences
:Def6: f,it are_fiberwise_equipotent;
existence by INTEGRA2:3;
uniqueness
proof
 thus for f1,f2 being non-decreasing FinSequence of REAL st
  f,f1 are_fiberwise_equipotent & f,f2 are_fiberwise_equipotent
  holds f1=f2
  proof let f1,f2 be non-decreasing FinSequence of REAL;
   assume f,f1 are_fiberwise_equipotent & f,f2 are_fiberwise_equipotent;
   then f1,f2 are_fiberwise_equipotent by RFINSEQ:2;
   hence f1=f2 by Th19;
  end;
end;
end;

theorem   for f being non-increasing FinSequence of REAL
 holds sort_d f=f by Def5;

theorem   for f being non-decreasing FinSequence of REAL
 holds sort_a f=f by Def6;

theorem   for f being FinSequence of REAL
 holds sort_d (sort_d f)=sort_d f by Def5;

theorem   for f being FinSequence of REAL
 holds sort_a (sort_a f)=sort_a f by Def6;

theorem Th24: for f being FinSequence of REAL st f is non-increasing
holds -f is non-decreasing
proof let f be FinSequence of REAL;
 assume A1: f is non-increasing;
    for n being Nat st n in dom (-f) & n+1 in dom (-f) holds
         (-f).n <= (-f).(n+1)
  proof let n be Nat;
   assume A2: n in dom (-f) & n+1 in dom (-f);
      dom (-f)=dom f by RVSUM_1:34;
    then A3: f.n >= f.(n+1) by A1,A2,RFINSEQ:def 4;
    A4: (-f).n=-(f.n) by RVSUM_1:35;
     (-f).(n+1)= -(f.(n+1)) by RVSUM_1:35;
   hence (-f).n <= (-f).(n+1) by A3,A4,REAL_1:50;
  end;
 hence -f is non-decreasing by INTEGRA2:def 1;
end;

theorem Th25: for f being FinSequence of REAL st f is non-decreasing
holds -f is non-increasing
proof let f be FinSequence of REAL;
 assume A1: f is non-decreasing;
    for n being Nat st n in dom (-f) & n+1 in dom (-f) holds
         (-f).n >= (-f).(n+1)
  proof let n be Nat;
   assume A2: n in dom (-f) & n+1 in dom (-f);
      dom (-f)=dom f by RVSUM_1:34;
    then A3: f.n <= f.(n+1) by A1,A2,INTEGRA2:def 1;
    A4: (-f).n=-(f.n) by RVSUM_1:35;
     (-f).(n+1)= -(f.(n+1)) by RVSUM_1:35;
   hence (-f).n >= (-f).(n+1) by A3,A4,REAL_1:50;
  end;
 hence -f is non-increasing by RFINSEQ:def 4;
end;

theorem Th26: for f,g being FinSequence of REAL,P being Permutation of dom g
st f = g*P & len g>=1 holds -f=(-g)*P
proof let f,g be FinSequence of REAL,P be Permutation of dom g;
 assume A1: f = g*P & len g>=1;
  A2: rng P=dom g by FUNCT_2:def 3;
  A3: dom g=Seg len g by FINSEQ_1:def 3;
  then 1 in dom g by A1,FINSEQ_1:3;
  then dom g <>{};
  then A4: dom P=dom g by FUNCT_2:def 1;
  A5: dom (-g)=dom g by RVSUM_1:34;
  A6: dom (-f)=dom (g*P) by A1,RVSUM_1:34;
  then A7: dom (-f)=dom P by A2,RELAT_1:46;
  then A8: dom (-f)=dom ((-g)*P) by A2,A5,RELAT_1:46;
  A9: rng ((-g)*P) = rng (-g) by A2,A5,RELAT_1:47;
  A10: rng (-g) c= REAL by FINSEQ_1:def 4;
    (-g)*P is FinSequence by A3,A4,A7,A8,FINSEQ_1:def 2;
  then reconsider k=(-g)*P as FinSequence of REAL by A9,A10,FINSEQ_1:def 4;
    for i being Nat st i in dom (-f) holds (-f).i=k.i
  proof let i be Nat;
   assume A11: i in dom (-f);
    then P.i in rng P by A7,FUNCT_1:12;
    then reconsider j=P.i as Nat by A2;
      (-f).i=-(f.i) by RVSUM_1:35
    .=-(g.(P.i)) by A1,A6,A11,FUNCT_1:22
    .=(-g).(j) by RVSUM_1:35 .=((-g)*P).i by A8,A11,FUNCT_1:22;
   hence (-f).i=k.i;
  end;
 hence -f=(-g)*P by A8,FINSEQ_1:17;
end;

theorem Th27: for f,g being FinSequence of REAL
st f,g are_fiberwise_equipotent holds -f,-g are_fiberwise_equipotent
proof let f,g be FinSequence of REAL;
 assume A1: f,g are_fiberwise_equipotent;
  then consider P being Permutation of dom g such that
  A2: f = g*P by RFINSEQ:17;
  A3: dom (-g)=dom g by RVSUM_1:34;
    now per cases;
  case len g>=1;
   hence -f=(-g)*P by A2,Th26;
  end;
  case len g<1; then len g<0+1;
   then len g<=0 by NAT_1:38;
   then A4: len g=0 by NAT_1:18;
   then A5: len f=0 by A1,RFINSEQ:16;
   then A6: f={} by FINSEQ_1:25;
   A7: g={} by A4,FINSEQ_1:25;
     len (-f)=0 by A5,EUCLID_2:5;
   then -f ={} by FINSEQ_1:25;
   hence -f=(-g)*P by A2,A6,A7;
  end;
  end;
 hence -f,-g are_fiberwise_equipotent by A3,RFINSEQ:17;
end;

theorem   for f being FinSequence of REAL holds
 sort_d (-f) = - (sort_a f)
proof let f be FinSequence of REAL;
  A1: f,sort_a(f) are_fiberwise_equipotent by Def6;
  A2: -sort_d (-f) is non-decreasing by Th24;
    -f,sort_d(-f) are_fiberwise_equipotent by Def5;
  then --f,-sort_d(-f) are_fiberwise_equipotent by Th27;
  then sort_a(f),-sort_d (-f) are_fiberwise_equipotent by A1,RFINSEQ:2;
  then -sort_d (-f)=sort_a f by A2,Th19;
 hence sort_d (-f)=- (sort_a f);
end;

theorem   for f being FinSequence of REAL holds
 sort_a (-f) = - (sort_d f)
proof let f be FinSequence of REAL;
  A1: f,sort_d(f) are_fiberwise_equipotent by Def5;
  A2: -sort_a (-f) is non-increasing by Th25;
    -f,sort_a(-f) are_fiberwise_equipotent by Def6;
  then --f,-sort_a(-f) are_fiberwise_equipotent by Th27;
  then sort_d(f),-sort_a (-f) are_fiberwise_equipotent by A1,RFINSEQ:2;
  then -sort_a (-f)=sort_d f by A2,RFINSEQ:36;
 hence sort_a (-f)=- (sort_d f);
end;

theorem Th30: for f being FinSequence of REAL holds
 dom (sort_d f)=dom f & len (sort_d f)=len f
proof let f be FinSequence of REAL;
    f,(sort_d f) are_fiberwise_equipotent by Def5;
 hence dom (sort_d f)=dom f & len (sort_d f)=len f by RFINSEQ:16;
end;

theorem Th31: for f being FinSequence of REAL holds
 dom (sort_a f)=dom f & len (sort_a f)=len f
proof let f be FinSequence of REAL;
    f,(sort_a f) are_fiberwise_equipotent by Def6;
 hence dom (sort_a f)=dom f & len (sort_a f)=len f by RFINSEQ:16;
end;

theorem   for f being FinSequence of REAL st
   len f >=1 holds
    max_p(sort_d f)=1 & min_p(sort_a f)=1
    & (sort_d f).1=max f & (sort_a f).1=min f
proof let f be FinSequence of REAL;
  assume A1: len f>=1;
   then A2: len f>0;
   A3: len (sort_d f)=len f by Th30;
   then 1 in Seg len (sort_d f) by A1,FINSEQ_1:3;
   then A4: 1 in dom (sort_d f) by FINSEQ_1:def 3;
   A5: for i being Nat,r1,r2 being Real st
   i in dom (sort_d f) & r1=(sort_d f).i & r2=(sort_d f).1
   holds r1<=r2
   proof let i be Nat,r1,r2 be Real;
    assume A6: i in dom (sort_d f) & r1=(sort_d f).i & r2=(sort_d f).1;
     then i in Seg len (sort_d f) by FINSEQ_1:def 3;
     then A7: 1<=i by FINSEQ_1:3;
       now per cases;
     case 1=i;
      hence r1<=r2 by A6;
     end;
     case 1<>i; then 1<i by A7,REAL_1:def 5;
      hence r1<=r2 by A4,A6,RFINSEQ:32;
     end;
     end;
    hence r1<=r2;
   end;
   A8: for j being Nat st j in dom (sort_d f)
        & (sort_d f).j=(sort_d f).1 holds 1<=j
    proof let j be Nat;
     assume j in dom (sort_d f) & (sort_d f).j=(sort_d f).1;
      then j in Seg len (sort_d f) by FINSEQ_1:def 3;
     hence 1<=j by FINSEQ_1:3;
    end;
  then A9: max_p(sort_d f)=1 by A2,A3,A4,A5,Def1;
  A10: f,(sort_d f) are_fiberwise_equipotent by Def5;
  A11: (sort_d f).1=max (sort_d f) by A9,Def3.=max f by A10,Th14;
   A12: len (sort_a f)=len f by Th31;
   then A13: 1 in dom (sort_a f) by A1,FINSEQ_3:27;
   A14: for i being Nat,r1,r2 being Real st
   i in dom (sort_a f) & r1=(sort_a f).i & r2=(sort_a f).1
   holds r1>=r2
   proof let i be Nat,r1,r2 be Real;
    assume A15: i in dom (sort_a f) & r1=(sort_a f).i & r2=(sort_a f).1;
     then A16: 1<=i by FINSEQ_3:27;
     per cases;
     suppose 1=i;
      hence r1>=r2 by A15;
     end;
     suppose 1<>i; then 1<i by A16,REAL_1:def 5;
      hence r1>=r2 by A13,A15,Th17;
   end;
   end;
A17: for j being Nat st j in dom (sort_a f)
        & (sort_a f).j=(sort_a f).1 holds 1<=j by FINSEQ_3:27;
  then A18: min_p(sort_a f)=1 by A2,A12,A13,A14,Def2;
  A19: f,(sort_a f) are_fiberwise_equipotent by Def6;
    (sort_a f).1=min (sort_a f) by A18,Def4.=min f by A19,Th15;
 hence thesis by A2,A3,A4,A5,A8,A11,A12,A13,A14,A17,Def1,Def2;
end;

