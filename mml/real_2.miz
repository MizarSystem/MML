:: Equalities and Inequalities in Real Numbers. Continuation of REAL_1
::  by Andrzej Kondracki
::
:: Received September 5, 1990
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies ARYTM, ARYTM_1, ARYTM_3, RELAT_1, BOOLE;
 notations XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, REAL_1, XCMPLX_0, XREAL_0;
 constructors REAL_1, XREAL_0, MEMBERED, XBOOLE_0;
 registrations XREAL_0, MEMBERED;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 theorems REAL_1, AXIOMS, TARSKI, XREAL_0, XCMPLX_1, XREAL_1;

begin

 reserve a,b,d,e for real number;

canceled 62;

Lm1: 1/1=1 & 1"=1 & 1/(-1)=-1 & (-1)"=-1 & (-1)*(-1)=1;

theorem
  for a,b ex e st a=b-e
  proof
   let a,b;
   take b-a;
   thus thesis;
  end;

theorem
  for a,b st b<>0 ex e st a=b/e by XREAL_1:227;

canceled 40;

theorem
 (a+-b<=0 or b-a>=0 or b+-a>=0
or a-e<=b+-e or a+-e<=b-e or e-a>=e-b) implies a<=b
 proof
  assume
  A1:a+-b<=0 or b-a>=0 or b+-a>=0 or a-e<=b+-e or a+-e<=b-e or e-a>=e-b;
     per cases by A1;
   suppose a+-b<=0; then a+-b+b<=0+b by AXIOMS:24;
    hence a<=b;
   end;
   suppose b-a>=0; hence b>=a by XREAL_1:51;
   end;
   suppose b+-a>=0; then b+-a+a>=0+a by AXIOMS:24;
    hence b>=a;
   end;
   suppose a-e<=b+-e; then a-e<=b-e;hence a<=b by REAL_1:54;
   end;
   suppose a+-e<=b-e; then a-e<=b-e;hence a<=b by REAL_1:54;
   end;
   suppose e-a>=e-b; hence a<=b by XREAL_1:12;
  end;
 end;

theorem
 (a+-b<0 or b-a>0 or -a+b>0 or a-e<b+-e or a+-e<b-e or e-a>e-b) implies a<b
 proof
  assume A1:a+-b<0 or b-a>0 or -a+b>0 or a-e<b+-e or a+-e<b-e or e-a>e-b;
     per cases by A1;
   suppose a+-b<0; then a+-b+b<0+b by REAL_1:53;
    hence a<b;
   end;
   suppose b-a>0; hence b>a by XREAL_1:49;
   end;
   suppose b+-a>0; then b+-a+a>0+a by REAL_1:53;
    hence b>a;
   end;
   suppose a-e<b+-e; then a-e<b-e;hence a<b by REAL_1:49;
   end;
   suppose a+-e<b-e; then a-e<b-e;hence a<b by REAL_1:49;
   end;
   suppose e-a>e-b; hence a<b by XREAL_1:12;
  end;
 end;

canceled 2;

theorem
 a<=-b implies a+b<=0 & -a>=b by XREAL_1:27,61;

theorem
 a<-b implies a+b<0 & -a>b by XREAL_1:28,63;

theorem
    -a<=b implies a+b>=0 by XREAL_1:62;

theorem
    -b<a implies a+b>0 by XREAL_1:64;

canceled 4;

theorem
    b>0 implies (a/b>1 implies a>b) & (a/b<1 implies a<b)
           & (a/b>-1 implies a>-b & b>-a) & (a/b<-1 implies a<-b & b<-a)
           by XREAL_1:183,187,195,196,203,204;

theorem
    b>0 implies (a/b>=1 implies a>=b) & (a/b<=1 implies a<=b)
           & (a/b>=-1 implies a>=-b & b>=-a) & (a/b<=-1 implies a<=-b & b<=-a)
           by XREAL_1:189,193,200,199,207,208;

theorem
    b<0 implies (a/b>1 implies a<b) & (a/b<1 implies a>b)
      & (a/b>-1 implies a<-b & b<-a) & (a/b<-1 implies a>-b & b>-a)
      by XREAL_1:184,188,197,198,205,206;

theorem
    b<0 implies (a/b>=1 implies a<=b) & (a/b<=1 implies a>=b)
           & (a/b>=-1 implies a<=-b & b<=-a) & (a/b<=-1 implies a>=-b & b>=-a)
           by XREAL_1:190,194,202,201,210,209;

theorem
    a>=0 & b>=0 or a<=0 & b<=0 implies a*b>=0 by XREAL_1:129,130;

theorem
    a<0 & b<0 or a>0 & b>0 implies a*b>0 by XREAL_1:131,132;

theorem
    a>=0 & b<=0 or a<=0 & b>=0 implies a*b<=0 by XREAL_1:133;

canceled;

theorem
 a<=0 & b<=0 or a>=0 & b>=0 implies a/b>=0 by XREAL_1:137,138;

theorem
 a>=0 & b<0 or a<=0 & b>0 implies a/b<=0 by XREAL_1:139,140;

theorem Th127:
 a>0 & b>0 or a<0 & b<0 implies a/b>0 by XREAL_1:141,142;

theorem Th128:
 a<0 & b>0 implies a/b<0 & b/a<0 by XREAL_1:143,144;

theorem
    a*b<=0 implies a>=0 & b<=0 or a<=0 & b>=0 by XREAL_1:131,132;

canceled 2;

theorem
    a*b<0 implies a>0 & b<0 or a<0 & b>0 by XREAL_1:135;

theorem
    b<>0 & a/b<=0 implies b>0 & a<=0 or b<0 & a>=0
    by XREAL_1:141,142;

theorem
    b<>0 & a/b>=0 implies b>0 & a>=0 or b<0 & a<=0
    by XREAL_1:143,144;

theorem
    b<>0 & a/b<0 implies b<0 & a>0 or b>0 & a<0 by XREAL_1:145;

theorem
    b<>0 & a/b>0 implies b>0 & a>0 or b<0 & a<0 by XREAL_1:146;

theorem
    a>1 & (b>1 or b>=1) or a<-1 & (b<-1 or b<=-1) implies a*b>1
              by XREAL_1:163,166;

theorem
    a>=1 & b>=1 or a<=-1 & b<=-1 implies a*b>=1 by XREAL_1:161,165;

theorem
     0<=a & a<1 & 0<=b & b<=1 or 0>=a & a>-1 & 0>=b & b>=-1 implies a*b<1
               by XREAL_1:164,168;

theorem
     0<=a & a<=1 & 0<=b & b<=1 or 0>=a & a>=-1 & 0>=b & b>=-1 implies a*b<=1
     by XREAL_1:162,167;

canceled;

theorem
     0<a & a<b or b<a & a<0 implies a/b<1 & b/a>1
        by XREAL_1:190,191,192,189;

theorem
     0<a & a<=b or b<=a & a<0 implies a/b<=1 & b/a>=1
        by XREAL_1:184,185,186,183;

theorem
     a>0 & b>1 or a<0 & b<1 implies a*b>a by XREAL_1:157,158;

theorem
     a>0 & b<1 or a<0 & b>1 implies a*b<a by XREAL_1:159,160;

theorem
    a>=0 & b>=1 or a<=0 & b<=1 implies a*b>=a by XREAL_1:153,154;

theorem
    a>=0 & b<=1 or a<=0 & b>=1 implies a*b<=a by XREAL_1:155,156;

canceled;

theorem Th149:
 a<0 implies 1/a<0 & a"<0
  proof
   assume a<0;
    then 1/a<0 by Th128;
   hence 1/a<0 & a"<0 by XCMPLX_1:217;
  end;

theorem
    (1/a<0 implies a<0) & (1/a>0 implies a>0)
  proof
   hereby assume 1/a<0; then 1/(1/a)<0 by Th149;
    hence a<0 by XCMPLX_1:56;
   end;
   hereby assume 1/a>0; then 1/(1/a)>0 by Th127;
    hence a>0 by XCMPLX_1:56;
   end;
  end;

theorem Th151:
 (0<a or b<0) & a<b implies 1/a>1/b
  proof
   assume A1:(0<a or 0>b) & a<b;
   per cases by A1;
    suppose A2:0<a; then b>0 by A1;
     then 1/b>0 by Th127; then a*(1/b)<b*(1/b) by A1,REAL_1:70;
     then A3:a*(1/b)<1 by A1,XCMPLX_1:88;
        1/a>0 by A2,Th127; then 1/a*(a*(1/b))<1*(1/a) by A3,REAL_1:70;
     hence 1/b<1/a by A2,XCMPLX_1:108;
    end;
    suppose A4:0>b; then a<0 by A1;
     then 1/a<0 by Th149; then b*(1/a)<a*(1/a) by A1,REAL_1:71;
     then A5:b*(1/a)<1 by A1,XCMPLX_1:88;
        1/b<0 by A4,Th149; then 1/b*(b*(1/a))>1*(1/b) by A5,REAL_1:71;
     hence thesis by A4,XCMPLX_1:108;
  end;
  end;

theorem Th152:
 (0<a or b<0) & a<=b implies 1/a>=1/b
  proof
   assume A1:(0<a or 0>b) & a<=b;
   per cases by A1;
    suppose A2:0<a; then b>0 by A1;
     then 1/b>=0 by Th127;
     then a*(1/b)<=b*(1/b) by A1,AXIOMS:25;
     then A3:a*(1/b)<=1 by A1,XCMPLX_1:88;
        1/a>=0 by A2,Th127;
     then 1/a*(a*(1/b))<=1*(1/a) by A3,AXIOMS:25;
     hence thesis by A2,XCMPLX_1:108;
    end;
    suppose A4:0>b; then a<0 by A1;
     then 1/a<=0 by Th149;
     then b*(1/a)<=a*(1/a) by A1,REAL_1:52;
     then A5:b*(1/a)<=1 by A1,XCMPLX_1:88;
        1/b<=0 by A4,Th149;
     then 1/b*(b*(1/a))>=1*(1/b) by A5,REAL_1:52;
     hence thesis by A4,XCMPLX_1:108;
  end;
  end;

theorem Th153:
 a<0 & b>0 implies 1/a<1/b
  proof
   assume a<0 & b>0;
   then 1/a<0 & 1/b>0 by Th127,Th149;
   hence thesis;
  end;

theorem
    (1/b>0 or 1/a<0) & 1/a>1/b implies a<b
  proof
   assume (1/b>0 or 1/a<0) & 1/a>1/b;
   then 1/(1/a)<1/(1/b) by Th151;
   then a<1/(1/b) by XCMPLX_1:56;hence thesis by XCMPLX_1:56;
  end;

theorem
    (1/b>0 or 1/a<0) & 1/a>=1/b implies a<=b
  proof
   assume (1/b>0 or 1/a<0) & 1/a>=1/b;
   then 1/(1/a)<=1/(1/b) by Th152;
   then a<=1/(1/b) by XCMPLX_1:56;hence thesis by XCMPLX_1:56;
  end;

theorem
     1/a<0 & 1/b>0 implies a<b
  proof
   assume 1/a<0 & 1/b>0;
   then 1/(1/a)<1/(1/b) by Th153;
   then a<1/(1/b) by XCMPLX_1:56;hence thesis by XCMPLX_1:56;
  end;

theorem
    a<-1 implies 1/a>-1 by Lm1,Th151;

theorem
    a<=-1 implies 1/a>=-1 by Lm1,Th152;

canceled 5;

theorem
    1<=a implies 1/a<=1 by Lm1,Th152;

theorem
    (b<=e-a implies a<=e-b) & (b>=e-a implies a>=e-b) by XREAL_1:13,14;

canceled;

theorem
 a+b<=e+d implies a-e<=d-b by XREAL_1:23;

theorem
 a+b<e+d implies a-e<d-b by XREAL_1:23;

theorem
    a-b<=e-d implies a+d<=e+b & a-e<=b-d & e-a>=d-b & b-a>=d-e
      by XREAL_1:18,19,20,23;

theorem
    a-b<e-d implies a+d<e+b & a-e<b-d & e-a>d-b & b-a>d-e
      by XREAL_1:18,19,20,23;

theorem
     (a+b<=e-d implies a+d<=e-b) & (a+b>=e-d implies a+d>=e-b)
             by XREAL_1:24,25;

canceled;

theorem
    (a<0 implies a+b<b & b-a>b) & (a+b<b or b-a>b implies a<0)
       by XREAL_1:32,33,45,48;

theorem
    (a<=0 implies a+b<=b & b-a>=b) & (a+b<=b or b-a>=b implies a<=0)
                   by XREAL_1:31,34,46,47;

canceled 2;

theorem
  (b>0 & a*b<=e implies a<=e/b) & (b<0 & a*b<=e implies a>=e/b) &
  (b>0 & a*b>=e implies a>=e/b) & (b<0 & a*b>=e implies a<=e/b)
     by XREAL_1:79,80,81,82;

theorem
  (b>0 & a*b<e implies a<e/b) & (b<0 & a*b<e implies a>e/b) &
  (b>0 & a*b>e implies a>e/b) & (b<0 & a*b>e implies a<e/b)
       by XREAL_1:83,84,85,86;

canceled 2;

theorem
    (for a st a>0 holds b+a>=e) or (for a st a<0 holds b-a>=e) implies b>=e
       by XREAL_1:43,58;

theorem
    (for a st a>0 holds b-a<=e) or (for a st a<0 holds b+a<=e) implies b<=e
     by XREAL_1:44,59;

theorem
 (for a st a>1 holds b*a>=e) or (for a st 0<a & a<1 holds b/a>=e) implies
   b>=e by XREAL_1:169,211;

theorem
    (for a st 0<a & a<1 holds b*a<=e)
or (for a st a>1 holds b/a<=e) implies b<=e by XREAL_1:170,212;

theorem
    (b>0 & d>0 or b<0 & d<0) & a*d<e*b implies a/b<e/d by XREAL_1:108,109;

theorem
    (b>0 & d<0 or b<0 & d>0) & a*d<e*b implies a/b>e/d by XREAL_1:110,111;

theorem
    (b>0 & d>0 or b<0 & d<0) & a*d<=e*b implies a/b<=e/d by XREAL_1:104,105;

theorem
    (b>0 & d<0 or b<0 & d>0) & a*d<=e*b implies a/b>=e/d by XREAL_1:106,107;

canceled 4;

theorem
    b<0 & d<0 or b>0 & d>0 implies
       (a*b<e/d implies a*d<e/b) & (a*b>e/d implies a*d>e/b)
       by XREAL_1:112,113,114,115;

theorem
    b<0 & d>0 or b>0 & d<0 implies
       (a*b<e/d implies a*d>e/b) & (a*b>e/d implies a*d<e/b)
       by XREAL_1:116,117,119,118;

canceled 2;

theorem
 (0<a or 0<=a) & (a<b or a<=b) & (0<e or 0<=e) & e<=d implies a*e<=b*d
                  by XREAL_1:68;

theorem
    0>=a & a>=b & 0>=e & e>=d implies a*e<=b*d by XREAL_1:69;

theorem
    0<a & a<=b & 0<e & e<d or 0>a & a>=b & 0>e & e>d implies a*e<b*d
        by XREAL_1:72,100;

theorem
     (e>0 & a>0 & a<b implies e/a>e/b) &
    (e>0 & b<0 & a<b implies e/a>e/b) by XREAL_1:78,101;

theorem
    e>=0 & (a>0 or b<0) & a<=b implies e/a>=e/b by XREAL_1:120,121;

theorem
    e<0 & (a>0 or b<0) & a<b implies e/a<e/b by XREAL_1:102,103;

theorem
    e<=0 & (a>0 or b<0) & a<=b implies e/a<=e/b by XREAL_1:122,123;

theorem
    for X,Y being Subset of REAL st
  X<>{} & Y<>{} & for a,b st a in X & b in Y holds a<=b
 holds ex d st (for a st a in X holds a<=d) & for b st b in Y holds d<=b
 proof
  let X,Y be Subset of REAL; assume
   A1: X<>{} & Y<>{};
  assume
A2: for a,b st a in X & b in Y holds a<=b;
  consider x being Element of X;
  reconsider a=x as Real by A1,TARSKI:def 3;
  A3:a in X by A1;
  consider y being Element of Y;
  reconsider b=y as Real by A1,TARSKI:def 3;
  A4:b in Y by A1;
  consider d being real number such that
     A5:for a,b being real number st a in X & b in Y holds a<=d & d<=b
      by A2,AXIOMS:26;
  reconsider d as Real by XREAL_0:def 1;
  take d;
  thus thesis by A3,A4,A5;
 end;

