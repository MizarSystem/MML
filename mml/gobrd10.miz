:: Adjacency Concept for Pairs of Natural Numbers
::  by Yatsuka Nakamura and Andrzej Trybulec
::
:: Received June 10, 1996
:: Copyright (c) 1996 Association of Mizar Users

environ

 vocabularies ARYTM_1, FINSEQ_2, FINSEQ_1, FUNCT_1, RELAT_1, MATRIX_1, GOBRD10,
      FINSEQ_4, ARYTM;
 notations TARSKI, ZFMISC_1, SUBSET_1, NUMBERS, XCMPLX_0, NAT_1, BINARITH,
      RELAT_1, FUNCT_1, PARTFUN1, FINSEQ_1, FINSEQ_2, MATRIX_1, XXREAL_0;
 constructors PARTFUN1, XXREAL_0, NAT_1, BINARITH, MATRIX_1, INT_1;
 registrations SUBSET_1, ORDINAL1, RELSET_1, XXREAL_0, XREAL_0, NAT_1,
      XBOOLE_0, FINSEQ_1;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, FINSEQ_2;
 theorems TARSKI, NAT_1, FINSEQ_1, REAL_1, FINSEQ_4, BINARITH, FUNCT_1,
      FINSEQ_2, FUNCOP_1, FINSEQ_3, ZFMISC_1, XREAL_1, XXREAL_0, PARTFUN1;
 schemes NAT_1, FINSEQ_1, FINSEQ_2;

begin

reserve i,j,k,k1,k2,n,m,i1,i2,j1,j2 for Element of NAT,
  x for set;

definition
  let i1,i2;
  pred i1,i2 are_adjacent1 means
  :Def1:
  i2=i1+1 or i1=i2+1;
  symmetry;
  irreflexivity;
end;

theorem Th1:
  for i1,i2 st i1,i2 are_adjacent1 holds i1+1,i2+1 are_adjacent1
proof
  let i1,i2;
  assume i1,i2 are_adjacent1;
  then i2=i1+1 or i1=i2+1 by Def1;
  hence i1+1,i2+1 are_adjacent1 by Def1;
end;

theorem Th2:
  for i1,i2 st i1,i2 are_adjacent1 & 1<=i1 & 1<=i2
  holds i1-'1,i2-'1 are_adjacent1
proof
  let i1,i2;
  assume
A1: i1,i2 are_adjacent1 & 1<=i1 & 1<=i2;
  then i2=i1+1 or i1=i2+1 by Def1;
  then
A2: i2-1=i1-1+1 or i1-1=i2-1+1;
  0<=i1-1 & 0<=i2-1 by A1,XREAL_1:50;
  then i1-'1=i1-1 & i2-'1=i2-1 by BINARITH:def 3;
  hence i1-'1,i2-'1 are_adjacent1 by A2,Def1;
end;

definition
  let i1,j1,i2,j2;
  pred i1,j1,i2,j2 are_adjacent2 means
  :Def2:
  i1,i2 are_adjacent1 & j1=j2 or i1=i2 & j1,j2 are_adjacent1;
end;

theorem Th3:
  for i1,i2,j1,j2 st i1,j1,i2,j2 are_adjacent2
  holds i1+1,j1+1,i2+1,j2+1 are_adjacent2
proof
  let i1,i2,j1,j2;
  assume i1,j1,i2,j2 are_adjacent2;
  then i1,i2 are_adjacent1 & j1=j2 or i1=i2 & j1,j2 are_adjacent1 by Def2;
  then i1+1,i2+1 are_adjacent1 & j1+1=j2+1
  or i1+1=i2+1 & j1+1,j2+1 are_adjacent1 by Th1;
  hence i1+1,j1+1,i2+1,j2+1 are_adjacent2 by Def2;
end;

theorem
  for i1,i2,j1,j2 st i1,j1,i2,j2 are_adjacent2 &
  1<=i1 & 1<=i2 & 1<=j1 & 1<=j2 holds i1-'1,j1-'1,i2-'1,j2-'1 are_adjacent2
proof
  let i1,i2,j1,j2;
  assume
A1: i1,j1,i2,j2 are_adjacent2 & 1<=i1 & 1<=i2 & 1<=j1 & 1<=j2;
  then i1,i2 are_adjacent1 & j1=j2 or i1=i2 & j1,j2 are_adjacent1 by Def2;
  then i1-'1,i2-'1 are_adjacent1 & j1-'1=j2-'1
  or i1-'1=i2-'1 & j1-'1,j2-'1 are_adjacent1 by A1,Th2;
  hence i1-'1,j1-'1,i2-'1,j2-'1 are_adjacent2 by Def2;
end;

definition
  let n, i;
  redefine func n |-> i -> FinSequence of NAT means
  :Def3:
  len it = n & for j st 1 <= j & j <= n holds it.j = i;
  coherence by FINSEQ_2:77;
  compatibility
  proof
    let a be FinSequence of NAT;
    hereby
      assume
A1:   a = n |-> i;
      hence len a = n by FINSEQ_2:69;
      thus for j st 1 <= j & j <= n holds a.j = i
      proof
        let j;
        assume 1 <= j & j <= n;
        then j in Seg n by FINSEQ_1:3;
        hence thesis by A1,FUNCOP_1:13;
      end;
    end;
    assume
A2: len a = n & for j st 1 <= j & j <= n holds a.j = i;
    then
A3: dom a = Seg n by FINSEQ_1:def 3;
    now
      let z be set;
      assume
A4:   z in dom a;
      then reconsider z' = z as Element of NAT;
      1 <= z' & z' <= n by A3,A4,FINSEQ_1:3;
      hence a.z = i by A2;
    end;
    hence a = n |-> i by A3,FUNCOP_1:17;
  end;
end;

canceled;

theorem Th6:
  for n,i,j st i<=n & j<=n
  holds ex fs1 being FinSequence of NAT st fs1.1=i & fs1.(len fs1)=j &
  len fs1=(i-'j)+(j-'i)+1 &
  (for k,k1 st 1<=k & k<=len fs1 & k1=fs1.k holds k1<=n) &
  for i1 st 1<=i1 & i1<len fs1 holds
  fs1.(i1+1)= (fs1/.i1)+1 or fs1.i1= (fs1/.(i1+1)) +1
proof
  let n,i,j;
  assume
A1: i<=n & j<=n;
  now per cases by REAL_1:def 5;
    case
A2:   i<j;
      then j-i>0 by XREAL_1:52;
      then
A3:   j-i+1>0+1 by XREAL_1:8;
      deffunc F(Nat) = i+$1-'1;
      ex p being FinSequence st len p = (j+1)-'i &
      for k be Nat st k in dom p holds p.k=F(k) from FINSEQ_1:sch 2;
      then consider p being FinSequence such that
A4:   len p = (j+1)-'i & for k be Nat st k in dom p holds p.k=i+k-'1;
A5:   for k,k1 st 1<=k & k<=len p & k1=p.k holds k1<=n
      proof
        let k,k1;
        assume
A6:     1<=k & k<=len p & k1=p.k;
        then k in dom p by FINSEQ_3:27;
        then
A7:     k1=k+i-'1 by A4,A6;
        k+i<=(j+1)-'i +i by A4,A6,XREAL_1:8;
        then
A8:     k+i<=j+1-i+i by A3,BINARITH:def 3;
        k+i-1=k+(i-1);
        then 1+(i-1)<=k+i-1 by A6,XREAL_1:8;
        then k1=k+i-1 by A7,BINARITH:def 3;
        then k1<=j+1-1 by A8,XREAL_1:11;
        hence k1<=n by A1,XXREAL_0:2;
      end;
A9:   j+1-i>=0 by A3;
A10:  (j+1)-'i=j+1-i by A3,BINARITH:def 3;
A11:  (j+1)-'i=j-i+1 by A9,BINARITH:def 3;
      1 in dom p by A3,A10,FINSEQ_3:27,A4;
      then p.1=i+1-'1 by A4;
      then
A12:  p.1=i by BINARITH:39;
A13:  j-i>=i-i by A2,XREAL_1:11;
A14:    i+(j+1-'i)-'1=i+(j+1-'i)-1 by A11,BINARITH:def 3;
      len p in dom p by FINSEQ_3:27,A3,A10,A4;
      then
A15:  p.(len p)=j by A4,A10,A14;
      i-j<j-j by A2,XREAL_1:11;
      then i-'j=0 by BINARITH:def 3;
      then
A16:  len p=(i-'j)+(j-'i)+1 by A4,A11,A13,BINARITH:def 3;
      rng p c=NAT
      proof
        let x;
        assume x in rng p;
        then consider y being set such that
A17:    y in dom p & p.y=x by FUNCT_1:def 5;
        y in Seg len p by A17,FINSEQ_1:def 3;
        then y in {k:1<=k & k<=len p} by FINSEQ_1:def 1;
        then consider k such that
A18:    k=y & (1<=k & k<=len p);
        p.k=i+k-'1 by A4,A18,A17;
        hence thesis by A17,A18;
      end;
      then reconsider fs2=p as FinSequence of NAT by FINSEQ_1:def 4;
      for i1 st 1<=i1 & i1<len fs2 holds
      fs2.(i1+1)= (fs2/.i1)+1 or fs2.i1= (fs2/.(i1+1)) +1
      proof
        let i1;
        assume
A19:    1<=i1 & i1<len fs2;
        then
A20:    i1+1<=len fs2 by NAT_1:13;
        1<=1+i1 by NAT_1:11;
        then i1+1 in dom p by A20,FINSEQ_3:27;
        then
A21:    fs2.(i1+1)=i+(i1+1)-'1 by A4;
A22:    i1 in dom fs2 by A19,FINSEQ_3:27;
A23:    fs2/.i1 = fs2.i1 by A19,FINSEQ_4:24;
        i+i1>=1+0 by A19,XREAL_1:9;
        then i+i1-1>=1-1 by XREAL_1:11;
        then
A24:    1+(i+i1-'1)=1+(i+i1-1) by BINARITH:def 3
          .=i+i1;
        1+(i+i1)>=1 by NAT_1:11;
        then 1+(i+i1)-1>=1-1 by XREAL_1:11;
        then (1+(i+i1))-'1=i+i1 by BINARITH:def 3;
        hence thesis by A4,A21,A22,A23,A24;
      end;
      hence thesis by A5,A12,A15,A16;
    end;
    case
A25:  i=j;
      consider f being Function such that
A26:  dom f=Seg 1 & rng f={i} by FINSEQ_1:4,FUNCT_1:15;
A27:  rng f c=NAT by A26,ZFMISC_1:37;
      f is FinSequence-like by A26,FINSEQ_1:def 2;
      then reconsider fs1=f as FinSequence of NAT by A27,FINSEQ_1:def 4;
      1 in dom f by A26,FINSEQ_1:3;
      then fs1.1 in rng f by FUNCT_1:def 5;
      then
A28:  fs1.1=i by A26,TARSKI:def 1;
A29:  len fs1=1 by A26,FINSEQ_1:def 3;
      i-j=0 by A25;
      then
A30:  i-'j=0 by BINARITH:def 3;
A31:  (for k,k1 st 1<=k & k<=len fs1 & k1=fs1.k holds k1<=n)
      by A1,A28,A29,XXREAL_0:1;
      for i1 st 1<=i1 & i1<len fs1 holds
      fs1.(i1+1)=(fs1/.i1)+1 or fs1.i1=(fs1/.(i1+1)) +1 by A26,FINSEQ_1:def 3;
      hence thesis by A25,A28,A29,A30,A31;
    end;
    case
A32:  j<i;
      then i-j>0 by XREAL_1:52;
      then
A33:  i-j+1>0+1 by XREAL_1:8;
      deffunc F(Nat) = i+1-'$1;
      ex p being FinSequence st len p = i+1-'j &
      for k be Nat st k in dom p holds p.k=F(k) from FINSEQ_1:sch 2;
      then consider p being FinSequence such that
A34:  len p = i+1-'j & for k be Nat st k in dom p holds p.k=i+1-'k;
A35:  for k,k1 st 1<=k & k<=len p & k1=p.k holds k1<=n
      proof
        let k,k1;
        assume
A36:    1<=k & k<=len p & k1=p.k;
        then k in dom p by FINSEQ_3:27;
        then
A37:    k1=i+1-'k by A34,A36;
        -k<=-1 by A36,XREAL_1:26;
        then
A38:    -k+(i+1)<= -1+(i+1) by XREAL_1:8;
        k<=i+1-j by A34,A36,BINARITH:def 3;
        then k+j<=i+1-j+j by XREAL_1:8;
        then k+j-k<=i+1-k by XREAL_1:11;
        then k1=i+1-k by A37,BINARITH:def 3;
        hence k1<=n by A1,A38,XXREAL_0:2;
      end;
A39:  i+1-'j=i+1-j by A33,BINARITH:def 3;
      then 1 in dom p by A33,FINSEQ_3:27,A34;
      then
A40:  p.1=i+1-'1 by A34;
      i+1-1>=0;
      then
A41:  p.1=i by A40,BINARITH:def 3;
A42:  i-j>=j-j by A32,XREAL_1:11;
      then i-j+1>=0+1 by XREAL_1:8;
      then
A43:  i+1-j>=0;
A44:  i+1-(i+1-'j)=i+1-(i+1-j) by A33,BINARITH:def 3
        .=j;
A45:  now per cases by A42;
        case i-j=0;
          hence j-'i=0 by BINARITH:def 3;
        end;
        case i-j>0;
          then --(i-j)>0;
          then j-i<0 by XREAL_1:60;
          hence j-'i=0 by BINARITH:def 3;
        end;
      end;
      len p in dom p by FINSEQ_3:27,A33,A34,A39;
      then
A46:  p.(len p)=(i+1-'(i+1-'j)) by A34
        .= j by A44,BINARITH:def 3;
      j-i<i-i by A32,XREAL_1:11;
      then j-'i=0 by BINARITH:def 3;
      then
A47:  (i-'j)+(j-'i)+1 =i-j+1+(j-'i) by A42,BINARITH:def 3
        .=i+1-j by A45;
      rng p c=NAT
      proof
        let x;
        assume x in rng p;
        then consider y being set such that
A48:    y in dom p & p.y=x by FUNCT_1:def 5;
        y in Seg len p by A48,FINSEQ_1:def 3;
        then y in {k:1<=k & k<=len p} by FINSEQ_1:def 1;
        then consider k such that
A49:    k=y & 1<=k & k<=len p;
        k in dom p by A49,FINSEQ_3:27;
        then p.k=i+1-'k by A34;
        hence thesis by A48,A49;
      end;
      then reconsider fs2=p as FinSequence of NAT by FINSEQ_1:def 4;
      for i1 st 1<=i1 & i1<len fs2 holds
      fs2.(i1+1)= (fs2/.i1)+1 or fs2.i1= (fs2/.(i1+1)) +1
      proof
        let i1;
        assume
A50:    1<=i1 & i1<len fs2;
        then i1 in dom fs2 by FINSEQ_3:27;
        then
A51:    fs2.(i1)=i+1-'i1 by A34;
A52:    1<=i1+1 & i1+1<=len fs2 by A50,NAT_1:13;
        then (i1+1) in dom fs2 by FINSEQ_3:27;
        then fs2.(i1+1)=i+1-'(i1+1) by A34;
        then
A53:    (fs2/.(i1+1))+1=1+(i+1-'(i1+1)) by A52,FINSEQ_4:24;
        i1+1<=i-j+1 by A34,A43,A52,BINARITH:def 3;
        then i1<=i-j by XREAL_1:8;
        then i1+j<=i by XREAL_1:21;
        then j<=i-i1 by XREAL_1:21;
        then 1+(i+1-'(i1+1))=1+(i+1-(i1+1)) by BINARITH:def 3
          .=i+1-i1;
        hence thesis by A51,A53,BINARITH:def 3;
      end;
      hence thesis by A34,A35,A39,A41,A46,A47;
    end;
  end;
  hence thesis;
end;

theorem Th7:
  for n,i,j st i<=n & j<=n
  holds ex fs1 being FinSequence of NAT st fs1.1=i & fs1.(len fs1)=j &
  len fs1=(i-'j)+(j-'i)+1 &
  (for k,k1 st 1<=k & k<=len fs1 & k1=fs1.k holds k1<=n) &
  for i1 st 1<=i1 & i1<len fs1 holds fs1/.i1,fs1/.(i1+1) are_adjacent1
proof
  let n,i,j;
  assume i<=n & j<=n;
  then consider fs1 being FinSequence of NAT such that
A1: fs1.1=i & fs1.(len fs1)=j & len fs1=(i-'j)+(j-'i)+1 &
  (for k,k1 st 1<=k & k<=len fs1 & k1=fs1.k holds k1<=n) &
  for i1 st 1<=i1 & i1<len fs1 holds
  fs1.(i1+1)= (fs1/.i1)+1 or fs1.i1= (fs1/.(i1+1)) +1 by Th6;
  for i1 st 1<=i1 & i1<len fs1 holds fs1/.i1,fs1/.(i1+1) are_adjacent1
  proof
    let i1;
    assume
A2: 1<=i1 & i1<len fs1;
    then 1<= i1+1 & i1+1<=len fs1 by NAT_1:13;
    then
A3: fs1.(i1+1)= fs1/.(i1+1) by FINSEQ_4:24;
A4: fs1.i1= fs1/.i1 by A2,FINSEQ_4:24;
    fs1.(i1+1)= (fs1/.i1)+1 or fs1.i1= (fs1/.(i1+1)) +1 by A1,A2;
    hence thesis by A3,A4,Def1;
  end;
  hence thesis by A1;
end;

theorem Th8:
  for n,m,i1,j1,i2,j2 st i1<=n & j1<=m & i2<=n & j2<=m
  ex fs1,fs2 being FinSequence of NAT st
  (for i,k1,k2 st i in dom fs1 & k1=fs1.i & k2=fs2.i
  holds k1 <= n & k2 <= m) & fs1.1=i1 & fs1.(len fs1)=i2 &
  fs2.1=j1 & fs2.(len fs2)=j2 & len fs1=len fs2 &
  len fs1=(i1-'i2)+(i2-'i1)+(j1-'j2)+(j2-'j1)+1 &
  for i st 1<=i & i<len fs1 holds
  fs1/.i,fs2/.i,fs1/.(i+1),fs2/.(i+1) are_adjacent2
proof
  let n,m,i1,j1,i2,j2;
  assume
A1: i1<=n & j1<=m & i2<=n & j2<=m;
  then consider gs1 being FinSequence of NAT such that
A2: gs1.1=i1 & gs1.(len gs1)=i2 & len gs1=(i1-'i2)+(i2-'i1)+1 &
  (for k,k1 st 1<=k & k<=len gs1 & k1=gs1.k holds k1<=n) &
  for k st 1<=k & k<len gs1 holds gs1/.k,gs1/.(k+1) are_adjacent1 by Th7;
  consider gs2 being FinSequence of NAT such that
A3: gs2.1=j1 & gs2.(len gs2)=j2 & len gs2=(j1-'j2)+(j2-'j1)+1 &
  (for k,k1 st 1<=k & k<=len gs2 & k1=gs2.k holds k1<=m) &
  for k st 1<=k & k<len gs2 holds gs2/.k,gs2/.(k+1) are_adjacent1 by A1,Th7;
  set hs1=gs1^((len gs2-'1) |-> i2), hs2=((len gs1-'1) |-> j1)^gs2;
A4: 1<=1 & 1<=len gs2 by A3,NAT_1:11;
A5: 1<=1 & 1<=len gs1 by A2,NAT_1:11;
  then
A6: 1 in dom gs1 by FINSEQ_3:27;
A7: len gs2 -1>=1-1 by A4,XREAL_1:11;
A8: len gs1 -1>=1-1 by A5,XREAL_1:11;
A9: len hs1=len gs1 + len ((len gs2-'1) |-> i2) by FINSEQ_1:35
    .=len gs1 + (len gs2-'1) by Def3
    .=len gs1 + (len gs2-1) by A7,BINARITH:def 3
    .=(len gs1-1)+ len gs2;
A10: (len gs1-1) + len gs2 =(len gs1-'1)+ len gs2 by A8,BINARITH:def 3
    .=len ((len gs1-'1) |-> j1) + len gs2 by Def3
    .=len hs2 by FINSEQ_1:35;
A11: now per cases;
    case len gs1-'1=0;
      then ((len gs1-'1) |-> j1)^gs2=<*>(NAT)^gs2 by FINSEQ_2:72
        .=gs2 by FINSEQ_1:47;
      hence hs2.1=j1 & hs2.len hs2=j2 by A3;
    end;
    case
A12:  len gs1-'1<>0;
A13:  len ((len gs1-'1) |-> j1)=len gs1-'1 by Def3;
      len gs1-'1>0 by A12;
      then
A14:  0+1<= len ((len gs1-'1) |-> j1) by A13,NAT_1:13;
      then 1 in dom ((len gs1-'1) |-> j1) by FINSEQ_3:27;
      then
A15:  hs2.1 = ((len gs1-'1) |-> j1).1 by FINSEQ_1:def 7;
A16:  len ((len gs1-'1) |-> j1) = len gs1-'1 by Def3;
A17:  len hs2=len ((len gs1-'1) |-> j1) + len gs2 by FINSEQ_1:35;
      len gs2<=len gs2 & 1<=len gs2 by A3,NAT_1:11;
      then len gs2 in dom gs2 by FINSEQ_3:27;
      hence hs2.1=j1 & hs2.len hs2=j2 by A3,A14,A15,A16,A17,Def3,
      FINSEQ_1:def 7;
    end;
  end;
A18: now per cases;
    case len gs2-'1=0;
      then gs1^((len gs2-'1) |-> i2) = gs1^<*>(NAT) by FINSEQ_2:72
        .=gs1 by FINSEQ_1:47;
      hence hs1.1=i1 & hs1.len hs1=i2 by A2;
    end;
    case len gs2-'1<>0;
      then
A19:  len gs2-'1>0;
A20:  len hs1=len gs1+len ((len gs2-'1) |-> i2) by FINSEQ_1:35;
A21:  len ((len gs2-'1) |-> i2) = len gs2-'1 by Def3;
A22:  0+1<=len gs2-'1 by A19,NAT_1:13;
      then
A23:  len ((len gs2-'1) |-> i2) in dom ((len gs2-'1) |-> i2)
      by A21,FINSEQ_3:27;
      ((len gs2-'1) |-> i2).len ((len gs2-'1) |-> i2) = i2 by A21,A22,Def3;
      hence hs1.1=i1 & hs1.len hs1=i2 by A2,A6,A20,A23,FINSEQ_1:def 7;
    end;
  end;
A24: for i,k1,k2 st i in dom hs1 & k1=hs1.i & k2=hs2.i holds k1<=n & k2<=m
  proof
    let i,k1,k2;
    assume
A25: i in dom hs1 & k1=hs1.i & k2=hs2.i;
    then
A26: i in Seg len hs1 by FINSEQ_1:def 3;
    dom hs1=Seg(len gs1 + len ((len gs2-'1) |-> i2)) by FINSEQ_1:def 7;
    then
A27: len hs1=len gs1 + len ((len gs2-'1) |-> i2) by FINSEQ_1:def 3;
    then
A28: len hs1=len gs1 + (len gs2 -'1) by Def3;
A29: len hs1 -' len gs1 = len hs1 - len gs1 by A27,BINARITH:def 3;
A30: 1<=i & i<=len gs1 or
    len gs1 -len gs1<i-len gs1 & i <=len hs1 by A25,FINSEQ_3:27 ,XREAL_1:11;
A31: now per cases by A29,A30,XREAL_1:11;
      case
A32:    1<=i & i<=len gs1;
        then i in dom gs1 by FINSEQ_3:27;
        then hs1.i=gs1.i by FINSEQ_1:def 7;
        hence k1<=n by A2,A25,A32;
      end;
      case
A33:    0<i-len gs1 & i-len gs1 <=len hs1 -' len gs1;
        then
A34:    i-len gs1=i-'len gs1 by BINARITH:def 3;
        then 0+1<=i-'len gs1 by A33,NAT_1:13;
        then (i-'len gs1) in Seg (len hs1-'len gs1) by A33,A34,FINSEQ_1:3;
        then
A35:    (i-'len gs1)in Seg (len gs2-'1) by A28,BINARITH:39;
        then (i-'len gs1)in Seg len ((len gs2-'1) |-> i2) by Def3;
        then
A36:    (i-'len gs1) in dom ((len gs2-'1) |-> i2) by FINSEQ_1:def 3;
A37:    1<=i-'len gs1 & i-'len gs1<=len gs2 -'1 by A35,FINSEQ_1:3;
A38:    i-'len gs1=i- len gs1 by A33,BINARITH:def 3;
        i=len gs1+(i-len gs1);
        then hs1.i= ((len gs2-'1) |-> i2).(i-'len gs1) by A36,A38,
        FINSEQ_1:def 7
          .=i2 by A37,Def3;
        hence k1<=n by A1,A25;
      end;
    end;
    dom hs2=Seg(len ((len gs1-'1) |-> j1)+ len gs2) by FINSEQ_1:def 7;
    then len hs2=len ((len gs1-'1) |-> j1) + len gs2 by FINSEQ_1:def 3;
    then
A39: len hs2=(len gs1 -'1)+len gs2 by Def3;
    1<=i & i<=len gs1-'1 or (len gs1 -'1)+1<=i & i<=len hs2
    by A9,A10,A26,FINSEQ_1:3,NAT_1:13;
    then 1<=i & i<=len gs1-'1 or len gs1-1+1<=i & i<=len hs2
    by A8,BINARITH:def 3;
    then 1<=i & i<=len gs1-1 or
    len gs1 -(len gs1-1)<=i-(len gs1-1) & i <=len hs2 by A8,
    BINARITH:def 3,XREAL_1:11;
    then 1<=i & i<=len gs1-1 or
    1<=i-(len gs1-1) & i-(len gs1-1) <=len hs2-(len gs1-1) by XREAL_1:11;
    then
A40: 1<=i & i<=len gs1-1 or 1<=i-len gs1+1 & i-len gs1+1 <=len hs2-len gs1+1;
    now per cases by A40,XREAL_1:8;
      case
A41:    1<=i-len gs1+1 & i-len gs1 <=len hs2 - len gs1;
        then
A42:    i-len gs1+1<=len hs2 - len gs1+1 by XREAL_1:8;
A43:    i+1-len gs1<=len hs2 - len gs1+1 by A41,XREAL_1:8;
A44:    len hs2+1 - len gs1=(len gs1 -1)+len gs2+1 - len gs1
        by A8,A39,BINARITH:def 3
          .=len gs2;
A45:    i+1-len gs1=i+1-'len gs1 by A41,BINARITH:def 3;
A46:    len ((len gs1-'1) |-> j1)=len gs1-'1 by Def3;
        i+1-'len gs1 in Seg len gs2 by A41,A42,A44,A45,FINSEQ_1:3;
        then
A47:    i+1-'len gs1 in dom gs2 by FINSEQ_1:def 3;
        i=len gs1-1+(i+1-len gs1)
          .=(len ((len gs1-'1) |-> j1)+(i+1-'len gs1))
        by A8,A45,A46,BINARITH:def 3;
        then hs2.i= gs2.(i+1-'len gs1) by A47,FINSEQ_1:def 7;
        hence k2<=m by A3,A25,A41,A43,A44,A45;
      end;
      case 1<=i & i<=len gs1-1;
        then
A48:    1<=i & i<=len gs1-'1 by BINARITH:def 3;
        then i in Seg (len gs1 -'1) by FINSEQ_1:3;
        then i in Seg len ((len gs1-'1) |-> j1) by Def3;
        then i in dom ((len gs1-'1) |-> j1) by FINSEQ_1:def 3;
        then hs2.i= ((len gs1-'1) |-> j1).i by FINSEQ_1:def 7
          .=j1 by A48,Def3;
        hence k2<=m by A1,A25;
      end;
    end;
    hence k1<=n & k2<=m by A31;
  end;
  for i st 1<=i & i<len hs1 holds
  hs1/.i,hs2/.i,hs1/.(i+1),hs2/.(i+1) are_adjacent2
  proof
    let i;
    assume
A49: 1<=i & i<len hs1;
    now per cases;
      case
A50:    i<len gs1;
        then
A51:    i+1<=len gs1 by NAT_1:13;
A52:    1<=i+1 by A49,NAT_1:13;
        i+1-1<=len gs1 -1 by A51,XREAL_1:11;
        then
A53:    1<=i & i<=len gs1-'1 by A49,BINARITH:def 3;
        then
A54:    i in Seg (len gs1 -'1) by FINSEQ_1:3;
        len ((len gs1-'1) |-> j1)=len gs1-'1 by Def3;
        then
A55:    i in dom ((len gs1-'1) |-> j1) by A54,FINSEQ_1:def 3;
A56:    dom ((len gs1-'1) |-> j1) c= dom hs2 by FINSEQ_1:39;
A57:    hs2.i= ((len gs1-'1) |-> j1).i by A55,FINSEQ_1:def 7
          .=j1 by A53,Def3;
A58:    now per cases;
          case
A59:        i+1<=len gs1-'1;
            then
A60:        i+1 in Seg (len gs1 -'1) by A52,FINSEQ_1:3;
            len ((len gs1-'1) |-> j1)=len gs1-'1 by Def3;
            then
A61:        i+1 in dom ((len gs1-'1) |-> j1) by A60,FINSEQ_1: def 3;
            then
A62:        hs2.(i+1)= ((len gs1-'1) |-> j1).(i+1) by FINSEQ_1 :def 7
              .=j1 by A52,A59,Def3;
            dom ((len gs1-'1) |-> j1) c= dom hs2 by FINSEQ_1:39;
            hence hs2/.(i+1)=j1 by A61,A62,PARTFUN1:def 8;
          end;
          case i+1>len gs1-'1;
            then
A63:        len gs1-'1+1 <=i+1 by NAT_1:13;
A64:        len gs1-'1+1=len gs1-1+1 by A2,BINARITH:def 3
              .=len gs1;
            then
A65:        len gs1=i+1 by A51,A63,XXREAL_0:1;
            1 in Seg len gs2 by A4,FINSEQ_1:3;
            then
A66:        1 in dom gs2 by FINSEQ_1:def 3;
            len ((len gs1-'1) |-> j1)+1=i+1 by A64,A65,Def3;
            then
A67:        hs2.(i+1)=j1 by A3,A66,FINSEQ_1:def 7;
            dom hs2=Seg(len ((len gs1-'1) |-> j1)+len gs2) by FINSEQ_1:def 7;
            then len hs2=len ((len gs1-'1) |-> j1)+len gs2 by FINSEQ_1:def 3
              .=(len gs1-'1)+len gs2 by Def3
              .=len gs1-1+len gs2 by A2,BINARITH:def 3
              .=len gs1+(len gs2-1)
              .=len gs1+(len gs2-'1) by A3,BINARITH:def 3;
            then len gs1 <= len hs2 by NAT_1:11;
            then len gs1 in Seg len hs2 by A5,FINSEQ_1:3;
            then (i+1) in dom hs2 by A65,FINSEQ_1:def 3;
            hence hs2/.(i+1)=j1 by A67,PARTFUN1:def 8;
          end;
        end;
        now per cases;
          case i+1<=len gs1-'1;
            then i+1<=len gs1 -1 by BINARITH:def 3;
            then i+1+1<=len gs1-1+1 by XREAL_1:8;
            then
A68:        i+1<len gs1 by NAT_1:13;
A69:        1<i+1 by A49,NAT_1:13;
A70:        i<len gs1 by A68,NAT_1:13;
A71:        dom gs1 c= dom hs1 by FINSEQ_1:39;
A72:        i in dom gs1 by A49,A70,FINSEQ_3:27;
A73:        (i+1) in dom gs1 by A68,A69,FINSEQ_3:27;
A74:        hs1/.i=hs1.i by A71,A72,PARTFUN1:def 8
              .=gs1.i by A72,FINSEQ_1:def 7
              .=gs1/.i by A72,PARTFUN1:def 8;
            hs1/.(i+1)=hs1.(i+1) by A71,A73,PARTFUN1:def 8
              .=gs1.(i+1) by A73,FINSEQ_1:def 7
              .=gs1/.(i+1) by A73,PARTFUN1:def 8;
            hence hs1/.i,hs1/.(i+1) are_adjacent1 by A2,A49,A70,A74;
          end;
          case i+1>len gs1-'1;
A75:        0+1<=i+1 by NAT_1:13;
A76:        dom gs1 c= dom hs1 by FINSEQ_1:39;
A77:        i in dom gs1 by A49,A50,FINSEQ_3:27;
A78:        (i+1) in dom gs1 by A51,A75,FINSEQ_3:27;
A79:        hs1/.i=hs1.i by A76,A77,PARTFUN1:def 8
              .=gs1.i by A77,FINSEQ_1:def 7
              .=gs1/.i by A77,PARTFUN1:def 8;
            hs1/.(i+1)=hs1.(i+1) by A76,A78,PARTFUN1:def 8
              .=gs1.(i+1) by A78,FINSEQ_1:def 7
              .=gs1/.(i+1)by A78,PARTFUN1:def 8;
            hence hs1/.i,hs1/.(i+1) are_adjacent1 by A2,A49,A50,A79;
          end;
        end;
        hence (hs1/.i,hs1/.(i+1) are_adjacent1)&(hs2/.i=hs2/.(i+1)) or
        (hs1/.i=hs1/.(i+1))& (hs2/.i,hs2/.(i+1) are_adjacent1)
        by A55,A56,A57,A58,PARTFUN1:def 8;
      end;
      case
A80:    i>=len gs1;
        then
A81:    i-len gs1>=0 by XREAL_1:50;
        then
A82:    i-len gs1=i-'len gs1 by BINARITH:def 3;
A83:    i-len gs1+1>=0+1 by A81,XREAL_1:8;
A84:    i in dom hs1 by A49,FINSEQ_3:27;
        len hs1=len gs1+len ((len gs2-'1) |-> i2) by FINSEQ_1:35
          .=len gs1 + (len gs2-'1) by Def3;
        then i-len gs1 < len gs1 + (len gs2 -'1) - len gs1 by A49,XREAL_1:11;
        then
A85:    (i-'len gs1)<=len gs2 -'1 by BINARITH:def 3;
A86:    now
          assume not 1<=(i-'len gs1);
          then i-'len gs1<0+1;
          then i-'len gs1<=0 by NAT_1:13;
          then
A87:      i-'len gs1=0;
          len gs1 in Seg len gs1 by A2,FINSEQ_1:5;
          then i in dom gs1 by A82,A87,FINSEQ_1:def 3;
          hence hs1.i=i2 by A2,A82,A87,FINSEQ_1:def 7;
        end;
A88:    len ((len gs2-'1) |-> i2)=len gs2-'1 by Def3;
        now
          assume
A89:      1<=(i-'len gs1);
          then (i-'len gs1) in Seg len ((len gs2-'1) |-> i2)
          by A85,A88,FINSEQ_1:3;
          then
A90:      (i-'len gs1) in dom ((len gs2-'1) |-> i2) by FINSEQ_1: def 3;
          i =len gs1+(i-len gs1)
            .=len gs1+(i-'len gs1) by A81,BINARITH:def 3;
          then hs1.i= ((len gs2-'1) |-> i2).(i-'len gs1)by A90,FINSEQ_1:def 7
            .=i2 by A85,A89,Def3;
          hence hs1.i=i2;
        end;
        then
A91:    hs1/.i=i2 by A84,A86,PARTFUN1:def 8;
A92:    i+1<=len hs1 by A49,NAT_1:13;
        0+1<=i+1 by NAT_1:13;
        then
A93:    i+1 in Seg len hs1 by A92,FINSEQ_1:3;
        then
A94:    i+1 in dom hs1 by FINSEQ_1:def 3;
A95:    0<=i+1-len gs1 by A80,NAT_1:12,XREAL_1:50;
A96:    len gs2-'1<=len gs2-'1+1 by NAT_1:11;
        i+1-len gs1+len gs1<=len gs2 -1+len gs1 by A9,A49,NAT_1:13;
        then i+1-len gs1<=len gs2 -1 by XREAL_1:8;
        then i+1-len gs1<=len gs2 -'1 by BINARITH:def 3;
        then
A97:    1<=(i+1-'len gs1) & (i+1-'len gs1)<=len gs2-'1 by A83, BINARITH:def 3;
        then 1<=(i+1-'len gs1) & (i+1-'len gs1)<=(len gs2-'1)+1
        by A96,XXREAL_0:2;
        then
A98:    1<=(i+1-'len gs1) & (i+1-'len gs1)<=(len gs2-1)+1
        by A7,BINARITH:def 3;
        len ((len gs2-'1) |-> i2)=len gs2-'1 by Def3;
        then (i+1-'len gs1) in Seg len ((len gs2-'1) |-> i2)
        by A97,FINSEQ_1:3;
        then
A99:    (i+1-'len gs1) in dom ((len gs2-'1) |-> i2) by FINSEQ_1: def 3;
        i+1 =len gs1+(i+1-len gs1)
          .=len gs1+(i+1-'len gs1) by A95,BINARITH:def 3;
        then
A100:   hs1.(i+1)=
        ((len gs2-'1) |-> i2).((i+1)-'len gs1) by A99,FINSEQ_1:def 7
          .=i2 by A97,Def3;
        0+1<=i-len gs1 +1 by A81,XREAL_1:8;
        then
A101:   1<=i+1-'len gs1 by A95,BINARITH:def 3;
A102:   1<=i+1-'len gs1 +1 by NAT_1:11;
A103:   i+1-'len gs1 in dom gs2 by A98,FINSEQ_3:27;
A104:   i in dom hs2 by A9,A10,A49,FINSEQ_3:27;
A105:   i+1 in dom hs2 by A9,A10,A93,FINSEQ_1:def 3;
        i-(len gs1-1)<len gs1-1+len gs2 -(len gs1-1) by A9,A49,XREAL_1:11;
        then
A106:   i+1-'len gs1<len gs2 by A95,BINARITH:def 3;
        then len gs2>=i+1-'len gs1+1 by NAT_1:13;
        then i+1-'len gs1+1 in Seg len gs2 by A102,FINSEQ_1:3;
        then
A107:   i+1-'len gs1+1 in dom gs2 by FINSEQ_1:def 3;
        len ((len gs1-'1) |-> j1)+(i+1-'len gs1+1)
        =(len gs1-'1)+(i+1-'len gs1+1) by Def3
          .=len gs1 -1+(i+1-'len gs1+1) by A8,BINARITH:def 3
          .=len gs1 -1+(i+1-len gs1+1) by A95,BINARITH:def 3
          .=i+1;
        then
A108:   hs2 /.(i+1)=hs2.(len ((len gs1-'1) |-> j1)+(i+1-'len gs1+1)) by A105,
        PARTFUN1:def 8
          .=gs2.((i+1-'len gs1+1)) by A107,FINSEQ_1:def 7
          .=gs2/.(i+1-'len gs1+1) by A107,PARTFUN1:def 8;
        len ((len gs1-'1) |-> j1)+(i+1-'len gs1)
        =(len gs1-'1)+(i+1-'len gs1) by Def3
          .=len gs1 -1+(i+1-'len gs1) by A8,BINARITH:def 3
          .=len gs1 -1+(i-len gs1+1) by A95,BINARITH:def 3
          .=i;
        then hs2/.i=hs2.(len ((len gs1-'1) |-> j1)+(i+1-'len gs1)) by A104,
        PARTFUN1:def 8
          .=gs2.(i+1-'len gs1) by A103,FINSEQ_1:def 7
          .=gs2/.(i+1-'len gs1) by A103,PARTFUN1:def 8;
        hence (hs1/.i,hs1/.(i+1) are_adjacent1)&(hs2/.i=hs2/.(i+1)) or
        (hs1/.i=hs1/.(i+1))& (hs2/.i,

hs2/.(i+1) are_adjacent1) by A3,A91,A94,A100,A101,A106,A108,PARTFUN1:def 8;
      end;
    end;
    hence thesis by Def2;
  end;
  hence thesis by A2,A3,A9,A10,A11,A18,A24;
end;

reserve S for set;

theorem
  for Y being Subset of S,F being Matrix of n,m, bool S st
  (ex i,j st i in Seg n & j in Seg m & F*(i,j) c= Y) &
  (for i1,j1,i2,j2 st i1 in Seg n & i2 in Seg n & j1 in Seg m & j2 in Seg m
  & i1,j1,i2,j2 are_adjacent2 holds F*(i1,j1)c=Y iff F*(i2,j2)c=Y)
  holds (for i,j st i in Seg n & j in Seg m holds F*(i,j)c=Y)
proof
  let Y be Subset of S,F be Matrix of n,m, (bool S);
  assume
A1: (ex i,j st i in Seg n & j in Seg m & F*(i,j) c= Y)&
  (for i1,j1,i2,j2 st i1 in Seg n & i2 in Seg n & j1 in Seg m & j2 in Seg m
  & i1,j1,i2,j2 are_adjacent2 holds F*(i1,j1)c=Y iff F*(i2,j2)c=Y);
  then consider i1,j1 such that
A2: i1 in Seg n & j1 in Seg m & F*(i1,j1) c= Y;
A3: 1<=i1 & i1<=n & 1<=j1 & j1<=m by A2,FINSEQ_1:3;
  then i1-1>=1-1 & j1-1>=1-1 by XREAL_1:11;
  then
A4: i1-'1=i1-1 & j1-'1=j1-1 by BINARITH:def 3;
  thus for i,j st i in Seg n & j in Seg m holds F*(i,j)c=Y
  proof
    let i2,j2;
    assume i2 in Seg n & j2 in Seg m;
    then
A5: 1<=i2 & i2<=n & 1<=j2 & j2<=m by FINSEQ_1:3;
    then i2-1>=1-1 & j2-1>=1-1 by XREAL_1:11;
    then
A6: i2-'1=i2-1 & j2-'1=j2-1 by BINARITH:def 3;
    now per cases;
      case n=0 or m=0;
        hence contradiction by A2,FINSEQ_1:4;
      end;
      case n<>0 & m<>0;
        then n>0 & m>0;
        then n>=0+1 & m>=0+1 by NAT_1:13;
        then n-1>=0 & m-1>=0 by XREAL_1:21;
        then
A7:     n-'1=n-1 & m-'1=m-1 by BINARITH:def 3;
        then
A8:     i1-'1<=n-'1 & j1-'1<=m-'1 by A3,A4,XREAL_1:11;
        i2-'1<=n-'1 & j2-'1<=m-'1 by A5,A6,A7,XREAL_1:11;
        then consider fs1,fs2 being FinSequence of NAT such that
A9:     (for i,k1,k2 st i in dom fs1 & k1=fs1.i & k2=fs2.i
        holds k1 <= n-'1 & k2 <= m-'1)& fs1.1=i1-'1 & fs1.(len fs1)=i2-'1 &
        fs2.1=j1-'1 & fs2.(len fs2)=j2-'1 & len fs1=len fs2 &
        len fs1=((i1-'1)-'(i2-'1))+((i2-'1)-'(i1-'1))
        +((j1-'1)-'(j2-'1))+((j2-'1)-'(j1-'1))+1 &
        for i st 1<=i & i<len fs1 holds
        fs1/.i,fs2/.i,fs1/.(i+1),fs2/.(i+1) are_adjacent2 by A8,Th8;
A10:    1<=len fs1 by A9,NAT_1:11;
        deffunc F(Nat) = (fs1/.$1)+1;
        ex p being FinSequence of NAT st len p = len fs1 &
        for j be Nat st j in dom p holds p.j = F(j) from FINSEQ_2:sch 1;
        then consider p1 being FinSequence of NAT such that
A11:    len p1 = len fs1
        & for k be Nat st k in dom p1 holds p1.k=(fs1/.k)+1;
A12: dom p1 = Seg len fs1 by A11,FINSEQ_1:def 3;
        deffunc F(Nat) = (fs2/.$1)+1;
        ex p being FinSequence of NAT st len p = len fs2
        & for k be Nat st k in dom p holds p.k=F(k) from FINSEQ_2:sch 1;
        then consider p2 being FinSequence of NAT such that
A13:    len p2 = len fs2
        & for k be Nat st k in dom p2 holds p2.k=(fs2/.k)+1;
A14: dom p2 = Seg len fs2 by A13,FINSEQ_1:def 3;
A15:    1 in Seg len fs1 by A10,FINSEQ_1:3;
        then
A16:    1 in dom fs1 by FINSEQ_1:def 3;
        1-1<=len fs1-1 by A10,XREAL_1:11;
        then
A17:    len fs1 -'1=len fs1 -1 by BINARITH:def 3;
A18:    1 in dom p1 by A11,A15,FINSEQ_1:def 3;
A19:    fs1/.1=i1-'1 by A9,A16,PARTFUN1:def 8;
A20:    p1/.1=p1.1 by A18,PARTFUN1:def 8
          .=i1-'1 +1 by A11,A15,A19,A12
          .=
        i1 by A4;
A21:    1 in dom fs2 by A9,A15,FINSEQ_1:def 3;
A22:    1 in dom p2 by A9,A13,A15,FINSEQ_1:def 3;
        defpred P[Element of NAT] means
        $1+1<=len p1 implies F*(p1/.($1+1),p2/.($1+1)) c= Y;
A23:    fs2/.1=j1-'1 by A9,A21,PARTFUN1:def 8;
        p2/.1=p2.1 by A22,PARTFUN1:def 8
          .=j1-'1 +1 by A9,A13,A15,A23,A14
          .=j1
        by A4;
        then
A24:    P[0] by A2,A20;
A25:    for k st P[k] holds P[k+1]
        proof
          let k;
          assume
A26:      k+1<=len p1 implies F*(p1/.(k+1),p2/.(k+1)) c= Y;
A27:      1<=k+1 by NAT_1:12;
          now per cases;
            case
A28:          k+1<=len p1;
              now per cases;
                case
A29:              k+1+1<=len p1;
                  then
A30:              k+1<len p1 by NAT_1:13;
                  then
A31:              k+1 in Seg len p1 by A27,FINSEQ_1:3;
                  then
A32:              k+1 in dom p1 by FINSEQ_1:def 3;
A33:              k+1 in dom fs1 by A11,A31,FINSEQ_1:def 3;
                  set lp1=fs1/.(k+1),lp2=fs2/.(k+1);
A34:              k+1 in dom p2 by A9,A11,A13,A31,FINSEQ_1:def 3;
A35:              k+1 in dom fs2 by A9,A11,A31,FINSEQ_1:def 3;
A36:              p1/.(k+1)=p1.(k+1) by A32,PARTFUN1:def 8
                    .=lp1+1 by A11,A31,A12;
A37:              lp1=fs1.(k+1) & lp2=fs2.(k+1) by A33,A35,PARTFUN1:def 8;
                  then lp1<=n-1 by A7,A9,A33;
                  then
A38:              lp1+1<=n-1+1 by XREAL_1:8;
                  1<=1+lp1 by NAT_1:11;
                  then
A39:              p1/.(k+1) in Seg n by A36,A38,FINSEQ_1 :3;
A40:              p2/.(k+1)=p2.(k+1) by A34,PARTFUN1:def 8
                    .=lp2+1 by A9,A11,A13,A31,A14;
                  lp2<=m-'1 by A9,A33,A37;
                  then
A41:              lp2+1<=m-1+1 by A7,XREAL_1:8;
                  1<=1+lp2 by NAT_1:11;
                  then
A42:              p2/.(k+1) in Seg m by A40,A41,FINSEQ_1 :3;
                  1<=k+1+1 by NAT_1:12;
                  then
A43:              k+1+1 in Seg len p1 by A29,FINSEQ_1:3;
                  then
A44:              k+1+1 in dom p1 by FINSEQ_1:def 3;
                  set lp11=fs1/.(k+1+1),lp21=fs2/.(k+1+1);
A45:              k+1+1 in dom fs1 by A11,A43,FINSEQ_1:def 3;
A46:              k+1+1 in dom p2 by A9,A11,A13,A43,FINSEQ_1:def 3;
A47:              k+1+1 in dom fs2 by A9,A11,A43,FINSEQ_1:def 3;
A48:              p1/.(k+1+1)=p1.(k+1+1) by A44,PARTFUN1:def 8
                    .=lp11+1 by A11,A43,A12;
A49:              fs1/.(k+1+1)=fs1.(k+1+1) by A45,PARTFUN1:def 8;
A50:              fs2/.(k+1+1)=fs2.(k+1+1) by A47,PARTFUN1:def 8;
                  then lp11<=n-1 by A7,A9,A45,A49;
                  then
A51:              lp11+1<=n-1+1 by XREAL_1:8;
                  k+1+1 in dom p2 by A9,A11,A13,A43,FINSEQ_1:def 3;
                  then p2.(k+1+1)=p2/.(k+1+1) by PARTFUN1:def 8;
                  then
A52:              p2/.(k+1+1)=lp21+1 by A9,A11,A13,A43,A14;
                  lp21<=m-'1 by A9,A45,A49,A50;
                  then
A53:              lp21+1<=m-1+1 by A7,XREAL_1:8;
                  1<=1+lp11 by NAT_1:11;
                  then
A54:              p1/.(k+1+1) in Seg n by A48,A51, FINSEQ_1:3;
A55:              p2/.(k+1+1)=p2.(k+1+1) by A46,PARTFUN1:def 8
                    .=lp21+1 by A9,A11,A13,A43,A14;
                  1<=1+lp21 by NAT_1:11;
                  then
A56:              p2/.(k+1+1) in Seg m by A52,A53, FINSEQ_1:3;
                  fs1/.(k+1),fs2/.(k+1),fs1/.(k+1+1),fs2/.(k+1+1)
                  are_adjacent2 by A9,A11,A27,A30;
                  then p1/.(k+1),p2/.(k+1),p1/.(k+1+1),p2/.(k+1+1)
                  are_adjacent2 by A36,A40,A48,A55,Th3;
                  hence k+1+1<=len p1 implies F*(p1/.(k+1+1),p2/.(k+1+1)) c= Y
                  by A1,A26,A28,A39,A42,A54,A56;
                end;
                case k+1+1>len p1;
                  hence k+1+1<=len p1 implies F*(p1/.(k+1+1),p2/.(k+1+1)) c= Y;
                end;
              end;
              hence k+1+1<=len p1 implies F*(p1/.(k+1+1),p2/.(k+1+1)) c= Y;
            end;
            case k+1>len p1;
              hence k+1+1<=len p1 implies F*(p1/.(k+1+1),p2/.(k+1+1)) c= Y
              by NAT_1:13;
            end;
          end;
          hence k+1+1<=len p1 implies F*(p1/.(k+1+1),p2/.(k+1+1)) c= Y;
        end;
A57:    for i holds P[i] from NAT_1:sch 1(A24,A25);
A58:    len fs1 -'1+1=len fs1 by A17;
A59:    len fs1 in Seg len fs1 by A10,FINSEQ_1:3;
        then
A60:    len fs1 in dom fs1 by FINSEQ_1:def 3;
A61:    len fs1 in dom fs2 by A9,A59,FINSEQ_1:def 3;
A62:    len fs1 in dom p1 by A11,A59,FINSEQ_1:def 3;
A63:    len fs1 in dom p2 by A9,A13,A59,FINSEQ_1:def 3;
A64:    (p1/.len fs1)=p1.(len fs1) by A62,PARTFUN1:def 8
          .= (fs1/.len fs1)+1 by A11,A59,A12;
A65:    fs1/.len fs1=i2-'1 by A9,A60,PARTFUN1:def 8;
A66:    (p2/.len fs1)=p2.(len fs1) by A63,PARTFUN1:def 8
          .= (fs2/.len fs1)+1 by A9,A13,A59,A14;
        fs2/.len fs1=j2-'1 by A9,A61,PARTFUN1:def 8;
        hence F*(i2,j2) c=Y by A6,A11,A57,A58,A64,A65,A66;
      end;
    end;
    hence F*(i2,j2) c=Y;
  end;
end;

