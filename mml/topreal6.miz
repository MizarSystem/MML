:: Compactness of the Bounded Closed Subsets of TOP-REAL 2
::  by Artur Korni{\l}owicz
::
:: Received February 19, 1999
:: Copyright (c) 1999 Association of Mizar Users
 
environ

 vocabularies NUMBERS, XREAL_0, ORDINAL1, REAL_1, SUBSET_1, XBOOLE_0, METRIC_1,
      PRE_TOPC, EUCLID, CARD_1, XXREAL_0, SQUARE_1, ARYTM_3, RELAT_1, COMPLEX1,
      ARYTM_1, CARD_3, FINSEQ_2, FINSEQ_1, NAT_1, FUNCT_1, NEWTON, RVSUM_1,
      TARSKI, XXREAL_1, ORDINAL2, XXREAL_2, FINSET_1, RELAT_2, CONNSP_1,
      T_0TOPSP, TOPS_2, SETFAM_1, RCOMP_1, ZFMISC_1, STRUCT_0, FUNCOP_1,
      SUPINF_2, MCART_1, RLTOPSP1, PSCOMP_1, SPPOL_1, TOPREAL4, CONVEX1,
      TOPREAL1, TOPREAL2, SPRECT_1, PCOMPS_1, JORDAN2C, SEQ_1, SEQ_2, TOPMETR,
      PARTFUN1, CONNSP_2, TOPS_1, BORSUK_1, SEQ_4;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, SETFAM_1, RELAT_1, FUNCT_1,
      RELSET_1, PARTFUN1, FUNCT_2, FUNCT_3, BINOP_1, FINSET_1, ORDINAL1,
      NUMBERS, XCMPLX_0, XREAL_0, COMPLEX1, REAL_1, SQUARE_1, NAT_1, NAT_D,
      FUNCOP_1, FUNCT_4, SEQ_1, SEQ_2, SEQ_4, STRUCT_0, METRIC_1, TBSP_1,
      FINSEQ_1, FINSEQ_2, RVSUM_1, NEWTON, XXREAL_0, XXREAL_2, CARD_3,
      FINSEQ_4, RCOMP_1, PRE_TOPC, TOPS_1, TOPS_2, CONNSP_1, CONNSP_2,
      COMPTS_1, BORSUK_1, MEASURE6, PCOMPS_1, RLVECT_1, RLTOPSP1, EUCLID,
      WEIERSTR, TOPMETR, TOPREAL1, TOPREAL2, T_0TOPSP, TOPREAL4, PSCOMP_1,
      SPPOL_1, SPRECT_1, JORDAN2C;
 constructors SETFAM_1, FUNCT_3, REAL_1, SQUARE_1, NAT_1, CARD_3, FINSEQOP,
      RCOMP_1, FINSEQ_4, FINSOP_1, NEWTON, NAT_D, TOPS_1, CONNSP_1, TOPS_2,
      COMPTS_1, TBSP_1, T_0TOPSP, MONOID_0, TOPREAL1, TOPREAL2, TOPREAL4,
      SPPOL_1, JORDAN1, PSCOMP_1, WEIERSTR, SPRECT_1, JORDAN2C, SEQ_1,
      XXREAL_2, SEQ_4, FUNCSDOM, CONVEX1, BINOP_2, SEQ_2, MEASURE6, PCOMPS_1;
 registrations XBOOLE_0, SUBSET_1, ORDINAL1, RELSET_1, FUNCT_4, FINSET_1,
      NUMBERS, XXREAL_0, XREAL_0, SQUARE_1, MEMBERED, FINSEQ_1, RCOMP_1,
      STRUCT_0, PRE_TOPC, TOPS_1, COMPTS_1, METRIC_1, PCOMPS_1, BORSUK_1,
      TEX_4, MONOID_0, EUCLID, TOPMETR, JORDAN1, PSCOMP_1, GOBOARD9, WAYBEL_2,
      TOPGRP_1, JORDAN2C, VALUED_0, XXREAL_2, RLTOPSP1, MEASURE6, TBSP_1,
      SPRECT_1;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, SUBSET_1, FUNCT_1, PRE_TOPC, TOPS_2, CONNSP_1, CONNSP_2,
      BORSUK_1, JORDAN2C, SEQ_4, PSCOMP_1, SPPOL_1, T_0TOPSP, TOPREAL2,
      XBOOLE_0, BINOP_1, EUCLID, SQUARE_1, STRUCT_0, FINSEQ_2, RLTOPSP1,
      JORDAN1, XXREAL_2;
 theorems ABSVALUE, BORSUK_1, BORSUK_3, CARD_3, FUNCT_4, COMPTS_1, CONNSP_1,
      CONNSP_2, EUCLID, FINSEQ_1, FINSEQ_2, FINSEQ_3, FUNCT_1, FUNCT_2,
      FUNCT_3, GOBOARD6, GOBOARD7, HEINE, JGRAPH_1, JORDAN1, JORDAN2C,
      JORDAN5A, JORDAN6, METRIC_1, METRIC_6, NEWTON, NAT_1, PCOMPS_1, PRE_TOPC,
      PREPOWER, PSCOMP_1, RCOMP_1, RELAT_1, RVSUM_1, SEQ_4, SPPOL_1, SPRECT_1,
      SQUARE_1, SUBSET_1, TARSKI, TBSP_1, TOPMETR, TOPREAL1, TOPREAL3,
      TOPREAL4, TOPS_1, TOPS_2, WEIERSTR, YELLOW12, ZFMISC_1, XREAL_0,
      FUNCOP_1, XBOOLE_0, XBOOLE_1, XCMPLX_1, COMPLEX1, XREAL_1, XXREAL_0,
      XXREAL_1, XXREAL_2, NAT_D, VALUED_1, MEASURE6;
 schemes FINSET_1, NAT_1, BINOP_1;

begin  :: Real numbers

reserve a, b for real number,
  r for Real,
  i, j, n for Element of NAT,
  M for non empty MetrSpace,
  p, q, s for Point of TOP-REAL 2,
  e for Point of Euclid 2,
  w for Point of Euclid n,
  z for Point of M,
  A, B for Subset of TOP-REAL n,
  P for Subset of TOP-REAL 2,
  D for non empty Subset of TOP-REAL 2;

canceled 5;

theorem Th6:
  0 <= a & 0 <= b implies sqrt(a+b) <= sqrt a + sqrt b
proof
  assume that
A1: 0 <= a and
A2: 0 <= b;
A3: 0 <= sqrt a by A1,SQUARE_1:def 4;
  0 <= sqrt(a*b) by A1,A2,SQUARE_1:def 4;
  then 0 <= sqrt a*sqrt b by A1,A2,SQUARE_1:97;
  then 0 <= 2*(sqrt a*sqrt b);
  then a + 0 <= a + 2*sqrt a*sqrt b by XREAL_1:8;
  then
A4: a + b <= a + 2*sqrt a*sqrt b + b by XREAL_1:8;
A5: 0 <= sqrt b by A2,SQUARE_1:def 4;
  sqrt(a + 2*sqrt a*sqrt b + b) = sqrt((sqrt a)^2 + 2*sqrt a*sqrt b + b)
  by A1,SQUARE_1:def 4
    .= sqrt((sqrt a)^2 + 2*sqrt a*sqrt b + (sqrt b)^2) by A2,SQUARE_1:def 4
    .= sqrt((sqrt a + sqrt b)^2)
    .= sqrt a + sqrt b by A3,A5,SQUARE_1:89;
  hence thesis by A1,A2,A4,SQUARE_1:94;
end;

theorem Th7:
  0 <= a & a <= b implies abs(a) <= abs(b)
proof
  assume that
A1: 0 <= a and
A2: a <= b;
  abs(a) = a by A1,ABSVALUE:def 1;
  hence thesis by A1,A2,ABSVALUE:def 1;
end;

theorem Th8:
  b <= a & a <= 0 implies abs(a) <= abs(b)
proof
  assume that
A1: b <= a and
A2: a <= 0;
  per cases by A2;
  suppose
    a = 0;
    then abs(a) = 0 by ABSVALUE:7;
    hence thesis by COMPLEX1:132;
  end;
  suppose
A3: a < 0;
    then
A4: abs(a) = -a by ABSVALUE:def 1;
    abs(b) = -b by A1,A3,ABSVALUE:def 1;
    hence thesis by A1,A4,XREAL_1:26;
  end;
end;

theorem
  Product(0|->r) = 1 by RVSUM_1:124;

theorem Th10:
  Product(1|->r) = r
proof
  thus Product(1|->r) = Product<*r*> by FINSEQ_2:73
    .= r by RVSUM_1:125;
end;

theorem
  Product(2|->r) = r * r
proof
  thus Product(2|->r) = Product<*r,r*> by FINSEQ_2:75
    .= r * r by RVSUM_1:129;
end;

theorem Th12:
  Product((n+1) |-> r) = (Product(n|->r))*r
proof
  thus Product((n+1) |-> r) = (Product(n|->r))*(Product(1|->r)) by RVSUM_1:134
    .= (Product(n|->r))*r by Th10;
end;

theorem Th13:
  j <> 0 & r = 0 iff Product(j|->r) = 0
proof
  set f = j|->r;
A1: dom f = Seg j by FUNCOP_1:19;
  hereby
    assume that
A2: j <> 0 and
A3: r = 0;
    ex n be Nat st j = n + 1 by A2,NAT_1:6;
    then 1 <= j by NAT_1:11;
    then
A4: 1 in Seg j by FINSEQ_1:3;
    then f.1 = 0 by A3,FUNCOP_1:13;
    hence Product f = 0 by A1,A4,RVSUM_1:133;
  end;
  assume Product f = 0;
  then ex n being Nat st n in dom f & f.n = 0 by RVSUM_1:133;
  hence thesis by A1,FUNCOP_1:13;
end;

theorem Th14:
  r <> 0 & j <= i implies Product((i-'j) |-> r) = Product(i|->r) /
  Product(j|->r)
proof
  assume that
A1: r <> 0 and
A2: j <= i;
  defpred P[Element of NAT] means j <= $1 implies Product(($1-'j) |-> r) =
  Product($1|->r) / Product(j|->r);
A3: Product(j|->r) <> 0 by A1,Th13;
A4: for n being Element of NAT st P[n] holds P[n+1]
  proof
    let n be Element of NAT;
    assume that
A5: P[n] and
A6: j <= n+1;
    per cases by A6,NAT_1:8;
    suppose
A7:   j <= n;
      Product((n-'j+1) |-> r) = Product((n-'j) |-> r)*Product(1|->r)
      by RVSUM_1:134
        .= Product(n|->r) / Product(j|->r) * r by A5,A7,Th10
        .= Product(n|->r) * r / Product(j|->r) by XCMPLX_1:75
        .= Product((n+1) |-> r) / Product(j|->r) by Th12;
      hence thesis by A7,NAT_D:38;
    end;
    suppose
A8:   j = n+1;
      hence Product(((n+1)-'j) |-> r) = Product(0|->r) by XREAL_1:234
        .= 1 by RVSUM_1:124
        .= Product((n+1) |-> r) / Product(j|->r) by A3,A8,XCMPLX_1:60;
    end;
  end;
A9: P[0]
  proof
    assume
A10: j <= 0;
    then j = 0 by NAT_1:3;
    hence Product((0-'j) |-> r) = Product(0|->r) / Product<*>REAL by NAT_D:40
,RVSUM_1:124
      .= Product(0|->r) / Product(j|->r) by A10,NAT_1:3;
  end;
  for n being Element of NAT holds P[n] from NAT_1:sch 1(A9,A4);
  hence thesis by A2;
end;

theorem
  r <> 0 & j <= i implies r|^(i-'j) = r|^i / r|^j
proof
  assume that
A1: r <> 0 and
A2: j <= i;
  thus r|^i / r|^j = (Product(i |-> r)) / r|^j by NEWTON:def 1
    .= (Product(i |-> r)) / (Product(j |-> r)) by NEWTON:def 1
    .= Product((i-'j) |-> r) by A1,A2,Th14
    .= r|^(i-'j) by NEWTON:def 1;
end;

reserve a, b for Real;

theorem Th16:
  sqr <*a,b*> = <*a^2,b^2*>
proof
  dom sqrreal = REAL by FUNCT_2:def 1;
  then
A1: rng <*a,b*> c= dom sqrreal;
A2: dom <*a^2,b^2*> = {1,2} by FINSEQ_1:4,FINSEQ_3:29;
A3: for i being set st i in dom <*a^2,b^2*> holds (sqr <*a,b*>).i = <*a^2,b
  ^2*>.i
  proof
    let i be set;
A4: <*a,b*>.1 = a by FINSEQ_1:61;
A5: <*a,b*>.2 = b by FINSEQ_1:61;
    assume
A6: i in dom <*a^2,b^2*>;
    per cases by A2,A6,TARSKI:def 2;
    suppose
A7:   i = 1;
      hence (sqr <*a,b*>).i = a^2 by A4,VALUED_1:11
        .= <*a^2,b^2*>.i by A7,FINSEQ_1:61;
    end;
    suppose
A8:   i = 2;
      hence (sqr <*a,b*>).i = b^2 by A5,VALUED_1:11
        .= <*a^2,b^2*>.i by A8,FINSEQ_1:61;
    end;
  end;
  dom sqr <*a,b*> = dom (sqrreal*<*a,b*>) by RVSUM_1:def 8
    .= dom <*a,b*> by A1,RELAT_1:46
    .= {1,2} by FINSEQ_1:4,FINSEQ_3:29;
  hence thesis by A3,FINSEQ_1:4,FINSEQ_3:29,FUNCT_1:9;
end;

theorem Th17:
  for i be Nat for F being FinSequence of REAL st i in dom abs F &
  a = F.i holds (abs F).i = abs(a)
proof
  let i be Nat;
  let F be FinSequence of REAL such that
A1: i in dom abs F and
A2: a = F.i;
  (abs F).i = absreal.a by A1,A2,FUNCT_1:22;
  hence thesis by EUCLID:def 2;
end;

theorem
  abs <*a,b*> = <*abs(a),abs(b)*>
proof
  dom absreal = REAL by FUNCT_2:def 1;
  then rng <*a,b*> c= dom absreal;
  then
A1: dom abs <*a,b*> = dom <*a,b*> by RELAT_1:46
    .= {1,2} by FINSEQ_1:4,FINSEQ_3:29;
A2: dom <*abs(a),abs(b)*> = {1,2} by FINSEQ_1:4,FINSEQ_3:29;
  for i being set st i in dom <*abs(a),abs(b)*> holds (abs <*a,b*>).i = <*
  abs(a),abs(b)*>.i
  proof
    let i be set;
A3: <*a,b*>.1 = a by FINSEQ_1:61;
A4: <*a,b*>.2 = b by FINSEQ_1:61;
A5: 2 in {1,2} by TARSKI:def 2;
A6: 1 in {1,2} by TARSKI:def 2;
    assume
A7: i in dom <*abs(a),abs(b)*>;
    per cases by A2,A7,TARSKI:def 2;
    suppose
A8:   i = 1;
      hence (abs <*a,b*>).i = abs(a) by A1,A3,A6,Th17
        .= <*abs(a),abs(b)*>.i by A8,FINSEQ_1:61;
    end;
    suppose
A9:   i = 2;
      hence (abs <*a,b*>).i = abs(b) by A1,A4,A5,Th17
        .= <*abs(a),abs(b)*>.i by A9,FINSEQ_1:61;
    end;
  end;
  hence thesis by A1,FINSEQ_1:4,FINSEQ_3:29,FUNCT_1:9;
end;

theorem
  for a, b, c, d being real number st a <= b & c <= d holds abs(b-a) +
  abs(d-c) = (b-a) + (d-c)
proof
  let a, b, c, d be real number;
  assume that
A1: a <= b and
A2: c <= d;
  a - a <= b - a by A1,XREAL_1:15;
  then
A3: abs(b-a) = b - a by ABSVALUE:def 1;
  c - c <= d - c by A2,XREAL_1:15;
  hence thesis by A3,ABSVALUE:def 1;
end;

theorem Th20:
  for a, r being real number holds r > 0 implies a in ].a-r,a+r.[
proof
  let a, r be real number;
  assume r > 0;
  then abs(a-a) < r by ABSVALUE:def 1;
  hence thesis by RCOMP_1:8;
end;

theorem
  for a, r being real number holds r >= 0 implies a in [.a-r,a+r.]
proof
  let a, r be real number;
  assume
A1: r >= 0;
  reconsider a, r as Real by XREAL_0:def 1;
A2: a+0 <= a+r by A1,XREAL_1:9;
A3: [.a-r,a+r.] = {b : a-r <= b & b <= a+r} by RCOMP_1:def 1;
  a-r <= a-0 by A1,XREAL_1:15;
  hence thesis by A3,A2;
end;

theorem Th22:
  for a, b being real number holds a < b implies lower_bound ].a,b.[ = a &
  upper_bound ].a,b.[ = b
proof
  let a, b be real number;
  assume
A1: a < b;
  set X = ].a,b.[;
  reconsider a, b as Real by XREAL_0:def 1;
A2: (a+b)/2 < b by A1,XREAL_1:228;
A3: a < (a+b)/2 by A1,XREAL_1:228;
  then
A4: (a+b)/2 in { l where l is Real : a < l & l < b } by A2;
A5: for s being real number st 0 < s ex r being real number st r in X & r < a+s
  proof
    let s be real number such that
A6: 0 < s and
A7: for r being real number st r in X holds r >= a+s;
    reconsider s as Real by XREAL_0:def 1;
    per cases;
    suppose
A8:   a + s >= b;
A9:   (a+b)/2 in X by A4,RCOMP_1:def 2;
      (a+b)/2 < a+s by A2,A8,XXREAL_0:2;
      hence thesis by A7,A9;
    end;
    suppose
A10:  a + s < b;
A11:  a < a + s by A6,XREAL_1:31;
      then (a+(a+s))/2 < a+s by XREAL_1:228;
      then
A12:  (a+(a+s))/2 < b by A10,XXREAL_0:2;
      a < (a+(a+s))/2 by A11,XREAL_1:228;
      then (a+(a+s))/2 in {r : a < r & r < b } by A12;
      then (a+(a+s))/2 in X by RCOMP_1:def 2;
      hence thesis by A7,A11,XREAL_1:228;
    end;
  end;
A13: for s being real number st 0 < s ex r being real number st r in X & b-s
  < r
  proof
    let s be real number such that
A14: 0 < s and
A15: for r being real number st r in X holds r <= b-s;
    reconsider s as Real by XREAL_0:def 1;
    per cases;
    suppose
A16:  b - s <= a;
A17:  (a+b)/2 in X by A4,RCOMP_1:def 2;
      (a+b)/2 > b-s by A3,A16,XXREAL_0:2;
      hence thesis by A15,A17;
    end;
    suppose
A18:  b - s > a;
A19:  b - s < b - 0 by A14,XREAL_1:17;
      then b-s < (b+(b-s))/2 by XREAL_1:228;
      then
A20:  a < (b+(b-s))/2 by A18,XXREAL_0:2;
      (b+(b-s))/2 < b by A19,XREAL_1:228;
      then (b+(b-s))/2 in {r : a < r & r < b } by A20;
      then (b+(b-s))/2 in X by RCOMP_1:def 2;
      hence thesis by A15,A19,XREAL_1:228;
    end;
  end;
  a is LowerBound of X
  proof
    let r be ext-real number;
    assume r in X;
    then r in { l where l is Real : a < l & l < b } by RCOMP_1:def 2;
    then ex r1 being Real st r1 = r & a < r1 & r1 < b;
    hence thesis;
  end;
  then
A21: X is bounded_below by XXREAL_2:def 9;
A22: for r being real number st r in X holds a <= r
  proof
    let r be real number;
    assume r in X;
    then r in { l where l is Real : a < l & l < b } by RCOMP_1:def 2;
    then ex r1 being Real st r1 = r & a < r1 & r1 < b;
    hence thesis;
  end;
  b is UpperBound of X
  proof
    let r be ext-real number;
    assume r in X;
    then r in { l where l is Real : a < l & l < b } by RCOMP_1:def 2;
    then ex r1 being Real st r1 = r & a < r1 & r1 < b;
    hence thesis;
  end;
  then
A23: X is bounded_above by XXREAL_2:def 10;
A24: for r being real number st r in X holds b >= r
  proof
    let r be real number;
    assume r in X;
    then r in { l where l is Real : a < l & l < b } by RCOMP_1:def 2;
    then ex r1 being Real st r1 = r & a < r1 & r1 < b;
    hence thesis;
  end;
  (a+b)/2 in X by A4,RCOMP_1:def 2;
  hence thesis by A21,A23,A22,A5,A24,A13,SEQ_4:def 4,def 5;
end;

canceled;

theorem Th24:
  for A being bounded Subset of REAL holds A c= [.lower_bound A,upper_bound A.]
proof
  let A be bounded Subset of REAL;
  let a be set;
  assume
A1: a in A;
  then reconsider r = a as Real;
A2: r <= upper_bound A by A1,SEQ_4:def 4;
  lower_bound A <= r by A1,SEQ_4:def 5;
  hence thesis by A2,XXREAL_1:1;
end;

begin  :: Topological preliminaries

registration
  let T be TopStruct, A be finite Subset of T;
  cluster T|A -> finite;
  coherence by PRE_TOPC:29;
end;

registration
  let T be TopStruct;
  cluster empty -> connected Subset of T;
  coherence
  proof
    let C be Subset of T;
    assume C is empty;
    then reconsider D = C as empty Subset of T;
    let A, B be Subset of T|C such that
    [#](T|C) = A \/ B and
    A,B are_separated;
    [#](T|D) = {};
    hence thesis;
  end;
end;

theorem Th25:
  for S, T being TopSpace st S, T are_homeomorphic & S is
  connected holds T is connected
proof
  let S, T be TopSpace;
  given f being Function of S,T such that
A1: f is being_homeomorphism;
A2: rng f = [#]T by A1,TOPS_2:def 5;
  assume
A3: S is connected;
  dom f = [#]S by A1,TOPS_2:def 5;
  then f.:[#]S = [#]T by A2,RELAT_1:146;
  hence thesis by A1,A3,CONNSP_1:15;
end;

theorem
  for T being TopSpace, F being finite Subset-Family of T st for X being
  Subset of T st X in F holds X is compact holds union F is compact
proof
  let T be TopSpace, F be finite Subset-Family of T such that
A1: for X being Subset of T st X in F holds X is compact;
  defpred P[set] means ex A being Subset of T st A = union $1 & A is compact;
A2: for x, B being set st x in F & B c= F & P[B] holds P[B \/ {x}]
  proof
    let x, B be set such that
A3: x in F and
A4: B c= F;
    B c= bool the carrier of T
    proof
      let b be set;
      assume b in B;
      then b in F by A4;
      hence thesis;
    end;
    then reconsider C = B as Subset-Family of T;
    reconsider X = x as Subset of T by A3;
    given A being Subset of T such that
A5: A = union B and
A6: A is compact;
    set K = union C \/ X;
    take K;
    union {x} = x by ZFMISC_1:31;
    hence K = union (B \/ {x}) by ZFMISC_1:96;
    X is compact by A1,A3;
    hence thesis by A5,A6,COMPTS_1:19;
  end;
A7: P[{}]
  proof
    take {}T;
    thus thesis by ZFMISC_1:2;
  end;
A8: F is finite;
  P[F] from FINSET_1:sch 2(A8,A7,A2);
  hence thesis;
end;

begin

canceled 2;

theorem Th29:
  for A, B, C, D, a, b being set st A c= B & C c= D holds product
  ((a,b) --> (A,C)) c= product ((a,b) --> (B,D))
proof
  let A, B, C, D, a, b be set such that
A1: A c= B and
A2: C c= D;
  set f = (a,b) --> (A,C), g = (a,b) --> (B,D);
A3: dom f = {a,b} by FUNCT_4:65;
A4: for x being set st x in dom f holds f.x c= g.x
  proof
    let x be set;
    assume x in dom f;
    then
A5: x = a or x = b by A3,TARSKI:def 2;
    per cases;
    suppose
A6:   a <> b;
A7:   f.b = C by FUNCT_4:66;
      f.a = A by A6,FUNCT_4:66;
      hence thesis by A1,A2,A5,A6,A7,FUNCT_4:66;
    end;
    suppose
A8:   a = b;
      then f = a .--> C by FUNCT_4:86;
      then
A9:   f.a = C by FUNCOP_1:87;
      g = a .--> D by A8,FUNCT_4:86;
      hence thesis by A2,A5,A8,A9,FUNCOP_1:87;
    end;
  end;
  dom g = {a,b} by FUNCT_4:65;
  hence thesis by A4,CARD_3:38,FUNCT_4:65;
end;

theorem Th30:
  for A, B being Subset of REAL holds product ((1,2) --> (A,B)) is
  Subset of TOP-REAL 2
proof
  let A, B be Subset of REAL;
  set f = (1,2) --> (A,B);
  product f c= the carrier of TOP-REAL 2
  proof
    let a be set;
A1: f.1 = A by FUNCT_4:66;
A2: f.2 = B by FUNCT_4:66;
    assume a in product f;
    then consider g being Function such that
A3: a = g and
A4: dom g = dom f and
A5: for x being set st x in dom f holds g.x in f.x by CARD_3:def 5;
A6: dom f = {1,2} by FUNCT_4:65;
    then 2 in dom f by TARSKI:def 2;
    then
A7: g.2 in B by A5,A2;
    1 in dom f by A6,TARSKI:def 2;
    then g.1 in A by A5,A1;
    then reconsider m = g.1, n = g.2 as Real by A7;
A8: now
      let k be set;
      assume k in dom g;
      then k = 1 or k = 2 by A4,TARSKI:def 2;
      hence g.k = <*g.1,g.2*>.k by FINSEQ_1:61;
    end;
    dom <*g.1,g.2*> = {1,2} by FINSEQ_1:4,FINSEQ_3:29;
    then a = |[m,n]| by A3,A4,A8,FUNCT_1:9,FUNCT_4:65;
    hence thesis;
  end;
  hence thesis;
end;

theorem
  |.|[0,a]|.| = abs(a) & |.|[a,0]|.| = abs(a)
proof
  |.<*0,a*>.| = sqrt Sum <*0^2,a^2*> by Th16
    .= sqrt (0+a^2) by RVSUM_1:107
    .= abs(a) by COMPLEX1:158;
  hence |.|[0,a]|.| = abs(a);
  |.<*a,0 *>.| = sqrt Sum <*a^2,0^2 *> by Th16
    .= sqrt (a^2+0) by RVSUM_1:107
    .= abs a by COMPLEX1:158;
  hence thesis;
end;

theorem Th32:
  for p being Point of Euclid 2, q being Point of TOP-REAL 2 st p
  = 0.TOP-REAL 2 & p = q holds q = <* 0,0 *> & q`1 = 0 & q`2 = 0
proof
  let p be Point of Euclid 2, q be Point of TOP-REAL 2 such that
A1: p = 0.TOP-REAL 2 and
A2: p = q;
  0.REAL 2 = 0.TOP-REAL 2 by EUCLID:70;
  then p = <*0,0 *> by A1,FINSEQ_2:75;
  hence thesis by A2,EUCLID:56;
end;

theorem
  for p, q being Point of Euclid 2, z being Point of TOP-REAL 2 st p =
  0.REAL 2 & q = z holds dist(p,q) = |.z.|
proof
  let p, q be Point of Euclid 2, z be Point of TOP-REAL 2 such that
A1: p = 0.REAL 2 and
A2: q = z;
  reconsider P = p as Point of TOP-REAL 2 by TOPREAL3:13;
A3: 0.REAL 2 = 0.TOP-REAL 2 by EUCLID:70;
  then
A4: P`1 = 0 by A1,Th32;
A5: P`2 = 0 by A1,A3,Th32;
  dist(p,q) = (Pitag_dist 2).(p,q) by METRIC_1:def 1
    .= sqrt ((P`1 - z`1)^2 + (P`2 - z`2)^2) by A2,TOPREAL3:12
    .= sqrt ((z`1)^2 + (z`2)^2) by A4,A5;
  hence thesis by JGRAPH_1:47;
end;

theorem Th34:
  r*p = |[r*p`1,r*p`2]|
proof
  p = |[p`1,p`2]| by EUCLID:57;
  hence thesis by EUCLID:62;
end;

theorem Th35:
  s = (1-r)*p + r*q & s <> p & 0 <= r implies 0 < r
proof
  assume that
A1: s = (1-r)*p + r*q and
A2: s <> p and
A3: 0 <= r;
  assume
A4: r <= 0;
  then s = (1-0)*p + r*q by A1,A3
    .= (1-0)*p + 0 * q by A3,A4
    .= 1 * p + 0.TOP-REAL 2 by EUCLID:33
    .= 1 * p by EUCLID:31
    .= p by EUCLID:33;
  hence thesis by A2;
end;

theorem Th36:
  s = (1-r)*p + r*q & s <> q & r <= 1 implies r < 1
proof
  assume that
A1: s = (1-r)*p + r*q and
A2: s <> q and
A3: r <= 1;
  assume
A4: r >= 1;
  then s = (1-1)*p + r*q by A1,A3,XXREAL_0:1
    .= 0 * p + 1 * q by A3,A4,XXREAL_0:1
    .= 0.TOP-REAL 2 + 1 * q by EUCLID:33
    .= 1 * q by EUCLID:31
    .= q by EUCLID:33;
  hence thesis by A2;
end;

theorem
  s in LSeg(p,q) & s <> p & s <> q & p`1 < q`1 implies p`1 < s`1 & s`1 < q`1
proof
  assume that
A1: s in LSeg(p,q) and
A2: s <> p and
A3: s <> q and
A4: p`1 < q`1;
A5: p`1-q`1 < 0 by A4,XREAL_1:51;
  consider r such that
A6: s = (1-r)*p + r*q and
A7: 0 <= r and
A8: r <= 1 by A1;
  (1-r)*p = |[(1-r)*p`1,(1-r)*p`2]| by Th34;
  then
A9: ((1-r)*p)`1 = (1-r)*p`1 by EUCLID:56;
  r*q = |[r*q`1,r*q`2]| by Th34;
  then
A10: (r*q)`1 = r*q`1 by EUCLID:56;
  s = |[((1-r)*p)`1 + (r*q)`1,((1-r)*p)`2 + (r*q)`2]| by A6,EUCLID:59;
  then
A11: s`1 = (1-r)*p`1 + r*q`1 by A9,A10,EUCLID:56;
  then
A12: p`1 - s`1 = r*(p`1-q`1);
  r < 1 by A3,A6,A8,Th36;
  then
A13: 1-r > 0 by XREAL_1:52;
A14: q`1-p`1 > 0 by A4,XREAL_1:52;
  r > 0 by A2,A6,A7,Th35;
  then
A15: p`1 - s`1 < 0 by A12,A5,XREAL_1:134;
  q`1 - s`1 = (1-r)*(q`1-p`1) by A11;
  then q`1 - s`1 > 0 by A14,A13,XREAL_1:131;
  hence thesis by A15,XREAL_1:49,50;
end;

theorem
  s in LSeg(p,q) & s <> p & s <> q & p`2 < q`2 implies p`2 < s`2 & s`2 < q`2
proof
  assume that
A1: s in LSeg(p,q) and
A2: s <> p and
A3: s <> q and
A4: p`2 < q`2;
A5: p`2-q`2 < 0 by A4,XREAL_1:51;
  consider r such that
A6: s = (1-r)*p + r*q and
A7: 0 <= r and
A8: r <= 1 by A1;
  (1-r)*p = |[(1-r)*p`1,(1-r)*p`2]| by Th34;
  then
A9: ((1-r)*p)`2 = (1-r)*p`2 by EUCLID:56;
  r*q = |[r*q`1,r*q`2]| by Th34;
  then
A10: (r*q)`2 = r*q`2 by EUCLID:56;
  s = |[((1-r)*p)`1 + (r*q)`1,((1-r)*p)`2 + (r*q)`2]| by A6,EUCLID:59;
  then
A11: s`2 = (1-r)*p`2 + r*q`2 by A9,A10,EUCLID:56;
  then
A12: p`2 - s`2 = r*(p`2-q`2);
  r < 1 by A3,A6,A8,Th36;
  then
A13: 1-r > 0 by XREAL_1:52;
A14: q`2-p`2 > 0 by A4,XREAL_1:52;
  r > 0 by A2,A6,A7,Th35;
  then
A15: p`2 - s`2 < 0 by A12,A5,XREAL_1:134;
  q`2 - s`2 = (1-r)*(q`2-p`2) by A11;
  then q`2 - s`2 > 0 by A14,A13,XREAL_1:131;
  hence thesis by A15,XREAL_1:49,50;
end;

theorem
  for p being Point of TOP-REAL 2 ex q being Point of TOP-REAL 2 st q`1
  < W-bound D & p <> q
proof
  let p be Point of TOP-REAL 2;
  take q = |[W-bound D - 1, p`2 - 1]|;
  W-bound D - 1 < W-bound D - 0 by XREAL_1:17;
  hence q`1 < W-bound D by EUCLID:56;
  p`2 - 1 < p`2 - 0 by XREAL_1:17;
  hence thesis by EUCLID:56;
end;

theorem
  for p being Point of TOP-REAL 2 ex q being Point of TOP-REAL 2 st q`1
  > E-bound D & p <> q
proof
  let p be Point of TOP-REAL 2;
  take q = |[E-bound D + 1, p`2 - 1]|;
  E-bound D + 1 > E-bound D + 0 by XREAL_1:8;
  hence q`1 > E-bound D by EUCLID:56;
  p`2 - 1 < p`2 - 0 by XREAL_1:17;
  hence thesis by EUCLID:56;
end;

theorem
  for p being Point of TOP-REAL 2 ex q being Point of TOP-REAL 2 st q`2
  > N-bound D & p <> q
proof
  let p be Point of TOP-REAL 2;
  take q = |[p`1 - 1,N-bound D + 1]|;
  N-bound D + 1 > N-bound D + 0 by XREAL_1:8;
  hence q`2 > N-bound D by EUCLID:56;
  p`1 - 1 < p`1 - 0 by XREAL_1:17;
  hence thesis by EUCLID:56;
end;

theorem
  for p being Point of TOP-REAL 2 ex q being Point of TOP-REAL 2 st q`2
  < S-bound D & p <> q
proof
  let p be Point of TOP-REAL 2;
  take q = |[p`1 - 1,S-bound D - 1]|;
  S-bound D - 1 < S-bound D - 0 by XREAL_1:17;
  hence q`2 < S-bound D by EUCLID:56;
  p`1 - 1 < p`1 - 0 by XREAL_1:17;
  hence thesis by EUCLID:56;
end;

registration
  cluster non horizontal -> non empty Subset of TOP-REAL 2;
  coherence
  proof
    let P be Subset of TOP-REAL 2;
    assume P is non horizontal;
    then ex p, q being Point of TOP-REAL 2 st p in P & q in P & p`2 <> q`2 by
SPPOL_1:def 2;
    hence thesis;
  end;
  cluster non vertical -> non empty Subset of TOP-REAL 2;
  coherence
  proof
    let P be Subset of TOP-REAL 2;
    assume P is non vertical;
    then ex p, q being Point of TOP-REAL 2 st p in P & q in P & p`1 <> q`1 by
SPPOL_1:def 3;
    hence thesis;
  end;
  cluster being_Region -> open connected Subset of TOP-REAL 2;
  coherence by TOPREAL4:def 3;
  cluster open connected -> being_Region Subset of TOP-REAL 2;
  coherence by TOPREAL4:def 3;
end;

registration
  cluster empty -> horizontal Subset of TOP-REAL 2;
  coherence;
  cluster empty -> vertical Subset of TOP-REAL 2;
  coherence;
end;

registration
  cluster non empty convex Subset of TOP-REAL 2;
  existence
  proof
    consider C being non empty convex Subset of TOP-REAL 2;
    take C;
    thus thesis;
  end;
end;

registration
  let a, b be Point of TOP-REAL 2;
  cluster LSeg(a,b) -> connected;
  coherence;
end;

registration
  cluster R^2-unit_square -> connected;
  coherence
  proof
A1: LSeg(|[0,1]|,|[1,1]|) meets LSeg(|[1,0]|,|[1,1]|) by TOPREAL1:24
,XBOOLE_0:def 7;
A2: LSeg(|[0,0]|,|[1,0]|) meets LSeg(|[1,0]|,|[1,1]|) by TOPREAL1:22
,XBOOLE_0:def 7;
A3: LSeg(|[0,0]|,|[0,1]|) meets LSeg(|[0,1]|,|[1,1]|) by TOPREAL1:21
,XBOOLE_0:def 7;
    R^2-unit_square = LSeg(|[ 0,0 ]|, |[ 0,1 ]|) \/ LSeg(|[ 0,1 ]|, |[ 1,1
]|) \/ LSeg(|[ 1,1 ]|, |[ 1,0 ]|) \/ LSeg(|[ 1,0 ]|, |[ 0,0 ]|) by
TOPREAL1:def 4,XBOOLE_1:4;
    hence thesis by A3,A2,A1,JORDAN1:8;
  end;
end;

registration
  cluster being_simple_closed_curve -> connected Subset of TOP-REAL 2;
  coherence
  proof
    let P be Subset of TOP-REAL 2;
    given f being Function of (TOP-REAL 2) | R^2-unit_square, (TOP-REAL 2) |P
    such that
A1: f is being_homeomorphism;
A2: (TOP-REAL 2) |R^2-unit_square, (TOP-REAL 2) |P are_homeomorphic
    proof
      take f;
      thus thesis by A1;
    end;
    (TOP-REAL 2) |R^2-unit_square is connected by CONNSP_1:def 3;
    then (TOP-REAL 2) |P is connected by A2,Th25;
    hence thesis by CONNSP_1:def 3;
  end;
end;

theorem :: SPRECT_3:26
  LSeg(NE-corner P,SE-corner P) c= L~SpStSeq P
proof
A1: LSeg(NW-corner P,NE-corner P) \/ LSeg(NE-corner P,SE-corner P) c= (LSeg(
NW-corner P,NE-corner P) \/ LSeg(NE-corner P,SE-corner P)) \/ (LSeg(SE-corner P
  ,SW-corner P) \/ LSeg(SW-corner P,NW-corner P)) by XBOOLE_1:7;
  LSeg(NE-corner P,SE-corner P) c= (LSeg(NW-corner P,NE-corner P) \/ LSeg(
  NE-corner P,SE-corner P)) by XBOOLE_1:7;
  then
  LSeg(NE-corner P,SE-corner P) c= (LSeg(NW-corner P,NE-corner P) \/ LSeg(
NE-corner P,SE-corner P)) \/ (LSeg(SE-corner P,SW-corner P) \/ LSeg(SW-corner P
  ,NW-corner P)) by A1,XBOOLE_1:1;
  hence thesis by SPRECT_1:43;
end;

theorem
  LSeg(SW-corner P,SE-corner P) c= L~SpStSeq P
proof
  LSeg(NW-corner P,NE-corner P) \/ LSeg(NE-corner P,SE-corner P) \/ LSeg(
SE-corner P,SW-corner P) c= LSeg(NW-corner P,NE-corner P) \/ LSeg(NE-corner P,
SE-corner P) \/ LSeg(SE-corner P,SW-corner P) \/ LSeg(SW-corner P,NW-corner P)
  by XBOOLE_1:7;
  then
A1: LSeg(NW-corner P,NE-corner P) \/ LSeg(NE-corner P,SE-corner P) \/ LSeg(
SE-corner P,SW-corner P) c= LSeg(NW-corner P,NE-corner P) \/ LSeg(NE-corner P,
SE-corner P) \/ (LSeg(SE-corner P,SW-corner P) \/ LSeg(SW-corner P,NW-corner P)
  ) by XBOOLE_1:4;
  LSeg(SW-corner P,SE-corner P) c= (LSeg(NW-corner P,NE-corner P) \/ LSeg(
  NE-corner P,SE-corner P)) \/ LSeg(SW-corner P,SE-corner P) by XBOOLE_1:7;
  then
  LSeg(SW-corner P,SE-corner P) c= (LSeg(NW-corner P,NE-corner P) \/ LSeg(
NE-corner P,SE-corner P)) \/ (LSeg(SE-corner P,SW-corner P) \/ LSeg(SW-corner P
  ,NW-corner P)) by A1,XBOOLE_1:1;
  hence thesis by SPRECT_1:43;
end;

theorem
  LSeg(SW-corner P,NW-corner P) c= L~SpStSeq P
proof
  LSeg(SW-corner P,NW-corner P) c= LSeg(NW-corner P,NE-corner P) \/ LSeg(
NE-corner P,SE-corner P) \/ LSeg(SE-corner P,SW-corner P) \/ LSeg(SW-corner P,
  NW-corner P) by XBOOLE_1:7;
  then
  LSeg(SW-corner P,NW-corner P) c= LSeg(NW-corner P,NE-corner P) \/ LSeg(
NE-corner P,SE-corner P) \/ (LSeg(SE-corner P,SW-corner P) \/ LSeg(SW-corner P,
  NW-corner P)) by XBOOLE_1:4;
  hence thesis by SPRECT_1:43;
end;

theorem
  for C being Subset of TOP-REAL 2 holds {p where p is Point of TOP-REAL
  2: p`1 < W-bound C} is non empty convex connected Subset of TOP-REAL 2
proof
  let C be Subset of TOP-REAL 2;
  set A = {p where p is Point of TOP-REAL 2: p`1 < W-bound C};
  A c= the carrier of TOP-REAL 2
  proof
    let a be set;
    assume a in A;
    then ex p being Point of TOP-REAL 2 st a = p & p`1 < W-bound C;
    hence thesis;
  end;
  then reconsider A as Subset of TOP-REAL 2;
  set p = W-bound C;
  set b = |[p-1,0]|;
A1: b`1 = p-1 by EUCLID:56;
  p - 1 < p - 0 by XREAL_1:17;
  then
A2: b in A by A1;
  A is convex
  proof
    let w1, w2 be Point of TOP-REAL 2;
    assume w1 in A;
    then consider p being Point of TOP-REAL 2 such that
A3: w1 = p and
A4: p`1 < W-bound C;
    assume w2 in A;
    then consider q being Point of TOP-REAL 2 such that
A5: w2 = q and
A6: q`1 < W-bound C;
    let k be set;
    assume
A7: k in LSeg(w1,w2);
    then reconsider z = k as Point of TOP-REAL 2;
    per cases by XXREAL_0:1;
    suppose
      p`1 < q`1;
      then z`1 <= w2`1 by A3,A5,A7,TOPREAL1:9;
      then z`1 < W-bound C by A5,A6,XXREAL_0:2;
      hence thesis;
    end;
    suppose
      q`1 < p`1;
      then z`1 <= w1`1 by A3,A5,A7,TOPREAL1:9;
      then z`1 < W-bound C by A3,A4,XXREAL_0:2;
      hence thesis;
    end;
    suppose
      p`1 = q`1;
      then z`1 = p`1 by A3,A5,A7,GOBOARD7:5;
      hence thesis by A4;
    end;
  end;
  then reconsider A as non empty convex Subset of TOP-REAL 2 by A2;
  A is connected;
  hence thesis;
end;

begin  :: Balls as subsets of TOP-REAL n

reserve r for real number;

theorem Th47:
  e = q & p in Ball(e,r) implies q`1-r < p`1 & p`1 < q`1+r
proof
  assume that
A1: e = q and
A2: p in Ball(e,r);
  reconsider f = p as Point of Euclid 2 by TOPREAL3:13;
A3: dist(e,f) < r by A2,METRIC_1:12;
A4: dist(e,f) = (Pitag_dist 2).(e,f) by METRIC_1:def 1
    .= sqrt ((q`1 - p`1)^2 + (q`2 - p`2)^2) by A1,TOPREAL3:12;
A5: r > 0 by A2,TBSP_1:17;
  assume
A6: not thesis;
  per cases by A6;
  suppose
A7: q`1-r >= p`1;
    (q`2-p`2)^2 >= 0 by XREAL_1:65;
    then
A8: (q`1-p`1)^2 + (q`2-p`2)^2 >= (q`1-p`1)^2 + 0 by XREAL_1:8;
    q`1-p`1 >= r by A7,XREAL_1:13;
    then (q`1-p`1)^2 >= r^2 by A5,SQUARE_1:77;
    then (q`1-p`1)^2 + (q`2-p`2)^2 >= r^2 by A8,XXREAL_0:2;
    then sqrt((q`1-p`1)^2 + (q`2-p`2)^2) >= sqrt(r^2) by A5,SQUARE_1:94;
    hence contradiction by A3,A4,A5,SQUARE_1:89;
  end;
  suppose
A9: p`1 >= q`1+r;
    (q`2-p`2)^2 >= 0 by XREAL_1:65;
    then
A10: (q`1-p`1)^2 + (q`2-p`2)^2 >= (q`1-p`1)^2 + 0 by XREAL_1:8;
    p`1-q`1 >= r by A9,XREAL_1:21;
    then (-(q`1-p`1))^2 >= r^2 by A5,SQUARE_1:77;
    then (q`1-p`1)^2 + (q`2-p`2)^2 >= r^2 by A10,XXREAL_0:2;
    then sqrt((q`1-p`1)^2 + (q`2-p`2)^2) >= sqrt(r^2) by A5,SQUARE_1:94;
    hence contradiction by A3,A4,A5,SQUARE_1:89;
  end;
end;

theorem Th48:
  e = q & p in Ball(e,r) implies q`2-r < p`2 & p`2 < q`2+r
proof
  assume that
A1: e = q and
A2: p in Ball(e,r);
  reconsider f = p as Point of Euclid 2 by TOPREAL3:13;
A3: dist(e,f) < r by A2,METRIC_1:12;
A4: dist(e,f) = (Pitag_dist 2).(e,f) by METRIC_1:def 1
    .= sqrt ((q`1 - p`1)^2 + (q`2 - p`2)^2) by A1,TOPREAL3:12;
A5: r > 0 by A2,TBSP_1:17;
  assume
A6: not thesis;
  per cases by A6;
  suppose
A7: q`2-r >= p`2;
    (q`1-p`1)^2 >= 0 by XREAL_1:65;
    then
A8: (q`1-p`1)^2 + (q`2-p`2)^2 >= (q`2-p`2)^2 + 0 by XREAL_1:8;
    q`2-p`2 >= r by A7,XREAL_1:13;
    then (q`2-p`2)^2 >= r^2 by A5,SQUARE_1:77;
    then (q`1-p`1)^2 + (q`2-p`2)^2 >= r^2 by A8,XXREAL_0:2;
    then sqrt((q`1-p`1)^2 + (q`2-p`2)^2) >= sqrt(r^2) by A5,SQUARE_1:94;
    hence contradiction by A3,A4,A5,SQUARE_1:89;
  end;
  suppose
A9: p`2 >= q`2+r;
    (q`1-p`1)^2 >= 0 by XREAL_1:65;
    then
A10: (q`1-p`1)^2 + (q`2-p`2)^2 >= (q`2-p`2)^2 + 0 by XREAL_1:8;
    p`2-q`2 >= r by A9,XREAL_1:21;
    then (-(q`2-p`2))^2 >= r^2 by A5,SQUARE_1:77;
    then (q`1-p`1)^2 + (q`2-p`2)^2 >= r^2 by A10,XXREAL_0:2;
    then sqrt((q`1-p`1)^2 + (q`2-p`2)^2) >= sqrt(r^2) by A5,SQUARE_1:94;
    hence contradiction by A3,A4,A5,SQUARE_1:89;
  end;
end;

theorem Th49:
  p = e implies product ((1,2) --> (].p`1-r/sqrt 2,p`1+r/sqrt 2.[,
  ].p`2-r/sqrt 2,p`2+r/sqrt 2.[)) c= Ball(e,r)
proof
  set A = ].p`1-r/sqrt 2,p`1+r/sqrt 2.[, B = ].p`2-r/sqrt 2,p`2+r/sqrt 2.[, f
  = (1,2) --> (A,B);
  assume
A1: p = e;
  let a be set;
A2: A = {m where m is Real : p`1-r/sqrt 2 < m & m < p`1+r/sqrt 2 } by
RCOMP_1:def 2;
A3: f.2 = B by FUNCT_4:66;
A4: B = {n where n is Real : p`2-r/sqrt 2 < n & n < p`2+r/sqrt 2 } by
RCOMP_1:def 2;
A5: f.1 = A by FUNCT_4:66;
  assume a in product f;
  then consider g being Function such that
A6: a = g and
A7: dom g = dom f and
A8: for x being set st x in dom f holds g.x in f.x by CARD_3:def 5;
A9: dom f = {1,2} by FUNCT_4:65;
  then 1 in dom f by TARSKI:def 2;
  then
A10: g.1 in A by A8,A5;
  then consider m being Real such that
A11: m = g.1 and
  p`1-r/sqrt 2 < m and
  m < p`1+r/sqrt 2 by A2;
A12: 0 <= (m-p`1)^2 by XREAL_1:65;
  2 in dom f by A9,TARSKI:def 2;
  then
A13: g.2 in B by A8,A3;
  then consider n being Real such that
A14: n = g.2 and
  p`2-r/sqrt 2 < n and
  n < p`2+r/sqrt 2 by A4;
  abs(n-p`2) < r/sqrt 2 by A13,A14,RCOMP_1:8;
  then (abs(n-p`2))^2 < (r/sqrt 2)^2 by COMPLEX1:132,SQUARE_1:78;
  then (abs(n-p`2))^2 < r^2/(sqrt 2)^2 by XCMPLX_1:77;
  then (abs(n-p`2))^2 < r^2/2 by SQUARE_1:def 4;
  then
A15: (n-p`2)^2 < r^2/2 by COMPLEX1:161;
  p`1-(p`1+r/sqrt 2) < p`1-(p`1-r/sqrt 2) by A10,XREAL_1:17,XXREAL_1:28;
  then -r/sqrt 2+r/sqrt 2 < r/sqrt 2+r/sqrt 2 by XREAL_1:8;
  then
A16: 0 < r by SQUARE_1:84;
A17: now
    let k be set;
    assume k in dom g;
    then k = 1 or k = 2 by A7,TARSKI:def 2;
    hence g.k = <*g.1,g.2*>.k by FINSEQ_1:61;
  end;
A18: 0 <= (n-p`2)^2 by XREAL_1:65;
  abs(m-p`1) < r/sqrt 2 by A10,A11,RCOMP_1:8;
  then (abs(m-p`1))^2 < (r/sqrt 2)^2 by COMPLEX1:132,SQUARE_1:78;
  then (abs(m-p`1))^2 < r^2/(sqrt 2)^2 by XCMPLX_1:77;
  then (abs(m-p`1))^2 < r^2/2 by SQUARE_1:def 4;
  then (m-p`1)^2 < r^2/2 by COMPLEX1:161;
  then (m-p`1)^2 + (n-p`2)^2 < r^2/2 + r^2/2 by A15,XREAL_1:10;
  then sqrt((m-p`1)^2 + (n-p`2)^2) < sqrt(r^2) by A12,A18,SQUARE_1:95;
  then
A19: sqrt((m-p`1)^2 + (n-p`2)^2) < r by A16,SQUARE_1:89;
  dom <*g.1,g.2*> = {1,2} by FINSEQ_1:4,FINSEQ_3:29;
  then a = |[m,n]| by A6,A7,A11,A14,A17,FUNCT_1:9,FUNCT_4:65;
  then reconsider c = a as Point of TOP-REAL 2;
  reconsider b = c as Point of Euclid 2 by TOPREAL3:13;
  dist(b,e) = (Pitag_dist 2).(b,e) by METRIC_1:def 1
    .= sqrt ((c`1 - p`1)^2 + (c`2 - p`2)^2) by A1,TOPREAL3:12;
  hence thesis by A6,A11,A14,A19,METRIC_1:12;
end;

theorem Th50:
  p = e implies Ball(e,r) c= product((1,2)-->(].p`1-r,p`1+r.[,].p
  `2-r,p`2+r.[))
proof
  set A = ].p`1-r,p`1+r.[, B = ].p`2-r,p`2+r.[, f = (1,2)-->(A,B);
  assume that
A1: p = e;
  let a be set;
  assume
A2: a in Ball(e,r);
  then reconsider b = a as Point of Euclid 2;
  reconsider q = b as Point of TOP-REAL 2 by TOPREAL3:13;
  reconsider g = q as FinSequence;
A3: for x being set st x in dom f holds g.x in f.x
  proof
    let x be set;
    assume
A4: x in dom f;
    per cases by A4,TARSKI:def 2;
    suppose
A5:   x = 1;
A6:   f.1 = A by FUNCT_4:66;
A7:   q`1 < p`1+r by A1,A2,Th47;
      p`1-r < q`1 by A1,A2,Th47;
      hence thesis by A5,A6,A7,XXREAL_1:4;
    end;
    suppose
A8:   x = 2;
A9:   f.2 = B by FUNCT_4:66;
A10:  q`2 < p`2+r by A1,A2,Th48;
      p`2-r < q`2 by A1,A2,Th48;
      hence thesis by A8,A9,A10,XXREAL_1:4;
    end;
  end;
  q is Function of Seg 2, REAL by EUCLID:26;
  then
A11: dom g = {1,2} by FINSEQ_1:4,FUNCT_2:def 1;
  dom f = {1,2} by FUNCT_4:65;
  hence thesis by A11,A3,CARD_3:18;
end;

theorem Th51:
  P = Ball(e,r) & p = e implies proj1.:P = ].p`1-r,p`1+r.[
proof
  assume that
A1: P = Ball(e,r) and
A2: p = e;
  hereby
    let a be set;
    assume a in proj1.:P;
    then consider x being set such that
A3: x in the carrier of TOP-REAL 2 and
A4: x in P and
A5: a = proj1.x by FUNCT_2:115;
    reconsider b = a as Real by A5;
    reconsider x as Point of TOP-REAL 2 by A3;
A6: a = x`1 by A5,PSCOMP_1:def 28;
    then
A7: b < p`1+r by A1,A2,A4,Th47;
    p`1-r < b by A1,A2,A4,A6,Th47;
    hence a in ].p`1-r,p`1+r.[ by A7,XXREAL_1:4;
  end;
  let a be set;
  assume
A8: a in ].p`1-r,p`1+r.[;
  then reconsider b = a as Real;
  reconsider f = |[b,p`2]| as Point of Euclid 2 by TOPREAL3:13;
A9: dist(f,e) = (Pitag_dist 2).(f,e) by METRIC_1:def 1
    .= sqrt ((|[b,p`2]|`1 - p`1)^2 + (|[b,p`2]|`2 - p`2)^2 ) by A2,TOPREAL3:12
    .= sqrt ((b - p`1)^2 + (|[b,p`2]|`2 - p`2)^2) by EUCLID:56
    .= sqrt ((b - p`1)^2 + (p`2 - p`2)^2) by EUCLID:56
    .= sqrt ((b - p`1)^2 + 0);
  b < p`1+r by A8,XXREAL_1:4;
  then
A10: b - p`1 < p`1+r - p`1 by XREAL_1:11;
  now
    per cases;
    case
      0 <= b - p`1;
      hence dist(f,e) < r by A10,A9,SQUARE_1:89;
    end;
    case
A11:  0 > b - p`1;
      p`1 - r < b by A8,XXREAL_1:4;
      then p`1 - r + r < b + r by XREAL_1:8;
      then
A12:  p`1 - b < r + b - b by XREAL_1:11;
      sqrt ((b - p`1)^2) = sqrt ((-(b - p`1))^2)
        .= -(b - p`1) by A11,SQUARE_1:89;
      hence dist(f,e) < r by A9,A12;
    end;
  end;
  then
A13: |[b,p`2]| in P by A1,METRIC_1:12;
  a = |[b,p`2]|`1 by EUCLID:56
    .= proj1.(|[b,p`2]|) by PSCOMP_1:def 28;
  hence thesis by A13,FUNCT_2:43;
end;

theorem Th52:
  P = Ball(e,r) & p = e implies proj2.:P = ].p`2-r,p`2+r.[
proof
  assume that
A1: P = Ball(e,r) and
A2: p = e;
  hereby
    let a be set;
    assume a in proj2.:P;
    then consider x being set such that
A3: x in the carrier of TOP-REAL 2 and
A4: x in P and
A5: a = proj2.x by FUNCT_2:115;
    reconsider b = a as Real by A5;
    reconsider x as Point of TOP-REAL 2 by A3;
A6: a = x`2 by A5,PSCOMP_1:def 29;
    then
A7: b < p`2+r by A1,A2,A4,Th48;
    p`2-r < b by A1,A2,A4,A6,Th48;
    hence a in ].p`2-r,p`2+r.[ by A7,XXREAL_1:4;
  end;
  let a be set;
  assume
A8: a in ].p`2-r,p`2+r.[;
  then reconsider b = a as Real;
  reconsider f = |[p`1,b]| as Point of Euclid 2 by TOPREAL3:13;
A9: dist(f,e) = (Pitag_dist 2).(f,e) by METRIC_1:def 1
    .= sqrt ((|[p`1,b]|`1 - p`1)^2 + (|[p`1,b]|`2 - p`2)^2 ) by A2,TOPREAL3:12
    .= sqrt ((p`1 - p`1)^2 + (|[p`1,b]|`2 - p`2)^2) by EUCLID:56
    .= sqrt (0 + (b - p`2)^2) by EUCLID:56;
  b < p`2+r by A8,XXREAL_1:4;
  then
A10: b - p`2 < p`2+r - p`2 by XREAL_1:11;
  now
    per cases;
    case
      0 <= b - p`2;
      hence dist(f,e) < r by A10,A9,SQUARE_1:89;
    end;
    case
A11:  0 > b - p`2;
      p`2 - r < b by A8,XXREAL_1:4;
      then p`2 - r + r < b + r by XREAL_1:8;
      then
A12:  p`2 - b < r + b - b by XREAL_1:11;
      sqrt ((b - p`2)^2) = sqrt ((-(b - p`2))^2)
        .= -(b - p`2) by A11,SQUARE_1:89;
      hence dist(f,e) < r by A9,A12;
    end;
  end;
  then
A13: |[p`1,b]| in P by A1,METRIC_1:12;
  a = |[p`1,b]|`2 by EUCLID:56
    .= proj2.(|[p`1,b]|) by PSCOMP_1:def 29;
  hence thesis by A13,FUNCT_2:43;
end;

theorem
  D = Ball(e,r) & p = e implies W-bound D = p`1 - r
proof
  assume that
A1: D = Ball(e,r) and
A2: p = e;
  r > 0 by A1,TBSP_1:17;
  then
A3: p`1+0 < p`1+r by XREAL_1:8;
  p`1-r < p`1-0 by A1,TBSP_1:17,XREAL_1:17;
  then p`1-r < p`1+r by A3,XXREAL_0:2;
  then
A4: lower_bound ].p`1-r,p`1+r.[ = p`1-r by Th22;
  proj1.:D = ].p`1-r,p`1+r.[ by A1,A2,Th51;
  hence thesis by A4,SPRECT_1:48;
end;

theorem
  D = Ball(e,r) & p = e implies E-bound D = p`1 + r
proof
  assume that
A1: D = Ball(e,r) and
A2: p = e;
  r > 0 by A1,TBSP_1:17;
  then
A3: p`1+0 < p`1+r by XREAL_1:8;
  p`1-r < p`1-0 by A1,TBSP_1:17,XREAL_1:17;
  then p`1-r < p`1+r by A3,XXREAL_0:2;
  then
A4: upper_bound ].p`1-r,p`1+r.[ = p`1+r by Th22;
  proj1.:D = ].p`1-r,p`1+r.[ by A1,A2,Th51;
  hence thesis by A4,SPRECT_1:51;
end;

theorem
  D = Ball(e,r) & p = e implies S-bound D = p`2 - r
proof
  assume that
A1: D = Ball(e,r) and
A2: p = e;
  r > 0 by A1,TBSP_1:17;
  then
A3: p`2+0 < p`2+r by XREAL_1:8;
  p`2-r < p`2-0 by A1,TBSP_1:17,XREAL_1:17;
  then p`2-r < p`2+r by A3,XXREAL_0:2;
  then
A4: lower_bound ].p`2-r,p`2+r.[ = p`2-r by Th22;
  proj2.:D = ].p`2-r,p`2+r.[ by A1,A2,Th52;
  hence thesis by A4,SPRECT_1:49;
end;

theorem
  D = Ball(e,r) & p = e implies N-bound D = p`2 + r
proof
  assume that
A1: D = Ball(e,r) and
A2: p = e;
  r > 0 by A1,TBSP_1:17;
  then
A3: p`2+0 < p`2+r by XREAL_1:8;
  p`2-r < p`2-0 by A1,TBSP_1:17,XREAL_1:17;
  then p`2-r < p`2+r by A3,XXREAL_0:2;
  then
A4: upper_bound ].p`2-r,p`2+r.[ = p`2+r by Th22;
  proj2.:D = ].p`2-r,p`2+r.[ by A1,A2,Th52;
  hence thesis by A4,SPRECT_1:50;
end;

theorem
  D = Ball(e,r) implies D is non horizontal
proof
  reconsider p = e as Point of TOP-REAL 2 by TOPREAL3:13;
  assume
A1: D = Ball(e,r);
  then
A2: r > 0 by TBSP_1:17;
  take p, q = |[p`1,p`2-r/2]|;
  thus p in D by A1,TBSP_1:16,17;
  reconsider f = q as Point of Euclid 2 by TOPREAL3:13;
  dist(e,f) = (Pitag_dist 2).(e,f) by METRIC_1:def 1
    .= sqrt ((p`1 - q`1)^2 + (p`2 - q`2)^2) by TOPREAL3:12
    .= sqrt ((p`1 - p`1)^2 + (p`2 - q`2)^2) by EUCLID:56
    .= sqrt (0 + (p`2 - (p`2-r/2))^2) by EUCLID:56
    .= r/2 by A2,SQUARE_1:89;
  then dist(e,f) < r by A1,TBSP_1:17,XREAL_1:218;
  hence q in D by A1,METRIC_1:12;
  r/2 > 0 by A2,XREAL_1:141;
  then p`2-r/2 < p`2-0 by XREAL_1:17;
  hence thesis by EUCLID:56;
end;

theorem
  D = Ball(e,r) implies D is non vertical
proof
  reconsider p = e as Point of TOP-REAL 2 by TOPREAL3:13;
  assume
A1: D = Ball(e,r);
  then
A2: r > 0 by TBSP_1:17;
  take p, q = |[p`1-r/2,p`2]|;
  thus p in D by A1,TBSP_1:16,17;
  reconsider f = q as Point of Euclid 2 by TOPREAL3:13;
  dist(e,f) = (Pitag_dist 2).(e,f) by METRIC_1:def 1
    .= sqrt ((p`1 - q`1)^2 + (p`2 - q`2)^2) by TOPREAL3:12
    .= sqrt ((p`1 - (p`1-r/2))^2 + (p`2 - q`2)^2) by EUCLID:56
    .= sqrt ((p`1 - (p`1-r/2))^2 + (p`2 - p`2)^2) by EUCLID:56
    .= r/2 by A2,SQUARE_1:89;
  then dist(e,f) < r by A1,TBSP_1:17,XREAL_1:218;
  hence q in D by A1,METRIC_1:12;
  r/2 > 0 by A2,XREAL_1:141;
  then p`1-r/2 < p`1-0 by XREAL_1:17;
  hence thesis by EUCLID:56;
end;

theorem
  for f being Point of Euclid 2, x being Point of TOP-REAL 2 st x in
  Ball(f,a) holds not |[x`1-2*a,x`2]| in Ball(f,a)
proof
  let f be Point of Euclid 2, x be Point of TOP-REAL 2 such that
A1: x in Ball(f,a);
A2: a > 0 by A1,TBSP_1:17;
  set p = |[x`1-2*a,x`2]|;
  reconsider z = p, X = x as Point of Euclid 2 by TOPREAL3:13;
A3: dist(X,z) = (Pitag_dist 2).(X,z) by METRIC_1:def 1
    .= sqrt ((x`1 - p`1)^2 + (x`2 - p`2)^2) by TOPREAL3:12
    .= sqrt ((x`1 - (x`1-2*a))^2 + (x`2 - p`2)^2) by EUCLID:56
    .= sqrt (0^2 + 2*(x`1 - x`1)*(2*a) + (2*a)^2 + (x`2 - x`2)^2) by EUCLID:56
    .= 2*a by A2,SQUARE_1:89;
  assume |[x`1-2*a,x`2]| in Ball(f,a);
  then
A4: dist(f,z) < a by METRIC_1:12;
  dist(f,X) < a by A1,METRIC_1:12;
  then dist(f,X) + dist(f,z) < a + a by A4,XREAL_1:10;
  hence contradiction by A3,METRIC_1:4;
end;

theorem
  for X being non empty compact Subset of TOP-REAL 2, p being Point of
  Euclid 2 st p = 0.TOP-REAL 2 & a > 0 holds X c= Ball(p, abs(E-bound X)+abs(
  N-bound X)+abs(W-bound X)+abs(S-bound X)+a)
proof
  let X be non empty compact Subset of TOP-REAL 2, p be Point of Euclid 2 such
  that
A1: p = 0.TOP-REAL 2 and
A2: a > 0;
  set A = X, n = N-bound A, s = S-bound A, e = E-bound A, w = W-bound A, r =
  abs(e)+abs(n)+abs(w)+abs(s)+a;
A3: abs(e) + abs(n) + abs(w) + abs(s) + 0 < abs(e) + abs(n) + abs(w) + abs(
  s) + a by A2,XREAL_1:10;
  let x be set;
  assume
A4: x in X;
  then reconsider b = x as Point of Euclid 2 by TOPREAL3:13;
  reconsider P = p, B = b as Point of TOP-REAL 2 by TOPREAL3:13;
A5: P`1 = 0 by A1,Th32;
A6: B`1 <= e by A4,PSCOMP_1:71;
A7: B`2 <= n by A4,PSCOMP_1:71;
A8: s <= B`2 by A4,PSCOMP_1:71;
A9: P`2 = 0 by A1,Th32;
A10: dist(p,b) = (Pitag_dist 2).(p,b) by METRIC_1:def 1
    .= sqrt ((P`1 - B`1)^2 + (P`2 - B`2)^2) by TOPREAL3:12
    .= sqrt ((B`1)^2 + (B`2)^2) by A5,A9;
A11: 0 <= (B`2)^2 by XREAL_1:65;
  0 <= (B`1)^2 by XREAL_1:65;
  then sqrt ((B`1)^2 + (B`2)^2) <= sqrt(B`1)^2 + sqrt(B`2)^2 by A11,Th6;
  then sqrt ((B`1)^2 + (B`2)^2) <= abs(B`1) + sqrt(B`2)^2 by COMPLEX1:158;
  then
A12: sqrt ((B`1)^2 + (B`2)^2) <= abs(B`1) + abs(B`2) by COMPLEX1:158;
A13: 0 <= abs(n) by COMPLEX1:132;
A14: 0 <= abs(e) by COMPLEX1:132;
A15: 0 <= abs(w) by COMPLEX1:132;
A16: 0 <= abs(s) by COMPLEX1:132;
A17: w <= B`1 by A4,PSCOMP_1:71;
  now
    per cases;
    case
A18:  B`1 >= 0 & B`2 >= 0;
      abs(e) + abs(n) + 0 <= abs(e) + abs(n) + abs(w) by A15,XREAL_1:9;
      then abs(e) + abs(n) <= abs(e) + abs(n) + abs(w) + abs(s) by A16,
XREAL_1:9;
      then
A19:  abs(e) + abs(n) < r by A3,XXREAL_0:2;
A20:  abs(B`2) <= abs(n) by A7,A18,Th7;
      abs(B`1) <= abs(e) by A6,A18,Th7;
      then abs(B`1) + abs(B`2) <= abs(e) + abs(n) by A20,XREAL_1:9;
      then dist(p,b) <= abs(e) + abs(n) by A10,A12,XXREAL_0:2;
      hence dist(p,b) < r by A19,XXREAL_0:2;
    end;
    case
A21:  B`1 < 0 & B`2 >= 0;
      0 + (abs(n) + abs(w)) <= abs(e) + (abs(n) + abs(w)) by A14,XREAL_1:9;
      then abs(w) + abs(n) <= abs(e) + abs(n) + abs(w) + abs(s) by A16,
XREAL_1:9;
      then
A22:  abs(w) + abs(n) < r by A3,XXREAL_0:2;
A23:  abs(B`2) <= abs(n) by A7,A21,Th7;
      abs(B`1) <= abs(w) by A17,A21,Th8;
      then abs(B`1) + abs(B`2) <= abs(w) + abs(n) by A23,XREAL_1:9;
      then dist(p,b) <= abs(w) + abs(n) by A10,A12,XXREAL_0:2;
      hence dist(p,b) < r by A22,XXREAL_0:2;
    end;
    case
A24:  B`1 >= 0 & B`2 < 0;
A25:  abs(e) + abs(n) + abs(s) + 0 <= abs(e) + abs(n) + abs(s) + abs(w)
      by A15,XREAL_1:9;
      abs(e) + abs(s) + 0 <= abs(e) + abs(s) + abs(n) by A13,XREAL_1:9;
      then abs(e) + abs(s) <= abs(e) + abs(n) + abs(w) + abs(s) by A25,
XXREAL_0:2;
      then
A26:  abs(e) + abs(s) < r by A3,XXREAL_0:2;
A27:  abs(B`2) <= abs(s) by A8,A24,Th8;
      abs(B`1) <= abs(e) by A6,A24,Th7;
      then abs(B`1) + abs(B`2) <= abs(e) + abs(s) by A27,XREAL_1:9;
      then dist(p,b) <= abs(e) + abs(s) by A10,A12,XXREAL_0:2;
      hence dist(p,b) < r by A26,XXREAL_0:2;
    end;
    case
A28:  B`1 < 0 & B`2 < 0;
      then
A29:  abs(B`2) <= abs(s) by A8,Th8;
      abs(B`1) <= abs(w) by A17,A28,Th8;
      then abs(B`1) + abs(B`2) <= abs(w) + abs(s) by A29,XREAL_1:9;
      then
A30:  dist(p,b) <= abs(w) + abs(s) by A10,A12,XXREAL_0:2;
      0 + (abs(w) + abs(s)) <= abs(e) + abs(n) + (abs(w) + abs(s)) by A14,A13,
XREAL_1:9;
      then abs(w) + abs(s) < r by A2,XREAL_1:10;
      hence dist(p,b) < r by A30,XXREAL_0:2;
    end;
  end;
  hence thesis by METRIC_1:12;
end;

theorem Th61:
  for M being Reflexive symmetric triangle (non empty MetrStruct),
  z being Point of M holds r < 0 implies Sphere(z,r) = {}
proof
  let M be Reflexive symmetric triangle (non empty MetrStruct), z be Point of
  M;
  assume
A1: r < 0;
  thus Sphere(z,r) c= {}
  proof
    let a be set;
    assume
A2: a in Sphere(z,r);
    then reconsider b = a as Point of M;
    dist(b,z) = r by A2,METRIC_1:14;
    hence thesis by A1,METRIC_1:5;
  end;
  thus thesis by XBOOLE_1:2;
end;

theorem
  for M being Reflexive discerning (non empty MetrStruct), z being Point
  of M holds Sphere(z,0) = {z}
proof
  let M be Reflexive discerning (non empty MetrStruct), z be Point of M;
  thus Sphere(z,0) c= {z}
  proof
    let a be set;
    assume
A1: a in Sphere(z,0);
    then reconsider b = a as Point of M;
    dist(z,b) = 0 by A1,METRIC_1:14;
    then b = z by METRIC_1:2;
    hence thesis by TARSKI:def 1;
  end;
  let a be set;
  assume a in {z};
  then
A2: a = z by TARSKI:def 1;
  dist(z,z) = 0 by METRIC_1:1;
  hence thesis by A2,METRIC_1:14;
end;

theorem
  for M being Reflexive symmetric triangle (non empty MetrStruct), z
  being Point of M holds r < 0 implies cl_Ball(z,r) = {}
proof
  let M be Reflexive symmetric triangle (non empty MetrStruct), z be Point of
  M;
A1: Sphere(z,r) \/ Ball(z,r) = cl_Ball(z,r) by METRIC_1:17;
  assume
A2: r < 0;
  then Ball(z,r) = {} by TBSP_1:17;
  hence thesis by A2,A1,Th61;
end;

theorem
  cl_Ball(z,0) = {z}
proof
  thus cl_Ball(z,0) c= {z}
  proof
    let a be set;
    assume
A1: a in cl_Ball(z,0);
    then reconsider b = a as Point of M;
    dist(b,z) <= 0 by A1,METRIC_1:13;
    then dist(b,z) = 0 by METRIC_1:5;
    then b = z by METRIC_1:2;
    hence thesis by TARSKI:def 1;
  end;
  let a be set;
  assume a in {z};
  then
A2: a = z by TARSKI:def 1;
  dist(z,z) = 0 by METRIC_1:1;
  hence thesis by A2,METRIC_1:13;
end;

Lm1: for A being Subset of TopSpaceMetr M st A = cl_Ball(z,r) holds A` is open
proof
  let A be Subset of TopSpaceMetr M such that
A1: A = cl_Ball(z,r);
  for x being set holds x in A` iff ex Q being Subset of TopSpaceMetr M st
  Q is open & Q c= A` & x in Q
  proof
    let x be set;
    thus x in A` implies ex Q being Subset of TopSpaceMetr M st Q is open & Q
    c= A` & x in Q
    proof
      assume
A2:   x in A`;
      then reconsider e = x as Point of M by TOPMETR:16;
      reconsider Q = Ball(e,dist(e,z)-r) as Subset of TopSpaceMetr M by
TOPMETR:16;
      take Q;
      thus Q is open by TOPMETR:21;
      thus Q c= A`
      proof
        let q be set;
        assume
A3:     q in Q;
        then reconsider f = q as Point of M;
        dist(e,z) <= dist(e,f) + dist(f,z) by METRIC_1:4;
        then
A4:     dist(e,z) - r <= dist(e,f) + dist(f,z) - r by XREAL_1:11;
        dist(e,f) < dist(e,z)-r by A3,METRIC_1:12;
        then dist(e,f) < dist(e,f) + dist(f,z) - r by A4,XXREAL_0:2;
        then dist(e,f) - dist(e,f) < dist(e,f) + dist(f,z) - r - dist(e,f) by
XREAL_1:11;
        then 0 < dist(e,f) - dist(e,f) + dist(f,z) - r;
        then dist(f,z) > r by XREAL_1:49;
        then not q in A by A1,METRIC_1:13;
        hence thesis by A3,SUBSET_1:50;
      end;
      not x in A by A2,XBOOLE_0:def 5;
      then dist(z,e) > r by A1,METRIC_1:13;
      then dist(e,z)-r > r-r by XREAL_1:11;
      hence thesis by TBSP_1:16;
    end;
    thus thesis;
  end;
  hence thesis by TOPS_1:57;
end;

theorem Th65:
  for A being Subset of TopSpaceMetr M st A = cl_Ball(z,r) holds A is closed
proof
  let A be Subset of TopSpaceMetr M;
  assume A = cl_Ball(z,r);
  then A` is open by Lm1;
  hence thesis by TOPS_1:29;
end;

theorem
  A = cl_Ball(w,r) implies A is closed
proof
A1: the TopStruct of TOP-REAL n = TopSpaceMetr Euclid n by EUCLID:def 8;
  then reconsider E=A as Subset of TopSpaceMetr Euclid n;
  assume A = cl_Ball(w,r);
  then E is closed by Th65;
  hence A is closed by A1,PRE_TOPC:61;
end;

theorem Th67:
  for r being real number for M be Reflexive symmetric triangle
  non empty MetrStruct for x be Element of M holds cl_Ball(x,r) is bounded
proof
  let r be real number;
  let M be Reflexive symmetric triangle non empty MetrStruct;
  let x be Element of M;
  cl_Ball(x,r) c= Ball(x,r+1)
  proof
    let y be set such that
A1: y in cl_Ball(x,r);
    reconsider Y=y as Point of M by A1;
A2: r+0<r+1 by XREAL_1:10;
    dist(x,Y)<=r by A1,METRIC_1:13;
    then dist(x,Y)<r+1 by A2,XXREAL_0:2;
    hence thesis by METRIC_1:12;
  end;
  hence thesis by TBSP_1:21;
end;

theorem Th68:
  for A being Subset of TopSpaceMetr M st A = Sphere(z,r) holds A is closed
proof
  let A be Subset of TopSpaceMetr M such that
A1: A = Sphere(z,r);
  reconsider B = cl_Ball(z,r), C = Ball(z,r) as Subset of TopSpaceMetr M by
TOPMETR:16;
A2: (cl_Ball(z,r))` = B` by TOPMETR:16;
A3: A` = B` \/ C
  proof
    hereby
      let a be set;
      assume
A4:   a in A`;
      then reconsider e = a as Point of M by TOPMETR:16;
      not a in A by A4,XBOOLE_0:def 5;
      then dist(e,z) <> r by A1,METRIC_1:14;
      then dist(e,z) < r or dist(e,z) > r by XXREAL_0:1;
      then e in Ball(z,r) or not e in cl_Ball(z,r) by METRIC_1:12,13;
      then e in Ball(z,r) or e in cl_Ball(z,r)` by SUBSET_1:50;
      hence a in B` \/ C by A2,XBOOLE_0:def 3;
    end;
    let a be set;
    assume
A5: a in B` \/ C;
    then reconsider e = a as Point of M by TOPMETR:16;
    a in B` or a in C by A5,XBOOLE_0:def 3;
    then not e in cl_Ball(z,r) or e in C by XBOOLE_0:def 5;
    then dist(e,z) > r or dist(e,z) < r by METRIC_1:12,13;
    then not a in A by A1,METRIC_1:14;
    hence thesis by A5,SUBSET_1:50;
  end;
A6: C is open by TOPMETR:21;
  B` is open by Lm1;
  hence thesis by A3,A6,TOPS_1:29;
end;

theorem
  A = Sphere(w,r) implies A is closed
proof
A1: the TopStruct of TOP-REAL n = TopSpaceMetr Euclid n by EUCLID:def 8;
  then reconsider E=A as Subset of TopSpaceMetr Euclid n;
  assume A = Sphere(w,r);
  then E is closed by Th68;
  hence A is closed by A1,PRE_TOPC:61;
end;

theorem
  Sphere(z,r) is bounded
proof
  Sphere(z,r) c= cl_Ball(z,r) by METRIC_1:16;
  hence thesis by Th67,TBSP_1:21;
end;

theorem Th71:
  A is Bounded implies Cl A is Bounded
proof
  reconsider D = Cl A as Subset of Euclid n by TOPREAL3:13;
  assume A is bounded Subset of Euclid n;
  then consider r being Real, x being Point of Euclid n such that
  0 < r and
A1: A c= Ball(x,r) by METRIC_6:def 10;
  reconsider B = cl_Ball(x,r) as Subset of TOP-REAL n by TOPREAL3:13;
  Ball(x,r) c= cl_Ball(x,r) by METRIC_1:15;
  then A c= B by A1,XBOOLE_1:1;
  then
A2: Cl A c= Cl B by PRE_TOPC:49;
A3: the TopStruct of TOP-REAL n = TopSpaceMetr Euclid n by EUCLID:def 8;
  then reconsider E=B as Subset of TopSpaceMetr Euclid n;
  E is closed by Th65;
  then B is closed by A3,PRE_TOPC:61;
  then Cl A c= B by A2,PRE_TOPC:52;
  then D is bounded by Th67,TBSP_1:21;
  hence thesis by JORDAN2C:def 2;
end;

theorem
  for M being non empty MetrStruct holds M is bounded iff for X being
  Subset of M holds X is bounded
proof
  let M be non empty MetrStruct;
  hereby
    assume
A1: M is bounded;
    let X be Subset of M;
    [#]M is bounded by A1;
    hence X is bounded by TBSP_1:21;
  end;
  assume for X being Subset of M holds X is bounded;
  then [#]M is bounded;
  hence thesis by TBSP_1:25;
end;

theorem Th73:
  for M being Reflexive symmetric triangle (non empty MetrStruct),
  X, Y being Subset of M st the carrier of M = X \/ Y & M is non bounded & X is
  bounded holds Y is non bounded
proof
  let M be Reflexive symmetric triangle (non empty MetrStruct), X, Y be Subset
  of M such that
A1: the carrier of M = X \/ Y and
A2: M is non bounded;
  assume that
A3: X is bounded and
A4: Y is bounded;
  [#]M is bounded by A1,A3,A4,TBSP_1:20;
  hence thesis by A2,TBSP_1:25;
end;

theorem
  for X, Y being Subset of TOP-REAL n st n >= 1 & the carrier of
  TOP-REAL n = X \/ Y & X is Bounded holds Y is non Bounded
proof
  set M = TOP-REAL n;
  let X, Y be Subset of M such that
A1: n >= 1 and
A2: the carrier of M = X \/ Y;
  reconsider E = [#]M as Subset of Euclid n by TOPREAL3:13;
  [#](TOP-REAL n) is non Bounded by A1,JORDAN2C:41;
  then
A3: E is non bounded by JORDAN2C:def 2;
  reconsider D = Y as Subset of Euclid n by TOPREAL3:13;
  assume X is Bounded;
  then reconsider C = X as bounded Subset of Euclid n by JORDAN2C:def 2;
A4: the carrier of Euclid n = C \/ D by A2,TOPREAL3:13;
  E = [#]Euclid n by TOPREAL3:13;
  then Euclid n is non bounded by A3;
  then D is non bounded by A4,Th73;
  hence thesis by JORDAN2C:def 2;
end;

canceled;

theorem Th76:
  A is Bounded & B is Bounded implies A \/ B is Bounded
proof
  assume
A1: A is bounded Subset of Euclid n;
  then reconsider A as Subset of Euclid n;
  assume
A2: B is bounded Subset of Euclid n;
  then reconsider B as Subset of Euclid n;
  reconsider E = A \/ B as Subset of Euclid n;
  E is bounded Subset of Euclid n by A1,A2,TBSP_1:20;
  hence thesis by JORDAN2C:def 2;
end;

begin  :: Topological properties of real numbers subsets

registration
  let X be non empty Subset of REAL;
  cluster Cl X -> non empty;
  coherence by MEASURE6:94,XBOOLE_1:3;
end;

registration
  let D be bounded_below Subset of REAL;
  cluster Cl D -> bounded_below;
  coherence
  proof
    consider p being real number such that
B1:  p is LowerBound of D by XXREAL_2:def 9;
A1: for r being real number st r in D holds p <= r by B1,XXREAL_2:def 2;
    take p;
    let r be ext-real number;
    assume r in Cl D;
    then consider s being Real_Sequence such that
A2: rng s c= D and
A3: s is convergent and
A4: lim s = r by MEASURE6:100;
    for n holds s.n >= p
    proof
      let n;
      dom s = NAT by FUNCT_2:def 1;
      then s.n in rng s by FUNCT_1:def 5;
      hence thesis by A1,A2;
    end;
    hence thesis by A3,A4,PREPOWER:2;
  end;
end;

registration
  let D be bounded_above Subset of REAL;
  cluster Cl D -> bounded_above;
  coherence
  proof
    consider p being real number such that
B1:  p is UpperBound of D by XXREAL_2:def 10;
A1: for r being real number st r in D holds r <= p by B1,XXREAL_2:def 1;
    take p;
    let r be ext-real number;
    assume r in Cl D;
    then consider s being Real_Sequence such that
A2: rng s c= D and
A3: s is convergent and
A4: lim s = r by MEASURE6:100;
    for n holds s.n <= p
    proof
      let n;
      dom s = NAT by FUNCT_2:def 1;
      then s.n in rng s by FUNCT_1:def 5;
      hence thesis by A1,A2;
    end;
    hence thesis by A3,A4,PREPOWER:3;
  end;
end;

theorem Th77:
  for D being non empty bounded_below Subset of REAL holds lower_bound D =
  lower_bound Cl D
proof
  let D be non empty bounded_below Subset of REAL;
A1: for q being real number st for p being real number st p in D holds p >=
  q holds lower_bound Cl D >= q
  proof
    let q be real number such that
A2: for p being real number st p in D holds p >= q;
    for p being real number st p in Cl D holds p >= q
    proof
      let p be real number;
      assume p in Cl D;
      then consider s being Real_Sequence such that
A3:   rng s c= D and
A4:   s is convergent and
A5:   lim s = p by MEASURE6:100;
      for n holds s.n >= q
      proof
        let n;
        dom s = NAT by FUNCT_2:def 1;
        then s.n in rng s by FUNCT_1:def 5;
        hence thesis by A2,A3;
      end;
      hence thesis by A4,A5,PREPOWER:2;
    end;
    hence thesis by SEQ_4:60;
  end;
A6: lower_bound Cl D <= lower_bound D by MEASURE6:94,SEQ_4:64;
  for p being real number st p in D holds p >= lower_bound Cl D
  proof
    let p be real number;
    assume p in D;
    then lower_bound D <= p by SEQ_4:def 5;
    hence thesis by A6,XXREAL_0:2;
  end;
  hence thesis by A1,SEQ_4:61;
end;

theorem Th78:
  for D being non empty bounded_above Subset of REAL holds upper_bound D =
  upper_bound Cl D
proof
  let D be non empty bounded_above Subset of REAL;
A1: for q being real number st for p being real number st p in D holds p <=
  q holds upper_bound Cl D <= q
  proof
    let q be real number such that
A2: for p being real number st p in D holds p <= q;
    for p being real number st p in Cl D holds p <= q
    proof
      let p be real number;
      assume p in Cl D;
      then consider s being Real_Sequence such that
A3:   rng s c= D and
A4:   s is convergent and
A5:   lim s = p by MEASURE6:100;
      for n holds s.n <= q
      proof
        let n;
        dom s = NAT by FUNCT_2:def 1;
        then s.n in rng s by FUNCT_1:def 5;
        hence thesis by A2,A3;
      end;
      hence thesis by A4,A5,PREPOWER:3;
    end;
    hence thesis by SEQ_4:62;
  end;
A6: upper_bound Cl D >= upper_bound D by MEASURE6:94,SEQ_4:65;
  for p being real number st p in D holds p <= upper_bound Cl D
  proof
    let p be real number;
    assume p in D;
    then upper_bound D >= p by SEQ_4:def 4;
    hence thesis by A6,XXREAL_0:2;
  end;
  hence thesis by A1,SEQ_4:63;
end;

registration
  cluster R^1 -> T_2;
  coherence by PCOMPS_1:38,TOPMETR:def 7;
end;

Lm2: R^1 = TopStruct (#the carrier of RealSpace,Family_open_set RealSpace#) by
PCOMPS_1:def 6,TOPMETR:def 7;

theorem Th79:
  for A being Subset of REAL, B being Subset of R^1 st A = B holds
  A is closed iff B is closed
proof
  let A be Subset of REAL, B be Subset of R^1 such that
A1: A = B;
  thus A is closed implies B is closed
  proof
    assume A is closed;
    then A`` is closed;
    then A` is open by RCOMP_1:def 5;
    then A` in Family_open_set RealSpace by JORDAN5A:6;
    hence [#]R^1 \ B is open by A1,Lm2,PRE_TOPC:def 5,TOPMETR:24;
  end;
  assume B is closed;
  then B` in the topology of R^1 by PRE_TOPC:def 5;
  then A` is open by A1,Lm2,JORDAN5A:6,TOPMETR:24;
  then A`` is closed by RCOMP_1:def 5;
  hence thesis;
end;

theorem
  for A being Subset of REAL, B being Subset of R^1 st A = B holds Cl A = Cl B
proof
  let A be Subset of REAL, B be Subset of R^1 such that
A1: A = B;
  thus Cl A c= Cl B
  proof
    let a be set;
    assume
A2: a in Cl A;
    for G being Subset of R^1 st G is open & a in G holds B meets G
    proof
      let G be Subset of R^1 such that
A3:   G is open and
A4:   a in G;
      reconsider H = G as Subset of REAL by TOPMETR:24;
      G in Family_open_set RealSpace by A3,Lm2,PRE_TOPC:def 5;
      then H is open by JORDAN5A:6;
      then A /\ H is non empty by A2,A4,MEASURE6:99;
      hence thesis by A1,XBOOLE_0:def 7;
    end;
    hence thesis by A2,PRE_TOPC:def 13,TOPMETR:24;
  end;
  let a be set;
  assume
A5: a in Cl B;
  for O being open Subset of REAL st a in O holds O /\ A is non empty
  proof
    let O be open Subset of REAL such that
A6: a in O;
    reconsider P = O as Subset of R^1 by TOPMETR:24;
    P in Family_open_set RealSpace by JORDAN5A:6;
    then P is open by Lm2,PRE_TOPC:def 5;
    then P meets B by A5,A6,PRE_TOPC:def 13;
    hence thesis by A1,XBOOLE_0:def 7;
  end;
  hence thesis by A5,MEASURE6:99,TOPMETR:24;
end;

theorem Th81:
  for A being Subset of REAL, B being Subset of R^1 st A = B holds
  A is compact iff B is compact
proof
  let A be Subset of REAL, B be Subset of R^1 such that
A1: A = B;
  thus A is compact implies B is compact
  proof
    assume
A2: A is compact;
    per cases;
    suppose
      A = {};
      then reconsider C = B as empty Subset of R^1 by A1;
      C is compact;
      hence thesis;
    end;
    suppose
A3:   A <> {};
      reconsider X = [.lower_bound A,upper_bound A.] as Subset of R^1
      by TOPMETR:24;
A4:   A is bounded by A2,RCOMP_1:28;
      then
A5:   Closed-Interval-TSpace(lower_bound A,upper_bound A) = R^1|X
by A3,SEQ_4:24,TOPMETR:26;
A6:   B is closed by A1,A2,Th79;
A7:   X = {} or X <> {};
      Closed-Interval-TSpace(lower_bound A,upper_bound A) is compact
      by A3,A4,HEINE:11,SEQ_4:24
;
      then X is compact by A7,A5,COMPTS_1:12;
      hence thesis by A1,A6,A4,Th24,COMPTS_1:18;
    end;
  end;
  assume B is compact;
  then [#]B is compact by WEIERSTR:19;
  hence thesis by A1,WEIERSTR:def 3;
end;

registration
  cluster finite -> compact Subset of REAL;
  coherence by Th81,TOPMETR:24;
end;

registration
  let a, b be real number;
  cluster [.a,b.] -> compact Subset of REAL;
  coherence by RCOMP_1:24;
end;

theorem
  for a, b being real number holds a <> b iff Cl ].a,b.[ = [.a,b.]
proof
  let a, b be real number;
  thus a <> b implies Cl ].a,b.[ = [.a,b.]
  proof
    reconsider a1 = a, b1 = b as Real by XREAL_0:def 1;
    assume
A1: a <> b;
    per cases by A1,XXREAL_0:1;
    suppose
A2:   a > b;
      hence Cl ].a,b.[ = {} by MEASURE6:96,XXREAL_1:28
        .= [.a,b.] by A2,XXREAL_1:29;
    end;
    suppose
A3:   a < b;
      then
A4:   (a1+b1)/2 < b1 by XREAL_1:228;
      thus Cl ].a,b.[ c= [.a,b.] by MEASURE6:93,XXREAL_1:25;
      let p be set;
A5:   ].a,b.[ = {w where w is Real : a < w & w < b } by RCOMP_1:def 2;
      assume
A6:   p in [.a,b.];
      [.a,b.] = {w where w is Real : a <= w & w <= b } by RCOMP_1:def 1;
      then consider r being Real such that
A7:   p = r and
A8:   a <= r and
A9:   r <= b by A6;
      a1 < (a1+b1)/2 by A3,XREAL_1:228;
      then
A10:  (a1+b1)/2 in ].a1,b1.[ by A5,A4;
      now
        per cases by A8,A9,XXREAL_0:1;
        case
A11:      a < r & r < b;
A12:      ].a,b.[ c= Cl ].a,b.[ by MEASURE6:94;
          r in ].a,b.[ by A5,A11;
          hence thesis by A7,A12;
        end;
        case
A13:      a = r;
          for O being open Subset of REAL st a in O holds O /\ ].a,b.[ is
          non empty
          proof
            let O be open Subset of REAL;
            assume a in O;
            then consider g being real number such that
A14:        0 < g and
A15:        ].a-g,a+g.[ c= O by RCOMP_1:40;
            reconsider g as Real by XREAL_0:def 1;
A16:        a-g < a-0 by A14,XREAL_1:17;
A17:        ]. a-g,a+g.[ = {w where w is Real : a-g < w & w < a+g } by
RCOMP_1:def 2;
            per cases;
            suppose
A18:          a+g < b;
A19:          a+0 < a+g by A14,XREAL_1:8;
              then
A20:          a1 < (a1+(a1+g))/2 by XREAL_1:228;
A21:          (a1+(a1+g))/2 < a1+g by A19,XREAL_1:228;
              then (a+(a+g))/2 < b by A18,XXREAL_0:2;
              then
A22:          (a1+(a1+g))/2 in ].a1,b1.[ by A5,A20;
              a-g < (a+(a+g))/2 by A16,A20,XXREAL_0:2;
              then (a1+(a1+g))/2 in ].a1-g,a1+g.[ by A17,A21;
              hence thesis by A15,A22,XBOOLE_0:def 4;
            end;
            suppose
A23:          a+g >= b;
              (a1+b1)/2 < b1 by A3,XREAL_1:228;
              then
A24:          (a+b)/2 < a+g by A23,XXREAL_0:2;
              a1 < (a1+b1)/2 by A3,XREAL_1:228;
              then a-g < (a+b)/2 by A16,XXREAL_0:2;
              then (a1+b1)/2 in ].a1-g,a1+g.[ by A17,A24;
              hence thesis by A10,A15,XBOOLE_0:def 4;
            end;
          end;
          hence thesis by A7,A13,MEASURE6:99;
        end;
        case
A25:      b = r;
          for O being open Subset of REAL st b in O holds O /\ ].a,b.[ is
          non empty
          proof
            let O be open Subset of REAL;
            assume b in O;
            then consider g being real number such that
A26:        0 < g and
A27:        ].b-g,b+g.[ c= O by RCOMP_1:40;
            reconsider g as Real by XREAL_0:def 1;
A28:        b-g < b-0 by A26,XREAL_1:17;
A29:        b+0 < b+g by A26,XREAL_1:8;
A30:        ]. b-g,b+g.[ = {w where w is Real : b-g < w & w < b+g } by
RCOMP_1:def 2;
            per cases;
            suppose
A31:          b-g > a;
A32:          (b1+(b1-g))/2 < b1 by A28,XREAL_1:228;
A33:          b1-g < (b1+(b1-g))/2 by A28,XREAL_1:228;
              then a < (b+(b-g))/2 by A31,XXREAL_0:2;
              then
A34:          (b1+(b1-g))/2 in ].a1,b1.[ by A5,A32;
              (b1+(b1-g))/2 < b1 by A28,XREAL_1:228;
              then (b+(b-g))/2 < b+g by A29,XXREAL_0:2;
              then (b1+(b1-g))/2 in ].b1-g,b1+g.[ by A30,A33;
              hence thesis by A27,A34,XBOOLE_0:def 4;
            end;
            suppose
A35:          b-g <= a;
              (a1+b1)/2 < b1 by A3,XREAL_1:228;
              then
A36:          (a+b)/2 < b+g by A29,XXREAL_0:2;
              a1 < (a1+b1)/2 by A3,XREAL_1:228;
              then b-g < (a+b)/2 by A35,XXREAL_0:2;
              then (a1+b1)/2 in ].b1-g,b1+g.[ by A30,A36;
              hence thesis by A10,A27,XBOOLE_0:def 4;
            end;
          end;
          hence thesis by A7,A25,MEASURE6:99;
        end;
      end;
      hence thesis;
    end;
  end;
  assume that
A37: Cl ].a,b.[ = [.a,b.] and
A38: a = b;
  [.a,b.] = {a} by A38,XXREAL_1:17;
  hence contradiction by A37,A38,MEASURE6:96,XXREAL_1:28;
end;

registration
  cluster non empty finite bounded Subset of REAL;
  existence
  proof
    reconsider a = {0} as finite Subset of REAL;
    take a;
    thus thesis;
  end;
end;

theorem Th83:
  for T being TopStruct, f being RealMap of T, g being Function of
  T, R^1 st f = g holds f is continuous iff g is continuous
proof
  let T be TopStruct, f be RealMap of T, g be Function of T, R^1 such that
A1: f = g;
  thus f is continuous implies g is continuous
  proof
    assume
A2: for Y being Subset of REAL st Y is closed holds f"Y is closed;
    let P be Subset of R^1 such that
A3: P is closed;
    reconsider R = P as Subset of REAL by TOPMETR:24;
    R is closed by A3,Th79;
    hence thesis by A1,A2;
  end;
  assume
A4: for Y being Subset of R^1 st Y is closed holds g"Y is closed;
  let P be Subset of REAL such that
A5: P is closed;
  reconsider R = P as Subset of R^1 by TOPMETR:24;
  R is closed by A5,Th79;
  hence thesis by A1,A4;
end;

theorem Th84:
  for A, B being Subset of REAL, f being Function of [:R^1,R^1:],
  TOP-REAL 2 st for x, y being Real holds f. [x,y] = <*x,y*> holds f.:[:A,B:] =
  product ((1,2) --> (A,B))
proof
  let A, B be Subset of REAL, f be Function of [:R^1,R^1:], TOP-REAL 2 such
  that
A1: for x, y being Real holds f. [x,y] = <*x,y*>;
  set h = (1,2) --> (A,B);
A2: dom h = {1,2} by FUNCT_4:65;
  thus f.:[:A,B:] c= product h
  proof
    let x be set;
    assume x in f.:[:A,B:];
    then consider a being set such that
A3: a in the carrier of [:R^1,R^1:] and
A4: a in [:A,B:] and
A5: f.a = x by FUNCT_2:115;
    reconsider a as Point of [:R^1,R^1:] by A3;
    consider m, n being set such that
A6: m in A and
A7: n in B and
A8: a = [m,n] by A4,ZFMISC_1:def 2;
    f.a = x by A5;
    then reconsider g = x as Function of Seg 2, REAL by EUCLID:26;
    reconsider m, n as Real by A6,A7;
A9: for y being set st y in dom h holds g.y in h.y
    proof
      let y be set;
A10:  |[m,n]|`1 = m by EUCLID:56;
      assume y in dom h;
      then
A11:  y = 1 or y = 2 by TARSKI:def 2;
A12:  |[m,n]|`2 = n by EUCLID:56;
      f. [m,n] = |[m,n]| by A1;
      hence thesis by A5,A6,A7,A8,A11,A10,A12,FUNCT_4:66;
    end;
    dom g = dom h by A2,FINSEQ_1:4,FUNCT_2:def 1;
    hence thesis by A9,CARD_3:18;
  end;
A13: h.2 = B by FUNCT_4:66;
  let a be set;
  assume a in product h;
  then consider g being Function such that
A14: a = g and
A15: dom g = dom h and
A16: for x being set st x in dom h holds g.x in h.x by CARD_3:def 5;
  2 in dom h by A2,TARSKI:def 2;
  then
A17: g.2 in B by A13,A16;
A18: h.1 = A by FUNCT_4:66;
  1 in dom h by A2,TARSKI:def 2;
  then
A19: g.1 in A by A18,A16;
  then
A20: f. [g.1,g.2] = <*g.1,g.2*> by A1,A17;
A21: now
    let k be set;
    assume k in dom g;
    then k = 1 or k = 2 by A15,TARSKI:def 2;
    hence g.k = <*g.1,g.2*>.k by FINSEQ_1:61;
  end;
A22: dom <*g.1,g.2*> = {1,2} by FINSEQ_1:4,FINSEQ_3:29;
A23: [g.1,g.2] in [:A,B:] by A19,A17,ZFMISC_1:106;
  the carrier of [:R^1,R^1:] = [:the carrier of R^1,the carrier of R^1 :]
  by BORSUK_1:def 5;
  then [g.1,g.2] in the carrier of [:R^1,R^1:] by A19,A17,TOPMETR:24
,ZFMISC_1:106;
  hence thesis by A2,A14,A15,A23,A22,A21,A20,FUNCT_1:9,FUNCT_2:43;
end;

theorem Th85:
  for f being Function of [:R^1,R^1:], TOP-REAL 2 st for x, y
  being Real holds f. [x,y] = <*x,y*> holds f is being_homeomorphism
proof
  reconsider f1 = proj1, f2 = proj2 as Function of TOP-REAL 2, R^1 by
TOPMETR:24;
  let f be Function of [:R^1,R^1:], TOP-REAL 2 such that
A1: for x, y being Real holds f. [x,y] = <*x,y*>;
  thus dom f = [#][:R^1,R^1:] by FUNCT_2:def 1;
A2: the carrier of [:R^1,R^1:] = [:the carrier of R^1,the carrier of R^1:]
  by BORSUK_1:def 5;
  then
A3: dom f = [:the carrier of R^1,the carrier of R^1:] by FUNCT_2:def 1;
  thus
A4: rng f = [#]TOP-REAL 2
  proof
    thus rng f c= [#]TOP-REAL 2;
    let y be set;
    assume y in [#]TOP-REAL 2;
    then consider a, b such that
A5: y = <*a,b*> by EUCLID:55;
A6: f. [a,b] = <*a,b*> by A1;
    [a,b] in dom f by A3,TOPMETR:24,ZFMISC_1:106;
    hence thesis by A5,A6,FUNCT_1:def 5;
  end;
  thus
A7: f is one-to-one
  proof
    let x, y be set;
    assume x in dom f;
    then consider x1, x2 being set such that
A8: x1 in REAL and
A9: x2 in REAL and
A10: x = [x1,x2] by A2,TOPMETR:24,ZFMISC_1:def 2;
    assume y in dom f;
    then consider y1, y2 being set such that
A11: y1 in REAL and
A12: y2 in REAL and
A13: y = [y1,y2] by A2,TOPMETR:24,ZFMISC_1:def 2;
    reconsider x1, x2, y1, y2 as Real by A8,A9,A11,A12;
    assume
A14: f.x = f.y;
A15: f. [y1,y2] = <*y1,y2*> by A1;
A16: f. [x1,x2] = <*x1,x2*> by A1;
    then x1 = y1 by A10,A13,A15,A14,FINSEQ_1:98;
    hence thesis by A10,A13,A16,A15,A14,FINSEQ_1:98;
  end;
A17: now
A18: dom f2 = the carrier of TOP-REAL 2 by FUNCT_2:def 1;
    let x be set;
A19: dom f1 = the carrier of TOP-REAL 2 by FUNCT_2:def 1;
    assume
A20: x in dom (f");
    then consider a, b such that
A21: x = <*a,b*> by EUCLID:55;
    reconsider p = x as Point of TOP-REAL 2 by A20;
A22: [a,b] in dom f by A3,TOPMETR:24,ZFMISC_1:106;
A23: f. [a,b] = <*a,b*> by A1;
    thus f".x = (f qua Function)".x by A4,A7,TOPS_2:def 4
      .= [a,b] by A7,A21,A22,A23,FUNCT_1:54
      .= [p`1,b] by A21,EUCLID:56
      .= [p`1,p`2] by A21,EUCLID:56
      .= [f1.x,p`2] by PSCOMP_1:def 28
      .= [f1.x,f2.x] by PSCOMP_1:def 29
      .= <:f1,f2:>.x by A20,A19,A18,FUNCT_3:69;
  end;
  thus f is continuous
  proof
    let w be Point of [:R^1,R^1:], G be a_neighborhood of f.w;
    reconsider fw = f.w as Point of Euclid 2 by TOPREAL3:13;
    consider x, y being set such that
A24: x in the carrier of R^1 and
A25: y in the carrier of R^1 and
A26: w = [x,y] by A2,ZFMISC_1:def 2;
    reconsider x, y as Real by A24,A25,TOPMETR:24;
    fw in Int G by CONNSP_2:def 1;
    then consider r being real number such that
A27: r > 0 and
A28: Ball(fw,r) c= G by GOBOARD6:8;
    reconsider r as Real by XREAL_0:def 1;
    set A = ].(f.w)`1-r/sqrt 2,(f.w)`1+r/sqrt 2.[, B = ].(f.w)`2-r/sqrt 2,(f.w
    )`2+r/sqrt 2.[;
    reconsider A, B as Subset of R^1 by TOPMETR:24;
A29: f. [x,y] = <*x,y*> by A1;
    then y = (f.w)`2 by A26,EUCLID:56;
    then
A30: y in B by A27,Th20,SQUARE_1:84,XREAL_1:141;
    x = (f.w)`1 by A26,A29,EUCLID:56;
    then x in A by A27,Th20,SQUARE_1:84,XREAL_1:141;
    then
A31: w in [:A,B:] by A26,A30,ZFMISC_1:106;
    take [:A,B:];
A32: B is open by JORDAN6:46;
    A is open by JORDAN6:46;
    then [:A,B:] in Base-Appr [:A,B:] by A32;
    then w in union Base-Appr [:A,B:] by A31,TARSKI:def 4;
    then w in Int [:A,B:] by BORSUK_1:55;
    hence [:A,B:] is a_neighborhood of w by CONNSP_2:def 1;
    product ((1,2)-->(A,B)) c= Ball(fw,r) by Th49;
    then f.:[:A,B:] c= Ball(fw,r) by A1,Th84;
    hence thesis by A28,XBOOLE_1:1;
  end;
A33: f1 is continuous by Th83;
A34: f2 is continuous by Th83;
  dom <:f1,f2:> = the carrier of TOP-REAL 2 by FUNCT_2:def 1;
  then f" = <:f1,f2:> by A4,A7,A17,FUNCT_1:9,TOPS_2:62;
  hence thesis by A33,A34,YELLOW12:41;
end;

theorem
  [:R^1,R^1:], TOP-REAL 2 are_homeomorphic
proof
  defpred P[Element of REAL,Element of REAL,set] means ex c being Element of
  REAL 2 st c = $3 & $3 = <*$1,$2*>;
A1: the carrier of TOP-REAL 2 = REAL 2 by EUCLID:25;
A2: for x, y being Element of REAL ex u being Element of REAL 2 st P[x,y,u]
  proof
    let x, y be Element of REAL;
    take <*x,y*>;
    thus <*x,y*> is Element of REAL 2 by FINSEQ_2:157;
    <*x,y*> in 2-tuples_on REAL by FINSEQ_2:157;
    hence thesis;
  end;
  consider f being Function of [:REAL,REAL:],REAL 2 such that
A3: for x, y being Element of REAL holds P[x,y,f.(x,y)] from BINOP_1:sch
  3(A2);
  the carrier of [:R^1,R^1:] = [:the carrier of R^1,the carrier of R^1 :]
  by BORSUK_1:def 5;
  then reconsider f as Function of [:R^1,R^1:], TOP-REAL 2 by A1,TOPMETR:24;
  take f;
  for x, y being Real holds f. [x,y] = <*x,y*>
  proof
    let x, y be Real;
    P[x,y,f.(x,y)] by A3;
    hence thesis;
  end;
  hence thesis by Th85;
end;

begin  :: Bounded subsets

theorem Th87:
  for A, B being compact Subset of REAL holds product ((1,2) --> (
  A,B)) is compact Subset of TOP-REAL 2
proof
  defpred P[Element of REAL,Element of REAL,set] means ex c being Element of
  REAL 2 st c = $3 & $3 = <*$1,$2*>;
  let A, B be compact Subset of REAL;
  reconsider X = product ((1,2) --> (A,B)) as Subset of TOP-REAL 2 by Th30;
  reconsider A1 = A, B1 = B as Subset of R^1 by TOPMETR:24;
A1: the carrier of TOP-REAL 2 = REAL 2 by EUCLID:25;
A2: for x, y being Element of REAL ex u being Element of REAL 2 st P[x,y,u]
  proof
    let x, y be Element of REAL;
    take <*x,y*>;
    thus <*x,y*> is Element of REAL 2 by FINSEQ_2:157;
    <*x,y*> in 2-tuples_on REAL by FINSEQ_2:157;
    hence thesis;
  end;
  consider h being Function of [:REAL,REAL:],REAL 2 such that
A3: for x, y being Element of REAL holds P[x,y,h.(x,y)] from BINOP_1:sch
  3(A2);
  the carrier of [:R^1,R^1:] = [:the carrier of R^1,the carrier of R^1 :]
  by BORSUK_1:def 5;
  then reconsider h as Function of [:R^1,R^1:], TOP-REAL 2 by A1,TOPMETR:24;
A4: for x, y being Real holds h. [x,y] = <*x,y*>
  proof
    let x, y be Real;
    P[x,y,h.(x,y)] by A3;
    hence thesis;
  end;
  then
A5: h is being_homeomorphism by Th85;
A6: B1 is compact by Th81;
  A1 is compact by Th81;
  then
A7: [:A1,B1:] is compact by A6,BORSUK_3:27;
  h.:[:A1,B1:] = X by A4,Th84;
  hence thesis by A7,A5,WEIERSTR:14;
end;

theorem Th88:
  P is Bounded closed implies P is compact
proof
  assume
A1: P is Bounded closed;
  then reconsider C = P as bounded Subset of Euclid 2 by JORDAN2C:def 2;
  consider r being Real, e such that
  0 < r and
A2: C c= Ball(e,r) by METRIC_6:def 10;
  reconsider p = e as Point of TOP-REAL 2 by TOPREAL3:13;
A3: ].p`2-r,p`2+r.[ c= [.p`2-r,p`2+r .] by XXREAL_1:25;
A4: Ball(e,r) c= product ((1,2)-->(].p`1-r,p`1+r.[,].p`2-r,p`2+r.[)) by Th50;
  ].p`1-r,p`1+r.[ c= [.p`1-r,p`1+r.] by XXREAL_1:25;
  then
  product ((1,2)-->(].p`1-r,p`1+r.[,].p`2-r,p`2+r.[)) c= product ((1,2)-->
  ([.p`1-r,p`1+r.],[.p`2-r,p`2+r.])) by A3,Th29;
  then
A5: Ball(e,r) c= product ((1,2)-->([.p`1-r,p`1+r.],[.p`2-r,p`2+r.])) by A4,
XBOOLE_1:1;
  product ((1,2)-->([.p`1-r,p`1+r.],[.p`2-r,p`2+r.])) is compact Subset of
  TOP-REAL 2 by Th87;
  hence thesis by A1,A2,A5,COMPTS_1:18,XBOOLE_1:1;
end;

theorem Th89:
  P is Bounded implies for g being continuous RealMap of TOP-REAL
  2 holds Cl(g.:P) c= g.:Cl P
proof
  assume P is Bounded;
  then
A1: Cl P is compact by Th71,Th88;
  let g be continuous RealMap of TOP-REAL 2;
  reconsider f = g as Function of TOP-REAL 2, R^1 by TOPMETR:24;
  f is continuous by Th83;
  then f.:Cl P is closed by A1,COMPTS_1:16,WEIERSTR:15;
  then
A2: g.:Cl P is closed by Th79;
  g.:P c= g.:Cl P by PRE_TOPC:48,RELAT_1:156;
  hence thesis by A2,MEASURE6:93;
end;

theorem Th90:
  proj1.:Cl P c= Cl(proj1.:P)
proof
  let y be set;
  assume y in proj1.:Cl P;
  then consider x being set such that
A1: x in the carrier of TOP-REAL 2 and
A2: x in Cl P and
A3: y = proj1.x by FUNCT_2:115;
  reconsider x as Point of TOP-REAL 2 by A1;
  set r = proj1.x;
  for O being open Subset of REAL st y in O holds O /\ proj1.:P is non empty
  proof
    reconsider e = x as Point of Euclid 2 by TOPREAL3:13;
    let O be open Subset of REAL;
    assume y in O;
    then consider g being real number such that
A4: 0 < g and
A5: ].r-g,r+g.[ c= O by A3,RCOMP_1:40;
    reconsider g as Real by XREAL_0:def 1;
    reconsider B = Ball(e,g/2) as Subset of TOP-REAL 2 by TOPREAL3:13;
A6: B is open by GOBOARD6:6;
    e in B by A4,TBSP_1:16,XREAL_1:141;
    then P meets B by A2,A6,TOPS_1:39;
    then P /\ B <> {} by XBOOLE_0:def 7;
    then consider d being Point of TOP-REAL 2 such that
A7: d in P /\ B by SUBSET_1:10;
A8: d in B by A7,XBOOLE_0:def 4;
    then x`1-g/2 < d`1 by Th47;
    then
A9: r-g/2 < d`1 by PSCOMP_1:def 28;
    d`1 < x`1+g/2 by A8,Th47;
    then
A10: d`1 < r+g/2 by PSCOMP_1:def 28;
    d in P by A7,XBOOLE_0:def 4;
    then proj1.d in proj1.:P by FUNCT_2:43;
    then
A11: d`1 in proj1.:P by PSCOMP_1:def 28;
A12: g/2 < g/1 by A4,XREAL_1:78;
    then r-g < r-g/2 by XREAL_1:17;
    then
A13: r-g < d`1 by A9,XXREAL_0:2;
    r+g/2 < r+g by A12,XREAL_1:8;
    then
A14: d`1 < r+g by A10,XXREAL_0:2;
    ].r-g,r+g.[ = {t where t is Real: r-g < t & t < r+g} by RCOMP_1:def 2;
    then d`1 in ].r-g,r+g.[ by A13,A14;
    hence thesis by A5,A11,XBOOLE_0:def 4;
  end;
  hence thesis by A3,MEASURE6:99;
end;

theorem Th91:
  proj2.:Cl P c= Cl(proj2.:P)
proof
  let y be set;
  assume y in proj2.:Cl P;
  then consider x being set such that
A1: x in the carrier of TOP-REAL 2 and
A2: x in Cl P and
A3: y = proj2.x by FUNCT_2:115;
  reconsider x as Point of TOP-REAL 2 by A1;
  set r = proj2.x;
  for O being open Subset of REAL st y in O holds O /\ proj2.:P is non empty
  proof
    reconsider e = x as Point of Euclid 2 by TOPREAL3:13;
    let O be open Subset of REAL;
    assume y in O;
    then consider g being real number such that
A4: 0 < g and
A5: ].r-g,r+g.[ c= O by A3,RCOMP_1:40;
    reconsider g as Real by XREAL_0:def 1;
    reconsider B = Ball(e,g/2) as Subset of TOP-REAL 2 by TOPREAL3:13;
A6: B is open by GOBOARD6:6;
    e in B by A4,TBSP_1:16,XREAL_1:141;
    then P meets B by A2,A6,TOPS_1:39;
    then P /\ B <> {} by XBOOLE_0:def 7;
    then consider d being Point of TOP-REAL 2 such that
A7: d in P /\ B by SUBSET_1:10;
A8: d in B by A7,XBOOLE_0:def 4;
    then x`2-g/2 < d`2 by Th48;
    then
A9: r-g/2 < d`2 by PSCOMP_1:def 29;
    d`2 < x`2+g/2 by A8,Th48;
    then
A10: d`2 < r+g/2 by PSCOMP_1:def 29;
    d in P by A7,XBOOLE_0:def 4;
    then proj2.d in proj2.:P by FUNCT_2:43;
    then
A11: d`2 in proj2.:P by PSCOMP_1:def 29;
A12: g/2 < g/1 by A4,XREAL_1:78;
    then r-g < r-g/2 by XREAL_1:17;
    then
A13: r-g < d`2 by A9,XXREAL_0:2;
    r+g/2 < r+g by A12,XREAL_1:8;
    then
A14: d`2 < r+g by A10,XXREAL_0:2;
    ].r-g,r+g.[ = {t where t is Real: r-g < t & t < r+g} by RCOMP_1:def 2;
    then d`2 in ].r-g,r+g.[ by A13,A14;
    hence thesis by A5,A11,XBOOLE_0:def 4;
  end;
  hence thesis by A3,MEASURE6:99;
end;

theorem Th92:
  P is Bounded implies Cl(proj1.:P) = proj1.:Cl P
proof
  assume P is Bounded;
  hence Cl(proj1.:P) c= proj1.:Cl P by Th89;
  thus thesis by Th90;
end;

theorem Th93:
  P is Bounded implies Cl(proj2.:P) = proj2.:Cl P
proof
  assume P is Bounded;
  hence Cl(proj2.:P) c= proj2.:Cl P by Th89;
  thus thesis by Th91;
end;

theorem
  D is Bounded implies W-bound D = W-bound Cl D
proof
A1: D c= Cl D by PRE_TOPC:48;
  assume
A2: D is Bounded;
  then Cl D is compact by Th71,Th88;
  then proj1.:Cl D is bounded_below;
  then proj1.:D is bounded_below by A1,RELAT_1:156,XXREAL_2:44;
  then
A3: lower_bound (proj1.:D) = lower_bound Cl(proj1.:D) by Th77
    .= lower_bound (proj1.:Cl D) by A2,Th92;
  W-bound D = lower_bound (proj1.:D) by SPRECT_1:48;
  hence thesis by A3,SPRECT_1:48;
end;

theorem
  D is Bounded implies E-bound D = E-bound Cl D
proof
A1: D c= Cl D by PRE_TOPC:48;
  assume
A2: D is Bounded;
  then Cl D is compact by Th71,Th88;
  then proj1.:Cl D is bounded_above;
  then proj1.:D is bounded_above by A1,RELAT_1:156,XXREAL_2:43;
  then
A3: upper_bound (proj1.:D) = upper_bound Cl(proj1.:D) by Th78
    .= upper_bound (proj1.:Cl D) by A2,Th92;
  E-bound D = upper_bound (proj1.:D) by SPRECT_1:51;
  hence thesis by A3,SPRECT_1:51;
end;

theorem
  D is Bounded implies N-bound D = N-bound Cl D
proof
A1: D c= Cl D by PRE_TOPC:48;
  assume
A2: D is Bounded;
  then Cl D is compact by Th71,Th88;
  then proj2.:Cl D is bounded_above;
  then proj2.:D is bounded_above by A1,RELAT_1:156,XXREAL_2:43;
  then
A3: upper_bound (proj2.:D) = upper_bound Cl(proj2.:D) by Th78
    .= upper_bound (proj2.:Cl D) by A2,Th93;
  N-bound D = upper_bound (proj2.:D) by SPRECT_1:50;
  hence thesis by A3,SPRECT_1:50;
end;

theorem
  D is Bounded implies S-bound D = S-bound Cl D
proof
A1: D c= Cl D by PRE_TOPC:48;
  assume
A2: D is Bounded;
  then Cl D is compact by Th71,Th88;
  then proj2.:Cl D is bounded_below;
  then proj2.:D is bounded_below by A1,RELAT_1:156,XXREAL_2:44;
  then
A3: lower_bound (proj2.:D) = lower_bound Cl(proj2.:D) by Th77
    .= lower_bound (proj2.:Cl D) by A2,Th93;
  S-bound D = lower_bound (proj2.:D) by SPRECT_1:49;
  hence thesis by A3,SPRECT_1:49;
end;
 
:: Moved from JORDAN1I, AK, 23.02.2006

theorem Th98:
  for A,B being Subset of TOP-REAL n holds A is Bounded or B is
  Bounded implies A /\ B is Bounded
proof
  let A,B be Subset of TOP-REAL n;
  assume
A1: A is Bounded or B is Bounded;
  per cases by A1;
  suppose
    A is Bounded;
    hence thesis by JORDAN2C:16,XBOOLE_1:17;
  end;
  suppose
    B is Bounded;
    hence thesis by JORDAN2C:16,XBOOLE_1:17;
  end;
end;

theorem
  for A,B being Subset of TOP-REAL n holds A is not Bounded & B is
  Bounded implies A \ B is not Bounded
proof
  let A,B be Subset of TOP-REAL n;
  assume that
A1: A is not Bounded and
A2: B is Bounded;
A3: (A \ B) \/ A /\ B = A \ (B \ B) by XBOOLE_1:52
    .= A \ {} by XBOOLE_1:37
    .= A;
  A /\ B is Bounded by A2,Th98;
  hence thesis by A1,A3,Th76;
end;

begin :: Addenda
::form GOBRD14, 2006.03.26, A.T.

definition
  let n be Element of NAT, a, b be Point of TOP-REAL n;
  func dist(a,b) -> Real means
  :Def1:
  ex p, q being Point of Euclid n st p = a & q = b & it = dist(p,q);
  existence
  proof
    reconsider p = a, q = b as Point of Euclid n by TOPREAL3:13;
    take dist(p,q);
    thus thesis;
  end;
  uniqueness;
  commutativity;
end;

reserve r1,r2,s1,s2 for real number;

theorem Th100:
  for u,v being Point of Euclid 2 st u = |[r1,s1]| & v = |[r2,s2
  ]| holds dist(u,v) =sqrt ((r1 - r2)^2 + (s1 - s2)^2)
proof
  let u,v be Point of Euclid 2 such that
A1: u = |[r1,s1]| and
A2: v = |[r2,s2]|;
A3: |[r1,s1]|`1 = r1 by EUCLID:56;
A4: |[r2,s2]|`2 = s2 by EUCLID:56;
A5: |[r2,s2]|`1 = r2 by EUCLID:56;
A6: |[r1,s1]|`2 = s1 by EUCLID:56;
  thus dist(u,v) = (Pitag_dist 2).(u,v) by METRIC_1:def 1
    .= sqrt ((r1 - r2)^2 + (s1 - s2)^2) by A1,A2,A3,A6,A5,A4,TOPREAL3:12;
end;

theorem Th101:
  dist(p,q) = sqrt ((p`1-q`1)^2 + (p`2-q`2)^2)
proof
A1: p = |[p`1, p`2]| by EUCLID:57;
A2: q = |[q`1, q`2]| by EUCLID:57;
  ex a, b being Point of Euclid 2 st p = a & q = b & dist(a,b) = dist(p,q)
  by Def1;
  hence thesis by A1,A2,Th100;
end;

theorem
  for p being Point of TOP-REAL n holds dist(p,p) = 0
proof
  let p be Point of TOP-REAL n;
  ex a, b being Point of Euclid n st a = p & b = p & dist(a,b) = dist(p,p)
  by Def1;
  hence thesis by METRIC_1:1;
end;

theorem
  for p, q, r being Point of TOP-REAL n holds dist(p,r) <= dist (p,q) +
  dist(q,r)
proof
  let p, q, r be Point of TOP-REAL n;
A1: ex a, b being Point of Euclid n st a = p & b = r & dist(a,b) = dist(p,r
  ) by Def1;
A2: ex a, b being Point of Euclid n st a = q & b = r & dist(a,b) = dist(q,r)
  by Def1;
  ex a, b being Point of Euclid n st a = p & b = q & dist(a,b) = dist(p,q
  ) by Def1;
  hence thesis by A1,A2,METRIC_1:4;
end;

theorem
  for x1, x2, y1, y2 being real number, a, b being Point of TOP-REAL 2
  st x1 <= a`1 & a`1 <= x2 & y1 <= a`2 & a`2 <= y2 & x1 <= b`1 & b`1 <= x2 & y1
  <= b`2 & b`2 <= y2 holds dist(a,b) <= (x2-x1) + (y2-y1)
proof
  let x1, x2, y1, y2 be real number, a, b be Point of TOP-REAL 2 such that
A1: x1 <= a`1 and
A2: a`1 <= x2 and
A3: y1 <= a`2 and
A4: a`2 <= y2 and
A5: x1 <= b`1 and
A6: b`1 <= x2 and
A7: y1 <= b`2 and
A8: b`2 <= y2;
A9: y2 is Real by XREAL_0:def 1;
  y1 is Real by XREAL_0:def 1;
  then
A10: abs(a`2-b`2) <= y2 - y1 by A3,A4,A7,A8,A9,JGRAPH_1:31;
A11: (a`1-b`1)^2 >= 0 by XREAL_1:65;
A12: (a`2-b`2)^2 >= 0 by XREAL_1:65;
  dist(a,b) = sqrt ((a`1-b`1)^2 + (a`2-b`2)^2) by Th101;
  then dist(a,b) <= sqrt(a`1-b`1)^2 + sqrt(a`2-b`2)^2 by A11,A12,Th6;
  then dist(a,b) <= abs(a`1-b`1) + sqrt(a`2-b`2)^2 by COMPLEX1:158;
  then
A13: dist(a,b) <= abs(a`1-b`1) + abs(a`2-b`2) by COMPLEX1:158;
A14: x2 is Real by XREAL_0:def 1;
  x1 is Real by XREAL_0:def 1;
  then abs(a`1-b`1) <= x2 - x1 by A1,A2,A5,A6,A14,JGRAPH_1:31;
  then abs(a`1-b`1) + abs(a`2-b`2) <= (x2 - x1) + (y2 - y1) by A10,XREAL_1:9;
  hence thesis by A13,XXREAL_0:2;
end;
