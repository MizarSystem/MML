:: Schemes
::  by Stanis\l aw T. Czuba
::
:: Received December 17, 1990
:: Copyright (c) 1990 Association of Mizar Users

environ


begin

reserve a,b,d for set;

scheme Schemat0 {P[set]} :
   ex a st P[a]
    provided
A1: for a holds P[a]
proof
  consider a;
  P[a] by A1;
 hence thesis;
end;

scheme Schemat3 {S[set,set]} :
   for b ex a st S[a,b]
    provided
A1: ex a st for b holds S[a,b]
proof
  consider a such that
A2:for b holds S[a,b] by A1;
  now let b;
    S[a,b] by A2;
   hence ex a st S[a,b];
  end;
 hence thesis;
end;

scheme Schemat8 {P[set],Q[set]} :
 (for a holds P[a]) implies for a holds Q[a]
    provided
A1: for a holds P[a] implies Q[a]
proof
  assume
A2:for a holds P[a];
   now let a;
     (P[a] implies Q[a]) & P[a] by A1,A2;
    hence Q[a];
   end;
  hence thesis;
end;

scheme Schemat9 {P[set],Q[set]} :
   (for a holds P[a]) iff for a holds Q[a]
    provided
A1: for a holds P[a] iff Q[a]
proof
 thus (for a holds P[a]) implies for a holds Q[a]
  proof
   assume
A2: for a holds P[a];
    now let a;
      (P[a] iff Q[a]) & P[a] by A1,A2;
     hence Q[a];
    end;
   hence thesis;
  end;
 assume A3:for a holds Q[a];
  now let a;
    (P[a] iff Q[a]) & Q[a] by A1,A3;
   hence P[a];
  end;
 hence thesis;
end;

scheme Schemat17 {P[set],T[]} :
   (for a holds P[a]) implies T[]
    provided
A1: for a holds P[a] implies T[]
proof
 assume A2:for a holds P[a];
  now let a;
    P[a] by A2;
   hence T[] by A1;
  end;
 hence thesis;
end;

scheme Schemat18a {P[set],Q[set]} :
   ex a st for b holds P[a] or Q[b]
    provided
A1: (ex a st P[a]) or for b holds Q[b]
proof
  now
A2: now
     given a such that
A3:   P[a];
      for b holds P[a] or Q[b] by A3;
     hence thesis;
    end;
    now
     assume
A4:   for b holds Q[b];
      consider a;
      for b holds P[a] or Q[b] by A4;
     hence thesis;
    end;
   hence thesis by A1,A2;
  end;
 hence thesis;
end;

scheme Schemat18b {P[set],Q[set]} :
   (ex a st P[a]) or for b holds Q[b]
    provided
A1: ex a st for b holds P[a] or Q[b]
proof
  consider a such that
A2:for b holds P[a] or Q[b] by A1;
  (ex b st not Q[b]) implies P[a] by A2;
 hence thesis;
end;

scheme Schemat20b {P[set],Q[set]} :
   ex a st for b holds P[a] or Q[b]
    provided
A1: for b ex a st P[a] or Q[b]
proof
A2: (ex a st P[a]) or for b holds Q[b] by A1;
  ex a st for b holds P[a] or Q[b] from Schemat18a(A2);
 hence thesis;
end;

scheme Schemat22a {P[set],Q[set]} :
   for b ex a st P[a] & Q[b]
    provided
A1: (ex a st P[a]) & for b holds Q[b]
proof
  now let b;
A2: Q[b] by A1;
    consider a such that A3:P[a] by A1;
   thus ex a st P[a] & Q[b] by A2,A3;
  end;
 hence thesis;
end;

scheme Schemat22b {P[set],Q[set]} :
   (ex a st P[a]) & for b holds Q[b]
    provided
A1: for b ex a st P[a] & Q[b]
proof
 assume
A2:(for a holds not P[a]) or ex b st not Q[b];
A3:now
    given b such that
A4:  not Q[b];
     now let d;
      assume d = b;
       ex a st P[a] & Q[b] by A1;
      hence contradiction by A4;
      end;
    hence thesis;
   end;
   now
    assume
A5:  for a holds not P[a];
     now let b;
      consider a such that
A6:   P[a] & Q[b] by A1;
     thus thesis by A5,A6;
    end;
   hence thesis;
  end;
 hence thesis by A2,A3;
end;

scheme Schemat23b {P[set],Q[set]} :
   ex a st for b holds P[a] & Q[b]
    provided
A1: for b ex a st P[a] & Q[b]
proof
A2: for b ex a st P[a] & Q[b] by A1;
   (ex a st P[a]) & for b holds Q[b] from Schemat22b(A2);
  hence thesis;
end;

scheme Schemat28 {S[set,set]} :
   ex b st for a holds S[a,b]
    provided
A1: for a,b holds S[a,b]
proof
  now let b;
    for a holds S[a,b] by A1;
   hence thesis;
  end;
 hence thesis;
end;

scheme Schemat30 {S[set,set]} :
   ex a st S[a,a]
    provided
A1: ex a st for b holds S[a,b]
proof
  consider a such that
A2:for b holds S[a,b] by A1;
   S[a,a] by A2;
  hence thesis;
end;

scheme Schemat31 {S[set,set]} :
   for a ex b st S[b,a]
    provided
A1: for a holds S[a,a]
proof
  let a;
   S[a,a] by A1;
  hence thesis;
end;

scheme Schemat33 {S[set,set]} :
   for a ex b st S[a,b]
    provided
A1: for a holds S[a,a]
proof
  let a;
   S[a,a] by A1;
  hence thesis;
end;

scheme Schemat36 {S[set,set]} :
   ex a,b st S[a,b]
    provided
A1: for b ex a st S[a,b]
proof
  consider b;
   ex a st S[a,b] by A1;
  hence thesis;
end;

