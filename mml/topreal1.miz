:: The Topological Space ${\calE}^2_{\rm T}$.
:: Arcs, Line Segments and Special Polygonal Arcs
::  by Agata Darmochwa{\l} and Yatsuka Nakamura
::
:: Received November 21, 1991
:: Copyright (c) 1991 Association of Mizar Users

environ

 vocabularies EUCLID, METRIC_1, PCOMPS_1, BOOLE, FINSEQ_1, RELAT_1, FUNCT_1,
      PRE_TOPC, BORSUK_1, TOPS_2, RCOMP_1, SUBSET_1, MCART_1, ARYTM_1, ARYTM_3,
      CONNSP_2, TOPS_1, TOPMETR, COMPLEX1, ABSVALUE, ORDINAL2, COMPTS_1,
      TARSKI, SETFAM_1, TOPREAL1, FINSEQ_4, ARYTM, JORDAN1A, NAT_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, SETFAM_1, ORDINAL1, NUMBERS, XCMPLX_0,
      XREAL_0, COMPLEX1, REAL_1, NAT_1, NAT_D, RELAT_1, FUNCT_1, FUNCT_2,
      TOPS_1, TOPS_2, CONNSP_2, COMPTS_1, RCOMP_1, FINSEQ_1, FINSEQ_4,
      STRUCT_0, METRIC_1, TOPMETR, PCOMPS_1, EUCLID, PRE_TOPC, XXREAL_0;
 constructors SETFAM_1, XXREAL_0, REAL_1, NAT_1, INT_1, MEMBERED, COMPLEX1,
      RCOMP_1, FINSEQ_4, TOPS_1, TOPS_2, COMPTS_1, EUCLID, TOPMETR, ORDINAL2,
      BINOP_2, FINSEQOP, NAT_D;
 registrations SUBSET_1, PRE_TOPC, RELSET_1, STRUCT_0, EUCLID, BORSUK_1, SEQ_1,
      XREAL_0, METRIC_1, INT_1, TOPMETR, XBOOLE_0, NAT_1, MEMBERED, PCOMPS_1,
      XXREAL_0, ORDINAL1;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, XBOOLE_0, FINSEQ_2, EUCLID;
 theorems BORSUK_1, COMPTS_1, CONNSP_2, ENUMSET1, EUCLID, FINSEQ_1, FUNCT_1,
      FUNCT_2, HEINE, METRIC_1, NAT_1, PCOMPS_1, PRE_TOPC, RCOMP_1, REAL_1,
      REAL_2, TARSKI, TOPMETR, TOPMETR2, TOPS_1, TOPS_2, ZFMISC_1, FINSEQ_3,
      FINSEQ_4, TBSP_1, PARTFUN2, RELSET_1, RELAT_1, INT_1, XBOOLE_0, XBOOLE_1,
      XREAL_0, XCMPLX_1, XREAL_1, COMPLEX1, XXREAL_0, FINSEQ_2;
 schemes NAT_1, CLASSES1, XBOOLE_0;

begin

 reserve r,y1,y2,lambda for Real, i,j,n for Element of NAT;
 reserve a for natural number;

Lm1: |[0,0]| = 0.REAL 2 by FINSEQ_2:75;
Lm2: the carrier of Euclid n = REAL n & the carrier of TOP-REAL n = REAL n
    by TOPMETR:16;

:: PRELIMINARIES

scheme FraenkelAlt {A() -> non empty set, P[set], Q[set]}:
 {v where v is Element of A(): P[v] or Q[v]} =
  {v1 where v1 is Element of A(): P[v1]} \/
   {v2 where v2 is Element of A(): Q[v2]}
proof
 set X = {v where v is Element of A(): P[v] or Q[v]},
     Y = {v1 where v1 is Element of A(): P[v1]},
     Z = {v2 where v2 is Element of A(): Q[v2]};
   now let x be set;
    thus x in X implies x in Y \/ Z
     proof
      assume x in X;
      then consider v being Element of A() such that
     A1: x = v and A2: P[v] or Q[v];
       per cases by A2;
        suppose P[v];
         then x in Y by A1;
         hence x in Y \/ Z by XBOOLE_0:def 2;
        end;
        suppose Q[v];
         then x in Z by A1;
         hence x in Y \/ Z by XBOOLE_0:def 2;
       end;
     end;
    assume A3: x in Y \/ Z;
    per cases by A3,XBOOLE_0:def 2;
      suppose x in Y;
       then ex v being Element of A() st x = v & P[v];
       hence x in X;
      end;
      suppose x in Z;
       then ex v being Element of A() st x = v & Q[v];
       hence x in X;
     end;
   end;
  hence thesis by TARSKI:2;
 end;

:: PROPER TEXT

 reserve p,p1,p2,q1,q2 for Point of TOP-REAL 2,
         P, P1 for Subset of TOP-REAL 2;

reserve T for TopSpace;

definition let T;
   let p1, p2 be Point of T,
       P be Subset of T;
 canceled;

  pred P is_an_arc_of p1,p2 means
   :Def2: ex f being Function of I[01], T|P st
                     f is_homeomorphism & f.0 = p1 & f.1 = p2;
end;

canceled 3;

theorem Th4:
  for P being Subset of T,
      p1,p2 being Point of T
    st P is_an_arc_of p1,p2 holds p1 in P & p2 in P
 proof let P be Subset of T,
 p1,p2 be Point of T;
  assume P is_an_arc_of p1,p2;
  then consider f being Function of I[01], T|P such that
A1: f is_homeomorphism and A2: f.0 = p1 & f.1 = p2 by Def2;
A3:  dom f = [#]I[01] by A1,TOPS_2:def 5
  .= the carrier of I[01] by PRE_TOPC:12;
       1 in [.0,1.] & 0 in [.0,1.] by RCOMP_1:15;
  then A4: p1 in rng f & p2 in rng f by A2,A3,BORSUK_1:83,FUNCT_1:def 5;
      rng f = [#](T|P) by A1,TOPS_2:def 5;
  hence thesis by A4,PRE_TOPC:def 10;
 end;

theorem Th5:
  for T being being_T2 TopSpace
  for P,Q being Subset of T,
  p1,p2,q1 being Point of T
   st P is_an_arc_of p1,p2 & Q is_an_arc_of p2,q1 & P /\ Q = {p2}
   holds P \/ Q is_an_arc_of p1,q1
 proof let T being being_T2 TopSpace;
  let P,Q be Subset of T,
 p1,p2,q1 be Point of T;
  assume that A1:P is_an_arc_of p1,p2 and A2:Q is_an_arc_of p2,q1
        and A3: P /\ Q = {p2};
   consider f1 being Function of I[01], T|P such that
A4:  f1 is_homeomorphism & f1.0 = p1 & f1.1 = p2 by A1,Def2;
   consider f2 being Function of I[01], T|Q such that
A5:  f2 is_homeomorphism & f2.0 = p2 & f2.1 = q1 by A2,Def2;
   consider h being Function of I[01], T|(P \/ Q) such that
A6:  h is_homeomorphism & h.0 = f1.0 & h.1 = f2.1 by A3,A4,A5,TOPMETR2:6;
   take h;
   thus thesis by A4,A5,A6;
 end;

definition let n be Element of NAT; let p1,p2 be Point of TOP-REAL n;
   func LSeg(p1,p2) -> Subset of TOP-REAL n equals
:Def3: { (1-lambda)*p1 + lambda*p2 : 0 <= lambda & lambda <= 1 };
  coherence
   proof
    set A = { (1-lambda)*p1 + lambda*p2 : 0 <= lambda & lambda <= 1 };
    A c= the carrier of TOP-REAL n
     proof let x be set;
      assume x in A;
      then ex lambda st x = (1-lambda)*p1 + lambda*p2 &
                0 <= lambda & lambda <= 1;
      hence thesis;
     end;
    hence thesis;
   end;
end;

definition
  func R^2-unit_square -> Subset of TOP-REAL 2 equals
    ( LSeg(|[0,0]|,|[0,1]|) \/ LSeg(|[0,1]|,|[1,1]|) ) \/
    ( LSeg(|[1,1]|,|[1,0]|) \/ LSeg(|[1,0]|,|[0,0]|) );
  coherence;
end;

registration let n; let p1,p2 be Point of TOP-REAL n;
 cluster LSeg(p1,p2) -> non empty;
 coherence
  proof
    1 - 0 = 1 & 1 * p1 = p1 & 0 * p2 = 0.REAL n & p1 + 0.REAL n = p1
      by EUCLID:31,33;
    then p1 in LSeg(p1,p2);
    hence thesis;
  end;
end;

theorem Th6:
  for p1,p2 being Point of TOP-REAL n holds
    p1 in LSeg(p1,p2) & p2 in LSeg(p1,p2)
 proof let p1,p2 be Point of TOP-REAL n;
      1 - 0 = 1 & 1 - 1 = 0 & 1*p1 = p1 & 0 * p1 = 0.REAL n & 0 * p2 = 0.REAL n
   & p1 + 0.REAL n = p1 & 0.REAL n + p2 = p2 &
   1*p2 = p2 by EUCLID:31,33;
  then p1 in { (1-y1)*p1 + y1*p2 : 0 <= y1 & y1 <= 1 } &
       p2 in { (1-y2)*p1 + y2*p2 : 0 <= y2 & y2 <= 1 };
  hence p1 in LSeg(p1,p2) & p2 in LSeg(p1,p2);
 end;

theorem Th7:
  for p being Point of TOP-REAL n holds LSeg(p,p) = {p}
 proof let p be Point of TOP-REAL n;
      p in LSeg(p,p) by Th6;
  then A1: {p} c= LSeg(p,p) by ZFMISC_1:37;
      LSeg(p,p) c= {p}
   proof
    let a be set; assume a in LSeg(p,p);
     then a in { (1-lambda)*p + lambda*p : 0 <= lambda & lambda <= 1 };
     then consider lambda such that
      A2: a = (1-lambda)*p + lambda*p and 0 <= lambda & lambda <= 1;
          a = ((1-lambda)+lambda)*p by A2,EUCLID:37 .= p by EUCLID:33;
    hence a in {p} by TARSKI:def 1;
   end;
  hence thesis by A1,XBOOLE_0:def 10;
 end;

theorem Th8:
  for n being Element of NAT
  for p1,p2 being Point of TOP-REAL n holds LSeg(p1,p2) = LSeg(p2,p1)
 proof let n be Element of NAT;let p1,p2 be Point of TOP-REAL n;
       for a being set holds a in LSeg(p1,p2) iff a in LSeg(p2,p1)
    proof let a be set;
     thus a in LSeg(p1,p2) implies a in LSeg(p2,p1)
      proof
       assume a in LSeg(p1,p2);
       then a in { (1-lambda)*p1 + lambda*p2 : 0 <= lambda & lambda <= 1 };
       then consider lambda such that
   A1: a = (1-lambda)*p1 + lambda*p2 and
           A2: 0 <= lambda and
           A3: lambda <= 1;
           0 <= 1-lambda & 1-lambda <= 1-0 & 1-0=1 &
            a = lambda*p2 + (1-lambda)*p1 &
            1-(1-lambda)=lambda by A1,A2,A3,REAL_2:106,XREAL_1:50;
          then a in { (1-r)*p2 + r*p1 :  0 <= r & r <= 1 };
          hence a in LSeg(p2,p1);
      end;
     assume a in LSeg(p2,p1);
      then a in { (1-lambda)*p2 + lambda*p1 : 0 <= lambda & lambda <= 1 };
      then consider lambda such that
       A4: a = (1-lambda)*p2 + lambda*p1 and
       A5:0 <= lambda and A6: lambda <= 1;
          0 <= 1-lambda & 1-lambda <= 1-0 & 1-0=1 & a = lambda*p1 + (1-lambda)*
p2 &
       1-(1-lambda)=lambda by A4,A5,A6,REAL_2:106,XREAL_1:50;
     then a in { (1-r)*p1 + r*p2 : 0 <= r & r <= 1 };
     hence a in LSeg(p1,p2);
    end;
   hence thesis by TARSKI:2;
 end;

definition let n be Element of NAT; let p1,p2 be Point of TOP-REAL n;
  redefine func LSeg(p1,p2);
  commutativity by Th8;
end;

Lm3: for p,p1,p2 being Point of TOP-REAL n st p in LSeg(p1,p2)
        holds LSeg(p1,p) c= LSeg(p1,p2)
proof let p,p1,p2 be Point of TOP-REAL n;
assume p in LSeg(p1,p2);
 then p in { (1-r)*p1 + r*p2 : 0 <= r & r <= 1 };
 then consider r such that
A1: p = (1-r)*p1 + r*p2 and
A2: 0 <= r and A3: r <= 1;
 let q be set;
 assume q in LSeg(p1,p);
        then q in { (1-lambda)*p1 + lambda*p : 0 <= lambda & lambda <= 1 };
        then consider b being Real such that
             A4: q = (1-b)*p1 + b*p and
             A5: 0 <= b & b <= 1;
             A6: q = (1-b)*p1 + (b*((1-r)*p1) + b*(r*p2))
                     by A1,A4,EUCLID:36
                   .= (1-b)*p1 + b*((1-r)*p1) + b*(r*p2) by EUCLID:30
                   .= (1-b)*p1 + b*(1-r)*p1 + b*(r*p2) by EUCLID:34
                   .= (1-b + b*(1-r))*p1 + b*(r*p2) by EUCLID:37
                   .= (1-b*r)*p1 + b*r*p2 by EUCLID:34;
                    0 <= b*r & b*r <= 1 by A2,A3,A5,REAL_2:121,140;
        then q in { (1-lambda)*p1 + lambda*p2 : 0 <= lambda & lambda <= 1 }
         by A6;
 hence q in LSeg(p1,p2);
end;

theorem
 p1`1 <= p2`1 & p in LSeg(p1,p2) implies p1`1 <= p`1 & p`1 <= p2`1
 proof
  assume that A1: p1`1 <= p2`1 and A2: p in LSeg(p1,p2);
      p in { (1-lambda)*p1 + lambda*p2 : 0 <= lambda & lambda <= 1 } by A2;
  then consider lambda such that
   A3: p = (1-lambda)*p1 + lambda*p2 and
   A4: 0 <= lambda & lambda <= 1;
  A5: ((1-lambda)*p1)`1 = |[(1-lambda)*p1`1, (1-lambda)*p1`2]|`1 by EUCLID:61
      .= (1-lambda)*p1`1 by EUCLID:56;
  A6: (lambda*p2)`1 = |[lambda*p2`1, lambda*p2`2]|`1 by EUCLID:61
      .= lambda*p2`1 by EUCLID:56;
  A7: p`1 = |[((1-lambda)*p1)`1 + (lambda*p2)`1,
         ((1-lambda)*p1)`2 + (lambda*p2)`2]|`1 by A3,EUCLID:59
    .= (1-lambda)*p1`1 + lambda*p2`1 by A5,A6,EUCLID:56;
        lambda*p1`1 <= lambda*p2`1 by A1,A4,XREAL_1:66;
    then (1-lambda)*p1`1 + lambda*p1`1 <= p`1 by A7,XREAL_1:9;
    hence p1`1 <= p`1;
        0 <= 1-lambda by A4,XREAL_1:50;
    then (1-lambda)*p1`1 <= (1-lambda)*p2`1 by A1,XREAL_1:66;
    then p`1 <= (1-lambda)*p2`1 + lambda*p2`1 by A7,XREAL_1:8;
    hence p`1 <= p2`1;
 end;

theorem
 p1`2 <= p2`2 & p in LSeg(p1,p2) implies p1`2 <= p`2 & p`2 <= p2`2
 proof
  assume that A1: p1`2 <= p2`2 and A2: p in LSeg(p1,p2);
  p in { (1-lambda)*p1 + lambda*p2 : 0 <= lambda & lambda <= 1 } by A2;
  then consider lambda such that
A3: p = (1-lambda)*p1 + lambda*p2 and
A4: 0 <= lambda & lambda <= 1;
A5: ((1-lambda)*p1)`2 = |[(1-lambda)*p1`1, (1-lambda)*p1`2]|`2 by EUCLID:61
      .= (1-lambda)*p1`2 by EUCLID:56;
A6: (lambda*p2)`2 = |[lambda*p2`1, lambda*p2`2]|`2 by EUCLID:61
     .= lambda*p2`2 by EUCLID:56;
A7: p`2 = |[((1-lambda)*p1)`1 + (lambda*p2)`1,
         ((1-lambda)*p1)`2 + (lambda*p2)`2]|`2 by A3,EUCLID:59
    .= (1-lambda)*p1`2 + lambda*p2`2 by A5,A6,EUCLID:56;
        lambda*p1`2 <= lambda*p2`2 by A1,A4,XREAL_1:66;
    then (1-lambda)*p1`2 + lambda*p1`2 <= p`2 by A7,XREAL_1:9;
    hence p1`2 <= p`2;
        0 <= 1-lambda by A4,XREAL_1:50;
    then (1-lambda)*p1`2 <= (1-lambda)*p2`2 by A1,XREAL_1:66;
    then p`2 <= (1-lambda)*p2`2 + lambda*p2`2 by A7,XREAL_1:8;
    hence p`2 <= p2`2;
 end;

theorem Th11:
 for p,p1,p2 being Point of TOP-REAL n st p in LSeg(p1,p2)
   holds LSeg(p1,p2) = LSeg(p1,p) \/ LSeg(p,p2)
proof let p,p1,p2 be Point of TOP-REAL n;
      now assume A1: p in LSeg(p1,p2);
   then p in { (1-r)*p1 + r*p2 : 0 <= r & r <= 1 };
   then consider r such that
A2: p = (1-r)*p1 + r*p2 and
A3: 0 <= r and A4: r <= 1;
   now per cases;
   suppose A5: 0 <> r & r <> 1;
        LSeg(p1,p) c= LSeg(p1,p2) & LSeg(p,p2) c= LSeg(p1,p2) by A1,Lm3;
    then A6: LSeg(p1,p) \/ LSeg(p,p2) c= LSeg(p1,p2) by XBOOLE_1:8;
        LSeg(p1,p2) c= LSeg(p1,p) \/ LSeg(p,p2)
       proof
       now let q be set;
        assume q in LSeg(p1,p2);
        then q in { (1-lambda)*p1 + lambda*p2 : 0 <= lambda & lambda <= 1 };
        then consider b being Real such that
         A7: q = (1-b)*p1 + b*p2 and
         A8: 0 <= b & b <= 1;
         now per cases;
          suppose A9: b <= r;
           A10: 1/r >= 0 by A3,REAL_2:125;
           set x = b*(1/r);
A11:       b*0 <= x by A8,A10,XREAL_1:66;
               x <= r*(1/r) by A9,A10,XREAL_1:66;
           then A12: x <= 1 by A5,XCMPLX_1:107;
               (1-x)*p1 + x*p
             = (1-x)*p1 + (x*((1-r)*p1) + x*(r*p2)) by A2,EUCLID:36
            .= (1-x)*p1 + x*((1-r)*p1) + x*(r*p2) by EUCLID:30
            .= (1-x)*p1 + x*(1-r)*p1 + x*(r*p2) by EUCLID:34
            .= (1-x)*p1 + x*(1-r)*p1 + x*r*p2 by EUCLID:34
            .= ((1-x) + x*(1-r))*p1 + x*r*p2 by EUCLID:37
            .= (1 -x*r)*p1 + b*p2 by A5,XCMPLX_1:110
           .= q by A5,A7,XCMPLX_1:110;
          then q in { (1-lambda)*p1 + lambda*p : 0 <= lambda & lambda <= 1 }
           by A11,A12;
          then q in LSeg(p1,p);
          hence q in LSeg(p1,p) \/ LSeg(p,p2) by XBOOLE_0:def 2;
          end;
          suppose A13: not (b <= r);
         set bp =1-b ,rp=1-r;
           A14: 0 <= bp by A8,XREAL_1:50;
           A15: bp <= rp by A13,REAL_2:105;
              A16: 1-0=1;
           A17:  0 <> rp & rp<>1 by A5;
               r-r=0;
           then 0 <= rp by A4,XREAL_1:11;
           then A18: 1/rp >= 0 by REAL_2:125;
         set x=bp*(1/rp);
A19:       bp*0 <= x by A14,A18,XREAL_1:66;
           x <= rp*(1/rp) by A15,A18,XREAL_1:66;
           then A20: x <= 1 by A17,XCMPLX_1:107;
            (1-x)*p2 + x*p = (1-x)*p2 + (x*((1-rp)*p2) + x*(rp*p1))
             by A2,EUCLID:36
                         .= (1-x)*p2 + x*((1-rp)*p2) + x*(rp*p1) by EUCLID:30
                         .= (1-x)*p2 + x*(1-rp)*p2 + x*(rp*p1) by EUCLID:34
                         .= (1-x)*p2 + x*(1-rp)*p2 + x*rp*p1 by EUCLID:34
                         .= ((1-x) + x*(1-rp))*p2 + x*rp*p1 by EUCLID:37
                         .= (1 -x*rp)*p2 + bp*p1 by A5,A16,XCMPLX_1:110
                         .= (1-bp)*p2 + bp*p1 by A17,XCMPLX_1:110
                         .= q by A7;
          then q in { (1-lambda)*p2 + lambda*p: 0 <= lambda & lambda <= 1 }
           by A19,A20;
          then q in LSeg(p,p2) by Def3;
          hence q in LSeg(p1,p) \/ LSeg(p,p2) by XBOOLE_0:def 2;
         end;
         end;
        hence q in LSeg(p1,p) \/ LSeg(p,p2);
       end;
      hence thesis by TARSKI:def 3;
     end;
    hence thesis by A6,XBOOLE_0:def 10;
   end;
   suppose A21: not (0<>r & r<>1);
   now per cases by A21;
     suppose r = 0;
      then A22: p = 1*p1 + 0.REAL n by A2,EUCLID:33
                 .= p1 + 0.REAL n by EUCLID:33
                 .= p1 by EUCLID:31;
      then LSeg(p1,p) = {p} & p in LSeg(p,p2) by Th6,Th7;
      then LSeg(p1,p) c= LSeg(p,p2) by ZFMISC_1:37;
      hence LSeg(p1,p2) = LSeg(p1,p) \/ LSeg(p,p2) by A22,XBOOLE_1:12;
     end;
     suppose r = 1;
      then A23: p = 0.REAL n + 1*p2 by A2,EUCLID:33
                 .= 0.REAL n + p2 by EUCLID:33
                 .= p2 by EUCLID:31;
      then LSeg(p,p2) = {p} & p in LSeg(p1,p) by Th6,Th7;
      then LSeg(p,p2) c= LSeg(p1,p) by ZFMISC_1:37;
      hence LSeg(p1,p2) = LSeg(p1,p) \/ LSeg(p,p2) by A23,XBOOLE_1:12;
     end;
    end;
    hence thesis;
   end;
   end;
   hence thesis;
  end;
 hence thesis;
end;

theorem Th12:
 for p1,p2,q1,q2 being Point of TOP-REAL n
    st q1 in LSeg(p1,p2) & q2 in LSeg(p1,p2) holds LSeg(q1,q2) c= LSeg(p1,p2)
 proof let p1,p2,q1,q2 be Point of TOP-REAL n;
  assume A1: q1 in LSeg(p1,p2) & q2 in LSeg(p1,p2);
   then A2: LSeg(p1,p2) = LSeg(p1,q1) \/ LSeg(q1,p2) by Th11;
       now per cases by A1,A2,XBOOLE_0:def 2;
   suppose q2 in LSeg(p1,q1);
     then A3: LSeg(q2,q1) c= LSeg(p1,q1) by Lm3;
         LSeg(p1,q1) c= LSeg(p1,p2) by A1,Lm3;
     hence thesis by A3,XBOOLE_1:1;
   end;
   suppose q2 in LSeg(q1,p2);
     then A4: LSeg(q1,q2) c= LSeg(q1,p2) by Lm3;
         LSeg(q1,p2) c= LSeg(p1,p2) by A1,Lm3;
     hence thesis by A4,XBOOLE_1:1;
   end;
   end;
  hence thesis;
 end;

theorem
  for p,q,p1,p2 being Point of TOP-REAL n st
    p in LSeg(p1,p2) & q in LSeg(p1,p2) holds
      LSeg(p1,p2) = LSeg(p1,p) \/ LSeg(p,q) \/ LSeg(q,p2)
proof let p,q,p1,p2 be Point of TOP-REAL n;
 assume A1: p in LSeg(p1,p2) & q in LSeg(p1,p2);
     p1 in LSeg(p1,p2) & p2 in LSeg(p1,p2) by Th6;
 then LSeg(p1,p) c= LSeg(p1,p2) & LSeg(p,q) c= LSeg(p1,p2) &
  LSeg(q,p2) c= LSeg(p1,p2) by A1,Th12;
 then LSeg(p1,p) \/ LSeg(p,q) c= LSeg(p1,p2) & LSeg(q,p2) c= LSeg(p1,p2)
  by XBOOLE_1:8;
then A2: LSeg(p1,p) \/ LSeg(p,q) \/ LSeg(q,p2) c= LSeg(p1,p2) by XBOOLE_1:8;
 A3: LSeg(p1,p2) = LSeg(p1,q) \/ LSeg(q,p2) by A1,Th11;
     now per cases;
  suppose p in LSeg(p1,q);
   hence LSeg(p1,p2) c= LSeg(p1,p) \/ LSeg(p,q) \/ LSeg(q,p2) by A3,Th11;
  end;
  suppose not p in LSeg(p1,q);
   then p in LSeg(q,p2) by A1,A3,XBOOLE_0:def 2;
   then LSeg(q,p2) = LSeg(q,p) \/ LSeg(p,p2) by Th11;
   then A4: LSeg(p,p2) c= LSeg(q,p2) by XBOOLE_1:7;
       LSeg(p1,p2) = LSeg(p1,p) \/ LSeg(p,p2) by A1,Th11;
   then A5: LSeg(p1,p2) c= LSeg(p1,p) \/ LSeg(q,p2) by A4,XBOOLE_1:9;
   A6: LSeg(p1,p) \/ LSeg(q,p2) c= LSeg(p1,p) \/ LSeg(q,p2) \/ LSeg(p,q)
     by XBOOLE_1:7;
       LSeg(p1,p) \/ LSeg(q,p2) \/ LSeg(p,q)
     = LSeg(p1,p) \/ LSeg(p,q) \/ LSeg(q,p2) by XBOOLE_1:4;
   hence LSeg(p1,p2) c= LSeg(p1,p) \/ LSeg(p,q) \/
 LSeg(q,p2) by A5,A6,XBOOLE_1:1;
   end;
   end;
  hence thesis by A2,XBOOLE_0:def 10;
end;

theorem
  for p, p1, p2 being Point of TOP-REAL n st
    p in LSeg(p1,p2) holds LSeg(p1,p) /\ LSeg(p,p2) = {p}
  proof
    let p, p1, p2 be Point of TOP-REAL n;
    assume A1: p in LSeg(p1,p2);
    p in LSeg(p1,p) & p in LSeg(p,p2) by Th6;
    then p in LSeg(p1,p) /\ LSeg(p,p2) by XBOOLE_0:def 3;
    then A2: {p} c= LSeg(p1,p) /\ LSeg(p,p2) by ZFMISC_1:37;
    now assume not LSeg(p1,p) /\ LSeg(p,p2) c= {p};then
    consider y being set such that
A3: y in LSeg(p1,p) /\ LSeg(p,p2)
    & not y in {p} by TARSKI:def 3;
    reconsider q=y as Point of TOP-REAL n by A3;
A4: q in LSeg(p1,p) & q in LSeg(p,p2) by A3,XBOOLE_0:def 3;
    p in {(1-a)*p1+a*p2 where a is Real: 0<=a & a<=1} by A1;then
    consider a being Real such that
A5: p=(1-a)*p1+a*p2 & 0<=a & a<=1;
    q in {(1-d)*p1+d*p where d is Real: 0<=d & d<=1} by A4;
    then consider d being Real such that
A6: q=(1-d)*p1+d*p & 0<=d & d<=1;
A7: q=(1-d)*p1+(d*((1-a)*p1)+d*(a*p2)) by A5,A6,EUCLID:36
    .=(1-d)*p1+d*((1-a)*p1) +d*(a*p2) by EUCLID:30
    .=(1-d)*p1+d*(1-a)*p1 +d*(a*p2) by EUCLID:34
    .=(1-d+d*(1-a))*p1+d*(a*p2) by EUCLID:37
    .=(1-d*a)*p1+d*a*p2 by EUCLID:34;
    now assume d=1;then
      q=(1-1)*p1+p by A6,EUCLID:33
       .=(0.REAL n) +p by EUCLID:33
       .=p by EUCLID:31;
     hence contradiction by A3,TARSKI:def 1;
    end;then
A8: d<1 by A6,REAL_1:def 5;
A9: now assume a=0;then
     p=(1-0)*p1+0.REAL n by A5,EUCLID:33
     .=(1-0)*p1 by EUCLID:31
     .=p1 by EUCLID:33;
     hence contradiction by A3,A4,Th7;
    end;
    q in {(1-e)*p+e*p2 where e is Real: 0<=e & e<=1} by A4;then
    consider e being Real such that
    A10: q=(1-e)*p+e*p2 & 0<=e & e<=1;
A11: q=(1-e)*((1-a)*p1)+(1-e)*(a*p2)+e*p2 by A5,A10,EUCLID:36
    .=(1-e)*(1-a)*p1+(1-e)*(a*p2)+e*p2 by EUCLID:34
    .=(1-e)*(1-a)*p1+(1-e)*a*p2+e*p2 by EUCLID:34
    .=(1-e)*(1-a)*p1+((1-e)*a*p2+e*p2) by EUCLID:30
    .=(1-e)*(1-a)*p1+((1-e)*a+e)*p2 by EUCLID:37;
    set f=(1-e)*a+e;
A12: p-q=(1-a)*p1 +a*p2 -(1-d*a)*p1-d*a*p2 by A7,A5,EUCLID:50
      .=(1-a)*p1-(1-d*a)*p1 +a*p2-d*a*p2 by EUCLID:49
      .=(1-a-(1-d*a))*p1+a*p2-d*a*p2 by EUCLID:54
      .=(d*a-a)*p1-d*a*p2+a*p2 by EUCLID:49
      .=(d*a-a)*p1-(d*a*p2-a*p2) by EUCLID:51
      .=(d*a-a)*p1-(d*a-a)*p2 by EUCLID:54
      .=(d*a-a)*(p1-p2) by EUCLID:53;
      p-q=(1-a)*p1 +a*p2 -(1-f)*p1-f*p2 by A11,A5,EUCLID:50
      .=(1-a)*p1-(1-f)*p1 +a*p2-f*p2 by EUCLID:49
      .=(1-a-(1-f))*p1+a*p2-f*p2 by EUCLID:54
      .=(f-a)*p1-f*p2+a*p2 by EUCLID:49
      .=(f-a)*p1-(f*p2-a*p2) by EUCLID:51
      .=(f-a)*p1-(f-a)*p2 by EUCLID:54
      .=(f-a)*(p1-p2) by EUCLID:53;then
     (f-a)*(p1-p2)-(d*a-a)*(p1-p2)=0.REAL n by A12,EUCLID:46;then
     (f-a-(d*a-a))*(p1-p2)=0.REAL n by EUCLID:54;then
     A13: f-d*a=0 or p1-p2=0.REAL n by EUCLID:35;
A14: ((1-e)*a+e)-d*a=(1-d)*a+(e*(1-a));
     1-d>0 by A8,XREAL_1:52;then
A15: (1-d)*a>0 by A5,A9,REAL_2:129;
     1-a>=0 by A5,XREAL_1:50;then
     e*(1-a)>=0 by A10,REAL_2:121;then
     f-d*a>0+0 by A14,A15,XREAL_1:10;then
     p1=p2 by A13,EUCLID:47;then
     p in {p1} by A1,Th7;then
     p=p1 by TARSKI:def 1;
    hence contradiction by A3,Th7,A4;
  end;
  hence thesis by A2,XBOOLE_0:def 10;
end;

theorem Th15:
  for p1,p2 being Point of TOP-REAL n
   st p1 <> p2 holds LSeg(p1,p2) is_an_arc_of p1,p2
 proof let p1,p2 be Point of TOP-REAL n;
  assume A1: p1 <> p2;
  set P = LSeg(p1,p2);
A2: the carrier of Euclid n = REAL n & the carrier of TOP-REAL n = REAL n
     by Lm2;
   reconsider p1' = p1, p2' = p2 as Element of REAL n by Lm2;
   defpred X[set,set] means
     for x being Real st x = $1 holds $2 = (1-x)*p1 + x*p2;
 A3: for a being set st a in [.0,1.] ex u being set st X[a,u]
   proof
    let a be set; assume a in [.0,1.];
     then reconsider x = a as Real;
     take (1-x)*p1 + x*p2; thus thesis;
   end;
  consider f being Function such that
 A4: dom f = [.0,1.] and
 A5: for a being set st a in [.0,1.] holds X[a,f.a] from CLASSES1:sch 1(A3);
A6:  rng f c= the carrier of (TOP-REAL n)|P
   proof
    let y be set; assume y in rng f;
    then consider x being set such that A7: x in dom f & y = f.x
     by FUNCT_1:def 5;
        x in {r: 0 <= r & r <= 1} by A4,A7,RCOMP_1:def 1;
    then consider r such that A8: r = x & 0 <= r & r <= 1;
        y = (1-r)*p1 + r*p2 by A4,A5,A7,A8;
    then y in { (1-lambda)*p1 + lambda*p2: 0 <= lambda & lambda <= 1 } by A8;
    then y in P;
    then y in [#]((TOP-REAL n)|P) by PRE_TOPC:def 10;
    hence y in the carrier of (TOP-REAL n)|P;
   end;
    then reconsider f as Function of I[01], (TOP-REAL n)|P by A4,BORSUK_1:83
,FUNCT_2:def 1,RELSET_1:11;
A9: rng f c= [#]((TOP-REAL n)|P) by A6,PRE_TOPC:12;
A10: [#]((TOP-REAL n)|P) c= rng f
    proof
     let a be set; assume a in [#]((TOP-REAL n)|P);
     then a in P by PRE_TOPC:def 10;
     then a in { (1-lambda)*p1 + lambda*p2 : 0 <= lambda & lambda <= 1 };
     then consider lambda such that
    A11: a = (1-lambda)*p1 + lambda*p2 & 0 <= lambda & lambda <= 1;
            lambda in { r: 0 <= r & r <= 1} by A11;
    then A12: lambda in dom f by A4,RCOMP_1:def 1;
      then a = f.lambda by A4,A5,A11;
      hence a in rng f by A12,FUNCT_1:def 5;
    end;
A13: [#]I[01] = [.0,1.] by BORSUK_1:83,PRE_TOPC:12;
A14: rng f = [#]((TOP-REAL n)|P) by A9,A10,XBOOLE_0:def 10;
       now let x1,x2 be set; assume
   A15: x1 in dom f & x2 in dom f & f.x1 = f.x2;
     then x1 in {r: 0 <= r & r <= 1} by A4,RCOMP_1:def 1;
     then consider r1 being Real such that
   A16: r1 = x1 & 0 <= r1 & r1 <= 1;
         x2 in {r: 0 <= r & r <= 1} by A4,A15,RCOMP_1:def 1;
     then consider r2 being Real such that
   A17: r2 = x2 & 0 <= r2 & r2 <= 1;
         f.x1 = (1-r1)*p1 + r1*p2 & f.x2 = (1-r2)*p1 + r2*p2 by A4,A5,A15,A16
,A17
;
     then (1-r1)*p1 + r1*p2 + (-r1)*p2 = (1-r2)*p1 + (r2*p2 + (-r1)*p2)
       by A15,EUCLID:30;
     then (1-r1)*p1 + r1*p2 + (-r1)*p2 = (1-r2)*p1 + (r2+(-r1))*p2
      by EUCLID:37;
     then (1-r1)*p1 + (r1*p2 + (-r1)*p2) = (1-r2)*p1 + (r2-r1)*p2 by EUCLID:30
;
     then (1-r1)*p1 + (r1+(-r1))*p2 = (1-r2)*p1 + (r2-r1)*p2 by EUCLID:37;
     then (1-r1)*p1 + 0.REAL n = (1-r2)*p1 + (r2-r1)*p2 by EUCLID:33;
     then (-(1-r2))*p1 + (1-r1)*p1 = (-(1-r2))*p1 + ((1-r2)*p1 + (r2-r1)*p2)
 by EUCLID:31;
     then (-(1-r2))*p1 + (1-r1)*p1 = ((-(1-r2))*p1 + (1-r2)*p1) + (r2-r1)*p2
       by EUCLID:30;
     then (-(1-r2))*p1 + (1-r1)*p1 = (-(1-r2)+ (1-r2))*p1 + (r2-r1)*p2
      by EUCLID:37;
     then (-(1-r2))*p1 + (1-r1)*p1 = 0.REAL n + (r2-r1)*p2 by EUCLID:33;
     then (-(1-r2))*p1 + (1-r1)*p1 = (r2-r1)*p2 by EUCLID:31;
     then ((r2-1)+(1-r1))*p1 = (r2-r1)*p2 by EUCLID:37;
     then r2-r1 = 0 by A1,EUCLID:38;
     hence x1 = x2 by A16,A17;
    end;
then A18: f is one-to-one by FUNCT_1:def 8;
   reconsider PP = P as Subset of Euclid n by A2;
       PP is non empty;
   then reconsider PP = P as non empty Subset of Euclid n;
A19: (TOP-REAL n)|P = TopSpaceMetr((Euclid n)|PP) by TOPMETR:20;
       for W being Point of I[01], G being a_neighborhood of f.W
    ex H being a_neighborhood of W st f.:H c= G
    proof
     let W be Point of I[01], G be a_neighborhood of f.W;
         f.W in Int G by CONNSP_2:def 1;
     then consider Q being Subset of (TOP-REAL n)|P such that
   A20: Q is open & Q c= G & f.W in Q by TOPS_1:54;
         [#]((TOP-REAL n)|P) = P by PRE_TOPC:def 10;
     then the carrier of (TOP-REAL n)|P = P &
      the carrier of (Euclid n)|PP = P by PRE_TOPC:12,TOPMETR:def 2;
     then reconsider Y = f.W as Point of (Euclid n)|PP;
     consider r being real number such that
   A21: r > 0 & Ball(Y,r) c= Q by A19,A20,TOPMETR:22;
     reconsider r as Element of REAL by XREAL_0:def 1;
     set delta = r/(Pitag_dist n).(p1',p2');
     reconsider W' = W as Point of Closed-Interval-MSpace(0,1)
      by BORSUK_1:83,TOPMETR:14;
     reconsider H = Ball(W',delta) as Subset of I[01] by BORSUK_1:83,TOPMETR:14
;
         I[01] = Closed-Interval-TSpace(0,1) & Closed-Interval-TSpace(0,1) =
      TopSpaceMetr(Closed-Interval-MSpace(0,1)) by TOPMETR:27,def 8;
     then A22: H is open by TOPMETR:21;
     reconsider p11=p1 ,p22=p2 as Point of Euclid n by A2;
  A23: (Pitag_dist n).(p1',p2') = dist(p11,p22) by METRIC_1:def 1;
      A24: dist(p11,p22) >= 0 & dist(p11,p22) <> 0 by A1,METRIC_1:2,5;
  then A25: delta > 0 by A21,A23,REAL_2:127;
     then W in H by TBSP_1:16;
     then W in Int H by A22,TOPS_1:55;
     then reconsider H as a_neighborhood of W by CONNSP_2:def 1;
     take H;
     reconsider W1 = W as Real by BORSUK_1:83,TARSKI:def 3;
         P = the carrier of (Euclid n)|PP & P c= the carrier of TOP-REAL n &
      the carrier of TOP-REAL n =
      the carrier of Euclid n by A2,TOPMETR:def 2;
     then reconsider WW' = Y as Point of Euclid n by TARSKI:def 3;
         f.:H c= Ball(Y,r)
      proof
       let a be set; assume a in f.:H;
       then consider u being set such that
      A26: u in dom f & u in H & a = f.u by FUNCT_1:def 12;
       reconsider u1 = u as Real by A4,A26;
       reconsider u' = u as Point of Closed-Interval-MSpace(0,1)
        by A26;
           f.u in rng f & rng f c= the carrier of (TOP-REAL n)|P &
        [#]
((TOP-REAL n)|P) = P by A26,FUNCT_1:def 5,PRE_TOPC:def 10,RELSET_1:12;
       then the carrier of (TOP-REAL n)|P = P &
        the carrier of (Euclid n)|PP = P &
        f.u in the carrier of (TOP-REAL n)|P
        by PRE_TOPC:12,TOPMETR:def 2;
       then reconsider aa = a as Point of (Euclid n)|PP by A26;
           P = the carrier of (Euclid n)|PP & P c= the carrier of TOP-REAL n &
        the carrier of TOP-REAL n =
        the carrier of Euclid n by A2,TOPMETR:def 2;
       then reconsider aa' = aa as Point of Euclid n by TARSKI:def 3;
       reconsider aa1 = aa' as Element of REAL n;
       reconsider WW1 = WW' as Element of REAL n;
      A27: f.W = (1-W1)*p1 + W1*p2 by A5,BORSUK_1:83;
           [#]((TOP-REAL n)|P) c= [#](TOP-REAL n) by PRE_TOPC:def 9;
       then [#]((TOP-REAL n)|P) c= the carrier of TOP-REAL n &
        a in rng f & rng f c= the carrier of (TOP-REAL n)|P
         by A26,FUNCT_1:def 5,PRE_TOPC:12,RELSET_1:12;
       then the carrier of (TOP-REAL n)|P c= the carrier of TOP-REAL n &
        a in the carrier of (TOP-REAL n)|P by PRE_TOPC:12;
       then reconsider a'=a, fW=f.W as Point of TOP-REAL n by TARSKI:def 3;
       reconsider p12 = p1 - p2 as Element of REAL n by Lm2;
           WW1 - aa1 = fW - a'
         .= (1-W1)*p1 + W1*p2 - ((1-u1)*p1 + u1*p2) by A4,A5,A26,A27
         .= W1*p2 + (1-W1)*p1 - (1-u1)*p1 - u1*p2 by EUCLID:50
         .= W1*p2 + ((1-W1)*p1 - (1-u1)*p1) - u1*p2 by EUCLID:49
         .= W1*p2 + ((1-W1)-(1-u1))*p1 - u1*p2 by EUCLID:54
         .= (u1-W1)*p1 + (W1*p2 - u1*p2) by EUCLID:49
         .= (u1-W1)*p1 + (W1-u1)*p2 by EUCLID:54
         .= (u1-W1)*p1 + (-(u1-W1))*p2
         .= (u1-W1)*p1 + -(u1-W1)*p2 by EUCLID:44
         .= (u1-W1)*p1 - (u1-W1)*p2 by EUCLID:45
         .= (u1-W1)*(p1 - p2) by EUCLID:53
         .= (u1-W1)*(p12)
         .= (u1-W1)*(p1' - p2');
   then A28: |. WW1 -aa1 .| = abs(u1-W1)*|.p1' - p2'.| by EUCLID:14;
   A29: dist(W',u') < delta by A26,METRIC_1:12;
       reconsider W'' = W', u'' = u' as Point of RealSpace by TOPMETR:12;
           dist(W',u') = (the distance of Closed-Interval-MSpace(0,1)).(W',u')
        by METRIC_1:def 1 .= (the distance of RealSpace).(W'',u'')
        by TOPMETR:def 1 .= dist(W'',u'') by METRIC_1:def 1;
       then abs(W1-u1) < delta by A29,TOPMETR:15; then abs(-(u1-W1)) < delta;
   then A30: abs(u1-W1) < delta by COMPLEX1:138;
   A31: delta <> 0 & (Pitag_dist n).(p1',p2') <> 0 by A21,A23,A24,REAL_2:127;
       then (Pitag_dist n).(p1',p2') = r/delta by XCMPLX_1:54;
   then A32: |.p1' - p2'.| = r/delta by EUCLID:def 6;
           r/delta > 0 by A21,A25,REAL_2:127;
       then |. WW1 - aa1.| < delta*(r/delta) by A28,A30,A32,XREAL_1:70;
       then |. WW1 - aa1 .| < r by A31,XCMPLX_1:88;
       then (the distance of Euclid n).(WW',aa') < r by EUCLID:def 6;
       then (the distance of (Euclid n)|PP).(Y,aa) < r by TOPMETR:def 1;
       then dist(Y,aa) < r by METRIC_1:def 1;
       hence a in Ball(Y,r) by METRIC_1:12;
      end;
     then f.:H c= Q by A21,XBOOLE_1:1;
     hence f.:H c= G by A20,XBOOLE_1:1;
    end;
then A33: f is continuous by BORSUK_1:def 3;
  take f;
A34: I[01] is compact by HEINE:11,TOPMETR:27;
      TopSpaceMetr(Euclid n) is_T2 by PCOMPS_1:38;
  then TOP-REAL n is_T2;
  then (TOP-REAL n)|P is_T2 by TOPMETR:3;
  hence f is_homeomorphism by A4,A13,A14,A18,A33,A34,COMPTS_1:26;
       0 in [.0,1.] by RCOMP_1:15;
  hence f.0 = (1-0)*p1 + 0 * p2 by A5 .= p1 + 0 * p2
   by EUCLID:33 .= p1 + 0.REAL n by EUCLID:33 .= p1 by EUCLID:31;
     1 in [.0,1.] by RCOMP_1:15;
  hence f.1 = (1-1)*p1 + 1*p2 by A5 .=
 0.REAL n + 1*p2 by EUCLID:33 .= 1*p2 by EUCLID:31
    .= p2 by EUCLID:33;
 end;

registration let n;
 cluster TOP-REAL n -> being_T2;
 coherence by PCOMPS_1:38;
end;

theorem Th16:
  for P being Subset of TOP-REAL n,
      p1,p2,q1 being Point of TOP-REAL n
   st P is_an_arc_of p1,p2 & P /\ LSeg(p2,q1) = {p2}
    holds P \/ LSeg(p2,q1) is_an_arc_of p1,q1
 proof let P be Subset of TOP-REAL n,
   p1,p2,q1 be Point of TOP-REAL n;
  assume that A1: P is_an_arc_of p1,p2 and
              A2: P /\ LSeg(p2,q1) = {p2};
  per cases;
   suppose p2 <> q1;
   then LSeg(p2,q1) is_an_arc_of p2,q1 by Th15;
   hence P \/ LSeg(p2,q1) is_an_arc_of p1,q1 by A1,A2,Th5;
   end;
   suppose A3: p2 = q1;
    then LSeg(p2,q1) = {q1} & q1 in P by A1,Th4,Th7;
   hence P \/ LSeg(p2,q1) is_an_arc_of p1,q1 by A1,A3,ZFMISC_1:46;
  end;
 end;

theorem Th17:
  for P being Subset of TOP-REAL n,
      p1,p2,q1 being Point of TOP-REAL n
   st P is_an_arc_of p2,p1 & LSeg(q1,p2) /\ P = {p2}
    holds LSeg(q1,p2) \/ P is_an_arc_of q1,p1
 proof let P be Subset of TOP-REAL n,
           p1,p2,q1 be Point of TOP-REAL n;
  assume that A1: P is_an_arc_of p2,p1 and
              A2: LSeg(q1,p2) /\ P = {p2};
  per cases;
   suppose p2 <> q1;
   then LSeg(q1,p2) is_an_arc_of q1,p2 by Th15;
   hence LSeg(q1,p2) \/ P is_an_arc_of q1,p1 by A1,A2,Th5;
   end;
   suppose A3: p2 = q1;
    then LSeg(q1,p2) = {q1} & q1 in P by A1,Th4,Th7;
   hence LSeg(q1,p2) \/ P is_an_arc_of q1,p1 by A1,A3,ZFMISC_1:46;
  end;
 end;

theorem
      for p1,p2,q1 being Point of TOP-REAL n
  st (p1 <> p2 or p2 <> q1) & LSeg(p1,p2) /\ LSeg(p2,q1) = {p2}
   holds LSeg(p1,p2) \/ LSeg(p2,q1) is_an_arc_of p1,q1
 proof let p1,p2,q1 be Point of TOP-REAL n;
  assume that A1: p1 <> p2 or p2 <> q1 and
              A2: LSeg(p1,p2) /\ LSeg(p2,q1) = {p2};
  per cases by A1;
   suppose p1 <> p2;
    then LSeg(p1,p2) is_an_arc_of p1,p2 by Th15;
   hence LSeg(p1,p2) \/ LSeg(p2,q1) is_an_arc_of p1,q1 by A2,Th16;
   end;
   suppose p2 <> q1;
    then LSeg(p2,q1) is_an_arc_of p2,q1 by Th15;
   hence LSeg(p1,p2) \/ LSeg(p2,q1) is_an_arc_of p1,q1 by A2,Th17;
  end;
 end;

theorem Th19:
   LSeg(|[ 0,0 ]|, |[ 0,1 ]|) = { p1 : p1`1 = 0 & p1`2 <= 1 & p1`2 >= 0}
 & LSeg(|[ 0,1 ]|, |[ 1,1 ]|) = { p2 : p2`1 <= 1 & p2`1 >= 0 & p2`2 = 1}
 & LSeg(|[ 0,0 ]|, |[ 1,0 ]|) = { q1 : q1`1 <= 1 & q1`1 >= 0 & q1`2 = 0}
 & LSeg(|[ 1,0 ]|, |[ 1,1 ]|) = { q2 : q2`1 = 1 & q2`2 <= 1 & q2`2 >= 0}
proof
  set L1 = { p : p`1 = 0 & p`2 <= 1 & p`2 >= 0},
      L2 = { p : p`1 <= 1 & p`1 >= 0 & p`2 = 1},
      L3 = { p : p`1 <= 1 & p`1 >= 0 & p`2 = 0},
      L4 = { p : p`1 = 1 & p`2 <= 1 & p`2 >= 0};
  set p0 = |[ 0,0 ]|, p01 = |[ 0,1 ]|, p10 = |[ 1,0 ]|, p1 = |[ 1,1 ]|;
 A1: p01`1 = 0 & p01`2 = 1 by EUCLID:56;
 A2: p1`1 = 1 & p1`2 = 1 by EUCLID:56;
 A3: p10`1 = 1 & p10`2 = 0 by EUCLID:56;
  thus L1 = LSeg(p0,p01)
   proof
    A4: L1 c= LSeg(p0,p01)
     proof
      let a be set; assume a in L1;
      then consider p such that
A5:   a = p and A6: p`1 = 0 & p`2 <= 1 & p`2 >= 0;
      set lambda = p`2;
      (1-lambda)*p0 + lambda*p01 = 0.REAL 2 + lambda*p01 by EUCLID:32,Lm1
        .= lambda*p01 by EUCLID:31
        .= |[lambda*p01`1, lambda*p01`2 ]| by EUCLID:61
        .= p by A1,A6,EUCLID:57;
      then a in {(1-r)*p0 + r*p01: 0 <= r & r <= 1 } by A5,A6;
      hence a in LSeg(p0,p01);
     end;
        LSeg(p0,p01) c= L1
     proof
      let a be set; assume a in LSeg(p0,p01);
      then a in {(1-lambda)*p0 + lambda*p01: 0 <= lambda & lambda <= 1 };
      then consider lambda such that
       A7: a = (1-lambda)*p0 + lambda*p01 & 0 <= lambda & lambda <= 1;
      set q = (1-lambda)*p0 + lambda*p01;
           (1-lambda)*p0 + lambda*p01 = 0.REAL 2 + lambda*p01
             by EUCLID:32,Lm1
        .= lambda*p01 by EUCLID:31
        .= |[lambda*(p01`1), lambda*(p01`2) ]| by EUCLID:61
        .= |[ 0, lambda ]| by A1;
      then q`1 = 0 & q`2 <= 1 & q`2 >= 0 by A7,EUCLID:56;
      hence a in L1 by A7;
     end;
    hence thesis by A4,XBOOLE_0:def 10;
   end;
  thus L2 = LSeg(p01,p1)
   proof
    A8: L2 c= LSeg(p01,p1)
     proof
      let a be set; assume a in L2;
      then consider p such that A9: a = p and A10: p`1 <= 1 & p`1 >=
 0 & p`2 = 1;
      set lambda = p`1;
          (1-lambda)*p01 + lambda*p1
         = |[(1-lambda)*0, (1-lambda)*p01`2]| + lambda*p1 by A1,EUCLID:61
        .= |[0, 1-lambda]| + |[lambda*1, lambda]| by A1,A2,EUCLID:61
        .= |[0+lambda, 1-lambda+lambda]| by EUCLID:60
        .= p by A10,EUCLID:57;
      then a in {(1-r)*p01 + r*p1: 0 <= r & r <= 1 } by A9,A10;
      hence a in LSeg(p01,p1);
     end;
        LSeg(p01,p1) c= L2
     proof
      let a be set; assume a in LSeg(p01,p1);
      then a in {(1-lambda)*p01 + lambda*p1: 0 <= lambda & lambda <= 1 };
      then consider lambda such that
       A11: a = (1-lambda)*p01 + lambda*p1 & 0 <= lambda & lambda <= 1;
      set q = (1-lambda)*p01 + lambda*p1;
          (1-lambda)*p01 + lambda*p1
         = |[(1-lambda)*0, (1-lambda)*p01`2]| + lambda*p1 by A1,EUCLID:61
        .= |[0, 1-lambda]| + |[lambda, lambda*1]| by A1,A2,EUCLID:61
        .= |[0+lambda, 1-lambda+lambda]| by EUCLID:60
        .= |[lambda, 1]|;
      then q`1 <= 1 & q`1 >= 0 & q`2 = 1 by A11,EUCLID:56;
      hence a in L2 by A11;
     end;
    hence thesis by A8,XBOOLE_0:def 10;
   end;
 thus L3 = LSeg(p0,p10)
   proof
    A12: L3 c= LSeg(p0,p10)
     proof
      let a be set; assume a in L3;
      then consider p such that
A13:  a = p and A14: p`1 <= 1 & p`1 >= 0 & p`2 = 0;
      set lambda = p`1;
      (1-lambda)*p0 + lambda*p10 = 0.REAL 2 + lambda*p10
            by EUCLID:32,Lm1
        .= lambda*p10 by EUCLID:31
        .= |[lambda*p10`1, lambda*p10`2 ]| by EUCLID:61
        .= p by A3,A14,EUCLID:57;
      then a in {(1-r)*p0 + r*p10: 0 <= r & r <= 1 } by A13,A14;
      hence a in LSeg(p0,p10);
     end;
        LSeg(p0,p10) c= L3
     proof
      let a be set; assume a in LSeg(p0,p10);
      then a in {(1-lambda)*p0 + lambda*p10: 0 <= lambda & lambda <= 1 };
      then consider lambda such that
       A15: a = (1-lambda)*p0 + lambda*p10 & 0 <= lambda & lambda <= 1;
       set q =(1-lambda)*p0 + lambda*p10;
           (1-lambda)*p0 + lambda*p10 = 0.REAL 2 + lambda*p10
             by EUCLID:32,Lm1
        .= lambda*p10 by EUCLID:31
        .= |[ lambda*(p10`1), lambda*(p10`2) ]| by EUCLID:61
        .= |[ lambda, 0 ]| by A3;
      then q`1 <= 1 & q`1 >= 0 & q`2 = 0 by A15,EUCLID:56;
      hence a in L3 by A15;
     end;
    hence thesis by A12,XBOOLE_0:def 10;
   end;
 thus L4 = LSeg(p10,p1)
   proof
    A16: L4 c= LSeg(p10,p1)
     proof
      let a be set; assume a in L4;
      then consider p such that A17: a = p and A18: p`1 = 1 & p`2 <= 1 & p`2 >=
0;
      set lambda = p`2;
          (1-lambda)*p10 + lambda*p1
         = |[(1-lambda)*1, (1-lambda)*p10`2]| + lambda*p1 by A3,EUCLID:61
        .= |[(1-lambda), 0]| + |[lambda*1, lambda]| by A2,A3,EUCLID:61
        .= |[1-lambda+lambda, 0+lambda]| by EUCLID:60
        .= p by A18,EUCLID:57;
      then a in { (1-r)*p10 + r*p1: 0 <= r & r <= 1 } by A17,A18;
      hence a in LSeg(p10,p1);
     end;
     LSeg(p10,p1) c= L4
     proof
      let a be set; assume a in LSeg(p10,p1);
      then a in {(1-lambda)*p10 + lambda*p1: 0 <= lambda & lambda <= 1 };
      then consider lambda such that
       A19: a = (1-lambda)*p10 + lambda*p1 & 0 <= lambda & lambda <= 1;
       set q = (1-lambda)*p10 + lambda*p1;
          (1-lambda)*p10 + lambda*p1
         = |[(1-lambda)*1, (1-lambda)*p10`2]| + lambda*p1 by A3,EUCLID:61
        .= |[(1-lambda), 0]| + |[lambda, lambda*1]| by A2,A3,EUCLID:61
        .= |[1-lambda+lambda, 0+lambda]| by EUCLID:60
        .= |[1, lambda]|;
      then q`1 = 1 & q`2 <= 1 & q`2 >= 0 by A19,EUCLID:56;
      hence a in L4 by A19;
     end;
    hence thesis by A16,XBOOLE_0:def 10;
   end;
 end;

theorem
 R^2-unit_square ={ p : p`1 = 0 & p`2 <= 1 & p`2 >= 0 or
                        p`1 <= 1 & p`1 >= 0 & p`2 = 1 or
                        p`1 <= 1 & p`1 >= 0 & p`2 = 0 or
                        p`1 = 1 & p`2 <= 1 & p`2 >= 0}
proof
   defpred X12[Point of TOP-REAL 2] means
       $1`1 = 0 & $1`2 <= 1 & $1`2 >= 0 or
       $1`1 <= 1 & $1`1 >= 0 & $1`2 = 1;
   defpred X34[Point of TOP-REAL 2] means
       $1`1 <= 1 & $1`1 >= 0 & $1`2 = 0 or
       $1`1 = 1 & $1`2 <= 1 & $1`2 >= 0;
A1: { p2 : X12[p2] or X34[p2] } = { p : X12[p] } \/ { q1: X34[q1] }
             from FraenkelAlt;
   defpred X1[Point of TOP-REAL 2] means
       $1`1 = 0 & $1`2 <= 1 & $1`2 >= 0;
   defpred X2[Point of TOP-REAL 2] means
       $1`1 <= 1 & $1`1 >= 0 & $1`2 = 1;
   defpred X3[Point of TOP-REAL 2] means
       $1`1 <= 1 & $1`1 >= 0 & $1`2 = 0;
   defpred X4[Point of TOP-REAL 2] means
       $1`1 = 1 & $1`2 <= 1 & $1`2 >= 0;
  set L1 = { p : X1[p]}, L2 = { p : X2[p]},
      L3 = { p : X3[p]}, L4 = { p : X4[p]};
A2:   { p : X1[p] or X2[p] } = L1 \/ L2 from FraenkelAlt
     .= LSeg(|[0,0]|,|[0,1]|) \/ LSeg(|[ 0,1 ]|, |[ 1,1 ]|) by Th19;
          { q1: X3[q1] or X4[q1] } = L3 \/ L4 from FraenkelAlt
     .= LSeg(|[ 0,0 ]|, |[ 1,0 ]|) \/ LSeg(|[ 1,0 ]|, |[ 1,1 ]|) by Th19;
 hence thesis by A1,A2;
end;

registration
 cluster R^2-unit_square -> non empty;
coherence
 proof
   R^2-unit_square =
    ( LSeg(|[0,0]|,|[0,1]|) \/ LSeg(|[0,1]|,|[1,1]|) ) \/
    ( LSeg(|[1,1]|,|[1,0]|) \/ LSeg(|[1,0]|,|[0,0]|) );
  hence thesis;
 end;
end;

theorem
  LSeg(|[0,0]|,|[0,1]|) /\ LSeg(|[0,1]|,|[1,1]|) = {|[0,1]|}
 proof
      for a being set
   holds a in LSeg(|[0,0]|,|[0,1]|) /\ LSeg(|[0,1]|,|[1,1]|) iff a = |[0,1]|
  proof let a be set;
  set p00 = |[0,0]|, p01 = |[0,1]|, p11 = |[1,1]|;
  thus a in LSeg(p00,p01) /\ LSeg(p01,p11) implies a = p01
   proof
    assume a in LSeg(p00,p01) /\ LSeg(p01,p11);
    then A1: a in {p : p`1 = 0 & p`2 <= 1 & p`2 >= 0} &
      a in {p2 : p2`1 <= 1 & p2`1 >= 0 & p2`2 = 1} by Th19,XBOOLE_0:def 3;
    then A2: ex p st p = a & p`1 = 0 & p`2 <= 1 & p`2 >= 0;
        ex p2 st p2=a & p2`1<=1 & p2`1>=0 & p2`2=1 by A1;
    hence a = p01 by A2,EUCLID:57;
   end;
   assume a = p01;
    then a in LSeg(p00,p01) & a in LSeg(p01,p11) by Th6;
    hence a in LSeg(p00,p01) /\ LSeg(p01,p11) by XBOOLE_0:def 3;
   end;
  hence thesis by TARSKI:def 1;
 end;

theorem
  LSeg(|[0,0]|,|[1,0]|) /\ LSeg(|[1,0]|,|[1,1]|) = {|[1,0]|}
 proof
   for a being set
   holds a in LSeg(|[0,0]|,|[1,0]|) /\ LSeg(|[1,0]|,|[1,1]|) iff a = |[1,0]|
  proof let a be set;
  set p00 = |[0,0]|, p10 = |[1,0]|, p11 = |[1,1]|;
  thus a in LSeg(p00,p10) /\ LSeg(p10,p11) implies a = p10
   proof
    assume A1: a in LSeg(p00,p10) /\ LSeg(p10,p11);
    then A2: a in LSeg(p10,p11) by XBOOLE_0:def 3;
        a in { p : p`1 <= 1 & p`1 >= 0 & p`2 = 0} by A1,Th19,XBOOLE_0:def 3;
    then A3: ex p st p = a & p`1 <= 1 & p`1 >= 0 & p`2 = 0;
        ex p2 st p2=a & p2`1 = 1 & p2`2 <= 1 & p2`2 >= 0 by A2,Th19;
    hence a = |[1,0]| by A3,EUCLID:57;
   end;
   assume a = |[1,0]|;
    then a in LSeg(p00,p10) & a in LSeg(p10,p11) by Th6;
    hence a in LSeg(p00,p10) /\ LSeg(p10,p11) by XBOOLE_0:def 3;
   end;
  hence thesis by TARSKI:def 1;
 end;

theorem Th23:
  LSeg(|[0,0]|,|[0,1]|) /\ LSeg(|[0,0]|,|[1,0]|) = {|[0,0]|}
proof
  for a being set
   holds a in LSeg(|[0,0]|,|[0,1]|) /\ LSeg(|[0,0]|,|[1,0]|) iff a = |[0,0]|
  proof let a be set;
   thus a in LSeg(|[0,0]|,|[0,1]|) /\ LSeg(|[0,0]|,|[1,0]|) implies a = |[0,0
]|
    proof
     assume A1: a in LSeg(|[0,0]|,|[0,1]|) /\ LSeg(|[0,0]|,|[1,0]|);
     then A2: a in LSeg(|[0,0]|,|[0,1]|)
      by XBOOLE_0:def 3;
         a in { p2 : p2`1 <= 1 & p2`1 >= 0 & p2`2 = 0 } by A1,Th19,
XBOOLE_0:def 3;
     then A3: ex p2 st p2 = a & p2`1 <= 1 & p2`1 >= 0 & p2`2 = 0;
         ex p st p = a & p`1 = 0 & p`2 <= 1 & p`2 >= 0 by A2,Th19;
     hence a = |[0,0]| by A3,EUCLID:57;
    end;
   assume a = |[0,0]|;
   then a in LSeg(|[0,0]|,|[0,1]|) & a in LSeg(|[0,0]|,|[1,0]|) by Th6;
   hence a in LSeg(|[0,0]|,|[0,1]|) /\ LSeg(|[0,0]|,|[1,0]|) by XBOOLE_0:def 3
;
  end;
 hence thesis by TARSKI:def 1;
end;

theorem Th24:
 LSeg(|[0,1]|,|[1,1]|) /\ LSeg(|[1,0]|,|[1,1]|) = {|[1,1]|}
 proof
   for a being set
   holds a in LSeg(|[0,1]|,|[1,1]|) /\ LSeg(|[1,0]|,|[1,1]|) iff a = |[1,1]|
  proof let a be set;
   thus a in LSeg(|[0,1]|,|[1,1]|) /\ LSeg(|[1,0]|,|[1,1]|) implies a = |[1,1
]|
    proof
     assume A1: a in LSeg(|[0,1]|,|[1,1]|) /\ LSeg(|[1,0]|,|[1,1]|);
     then A2: a in LSeg(|[1,0]|,|[1,1]|)
      by XBOOLE_0:def 3;
     a in { p : p`1 <= 1 & p`1 >= 0 & p`2 = 1} by A1,Th19,XBOOLE_0:def 3;
     then A3: ex p st p = a & p`1 <= 1 & p`1 >= 0 & p`2 = 1;
     ex p2 st p2 = a & p2`1 = 1 & p2`2 <= 1 & p2`2 >= 0 by A2,Th19;
     hence a = |[1,1]| by A3,EUCLID:57;
    end;
   assume a = |[1,1]|;
   then a in LSeg(|[0,1]|,|[1,1]|) & a in LSeg(|[1,0]|,|[1,1]|) by Th6;
   hence a in LSeg(|[0,1]|,|[1,1]|) /\ LSeg(|[1,0]|,|[1,1]|) by XBOOLE_0:def 3
;
  end;
  hence thesis by TARSKI:def 1;
 end;

theorem
     LSeg(|[0,0]|,|[1,0]|) misses LSeg(|[0,1]|,|[1,1]|)
  proof
    consider x being Element of
      LSeg(|[0,0]|,|[1,0]|) /\ LSeg(|[0,1]|,|[1,1]|);
   assume LSeg(|[0,0]|,|[1,0]|) /\ LSeg(|[0,1]|,|[1,1]|) <> {};
   then A1: x in LSeg(|[0,0]|,|[1,0]|) & x in LSeg(|[0,1]|,|[1,1]|) by
XBOOLE_0:def 3;
     then A2: ex p st p = x & p`1 <= 1 & p`1 >= 0 & p`2 = 1 by Th19;
     ex p2 st p2 = x & p2`1 <= 1 & p2`1 >= 0 & p2`2 = 0 by A1,Th19;
     hence contradiction by A2;
  end;

theorem Th26:
 LSeg(|[0,0]|,|[0,1]|) misses LSeg(|[1,0]|,|[1,1]|)
  proof
    consider x being Element of
      LSeg(|[0,0]|,|[0,1]|) /\ LSeg(|[1,0]|,|[1,1]|);
   assume LSeg(|[0,0]|,|[0,1]|) /\ LSeg(|[1,0]|,|[1,1]|) <> {};
   then A1: x in LSeg(|[1,0]|,|[1,1]|) & x in LSeg(|[0,0]|,|[0,1]|) by
XBOOLE_0:def 3;
     then A2: ex p st p = x & p`1 = 0 & p`2 <= 1 & p`2 >= 0 by Th19;
         ex p2 st p2 = x & p2`1 = 1 & p2`2 <= 1 & p2`2 >= 0 by A1,Th19;
     hence contradiction by A2;
  end;

definition let n; let f be FinSequence of TOP-REAL n; let i;
 func LSeg(f,i) -> Subset of TOP-REAL n equals
:Def5: LSeg(f/.i,f/.(i+1)) if 1 <= i & i+1 <= len f
         otherwise {};
 coherence
  proof
   thus 1 <= i & i+1 <= len f implies
    LSeg(f/.i,f/.(i+1)) is Subset of TOP-REAL n;
   assume i < 1 or len f < i+1;
        {}(TOP-REAL n) is Subset of TOP-REAL n;
   hence thesis;
  end;
 correctness;
end;

theorem Th27:
 for f being FinSequence of TOP-REAL n st 1 <= i & i+1 <= len f
  holds f/.i in LSeg(f,i) & f/.(i+1) in LSeg(f,i)
 proof let f be FinSequence of TOP-REAL n;
  assume 1 <= i & i+1 <= len f;
   then LSeg(f,i) = LSeg(f/.i,f/.(i+1)) by Def5;
  hence thesis by Th6;
 end;

definition let n; let f be FinSequence of TOP-REAL n;
  func L~f -> Subset of TOP-REAL n equals
   union { LSeg(f,i) : 1 <= i & i+1 <= len f };
 coherence
  proof
   set F = { LSeg(f,i) : 1 <= i & i+1 <= len f };
       F c= bool the carrier of TOP-REAL n
    proof let a be set; assume a in F;
     then ex i st a = LSeg(f,i) & 1 <= i & i+1 <= len f;
     hence a in bool the carrier of TOP-REAL n;
    end;
    then reconsider F as Subset-Family of TOP-REAL n;
        union F is Subset of TOP-REAL n;
   hence thesis;
  end;
end;

theorem Th28:
  for f being FinSequence of TOP-REAL n holds
    (len f = 0 or len f = 1) iff L~f = {}
 proof let f be FinSequence of TOP-REAL n;
 thus (len f = 0 or len f = 1) implies L~f = {}
  proof
  set L = { LSeg(f,i) : 1 <= i & i+1 <= len f };
  assume A1: len f = 0 or len f = 1;
   consider x being Element of L;
      now per cases by A1;
   suppose A2: len f = 0;
   now assume L <> {};
    then x in L;
    then consider i such that x = LSeg(f,i) and A3: 1 <= i & i+1 <= len f;
    thus contradiction by A2,A3;
   end;
   hence L~f = {} by ZFMISC_1:2;
   end;
   suppose A4: len f = 0+1;
   now assume L <> {};
    then x in L;
    then consider i such that x = LSeg(f,i) and A5: 1 <= i & i+1 <= len f;
    i <= 0 by A4,A5,XREAL_1:8;
    hence contradiction by A5;
   end;
   hence L~f = {} by ZFMISC_1:2;
   end;
   end;
   hence thesis;
   end;
  set L = { LSeg(f,i) : 1 <= i & i+1 <= len f };
  assume A6: L~f = {};
  assume A7: len f <> 0 & len f <> 1;
  now assume len f <= 1;
   then len f < 0+1 by A7,REAL_1:def 5;
   then len f <= 0 by NAT_1:13;
   hence contradiction by A7;
  end;
  then A8: len f >= 1+1 by NAT_1:13;
  then LSeg(f,1) in L;
  then LSeg(f,1) c= union L by ZFMISC_1:92;
  then LSeg(f,1) c= {} by A6;
  then LSeg(f,1) = {} by XBOOLE_1:3;
  hence contradiction by A8,Th27;
 end;

theorem Th29:
 for f being FinSequence of TOP-REAL n holds len f >= 2 implies L~f <> {}
 proof let f be FinSequence of TOP-REAL n;
  assume len f >= 2;
  then not len f = 0 & not len f = 1;
  hence thesis by Th28;
 end;

definition let IT be FinSequence of TOP-REAL 2;
  attr IT is special means
      for i st 1 <= i & i+1 <= len IT holds
    (IT/.i)`1 = (IT/.(i+1))`1 or (IT/.i)`2 = (IT/.(i+1))`2;
  attr IT is unfolded means
:Def8:  for i st 1 <= i & i + 2 <= len IT
       holds LSeg(IT,i) /\ LSeg(IT,i+1) = {IT/.(i+1)};
  attr IT is s.n.c. means
:Def9: for i,j st i+1 < j holds LSeg(IT,i) misses LSeg(IT,j);
end;

 reserve f,f1,f2,h for FinSequence of TOP-REAL 2;

definition let f;
  attr f is being_S-Seq means :Def10:
    f is one-to-one & len f >= 2 & f is unfolded s.n.c. special;
end;

notation let f;
  synonym f is_S-Seq for f is being_S-Seq;
end;

theorem Th30:
 ex f1,f2 st f1 is_S-Seq & f2 is_S-Seq & R^2-unit_square = L~f1 \/ L~f2 &
  L~f1 /\ L~f2 = {|[ 0,0 ]|, |[ 1,1 ]|} & f1/.1 = |[0,0]| &
  f1/.len f1=|[1,1]| & f2/.1 = |[0,0]| & f2/.len f2 = |[1,1]|
 proof
  set L1 = { p : p`1 = 0 & p`2 <= 1 & p`2 >= 0};
  set L2 = { p : p`1 <= 1 & p`1 >= 0 & p`2 = 1};
  set L3 = { p : p`1 <= 1 & p`1 >= 0 & p`2 = 0};
  set L4 = { p : p`1 = 1 & p`2 <= 1 & p`2 >= 0};
  set p0 = |[ 0,0 ]|, p01 = |[ 0,1 ]|, p10 = |[ 1,0 ]|, p1 = |[ 1,1 ]|;
 A1: p1`1 = 1 & p1`2 = 1 by EUCLID:56;
 A2: p10`1 = 1 & p10`2 = 0 by EUCLID:56;
 A3: p0`1 = 0 & p0`2 = 0 by EUCLID:56;
  reconsider f1 = <* p0,p01,p1 *> as FinSequence of TOP-REAL 2;
  reconsider f2 = <* p0,p10,p1 *> as FinSequence of TOP-REAL 2;
 take f1,f2;
A4: len f1 = 1 + 2 by FINSEQ_1:62;
A5: f1/.1 = p0 & f1/.2 = p01 & f1/.3 = p1 by FINSEQ_4:27;
thus f1 is_S-Seq
    proof
     p0 <> p01 & p01 <> p1 & p0 <> p1 by A1,A3,EUCLID:56;
     hence f1 is one-to-one by FINSEQ_3:104;
     thus len f1 >= 2 by A4;
     thus f1 is unfolded
      proof
       let i; assume A6: 1 <= i & i + 2 <= len f1;
       then i <= 1 by A4,XREAL_1:8;
       then A7: i = 1 by A6,XXREAL_0:1;
           1+1 in Seg len f1
        by A4,FINSEQ_1:3;
       then A8: LSeg(f1,1) = LSeg(p0,p01) & LSeg(f1,1+1) = LSeg(p01,p1)
        by A4,A5,Def5;
           for x being set holds x in LSeg(p0,p01) /\ LSeg(p01,p1) iff x = p01
        proof
         let x be set;
         thus x in LSeg(p0,p01) /\ LSeg(p01,p1) implies x = p01
          proof
           assume x in LSeg(p0,p01) /\ LSeg(p01,p1);
           then A9: x in {p : p`1 = 0 & p`2 <= 1 & p`2 >= 0} &
             x in {p2 : p2`1 <= 1 & p2`1 >= 0 & p2`2 = 1} by Th19,
XBOOLE_0:def 3;
           then A10: ex p st p = x & p`1 = 0 & p`2 <= 1 & p`2 >= 0;
               ex p2 st p2=x & p2`1<=1 & p2`1>=0 & p2`2=1 by A9;
           hence x = p01 by A10,EUCLID:57;
          end;
          assume x = p01;
          then x in LSeg(p0,p01) & x in LSeg(p01,p1) by Th6;
         hence x in LSeg(p0,p01) /\ LSeg(p01,p1) by XBOOLE_0:def 3;
        end;
       hence thesis by A5,A7,A8,TARSKI:def 1;
      end;
     thus f1 is s.n.c.
      proof
       let i,j such that A11: i+1 < j;
           now per cases;
        suppose 1 <= i;
         then A12:      1+1 <= i+1 by XREAL_1:8;
         now per cases;
          case 1 <= j & j+1 <= len f1;
          then j <= 2 by A4,XREAL_1:8;
          hence contradiction by A11,A12,XXREAL_0:2;
          end;
          case not (1 <= j & j+1 <= len f1);
          then LSeg(f1,j) = {} by Def5;
          hence LSeg(f1,i) /\ LSeg(f1,j) = {};
         end;
         end;
        hence LSeg(f1,i) /\ LSeg(f1,j) = {};
        end;
        suppose not (1 <= i & i+1 <= len f1);
        then LSeg(f1,i) = {} by Def5;
        hence LSeg(f1,i) /\ LSeg(f1,j) = {};
       end;
       end;
       hence LSeg(f1,i) /\ LSeg(f1,j) = {};
      end;
     let i; assume A13: 1 <= i & i + 1 <= len f1;
      then A14: i <= 1 + 1 by A4,XREAL_1:8;
      per cases by A13,A14,NAT_1:9;
       suppose A15: i = 1;
        then (f1/.i)`1 = p0`1 by FINSEQ_4:27 .= 0 by EUCLID:56
                 .= (f1/.(i+1))`1 by A5,A15,EUCLID:56;
        hence (f1/.i)`1 = (f1/.(i+1))`1 or (f1/.i)`2 = (f1/.(i+1))`2;
       end;
       suppose A16: i = 2;
        then (f1/.i)`2 = p01`2 by FINSEQ_4:27 .= 1 by EUCLID:56
                 .= (f1/.(i+1))`2 by A5,A16,EUCLID:56;
        hence (f1/.i)`1 = (f1/.(i+1))`1 or (f1/.i)`2 = (f1/.(i+1))`2;
       end;
    end;
A17:   L~f1 = union {LSeg(p0,p01),LSeg(p01,p1)}
    proof
         len f1 = 2+1 & 1+1 in Seg len f1
      by A4,FINSEQ_1:3;
     then 1+1 <= len f1 & LSeg(p0,p01) = LSeg(f1,1)
      by A5,Def5;
     then A18: LSeg(p0,p01) in {LSeg(f1,i): 1 <= i & i+1 <= len f1};
     2+1 <= len f1 & LSeg(p01,p1) = LSeg(f1,2)
      by A4,A5,Def5;
     then LSeg(p01,p1) in {LSeg(f1,i): 1 <= i & i+1 <= len f1};
     then A19: {LSeg(p0,p01),LSeg(p01,p1)} c= {LSeg(f1,i):1<=i & i+1<=len f1}
      by A18,ZFMISC_1:38;
         {LSeg(f1,i): 1 <= i & i+1 <= len f1} c= {LSeg(p0,p01),LSeg(p01,p1)}
      proof
       let a be set; assume a in {LSeg(f1,i): 1 <= i & i+1 <= len f1};
       then consider i such that A20: a = LSeg(f1,i) & 1<=i & i+1<=len f1;
           i+1 <= 2 + 1 by A20,FINSEQ_1:62; then i <= 1 + 1 by XREAL_1:8;
       then i = 1 or i = 2 by A20,NAT_1:9;
       then a = LSeg(p0,p01) or a = LSeg(p01,p1) by A5,A20,Def5;
       hence a in {LSeg(p0,p01),LSeg(p01,p1)} by TARSKI:def 2;
      end;
    hence union {LSeg(p0,p01),LSeg(p01,p1)} = L~f1 by A19,XBOOLE_0:def 10;
    end;
   then A21: L~f1 = (LSeg(p0,p01) \/ LSeg(p01,p1)) by ZFMISC_1:93;
A22: len f2 = 1 + 2 by FINSEQ_1:62;
A23: f2/.1 = p0 & f2/.2 = p10 & f2/.3 = p1 by FINSEQ_4:27;
    thus f2 is_S-Seq
    proof
     thus f2 is one-to-one by A1,A2,A3,FINSEQ_3:104;
     thus len f2 >= 2 by A22;
     thus f2 is unfolded
      proof
       let i; assume A24: 1 <= i & i + 2 <= len f2;
       then i <= 1 by A22,XREAL_1:8;
       then A25: i = 1 by A24,XXREAL_0:1;
       1+1 in Seg len f2 by A22,FINSEQ_1:3;
       then A26: LSeg(f2,1) = LSeg(p0,p10) & LSeg(f2,1+1) = LSeg(p10,p1)
        by A22,A23,Def5;
       for x being set holds x in LSeg(p0,p10) /\ LSeg(p10,p1) iff x = p10
        proof
         let x be set;
         thus x in LSeg(p0,p10) /\ LSeg(p10,p1) implies x = p10
          proof
           assume x in LSeg(p0,p10) /\ LSeg(p10,p1);
           then A27: x in { p : p`1 <= 1 & p`1 >= 0 & p`2 = 0} &
             x in { p2 : p2`1 = 1 & p2`2 <= 1 & p2`2 >= 0} by Th19,
XBOOLE_0:def 3;
           then A28: ex p st p = x & p`1 <= 1 & p`1 >= 0 & p`2 = 0;
           ex p2 st p2=x & p2`1=1 & p2`2<=1 & p2`2>=0 by A27;
           hence x = p10 by A28,EUCLID:57;
          end;
          assume x = p10;
          then x in LSeg(p0,p10) & x in LSeg(p10,p1) by Th6;
         hence x in LSeg(p0,p10) /\ LSeg(p10,p1) by XBOOLE_0:def 3;
        end;
       hence thesis by A23,A25,A26,TARSKI:def 1;
      end;
     thus f2 is s.n.c.
      proof
       let i,j such that A29: i+1 < j;
       per cases;
        suppose 1 <= i;
         then A30:      1+1 <= i+1 by XREAL_1:8;
         now per cases;
          case 1 <= j & j+1 <= len f2;
          then j <= 2 by A22,XREAL_1:8;
          hence contradiction by A29,A30,XXREAL_0:2;
          end;
          case not (1 <= j & j+1 <= len f2);
          then LSeg(f2,j) = {} by Def5;
          hence LSeg(f2,i) /\ LSeg(f2,j) = {};
         end;
         end;
        hence LSeg(f2,i) /\ LSeg(f2,j) = {};
        end;
        suppose not (1 <= i & i+1 <= len f2);
        then LSeg(f2,i) = {} by Def5;
        hence LSeg(f2,i) /\ LSeg(f2,j) = {};
       end;
      end;
     let i; assume A31: 1 <= i & i + 1 <= len f2;
      then A32: i <= 1 + 1 by A22,XREAL_1:8;
       per cases by A31,A32,NAT_1:9;
       suppose A33: i = 1;
        then (f2/.i)`2 = p0`2 by FINSEQ_4:27 .= 0 by EUCLID:56
                 .= (f2/.(i+1))`2 by A23,A33,EUCLID:56;
        hence (f2/.i)`1 = (f2/.(i+1))`1 or (f2/.i)`2 = (f2/.(i+1))`2;
       end;
       suppose A34: i = 2;
        then (f2/.i)`1 = p10`1 by FINSEQ_4:27 .= 1 by EUCLID:56
                 .= (f2/.(i+1))`1 by A23,A34,EUCLID:56;
        hence (f2/.i)`1 = (f2/.(i+1))`1 or (f2/.i)`2 = (f2/.(i+1))`2;
    end;
    end;
A35:   L~f2 = union {LSeg(p0,p10),LSeg(p10,p1)}
    proof
     len f2 = 2+1 & 1+1 in Seg len f2 by A22,FINSEQ_1:3;
     then 1+1 <= len f2 & LSeg(p0,p10) = LSeg(f2,1) by A23,Def5;
     then A36: LSeg(p0,p10) in {LSeg(f2,i): 1 <= i & i+1 <= len f2};
     2+1 <= len f2 & LSeg(p10,p1) = LSeg(f2,2) by A22,A23,Def5;
     then LSeg(p10,p1) in {LSeg(f2,i): 1 <= i & i+1 <= len f2};
     then A37: {LSeg(p0,p10),LSeg(p10,p1)} c= {LSeg(f2,i):1<=i & i+1<=len f2}
      by A36,ZFMISC_1:38;
      {LSeg(f2,i): 1 <= i & i+1 <= len f2} c= {LSeg(p0,p10),LSeg(p10,p1)}
      proof
       let a be set; assume a in {LSeg(f2,i): 1 <= i & i+1 <= len f2};
       then consider i such that A38: a = LSeg(f2,i) & 1<=i & i+1<=len f2;
       i+1 <= 2 + 1 by A38,FINSEQ_1:62; then i <= 1 + 1 by XREAL_1:8;
       then i = 1 or i = 2 by A38,NAT_1:9;
       then a = LSeg(p0,p10) or a = LSeg(p10,p1) by A23,A38,Def5;
       hence a in {LSeg(p0,p10),LSeg(p10,p1)} by TARSKI:def 2;
      end;
    hence union {LSeg(p0,p10),LSeg(p10,p1)} = L~f2 by A37,XBOOLE_0:def 10;
    end;
   then A39: L~f2 = (LSeg(p0,p10) \/ LSeg(p10,p1)) by ZFMISC_1:93;
   thus R^2-unit_square = L~f1 \/ L~f2 by A21,A35,ZFMISC_1:93;
     now assume L2 meets L3;
     then consider x being set such that
     A40: x in L2 & x in L3 by XBOOLE_0:3;
     A41: ex p st p = x & p`1 <= 1 & p`1 >= 0 & p`2 = 1 by A40;
         ex p2 st p2 = x & p2`1 <= 1 & p2`1 >= 0 & p2`2 = 0 by A40;
     hence contradiction by A41;
    end;
then A42: L2 /\ L3 = {} by XBOOLE_0:def 7;
A43: LSeg(|[0,0]|,|[0,1]|) /\ LSeg(|[1,0]|,|[1,1]|) = {} by Th26,XBOOLE_0:def 7
;
   thus L~f1 /\ L~f2 = (L1 \/ L2) /\ (L3 \/ L4) by A17,A39,Th19,ZFMISC_1:93
      .= (L1 \/ L2) /\ L3 \/ (L1 \/ L2) /\ L4 by XBOOLE_1:23
      .= L1 /\ L3 \/ L2 /\ L3 \/ (L1 \/ L2) /\ L4 by XBOOLE_1:23
      .= L1 /\ L3 \/ L2 /\ L3 \/ (L1 /\ L4 \/ L2 /\ L4) by XBOOLE_1:23
      .= {|[ 0,0 ]|, |[ 1,1 ]|} by A42,A43,Th19,Th23,Th24,ENUMSET1:41;
  thus f1/.1 = |[0,0]| & f1/.len f1=|[1,1]| & f2/.1 = |[0,0]| &
       f2/.len f2 = |[1,1]| by A4,A22,FINSEQ_4:27;
 end;

theorem Th31:
 h is_S-Seq implies L~h is_an_arc_of h/.1,h/.len h
 proof
  assume A1: h is_S-Seq; set P = L~h;
 set p1 = h/.1;
 A2: h is one-to-one by A1,Def10;
 A3: len h >= 1+1 by A1,Def10;
  deffunc Q(Element of NAT) = L~(h|($1+2));
  defpred ARC[Element of NAT] means
   1 <= $1 + 2 & $1 + 2 <= len h implies
   ex NE be non empty Subset of TOP-REAL 2 st NE = Q($1) &
    ex f being Function of I[01], (TOP-REAL 2)|NE st
      f is_homeomorphism & f.0 = p1 & f.1 = h/.($1+2);
  1 <= len h by A3,XXREAL_0:2;
  then A4: 1 in Seg len h by FINSEQ_1:3;
  A5: 1+1 in Seg len h by A3,FINSEQ_1:3;
  set p2 = h/.(1+1);
     h|2 = h|(Seg 2) by FINSEQ_1:def 15;
then A6: dom (h|2) = dom h /\ Seg 2 by RELAT_1:90
             .= Seg len h /\ Seg 2 by FINSEQ_1:def 3
             .= Seg 2 by A3,FINSEQ_1:9;
then A7: len(h|2) = 1+1 by FINSEQ_1:def 3;
then A8: 1 in Seg len (h|2) & 2 in Seg len (h|2) by FINSEQ_1:3;
    {LSeg(h|2,i) : 1 <= i & i+1 <= len(h|2) } = {LSeg(h,1)}
    proof
     now let x be set;
     thus x in {LSeg(h|2,i) : 1 <= i & i+1 <= len(h|2) } implies
      x = LSeg(h,1)
      proof
       assume x in {LSeg(h|2,i) : 1 <= i & i+1 <= len(h|2) };
       then consider i being Element of NAT such that
      A9: x = LSeg(h|2,i) and
      A10: 1 <= i & i+1 <= len(h|2);
       1 <= i & i+1 <= (1 + 1) by A6,A10,FINSEQ_1:def 3;
       then 1 <= i & i <= 1 by XREAL_1:8;
       then A11: 1 = i by XXREAL_0:1;
       (h|2)/.1 = h/.1 & (h|2)/.(1+1) = h/.(1+1) by A6,A7,A8,FINSEQ_4:85;
       hence x = LSeg(p1,p2) by A7,A9,A11,Def5
              .= LSeg(h,1) by A3,Def5;
      end;
     assume x = LSeg(h,1);
      then A12: x = LSeg(p1,p2) by A3,Def5;
      p1 = (h|2)/.1 & p2 = (h|2)/.2 by A6,A7,A8,FINSEQ_4:85;
      then x = LSeg(h|2,1) by A7,A12,Def5;
     hence x in {LSeg(h|2,i) : 1 <= i & i+1 <= len(h|2)} by A7;
    end;
    hence thesis by TARSKI:def 1;
    end;
   then A13: Q(0) = LSeg(h,1) by ZFMISC_1:31
                .= LSeg(p1,p2) by A3,Def5;
    1 <= 0 + 2 & 0 + 2 <= len h implies
    ex f being Function of I[01], (TOP-REAL 2)|(LSeg(p1,p2)) st
     f is_homeomorphism & f.0 = p1 & f.1 = h/.(0+2)
    proof assume 1 <= 0 + 2 & 0 + 2 <= len h;
      1 <> 2 & 1 in dom h & 2 in dom h by A4,A5,FINSEQ_1:def 3;
      then p1 <> p2 by A2,PARTFUN2:17;
      then LSeg(p1,p2) is_an_arc_of p1,p2 by Th15;
      hence thesis by Def2;
    end;
then A14: ARC[0] by A13;
A15: for n st ARC[n] holds ARC[n+1]
    proof
     let n;
     assume A16: ARC[n];
     assume
A17:    1 <= n + 1 + 2 & n + 1 + 2 <= len h;
      then A18: 1 <= n + 1 + 1 & n + 1 + 1 <= n + 2 + 1 & n + 2 + 1 <= len h
      by NAT_1:11;
     then A19: 1 <= n + 1 + 1 & n + 1 + 1 <= len h by XXREAL_0:2;
A20:  n + 1 + 1 = n + (1 + 1);
     consider NE being non empty Subset of TOP-REAL 2 such that
A21:    NE = Q(n) &
       ex f being Function of I[01], (TOP-REAL 2)|NE st
        f is_homeomorphism & f.0 = p1 & f.1 = h/.(n+2) by A16,A18,XXREAL_0:2;
     consider f being Function of I[01], (TOP-REAL 2)|NE such that
   A22: f is_homeomorphism & f.0 = p1 & f.1 = h/.(n+2) by A21;
     set pn = h/.(n+2), pn1 = h/.(n+2+1);
A23:    n + 1 +1 <> n + 2 + 1;
         n + 1 +1 in dom h & n + 2 + 1 in dom h by A17,A19,FINSEQ_3:27;
     then pn <> pn1 by A2,A23,PARTFUN2:17;
     then LSeg(pn,pn1) is_an_arc_of pn,pn1 by Th15;
     then consider f1 being Function
 of I[01], (TOP-REAL 2)|LSeg(pn,pn1) such that
   A24: f1 is_homeomorphism & f1.0 = pn & f1.1 = pn1 by Def2;
 A25: Q(n+1) = Q(n) \/ LSeg(h,n+2)
    proof
     now let x be set; assume x in Q(n+1);
      then x in union {LSeg(h|(n+1+2),i) : 1 <= i & i+1 <= len(h|(n+1+2))};
      then consider X being set such that
     A26: x in X and
     A27: X in {LSeg(h|(n+1+2),i) : 1 <= i & i+1 <= len(h|(n+1+2))}
      by TARSKI:def 4;
      consider i being Element of NAT such that
     A28: X = LSeg(h|(n+1+2),i) and
     A29: 1 <= i & i+1 <= len(h|(n+1+2)) by A27;
          i + 1 - 1 = i;
then A30:   i <= len(h|(n+1+2)) - 1 by A29,XREAL_1:11;
      A31: len(h|(n+1+2))-1 = n+1+2-1 by A17,FINSEQ_1:80.= n+(1+1);
A32:      n+(1+1) = n+1+1;
          now per cases by A30,A31,A32,NAT_1:8;
       suppose A33: i = n+2;
        A34: len(h|(n+1+2)) = n+1+2 by A17,FINSEQ_1:80;
            n+1+2 = n+2+1;
        then A35:     1 <= n+2 & n+2<=n+1+2 & n+2+1 <= n+1+2 & 1<=n+2+1 by A20,
NAT_1:11;
        then n + 2 in Seg len(h|(n+1+2)) & n + 2 + 1 in Seg len(h|(n+1+2))
         by A34,FINSEQ_1:3;
        then A36: n + 2 in dom(h|(n+1+2)) & n + 2 + 1 in dom(h|(n+1+2))
         by FINSEQ_1:def 3;
       then A37: (h|(n+1+2))/.(n+2) = h/.(n+2) by FINSEQ_4:85;
        (h|(n+1+2))/.(n+2+1) = h/.(n+(2+1)) by A36,FINSEQ_4:85;
        then LSeg(h|(n+1+2),n+2) = LSeg(pn,pn1) by A34,A35,A37,Def5
                    .= LSeg(h,n+2) by A18,Def5;
        hence x in Q(n) \/ LSeg(h,n+2) by A26,A28,A33,XBOOLE_0:def 2;
       end;
       suppose A38: i <= n+1;
       A39: len(h|(n+2)) = n + (1 + 1) by A19,FINSEQ_1:80;
            i+1 <= n+1+1 by A38,XREAL_1:8;
        then i+1 <= len(h|(n+2)) by A19,FINSEQ_1:80;
        then A40: LSeg(h|(n+2),i) in {LSeg(h|(n+2),j) : 1 <= j &
          j+1 <= len(h|(n+2))} by A29;
        A41: 1 <= 1+i & n+1 <= n+1+1 & n+1+1 = n+(1+1) & i+1 <= n+1+1
         by A38,NAT_1:11,XREAL_1:9;
        then A42: 1 <= i+1 & i <= n+2 & i+1 <= n+2 by A38,XXREAL_0:2;
        then A43: i in Seg len (h|(n+2)) & i+1 in Seg len(h|(n+2)) by A29,A39,
FINSEQ_1:3;
        set p1' = (h|(n+2))/.i, p2' = (h|(n+2))/.(i+1);
        A44: i in dom(h|(n+2)) & i+1 in dom(h|(n+2)) by A43,FINSEQ_1:def 3;
            n+2 <= n+2+1 & n+2+1 = n+1+2 by NAT_1:11;
      then i <= n+1+2 & i+1 <= n+1+2 by A42,XXREAL_0:2;
      then i in Seg(n+1+2) & i+1 in Seg(n+1+2) & n+1+2 <= len h
         by A17,A29,A41,FINSEQ_1:3;
         then i in Seg len(h|(n+1+2)) & i+1 in Seg len(h|(n+1+2))
            by FINSEQ_1:80;
        then A45: i in dom(h|(n+1+2)) & i+1 in
 dom(h|(n+1+2)) by FINSEQ_1:def 3;
        then A46: (h|(n+1+2))/.i = h/.i by FINSEQ_4:85
             .= p1' by A44,FINSEQ_4:85;
        A47: (h|(n+1+2))/.(i+1) = h/.(i+1) by A45,FINSEQ_4:85
             .= p2' by A44,FINSEQ_4:85;
            LSeg(h|(n+2),i) = LSeg(p1',p2') by A29,A39,A41,Def5
               .= LSeg(h|(n+1+2),i) by A29,A46,A47,Def5;
        then x in union {LSeg(h|(n+2),j) : 1 <= j & j+1 <= len(h|(n+2))}
          by A26,A28,A40,TARSKI:def 4;
        then x in Q(n);
        hence x in Q(n) \/ LSeg(h,n+2) by XBOOLE_0:def 2;
      end;
      end;
      hence x in Q(n) \/ LSeg(h,n+2);
     end; then A48: Q(n+1) c= Q(n) \/ LSeg(h,n+2) by TARSKI:def 3;
         now let x be set such that A49: x in Q(n) \/ LSeg(h,n+2);
          now per cases by A49,XBOOLE_0:def 2;
       suppose x in Q(n);
        then x in union {LSeg(h|(n+2),i) : 1 <= i & i+1 <= len(h|(n+2))};
        then consider X being set such that
       A50: x in X and
       A51: X in {LSeg(h|(n+2),i) : 1 <= i & i+1 <= len(h|(n+2))}
            by TARSKI:def 4;
        consider i being Element of NAT such that
       A52: X = LSeg(h|(n+2),i) and
       A53: 1 <= i & i+1 <= len(h|(n+2)) by A51;
        A54: n + 1 + 2 <= len h & n + 2 <= len h by A18,XXREAL_0:2;
        then A55:         i+1 <= n + (1 + 1) & len(h|(n+1+2)) = n+(1+2) by A53,
FINSEQ_1:80;
         i+1 <= n + 1 + 1 by A53,A54,FINSEQ_1:80;
        then A56: i <= n + 1 & n+1 <= n+(1+1) by XREAL_1:8;
            n+2 <= n + 3 by XREAL_1:8;
        then A57: 1 <= i & i+1 <= len(h|(n+1+2)) by A53,A55,XXREAL_0:2;
       A58: len(h|(n+2)) = n + 2 by A19,FINSEQ_1:80;
        A59: 1 <= 1+i & n+1+1 = n+(1+1) & i+1 <= n+1+1
         by A53,A54,FINSEQ_1:80,NAT_1:11;
        A60: 1 <= i+1 & i <= n+2 & i+1 <= n+2
         by A53,A54,A56,XXREAL_0:2,FINSEQ_1:80,NAT_1:11;
        then A61: i in Seg len(h|(n+2)) & i+1 in
 Seg len(h|(n+2)) by A53,A58,FINSEQ_1:3;
        set p1' = (h|(n+2))/.i, p2' = (h|(n+2))/.(i+1);
        A62: i in dom(h|(n+2)) & i+1 in dom(h|(n+2)) by A61,FINSEQ_1:def 3;
            n+2 <= n+2+1 & n+2+1 = n+1+2 by NAT_1:11;
then A63:    i <= n+1+2 & i+1 <= n+1+2 by A60,XXREAL_0:2;
        then i in Seg(n+1+2) & i+1 in Seg(n+1+2) & n+1+2 <= len h
         by A17,A53,A59,FINSEQ_1:3;
        then A64: i in Seg len(h|(n+1+2)) & i+1 in Seg len(h|(n+1+2)) &
           len(h|(n+1+2)) = n+1+2 by FINSEQ_1:80;
   then A65: i in dom(h|(n+1+2)) & i+1 in dom(h|(n+1+2)) by FINSEQ_1:def 3;
        then A66: (h|(n+1+2))/.i = h/.i by FINSEQ_4:85
             .= p1' by A62,FINSEQ_4:85;
        A67: (h|(n+1+2))/.(i+1) = h/.(i+1) by A65,FINSEQ_4:85
             .= p2' by A62,FINSEQ_4:85;
        X = LSeg(p1',p2') by A52,A53,Def5
         .= LSeg(h|(n+1+2),i) by A53,A63,A64,A66,A67,Def5;
        then X in {LSeg(h|(n+1+2),j): 1 <= j & j+1 <= len(h|(n+1+2))} by A57;
        then x in union {LSeg(h|(n+1+2),j): 1 <= j & j+1 <= len(h|(n+1+2))}
         by A50,TARSKI:def 4;
        hence x in Q(n+1);
       end;
       suppose A68: x in LSeg(h,n+2);
A69:      len(h|(n+1+2)) = n+1+2 & 1 <= n+1+2 & n+2 <= n+2+1 &
          1 <= n+2 & n+1+2 = n+2+1
          by A17,A20,FINSEQ_1:80,NAT_1:11;
        then n + 2 in Seg len(h|(n+1+2)) & n + 2 + 1 in Seg len(h|(n+1+2))
         by FINSEQ_1:3;
        then A70: n + 2 in dom(h|(n+1+2)) & n + 2 + 1 in dom(h|(n+1+2))
          by FINSEQ_1:def 3;
then A71:    1 <= n+2 & n+2+1 <= len(h|(n+1+2)) by FINSEQ_3:27;
        A72: pn = (h|(n+1+2))/.(n+2) by A70,FINSEQ_4:85;
        A73: pn1 = (h|(n+1+2))/.(n+2+1) by A70,FINSEQ_4:85;
            LSeg(h,n+2) = LSeg(pn,pn1) by A17,A69,Def5
                   .= LSeg(h|(n+1+2),n+2) by A69,A72,A73,Def5;
        then LSeg(h,n+2) in {LSeg(h|(n+1+2),i) : 1 <= i &
         i+1 <= len(h|(n+1+2))} by A71;
        then x in union {LSeg(h|(n+1+2),i) : 1 <= i & i+1 <= len(h|(n+1+2))}
         by A68,TARSKI:def 4;
        hence x in Q(n+1);
      end;
      end;
      hence x in Q(n+1);
     end; then Q(n) \/ LSeg(h,n+2) c= Q(n+1) by TARSKI:def 3;
     hence thesis by A48,XBOOLE_0:def 10;
    end;
      for x being set holds x in Q(n) /\ LSeg(pn,pn1) iff x = pn
        proof
         let x be set;
         thus x in Q(n) /\ LSeg(pn,pn1) implies x = pn
          proof
           assume x in Q(n) /\ LSeg(pn,pn1);
           then A74: x in LSeg(pn,pn1) & x in Q(n) by XBOOLE_0:def 3;
           then x in union {LSeg(h|(n+2),i) : 1 <= i & i+1 <= len(h|(n+2))};
           then consider X being set such that
          A75: x in X and
          A76: X in {LSeg(h|(n+2),i) : 1 <= i & i+1 <= len(h|(n+2))}
             by TARSKI:def 4;
           consider i such that
          A77: X = LSeg(h|(n+2),i) and
          A78: 1 <= i & i+1 <= len(h|(n+2)) by A76;
         A79: len(h|(n+2)) = n+(1+1) by A19,FINSEQ_1:80;
         A80: len(h|(n+2)) = n+1+1 by A19,FINSEQ_1:80;
then A81:     i <= n+1 by A78,XREAL_1:8;
A82:    h is s.n.c. by A1,Def10;
A83:    h is unfolded by A1,Def10;
        now assume
A84:    i < n+1;
         A85: 1 <= 1+i & i+1 <= n+(1+1) by A19,A78,FINSEQ_1:80,NAT_1:11;
A86:      i+1 <= len h by A19,A78,A80,XXREAL_0:2;
            A87: i+1 < n+1+1 by A84,XREAL_1:8;
         set p1' = h/.i, p2' = h/.(i+1);
             n+1 <= n+1+1 & n+1+1 = n+(1+1) by NAT_1:11;
         then i <= n+2 by A84,XXREAL_0:2;
         then i in Seg len(h|(n+2)) & i+1 in Seg len(h|(n+2))
          by A78,A79,A85,FINSEQ_1:3;
         then A88: i in dom(h|(n+2)) & i+1 in dom(h|(n+2)) by FINSEQ_1:def 3;
         then A89: (h|(n+2))/.i = p1' by FINSEQ_4:85;
         A90: (h|(n+2))/.(i+1) = p2' by A88,FINSEQ_4:85;
         A91: LSeg(h,i) = LSeg(p1',p2') by A78,A86,Def5
                      .= LSeg(h|(n+2),i) by A78,A89,A90,Def5;
         LSeg(h,n+2) = LSeg(pn,pn1) by A18,Def5;
         then LSeg(h|(n+2),i) misses LSeg(pn,pn1) by A82,A87,A91,Def9;
         hence contradiction by A74,A75,A77,XBOOLE_0:3;
         end;
         then A92: i = n + 1 by A81,REAL_1:def 5;
A93:      1 <= n+1 & 1 <= n+1+1 & n+1+1 <= len h
          by A18,A78,A81,XXREAL_0:2;
         set p1' = h/.(n+1), p2' = h/.(n+1+1);
A94:         n+1 <= n+1+1 & n+1+1 = n+(1+1) & 1 <= 1+n & 1 <= 1+(n+1)
          by NAT_1:11;
         then n+1 in Seg len(h|(n+2)) & n+1+1 in Seg len(h|(n+2))
          by A79,FINSEQ_1:3;
         then A95: n+1 in dom(h|(n+2)) & n+1+1 in dom(h|(n+2))
           by FINSEQ_1:def 3;
         then A96: (h|(n+2))/.(n+1) = p1' by FINSEQ_4:85;
         A97: (h|(n+2))/.(n+1+1) = p2' by A95,FINSEQ_4:85;
         A98: LSeg(h,n+1) = LSeg(p1',p2') by A93,Def5
                        .= LSeg(h|(n+2),n+1) by A79,A94,A96,A97,Def5;
          LSeg(pn,pn1) = LSeg(h,n+1+1) by A18,Def5;
         then A99: x in LSeg(h,n+1) /\ LSeg(h,n+1+1)
           by A74,A75,A77,A92,A98,XBOOLE_0:def 3;
         1 <= n+1 by NAT_1:11;
         then LSeg(h,n+1) /\ LSeg(h,n+1+1) = {h/.(n+1+1)} by A17,A83,Def8;
         hence x = pn by A99,TARSKI:def 1;
          end;
         assume A100: x = pn;
          then A101: x in LSeg(pn,pn1) by Th6;
          A102: len(h|(n+2)) = n+2 by A19,FINSEQ_1:80;
            then dom(h|(n+2)) = Seg(n+2) & n + 2 in Seg(n+2)
             by A18,FINSEQ_1:3,def 3;
            then A103: x = (h|(n+2))/.(n+1+1) by A100,FINSEQ_4:85;
         1 <= n+1 by NAT_1:11;
          then A104: x in LSeg(h|(n+2),n+1) by A102,A103,Th27;
               1 <= 1 + n & n + 1 + 1 <= n + (1 + 1) & n+2 <= len h
              by A18,XXREAL_0:2,NAT_1:11;
            then 1 <= n+1 & n+1+1 <= len(h|(n+2)) by FINSEQ_1:80;
            then LSeg(h|(n+2),n+1) in {LSeg(h|(n+2),i): 1 <= i & i+1 <=
 len(h|(n+2))};
          then x in union {LSeg(h|(n+2),i) : 1 <= i & i+1 <= len(h|(n+2))}
           by A104,TARSKI:def 4;
          then x in Q(n);
         hence x in Q(n) /\ LSeg(pn,pn1) by A101,XBOOLE_0:def 3;
        end;
then A105:   Q(n) /\ LSeg(pn,pn1) = {pn} by TARSKI:def 1;
      reconsider NE1 = Q(n) \/ LSeg(pn,pn1) as
       non empty Subset of TOP-REAL 2;
      consider hh being Function of I[01], (TOP-REAL 2)|NE1 such that
A106:     hh is_homeomorphism & hh.0 = f.0 & hh.1 = f1.1
         by A21,A22,A24,A105,TOPMETR2:6;
     take NE1;
     thus NE1 = Q(n+1) by A18,A25,Def5;
     take hh;
     thus hh is_homeomorphism & hh.0 = p1 & hh.1 = h/.(n+1+2)
       by A22,A24,A106;
   end;
A107: for n holds ARC[n] from NAT_1:sch 1(A14,A15);
  reconsider h1 = len h - 2 as Element of NAT by A3,INT_1:18;
 1 <= h1 + 2 by NAT_1:12;
   then consider NE being non empty Subset of TOP-REAL 2 such that
A108:  NE = Q(h1) &
     ex f being Function of I[01], (TOP-REAL 2)|NE st
      f is_homeomorphism & f.0 = p1 & f.1 = h/.(h1+2) by A107;
   consider f being Function of I[01], (TOP-REAL 2)|NE such that
A109: f is_homeomorphism & f.0 = p1 & f.1 = h/.(h1+2) by A108;
A110: h|(len h) = h|(Seg len h) by FINSEQ_1:def 15
             .= h|(dom h) by FINSEQ_1:def 3
             .= h by RELAT_1:97;
  then reconsider f as Function of I[01], (TOP-REAL 2)|P by A108;
  take f;
  thus f is_homeomorphism by A108,A109,A110;
  thus thesis by A109;
 end;

definition let P be Subset of TOP-REAL 2;
  attr P is being_S-P_arc means :Def11:
    ex f st f is_S-Seq & P = L~f;
end;

notation let P be Subset of TOP-REAL 2;
  synonym P is_S-P_arc for P is being_S-P_arc;
end;

theorem Th32:
  P1 is_S-P_arc implies P1 <> {}
 proof
  assume P1 is_S-P_arc;
  then consider f such that A1: f is_S-Seq & P1 = L~f by Def11;
  len f >= 2 by A1,Def10;
  hence thesis by A1,Th29;
 end;

registration
  cluster being_S-P_arc -> non empty Subset of TOP-REAL 2;
  coherence by Th32;
end;

canceled;

theorem
  ex P1, P2 being non empty Subset of TOP-REAL 2
  st P1 is_S-P_arc & P2 is_S-P_arc & R^2-unit_square = P1 \/ P2 &
   P1 /\ P2 = {|[ 0,0 ]|, |[ 1,1 ]|}
 proof
  consider f1,f2 such that
A1: f1 is_S-Seq & f2 is_S-Seq & R^2-unit_square = L~f1 \/ L~f2 &
    L~f1 /\ L~f2 = {|[ 0,0 ]|, |[ 1,1 ]|} and
        f1/.1=|[0,0]| & f1/.len f1=|[1,1]| &
    f2/.1=|[0,0]| & f2/.len f2=|[1,1]| by Th30;
  reconsider P1 = L~f1, P2 = L~f2 as non empty Subset of TOP-REAL 2 by A1;
  take P1, P2;
  thus thesis by A1,Def11;
 end;

theorem Th35:
  P is_S-P_arc implies ex p1,p2 st P is_an_arc_of p1,p2
 proof
  assume P is_S-P_arc;
  then consider h such that A1: h is_S-Seq & P = L~h by Def11;
  take p1 = h/.1, p2 = h/.len h;
  thus P is_an_arc_of p1,p2 by A1,Th31;
 end;

theorem
  P is_S-P_arc implies
    ex f being Function of I[01], (TOP-REAL 2)|P st f is_homeomorphism
proof
  assume P is_S-P_arc;
  then consider p1,p2 such that A1: P is_an_arc_of p1,p2 by Th35;
  ex f being Function of I[01], (TOP-REAL 2)|P st f is_homeomorphism &
  f.0 = p1 & f.1 = p2 by A1,Def2;
  hence thesis;
end;

:: Moved from JORDAN1A, AK, 23.02.2006

scheme TRSubsetEx { n() -> Element of NAT, P[set] } :
 ex A being Subset of TOP-REAL n() st
  for p being Point of TOP-REAL n() holds p in A iff P[p]
proof
    consider A being set such that
A1:  for x being set holds x in A iff x in the carrier of TOP-REAL n() & P[x]
      from XBOOLE_0:sch 1;
        A c= the carrier of TOP-REAL n()
    proof
      let x be set;
      assume x in A;
      hence thesis by A1;
    end;
    then reconsider A as Subset of TOP-REAL n();
    take A;
    thus thesis by A1;
  end;

scheme TRSubsetUniq { n() -> Element of NAT, P[set] } :
 for A, B being Subset of TOP-REAL n() st
  (for p being Point of TOP-REAL n() holds p in A iff P[p]) &
  (for p being Point of TOP-REAL n() holds p in B iff P[p])
 holds A = B
proof
    let A, B be Subset of TOP-REAL n() such that
A1:   for p being Point of TOP-REAL n() holds p in A iff P[p] and
A2:   for p being Point of TOP-REAL n() holds p in B iff P[p];
    hereby
      let x be set;
      assume
A3:     x in A;
      then P[x] by A1;
      hence x in B by A2,A3;
    end;
    let x be set;
    assume
A4:   x in B;
    then P[x] by A2;
    hence x in A by A1,A4;
  end;

definition let p be Point of TOP-REAL 2;
 func north_halfline p -> Subset of TOP-REAL 2 means :Def12:
  for x being Point of TOP-REAL 2 holds
   x in it iff x`1 = p`1 & x`2 >= p`2;
existence
 proof
   defpred P[Point of TOP-REAL 2] means $1`1 = p`1 & $1`2 >= p`2;
   thus ex IT being Subset of TOP-REAL 2 st
  for x being Point of TOP-REAL 2 holds
   x in IT iff P[x] from TRSubsetEx;
 end;
uniqueness
 proof
defpred P[Point of TOP-REAL 2] means $1`1 = p`1 & $1`2 >= p`2;
   thus for a,b being Subset of TOP-REAL 2 st
  (for x being Point of TOP-REAL 2 holds
   x in a iff P[x]) &
   (for x being Point of TOP-REAL 2 holds
   x in b iff P[x]) holds a=b from TRSubsetUniq;
 end;

 func east_halfline p -> Subset of TOP-REAL 2 means :Def13:
  for x being Point of TOP-REAL 2 holds
   x in it iff x`1 >= p`1 & x`2 = p`2;
existence
 proof
   defpred P[Point of TOP-REAL 2] means $1`1 >= p`1 & $1`2 = p`2;
   thus ex IT being Subset of TOP-REAL 2 st
  for x being Point of TOP-REAL 2 holds
   x in IT iff P[x] from TRSubsetEx;
 end;
uniqueness
 proof
   defpred P[Point of TOP-REAL 2] means $1`1 >= p`1 & $1`2 = p`2;
   thus for a,b being Subset of TOP-REAL 2 st
  (for x being Point of TOP-REAL 2 holds
   x in a iff P[x]) &
  (for x being Point of TOP-REAL 2 holds
   x in b iff P[x]) holds a=b from TRSubsetUniq;
 end;

 func south_halfline p -> Subset of TOP-REAL 2 means :Def14:
  for x being Point of TOP-REAL 2 holds
   x in it iff x`1 = p`1 & x`2 <= p`2;
existence
 proof
   defpred P[Point of TOP-REAL 2] means $1`1 = p`1 & $1`2 <= p`2;
   thus ex IT being Subset of TOP-REAL 2 st
  for x being Point of TOP-REAL 2 holds
   x in IT iff P[x] from TRSubsetEx;
 end;
uniqueness
 proof
   defpred P[Point of TOP-REAL 2] means $1`1 = p`1 & $1`2 <= p`2;
   thus for a,b being Subset of TOP-REAL 2 st
  (for x being Point of TOP-REAL 2 holds
   x in a iff P[x]) &
  (for x being Point of TOP-REAL 2 holds
   x in b iff P[x]) holds a=b from TRSubsetUniq;
 end;

 func west_halfline p -> Subset of TOP-REAL 2 means :Def15:
  for x being Point of TOP-REAL 2 holds
   x in it iff x`1 <= p`1 & x`2 = p`2;
existence
 proof
   defpred P[Point of TOP-REAL 2] means $1`1 <= p`1 & $1`2 = p`2;
   thus ex IT being Subset of TOP-REAL 2 st
  for x being Point of TOP-REAL 2 holds
   x in IT iff P[x] from TRSubsetEx;
 end;
uniqueness
 proof
   defpred P[Point of TOP-REAL 2] means $1`1 <= p`1 & $1`2 = p`2;
   thus for a,b being Subset of TOP-REAL 2 st
  (for x being Point of TOP-REAL 2 holds
   x in a iff P[x]) &
  (for x being Point of TOP-REAL 2 holds
   x in b iff P[x]) holds a=b from TRSubsetUniq;
 end;
end;

theorem
 north_halfline p =
   {q where q is Point of TOP-REAL 2: q`1 = p`1 & q`2 >= p`2}
  proof
    set A = {q where q is Point of TOP-REAL 2: q`1 = p`1 & q`2 >= p`2};
    hereby
      let x be set;
      assume
A1:     x in north_halfline p;
      then reconsider q = x as Point of TOP-REAL 2;
          q`1 = p`1 & q`2 >= p`2 by A1,Def12;
      hence x in A;
    end;
    let x be set;
    assume x in A;
    then ex q being Point of TOP-REAL 2 st x = q & q`1 = p`1 & q`2 >= p`2;
    hence thesis by Def12;
  end;

theorem Th38:
 north_halfline p =
   { |[ p`1,r ]| where r is Element of REAL: r >= p`2 }
  proof
    set A = {|[ p`1,r ]| where r is Element of REAL: r >= p`2};
    hereby
      let x be set;
      assume
A1:     x in north_halfline p;
      then reconsider q = x as Point of TOP-REAL 2;
A2:   q`1 = p`1 & q`2 >= p`2 by A1,Def12;
          q = |[q`1, q`2]| by EUCLID:57;
      hence x in A by A2;
    end;
    let x be set;
    assume x in A;
    then consider r being Element of REAL such that
A3:   x = |[p`1,r]| and
A4:   r >= p`2;
    reconsider q = x as Point of TOP-REAL 2 by A3;
        q`1 = p`1 & q`2 = r by A3,EUCLID:56;
    hence thesis by A4,Def12;
  end;

theorem
 east_halfline p =
   {q where q is Point of TOP-REAL 2: q`1 >= p`1 & q`2 = p`2}
  proof
    set A = {q where q is Point of TOP-REAL 2: q`1 >= p`1 & q`2 = p`2};
    hereby
      let x be set;
      assume
A1:     x in east_halfline p;
      then reconsider q = x as Point of TOP-REAL 2;
          q`1 >= p`1 & q`2 = p`2 by A1,Def13;
      hence x in A;
    end;
    let x be set;
    assume x in A;
    then ex q being Point of TOP-REAL 2 st x = q & q`1 >= p`1 & q`2 = p`2;
    hence thesis by Def13;
  end;

theorem Th40:
 east_halfline p =
   { |[ r,p`2 ]| where r is Element of REAL: r >= p`1 }
  proof
    set A = {|[ r,p`2 ]| where r is Element of REAL: r >= p`1};
    hereby
      let x be set;
      assume
A1:     x in east_halfline p;
      then reconsider q = x as Point of TOP-REAL 2;
A2:   q`1 >= p`1 & q`2 = p`2 by A1,Def13;
          q = |[q`1, q`2]| by EUCLID:57;
      hence x in A by A2;
    end;
    let x be set;
    assume x in A;
    then consider r being Element of REAL such that
A3:   x = |[r,p`2]| and
A4:   r >= p`1;
    reconsider q = x as Point of TOP-REAL 2 by A3;
        q`1 = r & q`2 = p`2 by A3,EUCLID:56;
    hence thesis by A4,Def13;
  end;

theorem
 south_halfline p =
   {q where q is Point of TOP-REAL 2: q`1 = p`1 & q`2 <= p`2}
  proof
    set A = {q where q is Point of TOP-REAL 2: q`1 = p`1 & q`2 <= p`2};
    hereby
      let x be set;
      assume
A1:     x in south_halfline p;
      then reconsider q = x as Point of TOP-REAL 2;
          q`1 = p`1 & q`2 <= p`2 by A1,Def14;
      hence x in A;
    end;
    let x be set;
    assume x in A;
    then ex q being Point of TOP-REAL 2 st x = q & q`1 = p`1 & q`2 <= p`2;
    hence thesis by Def14;
  end;

theorem Th42:
 south_halfline p =
   { |[ p`1,r ]| where r is Element of REAL: r <= p`2 }
  proof
    set A = {|[ p`1,r ]| where r is Element of REAL: r <= p`2};
    hereby
      let x be set;
      assume
A1:     x in south_halfline p;
      then reconsider q = x as Point of TOP-REAL 2;
A2:   q`1 = p`1 & q`2 <= p`2 by A1,Def14;
          q = |[q`1, q`2]| by EUCLID:57;
      hence x in A by A2;
    end;
    let x be set;
    assume x in A;
    then consider r being Element of REAL such that
A3:   x = |[p`1,r]| and
A4:   r <= p`2;
    reconsider q = x as Point of TOP-REAL 2 by A3;
        q`1 = p`1 & q`2 = r by A3,EUCLID:56;
    hence thesis by A4,Def14;
  end;

theorem
 west_halfline p =
   {q where q is Point of TOP-REAL 2: q`1 <= p`1 & q`2 = p`2}
  proof
    set A = {q where q is Point of TOP-REAL 2: q`1 <= p`1 & q`2 = p`2};
    hereby
      let x be set;
      assume
A1:     x in west_halfline p;
      then reconsider q = x as Point of TOP-REAL 2;
          q`1 <= p`1 & q`2 = p`2 by A1,Def15;
      hence x in A;
    end;
    let x be set;
    assume x in A;
    then ex q being Point of TOP-REAL 2 st x = q & q`1 <= p`1 & q`2 = p`2;
    hence thesis by Def15;
  end;

theorem Th44:
 west_halfline p =
   { |[ r,p`2 ]| where r is Element of REAL: r <= p`1 }
  proof
    set A = {|[ r,p`2 ]| where r is Element of REAL: r <= p`1};
    hereby
      let x be set;
      assume
A1:     x in west_halfline p;
      then reconsider q = x as Point of TOP-REAL 2;
A2:   q`1 <= p`1 & q`2 = p`2 by A1,Def15;
          q = |[q`1, q`2]| by EUCLID:57;
      hence x in A by A2;
    end;
    let x be set;
    assume x in A;
    then consider r being Element of REAL such that
A3:   x = |[r,p`2]| and
A4:   r <= p`1;
    reconsider q = x as Point of TOP-REAL 2 by A3;
        q`1 = r & q`2 = p`2 by A3,EUCLID:56;
    hence thesis by A4,Def15;
  end;

registration let p be Point of TOP-REAL 2;
 cluster north_halfline p -> non empty;
coherence
  proof
 north_halfline p =
      { |[ p`1,r ]| where r is Element of REAL: r >= p`2 } by Th38;
    then |[p`1,p`2]| in north_halfline p;
    hence thesis;
  end;
 cluster east_halfline p -> non empty;
coherence
  proof
 east_halfline p =
      { |[ r,p`2 ]| where r is Element of REAL: r >= p`1 } by Th40;
    then |[p`1,p`2]| in east_halfline p;
    hence thesis;
  end;
 cluster south_halfline p -> non empty;
coherence
  proof
 south_halfline p =
      { |[ p`1,r ]| where r is Element of REAL: r <= p`2 } by Th42;
    then |[p`1,p`2]| in south_halfline p;
    hence thesis;
  end;
 cluster west_halfline p -> non empty;
coherence
  proof
 west_halfline p =
      { |[ r,p`2 ]| where r is Element of REAL: r <= p`1 } by Th44;
    then |[p`1,p`2]| in west_halfline p;
    hence thesis;
  end;
end;

:: Moved from JORDAN1C, AK, 23.02.2006

theorem
  p in west_halfline p & p in east_halfline p &
  p in north_halfline p & p in south_halfline p
  proof
    p`1 <= p`1 & p`2 = p`2;
    hence thesis by Def12,Def13,Def14,Def15;
  end;

