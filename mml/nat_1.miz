:: The Fundamental Properties of Natural Numbers
::  by Grzegorz Bancerek
::
:: Received January 11, 1989
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies ORDINAL2, ARYTM, ARYTM_1, ARYTM_3, ORDINAL1, NAT_1, XREAL_0,
      ZF_LANG;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0,
      REAL_1, XXREAL_0;
 constructors NUMBERS, XCMPLX_0, XXREAL_0, XREAL_0, ORDINAL2;
 registrations REAL_1, NUMBERS, ORDINAL2, XREAL_0, ARYTM_3, ZFMISC_1, XBOOLE_0,
      XXREAL_0, ORDINAL1;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions REAL_1;
 theorems AXIOMS, REAL_1, ORDINAL1, XCMPLX_1, XREAL_1, XXREAL_0;
 schemes REAL_1;

begin

definition
  mode Nat is natural number;
end;

 reserve x for Real,
         p,k,l,m,n,s,h,i,j,k1,t,t1 for Nat,
         X for Subset of REAL;

:: The results of axioms of natural numbers

canceled;

theorem                       :: axiom of induction
 Th2: for X st 0 in X & for x st x in X holds x + 1 in X
  for n holds n in X
   proof let A be Subset of REAL such that
A1:  0 in A;
     assume x in A implies x + 1 in A;
     then for x being real number st x in A holds x + 1 in A;
then A2:  NAT c= A by A1,AXIOMS:29;
    let n;
    n in NAT by ORDINAL1:def 13;
    hence n in A by A2;
   end;

:: Addition and multiplication

:: The natural numbers are real numbers therefore some theorems of real
:: numbers are translated for natural numbers.

definition let n,k be Element of NAT;
  redefine func n + k -> Element of NAT;
  coherence
    proof
     defpred P[Real] means ex k st $1 = k & n + k is Element of NAT;
     consider X such that
A1:  x in X iff P[x] from REAL_1:sch 1;
     n + 0 = n;
     then A2:    0 in X by A1;
     now let x; assume x in X;
       then consider k such that
A3:    x = k & n + k is Element of NAT by A1;
A4:    (n + k) + 1 = n + (k + 1);
       reconsider k as Element of NAT by ORDINAL1:def 13;
       k + 1 is Element of NAT & (n + k) + 1 is Element of NAT
       by A3,AXIOMS:28;
       hence x + 1 in X by A1,A3,A4;
     end;
     then k in X by A2,Th2;
     then ex m st k = m & n + m is Element of NAT by A1;
     hence thesis;
   end;
 end;

registration let n,k be Nat;
  cluster n + k -> natural;
  coherence
  proof
    reconsider n,k as Element of NAT by ORDINAL1:def 13;
    n+k is Element of NAT;
    hence thesis;
  end;
end;

:: Now we can form and prove the scheme of induction.

scheme Ind { P[Nat] } :
 for k being Element of NAT holds P[k]
   provided
A1:   P[0] and
A2:   for k being Element of NAT st P[k] holds P[k + 1]
proof let k;
  defpred P1[Real] means ex k st P[k] & k = $1;
    consider X such that
A3:   x in X iff P1[x] from REAL_1:sch 1;
A4:   0 in X by A1,A3;
      for x st x in X holds x + 1 in X
      proof let x; assume x in X;
       then consider k such that
A5:      P[k] & k = x by A3;
         reconsider k as Element of NAT by ORDINAL1:def 13;
         P[k + 1] by A2,A5;
       hence thesis by A3,A5;
      end;
     then k in X by A4,Th2;
     then ex n st P[n] & n = k by A3;
    hence thesis;
   end;

scheme NatInd { P[Nat] } :
 for k being Nat holds P[k]
   provided
A1:   P[0] and
A2:   for k be Nat st P[k] holds P[k + 1]
proof
A3: P[0] by A1;
A4:   for k be Element of NAT st P[k] holds P[k + 1] by A2;
A5:   for k be Element of NAT holds P[k] from Ind(A3,A4);
      let k be Nat;
      k is Element of NAT by ORDINAL1:def 13;
      hence thesis by A5;
   end;

:: Like addition, the result of multiplication of two natural numbers is
:: a natural number.

definition let n,k be Element of NAT;
  redefine func n * k -> Element of NAT;
  coherence
  proof   defpred P[Nat] means n*$1 is Element of NAT;
A1: P[0];
A2: for m be Element of NAT holds P[m] implies P[m+1]
    proof
      let m be Element of NAT;
      assume P[m];
      then reconsider k = n * m as Element of NAT;
      k + n is Element of NAT;
      hence n * (m + 1) is Element of NAT;
    end;
    for m be Element of NAT holds P[m] from Ind(A1,A2);
    hence thesis;
  end;
end;

registration let n,k be Nat;
  cluster n * k -> natural;
  coherence
  proof
    reconsider n,k as Element of NAT by ORDINAL1:def 13;
    n*k is Element of NAT;
    hence thesis;
  end;
end;

                ::::::::::::::::::::
                :: Order relation ::
                ::::::::::::::::::::

:: Some theorems of not great relation "<=" in real numbers are translated
:: to natural number easy and it is necessary to have them here.

canceled 15;

theorem Th18:
  0 <= i
  proof
     defpred P[Nat] means 0 <= $1;
A1:  P[0];
A2:  now let n; assume P[n];
      then 0 + 1 <= n + 1 by XREAL_1:8;
      hence P[n + 1];
     end;
   for k holds P[k] from NatInd(A1,A2);
   hence thesis;
  end;

theorem
  0 <> i implies 0 < i by Th18;

theorem Th20:
  i <= j implies i * h <= j * h
  proof assume
A1:  i <= j;
   0 <= h by Th18;
   hence thesis by A1,XREAL_1:66;
  end;

theorem
  0 < i + 1
  proof assume
A1: 0 >= i + 1;
    0 <= i by Th18;
    then 0 + 1 <= 0 by A1,XREAL_1:9;
    hence contradiction;
  end;

theorem Th22:
  i = 0 or ex k st i = k + 1
  proof
    defpred P[Nat] means $1 = 0 or ex k st $1 = k + 1;
A1: P[0];
A2: P[h] implies P[h + 1];
    for i holds P[i] from NatInd(A1,A2);
    hence thesis;
  end;

theorem Th23:
  i + j = 0 implies i = 0 & j = 0
  proof assume
A1:  i + j = 0;
   0 <= i & 0 <= j by Th18;
   then 0 + i <= j + i & 0 + j <= i + j & 0 + i = i & 0 + j = j by XREAL_1:9;
   hence thesis by A1,Th18;
  end;

registration
  cluster non zero Nat;
  existence
  proof
    take 1;
    thus thesis;
  end;
end;

registration let m be Nat, n be non zero Nat;
  cluster m + n -> non zero;
  coherence by Th23;
  cluster n + m -> non zero;
  coherence;
end;

scheme DefbyInd { N()->Nat, F(Nat,Nat)->Nat, P[Nat,Nat] } :
  (for k being Element of NAT ex n being Element of NAT st P[k,n]) &
    for k,n,m being Element of NAT st P[k,n] & P[k,m] holds n = m
     provided
A1:    for k,n being Element of NAT holds P[k,n] iff
       k = 0 & n = N() or
       ex m,l being Element of NAT st k = m + 1 & P[m,l] & n = F(k,l)
  proof
    reconsider N = N() as Element of NAT by ORDINAL1:def 13;
      defpred P[Nat] means ex n being Element of NAT st P[$1,n];
      P[0,N] by A1;
then A2:   P[0];
A3: for k being Element of NAT holds P[k] implies P[k+1]
      proof let k be Element of NAT;
        given n being Element of NAT such that
A4:    P[k,n];
       reconsider F = F(k+1,n) as Element of NAT by ORDINAL1:def 13;
       take F;
       thus P[k+1,F] by A1,A4;
      end;
    thus for k being Element of NAT holds P[k] from Ind(A2,A3);
    defpred P[Nat] means
    for n,m being Element of NAT st P[$1,n] & P[$1,m] holds n = m;
A5:   P[0]
      proof let n,m be Element of NAT such that
A6:     P[0,n] & P[0,m];
        (not ex m,l being Element of NAT st 0 = m + 1 & P[m,l] & n = F(0,l) ) &
        not ex n,l being Element of NAT st 0 = n + 1 & P[n,l] & m = F(0,l);
        then n = N() & m = N() by A1,A6;
       hence n = m;
      end;
A7:   for k being Element of NAT st P[k] holds P[k+1]
      proof let k be Element of NAT such that
A8:      for n,m being Element of NAT st P[k,n] & P[k,m] holds n = m;
       let n,m be Element of NAT such that
A9:      P[k+1,n] & P[k+1,m];
       consider l,u being Element of NAT such that
A10:      k + 1 = l + 1 & P[l,u] & n = F(k + 1,u) by A1,A9;
        ex v,w being Element of NAT  st k + 1 = v + 1 & P[v,w] & m = F(k + 1,w)
         by A1,A9;
       hence n = m by A8,A10;
      end;
    thus for k being Element of NAT holds P[k] from Ind(A5,A7);
   end;

canceled 2;

theorem Th26:
  i <= j + 1 implies i <= j or i = j + 1
  proof
     defpred P[Nat] means
     for j holds $1 <= j + 1 implies $1 <= j or $1 = j+1;
A1:  P[0] by Th18;
A2:  for i st P[i] holds P[i+1]
     proof let i such that
A3:     for j holds i <= j + 1 implies i <= j or i = j + 1;
        let j;
        assume that
A4:     i + 1 <= j + 1;
A5:     now assume
A6:       j = 0;
then A7:       i <= 0 by A4,XREAL_1:8;
            0 <= i by Th18;
        hence thesis by A6,A7,XXREAL_0:1;
       end;
        now given k such that
A8:       j = k + 1;
         i <= k + 1 by A4,A8,XREAL_1:8;
         then i <= k or i = k + 1 by A3;
        hence thesis by A8,XREAL_1:8;
       end;
     hence thesis by A5,Th22;
    end;
    for i holds P[i] from NatInd(A1,A2);
    hence thesis;
  end;

theorem
  i <= j & j <= i + 1 implies i = j or j = i + 1
  proof assume
A1:  i <= j & j <= i + 1;
     then j <= i or j = i + 1 by Th26;
     hence thesis by A1,XXREAL_0:1;
  end;

theorem Th28:
  i <= j implies ex k st j = i + k
  proof
     defpred P[Nat] means
     i <= $1 implies ex k st $1 = i + k;
A1: P[0]
     proof assume
A2:   i <= 0;
      take 0;
      thus thesis by A2,Th18;
     end;
A3:  for j st P[j] holds P[j+1]
     proof let j such that
A4:   i <= j implies ex k st j = i + k;
      assume
A5:     i <= j + 1;
A6:     now assume i <= j;
        then consider k such that
A7:     j = i + k by A4;
        i + k + 1 = i + (k + 1);
        hence thesis by A7;
       end;
      now assume i = j + 1; then j + 1 = i + 0;
        hence thesis;
       end;
      hence thesis by A5,A6,Th26;
     end;
   for j holds P[j] from NatInd(A1,A3);
   hence thesis;
  end;

theorem Th29:
  i <= i + j
  proof
A1:  0 + i = i;
   0 <= j by Th18;
   hence thesis by A1,XREAL_1:9;
  end;

scheme CompInd { P[Nat] } :
  for k being Element of NAT holds P[k]
   provided
A1:   for k being Element of NAT st
      for n being Element of NAT st n < k holds P[n] holds P[k]
  proof
      defpred P1[Nat] means
      for n being Element of NAT st n < $1 holds P[n];
A2:   P1[0] by Th18;
A3:   for k be Element of NAT holds P1[k] implies P1[k+1]
      proof
        let k be Element of NAT;
        assume
A4:   for n being Element of NAT st n < k holds P[n];
      let n be Element of NAT; assume n < k + 1;
      then n <= k by Th26;
      then n < k or n = k & n <= k by REAL_1:def 5;
      hence thesis by A1,A4;
    end;
    let k be Element of NAT;
    for k being Element of NAT holds P1[k] from Ind(A2,A3);
    then for n being Element of NAT st n < k holds P[n];
    hence thesis by A1;
   end;

:: Principle of minimum

scheme Min { P[Nat] } :
 ex k being Element of NAT st P[k] &
 for n being Element of NAT st P[n] holds k <= n
  provided
A1:  ex k being Element of NAT st P[k]
proof assume A2: not thesis;
      defpred P1[Nat] means not P[$1];
A3:   for k being Element of NAT st
      for n being Element of NAT st n < k holds P1[n] holds P1[k]
      proof let k be Element of NAT;
A4:    not (ex n being Element of NAT st P[n] & not k <= n) implies not P[k]
       by A2;
       assume for n being Element of NAT st n < k holds not P[n];
       hence thesis by A4;
      end;
      for k be Element of NAT holds P1[k] from CompInd(A3);
    hence contradiction by A1;
   end;

:: Principle of maximum

scheme Max { P[Nat], N()->Nat } :
  ex k being Element of NAT st P[k] &
  for n being Element of NAT st P[n] holds n <= k
   provided
A1:   for k being Element of NAT st P[k] holds k <= N() and
A2:   ex k being Element of NAT st P[k]
  proof
      defpred P1[Nat] means
      for n being Element of NAT st P[n] holds n <= $1;
      N() in NAT by ORDINAL1:def 13;
      then
A3:   ex k being Element of NAT st P1[k] by A1;
    consider k being Element of NAT such that
A4:   P1[k] &
      for m being Element of NAT st P1[m] holds k <= m from Min(A3);
    take k;
    thus P[k]
      proof assume
A5:      not P[k];
       consider n being Element of NAT such that
A6:      P[n] by A2;
        n <= k & n <> k by A4,A5,A6;
       then k <> 0 by Th18;
       then consider m such that
A7:      k = m + 1 by Th22;
         reconsider m as Element of NAT by ORDINAL1:def 13;
        now let n be Element of NAT; assume P[n];
         then n <= k & n <> k by A4,A5;
         hence n <= m by A7,Th26;
        end;
        then A8:    k <= m by A4;
       (- m) + m + 1 = (- m) + (m + 1) & (- m) + m + 0 = (- m) + (m + 0);
       hence contradiction by A7,A8,XREAL_1:8;
      end;
    thus thesis by A4;
   end;

canceled 7;

theorem Th37:
  i <= j implies i <= j + h
  proof assume
A1:  i <= j;
       0 <= h by Th18;
    then i + 0 <= i + h & i + h <= j + h & i + 0 = i by A1,XREAL_1:9;
   hence i <= j + h by XXREAL_0:2;
  end;

theorem Th38:
  i < j + 1 iff i <= j
  proof
   thus i < j + 1 implies i <= j by Th26;
   assume
A1:  i <= j;
   hence i <= j + 1 by Th37;
   assume A2: i = j + 1;
A3:  j + (- j) = 0;
   j + 1 + (- j) = 1;
   hence contradiction by A1,A2,A3,XREAL_1:8;
  end;

theorem Th39:
  j < 1 implies j = 0
 proof
  assume j < 1;
    then j < 0 + 1;
    then A1: j <= 0 by Th38;
   assume j <> 0;
  hence thesis by A1,Th18;
 end;

theorem Th40:
  i * j = 1 implies i = 1 & j = 1
  proof assume
A1:  i * j = 1;
    then i <> 0;
   then consider m such that
A2:  i = m + 1 by Th22;
    j <> 0 by A1;
   then consider l such that
A3:  j = l + 1 by Th22;
    m * l + m + l + 1 = 0 + 1 by A1,A2,A3;
    then m * l + m = 0 & l = 0 by Th23;
   hence thesis by A2,A3;
  end;

theorem Th41:
  k <> 0 implies n < n + k
 proof
  assume A1: k <> 0;
   A2: n <> n + k by A1;
    n <= n + k & n <= k + n by Th37;
  hence n < n + k by A2,REAL_1:def 5;
 end;

scheme Regr { P[Nat] } :
  P[0]
  provided
A1:  ex k being Element of NAT st P[k] and
A2:  for k being Element of NAT st k <> 0 & P[k]
     ex n being Element of NAT st n < k & P[n]
proof
A3: ex k being Element of NAT st P[k] by A1;
   consider k being Element of NAT such that
A4:  P[k] & for n being Element of NAT st P[n] holds k <= n from Min(A3);
       now assume k <> 0;
      then ex n being Element of NAT st n < k & P[n] by A2,A4;
     hence contradiction by A4;
    end;
   hence P[0] by A4;
  end;

:: Exact division and rest of division

theorem Th42:
  0 < m implies for n ex k,t st n = (m*k)+t & t < m
   proof
    assume
A1:   0 < m;
     defpred P[Nat] means ex k,t st $1 = (m*k)+t & t < m;
        0 = m*0+0;
then A2:   P[0] by A1;
A3:   for n st P[n] holds P[n+1]
      proof let n;
       given k1,t1 such that
A4:     n = (m*k1)+t1 & t1 < m;
A5:        t1+1 <= m by A4,Th38;
A6:     t1+1 < m implies ex k,t st n+1 = (m*k)+t & t < m
         proof assume
A7:        t1+1 < m;
          take k = k1 , t = t1+1;
          thus n+1 = m*k+t by A4;
          thus t < m by A7;
         end;
           t1+1 = m implies ex k,t st n+1 = (m*k)+t & t < m
         proof assume
A8:        t1+1 = m;
          take k = k1+1 , t = 0;
          thus n+1 = m*k+t by A4,A8;
          thus t < m by A1;
         end;
       hence ex k,t st n+1 = (m*k)+t & t < m by A5,A6,REAL_1:def 5;
      end;
    thus for n holds P[n] from NatInd(A2,A3);
   end;

theorem Th43:
  n = m*k+t & t < m & n = m*k1+t1 & t1 < m implies k = k1 & t = t1
   proof
    assume
A1:   n = m*k+t & t < m & n = m*k1+t1 & t1 < m;
A2:   now assume k <= k1;
      then consider u being Nat such that
A3:     k1 = k + u by Th28;
          m * (k + u) + t1 = m * k + (m * u + t1);
then A4:     m * u + t1 = t by A1,A3,XCMPLX_1:2;
          now given w being Nat such that
A5:       u = w + 1;
          m * u + t1 = m + (m * w + t1) by A5;
        hence contradiction by A1,A4,Th29;
       end;
then A6:     u = 0 by Th22;
      hence k = k1 by A3;
      thus t = t1 by A1,A3,A6,XCMPLX_1:2;
     end;
        now assume k1 <= k;
      then consider u being Nat such that
A7:     k = k1 + u by Th28;
          m * (k1 + u) + t = m * k1 + (m * u + t);
then A8:     m * u + t = t1 by A1,A7,XCMPLX_1:2;
          now given w being Nat such that
A9:       u = w + 1;
          m * u + t = m + (m * w + t) by A9;
        hence contradiction by A1,A8,Th29;
       end;
then A10:     u = 0 by Th22;
      hence k = k1 by A7;
       thus t = t1 by A1,A7,A10,XCMPLX_1:2;
     end;
    hence thesis by A2;
   end;

definition let k,l be Nat;
  func k div l -> Nat means       :: the exact division
:Def1:  ( ex t being Nat st k = l * it + t & t < l ) or it = 0 & l = 0;
   existence
    proof
      per cases;
      suppose l <> 0;
        then 0 < l by Th18;
       hence thesis by Th42;
     end;
      suppose l = 0;
        hence thesis;
      end;
    end;
   uniqueness
    proof let t1,t2 be Nat such that
A1:   (ex t being Nat st k = l * t1 + t & t < l) or t1 = 0 & l = 0 and
A2:   (ex t being Nat st k = l * t2 + t & t < l) or t2 = 0 & l = 0;
      now let t; assume
A3:     t < 0;
        then ex m st 0 = t + m by Th28;
       hence contradiction by A3,Th23;
      end;
     hence thesis by A1,A2,Th43;
    end;

  func k mod l -> Nat means         :: the rest of division
:Def2: ( ex t being Nat st k = l * t + it & it < l ) or it = 0 & l = 0;
   existence
    proof
       now assume
A4:    l <> 0;
       0 <= l by Th18;
       then ex n,t st k = l * n + t & t < l by A4,Th42;
       hence thesis;
      end;
     hence thesis;
    end;
   uniqueness
    proof let t1,t2 be Nat such that
A5:  (ex t being Nat st k = l * t + t1 & t1 < l) or t1 = 0 & l = 0 and
A6:  (ex t being Nat st k = l * t + t2 & t2 < l) or t2 = 0 & l = 0;
      now let t; assume
A7:     t < 0;
        then ex m st 0 = t + m by Th28;
       hence contradiction by A7,Th23;
      end;
     hence thesis by A5,A6,Th43;
    end;
 end;

definition let k,l be Nat;
  redefine func k div l -> Element of NAT;
  coherence by ORDINAL1:def 13;
  redefine func k mod l -> Element of NAT;
  coherence by ORDINAL1:def 13;
end;

canceled 2;

theorem Th46:
  0 < i implies j mod i < i
   proof assume 0 < i;
     then ex t being Nat st j = i * t + (j mod i) & j mod i < i by Def2;
    hence j mod i < i;
   end;

theorem Th47:
  0 < i implies j = i * (j div i) + (j mod i)
   proof assume 0 < i;
     then ex k being Nat st j = i * k + (j mod i) & j mod i < i by Def2;
    hence j = i * (j div i) + (j mod i) by Def1;
   end;

:: The divisibility relation

definition let k,l be Nat;
  pred k divides l means
:Def3:  ex t being Nat st l = k * t;
  reflexivity
   proof let i;
    i = i * 1;
    hence thesis;
   end;
end;

canceled;

theorem Th49:
  j divides i iff i = j * (i div j)
   proof
     thus j divides i implies i = j * (i div j)
     proof assume j divides i;
       then consider k being Nat such that
A1:    i = j * k by Def3;
A2:    i = j * k + 0 by A1;
       now assume
A3:      j <> 0;
A4:      0 <= j by Th18;
then A5: i = j * (i div j) + (i mod j) by A3,Th47;
         i mod j < j by A3,A4,Th46;
         hence i = j * (i div j) by A2,A3,A4,A5,Th43;
       end;
       hence i = j * (i div j) by A1;
     end;
     assume i = j * (i div j);
     hence j divides i by Def3;
   end;

canceled;

theorem
  i divides j & j divides h implies i divides h
  proof assume i divides j & j divides h;
    then j = i * (j div i) & h = j * (h div j) by Th49;
    then h = i * ((j div i)*(h div j));
    hence i divides h by Def3;
   end;

theorem Th52:
  i divides j & j divides i implies i = j
   proof
    assume i divides j & j divides i;
then A1:   j = i * (j div i) & i = j * (i div j) by Th49;
then A2:     i * 1 = i * ((j div i) * (i div j));
A3:   i = 0 implies i = j by A1;
        (j div i) * (i div j) = 1 implies i = j
      proof assume (j div i) * (i div j) = 1;
        then j div i = 1 by Th40;
       hence i = j by A1;
      end;
    hence i = j by A2,A3,XCMPLX_1:5;
   end;

theorem Th53:
  i divides 0 & 1 divides i
  proof
   0 = i * 0;
   hence i divides 0 by Def3;
   i = i * 1;
   hence thesis by Def3;
  end;

theorem Th54:
  0 < j & i divides j implies i <= j
   proof assume
A1:   0 < j & i divides j;
then A2:   j = i * (j div i) by Th49;
     then j div i <> 0 by A1;
    then consider m such that
A3:   j div i = m + 1 by Th22;
        i * (m + 1) = i + i * m;
    hence i <= j by A2,A3,Th29;
   end;

theorem Th55:
  i divides j & i divides h implies i divides j+h
   proof
    assume i divides j & i divides h;
   then j = i * (j div i) & h = i * (h div i) by Th49;
     then j + h = i * ((j div i) + (h div i));
    hence i divides j + h by Def3;
   end;

theorem Th56:
  i divides j implies i divides j * h
  proof assume i divides j;
   then consider l being Nat such that
A1:  j = i * l by Def3;
     i * l * h = i * (l * h);
     hence thesis by A1,Def3;
  end;

theorem Th57:
  i divides j & i divides j + h implies i divides h
  proof assume
A1:  i divides j & i divides j + h;
   then consider t being Nat such that
A2:  j + h = i * t by Def3;
   consider u being Nat such that
A3:  j = i * u by A1,Def3;
       now assume i <> 0;
      then A4: i <> 0 & 0 <= i by Th18;
then A5:    h = i * (h div i) + (h mod i) by Th47;
A6:    j + (i * (h div i) + (h mod i)) = i * (u + (h div i)) + (h mod i) by A3;
A7:    h mod i < i by A4,Th46;
         j + h = i * t + 0 by A2;
      then h mod i = 0 by A4,A5,A6,A7,Th43;
      hence thesis by A5,Th49;
    end;
   hence thesis by A2,Th23;
  end;

theorem Th58:
  i divides j & i divides h implies i divides j mod h
  proof assume
A1:  i divides j & i divides h;
A2:  now assume h = 0; then j mod h = 0 by Def2;
     hence thesis by Th53;
    end;
       now assume
A3:    h <> 0;
         0 <= h by Th18;
then A4:    j = h * (j div h) + (j mod h) by A3,Th47;
         i divides h * (j div h) by A1,Th56;
     hence thesis by A1,A4,Th57;
    end;
   hence thesis by A2;
  end;

:: The least common multiple and the greatest common divisor

definition let k,n be Nat;
  func k lcm n -> Nat means
:Def4:  k divides it & n divides it &
  for m being Nat st k divides m & n divides m holds it divides m;
   existence
    proof
A1:    now assume
A2:      k = 0 or n = 0;
       take w = 0;
       thus k divides w & n divides w by Th53;
       let m be Nat; assume k divides m & n divides m;
       hence w divides m by A2;
      end;
         now assume
A3:      k <> 0 & n <> 0;
      reconsider k1=k, n1=n as Element of NAT by ORDINAL1:def 13;
      A4:      k1 divides k1 * n1 & n1 divides k1 * n1 by Def3;
      defpred P[Nat] means k divides $1 & n divides $1 & k <= $1;
           0 <= n by Th18; then 0 + 1 <= n by A3,Th38;
        then k * 1 <= k * n by Th20;
then A5:  ex m being Element of NAT st P[m] by A4;
       consider l being Element of NAT such that
A6:      P[l] & for m being Element of NAT st P[m] holds l <= m
          from Min(A5);
       take l;
       thus k divides l & n divides l by A6;
       let m be Nat such that
A7:      k divides m & n divides m;
           now A8: 0 <= k by Th18;
then A9:        m = l * (m div l) + (m mod l) by A3,A6,Th47;
             l = k * (l div k) & l = n * (l div n) by A6,Th49;
          then l * (m div l) = k * ((l div k) * (m div l)) &
           l * (m div l) = n * ((l div n) * (m div l)) by XCMPLX_1:4;
          then k divides l * (m div l) & n divides l * (m div l) by Def3;
then A10:        k divides m mod l & n divides m mod l by A7,A9,Th57;
             now assume
A11:          m mod l <> 0;
               0 <= m mod l by Th18;
            then k <= m mod l by A10,A11,Th54;
           then l <= m mod l by A6,A10;
           hence contradiction by A3,A6,A8,Th46;
          end; hence l divides m by A9,Th49;
        end;
       hence l divides m;
      end;
     hence thesis by A1;
    end;
   uniqueness
    proof let m1,m2 be Nat such that
A12:  k divides m1 & n divides m1 & for m being Nat st k divides m & n divides
 m holds m1 divides m and
A13: k divides m2 & n divides m2 & for m being Nat st k divides m & n divides
 m holds m2 divides m;
  m1 divides m2 & m2 divides m1 by A12,A13;
     hence m1 = m2 by Th52;
    end;
    idempotence;
    commutativity;
end;

definition let k,n be Nat;
  redefine func k lcm n -> Element of NAT;
  coherence by ORDINAL1:def 13;
end;

definition let k,n be Nat;
  func k hcf n -> Nat means
:Def5:  it divides k & it divides n &
    for m being Nat st m divides k & m divides n holds m divides it;
   existence
    proof
       defpred P[Nat] means $1 divides k & $1 divides n;
         1 divides k & 1 divides n by Th53;
then A1:    ex m being Element of NAT st P[m];
A2:    now assume
A3:     k = 0;
       take m = n;
       thus m divides k & m divides n by A3,Th53;
       let l be Nat; assume l divides k & l divides n;
       hence l divides m;
      end;
      now assume
A4:     k <> 0;
        0 <= k by Th18;
        then A5: for m being Element of NAT st P[m] holds m <= k by A4,Th54;
       consider m being Element of NAT such that
A6:    P[m] & for l being Element of NAT st P[l] holds l <= m from Max(A5,A1);
       take m;
       thus m divides k & m divides n by A6;
       let l be Nat;
       assume l divides k & l divides n;
then A7:      l lcm m divides k & l lcm m divides n by A6,Def4;
then A8:      l lcm m <= m by A6;
           m <= l lcm m
         proof
A9:        m divides l lcm m by Def4;
A10:        now assume l lcm m = 0;
             then k = 0 * (k div 0) by A7,Th49;
            hence contradiction by A4;
           end;
              0 <= l lcm m by Th18;
          hence thesis by A9,A10,Th54;
         end;
       then l lcm m = m by A8,XXREAL_0:1;
       hence l divides m by Def4;
      end;
     hence thesis by A2;
    end;
   uniqueness
    proof let m1,m2 be Nat such that
A11:  m1 divides k & m1 divides n & for m being Nat st m divides k & m divides
 n holds m divides m1 and
A12: m2 divides k & m2 divides n & for m being Nat st m divides k & m divides
 n holds m divides m2;
  m1 divides m2 & m2 divides m1 by A11,A12;
     hence m1 = m2 by Th52;
    end;
    idempotence;
    commutativity;
end;

definition let k,n be Nat;
  redefine func k hcf n -> Element of NAT;
  coherence by ORDINAL1:def 13;
end;

scheme Euklides { Q(Nat)->Nat, a,b()->Nat } :
  ex n being Element of NAT st Q(n) = a() hcf b() & Q(n + 1) = 0
   provided
A1:   0 < b() & b() < a() and
A2:   Q(0) = a() & Q(1) = b() and
A3:   for n being Element of NAT holds Q(n + 2) = Q(n) mod Q(n + 1)
  proof
     defpred P[Nat] means ex n being Element of NAT st $1 = Q(n);
     a() in NAT by ORDINAL1:def 13;
     then
A4:  ex k being Element of NAT st P[k] by A2;
A5:  for k being Element of NAT st k <> 0 & P[k]
     ex n being Element of NAT st n < k & P[n]
     proof let k be Element of NAT; assume
A6:   k <> 0 & ex n being Element of NAT st k = Q(n);
      then consider n being Element of NAT such that
A7:   k = Q(n);
      reconsider Q = Q(n+1) as Element of NAT by ORDINAL1:def 13;
A8:   (n = 0 implies Q < k) & ex m st m = n+1 & Q = Q(m)
        by A1,A2,A7;
      now given m such that
A9:     n = m + 1;
A10:    0 <= Q(n) by Th18;
        reconsider m1=m as Element of NAT by ORDINAL1:def 13;
        Q(m1 + 2) = Q(m1) mod Q(m1 + 1) by A3;
        hence Q(n + 1) < k by A6,A7,A9,A10,Th46;
        take m = n + 1;
        thus Q(n + 1) =Q(m);
      end;
      hence thesis by A8,Th22;
     end;
A11:  P[0] from Regr(A4,A5);
     defpred Q[Nat] means 0 = Q($1);
A12: ex n being Element of NAT st Q[n] by A11;
   consider k being Element of NAT such that
A13:  Q[k] & for n being Element of NAT st Q[n] holds k <= n from Min(A12);
   consider n such that
A14:  k = n + 1 by A1,A2,A13,Th22;
      reconsider n as Element of NAT by ORDINAL1:def 13;
   take n;
   defpred PP[Nat] means
    Q(n) divides Q($1) & Q(n) divides Q($1 + 1);
       PP[n] by A13,A14,Th53;
then A15:  ex k being Element of NAT st PP[k];
A16:  for k being Element of NAT st k <> 0 & PP[k]
      ex m being Element of NAT st m < k & PP[m]
     proof let l be Element of NAT ; assume
A17:    l <> 0 & Q(n) divides Q(l) & Q(n) divides Q(l + 1);
      then consider m such that
A18:    l = m + 1 by Th22;
        reconsider m as Element of NAT by ORDINAL1:def 13;
      take m;
A19:    m <= m + 1 by Th29;
        m <> m + 1;
      hence m < l by A18,A19,REAL_1:def 5;
A20:  now assume
A21:    Q(m + 1) = 0;
        now assume
A22:      m + 1 <> k;
          k <= m + 1 by A13,A21;
          then k < m + 1 by A22,REAL_1:def 5;then
A23:      k <= m by Th38;
          defpred Q[Nat] means k <= $1 implies Q($1) = 0;
A24: Q[0]
     proof assume A25: k <= 0;  0 <= k by Th18;
       hence thesis by A13,A25,XXREAL_0:1;
     end;
A26: for m st Q[m] holds Q[m+1]
     proof let m such that
A27:   k <= m implies Q(m) = 0 and
A28:   k <= m + 1;
       now assume k <> m + 1; then A29: k < m + 1 by A28,REAL_1:def 5;
         then consider l such that
A30:     m = l + 1 by A1,A2,A27,Th22,Th38;
         reconsider l as Element of NAT by ORDINAL1:def 13;
         Q(l + 2) = Q(l) mod Q(l + 1) by A3;
         hence Q(m + 1) = 0 by A27,A29,A30,Def2,Th38;
       end;
       hence Q(m + 1) = 0 by A13;
     end;
     for m holds Q[m] from NatInd(A24,A26);
     then Q(m) = 0 by A23;
     hence Q(n) divides Q(m) by Th53;
   end;
   hence Q(n) divides Q(m) by A14;
 end;
 now assume
A31:   Q(m + 1) <> 0;
A32:   0 <= Q(m + 1) by Th18;
       Q(m + 2) = Q(m) mod Q(m + 1) by A3;then
A33:   Q(m) = Q(m + 1) * (Q(m) div Q(m + 1)) + Q(m + 2) by A31,A32,Th47;
       Q(n) divides Q(m + 1) * (Q(m) div Q(m + 1)) &
       Q(n) divides Q(m + 2) by A17,A18,Th56;
       hence Q(n) divides Q(m) by A33,Th55;
     end;
     hence Q(n) divides Q(m) by A20;
     thus Q(n) divides Q(m + 1) by A17,A18;
   end;
   now
     PP[0] from Regr(A15,A16);
     hence Q(n) divides a() & Q(n) divides b() by A2;
     let m;
     defpred P1[Nat] means m divides Q($1) & m divides Q($1 + 1);
     assume m divides a() & m divides b();
then A34: P1[0] by A2;
A35: for k be Element of NAT st P1[k] holds P1[k+1]
       proof let k be Element of NAT; assume
A36:   m divides Q(k) & m divides Q(k + 1);
       hence m divides Q(k + 1);
       Q(k + 2) = Q(k) mod Q(k + 1) by A3;
       hence m divides Q(k + 1 + 1) by A36,Th58;
     end;
     for k be Element of NAT holds P1[k] from Ind(A34,A35);
     hence m divides Q(n);
    end;
   hence Q(n) = a() hcf b() by Def5;
   thus Q(n + 1) = 0 by A13,A14;
  end;

registration
  cluster -> ordinal Nat;
  coherence;
end;

registration
  cluster non empty ordinal Subset of REAL;
  existence proof take NAT; thus thesis; end;
end;

theorem
  k < k + n iff 1 <= n
 proof
  thus k < k + n implies 1 <= n
   proof assume A1: k < k + n;
     assume not 1 <= n;
      then n = 0 by Th39;
    hence thesis by A1;
   end;
   assume 1 <= n;
   hence thesis by Th41;
 end;

theorem
  k < n implies n - 1 is Element of NAT
 proof
   assume k < n;
   then k + 1 <= n by Th38;
   then consider j being Nat such that A1: n = k + 1 + j by Th28;
   n - 1 = k + j by A1;
   hence thesis by ORDINAL1:def 13;
 end;

theorem Th61:
  k <= n implies n - k is Element of NAT
 proof
   assume
A1: k <= n;
   per cases by A1,REAL_1:def 5;
   suppose k < n;
   then k + 1 <= n by Th38;
   then consider j being Nat such that A2: n = k + 1 + j by Th28;
   reconsider j as Element of NAT by ORDINAL1:def 13;
   n - k = 1 + j by A2;
   hence thesis;
   end;
   suppose k = n; then
   n - k = 0;
   hence thesis;
   end;
 end;

theorem
  n mod 2 = 0 or n mod 2 = 1
 proof
  assume A1: n mod 2 <> 0;
  2 = 1 + 1 & n mod 2 < 2 by Th46;
  then n mod 2 <= 1 & n mod 2 >= 1 by A1,Th38,Th39;
  hence n mod 2 = 1 by XXREAL_0:1;
 end;

theorem
  (k * n) mod k = 0
 proof
  per cases;
    suppose k = 0;
     hence thesis by Def2;
    end;
    suppose k <> 0;
    then k * n = k * n + 0 & 0 < k by Th18;
    hence thesis by Def2;
   end;
 end;

theorem
  k > 1 implies 1 mod k = 1
 proof
  assume A1: k > 1;
  1 = k * 0 + 1;
  hence thesis by A1,Def2;
 end;

theorem
  k mod n = 0 & l = k - m * n implies l mod n = 0
 proof
  assume that A1: k mod n = 0 and A2: l = k - m * n;
  per cases;
    suppose n = 0;
     hence thesis by A1,A2;
    end;
    suppose n <> 0;
      then consider t being Nat such that
A3:    k = n * t + 0 and A4: 0 < n by A1,Def2;
       A5: l = n * (t - m) + 0 by A2,A3;
       now assume t < m + 0; then
         t - m < 0 by XREAL_1:21;
         then l < n * 0 by A4,A5,XREAL_1:70;
        hence contradiction by Th18;
       end;
       then t - m is Element of NAT by Th61;
    hence thesis by A4,A5,Def2;
   end;
 end;

theorem
  n <> 0 & k mod n = 0 & l < n implies k + l mod n = l
 proof
   assume that A1: n <> 0 and A2: k mod n = 0 and A3: l < n;
   ex t being Nat st k = n * t + 0 & 0 < n by A1,A2,Def2;
  hence thesis by A3,Def2;
 end;

theorem
  k mod n = 0 implies k + l mod n = l mod n
 proof
   assume that A1: k mod n = 0;
   per cases;
    suppose
A2:  n = 0;
     hence k + l mod n = 0 by Def2
          .= l mod n by A2,Def2;
    end;
    suppose A3: n <> 0;
     then consider m being Nat such that
A4:  k = n * m + 0 & 0 < n by A1,Def2;
     consider t being Nat such that
A5:  l = n * t + (l mod n) & l mod n < n by A3,Def2;
     k + l = n * (m + t) + (l mod n) by A4,A5;
     hence thesis by A5,Def2;
   end;
 end;

theorem
  k <> 0 implies (k * n) div k = n
  proof
    assume k <> 0;
    then k * n = k * n + 0 & 0 < k by Th18;
    hence thesis by Def1;
 end;

theorem
  k mod n = 0 implies (k + l) div n = (k div n) + (l div n)
 proof assume A1: k mod n = 0;
   per cases;
   suppose A2: n <> 0;
    A3: 0 <= n by Th18;
   then A4: k = n * (k div n) + 0 by A1,A2,Th47;
A5: ex t being Nat st l = n * t + (l mod n) & l mod n < n
      by A2,Def2;
   l = n * (l div n) + (l mod n) by A2,A3,Th47;
   then k + l = n * ((k div n) + (l div n)) + (l mod n) by A4;
  hence thesis by A5,Def1;
  end;
  suppose n = 0;
  then (k + l) div n = 0 & k div n = 0 & l div n = 0 by Def1;
  hence thesis;
  end;
 end;

begin :: Addenda

:: from ALGSEQ_1

theorem Th70:
  m < n+1 implies m < n or m = n
 proof
  assume m<n+1;
  then m<=n by Th38;
  hence thesis by REAL_1:def 5;
 end;

theorem
  k < 2 implies k = 0 or k = 1
 proof
  assume k<2;
  then k<1+1;
  then k<1 or k=1 by Th70;
  hence thesis by Th39;
 end;

registration
  cluster non zero Element of NAT;
  existence
  proof
    take 1;
    thus thesis;
  end;
end;

registration
  cluster -> non negative Element of NAT;
  coherence by Th18;
end;

registration
  cluster -> non negative Nat;
  coherence by Th18;
end;

:: from AMI_5, 2005.11.16, A.T.

theorem
  k <> 0 implies m * k div k = m
  proof
   assume k <> 0;
   then m*k = k * m + 0 & 0 < k or m = 0 & k = 0;
   hence m*k div k = m by Def1;
  end;

:: from GR_CY_1, 2005.11.16, A.T.

theorem Th73:
  m mod n = (n*k + m) mod n
 proof
  per cases;
  suppose A1: n > 0;
       m mod n = t implies (n*k + m) mod n = t
   proof
    assume m mod n = t;
    then consider q being Nat such that A2: m = n * q + t & t < n by A1,Def2;
        ex p st n*k + m = n*p + t & t < n
      proof
        reconsider p = k+q as Element of NAT by ORDINAL1:def 13;
        take p;
       thus thesis by A2;
      end;
    hence thesis by Def2;
   end;
 hence thesis;
 end;
 suppose n = 0;
 hence thesis;
end;
end;

theorem Th74:
  (p+s) mod n = ((p mod n)+s) mod n
 proof
  per cases;
  suppose n > 0;
  then (p + s) mod n = (n*(p div n) + (p mod n) + s) mod n by Th47
                .=(n*(p div n) + ((p mod n) + s)) mod n
                .=((p mod n) + s) mod n by Th73;
   hence thesis;
   end;
   suppose
A1:  n = 0;
   hence (p+s) mod n = 0 by Def2
     .= ((p mod n)+s) mod n by A1,Def2;
 end;
 end;

theorem
  (p+s) mod n = (p + ( s mod n)) mod n by Th74;

theorem Th76:
  k < n implies k mod n = k
 proof
   assume A1:k<n;
    ex p st k=n*p + k & k<n
   proof
      k=n*0 + k;
     hence thesis by A1;
   end;
  hence thesis by Def2;
 end;

theorem
  n mod n = 0
 proof
  per cases;
  suppose A1:n>0;
      ex p st n=n*p +0 & 0<n
   proof
        n=n*1+0;
    hence thesis by A1;
   end;
  hence thesis by Def2;
  end;
  suppose n = 0;
  hence thesis by Def2;
 end;
 end;

theorem
  0 = 0 mod n
  proof
    n = 0 or n > 0;
    hence thesis by Th76,Def2;
  end;

:: from JORDAN4

theorem
 i <> 0 & h = j*i implies j <= h
proof assume i<>0;
  then consider k such that
A1: i = k+1 by Th22;
 assume h=j*i;
  then h=j*k+j*1 by A1;
 hence j<=h by Th29;
end;

theorem
  i < j implies i div j=0
proof assume A1: i<j;
 per cases by Def1;
  suppose ex j1 being Nat st i=j*(i div j)+j1 & j1<j;
   then consider j1 being Nat such that
A2: i=j*(i div j)+j1 and
    j1<j;
   assume i div j<>0;
     then consider k such that
A3:   i div j = k+1 by Th22;
    i = j + (j*k+j1) by A2,A3;
   hence contradiction by A1,Th29;
  end;
  suppose i div j=0 & j=0;
   hence i div j=0;
  end;
end;

:: from SCMFSA_7, 2006.03.15, A.T.

theorem
  k < n iff 1 <= k + 1 & k + 1 <= n
 proof
   hereby assume A1: k < n;
      0 + 1 <= k + 1 by XREAL_1:8;
      hence 1 <= k + 1 & k + 1 <= n by A1,Th38;
     end;
   assume 1 <= k + 1 & k + 1 <= n;
   hence k < n by Th38;
 end;

:: from BINOM, 2006.05.28, A.T.

scheme Ind1{M() -> Nat, P[Nat]} :
  for i being Element of NAT st M() <= i holds P[i]
provided
  A1:P[M()] and
  A2:for j being Element of NAT st M() <= j holds P[j] implies P[j+1]
proof
let i be Element of NAT;
assume A3: M() <= i;
defpred Q[Nat] means P[M() + $1];
A4: Q[0] by A1;
    reconsider M = M() as Element of NAT by ORDINAL1:def 13;
A5: now let m be Element of NAT;
    assume A6: Q[m];
       M() <= M() + m by Th29;
       then P[M+m+1] by A2,A6;
    hence Q[m+1];
    end;
A7: for m being Element of NAT holds Q[m] from Ind(A4,A5);
    consider m being Nat such that
A8: i = M() + m by A3,Th28;
    m in NAT by ORDINAL1:def 13;
   hence thesis by A7,A8;
end;

:: from INT_2, 2006.05.30, AG

scheme CompInd1 { a() -> Nat, P[Nat] } :
  for k being Element of NAT st k>=a() holds P[k]
  provided
  A1: for k being Element of NAT st k>=a() &
  (for n being Element of NAT st n>=a() & n<k holds P[n]) holds P[k]
proof
     defpred P1[Nat] means
      for n being Element of NAT st (n>=a() & n<$1) holds P[n];
A2: P1[a()];
A3: for k being Element of NAT st k>=a() holds P1[k] implies P1[k+1]
     proof
      let k be Element of NAT;
      assume k>=a();
      assume A4: for n being Element of NAT st n>=a() & n<k holds P[n];
      let n be Element of NAT;
      assume A5: n>=a() & n<k+1;
      then n<=k by Th38;
      then n<k or n = k by REAL_1:def 5;
      hence thesis by A1,A4,A5;
     end;
    let k be Element of NAT;
A6: for k being Element of NAT st k>=a() holds P1[k] from Ind1(A2,A3);
    assume A7: k>=a();
    then for n being Element of NAT st (n>=a() & n<k) holds P[n] by A6;
    hence P[k] by A1,A7;
   end;

:: Moved from CQC_THE1 on 07.07.2006 by AK

theorem Th82:
  n <= 1 implies n = 0 or n = 1
 proof
  assume A1: n <= 1;
  assume A2: not (n = 0 or n = 1);
  then n < 0+1 by A1,REAL_1:def 5;
  then n <= 0 by Th38;
  hence contradiction by A2;
 end;

theorem Th83:
  n <= 2 implies n = 0 or n = 1 or n = 2
 proof
  assume A1: n <= 2;
  assume A2: not (n = 0 or n = 1 or n = 2);
  then n < 1+1 by A1,REAL_1:def 5;
  then n <= 1 by Th38;
  hence thesis by A2,Th82;
 end;

theorem Th84:
  n <= 3 implies n = 0 or n = 1 or n = 2 or n = 3
 proof
  assume A1: n <= 3;
  assume A2: not (n = 0 or n = 1 or n = 2 or n = 3);
  then n < 2+1 by A1,REAL_1:def 5;
  then n <= 2 by Th38;
  hence thesis by A2,Th83;
 end;

theorem Th85:
  n <= 4 implies n = 0 or n = 1 or n = 2 or n = 3 or n = 4
 proof
  assume A1: n <= 4;
  assume A2: not (n = 0 or n = 1 or n = 2 or n = 3 or n = 4);
  then n < 3+1 by A1,REAL_1:def 5;
  then n <= 3 by Th38;
  hence thesis by A2,Th84;
 end;

theorem Th86:
  n <= 5 implies n = 0 or n = 1 or n = 2 or n = 3 or n = 4 or n = 5
 proof
  assume A1: n <= 5;
  assume A2: not (n = 0 or n = 1 or n = 2 or n = 3 or n = 4 or n = 5);
  then n < 4+1 by A1,REAL_1:def 5;
  then n <= 4 by Th38;
  hence thesis by A2,Th85;
 end;

theorem Th87:
  n <= 6 implies n = 0 or n = 1 or n = 2 or n = 3 or n = 4 or n = 5 or n = 6
 proof
  assume A1: n <= 6;
  assume A2: not (n = 0 or n = 1 or n = 2 or n = 3 or n = 4 or n = 5 or
   n = 6);
  then n < 5+1 by A1,REAL_1:def 5;
  then n <= 5 by Th38;
  hence thesis by A2,Th86;
 end;

theorem Th88:
  n <= 7 implies n = 0 or n = 1 or n = 2 or n = 3 or n = 4 or n = 5 or n = 6
  or n = 7
 proof
  assume A1: n <= 7;
  assume A2: not (n = 0 or n = 1 or n = 2 or n = 3 or n = 4 or n = 5 or
   n = 6 or n = 7);
  then n < 6+1 by A1,REAL_1:def 5;
  then n <= 6 by Th38;
  hence thesis by A2,Th87;
 end;

theorem Th89:
  n <= 8 implies n = 0 or n = 1 or n = 2 or n = 3
  or n = 4 or n = 5 or n = 6 or n = 7 or n = 8
 proof
  assume A1: n <= 8;
  assume A2: not (n = 0 or n = 1 or n = 2 or n = 3 or n = 4 or n = 5 or
   n = 6 or n = 7 or n = 8);
  then n < 7+1 by A1,REAL_1:def 5;
  then n <= 7 by Th38;
  hence thesis by A2,Th88;
 end;

theorem
  n <= 9 implies n = 0 or n = 1 or n = 2 or n = 3
  or n = 4 or n = 5 or n = 6 or n = 7 or n = 8 or n = 9
 proof
  assume A1: n <= 9;
  assume A2: not (n = 0 or n = 1 or n = 2 or n = 3 or n = 4 or n = 5 or
   n = 6 or n = 7 or n = 8 or n = 9);
  then n < 8+1 by A1,REAL_1:def 5;
  then n <= 8 by Th38;
  hence thesis by A2,Th89;
 end;

:: compare BINARITH:sch 1, 2006.07.19, A.T.

 scheme Indfrom1 { P[Nat] } :
 for k being non empty Nat holds P[k]
   provided
   A1: P[1] and
   A2: for k being non empty Nat st P[k] holds P[k + 1]
proof
          defpred P[Nat] means $1 is non empty implies P[$1];
       A3: P[0];
       A4: now
          let k be Nat;
          assume A5: P[k];
           now
          assume k+1 is non empty;
          per cases;
            suppose k is empty;
              then k = 0;
             hence P[k+1] by A1;
            end;
            suppose k is non empty;
              hence P[k+1] by A2,A5;
          end;
          end; hence P[k+1];
          end;
          for k being Nat holds P[k] from NatInd(A3,A4);
       hence thesis;
     end;
