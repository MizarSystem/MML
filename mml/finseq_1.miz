:: Segments of Natural Numbers and Finite Sequences
::  by Grzegorz Bancerek and Krzysztof Hryniewiecki
::
:: Received April 1, 1989
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies NUMBERS, NAT_1, FUNCT_1, SUBSET_1, XXREAL_0, TARSKI, XCMPLX_0,
      XBOOLE_0, CARD_1, ARYTM_3, RELAT_1, FINSET_1, ZFMISC_1, ORDINAL1,
      PARTFUN1, FUNCOP_1, ORDINAL4, ARYTM_1, REAL_1, FINSEQ_1, ORDINAL2,
      VALUED_0;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, XCMPLX_0, RELAT_1, FUNCT_1,
      FUNCOP_1, WELLORD2, ORDINAL1, REAL_1, NAT_1, CARD_1, NUMBERS, PARTFUN1,
      FINSET_1, XXREAL_0, FUNCT_2, INT_1, VALUED_0;
 constructors WELLORD2, FUNCOP_1, FUNCT_4, XXREAL_0, XREAL_0, NAT_1, CARD_1,
      RELSET_1, XCMPLX_0, INT_1, VALUED_0;
 registrations XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, ORDINAL1, FINSET_1,
      XXREAL_0, XREAL_0, NAT_1, CARD_1, NUMBERS, RELSET_1, INT_1, XCMPLX_0,
      VALUED_0;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, FINSET_1, FUNCT_1, WELLORD2, XBOOLE_0, RELAT_1;
 theorems TARSKI, AXIOMS, FUNCT_1, NAT_1, ZFMISC_1, RELAT_1, RELSET_1,
      WELLORD2, ORDINAL1, CARD_1, FINSET_1, XBOOLE_0, XBOOLE_1, FUNCOP_1,
      XREAL_1, XXREAL_0, SETFAM_1, ORDINAL3, SUBSET_1, VALUED_0;
 schemes FUNCT_1, SUBSET_1, NAT_1, XBOOLE_0, WELLORD2, RELAT_1, CLASSES1;

begin

reserve k,l,m,n,k1,a,b,c,i for Nat,
  x,y,z,y1,y2,X,Y for set,
  f,g for Function;

::   Segments of Natural Numbers

definition
  let n be Nat;
  func Seg n -> set equals
  { k where k is Element of NAT: 1 <= k & k <= n };
  correctness;
end;

definition
  let n be Nat;
  redefine func Seg n -> Subset of NAT;
  coherence
  proof
    set X = Seg n;
    X c= NAT
    proof
      let x;
      assume x in X;
      then ex k being Element of NAT st x = k & 1 <= k & k <= n;
      hence thesis;
    end;
    hence thesis;
  end;
end;

canceled 2;

theorem Th3:
  a in Seg b iff 1 <= a & a <= b
proof
A1: a in NAT by ORDINAL1:def 13;
  a in { m where m is Element of NAT: 1 <= m & m <= b } iff
  ex m being Element of NAT st a = m & 1 <= m & m <= b;
  hence thesis by A1;
end;

registration let n be zero Nat;
  cluster Seg n -> empty;
  coherence
   proof
    assume Seg n is not empty;
     then consider x being set such that
W:    x in Seg n by XBOOLE_0:def 1;
     ex k being Element of NAT st k = x & 1 <= k & k <= 0 by W;
    hence contradiction;
   end;
end;

theorem Th4:
  Seg 1 = { 1 } & Seg 2 = { 1,2 }
proof
  now
    let x;
    thus x in Seg 1 implies x in { 1 }
    proof
      assume x in Seg 1;
      then consider k being Element of NAT such that
A2:   x = k and
A3:   1 <= k & k <= 1;
      k = 1 by A3,XXREAL_0:1;
      hence thesis by A2,TARSKI:def 1;
    end;
    assume x in { 1 };
    then x = 1 by TARSKI:def 1;
    hence x in Seg 1;
  end;
  hence Seg 1 = { 1 } by TARSKI:2;
  now
    let x;
    thus x in Seg 2 implies x in { 1,2 }
    proof
      assume x in Seg 2;
      then consider k being Element of NAT such that
A4:   x = k and
A5:   1 <= k and
A6:   k <= 2;
      k <= 1 + 1 by A6;
      then k = 1 or k = 2 by A5,NAT_1:9;
      hence thesis by A4,TARSKI:def 2;
    end;
    assume x in { 1,2 };
    then x = 1 or x = 2 by TARSKI:def 2;
    hence x in Seg 2;
  end;
  hence thesis by TARSKI:2;
end;

theorem Th5:
  a = 0 or a in Seg a
proof
  assume a <> 0;
  then ex b be Nat st a = b + 1 by NAT_1:6;
  then a in NAT & 1 <= a by NAT_1:11;
  hence thesis;
end;

registration let n be non zero Nat;
  cluster Seg n -> non empty;
  coherence by Th5;
end;

theorem
  a+1 in Seg(a+1) by Th5;

theorem Th7:
  a <= b iff Seg a c= Seg b
proof
  thus a <= b implies Seg a c= Seg b
  proof
    assume
A1: a <= b;
    let x;
    assume
A2: x in Seg a;
    then reconsider c = x as Element of NAT;
A3: 1 <= c by A2,Th3;
    c <= a by A2,Th3;
    then c <= b by A1,XXREAL_0:2;
    hence thesis by A3;
  end;
  assume
A4: Seg a c= Seg b;
  now
    assume a <> 0;
    then a in Seg a by Th5;
    hence thesis by A4,Th3;
  end;
  hence thesis;
end;

theorem Th8:
  Seg a = Seg b implies a = b
proof
  assume Seg a = Seg b;
  then a <= b & b <= a by Th7;
  hence thesis by XXREAL_0:1;
end;

theorem Th9:
  c <= a implies Seg c = Seg a /\ Seg c
proof
  assume c <= a;
  then Seg c c= Seg a by Th7;
  hence thesis by XBOOLE_1:28;
end;

theorem
  Seg c = Seg c /\ Seg a implies c <= a by Th8,Th9;

theorem Th11:
  Seg a \/ { a+1 } = Seg (a+1)
proof
  thus Seg a \/ { a+1 } c= Seg (a+1)
  proof
    a+0<=a+1 by XREAL_1:9;
    then
A1: Seg a c= Seg(a+1) by Th7;
    let x;
    assume x in Seg a \/ { a+1 };
    then x in Seg a or x in { a+1 } by XBOOLE_0:def 3;
    then x in Seg (a+1) or x = a+1 by A1,TARSKI:def 1;
    hence thesis by Th5;
  end;
  let x;
  assume
A2: x in Seg (a+1);
  then reconsider x as Element of NAT;
A3: x<=a+1 by A2,Th3;
A4: 1<=x by A2,Th3;
  x<=a or x=a+1 by A3,NAT_1:8;
  then x in Seg a or x in {a+1} by A4,TARSKI:def 1;
  hence thesis by XBOOLE_0:def 3;
end;

theorem
  for k being Nat holds Seg k = Seg(k + 1) \ {k + 1}
proof
  let k be Nat;
A1: Seg(k + 1) = Seg k \/ {k + 1} by Th11;
  Seg k misses {k + 1}
  proof
    assume not thesis;
    then
A2: Seg k /\ {k + 1} <> {} by XBOOLE_0:def 7;
    consider x being Element of Seg k /\ {k + 1};
A3: x in Seg k by A2,XBOOLE_0:def 4;
    then reconsider x as Element of NAT;
    x in {k + 1} by A2,XBOOLE_0:def 4;
    then
A4: x = k + 1 by TARSKI:def 1;
    x <= k by A3,Th3;
    hence thesis by A4,XREAL_1:31;
  end;
  hence thesis by A1,XBOOLE_1:88;
end;

::  Finite Sequences

definition
  let IT be Relation;
  attr IT is FinSequence-like means
  :Def2:
  ex n st dom IT = Seg n;
end;

registration
  cluster empty -> FinSequence-like Relation;
  coherence
  proof
    let R be Relation;
    assume
A1: R is empty;
    take 0;
    thus thesis by A1;
  end;
end;

definition
  mode FinSequence is FinSequence-like Function;
end;

reserve p,q,r,s,t for FinSequence;

defpred P[set,set] means ex k st $1 = k & $2 = k+1;

registration
  let n be Nat;
  cluster Seg n -> finite;
  coherence
  proof
    Seg n is finite
    proof
A1:   n = { k where k is Element of NAT: k < n } by AXIOMS:30;
A2:   for x st x in n ex y st P[x,y]
      proof
        let x;
        assume x in n;
        then ex k being Element of NAT st x = k & k < n by A1;
        then reconsider k = x as Nat;
        take k+1;
        thus thesis;
      end;
      consider f such that
A3:   dom f = n and
A4:   for x st x in n holds P[x,f.x] from CLASSES1:sch 1(A2);
      take f;
      thus rng f = Seg n
      proof
        thus rng f c= Seg n
        proof
          let x;
          assume x in rng f;
          then consider y being set such that
A5:       y in dom f and
A6:       x = f.y by FUNCT_1:def 5;
          consider k such that
A7:       y = k and
A8:       x = k+1 by A3,A4,A5,A6;
          ex m being Element of NAT st m = y & m < n by A1,A3,A5;
          then 1 <= k+1 & k+1 <= n by A7,NAT_1:11,13;
          hence thesis by A8;
        end;
        let x;
        assume x in Seg n;
        then consider k being Element of NAT such that
A9:     x = k and
A10:    1 <= k and
A11:    k <= n;
        consider i being Nat such that
A12:    1+i = k by A10,NAT_1:10;
        i in NAT & i < n by A11,A12,NAT_1:13,ORDINAL1:def 13;
        then
A13:    i in n by A1;
        then P[i,f.i] by A4;
        hence thesis by A3,A9,A12,A13,FUNCT_1:def 5;
      end;
      thus thesis by A3,ORDINAL1:def 13;
    end;
    hence thesis;
  end;
end;

registration
  cluster FinSequence-like -> finite Function;
  coherence
  proof
    let f be Function;
    given n such that
A1: dom f = Seg n;
    rng f is finite by A1,FINSET_1:26;
    then [:dom f, rng f:] is finite by A1;
    hence thesis by FINSET_1:13,RELAT_1:21;
  end;
end;

Lm1: Seg n,n are_equipotent
proof
  defpred P[Nat] means Seg $1,$1 are_equipotent;
A1: P[0];
A2: for n st P[n] holds P[n+1]
  proof
    let n such that
A3: Seg n,n are_equipotent;
A4: (n+1) = succ n by NAT_1:39
      .= n \/ { n } by ORDINAL1:def 1;
    A5: Seg(n+1) = Seg n \/ { n+1 } & { n+1 },{ n } are_equipotent by Th11,
    CARD_1:48;
A6: now
      assume n meets { n };
      then consider x being set such that
A7:   x in n and
A8:   x in { n } by XBOOLE_0:3;
      x = n by A8,TARSKI:def 1;
      hence contradiction by A7;
    end;
    now
      assume Seg n meets { n+1 };
      then consider x being set such that
A9:   x in Seg n and
A10:  x in { n+1 } by XBOOLE_0:3;
A11:  x = n+1 by A10,TARSKI:def 1;
      not n+1 <= n by NAT_1:13;
      hence contradiction by A9,A11,Th3;
    end;
    hence thesis by A3,A4,A5,A6,CARD_1:58;
  end;
  for n holds P[n] from NAT_1:sch 2(A1,A2);
  hence thesis;
end;

Lm2: card Seg n = card n proof Seg n,n are_equipotent by Lm1;
  hence thesis by CARD_1:21;
end;

notation
  let p;
  synonym len p for card p;
end;

definition
  let p;
  redefine func len p -> Element of NAT means
  :Def3:
  Seg it = dom p;
  coherence
  proof card p = card p;
    hence thesis;
  end;
  compatibility
  proof
    let k be Element of NAT;
    consider n such that
A1: dom p = Seg n by Def2;
    dom p,p are_equipotent
    proof
      deffunc F(set) = [$1,p.$1];
      consider g being Function such that
A2:   dom g = dom p and
A3:   for x st x in dom p holds g.x = F(x) from FUNCT_1:sch 3;
      take g;
      thus g is one-to-one
      proof
        let x,y;
        assume that
A4:     x in dom g and
A5:     y in dom g;
        assume g.x = g.y;
        then [x,p.x] = g.y by A2,A3,A4
          .= [y,p.y] by A2,A3,A5;
        hence thesis by ZFMISC_1:33;
      end;
      thus dom g = dom p by A2;
      thus rng g c= p
      proof
        let i be set;
        assume i in rng g;
        then consider x such that
A6:     x in dom g and
A7:     g.x = i by FUNCT_1:def 5;
        g.x = [x,p.x] by A2,A3,A6;
        hence thesis by A2,A6,A7,FUNCT_1:8;
      end;
      let x,y be set;
      assume
A8:   [x,y] in p;
      then
A9:   x in dom p by FUNCT_1:8;
      y = p.x by A8,FUNCT_1:8;
      then g.x = [x,y] by A3,A9;
      hence thesis by A2,A9,FUNCT_1:def 5;
    end;
    then
A10: card p = card dom p by CARD_1:21;
    thus k = card p implies Seg k = dom p
    proof
      assume k = card p;
      then k = card n by A1,A10,Lm2;
      hence thesis by A1,CARD_1:def 5;
    end;
    assume
A11: Seg k = dom p;
    thus k = card k by CARD_1:def 5
      .= card p by A10,A11,Lm2;
  end;
end;

definition
  let p;
  redefine func dom p -> Subset of NAT;
  coherence
  proof dom p = Seg len p by Def3;
    hence thesis;
  end;
end;

canceled 2;

theorem
  (ex k st dom f c= Seg k) implies ex p st f c= p
proof
  given k such that
A1: dom f c= Seg k;
  deffunc F(set) = f.$1;
  consider g such that
A2: dom g = Seg k & for x st x in Seg k holds g.x = F(x) from FUNCT_1:sch 3;
  reconsider g as FinSequence by A2,Def2;
  take g;
  let y,z be set;
  assume
A3: [y,z] in f;
  then
A4: y in dom f by RELAT_1:def 4;
  then
A5: f.y = z by A3,FUNCT_1:def 4;
  [y,g.y] in g by A1,A2,A4,FUNCT_1:8;
  hence thesis by A1,A2,A4,A5;
end;

scheme SeqEx{A()->Nat,P[set,set]}:
  ex p st dom p = Seg A() & for k st k in Seg A() holds P[k,p.k]
provided
A1: for k st k in Seg A() ex x st P[k,x]
proof
A2: for x st x in Seg A() ex y st P[x,y] by A1;
  consider f being Function such that
A3: dom f = Seg A() &
  for x st x in Seg A() holds P[x,f.x] from CLASSES1:sch 1(A2);
  reconsider p=f as FinSequence by A3,Def2;
  take p;
  thus thesis by A3;
end;

scheme SeqLambda{A()->Nat,F(set) -> set}:
  ex p being FinSequence st len p = A() & for k st k in dom p holds p.k=F(k)
proof
  consider f being Function such that
  A1: dom
 f = Seg A() & for x st x in Seg A() holds f.x=F(x) from FUNCT_1:sch 3;
A2: A() in NAT by ORDINAL1:def 13;
  reconsider p=f as FinSequence by A1,Def2;
  take p;
  thus thesis by A1,A2,Def3;
end;

theorem
  z in p implies ex k st k in dom p & z=[k,p.k]
proof
  assume
A1: z in p;
  then consider x,y such that
A2: z=[x,y] by RELAT_1:def 1;
  x in dom p by A1,A2,FUNCT_1:8;
  then reconsider k = x as Nat;
  take k;
  thus thesis by A1,A2,FUNCT_1:8;
end;

theorem Th17:
  dom p = dom q & (for k st k in dom p holds p.k = q.k) implies p=q
proof
  assume that
A1: dom p = dom q and
A2: for k st k in dom p holds p.k = q.k;
  for x st x in dom p holds p.x=q.x by A2;
  hence thesis by A1,FUNCT_1:9;
end;

theorem Th18:
  len p = len q & (for k st 1 <= k & k <= len p holds p.k = q.k) implies p=q
proof
  assume that
A1: len p = len q and
A2: for k st 1<=k & k<=len p holds p.k = q.k;
A3: dom p = Seg len p by Def3;
A4: dom q = Seg len q by Def3;
  now
    let x;
    assume
A5: x in dom p;
    then reconsider k=x as Nat;
    1 <= k & k <= len p by A3,A5,Th3;
    hence p.x = q.x by A2;
  end;
  hence thesis by A1,A3,A4,FUNCT_1:9;
end;

theorem Th19:
  p|(Seg a) is FinSequence
proof
A1: dom(p|Seg a) = dom p /\ Seg a by RELAT_1:90
    .=Seg len p /\ Seg a by Def3;
  len p <= a or a <= len p;
  then dom(p|Seg a) = Seg len p or dom(p|Seg a) = Seg a by A1,Th9;
  hence thesis by Def2;
end;

theorem
  rng p c= dom f implies f*p is FinSequence
proof
  assume rng p c= dom f;
  then dom(f*p) = dom p by RELAT_1:46
    .= Seg len p by Def3;
  hence thesis by Def2;
end;

theorem Th21:
  a <= len p & q = p|(Seg a) implies len q = a & dom q = Seg a
proof
  assume that
A1: a <= len p and
A2: q = p|(Seg a);
  Seg a c= Seg len p by A1,Th7;
  then Seg a c= dom p by Def3;
  then a in NAT & dom q = Seg a by A2,ORDINAL1:def 13,RELAT_1:91;
  hence thesis by Def3;
end;

::                             ::
::    FinSequences of D        ::
::                             ::

definition
  let D be set;
  mode FinSequence of D -> FinSequence means
    :Def4:
    rng it c= D;
  existence
  proof
    rng {} c= D by XBOOLE_1:2;
    hence thesis;
  end;
end;

registration
  let D be set;
  cluster -> D-valued FinSequence of D;
  coherence
  proof
    let f be FinSequence of D;
    thus rng f c= D by Def4;
  end;
end;

Lm3: for D being set, f being FinSequence of D holds f is PartFunc of NAT,D
proof
  let D be set, f be FinSequence of D;
  dom f c= NAT & rng f c= D by Def4;
  hence thesis by RELSET_1:11;
end;

registration
  cluster empty -> FinSequence-like Relation;
  coherence;
end;

registration
  let D be set;
  cluster FinSequence-like PartFunc of NAT,D;
  existence
  proof {} is PartFunc of NAT,D by RELSET_1:25;
    hence thesis;
  end;
end;

definition
  let D be set;
  redefine mode FinSequence of D -> FinSequence-like PartFunc of NAT,D;
  coherence by Lm3;
end;

reserve D for set;

canceled;

theorem Th23:
  for p being FinSequence of D holds p|(Seg a) is FinSequence of D
proof
  let p be FinSequence of D;
A1: p|(Seg a) is FinSequence by Th19;
  rng(p|(Seg a)) c= rng p & rng p c= D by Def4,RELAT_1:99;
  then rng(p|(Seg a)) c= D by XBOOLE_1:1;
  hence thesis by A1,Def4;
end;

theorem
  for D being non empty set ex p being FinSequence of D st len p = a
proof
  let D be non empty set;
  reconsider a as Element of NAT by ORDINAL1:def 13;
  consider y being Element of D;
  set p = Seg a --> y;
A1: dom p = Seg a by FUNCOP_1:19;
  then reconsider p as FinSequence by Def2;
  rng p c= {y} & {y} c= D by FUNCOP_1:19,ZFMISC_1:37;
  then rng p c= D by XBOOLE_1:1;
  then reconsider q=p as FinSequence of D by Def4;
  take q;
  thus thesis by A1,Def3;
end;

::                                ::
::    The Empty FinSequence       ::
::                                ::

canceled 3;

Lm4: q = {} iff len q = 0;

theorem
  p <> {} iff len p >= 1
proof
  hereby
    assume p <> {};
    then len p+1 > 0+1 by XREAL_1:10;
    hence len p >=1 by NAT_1:13;
  end;
  assume len p >= 1;
  hence thesis;
end;

definition
  let x;
  func <*x*> -> set equals
  { [1,x] };
  coherence;
end;

definition
  let D be set;
  func <*>D -> FinSequence of D equals
  {};
  coherence
  proof
    rng {} c= D by XBOOLE_1:2;
    hence thesis by Def4;
  end;
end;

registration
  let D be set;
  cluster <*>D -> empty;
  coherence;
end;

registration
  let D be set;
  cluster empty FinSequence of D;
  existence
  proof
    take <*>D;
    thus thesis;
  end;
end;

canceled 4;

definition
  let p,q;
  func p^q -> FinSequence means
  :Def7:
  dom it = Seg (len p + len q) & (for k st k in dom p holds it.k=p.k) &
  for k st k in dom q holds it.(len p + k) = q.k;
  existence
  proof
    defpred P[set,set] means
    (for k st k=$1 & 1 <= k & k <= len p holds $2=p.k) &
    (for k st k=$1 & len p + 1 <= k & k <= len p + len q
    holds $2=q.(k-len p));
A1: for x st x in Seg(len p + len q) ex y st P[x,y]
    proof
      let x;
      assume x in Seg(len p + len q);
      then reconsider m=x as Element of NAT;
A2:   now
        assume
A3:     len p + 1 <= m;
        set y = q.(m-len p);
A4:     for k st k=x & 1 <= k & k <= len p holds y=p.k
        proof
          let k;
          assume that
A5:       k=x and 1 <= k and
A6:       k <= len p;
          m + (len p + 1) <= m + len p by A3,A5,A6,XREAL_1:9;
          then m + len p + 1 <= m + len p + 0;
          hence thesis by XREAL_1:8;
        end;
        for k st k=x & len p + 1 <= k & k <= len p + len q holds y=q.(k-len p);
        hence thesis by A4;
      end;
      now
        assume
A7:     not len p + 1 <= m;
        set y=p.m;
( for k st k=x & 1 <= k & k <= len p holds y=p.k)& for k st k=x & len p + 1 <=
        k & k <= len p + len q holds y=q.(k-len p) by A7;
        hence thesis;
      end;
      hence thesis by A2;
    end;
    consider f such that
A8: dom f=Seg(len p + len q) &
    for x st x in Seg(len p + len q) holds P[x,f.x] from CLASSES1:sch 1
    (A1);
A9: for k st k in dom p holds f.k=p.k
    proof
      let k;
      assume k in dom p;
      then
A10:  k in Seg len p by Def3;
      then
A11:  1 <= k & k <= len p by Th3;
      len p <= len p + len q by NAT_1:11;
      then Seg len p c= Seg(len p + len q) by Th7;
      hence thesis by A8,A10,A11;
    end;
A12: for n st n in dom q holds f.(len p+n)=q.n
    proof
      let n;
      assume n in dom q;
      then
A13:  n in Seg len q by Def3;
      then
A14:  1 <= n by Th3;
A15:  n <= len q by A13,Th3;
A16:  len p + 1 <= len p + n by A14,XREAL_1:9;
A17:  len p + n <= len p + len q by A15,XREAL_1:9;
      reconsider n as Element of NAT by ORDINAL1:def 13;
      len p + n <= len p + n + len p by NAT_1:11;
      then len p + 1 <= len p + n + len p by A16,XXREAL_0:2;
      then 1 <= len p + n by XREAL_1:8;
      then (len p + n) in Seg(len p + len q) by A17;
      then f.(len p + n)=q.(n + len p - len p) by A8,A16,A17;
      hence thesis;
    end;
    reconsider r=f as FinSequence by A8,Def2;
    take r;
    thus thesis by A8,A9,A12;
  end;
  uniqueness
  proof
    let f,g be FinSequence such that
A18: dom f = Seg(len p + len q) and
A19: for k st k in dom p holds f.k=p.k and
A20: for k st k in dom q holds f.(len p + k)=q.k and
A21: dom g = Seg(len p + len q) and
A22: for k st k in dom p holds g.k=p.k and
A23: for k st k in dom q holds g.(len p + k)=q.k;
    for x st x in Seg(len p + len q) holds f.x=g.x
    proof
      let x;
      assume
A24:  x in Seg(len p + len q);
      then reconsider k=x as Element of NAT;
A25:  1 <= k by A24,Th3;
A26:  now
        assume len p + 1 <= k;
        then consider m be Nat such that
A27:    len p + 1 + m = k by NAT_1:10;
        reconsider m as Element of NAT by ORDINAL1:def 13;
        len p + (1 + m) <= len p + len q by A24,A27,Th3;
        then 1+0<=1+m & 1 + m <= len q by XREAL_1:8;
        then (1+m) in Seg len q;
        then
A28:    (1+m) in dom q by Def3;
        then g.(len p +(1+m)) = q.(1+m) by A23;
        hence thesis by A20,A27,A28;
      end;
      now
        assume not len p + 1 <= k;
        then k <= len p by NAT_1:8;
        then k in Seg len p by A25;
        then
A29:    k in dom p by Def3;
        then f.k=p.k by A19;
        hence thesis by A22,A29;
      end;
      hence thesis by A26;
    end;
    hence thesis by A18,A21,FUNCT_1:9;
  end;
end;

theorem Th33:
  p = (p ^ q) | (dom p)
proof
A1: dom(p ^ q) = Seg(len p + len q) by Def7;
A2: dom p = Seg len p by Def3;
  then
A3: dom p = dom(p ^ q) /\ Seg len p by A1,Th9,NAT_1:12;
  for x st x in dom p holds p.x = (p ^ q).x by Def7;
  hence thesis by A2,A3,FUNCT_1:68;
end;

canceled;

theorem Th35:
  len(p^q) = len p + len q
proof
  dom(p^q) = Seg(len p + len q) by Def7;
  hence thesis by Def3;
end;

theorem Th36:
  for k being Nat st
  len p + 1 <= k & k <= len p + len q holds (p^q).k=q.(k-len p)
proof
  let k be Nat;
  assume that
A1: len p + 1 <= k and
A2: k <= len p + len q;
  consider m be Nat such that
A3: (len p + 1)+m=k by A1,NAT_1:10;
  reconsider m as Element of NAT by ORDINAL1:def 13;
A4: len p+(1+m) = k by A3;
  1+m = k - len p by A3;
  then
A5: 1 <= 1+m by A1,XREAL_1:21;
  k-len p <= len q by A2,XREAL_1:22;
  then 1+m in Seg len q by A3,A5;
  then 1+m in dom q by Def3;
  hence thesis by A4,Def7;
end;

theorem Th37:
  for k being Nat st len p < k & k <= len(p^q) holds (p^q).k = q.(k - len p)
proof
  let k be Nat;
  assume len p < k & k <= len(p^q);
  then len p + 1 <= k & k <= len p + len q by Th35,NAT_1:13;
  hence thesis by Th36;
end;

theorem Th38:
  for k being Nat st k in dom (p^q) holds
  (k in dom p or ex n st n in dom q & k=len p + n )
proof
  let k be Nat;
  assume k in dom(p^q);
  then
A1: k in Seg len (p^q) by Def3;
  then
A2: k in Seg(len p + len q) by Th35;
A3: k in NAT & 1 <= k by A1,Th3;
A4: now
    assume not len p+1 <= k;
    then k <= len p by NAT_1:8;
    then k in Seg len p by A3;
    hence thesis by Def3;
  end;
  now
    assume len p + 1 <= k;
    then consider n be Nat such that
A5: k=len p + 1 + n by NAT_1:10;
    reconsider n as Element of NAT by ORDINAL1:def 13;
    len p + (1 + n) <= len p + len q by A2,A5,Th3;
    then
A6: 1+n <= len q by XREAL_1:8;
    1 <= 1+n by NAT_1:11;
    then 1+n in Seg len q by A6;
    then
A7: 1+n in dom q by Def3;
    k=len p + (1+n) by A5;
    hence thesis by A7;
  end;
  hence thesis by A4;
end;

theorem Th39:
  dom p c= dom(p^q)
proof
  len p <= len p + len q by NAT_1:11;
  then Seg len p c= Seg(len p + len q) by Th7;
  then Seg len p c= dom(p^q) by Def7;
  hence thesis by Def3;
end;

theorem Th40:
  x in dom q implies ex k st k=x & len p + k in dom(p^q)
proof
  assume
A1: x in dom q;
  then
A2: x in Seg len q by Def3;
  reconsider k=x as Element of NAT by A1;
  take k;
A3: 1 <= k by A2,Th3;
A4: k <= len q by A2,Th3;
  k <= len p + k by NAT_1:11;
  then
A5: 1 <= len p + k by A3,XXREAL_0:2;
  len p + k <= len p + len q by A4,XREAL_1:9;
  then len p + k in Seg(len p + len q) by A5;
  hence thesis by Def7;
end;

theorem Th41:
  k in dom q implies len p + k in dom(p^q)
proof
  assume k in dom q;
  then ex n st n=k & len p + n in dom(p^q) by Th40;
  hence thesis;
end;

theorem Th42:
  rng p c= rng(p^q)
proof
  now
    let x;
    assume x in rng p;
    then consider y such that
A1: y in dom p and
A2: x=p.y by FUNCT_1:def 5;
    reconsider k=y as Element of NAT by A1;
A3: dom p c= dom(p^q) by Th39;
    (p^q).k=p.k by A1,Def7;
    hence x in rng(p^q) by A1,A2,A3,FUNCT_1:def 5;
  end;
  hence thesis by TARSKI:def 3;
end;

theorem Th43:
  rng q c= rng(p^q)
proof
  now
    let x;
    assume x in rng q;
    then consider y such that
A1: y in dom q and
A2: x=q.y by FUNCT_1:def 5;
    reconsider k=y as Element of NAT by A1;
    len p + k in dom(p^q) & (p^q).(len p + k) = q.k by A1,Def7,Th41;
    hence x in rng(p^q) by A2,FUNCT_1:def 5;
  end;
  hence thesis by TARSKI:def 3;
end;

theorem Th44:
  rng(p^q) = rng p \/ rng q
proof
  now
    let x;
    assume x in rng(p^q);
    then consider y such that
A1: y in dom(p^q) and
A2: x=(p^q).y by FUNCT_1:def 5;
A3: y in Seg(len p + len q) by A1,Def7;
    reconsider k=y as Element of NAT by A1;
A4: 1 <= k by A3,Th3;
A5: k <= len p + len q by A3,Th3;
A6: now
      assume
A7:   len p + 1 <= k;
      then
A8:   q.(k-len p) = x by A2,A5,Th36;
      consider m be Nat such that
A9:   (len p+1)+m = k by A7,NAT_1:10;
      reconsider m as Element of NAT by ORDINAL1:def 13;
      len p +(1+m) = k by A9;
      then 1+0<=1+m & 1+m <= len q by A5,XREAL_1:8;
      then 1+m in Seg len q;
      then k-len p in dom q by A9,Def3;
      hence x in rng q by A8,FUNCT_1:def 5;
    end;
    now
      assume not len p + 1 <= k;
      then k <= len p by NAT_1:8;
      then k in Seg len p by A4;
      then
A10:  k in dom p by Def3;
      then p.k = x by A2,Def7;
      hence x in rng p by A10,FUNCT_1:def 5;
    end;
    hence x in rng p \/ rng q by A6,XBOOLE_0:def 3;
  end;
  then
A11: rng(p^q) c= rng p \/ rng q by TARSKI:def 3;
  rng p c= rng(p^q) & rng q c= rng(p^q) by Th42,Th43;
  then (rng p \/ rng q) c= rng(p^q) by XBOOLE_1:8;
  hence thesis by A11,XBOOLE_0:def 10;
end;

theorem Th45:
  p^q^r = p^(q^r)
proof
A1: dom ((p^q)^r) = Seg(len (p^q) + len r) by Def7
    .= Seg(len p + len q + len r) by Th35
    .= Seg(len p + (len q + len r))
    .= Seg(len p + len(q^r)) by Th35;
A2: for k st k in dom p holds ((p^q)^r).k=p.k
  proof
    let k;
    assume
A3: k in dom p;
    dom p c= dom(p^q) by Th39;
    hence (p^q^r).k=(p^q).k by A3,Def7
      .=p.k by A3,Def7;
  end;
  for k st k in dom(q^r) holds ((p^q)^r).(len p + k)=(q^r).k
  proof
    let k;
    assume
A4: k in dom(q^r);
A5: now
      assume
A6:   k in dom q;
      then (len p + k) in dom(p^q) by Th41;
      hence (p^q^r).(len p + k) = (p^q).(len p + k) by Def7
        .=q.k by A6,Def7
        .=(q^r).k by A6,Def7;
    end;
    now
      assume not k in dom q;
      then consider n such that
A7:   n in dom r and
A8:   k=len q + n by A4,Th38;
      thus (p^q^r).(len p + k) =(p^q^r).(len p + len q + n) by A8
        .=(p^q^r).(len(p^q) + n) by Th35
        .=r.n by A7,Def7
        .=(q^r).k by A7,A8,Def7;
    end;
    hence thesis by A5;
  end;
  hence thesis by A1,A2,Def7;
end;

theorem
  p^r = q^r or r^p = r^q implies p = q
proof
  assume
A1: p^r = q^r or r^p = r^q;
A2: now
    assume
A3: p^r = q^r;
    then len p + len r = len(q^r) by Th35;
    then len p + len r = len q + len r by Th35;
    then
A4: dom p = Seg len q by Def3
      .= dom q by Def3;
    for k st k in dom p holds p.k=q.k
    proof
      let k;
      assume
A5:   k in dom p;
      hence p.k=(q^r).k by A3,Def7
        .=q.k by A4,A5,Def7;
    end;
    hence thesis by A4,Th17;
  end;
  now
    assume
A6: r^p=r^q;
    then len r + len p = len(r^q) by Th35
      .=len r + len q by Th35;
    then
A7: dom p = Seg len q by Def3
      .= dom q by Def3;
    for k st k in dom p holds p.k=q.k
    proof
      let k;
      assume
A8:   k in dom p;
      hence p.k = (r^q).(len r + k) by A6,Def7
        .= q.k by A7,A8,Def7;
    end;
    hence thesis by A7,Th17;
  end;
  hence thesis by A1,A2;
end;

theorem Th47:
  p^{} = p & {}^p = p
proof
A1: dom(p^{}) = Seg len (p^{}) by Def3
    .= Seg (len p + len {}) by Th35
    .= dom p by Def3;
  for k st k in dom p holds p.k=(p^{}).k by Def7;
  hence p^{} = p by A1,Th17;
A2: dom({}^p) = Seg len ({}^p) by Def3
    .= Seg (len {} + len p) by Th35
    .= dom p by Def3;
  for k st k in dom p holds p.k = ({}^p).k
  proof
    let k;
    assume
A3: k in dom p;
    thus ({}^p).k =({}^p).(len {} + k) .=p.k by A3,Def7;
  end;
  hence {}^p=p by A2,Th17;
end;

theorem Th48:
  p^q = {} implies p={} & q={}
proof
  assume p^q={};
  then 0 = len (p^q) .= len p + len q by Th35;
  hence thesis;
end;

definition
  let D be set;
  let p,q be FinSequence of D;
  redefine func p^q -> FinSequence of D;
  coherence
  proof
A1: rng(p^q) = rng p \/ rng q & rng p c= D by Def4,Th44;
    rng q c= D by Def4;
    hence rng(p^q) c= D by A1,XBOOLE_1:8;
  end;
end;

Lm5: for x1, y1 being set holds [x,y] in {[x1,y1]} implies x = x1 & y = y1
proof
  let x1, y1 be set;
  assume [x,y] in {[x1,y1]};
  then [x,y] = [x1,y1] by TARSKI:def 1;
  hence thesis by ZFMISC_1:33;
end;

definition
  let x;
  redefine func <*x*> -> Function means
  :Def8:
  dom it = Seg 1 & it.1 = x;
  coherence;
  compatibility
  proof
    let f be Function;
    hereby
      assume
A1:   f = <*x*>;
      hence dom f = Seg 1 by Th4,RELAT_1:23;
      [1,x] in f by A1,TARSKI:def 1;
      hence f.1 = x by FUNCT_1:8;
    end;
    assume that
A2: dom f = Seg 1 and
A3: f.1 = x;
    reconsider g = { [1,f.1] } as Function;
    [y,z] in f iff [y,z] in g
    proof
      hereby
        assume
A4:     [y,z] in f;
        then
A5:     y in {1} by A2,Th4,RELAT_1:def 4;
A6:     z in rng f by A4,RELAT_1:def 5;
A7:     rng f = {f.1} by A2,Th4,FUNCT_1:14;
A8:     y = 1 by A5,TARSKI:def 1;
        z = f.1 by A6,A7,TARSKI:def 1;
        hence [y,z] in g by A8,TARSKI:def 1;
      end;
      assume [y,z] in g;
      then
A9:   y = 1 & z = f.1 by Lm5;
      1 in dom f by A2;
      hence thesis by A9,FUNCT_1:def 4;
    end;
    hence thesis by A3,RELAT_1:def 2;
  end;
end;

registration
  let x;
  cluster <*x*> -> Function-like Relation-like;
  coherence;
end;

registration
  let x;
  cluster <*x*> -> FinSequence-like;
  coherence
  proof
    take 1;
    thus thesis by Def8;
  end;
end;

canceled;

theorem Th50:
  p^q is FinSequence of D implies p is FinSequence of D & q is FinSequence of D
proof
  assume p^q is FinSequence of D;
  then rng(p^q) c= D by Def4;
  then
A1: rng p \/ rng q c= D by Th44;
  rng p c= rng p \/ rng q by XBOOLE_1:7;
  then rng p c= D by A1,XBOOLE_1:1;
  hence p is FinSequence of D by Def4;
  rng q c= rng p \/ rng q by XBOOLE_1:7;
  then rng q c= D by A1,XBOOLE_1:1;
  hence thesis by Def4;
end;

definition
  let x,y;
  func <*x,y*> -> set equals
  <*x*>^<*y*>;
  correctness;
  let z;
  func <*x,y,z*> -> set equals
  <*x*>^<*y*>^<*z*>;
  correctness;
end;

registration
  let x,y;
  cluster <*x,y*> -> Function-like Relation-like;
  coherence;
  let z;
  cluster <*x,y,z*> -> Function-like Relation-like;
  coherence;
end;

registration
  let x,y;
  cluster <*x,y*> -> FinSequence-like;
  coherence;
  let z;
  cluster <*x,y,z*> -> FinSequence-like;
  coherence;
end;

canceled;

theorem
  <*x*> = { [1,x] };

canceled 2;

theorem Th55:
  p=<*x*> iff dom p = Seg 1 & rng p = {x}
proof
  thus p = <*x*> implies dom p = Seg 1 & rng p = {x}
  proof
    assume
A1: p = <*x*>;
    hence dom p = Seg 1 by Def8;
    dom p = {1} by A1,Def8,Th4;
    then rng p = {p.1} by FUNCT_1:14;
    hence thesis by A1,Def8;
  end;
  assume that
A2: dom p = Seg 1 and
A3: rng p = {x};
  1 in dom p by A2;
  then p.1 in {x} by A3,FUNCT_1:def 5;
  then p.1 = x by TARSKI:def 1;
  hence thesis by A2,Def8;
end;

theorem Th56:
  p=<*x*> iff len p = 1 & rng p = {x}
proof
  len p = 1 iff dom p = Seg 1 by Def3;
  hence thesis by Th55;
end;

theorem Th57:
  p = <*x*> iff len p = 1 & p.1 = x
proof
  len p = 1 iff dom p = Seg 1 by Def3;
  hence thesis by Def8;
end;

theorem
  (<*x*>^p).1 = x
proof
  1 in Seg 1;
  then 1 in dom <*x*> by Def8;
  then (<*x*>^p).1 = <*x*>.1 by Def7;
  hence thesis by Def8;
end;

theorem Th59:
  (p^<*x*>).(len p + 1)=x
proof
  dom <*x*> = Seg 1 by Def8;
  then 1 in dom <*x*>;
  hence (p^<*x*>).(len p + 1) = <*x*>.1 by Def7
    .=x by Def8;
end;

theorem
  <*x,y,z*>=<*x*>^<*y,z*> & <*x,y,z*>=<*x,y*>^<*z*> by Th45;

theorem Th61:
  p = <*x,y*> iff len p = 2 & p.1=x & p.2=y
proof
  thus
  p = <*x,y*> implies len p = 2 & p.1=x & p.2=y
  proof
    assume
A1: p=<*x,y*>;
    hence len p = len <*x*> + len <*y*> by Th35
      .= 1 + len <*y*> by Th56
      .= 1 + 1 by Th56
      .=2;
A2: 1 in {1} by TARSKI:def 1;
    then 1 in dom <*x*> by Def8,Th4;
    hence p.1 = <*x*>.1 by A1,Def7
      .= x by Def8;
A3: 1 in dom <*y*> by A2,Def8,Th4;
    thus p.2 = (<*x*>^<*y*>).(1+1) by A1
      .= (<*x*>^<*y*>).(len <*x*> + 1) by Th56
      .= <*y*>.1 by A3,Def7
      .= y by Def8;
  end;
  assume that
A4: len p = 2 and
A5: p.1=x and
A6: p.2=y;
A7: dom p = Seg(1+1) by A4,Def3
    .= Seg(len <*x*> + 1) by Th56
    .= Seg(len <*x*> + len <*y*>) by Th56;
A8: for k st k in dom <*x*> holds p.k=<*x*>.k
  proof
    let k;
    assume k in dom <*x*>;
    then k in {1} by Def8,Th4;
    then k=1 by TARSKI:def 1;
    hence thesis by A5,Def8;
  end;
  for k st k in dom <*y*> holds p.((len <*x*>)+k)=<*y*>.k
  proof
    let k;
    assume k in dom <*y*>;
    then
A9: k in {1} by Def8,Th4;
    thus p.((len <*x*>) + k) = p.(1+k) by Th56
      .=p.(1+1) by A9,TARSKI:def 1
      .=<*y*>.1 by A6,Def8
      .= <*y*>.k by A9,TARSKI:def 1;
  end;
  hence thesis by A7,A8,Def7;
end;

theorem Th62:
  p = <*x,y,z*> iff len p = 3 & p.1 = x & p.2 = y & p.3 = z
proof
  thus p = <*x,y,z*> implies len p = 3 & p.1 = x & p.2 = y & p.3 = z
  proof
    assume
A1: p =<*x,y,z*>;
    hence len p =len <*x,y*> + len <*z*> by Th35
      .=2 + len <*z*> by Th61
      .=2+1 by Th56
      .=3;
A2: 1 in {1} by TARSKI:def 1;
    then
A3: 1 in dom <*x*> by Def8,Th4;
    thus p.1 = (<*x*>^<*y,z*>).1 by A1,Th45
      .=<*x*>.1 by A3,Def7
      .=x by Def8;
    2 in Seg 2 & len <*x,y*> = 2 by Th61;
    then 2 in dom <*x,y*> by Def3;
    hence p.2 =<*x,y*>.2 by A1,Def7
      .=y by Th61;
A4: 1 in dom <*z*> by A2,Def8,Th4;
    thus p.3 = (<*x,y*>^<*z*>).(2+1) by A1
      .=(<*x,y*>^<*z*>).(len (<*x,y*>) + 1) by Th61
      .= <*z*>.1 by A4,Def7
      .= z by Def8;
  end;
  assume that
A5: len p = 3 and
A6: p.1 = x and
A7: p.2 = y and
A8: p.3 = z;
A9: dom p = Seg(2+1) by A5,Def3
    .= Seg((len <*x,y*>) + 1) by Th61
    .= Seg((len <*x,y*>) + len <*z*>) by Th56;
A10: for k st k in dom <*x,y*> holds p.k=<*x,y*>.k
  proof
    let k such that
A11: k in dom <*x,y*>;
    len <*x,y*> = 2 by Th61;
    then
A12: k in Seg 2 by A11,Def3;
A13: k=1 implies p.k=<*x,y*>.k by A6,Th61;
    k=2 implies p.k=<*x,y*>.k by A7,Th61;
    hence thesis by A12,A13,Th4,TARSKI:def 2;
  end;
  for k st k in dom <*z*> holds p.( (len <*x,y*>) + k) = <*z*>.k
  proof
    let k;
    assume k in dom <*z*>;
    then k in {1} by Def8,Th4;
    then
A14: k = 1 by TARSKI:def 1;
    hence p.( (len <*x,y*>) + k) = p.(2+1) by Th61
      .=<*z*>.k by A8,A14,Def8;
  end;
  hence thesis by A9,A10,Def7;
end;

theorem Th63:
  p <> {} implies ex q,x st p=q^<*x*>
proof
  assume p <> {};
  then consider n be Nat such that
A1: len p = n + 1 by NAT_1:6;
  reconsider n as Element of NAT by ORDINAL1:def 13;
  reconsider q=p|Seg n as FinSequence by Th19;
  take q;
  take p.(len p);
A2: dom q = dom p /\ Seg n by RELAT_1:90
    .= Seg len p /\ Seg n by Def3;
  n <=len p by A1,NAT_1:11;
  then Seg n c= Seg len p by Th7;
  then
A3: dom q = Seg n by A2,XBOOLE_1:28;
A4: dom(q^<*p.(len p)*>) = Seg len (q^<*p.(len p)*>) by Def3
    .= Seg(len q + len <*p.(len p)*>) by Th35
    .= Seg(len q + 1) by Th56
    .= Seg len p by A1,A3,Def3
    .= dom p by Def3;
  for x st x in dom p holds p.x = (q^<*p.(len p)*>).x
  proof
    let x;
    assume
A5: x in dom p;
    then reconsider k = x as Element of NAT;
    k in Seg(n+1) by A1,A5,Def3;
    then
A6: k in Seg n \/ {n+1} by Th11;
A7: now
      assume
A8:   k in Seg n;
      hence p.k=q.k by A3,FUNCT_1:70
        .=(q^<*p.(len p)*>).k by A3,A8,Def7;
    end;
    now
      assume
A9:   k in {n+1};
      1 in Seg 1;
      then
A10:  1 in dom <*p.(len p)*> by Def8;
      thus (q^<*p.(len p)*>).k =(q^<*p.(len p)*>).(n+1) by A9,TARSKI:def 1
        .=(q^<*p.(len p)*>).(len q + 1) by A3,Def3
        .=<*p.(len p)*>.1 by A10,Def7
        .=p.(n+1) by A1,Def8
        .=p.k by A9,TARSKI:def 1;
    end;
    hence thesis by A6,A7,XBOOLE_0:def 3;
  end;
  hence q^<*p.(len p)*>=p by A4,FUNCT_1:9;
end;

definition
  let D be non empty set;
  let x be Element of D;
  redefine func <*x*> -> FinSequence of D;
  coherence
  proof
    let y;
    assume y in rng <*x*>;
    then y in {x} by Th56;
    then y = x by TARSKI:def 1;
    hence thesis;
  end;
end;

:: scheme of induction for finite sequences ::

scheme IndSeq{P[FinSequence]}: for p holds P[p]
provided
A1: P[{}] and
A2: for p,x st P[p] holds P[p^<*x*>]
proof
  let p;
  defpred R[set] means for p st len p = $1 holds P[p];
  consider X being Subset of REAL such that
A3: for x being Real holds x in X iff R[x] from SUBSET_1:sch 3;
  for k holds k in X
  proof
    defpred S[Nat] means $1 in X;
    for q st len q = 0 holds P[q] by A1,Lm4;
    then
A4: S[0] by A3;
    now
      let n such that
A5:   n in X;
      now
        let q;
        assume
A6:     len q = n+1;
        then q <> {};
        then consider r,x such that
A7:     q=r^<*x*> by Th63;
        len q = len r + len <*x*> by A7,Th35
          .= len r + 1 by Th56;
        then P[r] by A3,A5,A6;
        hence P[q] by A2,A7;
      end;
      hence n+1 in X by A3;
    end;
    then
A8: for n st S[n] holds S[n+1];
    thus for n holds S[n] from NAT_1:sch 2(A4,A8);
  end;
  then len p in X;
  hence thesis by A3;
end;

theorem
  for p,q,r,s being FinSequence st p^q = r^s & len p <= len r
  ex t being FinSequence st p^t = r
proof
  defpred S[FinSequence] means
  for p,q,s st p^q=$1^s & len p <= len $1 holds ex t st p^t=$1;
A1: S[{}]
  proof
    let p,q,s;
    assume that
    p^q={}^s and
A2: len p <= len {};
    take {};
    thus p^{} = p by Th47
      .= {} by A2;
  end;
A3: for r,x st S[r] holds S[r^<*x*>]
  proof
    let r,x;
    assume
A4: for p,q,s st p^q=r^s & len p <= len r ex t st p^t=r;
    let p,q,s;
    assume that
A5: p^q=(r^<*x*>)^s and
A6: len p <= len (r^<*x*>);
A7: now
      assume len p = len(r^<*x*>);
      then
A8:   dom p = Seg len(r^<*x*>) by Def3
        .= dom(r^<*x*>) by Def3;
A9:   for k st k in dom p holds p.k=(r^<*x*>).k
      proof
        let k;
        assume
A10:    k in dom p;
        hence p.k = (r^<*x*>^s).k by A5,Def7
          .=(r^<*x*>).k by A8,A10,Def7;
      end;
      p^{} = p by Th47
        .=r^<*x*> by A8,A9,Th17;
      hence thesis;
    end;
    now
      assume len p <> len(r^<*x*>);
      then len p <> len r + len <*x*> by Th35;
      then
A11:  len p <> len r + 1 by Th56;
      len p <= len r + len <*x*> by A6,Th35;
      then
A12:  len p <= len r + 1 by Th56;
      p^q=r^(<*x*>^s) by A5,Th45;
      then consider t being FinSequence such that
A13:  p^t = r by A4,A11,A12,NAT_1:8;
      p^(t^<*x*>) = r^<*x*> by A13,Th45;
      hence thesis;
    end;
    hence thesis by A7;
  end;
  for r holds S[r] from IndSeq(A1,A3);
  hence thesis;
end;

definition
  let D be set;
  func D* -> set means
  :Def11:
  x in it iff x is FinSequence of D;
  existence
  proof
    defpred P[set] means $1 is FinSequence of D;
    consider X such that
A1: x in X iff x in bool [:NAT,D:] & P[x] from XBOOLE_0:sch 1;
    take X;
    let x;
    thus x in X implies x is FinSequence of D by A1;
    assume x is FinSequence of D;
    then reconsider p = x as FinSequence of D;
    p c= [:NAT,D:];
    hence thesis by A1;
  end;
  uniqueness
  proof
    let D1,D2 be set such that
A2: x in D1 iff x is FinSequence of D and
A3: x in D2 iff x is FinSequence of D;
    now
      let x;
      thus x in D1 implies x in D2
      proof
        assume x in D1;
        then x is FinSequence of D by A2;
        hence thesis by A3;
      end;
      assume x in D2;
      then x is FinSequence of D by A3;
      hence x in D1 by A2;
    end;
    hence thesis by TARSKI:2;
  end;
end;

registration
  let D be set;
  cluster D* -> non empty;
  coherence
  proof
    consider f being FinSequence of D;
    f in D* by Def11;
    hence thesis;
  end;
end;

theorem
  rng p = rng q & p is one-to-one & q is one-to-one implies len p = len q
proof
  defpred P[FinSequence] means $1 is one-to-one implies
  for q st rng $1 = rng q & q is one-to-one holds len $1 = len q;
A1: P[{}] by RELAT_1:64;
  now
    let p,x;
    assume
A2: p is one-to-one implies
    for q st rng p = rng q & q is one-to-one holds len p = len q;
    assume
A3: p ^ <* x *> is one-to-one;
    let q;
    assume that
A4: rng(p ^ <* x *>) = rng q and
A5: q is one-to-one;
A6: rng(p ^ <* x *>) = rng p \/ rng<* x *> by Th44
      .= rng p \/ {x} by Th55;
    x in {x} by TARSKI:def 1;
    then x in rng q by A4,A6,XBOOLE_0:def 3;
    then consider y such that
A7: y in dom q and
A8: x = q.y by FUNCT_1:def 5;
A9: y in Seg(len q) by A7,Def3;
    reconsider y as Element of NAT by A7;
A10: 1 <= y by A9,Th3;
    then consider k be Nat such that
A11: 1 + k = y by NAT_1:10;
A12: y <= len q by A9,Th3;
    then consider n be Nat such that
A13: y + n = len q by NAT_1:10;
    reconsider k,n as Element of NAT by ORDINAL1:def 13;
    reconsider q1 = q | (Seg k) as FinSequence by Th19;
    defpred P[Nat,set] means $2 = q.(y + $1);
A14: for j being Nat st j in Seg n ex x st P[j,x];
    consider q2 being FinSequence such that
A15: dom q2 = Seg n and
A16: for j being Nat st j in Seg n holds P[j,q2.j] from SeqEx(A14);
    k <= y by A11,NAT_1:12;
    then
A17: k <= len q by A12,XXREAL_0:2;
    then
A18: len q1 = k by Th21;
    len(q1 ^ <* x *>) + len q2 = len q1 + len<* x *> + len q2 by Th35
      .= k + 1 + len q2 by A18,Th56
      .= len q by A11,A13,A15,Def3;
    then
A19: dom q = Seg(len(q1 ^ <* x *>) + len q2) by Def3;
A20: rng(q1 ^ q2) = rng q \ {x}
    proof
      thus rng(q1 ^ q2) c= rng q \ {x}
      proof
        let z be set;
        assume z in rng(q1 ^ q2);
        then
A21:    z in rng q1 \/ rng q2 by Th44;
A22:    now
          assume
A23:      z in rng q1;
          A24:      rng
 q1 = q .: (Seg k) & q .: (Seg k) c= rng q by RELAT_1:144,148;
          consider y1 such that
A25:      y1 in dom q1 and
A26:      q1.y1 = z by A23,FUNCT_1:def 5;
A27:      q1.y1 = q.y1 by A25,FUNCT_1:70;
A28:      y1 in Seg(len q1) by A25,Def3;
          reconsider y1 as Element of NAT by A25;
A29:      y1 <= k by A18,A28,Th3;
A30:      k < y by A11,XREAL_1:31;
          Seg k c= Seg(len q) by A17,Th7;
          then dom q1 c= Seg(len q) by A17,Th21;
          then dom q1 c= dom q by Def3;
          then x <> z by A5,A7,A8,A25,A26,A27,A29,A30,FUNCT_1:def 8;
          then not z in {x} by TARSKI:def 1;
          hence thesis by A23,A24,XBOOLE_0:def 5;
        end;
        now
          assume z in rng q2;
          then consider y1 such that
A31:      y1 in dom q2 and
A32:      q2.y1 = z by FUNCT_1:def 5;
          reconsider y1 as Element of NAT by A31;
A33:      q2.y1 = q.(y + y1) by A15,A16,A31;
A34:      1 <= y + y1 by A10,NAT_1:12;
          y1 <= n by A15,A31,Th3;
          then y + y1 <= len q by A13,XREAL_1:9;
          then y + y1 in Seg(len q) by A34;
          then
A35:      y + y1 in dom q by Def3;
          then
A36:      z in rng q by A32,A33,FUNCT_1:def 5;
          y1 <> 0 by A15,A31,Th3;
          then y <> y + y1;
          then x <> z by A5,A7,A8,A32,A33,A35,FUNCT_1:def 8;
          then not z in {x} by TARSKI:def 1;
          hence thesis by A36,XBOOLE_0:def 5;
        end;
        hence thesis by A21,A22,XBOOLE_0:def 3;
      end;
      let z be set;
      assume
A37:  z in rng q \ {x};
      then consider y1 such that
A38:  y1 in dom q and
A39:  z = q.y1 by FUNCT_1:def 5;
A40:  y1 in Seg(len q) by A38,Def3;
      reconsider y1 as Element of NAT by A38;
      not z in {x} by A37,XBOOLE_0:def 5;
      then
A41:  y <> y1 by A8,A39,TARSKI:def 1;
A42:  now
        assume
A43:    y < y1;
        then consider j being Nat such that
A44:    y1 = y + j by NAT_1:10;
        reconsider j as Element of NAT by ORDINAL1:def 13;
A45:    1 <= j by A43,A44,NAT_1:19;
        y + j <= len q by A40,A44,Th3;
        then j <= n by A13,XREAL_1:8;
        then
A46:    j in Seg n by A45;
        then z = q2.j by A16,A39,A44;
        hence z in rng q2 by A15,A46,FUNCT_1:def 5;
      end;
      now
        assume
A47:    y1 < y;
A48:    1 <= y1 by A40,Th3;
        y1 <= k by A11,A47,NAT_1:13;
        then y1 in Seg k by A48;
        then
A49:    y1 in dom q1 by A17,Th21;
        then q1.y1 = z by A39,FUNCT_1:70;
        hence z in rng q1 by A49,FUNCT_1:def 5;
      end;
      then z in rng q1 \/ rng q2 by A41,A42,XBOOLE_0:def 3,XXREAL_0:1;
      hence thesis by Th44;
    end;
A50: p = (p ^ <* x *>) | (dom p) by Th33;
A51: rng p = rng(p ^ <* x *>) \ {x}
    proof
      thus rng p c= rng(p ^ <* x *>) \ {x}
      proof
        let z be set;
        assume
A52:    z in rng p;
A53:    rng p c= rng(p ^ <* x *>) by A50,RELAT_1:99;
        consider y1 such that
A54:    y1 in dom p and
A55:    p.y1 = z by A52,FUNCT_1:def 5;
A56:    y1 in Seg(len p) by A54,Def3;
        reconsider y1 as Element of NAT by A54;
A57:    (p ^ <* x *>).(len p + 1) = x by Th59;
A58:    (p ^ <* x *>).y1 = p.y1 by A50,A54,FUNCT_1:70;
A59:    y1 <= len p by A56,Th3;
A60:    len p < len p +1 by XREAL_1:31;
A61:    1 <= y1 by A56,Th3;
        y1 <= len p + 1 by A59,A60,XXREAL_0:2;
        then
A62:    y1 in Seg(len p + 1) by A61;
A63:    len p + 1 in Seg(len p + 1) by Th5;
A64:    y1 in Seg(len p + len<* x *>) by A62,Th57;
A65:    len p + 1 in Seg(len p + len<* x *>) by A63,Th57;
A66:    y1 in dom(p ^ <* x *>) by A64,Def7;
        len p + 1 in dom(p ^ <* x *>) by A65,Def7;
        then x <> z by A3,A55,A57,A58,A59,A60,A66,FUNCT_1:def 8;
        then not z in {x} by TARSKI:def 1;
        hence thesis by A52,A53,XBOOLE_0:def 5;
      end;
      let z be set;
      assume
A67:  z in rng(p ^ <* x *>) \ {x};
      then
A68:  z in rng(p ^ <* x *>);
A69:  not z in {x} by A67,XBOOLE_0:def 5;
      z in rng p \/ rng<* x *> by A68,Th44;
      then z in rng p or z in rng<* x *> by XBOOLE_0:def 3;
      hence thesis by A69,Th55;
    end;
A70: q1 ^ q2 is one-to-one
    proof
      let y1,y2;
      assume that
A71:  y1 in dom(q1 ^ q2) & y2 in dom(q1 ^ q2) and
A72:  (q1 ^ q2).y1 = (q1 ^ q2).y2;
      reconsider m1 = y1, m2 = y2 as Element of NAT by A71;
A73:  q1 is one-to-one by A5,FUNCT_1:84;
A74:  now
        assume
A75:    m1 in dom q1 & m2 in dom q1;
        then (q1 ^ q2).m1 = q1.m1 & (q1 ^ q2).m2 = q1.m2 by Def7;
        hence thesis by A72,A73,A75,FUNCT_1:def 8;
      end;
A76:  now
        assume
A77:    m1 in dom q1;
        given j being Nat such that
A78:    j in dom q2 and
A79:    m2 = len q1 + j;
        reconsider j as Element of NAT by ORDINAL1:def 13;
A80:    (q1 ^ q2).m2 = q2.j by A78,A79,Def7;
        (q1 ^ q2).m1 = q1.m1 & q1.m1 = q.m1 by A77,Def7,FUNCT_1:70;
        then
A81:    q.m1 = q.(y + j) by A15,A16,A72,A78,A80;
        1 <= j by A15,A78,Th3;
        then
A82:    1 <= y + j by NAT_1:12;
        j <= n by A15,A78,Th3;
        then y + j <= len q by A13,XREAL_1:9;
        then y + j in Seg(len q) by A82;
        then
A83:    y + j in dom q by Def3;
        m1 in Seg k by A17,A77,Th21;
        then
A84:    m1 <= k by Th3;
        k < y by A11,XREAL_1:31;
        then
A85:    m1 < y by A84,XXREAL_0:2;
        dom q1 c= dom q & y <= y + j by NAT_1:12,RELAT_1:89;
        hence thesis by A5,A77,A81,A83,A85,FUNCT_1:def 8;
      end;
A86:  now
        assume
A87:    m2 in dom q1;
        given j be Nat such that
A88:    j in dom q2 and
A89:    m1 = len q1 + j;
        reconsider j as Element of NAT by ORDINAL1:def 13;
A90:    (q1 ^ q2).m1 = q2.j by A88,A89,Def7;
        (q1 ^ q2).m2 = q1.m2 & q1.m2 = q.m2 by A87,Def7,FUNCT_1:70;
        then
A91:    q.m2 = q.(y + j) by A15,A16,A72,A88,A90;
        1 <= j by A15,A88,Th3;
        then
A92:    1 <= y + j by NAT_1:12;
        j <= n by A15,A88,Th3;
        then y + j <= len q by A13,XREAL_1:9;
        then y + j in Seg(len q) by A92;
        then
A93:    y + j in dom q by Def3;
        m2 in Seg k by A17,A87,Th21;
        then
A94:    m2 <= k by Th3;
        k < y by A11,XREAL_1:31;
        then
A95:    m2 < y by A94,XXREAL_0:2;
        dom q1 c= dom q & y <= y + j by NAT_1:12,RELAT_1:89;
        hence thesis by A5,A87,A91,A93,A95,FUNCT_1:def 8;
      end;
      now
        given j1 being Nat such that
A96:    j1 in dom q2 and
A97:    m1 = len q1 + j1;
        given j2 being Nat such that
A98:    j2 in dom q2 and
A99:    m2 = len q1 + j2;
        reconsider j1,j2 as Element of NAT by ORDINAL1:def 13;
A100:   (q1 ^ q2).m1 = q2.j1 by A96,A97,Def7;
A101:   (q1 ^ q2).m2 = q2.j2 by A98,A99,Def7;
A102:   (q1 ^ q2).m1 = q.(y + j1) by A15,A16,A96,A100;
A103:   (q1 ^ q2).m2 = q.(y + j2) by A15,A16,A98,A101;
A104:   1 <= j1 by A15,A96,Th3;
A105:   1 <= j2 by A15,A98,Th3;
A106:   1 <= y + j1 by A104,NAT_1:12;
A107:   1 <= y + j2 by A105,NAT_1:12;
A108:   j1 <= n by A15,A96,Th3;
A109:   j2 <= n by A15,A98,Th3;
A110:   y + j1 <= len q by A13,A108,XREAL_1:9;
A111:   y + j2 <= len q by A13,A109,XREAL_1:9;
A112:   y + j1 in Seg(len q) by A106,A110;
A113:   y + j2 in Seg(len q) by A107,A111;
A114:   y + j1 in dom q by A112,Def3;
        y + j2 in dom q by A113,Def3;
        then y + j1 = y + j2 by A5,A72,A102,A103,A114,FUNCT_1:def 8;
        hence thesis by A97,A99;
      end;
      hence thesis by A71,A74,A76,A86,Th38;
    end;
A115: now
      let j be Nat;
      assume
A116: j in dom(q1 ^ <* x *>);
A117: now
        assume
A118:   j in dom q1;
        then (q1 ^ <* x *>).j = q1.j by Def7;
        hence q.j = (q1 ^ <* x *>).j by A118,FUNCT_1:70;
      end;
      now
        given i be Nat such that
A119:   i in dom<* x *> and
A120:   j = len q1 + i;
        i in {1} by A119,Th4,Th55;
        then i = 1 by TARSKI:def 1;
        hence q.j = (q1 ^ <* x *>).j by A8,A11,A18,A120,Th59;
      end;
      hence q.j = (q1 ^ <* x *>).j by A116,A117,Th38;
    end;
    now
      let j be Nat;
      assume j in dom q2;
      hence q2.j = q.(len q1 + 1 + j) by A11,A15,A16,A18
        .= q.(len q1 + len<* x *> + j) by Th56
        .= q.(len(q1 ^ <* x *>) + j) by Th35;
    end;
    then q1 ^ <* x *> ^ q2 = q by A19,A115,Def7;
    then
A121: len q = len(q1 ^ <* x *>) + len q2 by Th35
      .= len <* x *> + len q1 + len q2 by Th35
      .= 1 + len q1 + len q2 by Th57
      .= 1 + (len q1 + len q2)
      .= len(q1 ^ q2) + 1 by Th35;
    len(p ^ <* x *>) = len p + len<* x *> by Th35
      .= len p + 1 by Th57;
    hence len(p ^ <* x *>) = len q by A2,A3,A4,A20,A50,A51,A70,A121,
    FUNCT_1:84;
  end;
  then
A122: for p,x st P[p] holds P[p^<*x*>];
  for p holds P[p] from IndSeq(A1,A122);
  hence thesis;
end;

theorem
  {} in D*
proof
  {} = <*>D;
  hence thesis by Def11;
end;

scheme SepSeq{D()->non empty set, P[FinSequence]}:
  ex X st for x holds x in X iff ex p st p in D()* & P[p] & x=p
proof
  defpred R[set] means ex p st P[p] & $1=p;
  consider Y such that
A1: x in Y iff x in D()* & R[x] from XBOOLE_0:sch 1;
  take Y;
  x in Y iff ex p st p in D()* & P[p] & x=p
  proof
    now
      assume x in Y;
      then x in D()* & ex p st P[p] & x=p by A1;
      hence ex p st p in D()* & P[p] & x=p;
    end;
    hence thesis by A1;
  end;
  hence thesis;
end;

::                           ::
::        Subsequences       ::
::                           ::

definition
  let IT be Function;
  attr IT is FinSubsequence-like means
  :Def12:
  ex k st dom IT c= Seg k;
end;

registration
  cluster FinSubsequence-like Function;
  existence
  proof
    take {},len {};
    thus thesis;
  end;
end;

definition
  mode FinSubsequence is FinSubsequence-like Function;
end;

registration
 cluster FinSequence-like -> FinSubsequence-like Function;
 coherence
proof
  let p be Function;
  assume p is FinSequence-like;
   then reconsider p as FinSequence;
  dom p = Seg len p by Def3;
  hence thesis by Def12;
end;
 let p be FinSubsequence, X be set;
 cluster p|X -> FinSubsequence-like;
 coherence
proof
  consider k such that
W: dom p c= Seg k by Def12;
   dom(p|X) c= dom p by RELAT_1:89;
   then  dom(p|X) c= Seg k by W,XBOOLE_1:1;
  hence thesis by Def12;
end;
 cluster X|p -> FinSubsequence-like;
 coherence
proof
  consider k such that
W: dom p c= Seg k by Def12;
   dom(X|p) c= dom p by FUNCT_1:89;
   then  dom(X|p) c= Seg k by W,XBOOLE_1:1;
  hence thesis by Def12;
end;
end;

reserve p9 for FinSubsequence;

definition
  let X;
  given k being Nat such that
A1: X c= Seg k;
  func Sgm X -> FinSequence of NAT means
  :Def13:
  rng it = X & for l,m,k1,k2 being Nat st 1 <= l & l < m & m <= len it &
  k1=it.l & k2=it.m holds k1 < k2;
  existence
  proof
    defpred P[Nat] means for X st X c= Seg $1 ex p being FinSequence of NAT st
    rng p = X & for l,m,k1,k2 being Nat st ( 1 <= l & l < m & m <= len p &
    k1=p.l & k2=p.m) holds k1 < k2;
A2: P[0]
    proof
      let X;
      assume
A3:   X c= Seg 0;
      take <*>(NAT);
      thus rng <*>(NAT) = X by A3;
      thus thesis;
    end;
A4: for k being Nat st P[k] holds P[k+1]
    proof
      let k be Nat such that
A5:   for X st X c= Seg k holds ex p being FinSequence of NAT st
      rng p = X & for l,m,k1,k2 be Nat st 1 <= l & l < m & m <= len p &
      k1=p.l & k2=p.m holds k1 < k2;
      let X;
      assume
A6:   X c= Seg(k+1);
      now
        assume not X c= Seg k;
        then consider x such that
A7:     x in X and
A8:     not x in Seg k by TARSKI:def 3;
        x in Seg(k+1) by A6,A7;
        then reconsider n=x as Element of NAT;
A9:     n=k+1
        proof
          assume
A10:      n <> k+1;
A11:      n <= k+1 by A6,A7,Th3;
A12:      1 <= n by A6,A7,Th3;
          n <= k by A10,A11,NAT_1:8;
          hence contradiction by A8,A12;
        end;
        set Y=X\{k+1};
A13:    Y c= Seg k
        proof
          let x;
          assume
A14:      x in Y;
          then
A15:      x in X;
A16:      not x in {k+1} by A14,XBOOLE_0:def 5;
A17:      x in Seg(k+1) by A6,A15;
A18:      x<>k+1 by A16,TARSKI:def 1;
          reconsider m=x as Element of NAT by A17;
A19:      m <= k+1 by A6,A15,Th3;
A20:      1 <= m by A6,A15,Th3;
          m <= k by A18,A19,NAT_1:8;
          hence thesis by A20;
        end;
        then consider p being FinSequence of NAT such that
A21:    rng p = Y and
A22:    for l,m,k1,k2 be Nat st 1 <= l & l < m & m <= len p & k1=p.l & k2
        =p.m holds k1 < k2
        by A5;
        reconsider k as Element of NAT by ORDINAL1:def 13;
        consider q such that
A23:    q=p^<*k+1*>;
        reconsider q as FinSequence of NAT by A23;
A24:    {k+1} c= X by A7,A9,ZFMISC_1:37;
A25:    rng q = rng p \/ rng <*k+1*> by A23,Th44
          .= Y \/ {k+1} by A21,Th55
          .= X \/ {k+1} by XBOOLE_1:39
          .= X by A24,XBOOLE_1:12;
        for l,m,k1,k2 be Nat st 1 <= l & l < m & m <= len q &
        k1=q.l & k2=q.m holds k1 < k2
        proof
          let l,m,k1,k2 be Nat;
          assume that
A26:      1 <= l and
A27:      l < m and
A28:      m <= len q and
A29:      k1=q.l and
A30:      k2=q.m;
          l < len (p^<*k+1*>) by A23,A27,A28,XXREAL_0:2;
          then l < len p + len <*k+1*> by Th35;
          then l < len p + 1 by Th56;
          then l <= len p by NAT_1:13;
          then l in Seg len p by A26,Th3;
          then
A31:      l in dom p by Def3;
A32:      1 <= m by A26,A27,XXREAL_0:2;
A33:      now
            assume
A34:        m=len q;
            k1 = p.l by A23,A29,A31,Def7;
            then k1 in Y by A21,A31,FUNCT_1:def 5;
            then
A35:        k1 <= k by A13,Th3;
            q.m = (p^<*k+1*>).(len p + len <*k+1*>) by A23,A34,Th35
              .=(p^<*k+1*>).(len p + 1) by Th56
              .= k+1 by Th59;
            hence thesis by A30,A35,NAT_1:13;
          end;
          now
            assume m <> len q;
            then m < len (p^<*k+1*>) by A23,A28,XXREAL_0:1;
            then m < len p + len <*k+1*> by Th35;
            then m < len p + 1 by Th56;
            then
A36:        m <= len p by NAT_1:13;
            then m in Seg len p by A32,Th3;
            then m in dom p by Def3;
            then
A37:        k2 = p.m by A23,A30,Def7;
            k1 = p.l by A23,A29,A31,Def7;
            hence thesis by A22,A26,A27,A36,A37;
          end;
          hence thesis by A33;
        end;
        hence thesis by A25;
      end;
      hence thesis by A5;
    end;
    for k being Nat holds P[k] from NAT_1:sch 2(A2,A4);
    hence thesis by A1;
  end;
  uniqueness
  proof
    let p,q be FinSequence of NAT such that
    A38: (
 rng p = X & for l,m,k1,k2 being Nat st 1 <= l & l < m & m <= len p & k1=p.
l & k2=p.m holds k1 < k2 )&( rng q = X & for l,m,k1,k2 being Nat st 1 <= l &
    l < m & m <= len q & k1=q.l & k2=q.m holds k1 < k2 );
    defpred S[FinSequence] means for X st ex k being Nat st X c= Seg k holds
    ($1 is FinSequence of NAT & rng $1 = X &
    for l,m,k1,k2 being Nat st ( 1 <= l & l < m & m <= len $1 &
    k1=$1.l & k2=$1.m) holds k1 < k2) implies
    for q being FinSequence of NAT st rng q = X &
    for l,m,k1,k2 being Nat st ( 1 <= l & l < m & m <= len q &
    k1=q.l & k2=q.m) holds k1 < k2 holds q=$1;
A39: S[{}];
A40: for p,x st S[p] holds S[p^<*x*>]
    proof
      let p,x;
      assume
A41:  S[p];
      let X;
      given k being Nat such that
A42:  X c= Seg k;
      assume that
A43:  p^<*x*> is FinSequence of NAT and
A44:  rng (p^<*x*>) = X and
A45:  for l,m,k1,k2 being Nat st 1 <= l & l < m & m <= len(p^<*x*>) &
      k1=(p^<*x*>).l & k2=(p^<*x*>).m holds k1 < k2;
      let q be FinSequence of NAT;
      assume that
A46:  rng q = X and
A47:  for l,m,k1,k2 being Nat st 1 <= l & l < m & m <= len q & k1=q.l &
      k2=q.m holds k1 < k2;
      1 in Seg 1;
      then
A48:  1 in dom <*x*> by Def8;
A49:  ex m st m=x & for l st l in X & l <> x holds l < m
      proof
        <*x*> is FinSequence of NAT by A43,Th50;
        then rng <*x*> c= NAT by Def4;
        then {x} c= NAT by Th55;
        then reconsider m=x as Element of NAT by ZFMISC_1:37;
        take m;
        thus m=x;
        thus for l st l in X & l <> x holds l < m
        proof
          let l;
          assume that
A50:      l in X and
A51:      l <> x;
          consider y such that
A52:      y in dom (p^<*x*>) and
A53:      l=(p^<*x*>).y by A44,A50,FUNCT_1:def 5;
A54:      y in Seg len (p^<*x*>) by A52,Def3;
          reconsider k=y as Element of NAT by A52;
          k <= len (p^<*x*>) by A54,Th3;
          then k <= len p + len <*x*> by Th35;
          then
A55:      k <= len p + 1 by Th56;
A56:      k <> len p + 1
          proof
            assume k = len p + 1;
            then (p^<*x*>).k = <*x*>.1 by A48,Def7
              .= x by Def8;
            hence contradiction by A51,A53;
          end;
A57:      1 <= k by A54,Th3;
          k < len p + 1 by A55,A56,XXREAL_0:1;
          then k < len p + len <*x*> by Th56;
          then
A58:      k < len(p^<*x*>) by Th35;
          m=(p^<*x*>).(len p + 1) by Th59
            .= (p^<*x*>).(len p + len <*x*>) by Th56
            .= (p^<*x*>).(len(p^<*x*>)) by Th35;
          hence thesis by A45,A53,A57,A58;
        end;
      end;
      then reconsider m = x as Nat;
      len q <> 0
      proof
        assume len q = 0;
        then p^<*x*> = {} by A44,A46,Lm4,RELAT_1:60;
        then 0 = len (p^<*x*>) .= len p + len <*x*> by Th35
          .= 1 + len p by Th56;
        hence contradiction;
      end;
      then consider n be Nat such that
A59:  len q = n+1 by NAT_1:6;
      deffunc F(Nat) = q.$1;
      ex q9 being FinSequence st len q9 = n &
      for m st m in dom q9 holds q9.m = F(m) from SeqLambda;
      then consider q9 being FinSequence such that
A60:  len q9 = n and
A61:  for m st m in dom q9 holds q9.m = q.m;
      now
        let x;
        assume x in rng q9;
        then consider y such that
A62:    y in dom q9 and
A63:    x=q9.y by FUNCT_1:def 5;
A64:    y in Seg len q9 by A62,Def3;
        reconsider y as Element of NAT by A62;
A65:    y <= n by A60,A64,Th3;
A66:    n <= n + 1 by NAT_1:11;
A67:    1 <= y by A64,Th3;
        y <= n+1 by A65,A66,XXREAL_0:2;
        then y in Seg (n+1) by A67;
        then y in dom q by A59,Def3;
        then
A68:    q.y in rng q by FUNCT_1:def 5;
        rng q c= NAT by Def4;
        then q.y in NAT by A68;
        hence x in NAT by A61,A62,A63;
      end;
      then rng q9 c= NAT by TARSKI:def 3;
      then reconsider f=q9 as FinSequence of NAT by Def4;
A69:  dom q = Seg (n+1) by A59,Def3
        .= Seg (len q9 + len <*x*>) by A60,Th56;
A70:  for m st m in dom <*x*> holds q.(len q9 + m) = <*x*>.m
      proof
        let m;
        assume m in dom <*x*>;
        then
A71:    m in {1} by Def8,Th4;
        then
A72:    m=1 by TARSKI:def 1;
        q.(len q9 + m) = x
        proof
          assume q.(len q9 + m) <> x;
          then
A73:      q.len q <> x by A59,A60,A71,TARSKI:def 1;
          consider d being Nat such that
A74:      d=x and
A75:      for l st l in X & l<>x holds l<d by A49;
          x in {x} by TARSKI:def 1;
          then x in rng <*x*> by Th55;
          then x in rng p \/ rng <*x*> by XBOOLE_0:def 3;
          then x in rng q by A44,A46,Th44;
          then consider y such that
A76:      y in dom q and
A77:      x=q.y by FUNCT_1:def 5;
A78:      y in Seg len q by A76,Def3;
          reconsider y as Element of NAT by A76;
A79:      1 <= y by A78,Th3;
A80:      y <= len q by A78,Th3;
          then
A81:      y < len q by A73,A77,XXREAL_0:1;
          1 <= len q by A79,A80,XXREAL_0:2;
          then len q in Seg len q;
          then
A83:      len q in dom q by Def3;
A85:      q.len q in X by A46,A83,FUNCT_1:def 5;
          reconsider k = q.len q as Nat;
          k < d by A73,A75,A85;
          hence contradiction by A47,A74,A77,A79,A81;
        end;
        hence thesis by A72,Th57;
      end;
      then
A86:  q9^<*x*> = q by A61,A69,Def7;
A87:  ex m st X\{x} c= Seg m by A42,XBOOLE_1:1;
A88:  not x in rng p
      proof
        assume x in rng p;
        then consider y such that
A89:    y in dom p and
A90:    x=p.y by FUNCT_1:def 5;
A91:    y in Seg len p by A89,Def3;
        reconsider y as Element of NAT by A89;
A92:    y <= len p by A91,Th3;
A93:    len p + 1 = len p + len <*x*> by Th56
          .= len (p^<*x*>) by Th35;
A94:    1 <= y by A91,Th3;
A95:    y < len p + 1 by A92,NAT_1:13;
A96:    m = (p^<*x*>).y by A89,A90,Def7;
        m = (p^<*x*>).(len p + 1 ) by Th59;
        hence contradiction by A45,A93,A94,A95,A96;
      end;
A97:  X = rng p \/ rng <*x*> by A44,Th44
        .= rng p \/ {x} by Th56;
A98:  for z holds z in rng p \/ {x} \ {x} iff z in rng p
      proof
        let z;
        thus z in rng p \/ {x} \ {x} implies z in rng p
        proof
          assume
A99:      z in rng p \/ {x} \ {x};
          then not z in {x} by XBOOLE_0:def 5;
          hence thesis by A99,XBOOLE_0:def 3;
        end;
        assume z in rng p;
        then
        ( not z in {x})& z in rng p \/ {x} by A88,TARSKI:def 1,XBOOLE_0:def 3;
        hence thesis by XBOOLE_0:def 5;
      end;
A100: for l,m,k1,k2 being Nat st 1 <= l & l < m & m <= len p &
      k1=p.l & k2=p.m holds k1 < k2
      proof
        let l,m,k1,k2 be Nat;
        assume that
A101:   1 <= l and
A102:   l < m and
A103:   m <= len p and
A104:   k1=p.l and
A105:   k2=p.m;
        l <= len p by A102,A103,XXREAL_0:2;
        then l in Seg len p by A101,Th3;
        then l in dom p by Def3;
        then
A106:   k1 = (p^<*x*>).l by A104,Def7;
        1 <= m by A101,A102,XXREAL_0:2;
        then m in Seg len p by A103,Th3;
        then m in dom p by Def3;
        then
A107:   k2 = (p^<*x*>).m by A105,Def7;
        len p <= len p + 1 by NAT_1:11;
        then m <= len p + 1 by A103,XXREAL_0:2;
        then m <= len p + len <*x*> by Th56;
        then m <= len (p^<*x*>) by Th35;
        hence thesis by A45,A101,A102,A106,A107;
      end;
A108: p is FinSequence of NAT by A43,Th50;
A109: rng p = X\{x} by A97,A98,TARSKI:2;
A110: not x in rng f
      proof
        assume x in rng f;
        then consider y such that
A111:   y in dom f and
A112:   x=f.y by FUNCT_1:def 5;
A113:   y in Seg len f by A111,Def3;
        reconsider y as Element of NAT by A111;
A114:   y <= len f by A113,Th3;
A115:   len f + 1 = len f + len <*x*> by Th56
          .= len (f^<*x*>) by Th35;
A116:   1 <= y by A113,Th3;
A117:   y < len f + 1 by A114,NAT_1:13;
A118:   m = q.y by A61,A111,A112;
        m = q.(len f + 1) by A86,Th59;
        hence contradiction by A47,A86,A115,A116,A117,A118;
      end;
A119: X = rng f \/ rng <*x*> by A46,A86,Th44
        .= rng f \/ {x} by Th56;
A120: for z holds z in rng f \/ {x} \ {x} iff z in rng f
      proof
        let z;
        thus z in rng f \/ {x} \ {x} implies z in rng f
        proof
          assume
A121:     z in rng f \/ {x} \ {x};
          then not z in {x} by XBOOLE_0:def 5;
          hence thesis by A121,XBOOLE_0:def 3;
        end;
        assume z in rng f;
        then
        ( not z in {x})& z in rng f \/ {x} by A110,TARSKI:def 1,XBOOLE_0:def 3;
        hence thesis by XBOOLE_0:def 5;
      end;
      A122: for l,m,k1,k2 being Nat st 1 <= l & l < m & m <= len f &
      k1=f.l & k2=f.m holds k1 < k2
      proof
        let l,m,k1,k2 be Nat;
        assume that
A123:   1 <= l and
A124:   l < m and
A125:   m <= len f and
A126:   k1=f.l and
A127:   k2=f.m;
A128:   m <= len q by A59,A60,A125,NAT_1:13;
        l <= n by A60,A124,A125,XXREAL_0:2;
        then
A129:   l in Seg n by A123,Th3;
        1 <= m by A123,A124,XXREAL_0:2;
        then
A130:   m in Seg n by A60,A125,Th3;
A131:   l in dom q9 by A60,A129,Def3;
A132:   m in dom q9 by A60,A130,Def3;
A133:   k1 = q.l by A61,A126,A131;
        k2 = q.m by A61,A127,A132;
        hence thesis by A47,A123,A124,A128,A133;
      end;
      rng f = X\{x} by A119,A120,TARSKI:2;
      then q9 = p by A41,A87,A100,A108,A109,A122;
      hence thesis by A61,A69,A70,Def7;
    end;
    for p holds S[p] from IndSeq(A39,A40);
    hence thesis by A1,A38;
  end;
end;

canceled 4;

theorem Th71:
  rng Sgm dom p9 = dom p9
proof
  ex k st dom p9 c= Seg k by Def12;
  hence thesis by Def13;
end;

definition
  let p9;
  func Seq p9 -> Function equals
  p9* Sgm(dom p9);
  coherence;
end;

registration
  let p9;
  cluster Seq p9 -> FinSequence-like;
  coherence
  proof
    rng Sgm dom p9 = dom p9 by Th71;
    then dom(p9*Sgm(dom p9)) = dom Sgm dom p9 by RELAT_1:46
      .=Seg len Sgm dom p9 by Def3;
    hence thesis by Def2;
  end;
end;

theorem
  for X st ex k st X c= Seg k holds Sgm X = {} iff X = {}
proof
  let X;
  given k such that
A1: X c= Seg k;
  thus Sgm X = {} implies X = {}
  proof
    assume Sgm X = {};
    hence X = rng {} by A1,Def13
      .= {};
  end;
  assume X={};
  then rng Sgm X = {} by A1,Def13;
  then dom Sgm X = Seg 0 by RELAT_1:65;
  hence thesis;
end;

begin :: from FINSET_1, 1998

theorem :: FINSET_1:def 1
  D is finite iff ex p st D = rng p
proof
  thus D is finite implies ex p st D = rng p
  proof
    given g being Function such that
A1: rng g = D and
A2: dom g in omega;
    reconsider n = dom g as Element of NAT by A2;
    defpred R[set,set] means P[$2,$1];
A3: for x st x in Seg n ex y st R[x,y]
    proof
      let x;
      assume
A4:   x in Seg n;
      then reconsider x as Element of NAT;
      x >= 1 by A4,Th3;
      then ex k be Nat st x = 1+k by NAT_1:10;
      hence thesis;
    end;
    consider f such that
A5: dom f = Seg n and
A6: for x st x in Seg n holds R[x,f.x] from CLASSES1:sch 1(A3);
A7: rng f = dom g
    proof
A8:   n = { k where k is Element of NAT: k < n } by AXIOMS:30;
      thus rng f c= dom g
      proof
        let x;
        assume x in rng f;
        then consider y such that
A9:     y in dom f and
A10:    x = f.y by FUNCT_1:def 5;
        consider k such that
A11:    x = k and
A12:    y = k+1 by A5,A6,A9,A10;
        k + 1 <= n by A5,A9,A12,Th3;
        then k in NAT & k < n by NAT_1:13,ORDINAL1:def 13;
        hence thesis by A8,A11;
      end;
      let x;
      assume x in dom g;
      then consider k being Element of NAT such that
A13:  x = k and
A14:  k < n by A8;
      1 <= k+1 & k+1 <= n by A14,NAT_1:11,13;
      then
A15:  k+1 in Seg n;
      then ex k1 st f.(k+1) = k1 & k+1 = k1+1 by A6;
      hence thesis by A5,A13,A15,FUNCT_1:def 5;
    end;
    then dom(g*f) = Seg n by A5,RELAT_1:46;
    then reconsider p = g*f as FinSequence by Def2;
    take p;
    thus thesis by A1,A7,RELAT_1:47;
  end;
  given p such that
A16: D = rng p;
  consider n such that
A17: dom p = Seg n by Def2;
A18: n = { k where k is Element of NAT: k < n } by AXIOMS:30;
A19: for x st x in n ex y st P[x,y]
  proof
    let x;
    assume x in n;
    then ex k being Element of NAT st x = k & k < n by A18;
    then reconsider k = x as Nat;
    take k+1;
    thus thesis;
  end;
  consider f such that
A20: dom f = n and
A21: for x st x in n holds P[x,f.x] from CLASSES1:sch 1(A19);
  take p*f;
A22: rng f = dom p
  proof
    thus rng f c= dom p
    proof
      let x;
      assume x in rng f;
      then consider y such that
A23:  y in dom f and
A24:  x = f.y by FUNCT_1:def 5;
      consider k such that
A25:  y = k and
A26:  x = k+1 by A20,A21,A23,A24;
      ex m being Element of NAT st k = m & m < n by A18,A20,A23,A25;
      then 1 <= k+1 & k+1 <= n by NAT_1:11,13;
      hence thesis by A17,A26;
    end;
    let x;
    assume
A27: x in dom p;
    then reconsider x as Element of NAT;
    1 <= x by A17,A27,Th3;
    then consider m be Nat such that
A28: x = 1+m by NAT_1:10;
    reconsider m as Element of NAT by ORDINAL1:def 13;
    x <= n by A17,A27,Th3;
    then m < n by A28,NAT_1:13;
    then
A29: m in n by A18;
    then ex k st m = k & f.m = k+1 by A21;
    hence thesis by A20,A28,A29,FUNCT_1:def 5;
  end;
  hence rng(p*f) = D by A16,RELAT_1:47;
  dom(p*f) = dom f by A22,RELAT_1:46;
  hence thesis by A20,ORDINAL1:def 13;
end;

begin :: Moved from CARD_1, 1999

theorem
  Seg n,Seg m are_equipotent implies n = m
proof
  defpred P[Nat] means ex n st Seg n,Seg $1 are_equipotent & n <> $1;
  assume Seg n,Seg m are_equipotent & n <> m;
  then
A1: ex m being Nat st P[m];
  consider m being Nat such that
A2: P[m] and
A3: for k being Nat st P[k] holds m <= k from NAT_1:sch 5(A1);
  consider n such that
A4: Seg n,Seg m are_equipotent and
A5: n <> m by A2;
A6: ex f st
  f is one-to-one & dom f = Seg n & rng f = Seg m by A4,WELLORD2:def 4;
A7: now
    assume m = 0;
    then Seg m = {};
    then Seg m = Seg n by A6,RELAT_1:65;
    hence contradiction by A5,Th8;
  end;
  then consider m1 being Nat such that
A8: m = m1+1 by NAT_1:6;
A9: now
    assume n = 0;
    then Seg n = {};
    then Seg m = Seg n by A6,RELAT_1:65;
    hence contradiction by A5,Th8;
  end;
  then consider n1 being Nat such that
A10: n = n1+1 by NAT_1:6;
  reconsider m1,n1 as Element of NAT by ORDINAL1:def 13;
A11: n in Seg n by A9,Th5;
A12: m in Seg m by A7,Th5;
A13: not n1+1 <= n1 by NAT_1:13;
A14: not m1+1 <= m1 by NAT_1:13;
A15: not n in Seg n1 by A10,A13,Th3;
A16: not m in Seg m1 by A8,A14,Th3;
A17: (Seg n1) /\ { n } c= {}
  proof
    let x;
    assume x in (Seg n1) /\ { n };
    then x in Seg n1 & x in { n } by XBOOLE_0:def 4;
    hence thesis by A15,TARSKI:def 1;
  end;
A18: (Seg m1) /\ { m } c= {}
  proof
    let x;
    assume x in (Seg m1) /\ { m };
    then x in Seg m1 & x in { m } by XBOOLE_0:def 4;
    hence thesis by A16,TARSKI:def 1;
  end;
A19: Seg n = (Seg n1) \/ { n } by A10,Th11;
A20: Seg m = (Seg m1) \/ { m } by A8,Th11;
A21: (Seg n1) /\ { n } = {} by A17;
A22: (Seg m1) /\ { m } = {} by A18;
  A23: (
Seg n1) \ { n } = ((Seg n1) \/ { n }) \ { n } & (Seg n1) misses { n } by
  A21,XBOOLE_0:def 7,XBOOLE_1:40;
  A24: (
Seg m1) \ { m } = ((Seg m1) \/ { m }) \ { m } & (Seg m1) misses { m } by
  A22,XBOOLE_0:def 7,XBOOLE_1:40;
A25: (Seg n) \ { n } = Seg n1 by A19,A23,XBOOLE_1:83;
  (Seg m) \ { m } = Seg m1 by A20,A24,XBOOLE_1:83;
  hence contradiction by A3,A4,A5,A8,A10,A11,A12,A14,A25,CARD_1:61;
end;

theorem
  Seg n,n are_equipotent by Lm1;

theorem
  card Seg n = card n by Lm2;

theorem
  X is finite implies ex n st X,Seg n are_equipotent
proof
  assume X is finite;
  then consider n being Nat such that
A1: X,n are_equipotent by CARD_1:74;
  take n;
  n,Seg n are_equipotent by Lm1;
  hence thesis by A1,WELLORD2:22;
end;

theorem
  for n being Nat holds card Seg n = n
proof
  let n be Nat;
  Seg n,n are_equipotent by Lm1;
  hence thesis by CARD_1:def 5;
end;

begin :: Addenda
:: from FINSEQ_5

registration
  let x be set;
  cluster <*x*> -> non empty;
  coherence;
end;

:: From SPRECT_1

registration
  cluster non empty FinSequence;
  existence
  proof
    take <* 0 *>;
    thus thesis;
  end;
end;

:: From FUNCT_7, actually from GOBOARD4

registration
  let f1 be FinSequence, f2 be non empty FinSequence;
  cluster f1^f2 -> non empty;
  coherence by Th48;
  cluster f2^f1 -> non empty;
  coherence by Th48;
end;

registration
  let x,y be set;
  cluster <*x,y*> -> non empty;
  coherence;
  let z be set;
  cluster <*x,y,z*> -> non empty;
  coherence;
end;

:: from MATRIX_2

scheme SeqDEx{D()->non empty set,A()->Nat,P[set,set]}:
  ex p being FinSequence of D() st dom p = Seg A() &
  for k st k in Seg A() holds P[k,p.k]
provided
A1: for k st k in Seg A() ex x being Element of D() st P[k,x]
proof
A2: for y be set st y in Seg A() ex x st x in D() & P[y,x]
  proof
    let y;
    assume
A3: y in Seg A();
    then reconsider k=y as Element of NAT;
    consider x being Element of D() such that
A4: P[k,x] by A1,A3;
    take x;
    thus thesis by A4;
  end;
  consider f being Function such that
A5: dom f = Seg A() and
A6: rng f c= D() and
A7: for y be set st y in Seg A() holds P[y,f.y] from WELLORD2:sch 1(A2);
  reconsider f as FinSequence by A5,Def2;
  reconsider p=f as FinSequence of D() by A6,Def4;
  take p;
  thus thesis by A5,A7;
end;

:: from TOPREAL1

reserve D for set,
  p for FinSequence of D,
  m for Nat;

definition
  let p be FinSequence;
  let m;
  func p|m -> FinSequence equals
  p|Seg m;
  coherence by Th19;
end;

definition
  let D,p,m;
  redefine func p|m -> FinSequence of D;
  coherence by Th23;
end;

registration
  let f be FinSequence;
  cluster f|0 -> empty;
  coherence;
end;

theorem
  len q <= i implies q|i = q
proof
  assume len q <= i;
  then Seg len q c= Seg i by Th7;
  then dom q c= Seg i by Def3;
  hence thesis by RELAT_1:97;
end;

theorem
  i <= len q implies len(q|i) = i
proof
  assume i <= len q;
  then Seg i c= Seg(len q) by Th7;
  then Seg i c= dom q by Def3;
  then i in NAT & Seg i = dom(q|i) by ORDINAL1:def 13,RELAT_1:91;
  hence thesis by Def3;
end;

:: from FSM_1

theorem
  i in Seg n implies i+m in Seg (n+m)
proof
  assume
A1: i in Seg n;
  then
A2: 1 <= i by Th3;
A3: i <= n by A1,Th3;
  i <= i+m by NAT_1:11;
  then
A4: 1 <= i+m by A2,XXREAL_0:2;
  i+m <= n+m by A3,XREAL_1:9;
  hence thesis by A4,Th3;
end;

theorem
  i>0 & i+m in Seg (n+m) implies i in Seg n & i in Seg (n+m)
proof
  assume that
A1: i > 0 and
A2: i+m in Seg (n+m);
  1 = 0+1;
  then
A3: 1 <= i by A1,NAT_1:13;
A4: i+m <= n+m by A2,Th3;
  then i <= n by XREAL_1:8;
  hence i in Seg n by A3,Th3;
  i <= i+m by NAT_1:11;
  then i <= n+m by A4,XXREAL_0:2;
  hence thesis by A3,Th3;
end;

:: from LANG1

definition
  let R be Relation;
  func R[*] -> Relation means
  for x,y being set holds [x,y] in it iff x in field R & y in field R &
  ex p being FinSequence st len p >= 1 & p.1 = x & p.(len p) = y &
  for i being Nat st i >= 1 & i < len p holds [p.i,p.(i+1)] in R;
  existence
  proof
    defpred X[set,set] means
    ex p being FinSequence st len p >= 1 & p.1 = $1 & p.(len p) = $2 &
    for i being Nat st i >= 1 & i < len p holds [p.i,p.(i+1)] in R;
    thus ex S being Relation st for x,y being set holds
    [x,y] in S iff x in field R & y in field R & X[x,y] from RELAT_1:sch 1;
  end;
  uniqueness
  proof
    let R1,R2 be Relation such that
A1: for x,y being set holds [x,y] in R1 iff x in field R & y in field R &
    ex p being FinSequence st len p >= 1 & p.1 = x & p.(len p) = y &
    for i being Nat st i >= 1 & i < len p holds [p.i,p.(i+1)] in R and
A2: for x,y being set holds [x,y] in R2 iff x in field R & y in field R &
    ex p being FinSequence st len p >= 1 & p.1 = x & p.(len p) = y &
    for i being Nat st i >= 1 & i < len p holds [p.i,p.(i+1)] in R;
    let x,y be set;
    thus [x,y] in R1 implies [x,y] in R2
    proof
      assume
A3:   [x,y] in R1;
      then
A4:   x in field R & y in field R by A1;
      ex p being FinSequence st len p >= 1 & p.1 = x & p.(len p) = y & for i
      being Nat st i >= 1 & i < len p holds [p.i,p.(i+1)] in R by A1,A3;
      hence thesis by A2,A4;
    end;
    assume
A5: [x,y] in R2;
    then
A6: x in field R & y in field R by A2;
    ex p being FinSequence st len p >= 1 & p.1 = x & p.(len p) = y & for i
    being Nat st i >= 1 & i < len p holds [p.i,p.(i+1)] in R by A2,A5;
    hence thesis by A1,A6;
  end;
end;

theorem
  for D1,D2 being set st D1 c= D2 holds D1* c= D2*
proof
  let D1,D2 be set such that
A1: D1 c= D2;
  let x be set;
  assume x in D1*;
  then reconsider p = x as FinSequence of D1 by Def11;
  rng p c= D1 by Def4;
  then rng p c= D2 by A1,XBOOLE_1:1;
  then x is FinSequence of D2 by Def4;
  hence thesis by Def11;
end;

:: 2005.05.13, A.T.

registration
  let D be set;
  cluster D* -> functional;
  coherence
  proof
    let x be set;
    thus thesis by Def11;
  end;
end;

:: from SCMFSA_7, 2005.11.21, A.T.

theorem
  for p,q being FinSequence st p c= q holds len p <= len q
proof
  let p,q be FinSequence;
  assume p c= q;
  then card p c= card q by CARD_1:27;
  hence thesis by NAT_1:40;
end;

theorem
  for p,q being FinSequence, i being Nat st
  1 <= i & i <= len p holds (p ^ q).i = p.i
proof
  let p,q be FinSequence, i be Nat;
  assume
A1: 1 <= i & i <= len p;
  i in NAT & Seg len p = dom p by Def3,ORDINAL1:def 13;
  then i in dom p by A1;
  hence thesis by Def7;
end;

theorem
  for p,q being FinSequence, i being Nat st
  1 <= i & i <= len q holds (p ^ q).(len p + i) = q.i
proof
  let p,q be FinSequence, i be Nat;
  assume 1 <= i & i <= len q;
  then
  len p + 1 <= len p + i & len p + i <= len p + len q by XREAL_1:8;
  hence (p ^ q).(len p + i) = q.(len p + i - len p) by Th36
    .= q.i;
end;

:: from GRAPH_2, 2006.01.09, A.T.



scheme FinSegRng{m, n() -> Nat, F(set)->set, P[set]}:
  {F(i) where i is Element of NAT: m()<=i & i<=n() & P[i]} is finite
proof
  set F = {F(i) where i is Element of NAT: m()<=i & i<=n() & P[i]};
  deffunc G(Nat) = F($1-1);
  consider f being FinSequence such that
A1: len f = n()+1 and
A2: for k st k in dom f holds f.k = G(k) from SeqLambda;
  F c= rng f
  proof
    let x be set;
    assume x in F;
    then consider j being Element of NAT such that
A3: x=F(j) and m()<=j and
A4: j<=n() and P[j];
    1<=j+1 & j+1<=n()+1 by A4,NAT_1:11,XREAL_1:8;
    then j+1 in Seg (n()+1);
    then
A5: j+1 in dom f by A1,Def3;
    then f.(j+1) = F(j+1-1) by A2
      .= F(j);
    hence thesis by A3,A5,FUNCT_1:def 5;
  end;
  hence thesis;
end;
 
:: from SCM_1, 2006.03.13, A.T.

Lm6: 1 in Seg 3 & 2 in Seg 3;

Lm7: 3 in Seg 3;

Lm8: 1 in Seg 4 & 2 in Seg 4;

Lm9: 3 in Seg 4 & 4 in Seg 4;

Lm10: 1 in Seg 5 & 2 in Seg 5;

Lm11: 3 in Seg 5 & 4 in Seg 5;

Lm12: 5 in Seg 5;

Lm13: 1 in Seg 6 & 2 in Seg 6;

Lm14: 3 in Seg 6 & 4 in Seg 6;

Lm15: 5 in Seg 6 & 6 in Seg 6;

Lm16: 1 in Seg 7 & 2 in Seg 7;

Lm17: 3 in Seg 7 & 4 in Seg 7;

Lm18: 5 in Seg 7 & 6 in Seg 7;

Lm19: 7 in Seg 7;

Lm20: 1 in Seg 8 & 2 in Seg 8;

Lm21: 3 in Seg 8 & 4 in Seg 8;

Lm22: 5 in Seg 8 & 6 in Seg 8;

Lm23: 7 in Seg 8 & 8 in Seg 8;

theorem Th87:
  for x1, x2, x3, x4 being set, p being FinSequence
  st p = <*x1*>^<*x2*>^<*x3*>^<*x4*>
  holds len p = 4 & p.1 = x1 & p.2 = x2 & p.3 = x3 & p.4 = x4
proof
  let x1, x2, x3, x4 be set, p be FinSequence;
  assume
A1: p = <*x1*>^<*x2*>^<*x3*>^<*x4*>;
  set p13 = <*x1*>^<*x2*>^<*x3*>;
A2: p13 = <*x1, x2, x3*>;
  then
A3: len p13 = 3 by Th62;
A4: p13.1 = x1 & p13.2 = x2 by A2,Th62;
A5: p13.3 = x3 by A2,Th62;
  thus len p = len p13 + len <*x4*> by A1,Th35
    .= 3 + 1 by A3,Th57
    .= 4;
  dom p13 = Seg 3 by A3,Def3;
  hence p.1 = x1 & p.2 = x2 & p.3 = x3 by A1,A4,A5,Def7,Lm6,Lm7;
  thus p.4 = p.(len p13 + 1) by A3
    .= x4 by A1,Th59;
end;

theorem Th88:
  for x1, x2, x3, x4, x5 being set, p being FinSequence
  st p = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>
  holds len p = 5 & p.1 = x1 & p.2 = x2 & p.3 = x3 & p.4 = x4 & p.5 = x5
proof
  let x1, x2, x3, x4, x5 be set, p be FinSequence;
  assume
A1: p = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>;
  set p14 = <*x1*>^<*x2*>^<*x3*>^<*x4*>;
A2: len p14 = 4 by Th87;
A3: p14.1 = x1 & p14.2 = x2 by Th87;
A4: p14.3 = x3 & p14.4 = x4 by Th87;
  thus len p = len p14 + len <*x5*> by A1,Th35
    .= 4 + 1 by A2,Th57
    .= 5;
  dom p14 = Seg 4 by A2,Def3;
  hence p.1 = x1 & p.2 = x2 & p.3 = x3 & p.4 = x4 by A1,A3,A4,Def7,Lm8,Lm9;
  thus p.5 = p.(len p14 + 1) by A2
    .= x5 by A1,Th59;
end;

theorem Th89:
  for x1, x2, x3, x4, x5, x6 being set, p being FinSequence
  st p = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>^<*x6*>
  holds len p = 6 & p.1 = x1 & p.2 = x2 & p.3 = x3 & p.4 = x4 & p.5 = x5 &
  p.6 = x6
proof
  let x1, x2, x3, x4, x5, x6 be set, p be FinSequence;
  assume
A1: p = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>^<*x6*>;
  set p15 = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>;
A2: len p15 = 5 by Th88;
A3: p15.1 = x1 & p15.2 = x2 by Th88;
A4: p15.3 = x3 & p15.4 = x4 by Th88;
A5: p15.5 = x5 by Th88;
  thus len p = len p15 + len <*x6*> by A1,Th35
    .= 5 + 1 by A2,Th57
    .= 6;
  dom p15 = Seg 5 by A2,Def3;
  hence p.1 = x1 & p.2 = x2 & p.3 = x3 & p.4 = x4 & p.5 = x5
  by A1,A3,A4,A5,Def7,Lm10,Lm11,Lm12;
  thus p.6 = p.(len p15 + 1) by A2
    .= x6 by A1,Th59;
end;

theorem Th90:
  for x1, x2, x3, x4, x5, x6, x7 being set, p being FinSequence
  st p = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>^<*x6*>^<*x7*>
  holds len p = 7 & p.1 = x1 & p.2 = x2 & p.3 = x3 & p.4 = x4 & p.5 = x5 &
  p.6 = x6 & p.7 = x7
proof
  let x1, x2, x3, x4, x5, x6, x7 be set, p be FinSequence;
  assume
A1: p = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>^<*x6*>^<*x7*>;
  set p16 = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>^<*x6*>;
A2: len p16 = 6 by Th89;
A3: p16.1 = x1 & p16.2 = x2 by Th89;
A4: p16.3 = x3 & p16.4 = x4 by Th89;
A5: p16.5 = x5 & p16.6 = x6 by Th89;
  thus len p = len p16 + len <*x7*> by A1,Th35
    .= 6 + 1 by A2,Th57
    .= 7;
  dom p16 = Seg 6 by A2,Def3;
  hence p.1 = x1 & p.2 = x2 & p.3 = x3 & p.4 = x4 & p.5 = x5 & p.6 = x6
  by A1,A3,A4,A5,Def7,Lm13,Lm14,Lm15;
  thus p.7 = p.(len p16 + 1) by A2
    .= x7 by A1,Th59;
end;

theorem Th91:
  for x1,x2,x3,x4, x5, x6, x7, x8 being set, p being FinSequence
  st p = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>^<*x6*>^<*x7*>^<*x8*>
  holds len p = 8 & p.1 = x1 & p.2 = x2 & p.3 = x3 & p.4 = x4 & p.5 = x5 &
  p.6 = x6 & p.7 = x7 & p.8 = x8
proof
  let x1, x2, x3, x4, x5, x6, x7, x8 be set, p be FinSequence;
  assume
A1: p = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>^<*x6*>^<*x7*>^<*x8*>;
  set p17 = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>^<*x6*>^<*x7*>;
A2: len p17 = 7 by Th90;
A3: p17.1 = x1 & p17.2 = x2 by Th90;
A4: p17.3 = x3 & p17.4 = x4 by Th90;
A5: p17.5 = x5 & p17.6 = x6 by Th90;
A6: p17.7 = x7 by Th90;
  thus len p = len p17 + len <*x8*> by A1,Th35
    .= 7 + 1 by A2,Th57
    .= 8;
  dom p17 = Seg 7 by A2,Def3;
  hence p.1 = x1 & p.2 = x2 & p.3 = x3 & p.4 = x4 & p.5 = x5 & p.6 = x6 &
  p.7 = x7 by A1,A3,A4,A5,A6,Def7,Lm16,Lm17,Lm18,Lm19;
  thus p.8 = p.(len p17 + 1) by A2
    .= x8 by A1,Th59;
end;

theorem
  for x1,x2,x3,x4,x5,x6,x7, x8, x9 being set, p being FinSequence
  st p = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>^<*x6*>^<*x7*>^<*x8*>^<*x9*>
  holds len p = 9 & p.1 = x1 & p.2 = x2 & p.3 = x3 & p.4 = x4 & p.5 = x5 &
  p.6 = x6 & p.7 = x7 & p.8 = x8 & p.9 = x9
proof
  let x1, x2, x3, x4, x5, x6, x7, x8, x9 be set, p be FinSequence;
  assume
A1: p = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>^<*x6*>^<*x7*>^<*x8*>^<*x9*>;
  set p17 = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>^<*x6*>^<*x7*>^<*x8*>;
A2: len p17 = 8 by Th91;
A3: p17.1 = x1 & p17.2 = x2 by Th91;
A4: p17.3 = x3 & p17.4 = x4 by Th91;
A5: p17.5 = x5 & p17.6 = x6 by Th91;
A6: p17.7 = x7 & p17.8 = x8 by Th91;
  thus len p = len p17 + len <*x9*> by A1,Th35
    .= 8 + 1 by A2,Th57
    .= 9;
  dom p17 = Seg 8 by A2,Def3;
  hence p.1 = x1 & p.2 = x2 & p.3 = x3 & p.4 = x4 & p.5 = x5 & p.6 = x6 &
  p.7 = x7 & p.8 = x8 by A1,A3,A4,A5,A6,Def7,Lm20,Lm21,Lm22,Lm23;
  thus p.9 = p.(len p17 + 1) by A2
    .= x9 by A1,Th59;
end;
 
:: from SCMFSA_7, 2006.03.14, A.T.

theorem
  for p being FinSequence holds p | Seg 0 = {};

theorem
  for f,g being FinSequence holds f | Seg 0 = g | Seg 0;

theorem
  for D being non empty set, x being Element of D holds
  <* x *> is FinSequence of D;

theorem
  for D being set, p,q being FinSequence of D holds p ^ q is FinSequence of D;
 
:: from GROUP_7, 2007.02.10, AK

reserve a, b, c, d, e, f for set;

theorem
  <*a*> = <*b*> implies a = b
proof
  assume
A1: <*a*> = <*b*>;
  thus a = <*a*>.1 by Def8
    .= b by A1,Def8;
end;

theorem
  <*a,b*> = <*c,d*> implies a = c & b = d
proof
  assume
A1: <*a,b*> = <*c,d*>;
  thus a = <*a,b*>.1 by Th61
    .= c by A1,Th61;
  thus b = <*a,b*>.2 by Th61
    .= d by A1,Th61;
end;

theorem
  <*a,b,c*> = <*d,e,f*> implies a = d & b = e & c = f
proof
  assume
A1: <*a,b,c*> = <*d,e,f*>;
  thus a = <*a,b,c*>.1 by Th62
    .= d by A1,Th62;
  thus b = <*a,b,c*>.2 by Th62
    .= e by A1,Th62;
  thus c = <*a,b,c*>.3 by Th62
    .= f by A1,Th62;
end;
 
:: from PRVECT_1, 2007.03.09, A.T

registration
  cluster non empty non-empty FinSequence;
  existence
  proof
    take <*{{}}*>;
    thus <*{{}}*> is non empty;
    assume {} in rng <*{{}}*>;
    then {} in {{{}}} by Th55;
    hence contradiction by TARSKI:def 1;
  end;
end;

theorem Th100:
  for p,q being FinSequence st q = p|Seg n holds len q <= len p
proof
  let p,q be FinSequence;
  assume q = p|Seg n;
  then
A1: dom q = dom p /\ Seg n by RELAT_1:90;
  Seg len q = dom q & Seg len p = dom p by Def3;
  then Seg len q c= Seg len p by A1,XBOOLE_1:17;
  hence thesis by Th7;
end;

theorem
  for p,r being FinSequence st r = p|Seg n ex q being FinSequence st p = r^q
proof
  let p,r be FinSequence;
  assume
A1: r = p|Seg n;
  then consider m be Nat such that
A2: len p = len r + m by Th100,NAT_1:10;
  deffunc U(Nat) = p.(len r + $1);
  consider q being FinSequence such that
A3: len q = m & for k st k in dom q holds q.k = U(k) from SeqLambda;
  take q;
A4: len p = len(r^q) by A2,A3,Th35;
  now
    let k such that
A5: 1 <= k and
A6: k <= len p;
A7: k in NAT by ORDINAL1:def 13;
A8: now
      assume k <= len r;
      then
A9:   k in Seg len r by A5,A7;
A10:  dom r = Seg len r by Def3;
      then (r^q).k = r.k by A9,Def7;
      hence p.k = (r^q).k by A1,A9,A10,FUNCT_1:70;
    end;
    now
      assume
A11:  not k <= len r;
      then consider j being Nat such that
A12:  k = len r + j by NAT_1:10;
      k-len r = j by A12;
      then
A13:  (r^q).k = q.j by A4,A6,A11,Th37;
      j <> 0 by A11,A12;
      then
A14:  0+1 <= j by NAT_1:13;
      j <= len q by A2,A3,A6,A12,XREAL_1:8;
      then j in Seg m by A3,A14,Th3;
      then j in dom q by A3,Def3;
      hence p.k = (r^q).k by A3,A12,A13;
    end;
    hence p.k = (r^q).k by A8;
  end;
  hence thesis by A4,Th18;
end;
 
:: from GOBOARD1, PRALG_1, 2007.04.06, A.T

registration
  let D be non empty set;
  cluster non empty FinSequence of D;
  existence
  proof consider x being Element of D;
    take <*x*>;
    thus thesis;
  end;
end;
 
:: Added by AK, 2007.11.07

definition
  let p,q be FinSequence;
  redefine pred p = q means
  len p = len q & for k st 1 <= k & k <= len p holds p.k = q.k;
  compatibility by Th18;
end;
 
:: from MATRIX_1, 2007.07.22, A.T generalized

theorem
  for M1, M2 being set st card M1 = 0 & card M2 = 0 holds M1 = M2
proof
  let M1, M2 be set;
  assume that
A1: card M1 = {} and
A2: card M2 = {};
  M1 = {} by A1;
  hence thesis by A2;
end;
 
:: from CHAIN_1, 2008.08.31, A.K.

registration
  let n be non zero Nat;
  cluster Seg n -> non empty;
  coherence;
end;
 
:: from GROEB_2, 2008.10.08, A.T.

theorem
  for p being FinSequence, n,m being Nat st m <= n holds p|n|m = p|m
proof
  let p be FinSequence, n,m be Nat;
  assume m <= n;
  then Seg m c= Seg n by Th7;
  hence thesis by RELAT_1:103;
end;
 
:: from CARD_5, 2008.10.08, A.T.

reserve m for Element of NAT;

theorem
  Seg n = (n+1) \ {0}
proof
A1: n+1 = {m: m < n+1} by AXIOMS:30;
  thus Seg n c= (n+1) \ {0}
  proof
    let x;
    assume x in Seg n;
    then consider k being Element of NAT such that
A2: x = k and
A3: 1 <= k and
A4: k <= n;
    k < n+1 by A4,NAT_1:13;
    then
A5: x in n+1 by A1,A2;
    not x in {0} by A2,A3,TARSKI:def 1;
    hence thesis by A5,XBOOLE_0:def 5;
  end;
  let x;
  assume
A6: x in (n+1) \ {0};
  then
A7: x in n+1;
A8: not x in {0} by A6,XBOOLE_0:def 5;
  consider m such that
A9: x = m and
A10: m < n+1 by A1,A7;
A11: x <> 0 by A8,TARSKI:def 1;
  0+1 = 1;
  then
A12: 1 <= m by A9,A11,NAT_1:13;
  m <= n by A10,NAT_1:13;
  hence thesis by A9,A12;
end;
 
:: new, 2009.01.26, A.T.

definition
  let n be Nat, f be FinSequence;
  attr f is n-long means
  :Def18:
  len f = n;
end;
 
:: from CIRCCOMB, 2009.03.27, A.T.

registration
  let n be Nat;
  cluster n-long FinSequence;
  existence
  proof
    set p = Seg n --> {};
    dom p = Seg n by FUNCOP_1:19;
    then reconsider p as FinSequence by Def2;
    take p;
    Seg len p = dom p by Def3;
    hence len p = n by Th8,FUNCOP_1:19;
  end;
end;
 
:: from FACIRC_1, 2009.02.16, A.T.

registration
  let x be set;
  cluster <*x*> -> 1-long;
  coherence
  proof
    thus len<*x*> = 1 by Th57;
  end;
  let y be set;
  cluster <*x,y*> -> 2-long;
  coherence
  proof
    thus len<*x,y*> = 2 by Th61;
  end;
  let z be set;
  cluster <*x,y,z*> -> 3-long;
  coherence
  proof
    thus len<*x,y,z*> = 3 by Th62;
  end;
end;

registration
  cluster 0-long -> empty FinSequence;
  coherence
  proof
    let f be FinSequence;
    assume f is 0-long;
    then len f = 0 by Def18;
    hence thesis;
  end;
  cluster empty -> 0-long FinSequence;
  coherence
  proof
    let f be FinSequence;
    assume f is empty;
    then len f = 0;
    hence thesis by Def18;
  end;
end;
 
:: new, 2009.08.24, A.T.

definition let X be set;
 attr X is FinSequence-membered means
:Def19: x in X implies x is FinSequence;
end;

registration
 cluster empty -> FinSequence-membered set;
 coherence
  proof let X be set such that
Z: X is empty;
   let x;
   assume x in X;
   hence thesis by Z;
  end;
end;

registration
 cluster non empty FinSequence-membered set;
 existence
  proof
   take X = {the FinSequence};
   thus X is non empty;
   let x;
   assume x in X;
    then x = the FinSequence by TARSKI:def 1;
   hence thesis;
  end;
end;

registration let X be set;
 cluster X* -> FinSequence-membered;
 coherence
  proof
   let x;
   assume x in X*;
   hence x is FinSequence by Def11;
  end;
end;

theorem
 for f being Function st f in D* & x in dom f holds f.x in D
proof let f be Function such that
Z1: f in D* and
Z2: x in dom f;
  f is FinSequence of D by Z1,Def11;
  then
A:   rng f c= D by Def4;
  f.x in rng f by Z2,FUNCT_1:12;
 hence f.x in D by A;
end;

registration
 cluster FinSequence-membered -> functional set;
 coherence
  proof let X such that
Z:  X is FinSequence-membered;
   let x be set;
   assume x in X;
    then x is FinSequence by Z,Def19;
   hence x is Function;
  end;
end;

theorem
 for X being FinSequence-membered set ex Y being non empty set st X c= Y*
proof
 let X be FinSequence-membered set;
  set Z = {rng f where f is Element of X: f in X};
 take Y = succ union Z;
 let x;
 assume
Z: x in X;
  then reconsider x as FinSequence by Def19;
A: union Z c= Y by ORDINAL3:1;
  rng x in {rng f where f is Element of X: f in X} by Z;
  then rng x c= union Z by SETFAM_1:56;
  then rng x c= Y by A,XBOOLE_1:1;
  then x is FinSequence of Y by Def4;
 hence thesis by Def11;
end;

registration
  let X be FinSequence-membered set;
  cluster -> FinSequence-like Element of X;
  coherence
  proof
    let e be Element of X;
    X is empty or X is non empty;
    then e is empty or e in X by SUBSET_1:def 2;
    hence thesis by Def19;
  end;
end;

registration let X be FinSequence-membered set;
 cluster -> FinSequence-membered Subset of X;
 coherence
  proof let Y be Subset of X;
   let x;
   assume x in Y;
    then x in X;
   hence x is FinSequence;
  end;
end;
 
:: from TREES_1, 2009.09.09, A.T.

theorem
  for p,q being FinSequence st q = p|Seg n holds len q <= n
proof
  let p,q be FinSequence;
  assume q = p|Seg n;
then A2: dom q = dom p /\ Seg n by RELAT_1:90;
 Seg len q = dom q by Def3;
then  Seg len q c= Seg n by A2,XBOOLE_1:17;
  hence thesis by Th7;
end;

theorem
  for p,q being FinSequence st p = p^q or p = q^p holds q = {}
proof
  let p,q be FinSequence such that
A1: p = p^q or p = q^p;
   len(p^q) = len p + len q & len(q^p) = len q + len p by Th35;
  hence thesis by A1;
end;

theorem
  for p,q being FinSequence st p^q = <*x*> holds
  p = <*x*> & q = {} or p = {} & q = <*x*>
proof
  let p,q be FinSequence;
  assume
A1: p^q = <*x*>;
then A2: 1 = len(p^q) by Th57
    .= len p + len q by Th35;
A3: now
    assume len p = 0;
    thus
then  p = {};
    hence q = <*x*> by A1,Th47;
  end;
 now
    assume len p <> 0;
then A8: 0+1 <= len p by NAT_1:13;
 len p <= 1 by A2,NAT_1:11;
then  len p = 1 by A8,XXREAL_0:1;
    thus
then  q = {} by A2;
    hence p = <*x*> by A1,Th47;
  end;
  hence thesis by A3;
end;
 
:: missing, 2009.09.26, A.T.

theorem
 for f being n-long FinSequence holds dom f = Seg n
  proof let f be n-long FinSequence;
    len f = n by Def18;
   hence dom f = Seg n by Def3;
  end;

registration let n,m;
 let f be n-long FinSequence, g be m-long FinSequence;
 cluster f^g -> (n+m)-long;
 coherence
  proof
   len f = n & len g = m by Def18;
   hence len(f^g) = n+m by Th35;
  end;
end;
 
:: from JORDAN7, 2010.02.26, A.T

registration
  cluster increasing -> one-to-one (real-valued FinSequence);
  coherence
  proof
    let f be real-valued FinSequence;
    assume
A1: f is increasing;
    let x,y be set;
    assume that
A2: x in dom f & y in dom f and
A3: f.x=f.y;
    reconsider nx=x,ny=y as Element of NAT by A2;
    nx<=ny & nx>=ny by A1,A2,A3,VALUED_0:def 13;
    hence thesis by XXREAL_0:1;
  end;
end;
 
:: from AMI_6, 2010.04.07, A.T.

theorem
 for x, y being set st x in dom <*y*> holds x = 1
proof
  let x, y be set;
  assume x in dom <*y*>;
  then x in Seg 1 by Th55;
  hence thesis by Th4,TARSKI:def 1;
end;
