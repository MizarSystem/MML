:: Segments of Natural Numbers and Finite Sequences
::  by Grzegorz Bancerek and Krzysztof Hryniewiecki
::
:: Received April 1, 1989
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies ORDINAL2, ARYTM, FUNCT_1, BOOLE, RELAT_1, FINSET_1, TARSKI,
      ORDINAL1, CARD_1, PARTFUN1, ARYTM_1, FINSEQ_1, MCART_1, MONOID_0,
      FUNCOP_1, FRAENKEL, FUNCT_4, NAT_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, XCMPLX_0, RELAT_1, FUNCT_1,
      FRAENKEL, FUNCOP_1, WELLORD2, ORDINAL1, REAL_1, NAT_1, NAT_D, NUMBERS,
      PARTFUN1, FUNCT_4, MCART_1, FINSET_1, CARD_1, XXREAL_0;
 constructors WELLORD2, FUNCOP_1, FUNCT_4, FRAENKEL, XXREAL_0, XREAL_0, NAT_1,
      CARD_1, NAT_D;
 registrations FUNCT_1, RELAT_1, RELSET_1, CARD_1, SUBSET_1, NAT_1, XREAL_0,
      ORDINAL2, FINSET_1, FUNCOP_1, XXREAL_0, XBOOLE_0, ORDINAL1;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, FINSET_1, FUNCT_1, WELLORD2, XBOOLE_0, RELAT_1, FRAENKEL;
 theorems TARSKI, AXIOMS, FUNCT_1, REAL_1, NAT_1, ZFMISC_1, RELAT_1, RELSET_1,
      PARTFUN1, WELLORD2, ORDINAL1, CARD_1, FINSET_1, XBOOLE_0, XBOOLE_1,
      MCART_1, GRFUNC_1, FUNCOP_1, XREAL_1, XXREAL_0, FUNCT_4;
 schemes FUNCT_1, REAL_1, NAT_1, XBOOLE_0, FRAENKEL, WELLORD2, RELAT_1;

begin

 reserve k,l,m,n,k1 for Element of NAT,
         a,b,c,i for natural number,
         x,y,z,y1,y2,X,Y for set,
         f,g for Function;

::::::::::::::::::::::::::::::::::::::
::                                  ::
::   Segments of Natural Numbers    ::
::                                  ::
::::::::::::::::::::::::::::::::::::::

 definition let n be natural number;
  func Seg n -> set equals
  { k : 1 <= k & k <= n };
   correctness;
 end;

 definition let n be natural number;
  redefine func Seg n -> Subset of NAT;
   coherence
    proof
     set X = Seg n;
     X c= NAT
       proof let x; assume x in X;
         then x in { k : 1 <= k & k <= n };
         then (ex k st x = k & 1 <= k & k <= n) &
         ex x st x in NAT;
        hence x in NAT;
       end;
     hence thesis;
    end;
 end;

canceled 2;

theorem
 Th3: a in Seg b iff 1 <= a & a <= b
  proof
   A1: a is Element of NAT by ORDINAL1:def 13;
         a in { m : 1 <= m & m <= b } iff
         ex m st a = m & ( 1 <= m & m <= b );
      hence thesis by A1;
  end;

theorem
 Th4: Seg 0 = {} & Seg 1 = { 1 } & Seg 2 = { 1,2 }
  proof
   hereby consider x being Element of Seg 0;
    assume
A1:   Seg 0 <> {};
     then reconsider x as Element of NAT by TARSKI:def 3;
         1 <= x & x <= 0 by A1,Th3;
     hence contradiction;
    end;
      now let x;
     thus x in Seg 1 implies x in { 1 }
       proof assume
A2:       x in Seg 1;
        consider k such that
A3:       x = k & (1 <= k & k <= 1) by A2;
            k = 1 by A3,XXREAL_0:1;
        hence thesis by A3,TARSKI:def 1;
       end;
     assume x in { 1 };
then A4:    x = 1 by TARSKI:def 1;
     thus x in Seg 1 by A4;
    end;
   hence Seg 1 = { 1 } by TARSKI:2;
      now let x;
     thus x in Seg 2 implies x in { 1,2 }
       proof assume
A5:       x in Seg 2;
        consider k such that
A6:       x = k & (1 <= k & k <= 2 ) by A5;
            k <= 1 + 1 by A6;
         then k = 1 or k = 2 by A6,NAT_1:9;
        hence x in { 1,2 } by A6,TARSKI:def 2;
       end;
     assume x in { 1,2 };
    then x = 1 or x = 2 by TARSKI:def 2;
      then x in { m : 1 <= m & m <= 2 };
     hence x in Seg 2;
    end;
   hence Seg 2 = { 1,2 } by TARSKI:2;
  end;

theorem
 Th5: a = 0 or a in Seg a
  proof assume a <> 0;
   then consider b be Nat such that
A1:  a = b + 1 by NAT_1:6;
A2: a is Element of NAT by ORDINAL1:def 13;
       1 <= a & a <= a by A1,NAT_1:11;
    then a in { k : 1 <= k & k <= a } by A2;
   hence a in Seg a;
  end;

theorem
  a+1 in Seg(a+1) by Th5;

theorem
 Th7: a <= b iff Seg a c= Seg b
  proof
   thus a <= b implies Seg a c= Seg b
     proof assume
A1:     a <= b;
      let x; assume
A2:     x in Seg a;
      then reconsider c = x as Element of NAT;
A3:     1 <= c & c <= a by A2,Th3;
       then c <= b by A1,XXREAL_0:2;
       then x in { l : 1 <= l & l <= b } by A3;
      hence x in Seg b;
     end;
   assume
A4:  Seg a c= Seg b;
      now assume a <> 0; then a in Seg a by Th5; hence a <= b by A4,Th3;
    end;
   hence thesis;
  end;

theorem
 Th8: Seg a = Seg b implies a = b
  proof assume
       Seg a = Seg b;
    then a <= b & b <= a by Th7;
   hence a = b by XXREAL_0:1;
  end;

theorem Th9:
   c <= a implies Seg c = Seg a /\ Seg c
proof
   assume c <= a;
     then Seg c c= Seg a by Th7;
   hence thesis by XBOOLE_1:28;
end;

theorem
 Seg c = Seg c /\ Seg a implies c <= a
proof
 assume A1: Seg c = Seg c /\ Seg a;
 assume
A2:  not c <= a;
          then Seg a = Seg c by A1,Th9;
 hence contradiction by A2,Th8;
end;

theorem Th11:
    Seg a \/ { a+1 } = Seg (a+1)
proof
 thus Seg a \/ { a+1 } c= Seg (a+1)
    proof
       a+0<=a+1 by XREAL_1:9;
       then A1:   Seg a c= Seg(a+1) by Th7;
       let x;
       assume x in Seg a \/ { a+1 };
       then x in Seg a or x in { a+1 } by XBOOLE_0:def 2;
       then x in Seg (a+1) or x = a+1 by A1,TARSKI:def 1;
       hence thesis by Th5;
    end;
    let x;
    assume A2:x in Seg (a+1);
    then reconsider x as Element of NAT;
       1<=x & x<=a+1 by A2,Th3;
    then 1<=x & (x<=a or x=a+1) by NAT_1:8;
    then x in Seg a or x in {a+1} by TARSKI:def 1;
    hence thesis by XBOOLE_0:def 2;
end;

theorem
  for k being natural number holds Seg k = Seg(k + 1) \ {k + 1}
 proof
   let k be natural number;
   A1: Seg(k + 1) = Seg k \/ {k + 1} by Th11;
      Seg k misses {k + 1}
    proof assume not thesis;
       then A2: Seg k /\ {k + 1} <> {} by XBOOLE_0:def 7;
      consider x being Element of Seg k /\ {k + 1};
       A3: x in Seg k by A2,XBOOLE_0:def 3;
      then reconsider x as Element of NAT;
          x in {k + 1} by A2,XBOOLE_0:def 3;
       then x = k + 1 by TARSKI:def 1;
       then x <= k & k < x by A3,Th3,XREAL_1:31;
     hence thesis;
    end;
  hence thesis by A1,XBOOLE_1:88;
 end;

::  Finite Sequences

 definition let IT be Relation;
  attr IT is FinSequence-like means
:Def2: ex n st dom IT = Seg n;
 end;

 registration
  cluster FinSequence-like Function;
   existence
    proof
     take f = {} --> 0, 0;
     thus thesis by Th4;
    end;
 end;

definition
  mode FinSequence is FinSequence-like Function;
end;

 reserve p,q,r,s,t for FinSequence;

 defpred P[set,set] means ex k st $1 = k & $2 = k+1;

registration let n be natural number;
 cluster Seg n -> finite;
 coherence
  proof
   Seg n is finite
   proof
A1: n = { k : k < n } by AXIOMS:30;
A2: for x,y1,y2 st x in n & P[x,y1] & P[x,y2] holds y1 = y2;
A3:   for x st x in n ex y st P[x,y]
      proof let x;
       assume x in n;
        then ex k st x = k & k < n by A1;
        then reconsider k = x as Element of NAT;
       take k+1;
       thus thesis;
      end;
     consider f such that
A4:   dom f = n and
A5:   for x st x in n holds P[x,f.x] from FUNCT_1:sch 2(A2,A3);
    take f;
    thus rng f = Seg n
     proof
      thus rng f c= Seg n
       proof let x;
        assume x in rng f;
         then consider y being set such that
A6:       y in dom f and
A7:       x = f.y by FUNCT_1:def 5;
         consider k such that
A8:       y = k and
A9:       x = k+1 by A4,A5,A6,A7;
A10:       1 <= k+1 by NAT_1:11;
            ex m st m = y & m < n by A1,A4,A6;
         then k+1 <= n by A8,NAT_1:13;
        hence x in Seg n by A9,A10;
       end;
      let x;
      assume x in Seg n;
       then consider k such that
A11:     x = k and
A12:     1 <= k and
A13:     k <= n;
       consider i being Nat such that
A14:     1+i = k by A12,NAT_1:10;
       reconsider i as Element of NAT by ORDINAL1:def 13;
          i < n by A13,A14,NAT_1:13;
       then A15:     i in n by A1;
       then P[i,f.i] by A5;
       hence x in rng f by A4,A11,A14,A15,FUNCT_1:def 5;
     end;
    thus thesis by ORDINAL1:def 13,A4;
   end;
   hence thesis;
  end;
end;

registration
 cluster FinSequence-like -> finite Function;
 coherence
  proof let f be Function;
   given n such that
A1: dom f = Seg n;
        rng f is finite by A1,FINSET_1:26;
     then A2:    [:dom f, rng f:] is finite by A1,FINSET_1:19;
        f c= [:dom f, rng f:] by RELAT_1:21;
    hence f is finite by A2,FINSET_1:13;
  end;
end;

 Lm1: Seg n,n are_equipotent
  proof
     defpred P[Element of NAT] means Seg $1,$1 are_equipotent;
A1:  P[0] by Th4;
A2:  for n st P[n] holds P[n+1]
     proof let n such that
A3:    Seg n,n are_equipotent;
A4:    Seg(n+1) = Seg n \/ { n+1 } by Th11;
A5:    (n+1) = succ n by CARD_1:52 .= n \/ { n } by ORDINAL1:def 1;
A6:    { n+1 },{ n } are_equipotent by CARD_1:48;
A7:   now assume n meets { n };
        then consider x being set such that
A8:     x in n & x in { n } by XBOOLE_0:3;
        x = n by A8,TARSKI:def 1;
        hence contradiction by A8;
       end;
         now assume Seg n meets { n+1 };
        then consider x being set such that
A9:      x in Seg n & x in { n+1 } by XBOOLE_0:3;
A10:      x = n+1 & n <= n by A9,TARSKI:def 1;
           not n+1 <= n by NAT_1:13;
        hence contradiction by A9,A10,Th3;
       end;
      hence thesis by A3,A4,A5,A6,A7,CARD_1:58;
     end;
      for n holds P[n] from NAT_1:sch 1(A1,A2);
   hence thesis;
  end;

 Lm2: Card Seg n = Card n
  proof Seg n,n are_equipotent by Lm1;
   hence Card Seg n = Card n by CARD_1:21;
  end;

notation let p;
 synonym len p for Card p;
end;

definition let p;
 redefine func len p -> Element of NAT means
:Def3: Seg it = dom p;
 coherence
  proof Card p = card p;
   hence thesis;
  end;
 compatibility
  proof let k;
    consider n such that
A1:   dom p = Seg n by Def2;
       dom p,p are_equipotent
     proof
      deffunc F(set) = [$1,p.$1];
      consider g being Function such that
A2:    dom g = dom p and
A3:    for x st x in dom p holds g.x = F(x) from FUNCT_1:sch 3;
      take g;
      thus g is one-to-one
       proof let x,y;
        assume
A4:        x in dom g & y in dom g;
        assume g.x = g.y;
         then [x,p.x] = g.y by A2,A3,A4 .= [y,p.y] by A2,A3,A4;
        hence x = y by ZFMISC_1:33;
       end;
      thus dom g = dom p by A2;
      thus rng g c= p
       proof let i be set;
        assume i in rng g;
         then consider x such that
A5:       x in dom g and
A6:       g.x = i by FUNCT_1:def 5;
            g.x = [x,p.x] by A2,A3,A5;
        hence i in p by A2,A5,A6,FUNCT_1:8;
       end;
      let i be set;
      assume
A7:      i in p;
       then consider x,y such that
A8:      i = [x,y] by RELAT_1:def 1;
A9:     x in dom p & y = p.x by A7,A8,FUNCT_1:8;
       then g.x = i by A3,A8;
      hence i in rng g by A2,A9,FUNCT_1:def 5;
     end;
   then A10:  Card p = Card dom p by CARD_1:21;
   thus k = Card p implies Seg k = dom p
    proof assume k = Card p;
      then k = Card n by A1,A10,Lm2;
     hence thesis by A1,CARD_1:def 5;
    end;
   assume
A11:  Seg k = dom p;
   thus k = Card k by CARD_1:def 5 .= Card p by A10,A11,Lm2;
  end;
 end;

 definition let p;
  redefine func dom p -> Subset of NAT;
   coherence
  proof dom p = Seg len p by Def3;
   hence thesis;
  end;
 end;

canceled;

theorem Th14:
    {} is FinSequence
 proof
   set f = {} --> 0;
  A1: dom f = {} & for x st x in {} holds f.x = 0;
  thus thesis by A1,Def2,Th4;
 end;

theorem
     (ex k st dom f c= Seg k) implies ex p st f c= p
proof
  given k such that
A1: dom f c= Seg k;
  deffunc F(set) = f.$1;
  consider g such that
A2: dom g = Seg k & for x st x in Seg k holds g.x = F(x) from FUNCT_1:sch 3;
  reconsider g as FinSequence by A2,Def2;
  take g;
  let y,z be set; assume
A3: [y,z] in f;
   then y in dom f by RELAT_1:def 4;
   then y in dom g & f.y = z by A1,A2,A3,FUNCT_1:def 4;
   then [y,g.y] in g & g.y = z by A2,FUNCT_1:8;
  hence thesis;
end;

scheme SeqEx{A()->Element of NAT,P[set,set]}:
 ex p st dom p = Seg A() & for k st k in Seg A() holds P[k,p.k]
 provided
 A1: for k,y1,y2 st k in Seg A() & P[k,y1] & P[k,y2] holds y1=y2
   and
 A2: for k st k in Seg A() ex x st P[k,x]
proof
   A3: for x,y1,y2 st x in Seg A() & P[x,y1] & P[x,y2] holds y1=y2 by A1;
   A4: for x st x in Seg A() ex y st P[x,y] by A2;
    consider f being Function such that A5: dom f = Seg A() &
        (for x st x in Seg A() holds P[x,f.x]) from FUNCT_1:sch 2(A3,A4);
    reconsider p=f as FinSequence by A5,Def2;
    take p;
    thus thesis by A5;
 end;

scheme SeqLambda{A()->Element of NAT,F(set) -> set}:
 ex p being FinSequence st len p = A() & for k st k in Seg A() holds p.k=F(k)
proof
   consider f being Function such that
    A1: dom f = Seg A() & for x st x in Seg A() holds f.x=F(x) from
FUNCT_1:sch 3;
   reconsider p=f as FinSequence by A1,Def2;
   take p;
  thus thesis by A1,Def3;
 end;

theorem
     z in p implies ex k st k in dom p & z=[k,p.k]
 proof
   assume A1: z in p;
   then consider x,y such that A2: z=[x,y] by RELAT_1:def 1;
      x in dom p by A1,A2,FUNCT_1:8;
   then reconsider k = x as Element of NAT;
   take k;
   thus thesis by A1,A2,FUNCT_1:8;
 end;

theorem Th17:
  dom p = dom q & (for k st k in dom p holds p.k = q.k) implies p=q
proof
  assume A1: dom p = dom q & (for k st k in dom p holds p.k = q.k);
  then for x st x in dom p holds p.x=q.x;
  hence thesis by A1,FUNCT_1:9;
end;

theorem
  ( (len p = len q) & for k st 1 <=k & k <= len p holds p.k=q.k )
      implies p=q
proof
   assume
A1: len p = len q & for k st 1<=k & k<=len p holds p.k = q.k;
A2:   dom p = Seg len p & dom q = Seg len q by Def3;
     now let x;
     assume
A3:    x in dom p;
     then reconsider k=x as Element of NAT;
         1 <= k & k <= len p by A2,A3,Th3;
     hence p.x = q.x by A1;
   end;
   hence thesis by A1,A2,FUNCT_1:9;
end;

theorem Th19:
   p|(Seg a) is FinSequence
proof
A1: a is Element of NAT by ORDINAL1:def 13;
 A2: dom(p|Seg a) = dom p /\ Seg a by RELAT_1:90
                .=Seg len p /\ Seg a by Def3;
      len p <= a or a <= len p;
   then dom(p|Seg a) = Seg len p or dom(p|Seg a) = Seg a by A2,Th9;
  hence thesis by A1,Def2;
end;

theorem
  rng p c= dom f implies f*p is FinSequence
proof
  assume rng p c= dom f;
  then dom(f*p) = dom p by RELAT_1:46 .= Seg len p by Def3;
  hence thesis by Def2;
end;

theorem Th21:
  a <= len p & q = p|(Seg a) implies len q = a & dom q = Seg a
proof
A1: a is Element of NAT by ORDINAL1:def 13;
   assume A2: a <= len p & q = p|(Seg a);
   then Seg a c= Seg len p by Th7;
   then Seg a c= dom p by Def3;
   then dom q = Seg a by A2,RELAT_1:91;
  hence thesis by A1,Def3;
end;

:::::::::::::::::::::::::::::::::
::                             ::
::    FinSequences of D        ::
::                             ::
:::::::::::::::::::::::::::::::::

 definition let D be set;
  mode FinSequence of D -> FinSequence means
:Def4: rng it c= D;
   existence
    proof
       rng {} c= D by XBOOLE_1:2;
      hence thesis by Th14;
    end;
 end;

Lm3: for D being set, f being FinSequence of D holds f is PartFunc of NAT,D
 proof let D be set, f be FinSequence of D;
A1: dom f c= NAT;
     rng f c= D by Def4;
  hence thesis by A1,RELSET_1:11;
 end;

registration
 cluster {} -> FinSequence-like;
 coherence by Th14;
end;

registration let D be set;
 cluster FinSequence-like PartFunc of NAT,D;
  existence proof {} is PartFunc of NAT,D by PARTFUN1:56; hence thesis;end;
end;

definition let D be set;
 redefine mode FinSequence of D -> FinSequence-like PartFunc of NAT,D;
  coherence by Lm3;
end;

 reserve D for set;

canceled;

theorem Th23:
  for p being FinSequence of D holds p|(Seg a) is FinSequence of D
proof
  let p be FinSequence of D;
A1: p|(Seg a) is FinSequence by Th19;
      rng(p|(Seg a)) c= rng p & rng p c= D by Def4,RELAT_1:99;
   then rng(p|(Seg a)) c= D by XBOOLE_1:1;
 hence thesis by A1,Def4;
end;

theorem
  for D being non empty set
  ex p being FinSequence of D st len p = a
proof let D be non empty set;
   reconsider a as Element of NAT by ORDINAL1:def 13;
   consider y being Element of D;
   set p = Seg a --> y;
A1: dom p = Seg a by FUNCOP_1:19;
   then reconsider p as FinSequence by Def2;
A2: rng p c= {y} by FUNCOP_1:19;
   {y} c= D by ZFMISC_1:37;
   then rng p c= D by A2,XBOOLE_1:1;
   then reconsider q=p as FinSequence of D by Def4;
   take q;
  thus thesis by A1,Def3;
end;

::::::::::::::::::::::::::::::::::::
::                                ::
::    The Empty FinSequence       ::
::                                ::
::::::::::::::::::::::::::::::::::::

Lm4: q = {} iff len q = 0
 proof
   thus q = {} implies len q = 0
     proof
        assume A1:q = {};
        assume len q <> 0;
        then len q in Seg len q by Th5;
        then len q in dom q by Def3;
        then [len q,q.(len q)] in {} by A1,FUNCT_1:def 4;
       hence contradiction;
     end;
 assume len q = 0;
 then A2: dom q = {} by Def3,Th4;
  consider x being Element of q;
  assume
A3: not q={};
   then consider y,z such that A4: [y,z]=x by RELAT_1:def 1;
  thus contradiction by A2,A3,A4,RELAT_1:def 4;
 end;

registration
  cluster empty FinSequence;
  existence by Th14;
end;

theorem
  len p = 0 iff p = {} by Lm4;

theorem
  p={} iff dom p = {} by RELAT_1:60,64;

theorem
  p={} iff rng p = {} by RELAT_1:60,64;

theorem
  p <> {} iff len p >= 1
proof
    hereby
       assume p <> {};
       then len p <> 0 by Lm4;
       then len p > 0;
       then len p+1 > 0+1 by XREAL_1:10;
       hence len p >=1 by NAT_1:13;
    end;
    assume len p >= 1;
    then len p > 0;
    hence p <> {} by Lm4;
end;

theorem Th29:
  for D be set holds {} is FinSequence of D
proof let D be set;
     rng {} c= D by XBOOLE_1:2;
 hence thesis by Def4;
end;

registration
  let D be set;
  cluster empty FinSequence of D;
existence
 proof
   {} is FinSequence of D by Th29;
   hence thesis;
 end;
end;

definition let x;
  func <*x*> -> set equals
    { [1,x] };
  coherence;
end;

definition let D be set;
  func <*>D -> empty FinSequence of D equals
      {};
coherence by Th29;
end;

canceled 2;

theorem
  p=<*>D iff len p = 0 by Lm4;

 definition let p,q;
  func p^q -> FinSequence means
:Def7: dom it = Seg (len p + len q) &
       (for k st k in dom p holds it.k=p.k) &
       (for k st k in dom q holds it.(len p + k) = q.k);
   existence
    proof
       defpred P[set,set] means
         (for k st k=$1 & 1 <= k & k <= len p holds $2=p.k) &
         (for k st k=$1 & len p + 1 <= k & k <= len p + len q
                   holds $2=q.(k-len p));
A1:    for x,y1,y2 st x in Seg(len p + len q) & P[x,y1] & P[x,y2] holds y1=y2
       proof let x,y1,y2;
         assume A2: x in Seg(len p + len q) & P[x,y1] & P[x,y2];
          then reconsider m=x as Element of NAT;
      A3: now assume A4: len p + 1 <= m;
                  m <= len p + len q by A2,Th3;
               then y1 = q.(m-len p) & y2 = q.(m-len p) by A2,A4;
            hence thesis;
          end;
            now assume not len p + 1 <= m;
                then A5: m <= len p by NAT_1:8;
            A6: 1 <= m by A2,Th3;
             then y1=p.m by A2,A5;
            hence thesis by A2,A5,A6;
          end;
         hence thesis by A3;
       end;
A7:   for x st x in Seg(len p + len q) ex y st P[x,y]
       proof let x;
          assume x in Seg(len p + len q);
           then reconsider m=x as Element of NAT;
      A8: now assume A9: len p + 1 <= m;
               set y = q.(m-len p);
               A10: for k st k=x & 1 <= k & k <= len p holds y=p.k
                     proof let k;
                       assume k=x & 1 <= k & k <= len p;
                         then m + (len p + 1) <= m + len p by A9,XREAL_1:9;
                         then m + len p + 1 <= m + len p + 0;
                       hence thesis by XREAL_1:8;
                     end;
                      for k st k=x & len p + 1 <= k & k <= len p + len q
                       holds y=q.(k-len p);
            hence thesis by A10;
          end;
            now assume A11: not len p + 1 <= m;
                set y=p.m;
            A12: for k st k=x & 1 <= k & k <= len p holds y=p.k;
                   for k st k=x & len p + 1 <= k & k <= len p + len q
                     holds y=q.(k-len p) by A11;
            hence thesis by A12;
          end;
         hence thesis by A8;
       end;
     consider f such that A13: dom f=Seg(len p + len q) &
          for x st x in Seg(len p + len q) holds P[x,f.x] from FUNCT_1:sch 2
(A1,A7);
A14:   for k st k in dom p holds f.k=p.k
     proof let k;
           assume k in dom p;
       then A15: k in Seg len p by Def3;
       then A16: 1 <= k & k <= len p by Th3;
              len p <= len p + len q by NAT_1:11;
          then Seg len p c= Seg(len p + len q) by Th7; hence f.k=p.k by A13,A15
,A16;
     end;

A17:  for n st n in dom q holds f.(len p+n)=q.n
     proof let n;
         assume n in dom q;
          then n in Seg len q by Def3;
           then 1 <= n & n <= len q by Th3;
       then A18: len p + 1 <= len p + n & len p + n <= len p + len q by
XREAL_1:9;
              len p + n <= len p + n + len p by NAT_1:11;
           then len p + 1 <= len p + n + len p by A18,XXREAL_0:2;
           then 1 <= len p + n by XREAL_1:8;
           then (len p + n) in Seg(len p + len q) by A18;
           then f.(len p + n)=q.(n + len p - len p) by A13,A18;
         hence thesis;
       end;
      reconsider r=f as FinSequence by A13,Def2;
      take r;
     thus thesis by A13,A14,A17;
   end;

   uniqueness
    proof let f,g be FinSequence such that
    A19: dom f = Seg(len p + len q) &
        (for k st k in dom p holds f.k=p.k ) &
        (for k st k in dom q holds f.(len p + k)=q.k)
       and
    A20: dom g = Seg(len p + len q) &
        (for k st k in dom p holds g.k=p.k) &
        (for k st k in dom q holds g.(len p + k)=q.k);
          for x st x in Seg(len p + len q) holds f.x=g.x
        proof let x;
          assume A21: x in Seg(len p + len q);
          then reconsider k=x as Element of NAT;
          A22: 1 <= k & k <= len p + len q by A21,Th3;
      A23: now assume len p + 1 <= k;
               then consider m be Nat such that
A24:           len p + 1 + m = k by NAT_1:10;
               reconsider m as Element of NAT by ORDINAL1:def 13;
A25:           1+0<=1+m by XREAL_1:9;
               len p + 1 + m <= len p + len q by A21,A24,Th3;
               then len p + (1 + m) <= len p + len q;
               then 1 + m <= len q by XREAL_1:8;
               then (1+m) in Seg len q by A25;
           then A26: (1+m) in dom q by Def3;
                 then g.(len p +(1+m)) = q.(1+m) by A20;
             hence f.x=g.x by A19,A24,A26;
          end;
            now assume not len p + 1 <= k;
                then k <= len p by NAT_1:8;
                then k in Seg len p by A22;
            then A27: k in dom p by Def3;
             then f.k=p.k by A19;
             hence f.x=g.x by A20,A27;
          end;
         hence thesis by A23;
       end;
      hence f=g by A19,A20,FUNCT_1:9;
    end;
 end;

theorem Th33:
  p = (p ^ q) | (dom p)
 proof A1: dom(p ^ q) = Seg(len p + len q) & len p <= len p + len q &
       dom p = Seg len p by Def3,Def7,NAT_1:12;
   then A2: dom p = dom(p ^ q) /\ Seg len p by Th9;
      for x st x in dom p holds p.x = (p ^ q).x by Def7;
  hence thesis by A1,A2,FUNCT_1:68;
 end;

canceled;

theorem
 Th35: len(p^q) = len p + len q
  proof
   dom(p^q) = Seg(len p + len q) by Def7;
   hence thesis by Def3;
  end;

theorem Th36:
  for k being natural number st
    (len p + 1 <= k & k <= len p + len q) holds (p^q).k=q.(k-len p)
proof
  let k be natural number;
    assume A1:len p + 1 <= k & k <= len p + len q;
    then consider m be Nat such that A2: (len p + 1)+m=k by NAT_1:10;
    reconsider m as Element of NAT by ORDINAL1:def 13;
A3: len p+(1+m) = k by A2;
  1+m = k - len p by A2;
 then A4: 1 <= 1+m by A1,XREAL_1:21;
    k-len p <= len q by A1,XREAL_1:22;
    then 1+m in Seg len q by A2,A4;
    then 1+m in dom q by Def3;
   hence thesis by A3,Def7;
end;

theorem
  for k being natural number st
    len p < k & k <= len(p^q) holds (p^q).k = q.(k - len p)
proof
  let k be natural number;
  assume A1: len p < k & k <= len(p^q);
 then A2:  len p + 1 <= k by NAT_1:13;
  k <= len p + len q by A1,Th35;
  hence thesis by A2,Th36;
end;

theorem Th38:
  for k being Element of NAT st k in dom (p^q) holds
    (k in dom p or (ex n st n in dom q & k=len p + n))
proof
  let k be Element of NAT;
  assume k in dom(p^q);
    then k in Seg len (p^q) by Def3;
    then A1: k in Seg(len p + len q) by Th35;
then A2:  1 <= k & k <= len p + len q by Th3;
A3: now assume not len p+1 <= k;
            then k <= len p by NAT_1:8;
            then k in Seg len p by A2;
        hence thesis by Def3;
    end;
      now assume len p + 1 <= k;
        then consider n be Nat such that A4: k=len p + 1 + n by NAT_1:10;
        reconsider n as Element of NAT by ORDINAL1:def 13;
           len p + (1 + n) <= len p + len q by A1,A4,Th3;
    then A5: 1+n <= len q by XREAL_1:8;
        1 <= 1+n by NAT_1:11;
        then 1+n in Seg len q by A5;
    then A6: 1+n in dom q by Def3;
           k=len p + (1+n) by A4;
      hence thesis by A6;
    end;
  hence thesis by A3;
end;

theorem Th39:
  dom p c= dom(p^q)
proof
     len p <= len p + len q by NAT_1:11;
  then Seg len p c= Seg(len p + len q) by Th7;
  then Seg len p c= dom(p^q) by Def7;
 hence thesis by Def3;
end;

theorem Th40:
  x in dom q implies ex k st k=x & len p + k in dom(p^q)
proof
    assume A1: x in dom q;
    then A2: x in Seg len q by Def3;
    reconsider k=x as Element of NAT by A1;
    take k;
A3:  1 <= k & k <= len q by A2,Th3;
       k <= len p + k by NAT_1:11;
then A4:  1 <= len p + k by A3,XXREAL_0:2;
       len p + k <= len p + len q by A3,XREAL_1:9;
    then len p + k in Seg(len p + len q) by A4;
  hence thesis by Def7;
end;

theorem Th41:
  k in dom q implies len p + k in dom(p^q)
proof
  assume k in dom q;
   then ex n st n=k & len p + n in dom(p^q) by Th40;
 hence thesis;
end;

theorem Th42:
  rng p c= rng(p^q)
proof
    now let x; assume x in rng p;
       then consider y such that A1: y in dom p & x=p.y by FUNCT_1:def 5;
       reconsider k=y as Element of NAT by A1;
A2:    k in dom p & dom p c= dom(p^q) by A1,Th39;
          (p^q).k=p.k by A1,Def7;
     hence x in rng(p^q) by A1,A2,FUNCT_1:def 5;
  end;
 hence thesis by TARSKI:def 3;
end;

theorem Th43:
  rng q c= rng(p^q)
proof
   now let x; assume x in rng q;
      then consider y such that A1: y in dom q & x=q.y by FUNCT_1:def 5;
      reconsider k=y as Element of NAT by A1;
A2:    len p + k in dom(p^q) by A1,Th41;
    (p^q).(len p + k) = q.k by A1,Def7;
    hence x in rng(p^q) by A1,A2,FUNCT_1:def 5;
 end;
  hence thesis by TARSKI:def 3;
end;

theorem
 Th44: rng(p^q) = rng p \/ rng q
 proof
         now let x; assume x in rng(p^q);
             then consider y such that A1: y in dom(p^q) & x=(p^q).y by
FUNCT_1:def 5;
        A2:  y in Seg(len p + len q) by A1,Def7;
             reconsider k=y as Element of NAT by A1;
        A3:  1 <= k & k <= len p + len q by A2,Th3;
     A4:   now assume A5: len p + 1 <= k;
               then A6: q.(k-len p) = x by A1,A3,Th36;
                  consider m be Nat such that
A7:               (len p+1)+m = k by A5,NAT_1:10;
                  reconsider m as Element of NAT by ORDINAL1:def 13;
A8:               len p +(1+m) = k by A7;
A9:               1+0<=1+m by XREAL_1:9;
                  1+m <= len q by A3,A8,XREAL_1:8;
                  then 1+m in Seg len q by A9;
                  then k-len p in dom q by A7,Def3;
               hence x in rng q by A6,FUNCT_1:def 5;
             end;
               now assume not len p + 1 <= k;
                    then k <= len p by NAT_1:8;
                    then k in Seg len p by A3;
                 then A10: k in dom p by Def3;
                    then p.k = x by A1,Def7;
                hence x in rng p by A10,FUNCT_1:def 5;
             end;
          hence x in rng p \/ rng q by A4,XBOOLE_0:def 2;
       end;
     then A11: rng(p^q) c= rng p \/ rng q by TARSKI:def 3;
        rng p c= rng(p^q) & rng q c= rng(p^q) by Th42,Th43;
        then (rng p \/ rng q) c= rng(p^q) by XBOOLE_1:8;
   hence thesis by A11,XBOOLE_0:def 10;
 end;

theorem
 Th45: p^q^r = p^(q^r)
  proof
 A1:     dom ((p^q)^r) = Seg(len (p^q) + len r) by Def7
       .= Seg(len p + len q + len r) by Th35
       .= Seg(len p + (len q + len r))
       .= Seg(len p + len(q^r)) by Th35;
 A2:  for k st k in dom p holds ((p^q)^r).k=p.k
        proof let k;
            assume A3: k in dom p;
                dom p c= dom(p^q) by Th39;
           hence (p^q^r).k=(p^q).k by A3,Def7
              .=p.k by A3,Def7;
        end;
      for k st k in dom(q^r) holds ((p^q)^r).(len p + k)=(q^r).k
        proof let k;
          assume A4: k in dom(q^r);
            A5: now assume A6: k in dom q;
                     then (len p + k) in dom(p^q) by Th41;
                  hence (p^q^r).(len p + k) = (p^q).(len p + k) by Def7
                        .=q.k by A6,Def7
                        .=(q^r).k by A6,Def7;
                end;
                  now assume not k in dom q;
                   then consider n such that
                      A7: n in dom r & k=len q + n by A4,Th38;
                    thus (p^q^r).(len p + k) =(p^q^r).(len p + len q + n) by A7
                      .=(p^q^r).(len(p^q) + n) by Th35
                      .=r.n by A7,Def7
                      .=(q^r).k by A7,Def7;
                end;
            hence (p^q^r).(len p + k)=(q^r).k by A5;
        end;
     hence (p^q)^r=p^(q^r) by A1,A2,Def7;
  end;

theorem
  p^r = q^r or r^p = r^q implies p = q
  proof assume
A1:  p^r = q^r or r^p = r^q;
A2: now assume A3: p^r = q^r;
       then len p + len r = len(q^r) by Th35;
       then len p + len r = len q + len r by Th35;
       then Seg len p = Seg len q;
then A4:    dom p = Seg len q by Def3
         .= dom q by Def3;
          for k st k in dom p holds p.k=q.k
          proof let k; assume A5: k in dom p;
            hence p.k=(q^r).k by A3,Def7
                 .=q.k by A4,A5,Def7;
          end;
       hence thesis by A4,Th17;
    end;
       now assume A6: r^p=r^q;
       then len r + len p = len(r^q) by Th35
       .=len r + len q by Th35;
       then Seg len p = Seg len q;
    then A7: dom p = Seg len q by Def3
        .= dom q by Def3;
          for k st k in dom p holds p.k=q.k
         proof let k;
            assume A8: k in dom p;
          hence p.k = (r^q).(len r + k) by A6,Def7
                 .= q.k by A7,A8,Def7;
         end;
      hence thesis by A7,Th17;
    end;
   hence thesis by A1,A2;
  end;

theorem
 Th47: p^{} = p & {}^p = p
 proof
    A1: dom(p^{}) = Seg len (p^{}) by Def3
       .= Seg (len p + len {}) by Th35
       .= Seg (len p + 0) by Lm4
       .= dom p by Def3;
     thus p^{} = p
       proof
         for k st k in dom p holds p.k=(p^{}).k by Def7;
         hence p^{}=p by A1,Th17;
       end;
   A2: dom({}^p) = Seg len ({}^p) by Def3
      .= Seg (len {} + len p) by Th35
      .= Seg (0 + len p) by Lm4
      .= dom p by Def3;
   thus {}^p=p
      proof
            for k st k in dom p holds p.k = ({}^p).k
           proof let k;
               assume A3: k in dom p;
             thus ({}^p).k = ({}^p).(0 + k)
               .=({}^p).(len {} + k) by Lm4
               .=p.k by A3,Def7;
           end;
         hence {}^p=p by A2,Th17;
      end;
 end;

theorem Th48:
  p^q = {} implies p={} & q={}
proof
 assume p^q={};
    then 0 = len (p^q) by Lm4
      .= len p + len q by Th35;
   then len p = 0 & len q = 0 by NAT_1:7;
  hence thesis by Lm4;
end;

 definition let D be set;let p,q be FinSequence of D;
 redefine func p^q -> FinSequence of D;
   coherence
    proof
A1:    rng(p^q) = rng p \/ rng q by Th44;
         rng p c= D & rng q c= D by Def4;
     hence rng(p^q) c= D by A1,XBOOLE_1:8;
    end;
 end;

Lm5:
  {[x,y]} is Function
 proof
    now let a be set;
   thus a in {[x,y]} implies ex x,y st [x,y] = a
    proof assume a in {[x,y]};
      then a = [x,y] by TARSKI:def 1;
      hence thesis;
    end;
   let z,y1,y2;
   assume [z,y1] in {[x,y]} & [z,y2] in {[x,y]};
   then [z,y1] = [x,y] & [z,y2] = [x,y] by TARSKI:def 1;
   hence y1 = y2 by ZFMISC_1:33;
  end;
  hence thesis by FUNCT_1:2;
 end;

Lm6:
 for x1, y1 being set holds
 [x,y] in {[x1,y1]} implies x = x1 & y = y1
  proof
  let x1, y1 be set;
  assume [x,y] in {[x1,y1]};
   then [x,y] = [x1,y1] by TARSKI:def 1;
   hence x = x1 & y = y1 by ZFMISC_1:33;
  end;

 definition let x;
  redefine func <*x*> -> Function means
:Def8:  dom it = Seg 1 & it.1 = x;
   coherence by Lm5;
   compatibility
   proof
     let f be Function;
     hereby assume f = <*x*>;
then A1:    f = { [1,x] };
       hence dom f = Seg 1 by Th4,RELAT_1:23;
          [1,x] in f by A1,TARSKI:def 1;
       hence f.1 = x by FUNCT_1:8;
     end;
     assume A2: dom f = Seg 1 & f.1 = x;
     reconsider g = { [1,f.1] } as Function by Lm5;
        f = { [1,f.1] }
     proof
         [y,z] in f iff [y,z] in g
      proof
        hereby assume [y,z] in f;
         then y in {1} & z in rng f & rng f = {f.1}
            by A2,Th4,FUNCT_1:14,RELAT_1:def 4,def 5;
         then y = 1 & z = f.1 by TARSKI:def 1;
         hence [y,z] in g by TARSKI:def 1;
        end;
        assume [y,z] in g;
        then y = 1 & z = f.1 & 1 in dom f by A2,Lm6;
        hence [y,z] in f by FUNCT_1:def 4;
      end;
      hence thesis by RELAT_1:def 2;
     end;
    hence thesis by A2;
  end;
 end;

 registration let x;
  cluster <*x*> -> Function-like Relation-like;
  coherence;
 end;

 registration let x;
  cluster <*x*> -> FinSequence-like;
 coherence
    proof
     take n =1;
     thus dom <*x*> = Seg n by Def8;
    end;
 end;

canceled;

theorem Th50:
  p^q is FinSequence of D implies
     p is FinSequence of D & q is FinSequence of D
proof
  assume p^q is FinSequence of D;
  then rng(p^q) c= D by Def4;
 then A1: rng p \/ rng q c= D by Th44;
      rng p c= rng p \/ rng q by XBOOLE_1:7;
   then rng p c= D by A1,XBOOLE_1:1;
  hence p is FinSequence of D by Def4;
      rng q c= rng p \/ rng q by XBOOLE_1:7;
   then rng q c= D by A1,XBOOLE_1:1;
  hence q is FinSequence of D by Def4;
end;

definition let x,y;
  func <*x,y*> -> set equals
    <*x*>^<*y*>;
  correctness;

  let z;
  func <*x,y,z*> -> set equals
    <*x*>^<*y*>^<*z*>;
  correctness;

end;

registration let x,y;
  cluster <*x,y*> -> Function-like Relation-like;
  coherence;
  let z;
  cluster <*x,y,z*> -> Function-like Relation-like;
  coherence;
end;

registration let x,y;
  cluster <*x,y*> -> FinSequence-like;
  coherence;
  let z;
  cluster <*x,y,z*> -> FinSequence-like;
  coherence;
end;

canceled;

theorem
  <*x*> = { [1,x] };

canceled 2;

theorem Th55:
   p=<*x*> iff dom p = Seg 1 & rng p = {x}
proof
      thus p = <*x*> implies dom p = Seg 1 & rng p = {x}
            proof
               assume A1: p = <*x*>;
              hence dom p = Seg 1 by Def8;
                   dom p = {1} by A1,Def8,Th4;
             then rng p = {p.1} by FUNCT_1:14;
              hence thesis by A1,Def8;
            end;
        assume A2: dom p = Seg 1 & rng p = {x};
           then 1 in dom p;
           then p.1 in {x} by A2,FUNCT_1:def 5;
           then p.1 = x by TARSKI:def 1;
       hence p=<*x*> by A2,Def8;
end;

theorem Th56:
  p=<*x*> iff len p = 1 & rng p = {x}
proof
     len p = 1 iff dom p = Seg 1 by Def3;
 hence thesis by Th55;
end;

theorem Th57:
  p = <*x*> iff len p = 1 & p.1 = x
proof
     len p = 1 iff dom p = Seg 1 by Def3;
 hence thesis by Def8;
end;

theorem
  (<*x*>^p).1 = x
  proof
      1 in Seg 1;
   then 1 in dom <*x*> by Def8;
   then (<*x*>^p).1 = <*x*>.1 by Def7;
   hence thesis by Def8;
  end;

theorem Th59:
  (p^<*x*>).(len p + 1)=x
proof
  dom <*x*> = Seg 1 & 1<>0 by Def8;
    then 1 in dom <*x*>;
  hence (p^<*x*>).(len p + 1) = <*x*>.1 by Def7
   .=x by Def8;
end;

theorem Th60:
  <*x,y,z*>=<*x*>^<*y,z*> &
  <*x,y,z*>=<*x,y*>^<*z*> by Th45;

theorem Th61:
  p = <*x,y*> iff len p = 2 & p.1=x & p.2=y
proof
  thus
        p = <*x,y*> implies len p = 2 & p.1=x & p.2=y
      proof
         assume A1: p=<*x,y*>;
          hence
              len p = len <*x*> + len <*y*> by Th35
           .= 1 + len <*y*> by Th56
           .= 1 + 1 by Th56
           .=2;
A2:          1 in {1} by TARSKI:def 1;
          then A3: 1 in dom <*x*> by Def8,Th4;
         thus p.1 = <*x*>.1 by  A1,A3,Def7
              .= x by Def8;
         A4: 1 in dom <*y*> by A2,Def8,Th4;
         thus p.2 = (<*x*>^<*y*>).(1+1) by A1
           .= (<*x*>^<*y*>).(len <*x*> + 1) by Th56
           .= <*y*>.1 by A4,Def7
           .= y by Def8;
       end;
    assume A5: len p = 2 & p.1=x & p.2=y;
      then A6: dom p = Seg(1+1) by Def3
          .= Seg(len <*x*> + 1) by Th56
          .= Seg(len <*x*> + len <*y*>) by Th56;
      A7: for k st k in dom <*x*> holds p.k=<*x*>.k
         proof let k; assume k in dom <*x*>;
            then k in {1} by Def8,Th4;
           then k=1 by TARSKI:def 1;
          hence p.k = <*x*>.k by A5,Def8;
         end;
          for k st k in dom <*y*> holds p.((len <*x*>)+k)=<*y*>.k
         proof let k; assume k in dom <*y*>;
then A8:              k in {1} by Def8,Th4;
            thus p.((len <*x*>) + k) = p.(1+k) by Th56
              .=p.(1+1) by A8,TARSKI:def 1
              .=<*y*>.1 by A5,Def8
              .= <*y*>.k by A8,TARSKI:def 1;
         end;
    hence p= <*x,y*> by A6,A7,Def7;
end;

theorem Th62:
  p = <*x,y,z*> iff len p = 3 & p.1 = x & p.2 = y & p.3 = z
proof
   thus p = <*x,y,z*> implies len p = 3 & p.1 = x & p.2 = y & p.3 = z
      proof
         assume A1: p =<*x,y,z*>;
           hence
               len p =len <*x,y*> + len <*z*> by Th35
            .=2 + len <*z*> by Th61
            .=2+1 by Th56
            .=3;
A2:              1 in {1} by TARSKI:def 1;
              then A3:   1 in dom <*x*> by Def8,Th4;
         thus p.1 = (<*x*>^<*y,z*>).1 by A1,Th60
            .=<*x*>.1 by A3,Def7
            .=x by Def8;
          A4: 2 in Seg 2;
                len <*x,y*> = 2 by Th61;
          then A5: 2 in dom <*x,y*> by A4,Def3;
        thus p.2 =<*x,y*>.2 by  A1,A5,Def7
           .=y by Th61;
         A6: 1 in dom <*z*> by A2,Def8,Th4;
        thus p.3 = (<*x,y*>^<*z*>).(2+1) by A1
          .=(<*x,y*>^<*z*>).(len (<*x,y*>) + 1) by Th61
          .= <*z*>.1 by A6,Def7
          .= z by Def8;
      end;
    assume A7: len p = 3 & p.1 = x & p.2 = y & p.3 = z;
      then A8: dom p = Seg(2+1) by Def3
          .= Seg((len <*x,y*>) + 1) by Th61
          .= Seg((len <*x,y*>) + len <*z*>) by Th56;
     A9: for k st k in dom <*x,y*> holds p.k=<*x,y*>.k
           proof let k such that A10: k in dom <*x,y*>;
                   len <*x,y*> = 2 by Th61;
then A11:             k in Seg 2 by A10,Def3;
           A12:    k=1 implies p.k=<*x,y*>.k by A7,Th61;
                     k=2 implies p.k=<*x,y*>.k by A7,Th61;
              hence thesis by A11,A12,Th4,TARSKI:def 2;
           end;
         for k st k in dom <*z*> holds p.( (len <*x,y*>) + k) = <*z*>.k
         proof let k; assume k in dom <*z*>;
            then k in {1} by Def8,Th4;
       then A13:  k = 1 by TARSKI:def 1;
          hence
               p.( (len <*x,y*>) + k) = p.(2+1) by Th61
            .=<*z*>.k by A7,A13,Def8;
         end;
     hence p=<*x,y,z*> by A8,A9,Def7;
end;

theorem Th63:
   p <> {} implies ex q,x st p=q^<*x*>
proof
 assume p <> {};
 then len p <> 0 by Lm4;
 then consider n be Nat such that A1: len p = n + 1 by NAT_1:6;
 reconsider n as Element of NAT by ORDINAL1:def 13;
 reconsider q=p|Seg n as FinSequence by Th19;
 take q;
 take p.(len p);
A2: dom q = Seg n
      proof
       A3: dom q = dom p /\ Seg n by RELAT_1:90
            .= Seg len p /\ Seg n by Def3;
           n <=len p by A1,NAT_1:11;
         then Seg n c= Seg len p by Th7;
        hence dom q = Seg n by A3,XBOOLE_1:28;
      end;
   thus q^<*p.(len p)*>=p
   proof
     A4: dom(q^<*p.(len p)*>) = Seg len (q^<*p.(len p)*>) by Def3
         .= Seg(len q + len <*p.(len p)*>) by Th35
         .= Seg(len q + 1) by Th56
         .= Seg len p by A1,A2,Def3
         .= dom p by Def3;
         for x st x in dom p holds p.x = (q^<*p.(len p)*>).x
         proof let x; assume A5:  x in dom p;
            then reconsider k = x as Element of NAT;
               k in Seg(n+1) by A1,A5,Def3;
then A6:            k in Seg n \/ {n+1} by Th11;
       A7: now assume A8: k in Seg n;
              hence p.k=q.k by A2,FUNCT_1:70
                  .=(q^<*p.(len p)*>).k by A2,A8,Def7;
            end;
              now assume A9: k in {n+1};
              1 in Seg 1;
              then A10: 1 in dom <*p.(len p)*> by Def8;
               thus (q^<*p.(len p)*>).k =(q^<*p.(len p)*>).(n+1)
                 by A9,TARSKI:def 1
                .=(q^<*p.(len p)*>).(len q + 1) by A2,Def3
                .=<*p.(len p)*>.1 by A10,Def7
                .=p.(n+1) by A1,Def8
                .=p.k by A9,TARSKI:def 1;
            end;
         hence thesis by A6,A7,XBOOLE_0:def 2;
       end;
     hence q^<*p.(len p)*>=p by A4,FUNCT_1:9;
   end;
end;

definition let D be non empty set; let x be Element of D;
 redefine func <*x*> -> FinSequence of D;
   coherence
    proof let y; assume y in rng <*x*>;
      then y in {x} by Th56;
      then y = x by TARSKI:def 1;
     hence y in D;
    end;
 end;

::::::::::::::::::::::::::::::::::::::::::::::
:: scheme of induction for finite sequences ::
::::::::::::::::::::::::::::::::::::::::::::::

scheme IndSeq{P[FinSequence]}:
 for p holds P[p]
  provided
 A1: P[{}] and
 A2: for p,x st P[p] holds P[p^<*x*>]
proof let p;
    defpred R[set] means for p st len p = $1 holds P[p];
    consider X being Subset of REAL such that
      A3:  for x being Real holds x in X iff R[x] from REAL_1:sch 1;
      for k holds k in X
       proof
          defpred S[Element of NAT] means $1 in X;
      A4: S[0]
          proof
            for q st len q = 0 holds P[q] by A1,Lm4;
           hence 0 in X by A3;
          end;
          now let n such that A5:n in X;
              now let q;
              assume A6: len q = n+1;
              then q <> {} by Lm4;
              then consider r,x such that A7: q=r^<*x*> by Th63;
                 len q = len r + len <*x*> by A7,Th35
              .= len r + 1 by Th56;
              then len r = n + 1 - 1 by A6;
              then P[r] by A3,A5;
             hence P[q] by A2,A7;
            end;
           hence n+1 in X by A3;
          end;
    then A8:   for n st S[n] holds S[n+1];
         thus for n holds S[n] from NAT_1:sch 1(A4,A8);
       end;
      then len p in X;
      hence P[p] by A3;
 end;

theorem
  for p,q,r,s being FinSequence st p^q = r^s & len p <= len r
  ex t being FinSequence st p^t = r
 proof
     defpred S[FinSequence] means
     for p,q,s st p^q=$1^s & len p <= len $1 holds ex t st p^t=$1;
    A1: S[{}]
         proof
            let p,q,s; assume p^q={}^s & len p <= len {};
       then len p <= 0 by Lm4;
then A2:            len p = 0;
            take {};
            thus p^{} = p by Th47
              .= {} by A2,Lm4;
         end;
  A3:  for r,x st S[r] holds S[r^<*x*>]
        proof let r,x;
           assume A4: for p,q,s st p^q=r^s & len p <= len r
              ex t st p^t=r;
           let p,q,s;
           assume A5: p^q=(r^<*x*>)^s & len p <= len (r^<*x*>);
             A6: now assume len p = len(r^<*x*>);
                    then A7: dom p = Seg len(r^<*x*>) by Def3
                        .= dom(r^<*x*>) by Def3;
A8:                     for k st k in dom p holds p.k=(r^<*x*>).k
                        proof let k; assume A9: k in dom p;
                          hence
                               p.k = (r^<*x*>^s).k by A5,Def7
                            .=(r^<*x*>).k by A7,A9,Def7;
                        end;
                         p^{} = p by Th47
                         .=r^<*x*> by A7,A8,Th17;
                      hence thesis;
                end;
                  now assume len p <> len(r^<*x*>);
                    then len p <> len r + len <*x*> by Th35;
                    then A10: len p <> len r + 1 by Th56;
                    len p <= len r + len <*x*> by A5,Th35;
                    then len p <= len r + 1 by Th56;
                    then A11: len p <= len r by A10,NAT_1:8;
                    p^q=r^(<*x*>^s) by A5,Th45;
                    then consider t being FinSequence such that
               A12: p^t = r by A4,A11;
                    p^(t^<*x*>) = r^<*x*> by A12,Th45;
                   hence thesis;
                end;
              hence thesis by A6;
        end;
      for r holds S[r] from IndSeq(A1,A3);
   hence thesis;
 end;

 definition let D be set;
  func D* -> set means
:Def11: x in it iff x is FinSequence of D;
   existence
    proof
     defpred P[set] means $1 is FinSequence of D;
     consider X such that
A1:    x in X iff x in bool [:NAT,D:] & P[x] from XBOOLE_0:sch 1;
     take X;
     let x;
     thus x in X implies x is FinSequence of D by A1;
     assume x is FinSequence of D;
     then reconsider p = x as FinSequence of D;
      p c= [:NAT,D:];
     hence x in X by A1;
    end;
   uniqueness
    proof let D1,D2 be set such that
A2:   x in D1 iff x is FinSequence of D and
A3:   x in D2 iff x is FinSequence of D;
        now let x;
       thus x in D1 implies x in D2
         proof assume x in D1;
           then x is FinSequence of D by A2;
          hence x in D2 by A3;
         end;
       assume x in D2;
        then x is FinSequence of D by A3;
       hence x in D1 by A2;
      end;
     hence thesis by TARSKI:2;
    end;
 end;

 registration let D be set;
  cluster D* -> non empty;
  coherence
  proof
    consider f being FinSequence of D;
       f in D* by Def11;
    hence thesis;
  end;
 end;

theorem
  rng p = rng q & p is one-to-one & q is one-to-one implies
         len p = len q
 proof
   defpred P[FinSequence] means $1 is one-to-one implies
        for q st rng $1 = rng q & q is one-to-one holds len $1 = len q;
  A1: P[{}] by RELAT_1:64;
 A2: for p,x st P[p] holds P[p^<*x*>]
  proof
    now let p,x;
     assume A3: p is one-to-one implies
                for q st rng p = rng q & q is one-to-one holds len p = len q;
     assume A4: p ^ <* x *> is one-to-one;
    let q;
     assume that A5: rng(p ^ <* x *>) = rng q and A6: q is one-to-one;
       A7: rng(p ^ <* x *>) = rng p \/ rng<* x *> by Th44
                           .= rng p \/ {x} by Th55;
          x in {x} by TARSKI:def 1;
       then x in rng q by A5,A7,XBOOLE_0:def 2;
      then consider y such that A8: y in dom q and A9: x = q.y by FUNCT_1:def 5
;
       A10: y in Seg(len q) by A8,Def3;
      reconsider y as Element of NAT by A8;
       A11: 1 <= y by A10,Th3;
      then consider k be Nat such that A12: 1 + k = y by NAT_1:10;
       A13: y <= len q by A10,Th3;
      then consider n be Nat such that A14: y + n = len q by NAT_1:10;
      reconsider n as Element of NAT by ORDINAL1:def 13;
      reconsider q1 = q | (Seg k) as FinSequence by Th19;
      defpred P[Element of NAT,set] means $2 = q.(y + $1);
       A15: for j being Element of NAT,y1,y2 st
        j in Seg n & P[j,y1] & P[j,y2] holds y1 = y2;
       A16: for j being Element of NAT st j in Seg n ex x st P[j,x];
      consider q2 being FinSequence such that A17: dom q2 = Seg n and
       A18: for j being Element of NAT st j in Seg n holds
       P[j,q2.j] from SeqEx(A15,A16);
       k <= y by A12,NAT_1:12;
       then A19: k <= len q by A13,XXREAL_0:2;
       then A20: len q1 = k by Th21;
       A21: rng(q1 ^ q2) = rng q \ {x}
        proof
         thus rng(q1 ^ q2) c= rng q \ {x}
          proof let z be set;
            assume z in rng(q1 ^ q2);
              then A22: z in rng q1 \/ rng q2 by Th44;
              A23: now assume A24: z in rng q1;
                 A25: rng q1 = q .: (Seg k) & q .: (Seg k) c= rng q
                                                    by RELAT_1:144,148;
                consider y1 such that A26: y1 in dom q1 and A27: q1.y1 = z
                                              by A24,FUNCT_1:def 5;
                 A28: q1.y1 = q.y1 by A26,FUNCT_1:70;
                 A29: y1 in Seg(len q1) by A26,Def3;
                reconsider y1 as Element of NAT by A26;
A30:             y1 <= k & k < y by A12,A20,A29,Th3,XREAL_1:31;
                 Seg k c= Seg(len q) by A19,Th7;
                 then dom q1 c= Seg(len q) by A19,Th21;
                 then dom q1 c= dom q by Def3;
                 then x <> z by A6,A8,A9,A26,A27,A28,A30,FUNCT_1:def 8;
                 then not z in {x} by TARSKI:def 1;
               hence thesis by A24,A25,XBOOLE_0:def 4;
              end;
                 now assume z in rng q2;
                then consider y1 such that A31: y1 in dom q2 and A32: q2.y1 = z
                                                        by FUNCT_1:def 5;
                reconsider y1 as Element of NAT by A31;
                 A33: q2.y1 = q.(y + y1) by A17,A18,A31;
                 A34: 1 <= y + y1 by A11,NAT_1:12;
                    y1 <= n by A17,A31,Th3;
                 then y + y1 <= len q by A14,XREAL_1:9;
                 then y + y1 in Seg(len q) by A34;
                 then A35: y + y1 in dom q by Def3;
                 then A36: z in rng q by A32,A33,FUNCT_1:def 5;
                 y1 <> 0 by A17,A31,Th3;
                 then y <> y + y1;
                 then x <> z by A6,A8,A9,A32,A33,A35,FUNCT_1:def 8;
                 then not z in {x} by TARSKI:def 1;
               hence thesis by A36,XBOOLE_0:def 4;
              end;
           hence thesis by A22,A23,XBOOLE_0:def 2;
          end;
         let z be set;
          assume A37: z in rng q \ {x};
            then z in rng q;
           then consider y1 such that A38: y1 in dom q and A39: z = q.y1
                                                        by FUNCT_1:def 5;
            A40: y1 in Seg(len q) by A38,Def3;
           reconsider y1 as Element of NAT by A38;
               not z in {x} by A37,XBOOLE_0:def 4;
            then A41: y <> y1 by A9,A39,TARSKI:def 1;
            A42: now assume A43: y < y1;
              then consider j being Nat such that
              A44: y1 = y + j by NAT_1:10;
               reconsider j as Element of NAT by ORDINAL1:def 13;
               A45: 1 <= j by A43,A44,NAT_1:19;
               y + j <= len q by A40,A44,Th3;
               then j <= n by A14,XREAL_1:8;
               then A46: j in Seg n by A45;
               then z = q2.j by A18,A39,A44;
             hence z in rng q2 by A17,A46,FUNCT_1:def 5;
            end;
               now assume y1 < y;
              then 1 <= y1 & y1 <= k by A12,A40,Th3,NAT_1:13;
              then y1 in Seg k;
              then A47: y1 in dom q1 by A19,Th21;
              then q1.y1 = z by A39,FUNCT_1:70;
             hence z in rng q1 by A47,FUNCT_1:def 5;
            end;
           then z in rng q1 \/ rng q2 by A41,A42,REAL_1:def 5,XBOOLE_0:def 2;
         hence thesis by Th44;
        end;
       A48: p = (p ^ <* x *>) | (dom p) by Th33;
A49:     rng p = rng(p ^ <* x *>) \ {x}
        proof
         thus rng p c= rng(p ^ <* x *>) \ {x}
          proof let z be set;
            assume A50: z in rng p;
A51:             rng p c= rng(p ^ <* x *>) by A48,RELAT_1:99;
            consider y1 such that A52: y1 in dom p and A53: p.y1 = z
                                                   by A50,FUNCT_1:def 5;
             A54: y1 in Seg(len p) by A52,Def3;
            reconsider y1 as Element of NAT by A52;
             A55: (p ^ <* x *>).(len p + 1) = x & (p ^ <* x *>).y1 = p.y1
                     by A48,A52,Th59,FUNCT_1:70;
A56:             y1 <= len p & len p < len p +1 by A54,Th3,XREAL_1:31;
             then 1 <= y1 & y1 <= len p + 1 by A54,Th3,XXREAL_0:2;
             then y1 in Seg(len p + 1) & len p + 1 in Seg(len p + 1)
                     by Th5;
             then y1 in Seg(len p + len<* x *>) &
                  len p + 1 in Seg(len p + len<* x *>) by Th57;
             then y1 in dom(p ^ <* x *>) & len p + 1 in dom(p ^ <* x *>)
                                                          by Def7;
             then x <> z by A4,A53,A55,A56,FUNCT_1:def 8;
             then z in rng(p ^ <* x *>) & not z in {x}
              by A50,A51,TARSKI:def 1;
           hence thesis by XBOOLE_0:def 4;
          end;
         let z be set;
          assume z in rng(p ^ <* x *>) \ {x};
           then z in rng(p ^ <* x *>) & not z in {x} by XBOOLE_0:def 4;
           then z in rng p \/ rng<* x *> & z <> x by Th44,TARSKI:def 1;
           then (z in rng p or z in rng<* x *>) & x <> z by XBOOLE_0:def 2;
           then (z in rng p or z in {x}) & x <> z by Th55;
         hence thesis by TARSKI:def 1;
        end;
A57:       q1 ^ q2 is one-to-one
        proof let y1,y2;
          assume that A58: y1 in dom(q1 ^ q2) and A59: y2 in dom(q1 ^ q2) and
                      A60: (q1 ^ q2).y1 = (q1 ^ q2).y2;
            reconsider m1 = y1, m2 = y2 as Element of NAT by A58,A59;
            A61: q1 is one-to-one by A6,FUNCT_1:84;
            A62: now assume A63: m1 in dom q1 & m2 in dom q1;
              then (q1 ^ q2).m1 = q1.m1 & (q1 ^ q2).m2 = q1.m2
               by Def7;
             hence thesis by A60,A61,A63,FUNCT_1:def 8;
            end;
            A64: now assume A65: m1 in dom q1;
              given j being Element of NAT such that
              A66: j in dom q2 and A67: m2 = len q1 + j;
                  (q1 ^ q2).m1 = q1.m1 & (q1 ^ q2).m2 = q2.j & q1.m1 = q.m1
                                    by A65,A66,A67,Def7,FUNCT_1:70;
               then A68: q.m1 = q.(y + j) by A17,A18,A60,A66;
               A69: dom q1 c= dom q by RELAT_1:89;
                  1 <= j by A17,A66,Th3;
               then A70: 1 <= y + j by NAT_1:12;
                  j <= n by A17,A66,Th3;
               then y + j <= len q by A14,XREAL_1:9;
               then y + j in Seg(len q) by A70;
               then A71: y + j in dom q by Def3;
                  m1 in Seg k by A19,A65,Th21;
               then m1 <= k & k < y by A12,Th3,XREAL_1:31;
               then m1 < y & y <= y + j by XXREAL_0:2,NAT_1:12;
             hence thesis by A6,A65,A68,A69,A71,FUNCT_1:def 8;
            end;
            A72: now assume A73: m2 in dom q1;
              given j be Element of NAT such that
              A74: j in dom q2 and A75: m1 = len q1 + j;
                  (q1 ^ q2).m2 = q1.m2 & (q1 ^ q2).m1 = q2.j & q1.m2 = q.m2
                                     by A73,A74,A75,Def7,FUNCT_1:70;
               then A76: q.m2 = q.(y + j) by A17,A18,A60,A74;
               A77: dom q1 c= dom q by RELAT_1:89;
                  1 <= j by A17,A74,Th3;
               then A78: 1 <= y + j by NAT_1:12;
                  j <= n by A17,A74,Th3;
               then y + j <= len q by A14,XREAL_1:9;
               then y + j in Seg(len q) by A78;
               then A79: y + j in dom q by Def3;
                  m2 in Seg k by A19,A73,Th21;
               then m2 <= k & k < y by A12,Th3,XREAL_1:31;
               then m2 < y & y <= y + j by XXREAL_0:2,NAT_1:12;
             hence thesis by A6,A73,A76,A77,A79,FUNCT_1:def 8;
            end;
               now
              given j1 being Element of NAT such that A80: j1 in dom q2 and
                                           A81: m1 = len q1 + j1;
              given j2 being Element of NAT such that A82: j2 in dom q2 and
                                           A83: m2 = len q1 + j2;
                  (q1 ^ q2).m1 = q2.j1 & (q1 ^ q2).m2 = q2.j2
                                            by A80,A81,A82,A83,Def7;
               then A84: (q1 ^ q2).m1 = q.(y + j1) & (q1 ^ q2).m2 = q.(y + j2)
                                                by A17,A18,A80,A82;
                  1 <= j1 & 1 <= j2 by A17,A80,A82,Th3;
               then A85: 1 <= y + j1 & 1 <= y + j2 by NAT_1:12;
                  j1 <= n & j2 <= n by A17,A80,A82,Th3;
               then y + j1 <= len q & y + j2 <= len q by A14,XREAL_1:9;
               then y + j1 in Seg(len q) & y + j2 in
 Seg(len q) by A85;
               then y + j1 in dom q & y + j2 in dom q by Def3;
               then y + j1 = y + j2 by A6,A60,A84,FUNCT_1:def 8;
             hence thesis by A81,A83;
            end;
         hence thesis by A58,A59,A62,A64,A72,Th38;
        end;
          len(q1 ^ <* x *>) + len q2 = len q1 + len<* x *> + len q2 by Th35
                                 .= k + 1 + len q2 by A20,Th56
                                 .= len q by A12,A14,A17,Def3;
       then A86: dom q = Seg(len(q1 ^ <* x *>) + len q2) by Def3;
       A87: now let j be Element of NAT;
         assume A88: j in dom(q1 ^ <* x *>);
          A89: now assume j in dom q1;
            then (q1 ^ <* x *>).j = q1.j & q.j = q1.j
                                                  by Def7,FUNCT_1:70;
           hence q.j = (q1 ^ <* x *>).j;
          end;
             now given i be Element of NAT such that
             A90: i in dom<* x *> and A91: j = len q1 + i;
               i in {1} by A90,Th4,Th55;
            then i = 1 by TARSKI:def 1;
           hence q.j = (q1 ^ <* x *>).j by A9,A12,A20,A91,Th59;
          end;
        hence q.j = (q1 ^ <* x *>).j by A88,A89,Th38;
       end;
          now let j be Element of NAT;
         assume j in dom q2;
          hence q2.j = q.(len q1 + 1 + j) by A12,A17,A18,A20
                    .= q.(len q1 + len<* x *> + j) by Th56
                    .= q.(len(q1 ^ <* x *>) + j) by Th35;
       end;
       then q1 ^ <* x *> ^ q2 = q by A86,A87,Def7;
       then A92: len q = len(q1 ^ <* x *>) + len q2 by Th35
                     .= len <* x *> + len q1 + len q2 by Th35
                     .= 1 + len q1 + len q2 by Th57
                     .= 1 + (len q1 + len q2)
                     .= len(q1 ^ q2) + 1 by Th35;
          len(p ^ <* x *>) = len p + len<* x *> by Th35
                        .= len p + 1 by Th57;
    hence len(p ^ <* x *>) = len q by A3,A4,A5,A21,A48,A49,A57,A92,FUNCT_1:84;
   end;
   hence thesis;
   end;
   for p holds P[p] from IndSeq(A1,A2);
  hence thesis;
 end;

theorem
  {} in D*
proof
  {} = <*>D;
  hence {} in D* by Def11;
end;

scheme SepSeq{D()->non empty set, P[FinSequence]}:
 ex X st (for x holds x in X iff
            ex p st (p in D()* & P[p] & x=p))
proof
     defpred R[set] means ex p st P[p] & $1=p;
     consider Y such that A1: x in Y iff x in D()* & R[x]
       from XBOOLE_0:sch 1;
     take Y;
     x in Y iff ex p st p in D()* & P[p] & x=p
  proof
      now assume x in Y;
       then x in D()* & ex p st P[p] & x=p by A1;
       hence ex p st p in D()* & P[p] & x=p;
      end;
  hence thesis by A1;
 end;
hence thesis;
end;

:::::::::::::::::::::::::::::::
::                           ::
::        Subsequences       ::
::                           ::
:::::::::::::::::::::::::::::::

definition let IT be Function;
    attr IT is FinSubsequence-like means
  :Def12: ex k st dom IT c= Seg k;
end;

registration
    cluster FinSubsequence-like Function;
 existence
   proof
    take {},len {};
    thus thesis by Def3;
   end;
end;

definition
  mode FinSubsequence is FinSubsequence-like Function;
end;

canceled;

theorem
  for p being FinSequence holds p is FinSubsequence
proof
  let p be FinSequence;
  dom p = Seg len p by Def3;
  hence thesis by Def12;
end;

theorem
  p|X is FinSubsequence & X|p is FinSubsequence
 proof
A1: dom p = Seg len p by Def3;
A2:   dom(p|X) c= dom p by RELAT_1:89;
      dom(X|p) c= dom p by FUNCT_1:89;
  hence thesis by A1,A2,Def12;
 end;

reserve p' for FinSubsequence;

definition let X;
  given k being natural number such that A1: X c= Seg k;
  func Sgm X -> FinSequence of NAT means
:Def13: rng it = X & for l,m,k1,k2 being natural number st
               ( 1 <= l & l < m & m <= len it &
                    k1=it.l & k2=it.m) holds k1 < k2;
existence
proof
    defpred P[natural number] means
      for X st X c= Seg $1 ex p being FinSequence of NAT st
       rng p = X & for l,m,k1,k2 being natural number st
               ( 1 <= l & l < m & m <= len p &
                    k1=p.l & k2=p.m) holds k1 < k2;
A2: P[0]
    proof let X;
       assume A3: X c= Seg 0;
       take <*>(NAT);
       thus rng <*>(NAT) = X by A3,Th4,XBOOLE_1:3;
       thus for l,m,k1,k2 be natural number st
               ( 1 <= l & l < m & m <= len <*>(NAT) &
                    k1=<*>(NAT).l & k2=<*>(NAT).m) holds k1 < k2
          by Lm4;
    end;
A4:  for k being natural number st P[k] holds P[k+1]
    proof let k be natural number such that A5:
       for X st X c= Seg k holds ex p being FinSequence of NAT st
       rng p = X & for l,m,k1,k2 be natural number st
               ( 1 <= l & l < m & m <= len p &
                    k1=p.l & k2=p.m) holds k1 < k2;
       let X;
       assume A6: X c= Seg(k+1);
             now assume not X c= Seg k;
                then consider x such that A7: x in X & not x in
 Seg k by TARSKI:def 3;
                   x in Seg(k+1) by A6,A7;
                then reconsider n=x as Element of NAT;
         A8:   n=k+1
                  proof
                    assume A9: n <> k+1;
                       1 <= n & n <= k+1 by A6,A7,Th3;
                    then 1 <= n & n <= k by A9,NAT_1:8;
                   hence contradiction by A7;
                  end;
                set Y=X\{k+1};
          A10:   Y c= Seg k
                  proof let x;
                    assume x in Y;
                    then A11: x in X & not x in {k+1} by XBOOLE_0:def 4;
              then A12:   x in Seg(k+1) & x<>k+1 by A6,TARSKI:def 1;
                    then reconsider m=x as Element of NAT;
                       1 <= m & m <= k+1 by A6,A11,Th3;
                    then 1 <= m & m <= k by A12,NAT_1:8;
                   hence x in Seg k;
                  end;
                then consider p being FinSequence of NAT such that A13:
                   rng p = Y & for l,m,k1,k2 be natural number  st
                      ( 1 <= l & l < m & m <= len p &
                            k1=p.l & k2=p.m) holds k1 < k2 by A5;
                reconsider k as Element of NAT by ORDINAL1:def 13;
                consider q such that A14: q=p^<*k+1*>;
                reconsider q as FinSequence of NAT by A14;
           A15:  {k+1} c= X by A7,A8,ZFMISC_1:37;
           A16:  rng q = rng p \/ rng <*k+1*> by A14,Th44
                    .= Y \/ {k+1} by A13,Th55
                    .= X \/ {k+1} by XBOOLE_1:39
                    .= X by A15,XBOOLE_1:12;
             for l,m,k1,k2 be natural number st ( 1 <= l & l < m & m <= len q &
                    k1=q.l & k2=q.m) holds k1 < k2
                 proof let l,m,k1,k2 be natural number;
                    assume A17: 1 <= l & l < m & m <= len q & k1=q.l & k2=q.m;
                      A18: l in dom p
                            proof
                                 l < len (p^<*k+1*>) by A14,A17,XXREAL_0:2;
                              then l < len p + len <*k+1*> by Th35;
                              then l < len p + 1 by Th56;
                              then l <= len p by NAT_1:13;
                              then l in Seg len p by A17,Th3;
                             hence l in dom p by Def3;
                            end;
                      A19: 1 <= m by A17,XXREAL_0:2;
                      A20: now assume A21: m=len q;
                                 k1 = p.l by A14,A17,A18,Def7;
                              then k1 in Y by A13,A18,FUNCT_1:def 5;
                         then A22:  k1 <= k by A10,Th3;
                         q.m = (p^<*k+1*>).(len p + len <*k+1*>) by A14,A21,
Th35
                                .=(p^<*k+1*>).(len p + 1) by Th56
                                .= k+1 by Th59;
                            hence k1 < k2 by A17,A22,NAT_1:13;
                          end;
                            now assume m <> len q;
                             then m < len (p^<*k+1*>) by A14,A17,REAL_1:def 5;
                             then m < len p + len <*k+1*> by Th35;
                             then m < len p + 1 by Th56;
                      then A23:    m <= len p by NAT_1:13;
                             then m in Seg len p by A19,Th3;
                             then m in dom p by Def3;
                      then A24:    k2 = p.m by A14,A17,Def7;
                             k1 = p.l by A14,A17,A18,Def7;
                           hence k1 < k2 by A13,A17,A23,A24;
                          end;
                       hence thesis by A20;
                 end;
            hence thesis by A16;
        end;
      hence thesis by A5;
    end;
   for k being natural number holds P[k] from NAT_1:sch 2(A2,A4);
   hence thesis by A1;
end;
uniqueness
proof
  let p,q be FinSequence of NAT such that
A25: rng p = X & for l,m,k1,k2 being natural number st
               ( 1 <= l & l < m & m <= len p &
                    k1=p.l & k2=p.m) holds k1 < k2
        and
A26: rng q = X & for l,m,k1,k2 being natural number st
               ( 1 <= l & l < m & m <= len q &
                    k1=q.l & k2=q.m) holds k1 < k2;

      defpred S[FinSequence] means
      for X st ex k being natural number st X c= Seg k holds
      ($1 is FinSequence of NAT & rng $1 = X &
      for l,m,k1,k2 being natural number st ( 1 <= l & l < m & m <= len $1 &
      k1=$1.l & k2=$1.m) holds k1 < k2)
      implies
      for q being FinSequence of NAT st rng q = X &
      for l,m,k1,k2 being natural number st ( 1 <= l & l < m & m <= len q &
                      k1=q.l & k2=q.m) holds k1 < k2
                holds q=$1;
  A27: S[{}] by RELAT_1:64;
   A28: for p,x st S[p] holds S[p^<*x*>]
       proof let p,x;
         assume A29: S[p];
         let X;
         given k being natural number such that A30: X c= Seg k;
         assume A31: (p^<*x*> is FinSequence of NAT & rng (p^<*x*>) = X &
         for l,m,k1,k2 being natural number st
         ( 1 <= l & l < m & m <= len(p^<*x*>) &
                       k1=(p^<*x*>).l & k2=(p^<*x*>).m) holds k1 < k2);
         let q be FinSequence of NAT;
         assume A32: rng q = X &
         for l,m,k1,k2 being natural number st ( 1 <= l & l < m & m <= len q &
         k1=q.l & k2=q.m) holds k1 < k2;
           1 in Seg 1;
        then A33: 1 in dom <*x*> by Def8;
     A34: ex m st m=x & for l st l in X & l <> x holds l < m
         proof
           <*x*> is FinSequence of NAT by A31,Th50;
           then rng <*x*> c= NAT by Def4;
           then {x} c= NAT by Th55;
           then reconsider m=x as Element of NAT by ZFMISC_1:37;
           take m;
        thus m=x;
        thus for l st l in X & l <> x holds l < m
          proof
           let l;
           assume A35: l in X & l <> x;
           then consider y such that
              A36: y in dom (p^<*x*>) & l=(p^<*x*>).y by A31,FUNCT_1:def 5;
        A37: y in Seg len (p^<*x*>) by A36,Def3;
           reconsider k=y as Element of NAT by A36;
              1 <= k & k <= len (p^<*x*>) by A37,Th3;
           then 1 <= k & k <= len p + len <*x*> by Th35;
        then A38: 1 <= k & k <= len p + 1 by Th56;
                k <> len p + 1
              proof
                assume k = len p + 1;
                then (p^<*x*>).k = <*x*>.1 by A33,Def7
                  .= x by Def8;
                hence contradiction by A35,A36;
              end;
            then 1 <= k & k < len p + 1 by A38,REAL_1:def 5;
            then 1 <= k & k < len p + len <*x*> by Th56;
         then A39: 1 <= k & k < len(p^<*x*>) &
             len(p^<*x*>) <= len(p^<*x*>) by Th35;
                 m=(p^<*x*>).(len p + 1) by Th59
               .= (p^<*x*>).(len p + len <*x*>) by Th56
               .= (p^<*x*>).(len(p^<*x*>)) by Th35;
           hence l < m by A31,A36,A39;
          end;
         end;
        then reconsider m = x as Element of NAT;
         len q <> 0
            proof
               assume len q = 0;
               then X = {} by A32,Lm4,RELAT_1:60;
               then p^<*x*> = {} by A31,RELAT_1:64;
               then 0 = len (p^<*x*>) by Lm4
                 .= len p + len <*x*> by Th35
                 .= 1 + len p by Th56;
              hence contradiction;
            end;
         then consider n be Nat such that A40: len q = n+1 by NAT_1:6;
         reconsider n as Element of NAT by ORDINAL1:def 13;
         deffunc F(Element of NAT) = q.$1;
            ex q' being FinSequence st len q' = n &
            for m st m in Seg n holds q'.m = F(m) from SeqLambda;
         then consider q' being FinSequence such that A41: len q' = n &
            for m st m in Seg n holds q'.m = q.m;
            q' is FinSequence of NAT
           proof
               now let x;
                 assume x in rng q';
                 then consider y such that A42: y in dom q' & x=q'.y by
FUNCT_1:def 5;
             A43: y in Seg len q' by A42,Def3;
                 reconsider y as Element of NAT by A42;
             A44: 1 <= y & y <= n by A41,A43,Th3;
                   n <= n + 1 by NAT_1:11;
                 then 1 <= y & y <= n+1 by A44,XXREAL_0:2;
                 then y in Seg (n+1);
                 then y in dom q by A40,Def3;
             then A45: q.y in rng q by FUNCT_1:def 5;
                    rng q c= NAT by Def4;
              then q.y in NAT by A45;
                hence x in NAT by A41,A42,A43;
              end;
             then rng q' c= NAT by TARSKI:def 3;
            hence thesis by Def4;
           end;
         then reconsider f=q' as FinSequence of NAT;
     A46: q'^<*x*> = q
          proof
            A47: dom q = Seg (n+1) by A40,Def3
                 .= Seg (len q' + len <*x*>) by A41,Th56;
            A48: for m st m in dom q' holds q'.m = q.m
                 proof let m;
                     assume m in dom q';
                     then m in Seg n by A41,Def3;
                    hence thesis by A41;
                 end;
                   for m st m in dom <*x*> holds q.(len q' + m) = <*x*>.m
                  proof let m;
                      assume m in dom <*x*>;
then A49:                       m in {1} by Def8,Th4;
                   then A50: m=1 by TARSKI:def 1;
                         q.(len q' + m) = x
                        proof
                          assume q.(len q' + m) <> x;
                          then A51: q.len q <> x by A40,A41,A49,TARSKI:def 1;
                            consider d being Element of NAT such that
                             A52: d=x & for l st l in
 X & l<>x holds l<d by A34;
                              x in rng q
                             proof
                                  x in {x} by TARSKI:def 1;
                               then x in rng <*x*> by Th55;
                               then x in rng p \/ rng <*x*> by XBOOLE_0:def 2;
                              hence x in rng q by A31,A32,Th44;
                             end;
                           then consider y such that A53:
                             y in dom q & x=q.y by FUNCT_1:def 5;
                       A54: y in Seg len q by A53,Def3;
                           reconsider y as Element of NAT by A53;
                       A55: 1 <= y & y <= len q by A54,Th3;
                             then A56:   1 <= y & y < len q & len q <= len q
by A51,A53,REAL_1:def 5;
                       A57: q.len q is Element of NAT & q.len q in X
                               proof
                                 A58: rng q c= NAT by Def4;
                                      1 <= len q & len q <= len q
                                          by A55,XXREAL_0:2;
                                   then len q in Seg len q;
then A59:                                   len q in dom q by Def3;
                                then q.len q in rng q by FUNCT_1:def 5;
                                 hence q.len q is Element of NAT by A58;
                                 thus q.len q in X by A32,A59,FUNCT_1:def 5;
                               end;
                           then reconsider k = q.len q as Element of NAT;
                           k < d by A51,A52,A57;
                          hence contradiction by A32,A52,A53,A56;
                        end;
                     hence q.(len q' + m) = <*x*>.m by A50,Th57;
                  end;
               hence thesis by A47,A48,Def7;
          end;
         q' = p
           proof
              A60: ex m st X\{x} c= Seg m
                     proof reconsider k as Element of NAT by ORDINAL1:def 13;
                       take k;
                       thus thesis by A30,XBOOLE_1:1;
                     end;

              A61: (p is FinSequence of NAT & rng p = X\{x} &
                    for l,m,k1,k2 being natural number
                    st ( 1 <= l & l < m & m <= len p &
                        k1=p.l & k2=p.m) holds k1 < k2)
                     proof
                       thus p is FinSequence of NAT by A31,Th50;
                       thus rng p = X\{x}
                          proof
                            A62: not x in rng p
                                  proof
                                    assume x in rng p;
                                    then consider y such that A63:
                                       y in dom p & x=p.y by FUNCT_1:def 5;
                                A64: y in Seg len p by A63,Def3;
                                    reconsider y as Element of NAT by A63;
                                A65: 1 <= y & y <= len p by A64,Th3;
                            A66:    1 <= y & y < len p + 1 &
                                       len p + 1 <= len (p^<*x*>)
                                     proof
                                      thus 1 <= y by A64,Th3;
                                      thus
                                           y < len p + 1 by A65,NAT_1:13;
                                          len p + 1 = len p + len <*x*>
                                             by Th56
                                          .= len (p^<*x*>) by Th35;
                                        hence
                                             len p + 1 <= len (p^<*x*>);
                                     end;
                             A67:   m = (p^<*x*>).y by A63,Def7;
                                      m = (p^<*x*>).(len p + 1 ) by Th59;
                                   hence contradiction by A31,A66,A67;
                                  end;
A68:                               X = rng p \/ rng <*x*> by A31,Th44
                                  .= rng p \/ {x} by Th56;

                                   for z holds
                                    z in rng p \/ {x} \ {x} iff z in rng p
                                  proof let z;
                                    thus z in rng p \/ {x} \ {x} implies
                                       z in rng p
                                     proof
                                       assume z in rng p \/ {x} \ {x};
                                       then (z in rng p \/ {x}) & not z in {x}
                                          by XBOOLE_0:def 4;
                                       hence z in rng p by XBOOLE_0:def 2;
                                     end;
                                    assume A69: z in rng p;
                                    then A70: not z in {x} by A62,TARSKI:def 1;
                                       z in rng p \/ {x} by A69,XBOOLE_0:def 2;
                                   hence z in rng p \/ {x} \ {x} by A70,
XBOOLE_0:def 4;
                                  end;
                              hence rng p = X\{x} by A68,TARSKI:2;
                          end;
                       thus
                             for l,m,k1,k2 being natural number
                             st 1 <= l & l < m & m <= len p &
                            k1=p.l & k2=p.m holds k1 < k2
                          proof let l,m,k1,k2 be natural number;
                            assume A71: 1 <= l & l < m & m <= len p &
                                          k1=p.l & k2=p.m;
                            then 1 <= l & l <= len p by XXREAL_0:2;
                            then l in Seg len p by Th3;
                            then l in dom p by Def3;
                          then A72: k1 = (p^<*x*>).l by A71,Def7;
                               1 <= m & m <= len p by A71,XXREAL_0:2;
                            then m in Seg len p by Th3;
                            then m in dom p by Def3;
                          then A73: k2 = (p^<*x*>).m by A71,Def7;
                               len p <= len p + 1 by NAT_1:11;
                            then m <= len p + 1 by A71,XXREAL_0:2;
                            then m <= len p + len <*x*> by Th56;
                            then 1 <= l & l < m & m <= len (p^<*x*>) by A71,
Th35;
                           hence k1 < k2 by A31,A72,A73;
                         end;
                     end;
                   rng f = X\{x} & for l,m,k1,k2 being natural number st
                        ( 1 <= l & l < m & m <= len f &
                          k1=f.l & k2=f.m) holds k1 < k2
                      proof
                        thus rng f = X\{x}
                            proof
                            A74: not x in rng f
                                  proof
                                    assume x in rng f;
                                    then consider y such that A75:
                                       y in dom f & x=f.y by FUNCT_1:def 5;
                                A76: y in Seg len f by A75,Def3;
                                    reconsider y as Element of NAT by A75;
                                A77: 1 <= y & y <= len f by A76,Th3;
A78:                                   1 <= y & y < len f + 1 &
                                       len f + 1 <= len (f^<*x*>)
                                     proof
                                      thus 1 <= y by A76,Th3;
                                      thus y < len f + 1 by A77,NAT_1:13;
                                      len f + 1 = len f + len <*x*> by Th56
                                          .= len (f^<*x*>) by Th35;
                                      hence len f + 1 <= len (f^<*x*>);
                                     end;
                             A79:   m = q.y by A46,A75,Def7;
                                      m = q.(len f + 1) by A46,Th59;
                                   hence contradiction by A32,A46,A78,A79;
                                  end;
A80:                               X = rng f \/ rng <*x*> by A32,A46,Th44
                                  .= rng f \/ {x} by Th56;
                                   for z holds
                                    z in rng f \/ {x} \ {x} iff z in rng f
                                  proof let z;
                                    thus z in rng f \/ {x} \ {x} implies
                                       z in rng f
                                     proof
                                       assume z in rng f \/ {x} \ {x};
                                       then (z in rng f \/ {x}) & not z in {x}
                                          by XBOOLE_0:def 4;
                                       hence z in rng f by XBOOLE_0:def 2;
                                     end;
                                    assume A81: z in rng f;
                                    then A82: not z in {x} by A74,TARSKI:def 1;
                                       z in rng f \/ {x} by A81,XBOOLE_0:def 2;
                                   hence z in rng f \/ {x} \ {x} by A82,
XBOOLE_0:def 4;
                                  end;
                              hence rng f = X\{x} by A80,TARSKI:2;
                            end;
                        thus for l,m,k1,k2 being natural number st
                           ( 1 <= l & l < m & m <= len f &
                             k1=f.l & k2=f.m) holds k1 < k2
                            proof let l,m,k1,k2 be natural number;
                              assume A83: 1 <= l & l < m & m <= len f &
                                   k1=f.l & k2=f.m;
                               then A84:      1 <= l & l < m & m <=
 len q by A40,A41,NAT_1:13;
                                  l in Seg n & m in Seg n
                                  proof
                                   l <= n by A41,A83,XXREAL_0:2;
                                   hence l in Seg n by A83,Th3;
                                   1 <= m by A83,XXREAL_0:2;
                                   hence m in Seg n by A41,A83,Th3;
                                  end;
                               then k1 = q.l & k2 = q.m by A41,A83;
                              hence k1 < k2 by A32,A84;
                            end;
                      end;
              hence q'=p by A29,A60,A61;
           end;
        hence q = p^<*x*> by A46;
       end;
       for p holds S[p] from IndSeq(A27,A28);
 hence p = q by A1,A25,A26;
end;
end;

canceled;

theorem Th71:
  rng Sgm dom p' = dom p'
proof
 ex k st dom p' c= Seg k by Def12;
 hence thesis by Def13;
end;

definition let p';
  func Seq p' -> Function equals
  p'* Sgm(dom p');
 coherence;
end;

registration let p';
 cluster Seq p' -> FinSequence-like;
 coherence
 proof
     rng Sgm dom p' = dom p' by Th71;
     then dom(p'*Sgm(dom p')) = dom Sgm dom p' by RELAT_1:46
     .=Seg len Sgm dom p' by Def3;
     then p'*Sgm(dom p') is FinSequence by Def2;
    hence thesis;
 end;
end;

theorem
  for X st ex k st X c= Seg k holds Sgm X = {} iff X = {}
proof let X;
  given k such that A1: X c= Seg k;
  thus Sgm X = {} implies X = {}
  proof
     assume Sgm X = {};
   hence X = rng {} by A1,Def13
     .= {};
  end;
    assume X={};
     then rng Sgm X = {} by A1,Def13;
     then dom Sgm X = Seg 0 by Th4,RELAT_1:65;
     then len Sgm X = 0 by Def3;
   hence thesis by Lm4;
end;

begin :: from FINSET_1, 1998

theorem :: FINSET_1:def 1
  D is finite iff ex p st D = rng p
proof
 thus D is finite implies ex p st D = rng p
  proof given g being Function such that
A1: rng g = D and
A2: dom g in omega;
    reconsider n = dom g as Element of NAT by A2;
    defpred R[set,set] means P[$2,$1];
A3:   for x,y1,y2 st x in Seg n & R[x,y1] & R[x,y2] holds y1 = y2;
A4:   for x st x in Seg n ex y st R[x,y]
      proof let x;
       assume
A5:     x in Seg n;
        then reconsider x as Element of NAT;
        x >= 1 by A5,Th3;
        then consider k be Nat such that A6:x = 1+k by NAT_1:10;
        reconsider k as Element of NAT by ORDINAL1:def 13;
        x = 1 + k by A6;
       hence thesis;
      end;
     consider f such that
A7:   dom f = Seg n and
A8:   for x st x in Seg n holds R[x,f.x] from FUNCT_1:sch 2(A3,A4);
A9:  rng f = dom g
     proof
A10: n = { k : k < n } by AXIOMS:30;
      thus rng f c= dom g
       proof let x;
        assume x in rng f;
         then consider y such that
A11:       y in dom f and
A12:       x = f.y by FUNCT_1:def 5;
         consider k such that
A13:       x = k and
A14:       y = k+1 by A7,A8,A11,A12;
            k + 1 <= n by A7,A11,A14,Th3;
         then k < n by NAT_1:13;
        hence x in dom g by A10,A13;
       end;
      let x;
      assume x in dom g;
       then consider k such that
A15:     x = k and
A16:     k < n by A10;
          1 <= k+1 & k+1 <= n by A16,NAT_1:11,13;
       then A17:      k+1 in Seg n;
       then consider k1 such that
A18:     f.(k+1) = k1 and
A19:     k+1 = k1+1 by A8;
          k = k1 by A19;
      hence x in rng f by A7,A15,A17,A18,FUNCT_1:def 5;
     end;
    then dom(g*f) = Seg n by A7,RELAT_1:46;
    then reconsider p = g*f as FinSequence by Def2;
   take p;
   thus D = rng p by A1,A9,RELAT_1:47;
  end;
 given p such that
A20: D = rng p;
   consider n such that
A21:  dom p = Seg n by Def2;
A22: n = { k : k < n } by AXIOMS:30;
A23: for x,y1,y2 st x in n & P[x,y1] & P[x,y2] holds y1 = y2;
A24: for x st x in n ex y st P[x,y]
    proof let x;
     assume x in n;
      then ex k st x = k & k < n by A22;
      then reconsider k = x as Element of NAT;
     take k+1;
     thus thesis;
    end;
   consider f such that
A25: dom f = n and
A26: for x st x in n holds P[x,f.x] from FUNCT_1:sch 2(A23,A24);
 take p*f;
A27: rng f = dom p
    proof
     thus rng f c= dom p
      proof let x;
       assume x in rng f;
        then consider y such that
A28:      y in dom f and
A29:      x = f.y by FUNCT_1:def 5;
        consider k such that
A30:      y = k and
A31:      x = k+1 by A25,A26,A28,A29;
           ex m st k = m & m < n by A22,A25,A28,A30;
        then 1 <= k+1 & k+1 <= n by NAT_1:11,13;
       hence x in dom p by A21,A31;
      end;
     let x;
     assume
A32:    x in dom p;
      then reconsider x as Element of NAT;
         1 <= x by A21,A32,Th3;
      then consider m be Nat such that
A33:    x = 1+m by NAT_1:10;
      reconsider m as Element of NAT by ORDINAL1:def 13;
         x <= n by A21,A32,Th3;
      then m < n by A33,NAT_1:13;
      then A34:      m in n by A22;
      then ex k st m = k & f.m = k+1 by A26;
     hence thesis by A25,A33,A34,FUNCT_1:def 5;
    end;
 hence rng(p*f) = D by A20,RELAT_1:47;
     dom(p*f) = dom f by A27,RELAT_1:46;
 hence dom(p*f) in omega by A25;
end;

registration
 cluster finite empty Function;
 existence
  proof
   take {};
   thus thesis;
  end;
end;

registration
 cluster finite non empty Function;
 existence
  proof
     {[{},{}]} is Function by GRFUNC_1:15;
   hence thesis;
  end;
end;

registration let R be finite Relation;
 cluster rng R -> finite;
 coherence
  proof per cases;
   suppose R = {};
    hence thesis by RELAT_1:60;
   end;
   suppose R <> {};
    then reconsider R as finite non empty Relation;
    set X = { x`2 where x is Element of R: x in R };
A1:  rng R = X
     proof
      thus rng R c= X
       proof let z be set;
        assume z in rng R;
         then consider y being set such that
A2:       [y,z] in R by RELAT_1:def 5;
          [y,z]`2 = z by MCART_1:7;
        hence z in X by A2;
       end;
      let e be set;
      assume e in X;
       then consider x being Element of R such that
A3:     e = x`2 and x in R;
       consider y,z being set such that
A4:     x = [y,z] by RELAT_1:def 1;
        z = e by A3,A4,MCART_1:7;
      hence e in rng R by A4,RELAT_1:def 5;
     end;
A5:  R is finite;
     X is finite from FRAENKEL:sch 21(A5);
   hence thesis by A1;
  end;
  end;
end;

begin :: Moved from CARD_1, 1999

theorem
  Seg n,Seg m are_equipotent implies n = m
  proof
   defpred P[Element of NAT] means
    ex n st Seg n,Seg $1 are_equipotent & n <> $1;
   assume Seg n,Seg m are_equipotent & n <> m;
then A1:  ex m st P[m];
   consider m such that
A2:  P[m] and
A3:  for k st P[k] holds m <= k from NAT_1:sch 5(A1);
   consider n such that
A4:  Seg n,Seg m are_equipotent & n <> m by A2;
 A5:   ex f st
  f is one-to-one & dom f = Seg n & rng f = Seg m by A4,WELLORD2:def 4;
A6: now assume m = 0;
      then Seg m = Seg n by A5,Th4,RELAT_1:65;
     hence contradiction by A4,Th8;
    end;
   then consider m1 being Nat such that
A7:  m = m1+1 by NAT_1:6;
A8: now assume n = 0;
      then Seg m = Seg n by A5,Th4,RELAT_1:65;
     hence contradiction by A4,Th8;
    end;
   then consider n1 being Nat such that
A9: n = n1+1 by NAT_1:6;
    reconsider m1,n1 as Element of NAT by ORDINAL1:def 13;
    n in Seg n & m in Seg m by A6,A8,Th5;
then A10:  (Seg n) \ { n },(Seg m) \ { m } are_equipotent by A4,CARD_1:61;
A11: not n1+1 <= n1 & not m1+1 <= m1 by NAT_1:13;
then A12: not n in Seg n1 & not m in Seg m1 by A7,A9,Th3;
A13:  (Seg n1) /\ { n } c= {}
     proof let x; assume x in (Seg n1) /\ { n };
       then x in Seg n1 & x in { n } by XBOOLE_0:def 3;
      hence thesis by A12,TARSKI:def 1;
     end;
     (Seg m1) /\ { m } c= {}
     proof let x; assume x in (Seg m1) /\ { m };
       then x in Seg m1 & x in { m } by XBOOLE_0:def 3;
      hence thesis by A12,TARSKI:def 1;
     end;
then A14:  Seg n = (Seg n1) \/ { n } & Seg m = (Seg m1) \/ { m } &
     (Seg n1) \ { n } = ((Seg n1) \/ { n }) \ { n } &
      (Seg m1) \ { m } = ((Seg m1) \/ { m }) \ { m } &
       (Seg n1) /\ { n } = {} & (Seg m1) /\ { m } = {}
        by A7,A9,A13,Th11,XBOOLE_1:3,40;
  then (Seg n1) misses { n } & (Seg m1) misses { m } by XBOOLE_0:def 7;
  then (Seg n) \ { n } = Seg n1 & (Seg m) \ { m } = Seg m1 by A14,XBOOLE_1:83;
   hence contradiction by A3,A4,A7,A9,A10,A11;
  end;

theorem
  Seg n,n are_equipotent by Lm1;

theorem
  Card Seg n = Card n by Lm2;

theorem
     X is finite implies ex n st X,Seg n are_equipotent
  proof assume X is finite;
   then consider n such that
A1:  X,n are_equipotent by CARD_1:74;
   take n;
      n,Seg n are_equipotent by Lm1;
   hence X,Seg n are_equipotent by A1,WELLORD2:22;
  end;

theorem
  (for n being natural number holds card Seg n = n) &
   for n being Element of NAT holds card n = n & card Card n = n
  proof
    hereby
      let m be natural number;
      reconsider n = m as Element of NAT by ORDINAL1:def 13;
     Seg n is finite & n is finite & Card n is finite & Seg n,n are_equipotent
     by Lm1;
    then Card card n = Card n & card Seg n = card n &
     Card n = n & Card n = n & Card card Card n = Card Card n &
      Card Card n = Card n by CARD_1:81,def 11;
      hence card Seg m = m;
    end;
    let n be Element of NAT;
     Seg n is finite & n is finite & Card n is finite & Seg n,n are_equipotent
     by Lm1;
    then Card card n = Card n & card Seg n = card n &
     Card n = n & Card n = n & Card card Card n = Card Card n &
      Card Card n = Card n by CARD_1:81,def 11;
   hence thesis;
  end;

begin :: Addenda

:: from FINSEQ_5

registration let x be set;
 cluster <*x*> -> non empty;
  coherence;
end;

:: From SPRECT_1

registration
 cluster non empty FinSequence;
 existence proof take <* 0 *>; thus thesis; end;
end;

:: From FUNCT_7, actually from GOBOARD4

registration
 let f1 be FinSequence, f2 be non empty FinSequence;
 cluster f1^f2 -> non empty;
 coherence by Th48;
 cluster f2^f1 -> non empty;
 coherence by Th48;
end;

:: from MATRIX_2

scheme SeqDEx{D()->non empty set,A()->Element of NAT,P[set,set]}:
  ex p being FinSequence of D() st dom p = Seg A() &
                     for k st k in Seg A() holds P[k,p.k]
 provided
A1: for k st k in Seg A() ex x being Element of D() st P[k,x]
proof
 A2: for y be set st y in Seg A() ex x st x in D() & P[y,x]
  proof
   let y;
   assume A3:y in Seg A();
   then reconsider k=y as Element of NAT;
   consider x being Element of D() such that A4:P[k,x] by A1,A3;
   take x;
   thus thesis by A4;
  end;
 consider f being Function such that A5:dom f = Seg A() and A6:rng f c= D() and
   A7:for y be set st y in Seg A() holds P[y,f.y] from WELLORD2:sch 1(A2);
 reconsider f as FinSequence by A5,Def2;
 reconsider p=f as FinSequence of D() by A6,Def4;
 take p;
 thus thesis by A5,A7;
end;

:: from TOPREAL1

reserve D for set, p for FinSequence of D,
        m for natural number;

definition let p be FinSequence; let m;
 func p|m -> FinSequence equals
   p|Seg m;
 coherence by Th19;
end;

definition let D,p,m;
 redefine
 func p|m -> FinSequence of D;
 coherence by Th23;
end;

registration let D be set, f be FinSequence of D;
 cluster f|0 -> empty;
  coherence by Th4,RELAT_1:110;
end;

theorem
    len q <= i implies q|i = q
 proof
  assume len q <= i;
  then Seg len q c= Seg i by Th7;
  then dom q c= Seg i by Def3;
  then q|Seg i = q by RELAT_1:97;
  hence thesis;
 end;

theorem
 i <= len q implies len(q|i) = i
proof
A1:  i is Element of NAT by ORDINAL1:def 13;
 assume i <= len q;
 then Seg i c= Seg(len q) by Th7;
 then Seg i c= dom q by Def3;
 then Seg i = dom(q|i) by RELAT_1:91;
 hence i = len(q|i) by A1,Def3;
end;

:: from FSM_1

theorem
 i in Seg n implies i+m in Seg (n+m)
proof assume i in Seg n;
  then 1 <= i & i <= n & i <= i+m by Th3,NAT_1:11;
  then 1 <= i+m & i+m <= n+m by XXREAL_0:2,XREAL_1:9;
 hence thesis by Th3;
end;

theorem
 i>0 & i+m in Seg (n+m) implies i in Seg n & i in Seg (n+m)
proof assume
A1: i > 0 & i+m in Seg (n+m);   1 = 0+1;
then A2: 1 <= i by A1,NAT_1:13;
A3: i+m <= n+m by A1,Th3; then i <= n by XREAL_1:8;
 hence i in Seg n by A2,Th3;
      i <= i+m by NAT_1:11; then i <= n+m by A3,XXREAL_0:2;
 hence thesis by A2,Th3;
end;

:: from LANG1

definition
 let R be Relation;
 func R[*] -> Relation means
  for x,y being set holds [x,y] in it iff x in field R & y in field R &
   ex p being FinSequence st len p >= 1 & p.1 = x & p.(len p) = y &
    for i being Element of NAT st i >= 1 & i < len p holds [p.i,p.(i+1)] in R;
   existence
    proof
      defpred X[set,set] means
       ex p being FinSequence st len p >= 1 & p.1 = $1 & p.(len p) = $2 &
        for i being Element of NAT st i >= 1 & i < len p
         holds [p.i,p.(i+1)] in R;
     thus ex S being Relation st
      for x,y being set holds
       [x,y] in S iff x in field R & y in field R & X[x,y]
       from RELAT_1:sch 1;
    end;
   uniqueness
    proof let R1,R2 be Relation such that
A1:   for x,y being set holds [x,y] in R1 iff x in field R & y in field R &
      ex p being FinSequence st len p >= 1 & p.1 = x & p.(len p) = y &
      for i being Element of NAT st i >= 1 & i < len p
       holds [p.i,p.(i+1)] in R and
A2:   for x,y being set holds [x,y] in R2 iff x in field R & y in field R &
      ex p being FinSequence st len p >= 1 & p.1 = x & p.(len p) = y &
      for i being Element of NAT st i >= 1 & i < len p
       holds [p.i,p.(i+1)] in R;
     let x,y be set;
     thus [x,y] in R1 implies [x,y] in R2
       proof assume [x,y] in R1;
         then x in field R & y in field R & ex p being FinSequence st
         len p >= 1 & p.1 = x & p.(len p) = y &
         for i being Element of NAT st i >= 1 & i < len p
          holds [p.i,p.(i+1)] in R by A1;
        hence [x,y] in R2 by A2;
       end;
     assume [x,y] in R2;
      then x in field R & y in field R & ex p being FinSequence st
      len p >= 1 & p.1 = x & p.(len p) = y &
      for i being Element of NAT st i >= 1 & i < len p
       holds [p.i,p.(i+1)] in R by A2;
     hence thesis by A1;
    end;
end;

theorem
  for D1,D2 being set st D1 c= D2 holds D1* c= D2*
  proof let D1,D2 be set such that
A1:  D1 c= D2;
   let x be set; assume x in D1*;
   then reconsider p = x as FinSequence of D1 by Def11;
       rng p c= D1 by Def4;
    then rng p c= D2 by A1,XBOOLE_1:1;
    then x is FinSequence of D2 by Def4;
   hence thesis by Def11;
  end;

:: 2005.05.13, A.T.

registration let D be set;
 cluster D* -> functional;
 coherence
  proof let x be set;
   thus thesis by Def11;
  end;
end;

registration
  let f be finite Function;
  cluster dom f -> finite;
  coherence by FINSET_1:29;
end;

registration
  let f be finite Function, x be set;
  cluster f"x -> finite;
  coherence
  proof
    f"x c= dom f by RELAT_1:167;
    hence thesis by FINSET_1:13;
  end;
end;

:: from SCMFSA_7, 2005.11.21, A.T.

theorem
 for p,q being FinSequence st
     p c= q holds len p <= len q
 proof
   let p,q be FinSequence;
   assume p c= q;
    then card p c= card q by CARD_1:27;
   hence thesis by CARD_1:56;
 end;

theorem
 for p,q being FinSequence, i being Element of NAT st
     1 <= i & i <= len p holds (p ^ q).i = p.i
 proof
   let p,q be FinSequence, i be Element of NAT;
   assume A1: 1 <= i & i <= len p;
      Seg len p = dom p by Def3;
   then i in dom p by A1;
   hence thesis by Def7;
 end;

theorem
 for p,q being FinSequence, i being Element of NAT st
     1 <= i & i <= len q holds (p ^ q).(len p + i) = q.i
 proof
   let p,q be FinSequence, i be Element of NAT;
   assume 1 <= i & i <= len q;
   then len p + 1 <= len p + i & len p + i <= len p + len q by XREAL_1:8;
   hence (p ^ q).(len p + i) = q.(len p + i - len p) by Th36
   .= q.i;
 end;

:: from GRAPH_2, 2006.01.09, A.T.

reserve i,j for Element of NAT;

scheme FinSegRng{m, n() -> Element of NAT, F(set)->set, P[Element of NAT]}:
  {F(i): m()<=i & i<=n() & P[i]} is finite
proof
  set F = {F(i): m()<=i & i<=n() & P[i]};
  deffunc G(Element of NAT) = F($1-1);
  consider f being FinSequence such that
A1: len f = n()+1 and
A2: for k st k in Seg (n()+1) holds f.k = G(k) from SeqLambda;
     F c= rng f proof let x be set;assume x in F; then consider j such that
A3: x=F(j) & m()<=j & j<=n() and P[j];
      1<=j+1 & j+1<=n()+1 by A3,XREAL_1:8,NAT_1:11;
then A4: j+1 in Seg (n()+1);
then A5: j+1 in dom f by A1,Def3;
      f.(j+1) = F(j+1-1) by A2,A4 .= F(j);
  hence x in rng f by A3,A5,FUNCT_1:def 5;
  end;
  hence thesis by FINSET_1:13;
end;

:: from SCM_1, 2006.03.13, A.T.

Lm7:  1 in Seg 3 & 2 in Seg 3 & 3 in Seg 3;
Lm8:  1 in Seg 4 & 2 in Seg 4 & 3 in Seg 4 & 4 in Seg 4;
Lm9:  1 in Seg 5 & 2 in Seg 5 & 3 in Seg 5 & 4 in Seg 5 & 5 in Seg 5;
Lm10:  1 in Seg 6 & 2 in Seg 6 & 3 in Seg 6 & 4 in Seg 6 &
       5 in Seg 6 & 6 in Seg 6;
Lm11:  1 in Seg 7 & 2 in Seg 7 & 3 in Seg 7 & 4 in Seg 7 &
       5 in Seg 7 & 6 in Seg 7 & 7 in Seg 7;
Lm12:  1 in Seg 8 & 2 in Seg 8 & 3 in Seg 8 & 4 in Seg 8 &
       5 in Seg 8 & 6 in Seg 8 & 7 in Seg 8 & 8 in Seg 8;

theorem Th87: for x1, x2, x3, x4 being set, p being FinSequence
 st p = <*x1*>^<*x2*>^<*x3*>^<*x4*>
  holds len p = 4 & p.1 = x1 & p.2 = x2 & p.3 = x3 & p.4 = x4
proof
let x1, x2, x3, x4 be set, p be FinSequence; assume
A1: p = <*x1*>^<*x2*>^<*x3*>^<*x4*>;
  set p13 = <*x1*>^<*x2*>^<*x3*>;
      p13 = <*x1, x2, x3*>;
then A2: len p13 = 3 & p13.1 = x1 & p13.2 = x2 & p13.3 = x3 by Th62;
 thus len p = len p13 + len <*x4*> by A1,Th35
            .= 3 + 1 by A2,Th57
            .= 4;
      dom p13 = Seg 3 by A2,Def3;
 hence p.1 = x1 & p.2 = x2 & p.3 = x3 by A1,A2,Lm7,Def7;
 thus p.4 = p.(len p13 + 1) by A2
         .= x4 by A1,Th59;
end;

theorem Th88: for x1, x2, x3, x4, x5 being set, p being FinSequence
 st p = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>
  holds len p = 5 & p.1 = x1 & p.2 = x2 & p.3 = x3 & p.4 = x4 & p.5 = x5
proof
let x1, x2, x3, x4, x5 be set, p be FinSequence; assume
A1: p = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>;
  set p14 = <*x1*>^<*x2*>^<*x3*>^<*x4*>;
A2: len p14 = 4 & p14.1 = x1 & p14.2 = x2 & p14.3 = x3 & p14.4 = x4 by Th87;
 thus len p = len p14 + len <*x5*> by A1,Th35
            .= 4 + 1 by A2,Th57
            .= 5;
      dom p14 = Seg 4 by A2,Def3;
 hence p.1 = x1 & p.2 = x2 & p.3 = x3 & p.4 = x4
                                          by A1,A2,Lm8,Def7;
 thus p.5 = p.(len p14 + 1) by A2
         .= x5 by A1,Th59;
end;

theorem Th89: for x1, x2, x3, x4, x5, x6 being set, p being FinSequence
 st p = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>^<*x6*>
  holds len p = 6 & p.1 = x1 & p.2 = x2 & p.3 = x3 & p.4 = x4 & p.5 = x5 &
                    p.6 = x6
proof
let x1, x2, x3, x4, x5, x6 be set, p be FinSequence; assume
A1: p = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>^<*x6*>;
  set p15 = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>;
A2: len p15 = 5 & p15.1 = x1 & p15.2 = x2 & p15.3 = x3 & p15.4 = x4 &
                   p15.5 = x5 by Th88;
 thus len p = len p15 + len <*x6*> by A1,Th35
            .= 5 + 1 by A2,Th57
            .= 6;
      dom p15 = Seg 5 by A2,Def3;
 hence p.1 = x1 & p.2 = x2 & p.3 = x3 & p.4 = x4 & p.5 = x5
                                          by A1,A2,Lm9,Def7;
 thus p.6 = p.(len p15 + 1) by A2
         .= x6 by A1,Th59;
end;

theorem Th90: for x1, x2, x3, x4, x5, x6, x7 being set, p being FinSequence
 st p = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>^<*x6*>^<*x7*>
  holds len p = 7 & p.1 = x1 & p.2 = x2 & p.3 = x3 & p.4 = x4 & p.5 = x5 &
                    p.6 = x6 & p.7 = x7
proof
let x1, x2, x3, x4, x5, x6, x7 be set, p be FinSequence; assume
A1: p = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>^<*x6*>^<*x7*>;
  set p16 = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>^<*x6*>;
A2: len p16 = 6 & p16.1 = x1 & p16.2 = x2 & p16.3 = x3 & p16.4 = x4 &
                   p16.5 = x5 & p16.6 = x6 by Th89;
 thus len p = len p16 + len <*x7*> by A1,Th35
            .= 6 + 1 by A2,Th57
            .= 7;
      dom p16 = Seg 6 by A2,Def3;
 hence p.1 = x1 & p.2 = x2 & p.3 = x3 & p.4 = x4 & p.5 = x5 & p.6 = x6
                                            by A1,A2,Lm10,Def7;
 thus p.7 = p.(len p16 + 1) by A2
         .= x7 by A1,Th59;
end;


theorem Th91: for x1,x2,x3,x4, x5, x6, x7, x8 being set, p being FinSequence
 st p = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>^<*x6*>^<*x7*>^<*x8*>
  holds len p = 8 & p.1 = x1 & p.2 = x2 & p.3 = x3 & p.4 = x4 & p.5 = x5 &
                    p.6 = x6 & p.7 = x7 & p.8 = x8
proof
let x1, x2, x3, x4, x5, x6, x7, x8 be set, p be FinSequence; assume
A1: p = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>^<*x6*>^<*x7*>^<*x8*>;
  set p17 = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>^<*x6*>^<*x7*>;
A2: len p17 = 7 & p17.1 = x1 & p17.2 = x2 & p17.3 = x3 & p17.4 = x4 &
                   p17.5 = x5 & p17.6 = x6 & p17.7 = x7 by Th90;
 thus len p = len p17 + len <*x8*> by A1,Th35
            .= 7 + 1 by A2,Th57
            .= 8;
      dom p17 = Seg 7 by A2,Def3;
 hence p.1 = x1 & p.2 = x2 & p.3 = x3 & p.4 = x4 & p.5 = x5 & p.6 = x6 &
       p.7 = x7 by A1,A2,Lm11,Def7;
 thus p.8 = p.(len p17 + 1) by A2
         .= x8 by A1,Th59;
end;

theorem for x1,x2,x3,x4,x5,x6,x7, x8, x9 being set, p being FinSequence
 st p = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>^<*x6*>^<*x7*>^<*x8*>^<*x9*>
  holds len p = 9 & p.1 = x1 & p.2 = x2 & p.3 = x3 & p.4 = x4 & p.5 = x5 &
                    p.6 = x6 & p.7 = x7 & p.8 = x8 & p.9 = x9
proof
let x1, x2, x3, x4, x5, x6, x7, x8, x9 be set, p be FinSequence; assume
A1: p = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>^<*x6*>^<*x7*>^<*x8*>^<*x9*>;
  set p17 = <*x1*>^<*x2*>^<*x3*>^<*x4*>^<*x5*>^<*x6*>^<*x7*>^<*x8*>;
A2: len p17 = 8 & p17.1 = x1 & p17.2 = x2 & p17.3 = x3 & p17.4 = x4 &
                   p17.5 = x5 & p17.6 = x6 & p17.7 = x7 & p17.8 = x8 by Th91;
 thus len p = len p17 + len <*x9*> by A1,Th35
            .= 8 + 1 by A2,Th57
            .= 9;
      dom p17 = Seg 8 by A2,Def3;
 hence p.1 = x1 & p.2 = x2 & p.3 = x3 & p.4 = x4 & p.5 = x5 & p.6 = x6 &
       p.7 = x7 & p.8 = x8 by A1,A2,Lm12,Def7;
 thus p.9 = p.(len p17 + 1) by A2
         .= x9 by A1,Th59;
end;

:: from SCMFSA_7, 2006.03.14, A.T.

theorem Th93:
 for p being FinSequence holds
     p | Seg 0 = {}
 proof
   let p be FinSequence;
      (dom p) /\ Seg 0 = {} by Th4;
   then (dom p) misses Seg 0 by XBOOLE_0:def 7;
   hence p | Seg 0 = {} by RELAT_1:95;
 end;

theorem
 for f,g being FinSequence holds
     f | Seg 0 = g | Seg 0
 proof
   let f,g be FinSequence;
   f | Seg 0 = {} by Th93;
   hence thesis by Th93;
 end;

theorem
 for D being non empty set, x being Element of D holds
     <* x *> is FinSequence of D;

theorem
 for D being set, p,q being FinSequence of D holds
     p ^ q is FinSequence of D;

:: from AMISTD_2, 2006.03.26, A.T.

Lm13:
 for R being Relation st dom R <> {} holds R <> {} by RELAT_1:60;

registration
   let f be Function,
       g be non empty Function;
 cluster f +* g -> non empty;
coherence
  proof
    dom (f+*g) = dom f \/ dom g by FUNCT_4:def 1;
    hence thesis by Lm13;
  end;
 cluster g +* f -> non empty;
coherence
  proof
    dom (g+*f) = dom g \/ dom f by FUNCT_4:def 1;
    hence thesis by Lm13;
  end;
end;

registration
   let f, g be finite Function;
 cluster f +* g -> finite;
coherence
  proof
    dom (f+*g) = dom f \/ dom g by FUNCT_4:def 1;
    hence thesis by FINSET_1:29;
  end;
end;

