:: A Theory of Matrices of Complex Elements
::  by Wenpai Chang , Hiroshi Yamazaki and Yatsuka Nakamura
::
:: Received December 10, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies FINSEQ_1, RELAT_1, FUNCT_1, ARYTM_1, VECTSP_1, RLVECT_1,
      MATRIX_1, TREES_1, MATRIX_5, COMPLEX1, COMPLFLD, COMPLSP1, XCMPLX_0,
      ARYTM, GROUP_1;
 notations TARSKI, SUBSET_1, ORDINAL1, XREAL_0, NAT_1, FUNCT_1, STRUCT_0,
      FINSEQ_1, BINOP_1, MATRIX_3, MATRIX_1, NUMBERS, MATRIX_4, COMPLEX1,
      COMPLFLD, XCMPLX_0, VECTSP_1, RLVECT_1, GROUP_1, BINOP_2, BINARITH,
      XXREAL_0;
 constructors NAT_1, TOPMETR, MATRIX_3, FVSUM_1, MATRIX_4, COMPTRIG, BINARITH,
 XXREAL_0;
 registrations STRUCT_0, INT_1, RELSET_1, VECTSP_1, NUMBERS, COMPLFLD,
      COMPLEX1, XCMPLX_0, XREAL_0;
 definitions MATRIX_4;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM;
 theorems FINSEQ_1, FUNCT_1, NAT_1, RLVECT_1, MATRIX_3, VECTSP_1, MATRIX_1,
      FINSEQ_2, MATRIX_4, COMPLFLD, COMPLEX1, GROUP_1, BINOP_2, REAL_1,
      FINSEQ_3, BINARITH, XCMPLX_0, XREAL_1;
 schemes NAT_1;

begin

theorem
  1 = 1.F_Complex by COMPLEX1:def 7,COMPLFLD:10;

theorem
  0.F_Complex = 0 by COMPLEX1:def 6,COMPLFLD:9;

definition let A be Matrix of COMPLEX;
  func COMPLEX2Field A -> Matrix of F_Complex equals
  A;
  coherence by COMPLFLD:def 1;
end;

definition let A be Matrix of F_Complex;
  func Field2COMPLEX A -> Matrix of COMPLEX equals
  A;
  coherence by COMPLFLD:def 1;
end;

theorem
  for A,B being Matrix of COMPLEX st
    COMPLEX2Field A = COMPLEX2Field B holds A=B;

theorem
  for A,B being Matrix of F_Complex st
    Field2COMPLEX A = Field2COMPLEX B holds A=B;

theorem
  for A being Matrix of COMPLEX holds
    A = Field2COMPLEX COMPLEX2Field A;

theorem
  for A being Matrix of F_Complex holds
    A = COMPLEX2Field Field2COMPLEX A;

definition let A,B be Matrix of COMPLEX;
  func A+B -> Matrix of COMPLEX equals
  Field2COMPLEX ((COMPLEX2Field A)+(COMPLEX2Field B));
  coherence;
end;

definition let A be Matrix of COMPLEX;
  func -A -> Matrix of COMPLEX equals
  Field2COMPLEX (-COMPLEX2Field A);
  coherence;
end;

definition let A,B be Matrix of COMPLEX;
  func A - B -> Matrix of COMPLEX equals
  Field2COMPLEX ((COMPLEX2Field A)-(COMPLEX2Field B));
  coherence;
  func A*B -> Matrix of COMPLEX equals
  Field2COMPLEX ((COMPLEX2Field A)*(COMPLEX2Field B));
  coherence;
end;

definition let x be complex number, A be Matrix of COMPLEX;
  func x * A -> Matrix of COMPLEX means
:Def7:  for ea being Element of F_Complex st ea=x holds
  it=Field2COMPLEX (ea*(COMPLEX2Field A));
existence
 proof
   x in COMPLEX by XCMPLX_0:def 2; then
   reconsider aa=x as Element of F_Complex by COMPLFLD:def 1;
   set C=Field2COMPLEX (aa*(COMPLEX2Field A));
   for ea being Element of F_Complex st ea=x holds
    C=Field2COMPLEX (ea*(COMPLEX2Field A));
   hence thesis;
 end;
uniqueness
proof
 let C1,C2 be Matrix of COMPLEX;
 assume
  A1: (for ea being Element of F_Complex st ea=x holds
   C1=Field2COMPLEX (ea*(COMPLEX2Field A)))&
  (for ea being Element of F_Complex st ea=x holds
   C2=Field2COMPLEX (ea*(COMPLEX2Field A)));
   x in COMPLEX by XCMPLX_0:def 2; then
   reconsider aa=x as Element of F_Complex by COMPLFLD:def 1;
  C2=Field2COMPLEX (aa*(COMPLEX2Field A)) by A1;
 hence C1=C2 by A1;
end;
end;

theorem
  for A being Matrix of COMPLEX holds
    len A = len COMPLEX2Field A & width A=width COMPLEX2Field A;

theorem
  for A being Matrix of F_Complex holds
    len A =len Field2COMPLEX A & width A=width Field2COMPLEX A;

theorem
  for M being Matrix of COMPLEX st len M>0 holds --M=M by MATRIX_4:1;

theorem Th10:
  for K being Field, M being Matrix of K holds (1.K)*M=M
proof let K be Field, M be Matrix of K;
A1: len ((1.K)*M)=len M & width ((1.K)*M)=width M by MATRIX_3:def 5;
  for i,j being Nat st [i,j] in Indices M holds ((1.K)*M)*(i,j)=M*(i,j)
   proof let i,j be Nat;
    assume A2: [i,j] in Indices M;
    (1.K)*(M*(i,j))=M*(i,j) by VECTSP_1:def 19;
    hence ((1.K)*M)*(i,j)=M*(i,j) by A2,MATRIX_3:def 5;
   end;
 hence (1.K)*M=M by MATRIX_1:21,A1;
end;

theorem
  for M being Matrix of COMPLEX holds 1*M=M
proof let M be Matrix of COMPLEX;
  set e=1.F_Complex;
  1*M=Field2COMPLEX (e*(COMPLEX2Field M)) by COMPLEX1:def 7, COMPLFLD:10,Def7;
 hence 1*M=M by Th10;
end;

theorem
  for K being Field, a,b being Element of K,
      M being Matrix of K holds a*(b*M)=(a*b)*M
  proof let K be Field, a,b be Element of K,
        M being Matrix of K;
  A1: len (b*M)=len M & width (b*M)=width M by MATRIX_3:def 5;
  A2: len ((a*b)*M)=len M & width ((a*b)*M)=width M
      by MATRIX_3:def 5;
  A3: len (a*(b*M))=len (b*M) by MATRIX_3:def 5;then
  A4: len (a*(b*M)) =len M by MATRIX_3:def 5;
  A5: width (a*(b*M))=width (b*M) by MATRIX_3:def 5;then
  A6: width (a*(b*M)) =width M by MATRIX_3:def 5;
  for i,j being Nat st [i,j] in Indices (a*(b*M))
   holds (a*(b*M))*(i,j)=((a*b)*M)*(i,j)
  proof let i,j be Nat;
    assume A7: [i,j] in Indices (a*(b*M));
   A8: Indices (a*(b*M))=Indices ((b*M)) by MATRIX_4:55,A3,A5;
   A9: Indices ((b*M))=Indices (M) by MATRIX_4:55,A1;
   (a*(b*M))*(i,j)
      = (a*((b*M)*(i,j))) by MATRIX_3:def 5,A7,A8
     .= (a*(b*(M*(i,j)))) by MATRIX_3:def 5,A7,A8,A9
     .= (a*b)*(M*(i,j)) by GROUP_1:def 4;
   hence thesis by MATRIX_3:def 5,A7,A8,A9;
  end;
 hence a*(b*M)=(a*b)*M by MATRIX_1:21,A2,A4,A6;
end;

theorem Th13:
  for K being Field, a,b being Element of K,
      M being Matrix of K holds (a+b)*M=a*M + b*M
  proof let K be Field, a,b be Element of K, M be Matrix of K;
  A1: len ((a+b)*M)=len (M) &
      width ((a+b)*M)=width (M) by MATRIX_3:def 5;
  A2: len (a*M + b*M)=len (a*M) &
      width (a*M + b*M)=width (a*M) by MATRIX_3:def 3;
  A3: len (a*M)=len (M) &
      width (a*M)=width (M) by MATRIX_3:def 5;
  for i,j being Nat st [i,j] in Indices ((a+b)*M)
   holds ((a+b)*M)*(i,j)=(a*M + b*M)*(i,j)
  proof let i,j be Nat;
    assume A4: [i,j] in Indices ((a+b)*M);
   A5: Indices ((a+b)*M)=Indices M by MATRIX_4:55,A1;
   A6: Indices (a*M)=Indices M by MATRIX_4:55,A3;
   (a*M + b*M)*(i,j)
        =(a*M)*(i,j)+(b*M)*(i,j) by MATRIX_3:def 3,A4,A5,A6
       .=a*(M*(i,j))+(b*M)*(i,j) by MATRIX_3:def 5,A4,A5
       .=a*(M*(i,j))+b*(M*(i,j)) by MATRIX_3:def 5,A4,A5
       .=(a+b)*(M*(i,j)) by VECTSP_1:def 18;
   hence thesis by MATRIX_3:def 5,A4,A5;
  end;
  hence (a+b)*M=a*M + b*M by MATRIX_1:21,A1,A2,A3;
end;

theorem
  for M being Matrix of COMPLEX holds M+M = 2*M
proof let M be Matrix of COMPLEX;
  reconsider e2=(1.F_Complex) + (1.F_Complex) as Element of F_Complex;
  A1: (1.F_Complex)*(COMPLEX2Field M)= COMPLEX2Field M by Th10;
  (1.F_Complex) + (1.F_Complex) = 1 + 1 by COMPLFLD:3,10,COMPLEX1:def 7
  .= 2; then
  2 * M = Field2COMPLEX (e2*(COMPLEX2Field M)) by Def7
     .=Field2COMPLEX ((COMPLEX2Field M)+(COMPLEX2Field M)) by A1,Th13;
 hence M+M=2*M;
end;

theorem
  for M being Matrix of COMPLEX holds M+M+M = 3*M
proof let M be Matrix of COMPLEX;
  reconsider e3=(1.F_Complex) + (1.F_Complex)+ (1.F_Complex)
    as Element of F_Complex;
A1: len M =len COMPLEX2Field M & width M=width COMPLEX2Field M;
  A2: (1.F_Complex) + (1.F_Complex)
  =(the add of F_Complex).(1.F_Complex,1.F_Complex) by RLVECT_1:5
  .=addcomplex.(1r,1r) by COMPLFLD:10,def 1
  .=1+1 by BINOP_2:def 3,COMPLEX1:def 7;
  reconsider rr=1+1 as Element of COMPLEX by XCMPLX_0:def 2;
  (1.F_Complex) + (1.F_Complex) + (1.F_Complex)
   = (the add of F_Complex).(((1.F_Complex) + (1.F_Complex)),1.F_Complex)
     by RLVECT_1:5
  .= addcomplex.(1+1,1) by COMPLFLD:def 1,A2,COMPLEX1:def 7,COMPLFLD:10
  .= rr + 1 by BINOP_2:def 3,COMPLEX1:def 7
  .= 3; then
   3*M= Field2COMPLEX (e3*(COMPLEX2Field M)) by Def7
  .=Field2COMPLEX ((1.F_Complex)*(COMPLEX2Field M)
    + ((1.F_Complex)+ (1.F_Complex))*(COMPLEX2Field M)) by Th13
  .=Field2COMPLEX (COMPLEX2Field M
    + ((1.F_Complex)+ (1.F_Complex))*(COMPLEX2Field M)) by Th10
  .=Field2COMPLEX (COMPLEX2Field M
    + ((1.F_Complex)*(COMPLEX2Field M)+ (1.F_Complex)*(COMPLEX2Field M)))
    by Th13
  .=Field2COMPLEX (COMPLEX2Field M
    + (COMPLEX2Field M + (1.F_Complex)*(COMPLEX2Field M))) by Th10
  .=Field2COMPLEX (COMPLEX2Field M+(COMPLEX2Field M+COMPLEX2Field M))
    by Th10
  .=Field2COMPLEX (COMPLEX2Field M+COMPLEX2Field M+COMPLEX2Field M)
    by MATRIX_3:5,A1;
 hence thesis;
end;

definition let n,m be Nat;
  func 0_Cx(n,m) -> Matrix of COMPLEX equals
  Field2COMPLEX (0.(F_Complex,n,m));
  coherence;
end;

theorem
  for n,m being Nat holds
    COMPLEX2Field 0_Cx(n,m)=0.(F_Complex,n,m);

theorem
  for M being Matrix of COMPLEX st
    len M>0 holds M+-M=0_Cx(len M,width M) by MATRIX_4:2;

theorem
  for M being Matrix of COMPLEX st
    len M>0 holds M - M=0_Cx(len M,width M) by MATRIX_4:3;

theorem :: MATRIX_4:4
  for M1,M2,M3 being Matrix of COMPLEX st
  len M1=len M2 & len M2=len M3 &
  width M1=width M2 & width M2=width M3 &
  len M1>0 &
  M1+M3=M2+M3 holds M1=M2 by MATRIX_4:4;

theorem :: MATRIX_4:5
  for M1,M2 being Matrix of COMPLEX st len M2>0 holds
    M1--M2=M1+M2 by MATRIX_4:1;

theorem  :: MATRIX_4:6
  for M1,M2 being Matrix of COMPLEX st
  len M1=len M2 & width M1=width M2 & len M1>0 &
  M1 = M1 + M2 holds M2 = 0_Cx(len M1,width M1)
proof let M1,M2 be Matrix of COMPLEX;
 assume A1: len M1=len M2 & width M1=width M2 & len M1>0&
  M1 = M1 + M2;
  0_Cx(len M1,width M1)=M1+M2+(-M1) by A1,MATRIX_4:2
  .=Field2COMPLEX ((COMPLEX2Field M2)+(COMPLEX2Field M1)
   -(COMPLEX2Field M1)) by MATRIX_3:4,A1
  .=M2 by MATRIX_4:21,A1;
  hence thesis;
end;

theorem :: MATRIX_4:7
  for M1,M2 being Matrix of COMPLEX st
  len M1=len M2 & width M1=width M2 & len M1>0 &
  M1 - M2 = 0_Cx(len M1,width M1) holds M1 = M2 by MATRIX_4:7;

theorem  :: MATRIX_4:8
  for M1,M2 being Matrix of COMPLEX st
  len M1=len M2 & width M1=width M2 &
  len M1 >0 & M1+M2= 0_Cx(len M1,width M1) holds M2=-M1
proof let M1,M2 be Matrix of COMPLEX;
 assume A1: len M1=len M2 & width M1=width M2 & len M1>0&
  M1 + M2 = 0_Cx(len M1,width M1);
  A2: len -M2=len M2 &  width -M2=width M2 by MATRIX_3:def 2;
  COMPLEX2Field(0_Cx(len M1,width M1))
  = (COMPLEX2Field M1)-(-(COMPLEX2Field M2)) by MATRIX_4:1,A1;
  then COMPLEX2Field M1 = -COMPLEX2Field M2 by MATRIX_4:7,A1,A2;
  hence thesis by MATRIX_4:1,A1;
end;

theorem
  for n,m being Nat st n > 0 holds -(0_Cx(n,m))=0_Cx(n,m) by MATRIX_4:9;

theorem :: MATRIX_4:10
  for M1,M2 being Matrix of COMPLEX st
  len M1=len M2 & width M1=width M2 & len M1>0 &
  M2 - M1 = M2 holds M1=0_Cx(len M1,width M1)
proof let M1,M2 be Matrix of COMPLEX;
 assume A1: len M1=len M2 & width M1=width M2 & len M1>0& M2 - M1 = M2;
   COMPLEX2Field M2 +(COMPLEX2Field M1)
     = (COMPLEX2Field M2) by MATRIX_4:22,A1;
  hence thesis by MATRIX_4:6,A1;
end;

theorem :: MATRIX_4:11
  for M1,M2 being Matrix of COMPLEX st
  len M1=len M2 & width M1=width M2 & len M1>0 holds
    M1 = M1 -(M2 - M2) by MATRIX_4:11;

theorem :: MATRIX_4:12
  for M1,M2 being Matrix of COMPLEX st
  len M1=len M2 & width M1=width M2 & len M1>0 holds
    - (M1 + M2) = -M1+-M2 by MATRIX_4:12;

theorem  :: MATRIX_4:13
  for M1,M2 being Matrix of COMPLEX st
    len M1=len M2 & width M1=width M2 & len M1>0 holds
      M1 - (M1 - M2) = M2 by MATRIX_4:13;

theorem :: MATRIX_4:14
  for M1,M2,M3 being Matrix of COMPLEX st
    len M1=len M2 & len M2=len M3 &
      width M1=width M2 & width M2 = width M3 & len M1>0 &
        M1 - M3 = M2 - M3 holds M1 = M2 by MATRIX_4:14;

theorem :: MATRIX_4:15
  for M1,M2,M3 being Matrix of COMPLEX st
  len M1=len M2 & len M2=len M3 &
  width M1=width M2 & width M2 = width M3 & len M1>0 &
  M3 - M1 = M3 - M2 holds M1 = M2 by MATRIX_4:15;

theorem :: MATRIX_4:62
  for M1,M2,M3 being Matrix of COMPLEX st
  len M2=len M3 & width M2=width M3 & width M1=len M2
  & len M1>0 & len M2>0 holds
  M1*(M2+M3)=M1*M2 + M1*M3 by MATRIX_4:62;

theorem :: MATRIX_4:63
  for M1,M2,M3 being Matrix of COMPLEX st
  len M2=len M3 & width M2=width M3 & len M1=width M2
   & len M2>0 & len M1>0 holds
  (M2+M3)*M1=M2*M1 + M3*M1 by MATRIX_4:63;

theorem
  for M1,M2 being Matrix of COMPLEX st len M1= len M2 & width M1=width M2
    holds M1 + M2 = M2 + M1 by MATRIX_3:4;

theorem
  for M1,M2,M3 being Matrix of COMPLEX st len M1=len M2 & len M1=len M3 &
    width M1=width M2 & width M1=width M3 holds
      (M1 + M2) + M3= M1 + (M2 + M3) by MATRIX_3:5;

theorem
  for M being Matrix of COMPLEX st
    len M>0 holds M + 0_Cx(len M,width M) = M
proof let M be Matrix of COMPLEX;
  assume A1:len M > 0;
  A2: len M=len COMPLEX2Field M &
  width M=width COMPLEX2Field M;
  M + 0_Cx(len M,width M) = M +(- 0_Cx(len M,width M)) by MATRIX_4:9,A1
  .= Field2COMPLEX ((COMPLEX2Field M)-(COMPLEX2Field M-COMPLEX2Field M))
              by MATRIX_4:3,A1
  .= M by MATRIX_4:11,A1,A2;
  hence thesis;
end;

theorem Th36:
  for K being Field, b being Element of K,M1,M2 being Matrix of K st
    len M1 = len M2 & width M1 = width M2 & len M1 > 0 holds
      b*(M1+M2)=b*M1 + b*M2
  proof let K be Field, b be Element of K, M1,M2 be Matrix of K;
  assume A1: len M1= len M2 & width M1=width M2 &len M1>0;
  A2: len (b*(M1+M2))=len (M1+M2) & width (b*(M1+M2)) = width (M1+M2)
                   by MATRIX_3:def 5;
  A3: len (M1+M2)=len M1 & width (M1+M2)=width M1 by MATRIX_3:def 3;
  A4: len (b*M1)=len M1 & width (b*M1)=width M1 by MATRIX_3:def 5;
  A5: len (b*M1 + b*M2)=len (b*M1) & width (b*M1 + b*M2)=width (b*M1)
                   by MATRIX_3:def 3;
  for i,j being Nat st [i,j] in Indices (b*(M1+M2))
   holds (b*(M1+M2))*(i,j)=(b*M1 + b*M2)*(i,j)
  proof let i,j be Nat;
    assume A6: [i,j] in Indices (b*(M1+M2));
A7:  Indices (b*(M1+M2))=Indices (M1+M2) by MATRIX_4:55,A2;
A8:  Indices (M1+M2)=Indices M1 by MATRIX_4:55,A3;
A9:  Indices (b*M1)=Indices M1 by MATRIX_4:55,A4;
A10:  Indices M2=Indices M1 by MATRIX_4:55,A1;
A11: (b*M1 + b*M2)*(i,j)
        =(b*M1)*(i,j)+(b*M2)*(i,j) by MATRIX_3:def 3,A6,A7,A8,A9
       .=b*(M1*(i,j))+(b*M2)*(i,j) by MATRIX_3:def 5,A6,A7,A8
       .=b*(M1*(i,j))+b*(M2*(i,j)) by MATRIX_3:def 5,A6,A7,A8,A10
       .=(b*(M1*(i,j)+M2*(i,j))) by VECTSP_1:def 18;
   (b*M1 + b*M2)*(i,j)
       = (b*((M1 + M2)*(i,j))) by A11,MATRIX_3:def 3,A6,A7,A8
      .= (b*(M1 + M2)*(i,j)) by MATRIX_3:def 5,A6,A7;
   hence thesis;
  end;
  hence b*(M1+M2)=b*M1 + b*M2 by MATRIX_1:21,A2,A3,A4,A5;
end;

theorem
  for M1,M2 being Matrix of COMPLEX
  for a being complex number st
    len M1=len M2 & width M1=width M2 & len M1>0 holds
    a*(M1 + M2) = a*M1 + a*M2
proof let M1,M2 be Matrix of COMPLEX; let a be complex number;
 assume A1: len M1=len M2 & width M1=width M2 & len M1>0;
  a in COMPLEX by XCMPLX_0:def 2; then
  reconsider ea=a as Element of F_Complex by COMPLFLD:def 1;
A2: a*(M1 + M2)
   = Field2COMPLEX (ea*(COMPLEX2Field (M1+M2))) by Def7
  .= Field2COMPLEX (ea*(COMPLEX2Field M1)+ea*(COMPLEX2Field M2))
    by Th36,A1;
  a*M1 + a*M2
  = Field2COMPLEX ((COMPLEX2Field
    (Field2COMPLEX (ea*(COMPLEX2Field M1)))+(COMPLEX2Field (a*M2)))) by Def7
  .= Field2COMPLEX (((ea*(COMPLEX2Field M1))+(COMPLEX2Field
    (Field2COMPLEX (ea*(COMPLEX2Field M2)))))) by Def7
  .= Field2COMPLEX ((ea*(COMPLEX2Field M1)+(ea*(COMPLEX2Field M2))));
 hence thesis by A2;
end;

theorem Th38:
for K being Field,M1,M2 being Matrix of K
  st width M1=len M2 & len M1>0& len M2>0
   holds 0.(K,len M1,width M1)*M2=0.(K,len M1,width M2)
proof let K be Field,M1,M2 be Matrix of K;
  assume A1: width M1=len M2 & len M1>0& len M2>0;
A2: len (0.(K,len M1,width M1))=len M1 by MATRIX_1:def 3;
A3: width (0.(K,len M1,width M1))=width M1 by A1,A2,MATRIX_1:20;
A4: len ((0.(K,len M1,width M1))*M2)=len (0.(K,len M1,width M1))
      by MATRIX_3:def 4,A1,A3;
A5: width ((0.(K,len M1,width M1))*M2)=width M2 by MATRIX_3:def 4,A1,A3;
A6: len -((0.(K,len M1,width M1))*M2)=len ((0.(K,len M1,width M1))*M2)
      by MATRIX_3:def 2;
A7: width -((0.(K,len M1,width M1))*M2)=width ((0.(K,len M1,width M1))*M2)
      by MATRIX_3:def 2;
A8:(0.(K,len M1,width M1))*M2
   =((0.(K,len M1,width M1)+(0.(K,len M1,width M1))))*M2 by MATRIX_3:6
  .=((0.(K,len M1,width M1)))*M2+((0.(K,len M1,width M1)))*M2
   by MATRIX_4:63,A1,A2,A3;
  set B=(0.(K,len M1,width M1))*M2;
  0.(K,len M1,width M2)=B+B+(-B) by MATRIX_4:2,A2,A4,A5,A1,A8; then
  0.(K,len M1,width M2) =B+(B-B) by MATRIX_3:5,A6,A7
  .=(0.(K,len M1,width M1))*M2 by MATRIX_4:20,A1,A2,A4,A7;
  hence thesis;
end;

theorem
  for M1,M2 being Matrix of COMPLEX st
    width M1=len M2 &len M1>0&len M2>0 holds
      0_Cx(len M1,width M1)*M2=0_Cx(len M1,width M2)
proof let M1,M2 be Matrix of COMPLEX;
 assume A1:width M1=len M2& len M1>0& len M2>0;
  len M1=len COMPLEX2Field M1 &
  width M1=width COMPLEX2Field M1;
  hence thesis by Th38,A1;
end;

theorem Th40:
  for K being Field,M1 being Matrix of K st len M1>0 holds
    0.K*M1=0.(K,len M1,width M1)
proof let K be Field,M1 be Matrix of K;
  assume A1: len M1>0;
  A2: len (0.K*M1)=len M1 & width (0.K*M1)=width M1 by MATRIX_3:def 5;
  A3: len (0.(K,len M1,width M1))=len M1 by MATRIX_1:def 3;then
  A4: width (0.(K,len M1,width M1))=width M1 by A1,MATRIX_1:20;
  for i,j be Nat st [i,j] in Indices (0.(K,len M1,width M1)) holds
    ((0.K)*M1)*(i,j)=(0.(K,len M1,width M1))*(i,j)
  proof let i,j be Nat;
    assume A5: [i,j] in Indices (0.(K,len M1,width M1)); then
    A6: (0.(K,len M1,width M1))*(i,j)=0.K by MATRIX_3:3;
    A7: Indices (0.(K,len M1,width M1))= Indices M1
    by MATRIX_4:55,A3,A4;
    ((0.K)*M1)*(i,j)=(0.K)*(M1*(i,j)) by MATRIX_3:def 5,A5,A7;
    hence (0.(K,len M1,width M1))*(i,j)=((0.K)*M1)*(i,j)
    by A6,VECTSP_1:36;
   end;
  hence (0.K)*M1=0.(K,len M1,width M1) by MATRIX_1:21,A2,A3,A4;
end;

theorem
  for M1 being Matrix of COMPLEX st len M1>0 holds
    0*M1 = 0_Cx(len M1,width M1)
proof let M1 be Matrix of COMPLEX;
 assume A1: len M1>0;
  0 in COMPLEX by XCMPLX_0:def 2; then
  reconsider ea=0 as Element of F_Complex by COMPLFLD:def 1;
  0*M1 = Field2COMPLEX (ea*(COMPLEX2Field M1)) by Def7
 .=0_Cx(len M1,width M1) by Th40,A1,COMPLEX1:def 6, COMPLFLD:9;
  hence thesis;
end;

definition let s be FinSequence of COMPLEX, k be set;
 redefine func s.k -> Element of COMPLEX;
 coherence
 proof
  per cases;
   suppose k in dom s; hence thesis by FINSEQ_2:13;
  end;
  suppose not k in dom s;
  hence s.k is Element of COMPLEX by COMPLEX1:def 6,FUNCT_1:def 4;
 end;
 end;
end;

theorem
  for i,j being Nat,M1,M2 being Matrix of COMPLEX st
  len M1>0 & len M2>0 & width M1=len M2
   & 1<=i & i<= len M1 & 1<=j & j<=width M2 holds
(ex s being FinSequence of COMPLEX
 st len s = len M2
 & s.1=(M1*(i,1))*(M2*(1,j))
 & (for k be Nat st 1<=k & k < len M2
 holds s.(k+1)=s.k + (M1*(i,k+1))*(M2*(k+1,j))))
proof let i,j be Nat,M1,M2 be Matrix of COMPLEX;
 assume A1: len M1>0 & len M2>0 & width M1=len M2
   & 1<=i & i<= len M1 & 1<=j & j<=width M2;
 0+1<=len M2 by A1,NAT_1:26;then
 0+1-1<=len M2-1 by XREAL_1:11;then
 A2: len M2-'1=len M2-1 by BINARITH:def 3;then
 A3: 0+1<=(len M2)-'1+1 by A1,NAT_1:26;
 defpred P[Nat] means ex q being FinSequence of COMPLEX st
 1<=$1+1 & $1+1<=len M2 implies
 ( len q=$1 +1 & q.1=(M1*(i,1))*(M2*(1,j))
 & (for k be Nat st 1<=k & k<$1+1
 holds q.(k+1)=q.k+(M1*(i,k+1))*(M2*(k+1,j))));
 set q0=<*(M1*(i,1))*(M2*(1,j))*>;
 A4: len q0 = 1 by FINSEQ_1:56;
 A5: q0.1=(M1*(i,1))*(M2*(1,j)) by FINSEQ_1:def 8;
 for k be Nat st 1<=k & k<1 holds q0.(k+1)=q0.k+(M1*(i,k+1))*(M2*(k+1,j));then
 A6: P[0] by A4,A5;
 A7: for i2 being Nat st P[i2] holds P[i2+1]
 proof let i2 be Nat;
   assume P[i2]; then
   consider q2 being FinSequence of COMPLEX such that
   A8: 1<=i2+1 & i2+1<=len M2 implies
   ( len q2=i2+1 & q2.1=(M1*(i,1))*(M2*(1,j))
   & (for k2 being Nat st 1<=k2 & k2<i2+1
   holds q2.(k2+1)=q2.k2+(M1*(i,k2+1))*(M2*(k2+1,j))));
   set k0=i2;
   set q3=q2^<* q2.(k0+1)+(M1*(i,k0+1+1))*(M2*(k0+1+1,j)) *>;
   1<=i2+1+1 & i2+1+1<=len M2 implies
   ( len q3 = i2+1+1
   & q3.1=(M1*(i,1))*(M2*(1,j))
   & (for k be Nat st 1<=k & k< i2+1+1
   holds q3.(k+1)=q3.k+(M1*(i,k+1))*(M2*(k+1,j))))
   proof
     assume A9: 1<=i2+1+1 & i2+1+1<=len M2;
     per cases by NAT_1:37;
     suppose A10: 1 <= i2+1;
A11: len q3 = len q2+len (<* q2.(k0+1)+(M1*(i,k0+1+1))*(M2*(k0+1+1,j)) *>)
           by FINSEQ_1:35
        .=i2+1+1 by A8,A10,NAT_1:38,A9,FINSEQ_1:56;
        1 < len (q2^<* q2.(k0+1)+(M1*(i,k0+1+1))*(M2*(k0+1+1,j)) *>)
        by A11,NAT_1:38,A10;then
        1 < len q2 + len <* q2.(k0+1)+(M1*(i,k0+1+1))*(M2*(k0+1+1,j))*>
          by FINSEQ_1:35;then
        1 < len q2 + 1 by FINSEQ_1:56;then
        1 <= len q2 by NAT_1:38; then
A12:    1 in dom q2 by FINSEQ_3:27;
        for k2 being Nat st 1<=k2 & k2<i2+1+1
          holds q3.(k2+1)=q3.k2+(M1*(i,k2+1))*(M2*(k2+1,j))
        proof let k2 be Nat;
         assume A13: 1<=k2 & k2<i2+1+1;then
         A14: k2<=i2+1 by NAT_1:38;
          per cases by REAL_1:def 5,A14;
          suppose A15: k2<i2+1;then
            k2+1 < i2 + 1+1 by XREAL_1:8;then
            k2+1 < len q2 +
               len <* q2.(k0+1)+(M1*(i,k0+1+1))*(M2*(k0+1+1,j))*>
                     by FINSEQ_1:35,A11; then
            k2+1 < len q2 + 1 by FINSEQ_1:56;then
            A16: k2+1 <= len q2 by NAT_1:38;
            1 <= k2+1 by A13, NAT_1:38; then
            k2+1 in dom q2 by FINSEQ_3:27,A16;then
            A17: q3.(k2+1)=q2.(k2+1) by FINSEQ_1:def 7;
            k2 < len q3 by A11,A15,NAT_1:38;then
            k2 < len q2 + len <* q2.(k0+1)+(M1*(i,k0+1+1))*(M2*(k0+1+1,j))*>
              by FINSEQ_1:35; then
            k2 < len q2 + 1 by FINSEQ_1:56;then
            k2 <= len q2 by NAT_1:38; then
            k2 in dom q2 by FINSEQ_3:27,A13;then
            q3.k2=q2.k2 by FINSEQ_1:def 7;
           hence q3.(k2+1) = q3.k2+(M1*(i,k2+1))*(M2*(k2+1,j))
            by A8,NAT_1:38,A9,A10,A13,A15,A17;
          end;
          suppose A18: k2=i2+1;
            k2 < i2 + 1+1 by NAT_1:38,A18;then
            k2 < len q2 + len <* q2.(k0+1)+(M1*(i,k0+1+1))*(M2*(k0+1+1,j))*>
             by FINSEQ_1:35,A11; then
            k2 < len q2 + 1 by FINSEQ_1:56;then
            k2 <= len q2 by NAT_1:38; then
            k2 in dom q2 by FINSEQ_3:27,A13;then
            A19: q3.k2=q2.k2 by FINSEQ_1:def 7;
            A20: (<* q2.(k0+1)+(M1*(i,k0+1+1))*(M2*(k0+1+1,j)) *>).1
               = q2.(k0+1)+(M1*(i,k0+1+1))*(M2*(k0+1+1,j)) by FINSEQ_1:def 8;
            1 in Seg 1 by FINSEQ_1:5;then
            1 in dom (<* q2.(k0+1)+(M1*(i,k0+1+1))*(M2*(k0+1+1,j)) *>)
                         by FINSEQ_1:55;
           hence q3.(k2+1) =q3.k2+(M1*(i,k2+1))*(M2*(k2+1,j))
            by A18,A19,A8,NAT_1:38,A9,A10,A20,FINSEQ_1:def 7;
          end;
         end;
      hence thesis by A11,A12,A8,A10,NAT_1:38,A9,FINSEQ_1:def 7;
     end;
    end;
    hence thesis;
 end;
 for j being Nat holds P[j] from NAT_1:sch 1(A6,A7);
 hence thesis by A3,A2;
end;
