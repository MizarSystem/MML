:: Alexandroff One Point Compactification
::  by Czeslaw Bylinski
:: 
:: Received August 13, 2007
:: Copyright (c) 2007 Association of Mizar Users

environ

 vocabularies FINSET_1, SETFAM_1, BOOLE, SUBSET_1, TARSKI, FUNCT_1, RELAT_1,
      ORDINAL1, CANTOR_1, PRE_TOPC, TOPS_1, FUNCT_3, TOPS_2, QUOFIELD,
      ORDINAL2, CONNSP_2, NATTRA_1, YELLOW_6, COMPTS_1, WAYBEL_3, COMPACT1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, RELAT_1, FUNCT_1, RELSET_1,
      SETFAM_1, ORDINAL1, FINSET_1, FINSUB_1, DOMAIN_1, STRUCT_0, PRE_TOPC,
      TOPS_1, TOPS_2, URYSOHN1, TDLAT_3, TSEP_1, TEX_3, COMPTS_1, CONNSP_2,
      WAYBEL_3, YELLOW_9, YELLOW13;
 constructors FINSOP_1, SETFAM_1, DOMAIN_1, TOPS_1, TOPS_2, YELLOW_9, YELLOW13,
      TDLAT_3, CONNSP_2, URYSOHN1, T_0TOPSP, WAYBEL_3, TSEP_1, TEX_3;
 registrations XBOOLE_0, SUBSET_1, FINSET_1, STRUCT_0, FINSUB_1, RELAT_1,
      TOPS_1, BORSUK_2, FUNCT_1, PRE_TOPC, TEX_1, YELLOW13, BORSUK_6;
 requirements BOOLE, SUBSET;
 definitions TARSKI, XBOOLE_0, ORDINAL1, STRUCT_0, PRE_TOPC, SUBSET_1, TOPS_1,
      TOPS_2, COMPTS_1, WAYBEL_3, YELLOW13;
 theorems XBOOLE_1, ZFMISC_1, YELLOW_8, YELLOW_9, XBOOLE_0, RELAT_1, CONNSP_2,
      TEX_3, TEX_4, PRE_TOPC, TOPS_1, TOPS_2, TSEP_1, TARSKI, FUNCT_1,
      FINSET_1, STRUCT_0, TDLAT_3, FUNCT_2, SUBSET_1, COMPTS_1, WAYBEL_3,
      TOPMETR, YELLOW13, BORSUK_1;
 schemes WELLORD2;

begin :: Preliminaires

definition let X be TopSpace, P be Subset-Family of X;
  attr P is compact means :Def5:
    for U being Subset of X st U in P holds U is compact;
end;

definition let X be TopSpace, U be Subset of X;
  attr U is relatively-compact means :Def6:
    Cl U is compact;
end;

registration let X be TopSpace;
  cluster {}X -> relatively-compact;
  coherence
  proof
    Cl {}X = {}X by PRE_TOPC:52;
    hence thesis by Def6;
  end;
end;

registration let X be TopSpace;
  cluster relatively-compact Subset of X;
  existence
  proof
    take {}X;
    thus thesis;
  end;
end;

registration let X be TopSpace, U be relatively-compact Subset of X;
  cluster Cl U -> compact;
  coherence by Def6;
end;

notation let X be TopSpace, U be Subset of X;
  synonym U is pre-compact for U is relatively-compact;
end;

:: see WAYBEL_3:def 9
notation let X be non empty TopSpace;
  synonym X is liminally-compact for X is locally-compact;
end;

:: see WAYBEL_3:def 9
definition let X be non empty TopSpace;
  redefine attr X is liminally-compact means
:Def7: for x being Point of X ex B being basis of x st B is compact;
   compatibility
 proof
  hereby assume
A1:  X is liminally-compact;
   let x be Point of X;
   set B = {Q where Q is a_neighborhood of x: Q is compact &
            ex V being a_neighborhood of x st Q c= V};
   B c= bool [#]X
    proof let A be set;
     assume A in B; 
     then ex Q being a_neighborhood of x st A = Q & Q is compact &
            ex V being a_neighborhood of x st Q c= V;
     hence A in bool [#]X;
    end;
   then reconsider B as Subset-Family of X; 
A2:  B is basis of x
   proof
    let V be a_neighborhood of x;
    x in Int V by CONNSP_2:def 1;
    then consider Q being Subset of X such that
A5:   x in Int Q and
A3:   Q c= Int V and
A4:   Q is compact by A1,WAYBEL_3:def 9;
    reconsider Q as a_neighborhood of x by A5,CONNSP_2:def 1;
    take Q;
    Int V c= V by TOPS_1:44;
    hence thesis by A4,A3,XBOOLE_1:1;
   end;
  B is compact
   proof let U be Subset of X;
    assume U in B;
    then ex Q being a_neighborhood of x st U = Q & Q is compact &
            ex V being a_neighborhood of x st Q c= V;
    hence U is compact;
   end;
   hence ex B being basis of x st B is compact by A2;
  end;
  assume
A1:  for x being Point of X ex B being basis of x st B is compact;
  let x be Point of X, P be Subset of X such that
A2:  x in P and
A3:  P is open;
   consider B being basis of x such that
A4:  B is compact by A1;
   x in Int P by A2,A3,TOPS_1:55;
   then consider Q being Subset of X such that
A5:  Q in B and
A6:  x in Int Q & Q c= P by YELLOW13:def 1;
   take Q;
   thus x in Int Q & Q c= P by A6;
   thus Q is compact by A5,A4,Def5;
 end;
end;

definition let X be non empty TopSpace;
  attr X is locally-relatively-compact means
:Def8: for x being Point of X
          ex U being a_neighborhood of x st U is relatively-compact;
end;

definition let X be non empty TopSpace;
  attr X is locally-closed/compact means
:Def9: for x being Point of X
          ex U being a_neighborhood of x st U is closed compact;
end;

definition let X be non empty TopSpace;
  attr X is locally-compact means
:Def4: for x being Point of X
          ex U being a_neighborhood of x st U is compact;
end;

registration
  cluster liminally-compact -> locally-compact (non empty TopSpace);
  coherence
 proof let X be non empty TopSpace such that
A1:  X is liminally-compact;
  let x be Point of X;
  consider B being basis of x such that
A2:   B is compact by A1,Def7;
  consider V being a_neighborhood of x;
  consider Y being a_neighborhood of x such that
A3:   Y in B and Y c= V by YELLOW13:def 2;
  Y is compact by A2,A3,Def5;
  hence thesis;
 end;
end;

registration
  cluster locally-compact -> liminally-compact (non empty being_T3 TopSpace);
  coherence
 proof let X be non empty being_T3 TopSpace such that
A1:  X is locally-compact;
  let x be Point of X;
  set B = {Q where Q is a_neighborhood of x: Q is compact &
            ex V,W being a_neighborhood of x st Q c= V /\ W};
  B c= bool [#]X
   proof let A be set;
    assume A in B; 
    then ex Q being a_neighborhood of x st A = Q & Q is compact &
            ex V,W being a_neighborhood of x st Q c= V /\ W;
    hence A in bool [#]X;
   end;
  then reconsider B as Subset-Family of X; 
A2:  B is basis of x
  proof
   let V be a_neighborhood of x;
   consider W being a_neighborhood of x such that
A4: W is compact by A1,Def4;
A5: x in Int W by CONNSP_2:def 1;
    x in Int V by CONNSP_2:def 1;
   then x in Int V /\ Int W by A5,XBOOLE_0:def 3;
   then x in Int(V /\ W) by TOPS_1:46;
   then consider Q being Subset of X such that
A8: Q is closed and
A9: Q c= V /\ W and
A10: x in Int Q by YELLOW_8:36;
    reconsider Q as a_neighborhood of x by A10,CONNSP_2:def 1;
    take Q;
      V /\ W c= W by XBOOLE_1:17;
    then Q c= W by A9,XBOOLE_1:1;
    then Q is compact by A4,A8,COMPTS_1:18;
    hence Q in B by A9;
      V /\ W c= V by XBOOLE_1:17;
    hence Q c= V by A9,XBOOLE_1:1;
  end;
  B is compact
   proof let U be Subset of X;
    assume U in B;
    then ex Q being a_neighborhood of x st U = Q & Q is compact &
            ex V,W being a_neighborhood of x st Q c= V /\ W;
    hence U is compact;
   end;
  hence thesis by A2;
 end;
end;

registration
  cluster locally-relatively-compact ->
               locally-closed/compact (non empty TopSpace);
  coherence
 proof let X be non empty TopSpace such that
A1:  X is locally-relatively-compact;
  let x be Point of X;
  consider U being a_neighborhood of x such that
A2: U is relatively-compact by A1,Def8;
A3: Cl U is compact by A2,Def6;
   U c= Cl U by PRE_TOPC:48; then
A4:  Int U c= Int Cl U by TOPS_1:48;
   x in Int U by CONNSP_2:def 1;
   then Cl U is a_neighborhood of x by A4,CONNSP_2:def 1;
   hence thesis by A3;
 end;
end;

registration
  cluster locally-closed/compact ->
             locally-relatively-compact (non empty TopSpace);
  coherence
 proof let X be non empty TopSpace such that
A1:  X is locally-closed/compact;
  let x be Point of X;
  consider U being a_neighborhood of x such that
A2:  U is closed compact by A1,Def9;
  Cl U = U by A2,PRE_TOPC:52;
  then U is relatively-compact by A2,Def6;
  hence thesis;
 end;
end;

registration
  cluster locally-relatively-compact -> locally-compact (non empty TopSpace);
  coherence
 proof let X be non empty TopSpace such that
A1:  X is locally-relatively-compact;
   let x be Point of X;
   consider Y being a_neighborhood of x such that
A2:   Y is relatively-compact by A1,Def8;
A3:   Cl Y is compact by A2,Def6;
   Y c= Cl Y by PRE_TOPC:48;
   then
A4:  Int Y c= Int Cl Y by TOPS_1:48;
   x in Int Y by CONNSP_2:def 1;
   then Cl Y is a_neighborhood of x by A4,CONNSP_2:def 1;
   hence thesis by A3;
 end;
end;

registration
  cluster locally-compact ->
             locally-relatively-compact (non empty Hausdorff TopSpace);
  coherence
  proof let X be non empty Hausdorff TopSpace such that
A1:  X is locally-compact;
   let x be Point of X;
   consider U being a_neighborhood of x such that
A2:   U is compact by A1,Def4;
  U is closed by A2,COMPTS_1:16;
  then Cl U = U by PRE_TOPC:52;
  then U is relatively-compact by A2,Def6;
  hence thesis;
 end;
end;

registration
  cluster compact -> locally-compact (non empty TopSpace);
  coherence
  proof let X be non empty TopSpace;
   set Y = [#]X;
   assume X is compact; then
A3:   Y is compact by COMPTS_1:10;
   let x be Point of X;
   Int Y = Y  by TOPS_1:55;
   then Y is a_neighborhood of x by CONNSP_2:def 1;
   hence thesis by A3;
 end;
end;

registration
  cluster discrete -> locally-compact (non empty TopSpace);
  coherence
 proof let X be non empty TopSpace such that
A1:  X is discrete;
   let x be Point of X;
   set Y = {x};
   Y is open by A1,TDLAT_3:17;
   then Int Y = Y by TOPS_1:55;
   then x in Int Y by TARSKI:def 1;
   then reconsider Y as a_neighborhood of x by CONNSP_2:def 1;
   Y is compact by BORSUK_1:41;
   hence thesis;
 end;
end;

registration
  cluster discrete non empty TopSpace;
  existence
  proof
   consider X being discrete non empty TopSpace;
   take X;
   thus thesis;
 end;
end;

registration let X be locally-compact (non empty TopSpace),
                 C be closed non empty Subset of X;
  cluster X | C -> locally-compact;
  coherence
  proof let x be Point of X|C;
C1:  [#](X|C) = C by PRE_TOPC:29;
    reconsider xx = x as Point of X by TOPMETR:2;
    consider K being a_neighborhood of xx such that
K1:   K is compact by Def4;
E2:    K /\ C c= C by XBOOLE_1:17;
    reconsider KC = K /\ C as Subset of (X|C) by C1,XBOOLE_1:17;
    reconsider KC as a_neighborhood of x by C1,TOPMETR:6;
    take KC;
K2:   [#](X|K) = K by PRE_TOPC:29;
E1:    K /\ C c= K by XBOOLE_1:17;
    reconsider KK = K /\ C as Subset of (X|K) by K2,XBOOLE_1:17;
K3:    KK is closed by K2,PRE_TOPC:43; 
K4:    Int K c= K by TOPS_1:44;
C3:    xx in Int K by CONNSP_2:def 1;
    then X|K is compact by K1,K4,COMPTS_1:12;
    then
K7:    KK is compact by K3,COMPTS_1:17;
C4:    x in KC by C1,C3,K4,XBOOLE_0:def 3;
    then (X|K) | KK is compact by K7,COMPTS_1:12;
    then X | (K /\ C) is compact by E1,PRE_TOPC:28;
    then (X|C) | KC is compact by E2,PRE_TOPC:28;
    hence KC is compact by C4,COMPTS_1:12;
  end;
end;

registration let X be locally-compact (non empty being_T3 TopSpace),
                 P be open non empty Subset of X;
  cluster X | P -> locally-compact;
   coherence
  proof let x be Point of X|P;
   reconsider xx = x as Point of X by TOPMETR:2;
   consider B being basis of xx such that
A1:    B is compact by Def7;
C1:    [#](X|P) = P by PRE_TOPC:29;
   then xx in P;
   then xx in Int P by TOPS_1:55;
   then P is a_neighborhood of xx by CONNSP_2:def 1;
   then consider K being a_neighborhood of xx such that
A2:   K in B and
A3:   K c= P by YELLOW13:def 2;
    reconsider KK = K as Subset of X|P by A3,PRE_TOPC:29;
A4:   xx in Int K by CONNSP_2:def 1;
    Int K c= K by TOPS_1:44;
    then
A5:   x in K by A4;
    KK = K /\ P by A3,XBOOLE_1:28;
    then
A6:   KK is a_neighborhood of x by A3,A5,TOPMETR:6;
    K is compact by A1,A2,Def5;
    then KK is compact by C1,COMPTS_1:11;
    hence thesis by A6;    
  end;
end;

theorem
  for X being Hausdorff non empty TopSpace, E being non empty Subset of X
     st X|E is dense locally-compact
    holds X|E is open
proof let X be Hausdorff non empty TopSpace, E be non empty Subset of X
  such that
A1:  X|E is dense locally-compact;
A2:  [#](X|E) = E by PRE_TOPC:29;
 Int E = E
  proof
   thus Int E c= E by TOPS_1:44;
   let a be set;
   assume
B1:   a in E;
   then reconsider x = a as Point of X;
   reconsider xx = x as Point of X|E by PRE_TOPC:29,B1;
   consider K being a_neighborhood of xx such that
B2:   K is compact by A1,Def4;
B3:   xx in Int K by CONNSP_2:def 1;
   set UE = {G where G is Subset of X : G is open & G c= E};
   Int K in the topology of X|E by PRE_TOPC:def 5;
   then consider G being Subset of X such that
B5:   G in the topology of X and
B6:   Int K  = G /\ E by A2,PRE_TOPC:def 9;
G1:   G is open by B5,PRE_TOPC:def 5;
G2:   Int K c= G by B6,XBOOLE_1:17;
K4:   G /\ E c= K by B6,TOPS_1:44;
   E is dense by A1,A2,TEX_3:def 1;
   then
C1:   Cl(G /\ E) = Cl G by G1,TOPS_1:81;
K6:   K c= [#](X|E);
   reconsider KK = K as Subset of X by A2,XBOOLE_1:1;
   for P being Subset of X|E st P=KK holds P is compact by B2;
   then KK is compact by K6,COMPTS_1:11;
   then KK is closed by COMPTS_1:16;
   then 
C2:   Cl(G /\ E)  c= KK by K4,TOPS_1:31;
   G c= Cl G by PRE_TOPC:48;
   then G c= K by C1,C2,XBOOLE_1:1;
   then G c= E by A2,XBOOLE_1:1;
   then G in UE by G1;
   then a in union UE by G2,B3,TARSKI:def 4;
   hence a in Int E by TEX_4:4;
  end;
 hence X|E is open by A2,TSEP_1:16;
end;

theorem Th4:
  for X,Y being TopSpace, A being Subset of X st [#]X c= [#]Y
   holds incl(X,Y).:A = A
proof let X,Y be TopSpace, A be Subset of X;
 assume [#]X c= [#]Y;
 hence incl(X,Y).:A = id([#]X).:A by YELLOW_9:def 1
                   .= A by FUNCT_1:162;
end;

definition let X,Y be TopSpace, f be Function of X,Y;
  attr f is embedding means
    ex h being Function of X, Y | (rng f) st h = f & h is_homeomorphism;
end;

theorem Th3:
  for X,Y being TopSpace st [#]X c= [#]Y &
   ex Xy being Subset of Y st Xy = [#]X &
         the topology of Y|Xy = the topology of X
  holds incl(X,Y) is embedding
proof let X,Y be TopSpace such that
C:   [#]X c= [#]Y;
  given Xy being Subset of Y such that
T:   Xy = [#]X and
S:   the topology of Y|Xy = the topology of X;
A1:  incl(X,Y) = id X by C,YELLOW_9:def 1;
A2:  [#]Y = {} implies [#]X = {} by C,XBOOLE_1:3;
 set YY = Y| (rng incl(X,Y));
A3:  [#]YY = rng incl(X,Y) by PRE_TOPC:def 10;
 then reconsider h = incl(X,Y) as Function of X,YY by A2,FUNCT_2:8;
 take h;
 thus h = incl(X,Y);
 thus dom h = [#]X & rng h = [#]YY by A2,PRE_TOPC:def 10,FUNCT_2:def 1;
 thus h is one-to-one by A1;
A5:  [#]YY = {} implies [#]X = {} by A1,A3,RELAT_1:71;
 set f = id X; 
A9:  [#]X = {} implies [#]X = {};
  for P being Subset of YY st P is open holds h"P is open
   proof let P be Subset of YY;
     assume P is open;
     then 
P1:    P in the topology of YY by PRE_TOPC:def 5;
     reconsider Q = P as Subset of X by A1,A3,RELAT_1:71;
     Q in the topology of X by S,P1,T,A1,RELAT_1:71;
     then Q is open by PRE_TOPC:def 5;
     then f"Q is open by A9,TOPS_2:55;
     then f"Q in the topology of X by PRE_TOPC:def 5;
     hence h"P in the topology of X by C,YELLOW_9:def 1;
   end;
  hence h is continuous by A5,TOPS_2:55;
E1:  [#]X = {} implies [#]YY = {} by A1,A3,RELAT_1:71;
  for P being Subset of X st P is open holds (h")"P is open
   proof let P be Subset of X;
     assume P is open;
     then 
P1:    P in the topology of X by PRE_TOPC:def 5;
     reconsider Q = P as Subset of YY by A1,A3,RELAT_1:71;
       (h")"P = h.:Q by A1,A3,TOPS_2:67
             .= Q by A1,FUNCT_1:162;
     hence (h")"P in the topology of YY by P1,T,S,A1,RELAT_1:71;
   end;
  hence h" is continuous by E1,TOPS_2:55;
end;

definition let X be TopSpace, Y be TopSpace, h be Function of X,Y;
  attr h is compactification means
:Def1: h is embedding & Y is compact & h.:([#]X) is dense;
end;

registration let X be TopSpace, Y be TopSpace;
  cluster compactification -> embedding Function of X,Y;
  coherence by Def1;
end;

definition let X be TopStruct;
 func One-Point_Compactification(X) -> strict TopStruct means
:Def2: the carrier of it = succ([#]X) &
      the topology of it = (the topology of X) \/
        {U \/ {[#]X} where U is Subset of X: U is open & U` is compact};
  existence
  proof
    set T = succ [#]X,
        D = {U \/ {[#]X} where U is Subset of X: U is open & U` is compact}, 
        O = (the topology of X) \/ D;
    O c= bool T
     proof let a be set;
      assume a in O;
      then
A1:     a in the topology of X or a in D by XBOOLE_0:def 2;
      now assume a in D; then
       consider U being Subset of X such that
B1:     a = U \/ {[#]X} and U is open and U` is compact;
       thus a c= T
        proof let A be set;
         assume A in a;
         then A in U or A in {[#]X} by B1,XBOOLE_0:def 2;
         hence thesis by XBOOLE_0:def 2;
        end;
      end;
      then a is Subset of T by A1,XBOOLE_1:10;
      hence thesis;
     end; 
    then reconsider O as Subset-Family of T;
    set Y = TopStruct(#T,O#);
    Y is non empty by STRUCT_0:def 1;
    hence thesis;
  end;
  uniqueness;
end;

registration let X be TopStruct;
  cluster One-Point_Compactification(X) -> non empty;
  coherence
  proof
    the carrier of One-Point_Compactification(X) = succ([#]X) by Def2;
    hence thesis by STRUCT_0:def 1;
  end;
end;

theorem Th2:
  for X being TopStruct holds [#]X c= [#]One-Point_Compactification(X)
proof let X be TopStruct;
 [#]One-Point_Compactification(X) = succ([#]X) by Def2;
 hence [#]X c= [#]One-Point_Compactification(X) by XBOOLE_1:7;
end;  

registration let X be TopSpace;
  cluster One-Point_Compactification(X) -> TopSpace-like;
  coherence
  proof
A:  not [#]X in [#]X;
    set T = succ [#]X,
        D = {U \/ {[#]X} where U is Subset of X: U is open & U` is compact};
C:    [#]One-Point_Compactification(X) = T by Def2;
T:    the topology of One-Point_Compactification(X) = (the topology of X) \/ D
         by Def2;
    set Y = One-Point_Compactification(X);
    Y is TopSpace-like
     proof
      ([#]X)` = ({}X)``
             .= {}X;
      then T in D;
      hence the carrier of Y in the topology of Y by C,T,XBOOLE_0:def 2;
      hereby let a be Subset-Family of Y;
       assume 
E1:       a c= the topology of Y;
E2:       not [#]X in [#]X;
         set ua = {U where U is Subset of X: U is open &
                      (U in a or U \/ {[#]X} in a)};
E3:      ua c= the topology of X
          proof let x be set;
           assume x in ua;
           then ex U being Subset of X st x = U & U is open &
                       (U in a or U \/ {[#]X} in a);
           hence thesis by PRE_TOPC:def 5;
          end;
        reconsider ua as Subset-Family of X by E3,XBOOLE_1:1;
        union ua in the topology of X by E3,PRE_TOPC:def 1; then
F8:        union ua is open by PRE_TOPC:def 5; then
F7:        (union ua)` is closed by TOPS_1:30;
E5:        union ua = (union a) \ {[#]X}
         proof
          thus union ua c= (union a) \ {[#]X}
           proof let x be set;
            assume x in union ua;
            then consider A being set such that
K1:          x in A and
K2:          A in ua by TARSKI:def 4;
            consider U being Subset of X such that 
K3:           A = U and U is open and 
K5:           (U in a or U \/ {[#]X} in a) by K2;
K6:           now assume x in {[#]X}; then
K7:              x = [#]X by TARSKI:def 1;
               x in [#]X by K1,K3;
               hence contradiction by K7;
              end; 
            now per cases by K5;
             suppose U in a;
              hence x in union a by K1,K3,TARSKI:def 4;
             end;
             suppose
K7:             U \/ {[#]X} in a;
               x in U \/ {[#]X} by K1,K3,XBOOLE_0:def 2;
               hence x in union a by K7,TARSKI:def 4;
             end;
            end;
            hence thesis by K6,XBOOLE_0:def 4;
           end;
          let x be set;
          assume 
L1:          x in (union a) \ {[#]X};
          then x in union a by XBOOLE_0:def 4;
          then consider A being set such that
L2:         x in A and
L3:         A in a by TARSKI:def 4;
           per cases by E1,L3,T,XBOOLE_0:def 2;
            suppose
L5:            A in the topology of X; 
             then reconsider U = A as Subset of X;
             U is open by L5,PRE_TOPC:def 5;
             then U in ua by L3;
             hence x in union ua by L2,TARSKI:def 4;
            end; 
            suppose A in D; 
             then consider U being Subset of X such that
L6:            A = U \/ {[#]X} and 
L7:            U is open and U` is compact;
             not x in {[#]X} by L1,XBOOLE_0:def 4;
             then 
L8:            x in U by L2,L6,XBOOLE_0:def 2;
             U in ua by L3,L6,L7;
             hence x in union ua by L8,TARSKI:def 4;
            end; 
         end;
         per cases;
         suppose 
F0:         [#]X in union a; 
           then consider A being set such that
F1:         [#]X in A and
F2:         A in a by TARSKI:def 4;
            A in the topology of X or A in D by E1,F2,T,XBOOLE_0:def 2;
           then consider U being Subset of X such that
F4:          A = U \/ {[#]X} and U is open and
F6:          U` is compact by F1,E2;
G2:        now assume [#]X in U;
            then [#]X in [#]X; 
            hence contradiction;
           end;
G3:          A \ {[#]X} = U \ {[#]X} by F4,XBOOLE_1:40
                       .= U by G2,ZFMISC_1:65;
           A c= union a by F2,ZFMISC_1:92;
           then U c= union ua by E5,G3,XBOOLE_1:33;
           then (union ua)` c= U` by XBOOLE_1:34; then
F9:          (union ua)` is compact by F6,F7,COMPTS_1:18;     
           (union ua) \/  {[#]X} = (union a) \/ {[#]X} by E5,XBOOLE_1:39
                                .= (union a) by F0,ZFMISC_1:46;
           then union a in D by F8,F9;
          hence union a in the topology of Y by T,XBOOLE_0:def 2;
         end;
         suppose 
G0:         not [#]X in union a; 
G1:         a c= the topology of X
           proof let A be set;
            assume 
H0:           A in a;
            assume not A in the topology of X;
            then A in D by T,E1,H0,XBOOLE_0:def 2;
            then consider U being Subset of X such that
H2:           A = U \/ {[#]X} and U is open and U` is compact;
            [#]X in {[#]X} by TARSKI:def 1;
            then [#]X in A by H2,XBOOLE_0:def 2;
            hence contradiction by G0,H0,TARSKI:def 4;
           end; 
          then a is Subset-Family of [#]X by XBOOLE_1:1;
          then union a in the topology of X by G1,PRE_TOPC:def 1;
          hence union a in the topology of Y by T,XBOOLE_0:def 2;
         end;
      end;
      let a,b be Subset of Y;
      assume
A3:    a in the topology of Y;
      assume
A4:     b in the topology of Y;
      per cases by A3,A4,T,XBOOLE_0:def 2;
       suppose a in the topology of X & b in the topology of X;
        then a /\ b in the topology of X by PRE_TOPC:def 1;
        hence a /\ b in the topology of Y by T,XBOOLE_0:def 2;
       end; 
       suppose that
B1:       a in  the topology of X and
B2:       b in D;
        consider U being Subset of X such that
B3:       b = U \/ {[#]X} and 
B6:       U is open and U` is compact by B2;
        reconsider a' = a as Subset of X by B1;
        a' is open by B1,PRE_TOPC:def 5;
        then reconsider aXU = a' /\ U as open Subset of X by B6,TOPS_1:38;
        not [#]X in a' by A;
        then {[#]X} misses a' by ZFMISC_1:56;
        then a' /\ {[#]X} = {}X  by XBOOLE_0:def 7; 
        then reconsider aX = a' /\ {[#]X} as open Subset of X;
 B6:      aXU \/ aX is open by TOPS_1:37;     
        a /\ b = aXU \/ aX by B3,XBOOLE_1:23;
        then a /\ b in the topology of X by B6,PRE_TOPC:def 5;
        hence a /\ b in the topology of Y by T,XBOOLE_0:def 2;
       end; 
       suppose that
C1:       b in  the topology of X and 
C2:       a in D;
        consider U being Subset of X such that
C3:       a = U \/ {[#]X} and
C6:       U is open and U` is compact by C2;
        reconsider b' = b as Subset of X by C1;
        b' is open by C1,PRE_TOPC:def 5;
        then reconsider bXUa = b' /\ U as open Subset of X by C6,TOPS_1:38;
        not [#]X in b' by A;
        then {[#]X} misses b' by ZFMISC_1:56;
        then b' /\ {[#]X} = {}X  by XBOOLE_0:def 7; 
        then reconsider bX = b' /\ {[#]X} as open Subset of X;
C6:      bXUa \/ bX is open by TOPS_1:37;
        a /\ b = bXUa \/ bX by C3,XBOOLE_1:23;
        then a /\ b in the topology of X by C6,PRE_TOPC:def 5;
        hence a /\ b in the topology of Y by T,XBOOLE_0:def 2;
       end; 
       suppose that
D1:       a in D and
D2:       b in D;
        consider Ua being Subset of X such that
D3:       a = Ua \/ {[#]X} and 
D4:       Ua is open and
D5:       Ua` is compact by D1;
        consider Ub being Subset of X such that
D6:       b = Ub \/ {[#]X} and 
D7:       Ub is open and
D8:       Ub` is compact by D2;
D9:       a /\ b = (Ua /\ Ub) \/ {[#]X} by D3,D6,XBOOLE_1:24;
D10:      Ua /\ Ub is open by D4,D7,TOPS_1:38;
        Ua` \/ Ub` is compact by D5,D8,COMPTS_1:19;
        then (Ua /\ Ub)` is compact by XBOOLE_1:54;
        then a /\ b in D by D9,D10;
        hence a /\ b in the topology of Y by T,XBOOLE_0:def 2;
       end; 
     end;
    hence thesis;
  end;
end;

theorem Th6:
  for X being TopStruct holds X is SubSpace of One-Point_Compactification(X)
proof let X be TopStruct;
  thus
A2:  [#]X c= [#]One-Point_Compactification(X) by Th2;
  let P being Subset of X;
  set D = {U \/ {[#]X} where U is Subset of X: U is open & U` is compact};
A3:  the topology of One-Point_Compactification(X) = (the topology of X) \/ D
       by Def2;
  hereby assume 
D1:  P in the topology of X;
    reconsider Q = P as Subset of One-Point_Compactification(X)
       by A2,XBOOLE_1:1;
    take Q;
    thus Q in the topology of One-Point_Compactification(X)
       by D1,A3,XBOOLE_0:def 2;
    thus P = Q /\ [#]X by XBOOLE_1:28;
  end;         
  given Q being Subset of One-Point_Compactification(X) such that
B1:   Q in the topology of One-Point_Compactification(X) and
B2:   P = Q /\ [#]X;
  per cases by B1,A3,XBOOLE_0:def 2;
   suppose Q in the topology of X;
    hence P in the topology of X by B2,XBOOLE_1:28;
   end;
   suppose Q in D; 
    then consider U being Subset of X such that
C1:   Q = U \/ {[#]X} and 
C2:   U is open and U` is compact;
     not [#]X in [#]X;
    then {[#]X} misses [#]X by ZFMISC_1:56;
    then {[#]X} /\ [#]X = {} by XBOOLE_0:def 7;
    then P = (U /\ [#]X) \/ {} by B2,C1,XBOOLE_1:23
          .= U by XBOOLE_1:28;
    hence P in the topology of X by C2,PRE_TOPC:def 5;
   end;
end;

registration let X be TopSpace;
  cluster One-Point_Compactification(X) -> compact;
   coherence
  proof
   let F be Subset-Family of One-Point_Compactification(X) such that
A1:    F is_a_cover_of One-Point_Compactification(X) and
A2:    F is open;
A3:    not [#]X in [#]X;
      set D = {U \/ {[#]X} where U is Subset of X: U is open & U` is compact};
A4:    [#]One-Point_Compactification(X) = union F by A1,PRE_TOPC:def 8;
      set Fx = {U where U is Subset of X: U is open &
                     (U in F & not U \/ {[#]X} in F or U \/ {[#]X} in F)};
      Fx c= the topology of X
        proof let x be set;
         assume x in Fx;
         then ex U being Subset of X st x = U & U is open &
            (U in F & not U \/ {[#]X} in F or U \/ {[#]X} in F);
         hence thesis by PRE_TOPC:def 5;
        end;
      then reconsider Fx as Subset-Family of X by XBOOLE_1:1;
      [#]X in {[#]X} by TARSKI:def 1;
      then [#]X in succ([#]X) by XBOOLE_0:def 2;
      then [#]X in [#]One-Point_Compactification(X) by Def2;
      then consider A being set such that
A5:     [#]X in A and
A6:     A in F by A4,TARSKI:def 4;
      reconsider A as Subset of One-Point_Compactification(X) by A6;
      A is open by A2,A6,TOPS_2:def 1;
      then A in the topology of One-Point_Compactification(X)
        by PRE_TOPC:def 5;
      then 
A7:     A in (the topology of X) \/ D by Def2;
      not A in the topology of X by A3,A5;
      then A in D by A7,XBOOLE_0:def 2;
      then consider U being Subset of X such that
B1:     A = U \/ {[#]X} and U is open and
B3:     U` is compact;
B4:     Fx is open
       proof let P be Subset of X;
        assume P in Fx;
        then ex U being Subset of X st P = U & U is open &
          (U in F & not U \/ {[#]X} in F or U \/ {[#]X} in F);
        hence P is open;
       end;
     Fx is_a_cover_of X
       proof
        thus [#]X c= union Fx
         proof let x be set;
          assume
C0:         x in [#]X;
          then x in succ([#]X) by XBOOLE_0:def 2;
          then x in union F by A4,Def2; 
          then consider B being set such that
C1:         x in B and
C2:         B in F by TARSKI:def 4;
          reconsider B as Subset of One-Point_Compactification(X) by C2;
          B is open by A2,C2,TOPS_2:def 1;
          then B in the topology of One-Point_Compactification(X)
             by PRE_TOPC:def 5;
          then 
C3:       B in (the topology of X) \/ D by Def2;
          per cases by C3,XBOOLE_0:def 2;
          suppose
C4:          B in the topology of X;
            then reconsider Bx = B as Subset of X;
            Bx is open & (Bx in F & not Bx \/ {[#]X} in F or Bx \/ {[#]X} in F) 
             by C2,C4,PRE_TOPC:def 5; 
            then Bx in Fx;
            hence x in union Fx by C1,TARSKI:def 4;
          end;
          suppose B in D;
           then consider Bx being Subset of X such that
C5:          B = Bx \/ {[#]X} and 
C6:          Bx is open and Bx` is compact;
           now assume x in {[#]X};
             then x = [#]X by TARSKI:def 1;
             hence contradiction by C0;
           end;
           then 
C7:          x in Bx by C1,C5,XBOOLE_0:def 2;
            Bx in Fx by C2,C6,C5;
            hence x in union Fx by C7,TARSKI:def 4;
          end; 
         end;
        thus union Fx c= [#]X;
       end;
      then [#]X = union Fx by PRE_TOPC:def 8;
      then Fx is_a_cover_of U` by COMPTS_1:def 1;
      then consider Gx being Subset-Family of X such that
B6:      Gx c= Fx and
B7:      Gx is_a_cover_of U` and
B8:      Gx is finite by B3,B4,COMPTS_1:def 7;
      set GX = {W where W is Subset of One-Point_Compactification(X): W in F &
                  ex V being Subset of X st V in Gx & 
                    (V in F & not V \/ {[#]X} in F & W = V or 
                     V \/ {[#]X} in F & W = V \/ {[#]X})};
      defpred P[set,set] means $1 in Gx & 
                   ($1 \/ {[#]X} in F implies $2 = $1 \/ {[#]X}) & 
                   (not $1 \/ {[#]X} in F implies $2 = $1);
P1:    for x being set st x in Gx ex y being set st y in GX & P[x,y]
      proof let x be set;
       assume
Q1:       x in Gx;
        then x in Fx by B6;
        then consider U being Subset of X such that 
Q2:      x = U and U is open and
Q4:      U in F & not U \/ {[#]X} in F or U \/ {[#]X} in F;
       per cases;
       suppose 
Q6:       not U \/ {[#]X} in F;
        then 
        reconsider y = U as Subset of One-Point_Compactification(X) by Q4;
        take y; 
        thus y in GX by Q1,Q2,Q4,Q6;
        thus thesis by Q1,Q2,Q6;
       end; 
       suppose 
Q5:       U \/ {[#]X} in F;
        then reconsider y = U \/ {[#]X}
              as Subset of One-Point_Compactification(X);
        take y; 
        thus y in GX by Q1,Q2,Q5;
        thus thesis by Q1,Q2,Q5;
       end; 
      end;
      consider f being Function such that
F1:     dom f = Gx and rng f c= GX and
F2:     for x being set st x in Gx holds P[x,f.x] from WELLORD2:sch 1(P1);
F3:     GX c= rng f
       proof let y be set;
        assume y in GX;
         then consider W being Subset of One-Point_Compactification(X)
          such that 
R1:        y = W and W in F and
R3:        ex V being Subset of X st V in Gx & 
             (V in F & not V \/ {[#]X} in F & W = V or 
              V \/ {[#]X} in F & W = V \/ {[#]X});
         consider V being Subset of X such that 
R4:        V in Gx and
R5:        V in F & not V \/ {[#]X} in F & W = V or 
           V \/ {[#]X} in F & W = V \/ {[#]X} by R3;
          f.V in rng f by F1,R4,FUNCT_1:12;
         hence thesis by R1,R5,R4,F2;
       end;
H3:      rng f is finite by F1,B8,FINSET_1:26;
H2:     GX c= the topology of One-Point_Compactification(X)
       proof let x be set;
         assume x in GX;
          then consider W being Subset of One-Point_Compactification(X)
           such that 
F1:         x = W and
F2:         W in F and
            ex V being Subset of X st V in Gx & 
              (V in F & not V \/ {[#]X} in F & W = V or 
               V \/ {[#]X} in F & W = V \/ {[#]X});
           W is open by A2,F2,TOPS_2:def 1;
         hence thesis by F1,PRE_TOPC:def 5;
       end;
     reconsider GX as finite Subset-Family of One-Point_Compactification(X)
        by H3,H2,F3,XBOOLE_1:1,FINSET_1:13;
    take G = GX \/ {A};
H1:   GX c= F
       proof let P be set;
        assume P in GX;
        then ex W being Subset of One-Point_Compactification(X)
              st P = W & W in F &
             ex V being Subset of X st V in Gx & 
              (V in F & not V \/ {[#]X} in F & W = V or 
               V \/ {[#]X} in F & W = V \/ {[#]X});
        hence P in F; 
       end;
     {A} c= F by A6,ZFMISC_1:37;
    hence G c= F by H1,XBOOLE_1:8;
     union G = [#]One-Point_Compactification(X)
      proof thus union G c= [#]One-Point_Compactification(X);
       let P be set;
       assume P in [#]One-Point_Compactification(X);
        then
P1:       P in succ([#]X) by Def2;
        per cases by P1,XBOOLE_0:def 2;
        suppose P in [#]X;
         then P in U` \/ U by PRE_TOPC:18;
         then 
P3:         P in U` or P in U by XBOOLE_0:def 2;
         U` c= union Gx by B7,COMPTS_1:def 1;
         then 
P4:         P in union Gx or P in U by P3;
          union Gx c= union GX
           proof let z be set;
            assume z in union Gx;
             then consider S being set such that
K1:           z in S and
K2:           S in Gx by TARSKI:def 4;
             S in Fx by K2,B6;
             then consider Z being Subset of X such that 
F1:           S = Z and Z is open and 
F3:           Z in F & not Z \/ {[#]X} in F or Z \/ {[#]X} in F;
             per cases by F3;
             suppose Z in F & not Z \/ {[#]X} in F;
              then Z in GX by F1,K2;
              hence z in union GX by K1,F1,TARSKI:def 4;
             end;
             suppose 
F4:             Z \/ {[#]X} in F;
F6:             z in Z \/ {[#]X} by K1,F1,XBOOLE_0:def 2;
              Z \/ {[#]X} in GX by F4,K2,F1;
              hence z in union GX by F6,TARSKI:def 4;
             end; 
           end;
         then P in union GX or P in A by B1,P4,XBOOLE_0:def 2;
         then P in union GX or P in union {A} by ZFMISC_1:31;
         then P in (union GX) \/ (union {A}) by XBOOLE_0:def 2;
         hence P in union G by ZFMISC_1:96;
        end; 
        suppose P in {[#]X};
          then 
P2:         P = [#]X by TARSKI:def 1;
          A in {A} by TARSKI:def 1;
          then A in G by XBOOLE_0:def 2;
         hence P in union G by P2,A5,TARSKI:def 4;
        end;
      end;
    hence G is_a_cover_of One-Point_Compactification(X) by PRE_TOPC:def 8;
    thus G is finite;
  end;
end;
  
theorem  
  for X being non empty TopSpace
   holds X is Hausdorff locally-compact
          iff One-Point_Compactification(X) is Hausdorff
proof let X be non empty TopSpace;
  set D = {U \/ {[#]X} where U is Subset of X: U is open & U` is compact};
A0:  the topology of One-Point_Compactification(X) = (the topology of X) \/ D
   by Def2;
B0:  [#]One-Point_Compactification(X) = succ([#]X) by Def2; 
X1:  [#]X c= [#]One-Point_Compactification(X) by Th2;
  thus X is Hausdorff locally-compact
     implies One-Point_Compactification(X) is Hausdorff
  proof assume that
A1:  X is Hausdorff and
A2:  X is locally-compact;
  let p, q be Point of One-Point_Compactification(X) such that 
B1:   p <> q;
  per cases by B0,XBOOLE_0:def 2;
   suppose p in [#]X & q in [#]X;
    then consider W, V being Subset of X such that
C1:      W is open & V is open and
C2:      p in W & q in V & W misses V by A1,B1,COMPTS_1:def 4;
     reconsider W,V as Subset of One-Point_Compactification(X)
         by X1,XBOOLE_1:1;
     take W,V;
     W in the topology of X & V in the topology of X by C1,PRE_TOPC:def 5;
     then W in the topology of One-Point_Compactification(X) & 
          V in the topology of One-Point_Compactification(X)
       by A0,XBOOLE_0:def 2;
     hence W is open & V is open by PRE_TOPC:def 5;
     thus p in W & q in V & W misses V by C2;
   end;    
   suppose that 
D1:   p in [#]X and
D2:   q in {[#]X};
     reconsider px = p as Point of X by D1;
       consider P being a_neighborhood of px such that
D5:       P is compact by A2,Def4;
       [#]X c= [#]One-Point_Compactification(X) by B0,XBOOLE_1:7;
       then reconsider W = Int P as Subset of One-Point_Compactification(X)
         by XBOOLE_1:1;
D6:     P`` = P;
       P is closed by A1,D5,COMPTS_1:16;
       then P` is open by TOPS_1:29;
       then P` \/ {[#]X} in D by D5,D6; 
       then 
D7:       P` \/ {[#]X} in the topology of One-Point_Compactification(X)
            by A0,XBOOLE_0:def 2;
       then
       reconsider V = P`\/{[#]X} as Subset of One-Point_Compactification(X);
       take W,V;
       W in the topology of X by PRE_TOPC:def 5;
       then W in the topology of One-Point_Compactification(X)
          by A0,XBOOLE_0:def 2;
       hence W is open by PRE_TOPC:def 5;
       thus V is open by D7,PRE_TOPC:def 5;
       thus p in W by CONNSP_2:def 1;                         
       thus q in V by D2,XBOOLE_0:def 2;
       Int P c= [#]X & not [#]X in [#]X;
       then not [#]X in Int P;
       then 
D8:       Int P misses {[#]X} by ZFMISC_1:56;
       Int P c= P by TOPS_1:44;
       then Int P misses P` by SUBSET_1:44;
       hence W misses V by D8,XBOOLE_1:70;
   end;    
   suppose that 
D1:    q in [#]X and
D2:    p in {[#]X};
     reconsider qx = q as Point of X by D1;
      consider Q being a_neighborhood of qx such that
D5:       Q is compact by A2,Def4;
       [#]X c= [#]One-Point_Compactification(X) by Th2;
       then reconsider W = Int Q as Subset of One-Point_Compactification(X)
         by XBOOLE_1:1;
D6:     Q`` = Q;
       Q is closed by A1,D5,COMPTS_1:16;
       then Q` is open by TOPS_1:29;
       then Q` \/ {[#]X} in D by D5,D6; 
       then 
D7:       Q` \/ {[#]X} in the topology of One-Point_Compactification(X)
           by A0,XBOOLE_0:def 2;
       then
       reconsider V = Q`\/{[#]X} as Subset of One-Point_Compactification(X);
       take V,W;
       thus V is open by D7,PRE_TOPC:def 5;
       W in the topology of X by PRE_TOPC:def 5;
       then W in the topology of One-Point_Compactification(X)
         by A0,XBOOLE_0:def 2;
       hence W is open by PRE_TOPC:def 5;
       thus p in V by D2,XBOOLE_0:def 2;
       thus q in W by CONNSP_2:def 1;                         
       Int Q c= [#]X & not [#]X in [#]X;
       then not [#]X in Int Q;
       then 
D8:       Int Q misses {[#]X} by ZFMISC_1:56;
       Int Q c= Q by TOPS_1:44;
       then Int Q misses Q` by SUBSET_1:44;
       hence V misses W by D8,XBOOLE_1:70;
   end;    
   suppose p in {[#]X} & q in {[#]X};
    then p = [#]X & q = [#]X by TARSKI:def 1;
    hence thesis by B1;
   end;
  end;
 assume 
H1:  One-Point_Compactification(X) is Hausdorff;
S1:  X is SubSpace of One-Point_Compactification(X) by Th6;
 hence X is Hausdorff by H1,TOPMETR:3;
 let x be Point of X;
 x in [#]X;
 then reconsider p = x as Point of One-Point_Compactification(X) by X1;
 [#]X in {[#]X} by TARSKI:def 1;
 then reconsider q = [#]X as Point of One-Point_Compactification(X)
   by B0,XBOOLE_0:def 2;
  not [#]X in [#]X;
 then p <> q;
 then consider V, U being Subset of One-Point_Compactification(X) such that
L3:   V is open and
L4:   U is open and
L5:   p in V and 
L6:   q in U and
L7:   V misses U by H1,COMPTS_1:def 4;
U1:   U in the topology of One-Point_Compactification(X) by L4,PRE_TOPC:def 5;
   now assume U in the topology of X;
    then q in [#]X by L6;
    hence contradiction;
   end;
   then U in D by A0,U1,XBOOLE_0:def 2;
   then consider W being Subset of X such that
W1:   U = W \/ {[#]X} and W is open and
W3:   W` is compact;
T2:   V c= U` by L7,SUBSET_1:43;
X3:   [#]X in {[#]X} by TARSKI:def 1;
   then 
U2:   [#]X in U by W1,XBOOLE_0:def 2;
X2:   not [#]X in [#]X;
W5:   [#]X \ U = ([#]X \ W) /\ ([#]X \ {[#]X}) by W1,XBOOLE_1:53
              .= ([#]X \ W) /\ [#]X by X2,ZFMISC_1:65
              .= [#]X \ W by XBOOLE_1:28; 
U3:   [#]One-Point_Compactification(X) \ U = ([#]X \ U) \/ ({[#]X} \ U)
                     by B0,XBOOLE_1:42
                 .= ([#]X \ W ) \/ {} by U2,W5,ZFMISC_1:68
                 .= W`;
    then
V4:   V c= [#]X by T2,XBOOLE_1:1;
V5:   now assume V in D;
       then ex S being Subset of X st V = S \/ {[#]X} & 
         S is open & S` is compact;
        then [#]X in V by X3,XBOOLE_0:def 2;  
        then [#]X in [#]X by V4;
        hence contradiction;
      end;
   V in the topology of One-Point_Compactification(X) by L3,PRE_TOPC:def 5;
   then V in the topology of X by A0,V5,XBOOLE_0:def 2;
   then reconsider Vx = V as open Subset of X by PRE_TOPC:def 5;
   set K = Cl Vx;
U4:   U` /\ [#]X = W` by U3,XBOOLE_1:28;
   U` is closed by L4,TOPS_1:30;
   then W` is closed by S1,U4,PRE_TOPC:43;
   then
V8:  K c= W` by T2,U3,TOPS_1:31;
V9:  x in Int Vx by L5,TOPS_1:55;
   Vx c= K by PRE_TOPC:48;
   then Int Vx c= Int K  by TOPS_1:48;
   then reconsider K as a_neighborhood of x by V9,CONNSP_2:def 1;
   take K;
   thus K is compact by V8,W3,COMPTS_1:18;
end;  

theorem Th5:
  for X being non empty TopSpace 
   holds X is non compact iff
    ex X' being Subset of One-Point_Compactification(X)
       st X' = [#]X & X' is dense
proof let X be non empty TopSpace;
 set D = {U \/ {[#]X} where U is Subset of X: U is open & U` is compact};
D1:  the topology of One-Point_Compactification(X) = (the topology of X) \/ D
      by Def2;
E1:  [#]X in {[#]X} by TARSKI:def 1;
E4:  [#]One-Point_Compactification(X) = succ([#]X) by Def2;
 thus X is non compact implies
    ex X' being Subset of One-Point_Compactification(X)
      st X' = [#]X & X' is dense
  proof assume X is non compact;
   then
A3:  [#]X is non compact by COMPTS_1:10;
   [#]X c= [#]One-Point_Compactification(X) by Th2;
   then reconsider X' = [#]X as Subset of One-Point_Compactification(X);
  take X';
  thus X' = [#]X;
  thus Cl(X') c= [#]One-Point_Compactification(X);
E2:  [#]X c= Cl(X') by PRE_TOPC:48;
  then 
A7:  [#]One-Point_Compactification(X) c= Cl(X') \/ {[#]X} by E4,XBOOLE_1:9;
  now assume 
B1:  not [#]X in Cl(X');
   reconsider Xe = [#]X as Element of One-Point_Compactification(X)
     by E1,E4,XBOOLE_0:def 2;
   reconsider XX = {Xe} as Subset of One-Point_Compactification(X);
    [#]One-Point_Compactification(X) \ Cl(X') 
       = ([#]X \ Cl(X')) \/ ({[#]X} \ Cl(X')) by E4,XBOOLE_1:42
       .= {} \/ ({[#]X} \ Cl(X')) by E2,XBOOLE_1:37
       .= XX by B1,ZFMISC_1:67;   
   then 
B3:   XX is open by PRE_TOPC:def 6;
B4:   now assume XX in the topology of X;
       then [#]X in [#]X by E1;
       hence contradiction;
      end;
   XX in the topology of One-Point_Compactification(X) by B3,PRE_TOPC:def 5;
   then XX in D by D1,B4,XBOOLE_0:def 2;
   then consider U being Subset of X such that
C1:     XX = U \/ {[#]X} and U is open and
C2:     U` is compact;
   now assume U = XX; 
     then [#]X in [#]X by E1;
     hence contradiction;  
   end;
   then U = {}X by C1,ZFMISC_1:43;
   hence contradiction by C2,A3;
  end; 
  hence [#]One-Point_Compactification(X) c= Cl(X') by A7,ZFMISC_1:46;
 end;
 given X' being Subset of One-Point_Compactification(X) such that 
A1:  X' = [#]X and
A2:  X' is dense;
D2: Cl X'= [#]One-Point_Compactification(X) by A2,TOPS_1:def 3;
 assume X is compact;
  then [#]X is compact by COMPTS_1:10;
  then ({}X)` is compact;
  then {}X \/ {[#]X} in D; 
  then 
A4:  {[#]X} in the topology of One-Point_Compactification(X)
   by D1,XBOOLE_0:def 2;
  then reconsider X1 = {[#]X} as Subset of One-Point_Compactification(X);
  X1 is open by A4,PRE_TOPC:def 5;
  then X1` is closed by TOPS_1:30;
  then 
A5:  (Cl X1`) = X1` by PRE_TOPC:52;
A7:   not [#]X in [#]X; 
A6:   X1` = [#]X \ X1 by E4,XBOOLE_1:40
         .= [#]X by A7,ZFMISC_1:65;
  [#]X in [#]One-Point_Compactification(X) by E1,E4,XBOOLE_0:def 2;
  hence contradiction by A1,A5,A6,D2; 
end;

theorem
  for X being non empty TopSpace st X is non compact 
   holds incl(X,One-Point_Compactification X) is compactification
proof let X be non empty TopSpace;
  assume
A1:  X is non compact;
  set h = incl(X,One-Point_Compactification X);
A2:  [#]X c= [#](One-Point_Compactification X) by Th2;
  then reconsider Xy = [#]X as Subset of One-Point_Compactification(X); 
  set D = {U \/ {[#]X} where U is Subset of X: U is open & U` is compact};
A3:  the topology of One-Point_Compactification(X) = (the topology of X) \/ D
      by Def2;
A4:  [#]((One-Point_Compactification X) | Xy) = Xy by PRE_TOPC:def 10;
  the topology of (One-Point_Compactification X) | Xy = the topology of X
   proof
    thus the topology of (One-Point_Compactification X) |Xy
           c= the topology of X
     proof let x be set;
      assume 
B1:      x in the topology of (One-Point_Compactification X) | Xy;
      then reconsider P = x as Subset of (One-Point_Compactification X) | Xy;
      consider Q being Subset of One-Point_Compactification(X) such that
B2:     Q in the topology of One-Point_Compactification(X) and
B3:     P = Q /\ [#]((One-Point_Compactification X) | Xy) by B1,PRE_TOPC:def 9;
      per cases by B2,A3,XBOOLE_0:def 2;
      suppose Q in the topology of X;
       hence x in the topology of X by B3,A4,XBOOLE_1:28;
      end;
      suppose Q in D; 
       then consider U being Subset of X such that
C1:       Q = U \/ {[#]X} and
C2:       U is open and U` is compact;
       not [#]X in [#]X;
       then {[#]X} misses [#]X by ZFMISC_1:56;
       then {[#]X} /\ [#]X = {} by XBOOLE_0:def 7;
       then P = (U /\ [#]X) \/ {} by B3,C1,A4,XBOOLE_1:23
              .= U by XBOOLE_1:28;
       hence x in the topology of X by C2,PRE_TOPC:def 5;
      end;
     end;
    let x be set;
    assume 
D1:     x in the topology of X;
    then reconsider P = x as Subset of (One-Point_Compactification X) | Xy
      by A4;
    reconsider Q = P as Subset of One-Point_Compactification(X)
     by A4,XBOOLE_1:1;
D3:    Q in the topology of One-Point_Compactification(X)
         by D1,A3,XBOOLE_0:def 2;
    P = Q /\ [#]((One-Point_Compactification X) | Xy) by XBOOLE_1:28;
    hence x in the topology of (One-Point_Compactification X) | Xy
      by D3,PRE_TOPC:def 9;
   end;   
  hence h is embedding by A2,Th3;
  thus One-Point_Compactification(X) is compact;
  ex X' being Subset of One-Point_Compactification(X)
   st X' = [#]X & X' is dense by A1,Th5;
  hence thesis by A2,Th4; 
end;
