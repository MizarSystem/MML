:: Measurability of Extended Real Valued Functions
::  by Noboru Endou , Katsumi Wasaki and Yasunari Shidama
::
:: Received October 6, 2000
:: Copyright (c) 2000 Association of Mizar Users

environ

 vocabularies NUMBERS, XBOOLE_0, SUBSET_1, PARTFUN1, PROB_1, FUNCT_1, RAT_1,
      REAL_1, NAT_1, VALUED_0, RELAT_1, COMPLEX1, ARYTM_3, XXREAL_0, TARSKI,
      VALUED_1, MEASURE6, ARYTM_1, MESFUNC1, XREAL_0, ORDINAL1, SUPINF_2,
      RFUNCT_3, CARD_1, FUNCT_3, PROB_2, FINSEQ_1, FUNCOP_1, MEASURE1,
      SUPINF_1, ORDINAL4, MESFUNC2;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XREAL_0, VALUED_0,
      REAL_1, RELAT_1, FUNCT_1, RELSET_1, PARTFUN1, FUNCT_2, NAT_1, WELLORD2,
      RAT_1, FINSEQ_1, PROB_1, XXREAL_0, SUPINF_1, FUNCOP_1, SUPINF_2, FUNCT_3,
      PROB_2, MEASURE1, MEASURE2, MEASURE3, MEASURE6, EXTREAL1, MESFUNC1,
      EXTREAL2;
 constructors PARTFUN1, WELLORD2, FUNCT_3, FUNCOP_1, REAL_1, NAT_1, RAT_1,
      FINSEQ_1, PROB_2, MEASURE3, MEASURE6, EXTREAL1, MESFUNC1, EXTREAL2,
      SUPINF_1, RELSET_1, BINOP_2;
 registrations XBOOLE_0, ORDINAL1, RELSET_1, NUMBERS, XXREAL_0, XREAL_0, RAT_1,
      MEMBERED, FINSEQ_1, MEASURE1, VALUED_0, FUNCT_2, CARD_1, XXREAL_3,
      BINOP_2;
 requirements NUMERALS, REAL, BOOLE, SUBSET;
 definitions TARSKI, XBOOLE_0, MEASURE6, SUPINF_2, VALUED_0, XXREAL_3;
 theorems MEASURE1, TARSKI, SUBSET_1, PARTFUN1, FUNCT_1, FUNCT_2, NAT_1,
      SUPINF_2, WELLORD2, RAT_1, RELSET_1, EXTREAL1, EXTREAL2, MESFUNC1,
      ZFMISC_1, FINSEQ_1, FINSEQ_3, FINSEQ_5, XREAL_0, PROB_2, FUNCT_3,
      XBOOLE_0, XBOOLE_1, RELAT_1, FUNCOP_1, XREAL_1, FINSUB_1, PROB_1,
      XXREAL_0, ORDINAL1, VALUED_0, XXREAL_3;
 schemes FUNCT_2, SEQ_1, NAT_1;

begin  :: Finite Valued Function ::

reserve X for non empty set;
reserve e for set;
reserve x for Element of X;
reserve f,g for PartFunc of X,ExtREAL;
reserve S for SigmaField of X;
reserve F for Function of RAT,S;
reserve p,q for Rational;
reserve r for Real;
reserve n,m for Nat;
reserve A,B for Element of S;

definition
  let X, f;
  redefine attr f is real-valued means
  :Def1:
  for x st x in dom f holds |. f.x .| < +infty;
  compatibility
  proof
    thus
    f is real-valued implies for x st x in dom f holds |. f.x .| < +infty
    proof
      assume
A1:   f is real-valued;
      let x;
      assume x in dom f;
then A3:   f.x in rng f by FUNCT_1:12;
   rng f c= REAL by A1,VALUED_0:def 3;
      hence thesis by A3,EXTREAL2:102;
    end;
    assume
A5: for x st x in dom f holds |. f.x .| < +infty;
    let e;
    assume
A6: e in dom f;
    then reconsider x = e as Element of X;
 |. f.x .| < +infty by A5,A6;
then  f.x in REAL by EXTREAL2:102;
    hence thesis;
  end;
end;

theorem
  f = 1(#)f
proof
A1: dom f = dom (1(#)f) by MESFUNC1:def 6;
 for x st x in dom (1(#)f) holds f.x = (1(#)f).x
  proof
    let x;
    assume x in dom(1(#)f);
then  (1(#)f).x = (R_EAL 1) * f.x by MESFUNC1:def 6;
    hence thesis by XXREAL_3:92;
  end;
  hence thesis by A1,PARTFUN1:34;
end;

theorem Th2:
  f is real-valued or g is real-valued implies
  dom (f+g) = dom f /\ dom g & dom (f-g) = dom f /\ dom g
proof
  assume
A1: f is real-valued or g is real-valued;
 now per cases by A1;
    suppose
A3:   f is real-valued;
then    not +infty in rng f;
then A5:   f"{+infty} = {} by FUNCT_1:142;
   not -infty in rng f by A3;
then A7:   f"{-infty} = {} by FUNCT_1:142;
then
A8:   (f"{+infty} /\ g"{-infty}) \/ (f"{-infty} /\ g"{+infty}) = {} by A5;
A9:   (f"{+infty} /\ g"{+infty}) \/ (f"{-infty} /\ g"{-infty}) = {} by A5,A7;
  dom (f+g) = (dom f /\ dom g)\{} by A8,MESFUNC1:def 3;
      hence thesis by A9,MESFUNC1:def 4;
    end;
    suppose
A11:  g is real-valued;
then   not +infty in rng g;
then A13:  g"{+infty} = {} by FUNCT_1:142;
  not -infty in rng g by A11;
then A15:  g"{-infty} = {} by FUNCT_1:142;
then
A16:  (f"{+infty} /\ g"{-infty}) \/ (f"{-infty} /\ g"{+infty}) = {} by A13;
A17:  (f"{+infty} /\ g"{+infty}) \/ (f"{-infty} /\ g"{-infty}) = {} by A13,A15;
  dom (f+g) = (dom f /\ dom g)\{} by A16,MESFUNC1:def 3;
      hence thesis by A17,MESFUNC1:def 4;
    end;
  end;
  hence thesis;
end;

theorem Th3:
  for f,g,F,r,A st f is real-valued & g is real-valued &
  (for p holds F.p = (A /\ less_dom(f,R_EAL p)) /\ (A /\
  less_dom(g,R_EAL (r-p)))) holds A /\ less_dom(f+g,R_EAL r) = union (rng F)
proof
  let f,g,F,r,A;
  assume that
A1: f is real-valued and
A2: g is real-valued and
A3: for p holds F.p = (A /\ less_dom(f,R_EAL p)) /\ (A /\
  less_dom(g,R_EAL (r-p)));
A4: dom(f+g) = dom f /\ dom g by A1,Th2;
A5: A /\ less_dom(f+g,R_EAL r) c= union (rng F)
  proof
    let x be set;
    assume
A6: x in A /\ less_dom(f+g,R_EAL r);
then A7: x in A by XBOOLE_0:def 4;
A8: x in less_dom(f+g,R_EAL r) by A6,XBOOLE_0:def 4;
then A9: x in dom(f+g) by MESFUNC1:def 12;
A10: (f+g).x < R_EAL r by A8,MESFUNC1:def 12;
    reconsider x as Element of X by A6;
A11: f.x + g.x < R_EAL r by A9,A10,MESFUNC1:def 3;
A12: x in dom f by A4,A9,XBOOLE_0:def 4;
A13: x in dom g by A4,A9,XBOOLE_0:def 4;
A14: |. f.x .| < +infty by A1,A12,Def1;
A15: |. g.x .| < +infty by A2,A13,Def1;
A16: -(+infty) < f.x by A14,EXTREAL2:58;
A17: f.x < +infty by A14,EXTREAL2:58;
A18: -(+infty) < g.x by A15,EXTREAL2:58;
A19: g.x < +infty by A15,EXTREAL2:58;
then A20: f.x < R_EAL r - g.x by A11,A17,XXREAL_3:63;
A21: -infty < f.x by A16,XXREAL_3:23;
A22: -infty < g.x by A18,XXREAL_3:23;
    reconsider f1 = f.x as Real by A17,A21,XXREAL_0:14;
    reconsider g1 = g.x as Real by A19,A22,XXREAL_0:14;
 R_EAL r - g.x = r - g1 by SUPINF_2:5;
    then consider p such that
A24: f1 < p and
A25: p < r - g1 by A20,RAT_1:22;
A26: not r - p <= g1 by A25,XREAL_1:14;
A27: x in less_dom(f,R_EAL p) by A12,A24,MESFUNC1:def 12;
A28: x in less_dom(g,R_EAL(r-p)) by A13,A26,MESFUNC1:def 12;
A29: x in A /\ less_dom(f,R_EAL p) by A7,A27,XBOOLE_0:def 4;
 x in A /\ less_dom(g,R_EAL(r-p)) by A7,A28,XBOOLE_0:def 4;
then A31: x in (A /\ less_dom(f,R_EAL p))/\(A /\
    less_dom(g,R_EAL(r-p))) by A29,XBOOLE_0:def 4;
 p in RAT by RAT_1:def 2;
then A33: p in dom F by FUNCT_2:def 1;
A34: x in F.p by A3,A31;
 F.p in rng F by A33,FUNCT_1:def 5;
    hence thesis by A34,TARSKI:def 4;
  end;
 union (rng F) c= A /\ less_dom(f+g,R_EAL r)
  proof
    let x be set;
    assume x in union (rng F);
    then consider Y being set such that
A38: x in Y and
A39: Y in rng F by TARSKI:def 4;
    consider p being set such that
A40: p in dom F and
A41: Y = F.p by A39,FUNCT_1:def 5;
    reconsider p as Rational by A40;
A42: x in (A /\ less_dom(f,R_EAL p))/\(A /\ less_dom(g,R_EAL (r-p)))
    by A3,A38,A41;
then A43: x in A /\ less_dom(f,R_EAL p) by XBOOLE_0:def 4;
A44: x in A /\ less_dom(g,R_EAL (r-p)) by A42,XBOOLE_0:def 4;
A45: x in A by A43,XBOOLE_0:def 4;
A46: x in less_dom(f,R_EAL p) by A43,XBOOLE_0:def 4;
A47: x in less_dom(g,R_EAL (r-p)) by A44,XBOOLE_0:def 4;
A48: x in dom f by A46,MESFUNC1:def 12;
A49: x in dom g by A47,MESFUNC1:def 12;
    reconsider x as Element of X by A42;
A50: g.x < R_EAL (r-p) by A47,MESFUNC1:def 12;
A51: |.f.x.| < +infty by A1,A48,Def1;
A52: |.g.x.| < +infty by A2,A49,Def1;
A53: -(+infty) < f.x by A51,EXTREAL2:58;
A54: -(+infty) < g.x by A52,EXTREAL2:58;
A55: -infty< f.x by A53,XXREAL_3:23;
A56: f.x< +infty by A51,EXTREAL2:58;
A57: -infty< g.x by A54,XXREAL_3:23;
A58: g.x< +infty by A52,EXTREAL2:58;
    reconsider f1 = f.x as Real by A55,A56,XXREAL_0:14;
    reconsider g1 = g.x as Real by A57,A58,XXREAL_0:14;
A59: f1 < p by A46,MESFUNC1:def 12;
 p < r- g1 by A50,XREAL_1:14;
then  f1 < r - g1 by A59,XXREAL_0:2;
then A62: f1 + g1 < r by XREAL_1:22;
A63: x in dom (f+g) by A4,A48,A49,XBOOLE_0:def 4;
then  (f+g).x = f.x + g.x by MESFUNC1:def 3
      .= f1+g1 by SUPINF_2:1;
then  x in less_dom(f+g,R_EAL r) by A62,A63,MESFUNC1:def 12;
    hence thesis by A45,XBOOLE_0:def 4;
  end;
  hence thesis by A5,XBOOLE_0:def 10;
end;

begin  :: Measurability of f+g and f-g ::

theorem
  ex F being Function of NAT,RAT st F is one-to-one & dom F = NAT & rng F = RAT
proof
  consider F being Function such that
A1: F is one-to-one and
A2: dom F = NAT & rng F = RAT by MESFUNC1:5,WELLORD2:def 4;
 F is Function of NAT,RAT by A2,FUNCT_2:4;
  hence thesis by A1,A2;
end;

theorem Th5:
  for X,Y,Z be non empty set, F be Function of X,Z st X,Y are_equipotent holds
  ex G be Function of Y,Z st rng F = rng G
proof
  let X,Y,Z be non empty set;
  let F be Function of X,Z;
  assume X,Y are_equipotent;
  then consider H being Function such that
A2: H is one-to-one and
A3: dom H = Y and
A4: rng H = X by WELLORD2:def 4;
  reconsider H as Function of Y,X by A3,A4,FUNCT_2:4;
  reconsider G = F*H as Function of Y,Z;
A5: dom F = X by FUNCT_2:def 1;
A6: dom G = Y by FUNCT_2:def 1;
 for z being Element of Z holds z in rng F implies z in rng G
  proof
    let z be Element of Z;
    assume z in rng F;
    then consider x be set such that
A9: x in dom F and
A10: z = F.x by FUNCT_1:def 5;
 x in rng H by A4,A9;
then  x in dom (H") by A2,FUNCT_1:54;
then  (H").x in rng (H") by FUNCT_1:def 5;
then A14: (H").x in dom G by A2,A3,A6,FUNCT_1:55;
then  G.((H").x) in rng G by FUNCT_1:def 5;
then  F.(H.((H").x)) in rng G by A14,FUNCT_1:22;
    hence thesis by A2,A4,A9,A10,FUNCT_1:57;
  end;
then A17: rng F c= rng G by SUBSET_1:7;
 for z being Element of Z holds z in rng G implies z in rng F
  proof
    let z be Element of Z;
    assume z in rng G;
    then consider y be set such that
A20: y in dom G and
A21: z = G.y by FUNCT_1:def 5;
 y in rng (H") by A2,A3,A6,A20,FUNCT_1:55;
    then consider x be set such that
A23: x in dom (H") and
A24: y = (H").x by FUNCT_1:def 5;
A25: x in rng H by A2,A23,FUNCT_1:55;
then A26: F.x in rng F by A5,FUNCT_1:def 5;
 x = H.y by A2,A24,A25,FUNCT_1:54;
    hence thesis by A20,A21,A26,FUNCT_1:22;
  end;
then  rng G c= rng F by SUBSET_1:7;
then  rng F = rng G by A17,XBOOLE_0:def 10;
  hence thesis;
end;

theorem Th6:
  for S,f,g,A st f is_measurable_on A & g is_measurable_on A holds
  ex F being Function of RAT,S st for p being Rational holds
  F.p = (A /\ less_dom(f,R_EAL p)) /\ (A /\ less_dom(g,R_EAL (r-p)))
proof
  let S,f,g,A;
  assume
A1: f is_measurable_on A & g is_measurable_on A;
  defpred P[set,set] means ex p st p = $1 &
  $2 = (A /\ less_dom(f,R_EAL p)) /\ (A /\ less_dom(g,R_EAL (r-p)));
A2: for x1 being set st x1 in RAT ex y1 being set st y1 in S & P[x1,y1]
  proof
    let x1 be set;
    assume x1 in RAT;
    then consider p such that
A4: p = x1;
    A5: A
 /\ less_dom(f,R_EAL p) in S & A /\ less_dom(g,R_EAL (r-p)) in S by A1,
    MESFUNC1:def 17;
    take (A /\ less_dom(f,R_EAL p)) /\ (A /\ less_dom(g,R_EAL (r-p)));
    thus thesis by A4,A5,FINSUB_1:def 2;
  end;
  consider G being Function of RAT,S such that
A6: for x1 being set st x1 in RAT holds P[x1,G.x1] from FUNCT_2:sch 1(A2);
A7: for p being Rational holds
  G.p = (A /\ less_dom(f,R_EAL p)) /\ (A /\ less_dom(g,R_EAL (r-p)))
  proof
    let p be Rational;
 p in RAT by RAT_1:def 2;
then  ex q st q = p & G.p = (A /\ less_dom(f,R_EAL q)) /\ (A /\
    less_dom(g,R_EAL (r-q))) by A6;
    hence thesis;
  end;
  take G;
  thus thesis by A7;
end;

theorem Th7:
  for f,g,A st f is real-valued & g is real-valued & f is_measurable_on A &
  g is_measurable_on A holds f+g is_measurable_on A
proof
  let f,g,A;
  assume that
A1: f is real-valued & g is real-valued and
A2: f is_measurable_on A & g is_measurable_on A;
 for r be real number holds A /\ less_dom(f+g,R_EAL r) in S
  proof
    let r be real number;
    reconsider r as Real by XREAL_0:def 1;
    consider F being Function of RAT,S such that
A4: for p being Rational holds
    F.p = (A /\ less_dom(f,R_EAL p)) /\ (A /\ less_dom(g,R_EAL (r-p)))
    by A2,Th6;
    consider G being Function of NAT,S such that
A5: rng F = rng G by Th5,MESFUNC1:5;
 A /\ less_dom(f+g,R_EAL r) = union (rng G) by A1,A4,A5,Th3;
    hence thesis;
  end;
  hence thesis by MESFUNC1:def 17;
end;

canceled;

theorem Th9:
  for C being non empty set, f1,f2 being PartFunc of C,ExtREAL holds
  f1 - f2 = f1 + (-f2)
proof
  let C be non empty set;
  let f1,f2 be PartFunc of C,ExtREAL;
A1: dom (f1-f2)
  =(dom f1 /\ dom f2)\((f1"{+infty}/\f2"{+infty}) \/ (f1"{-infty}/\
  f2"{-infty})) by MESFUNC1:def 4;
   for x being Element of C st x in f2"{+infty} holds x in (-f2)"{-infty}
  proof
    let x be Element of C;
    assume
A3: x in f2"{+infty};
then A4: x in dom f2 by FUNCT_1:def 13;
A5: f2.x in {+infty} by A3,FUNCT_1:def 13;
A6: x in dom(-f2) by A4,MESFUNC1:def 7;
 f2.x = +infty by A5,TARSKI:def 1;
then  (-f2).x = -(+infty) by A6,MESFUNC1:def 7
      .= -infty by XXREAL_3:def 3;
then  (-f2).x in {-infty} by TARSKI:def 1;
    hence thesis by A6,FUNCT_1:def 13;
  end;
then A10: f2"{+infty} c= (-f2)"{-infty} by SUBSET_1:7;
   for x being Element of C st x in (-f2)"{-infty} holds x in f2"{+infty}
  proof
    let x be Element of C;
    assume
A12: x in (-f2)"{-infty};
then A13: x in dom(-f2) by FUNCT_1:def 13;
A14: (-f2).x in {-infty} by A12,FUNCT_1:def 13;
A15: x in dom f2 by A13,MESFUNC1:def 7;
 (-f2).x = -infty by A14,TARSKI:def 1;
then  -infty = -(f2.x) by A13,MESFUNC1:def 7;
then  f2.x in {+infty} by TARSKI:def 1,XXREAL_3:5;
    hence thesis by A15,FUNCT_1:def 13;
  end;
then  (-f2)"{-infty} c= f2"{+infty} by SUBSET_1:7;
then A20: f2"{+infty} = (-f2)"{-infty} by A10,XBOOLE_0:def 10;
   for x being Element of C st x in f2"{-infty} holds x in (-f2)"{+infty}
  proof
    let x be Element of C;
    assume
A22: x in f2"{-infty};
then A23: x in dom f2 by FUNCT_1:def 13;
A24: f2.x in {-infty} by A22,FUNCT_1:def 13;
A25: x in dom(-f2) by A23,MESFUNC1:def 7;
 f2.x = -infty by A24,TARSKI:def 1;
then  (-f2).x = +infty by A25,MESFUNC1:def 7,XXREAL_3:5;
then  (-f2).x in {+infty} by TARSKI:def 1;
    hence thesis by A25,FUNCT_1:def 13;
  end;
then A29: f2"{-infty} c= (-f2)"{+infty} by SUBSET_1:7;
   for x being Element of C st x in (-f2)"{+infty} holds x in f2"{-infty}
  proof
    let x be Element of C;
    assume
A31: x in (-f2)"{+infty};
then A32: x in dom(-f2) by FUNCT_1:def 13;
A33: (-f2).x in {+infty} by A31,FUNCT_1:def 13;
A34: x in dom f2 by A32,MESFUNC1:def 7;
 (-f2).x = +infty by A33,TARSKI:def 1;
then  +infty = -(f2.x) by A32,MESFUNC1:def 7;
then  f2.x = -(+infty)
      .= -infty by XXREAL_3:def 3;
then  f2.x in {-infty} by TARSKI:def 1;
    hence thesis by A34,FUNCT_1:def 13;
  end;
then  (-f2)"{+infty} c= f2"{-infty} by SUBSET_1:7;
then A40: f2"{-infty} = (-f2)"{+infty} by A29,XBOOLE_0:def 10;
 dom (f1+(-f2)) =(dom f1 /\ dom(-f2))\
  ((f1"{-infty}/\(-f2)"{+infty}) \/ (f1"{+infty}/\(-f2)"{-infty}))
  by MESFUNC1:def 3
    .=(dom f1 /\ dom f2)\
  ((f1"{-infty}/\f2"{-infty}) \/ (f1"{+infty}/\f2"{+infty}))
  by A20,A40,MESFUNC1:def 7;
then A42: dom(f1-f2)=dom(f1+(-f2)) by MESFUNC1:def 4;
   for
 x being Element of C st x in dom(f1-f2) holds (f1-f2).x = (f1+(-f2)).x
  proof
    let x be Element of C;
    assume
A44: x in dom(f1-f2);
 dom(f1-f2) c= dom f1 /\ dom f2 by A1,XBOOLE_1:36;
then  x in dom f2 by A44,XBOOLE_0:def 4;
then A47: x in dom (-f2) by MESFUNC1:def 7;
     (f1-f2).x = f1.x - f2.x & (f1+(-f2)).x = f1.x + (-f2).x by A42,A44,
    MESFUNC1:def 3,def 4;
    hence thesis by A47,MESFUNC1:def 7;
  end;
  hence thesis by A42,PARTFUN1:34;
end;

canceled;

theorem Th11:
  for C being non empty set, f being PartFunc of C,ExtREAL holds -f = (-1)(#)f
proof
  let C be non empty set;
  let f be PartFunc of C,ExtREAL;
A1: dom (-f) = dom f by MESFUNC1:def 7;
A2: dom ((-1)(#)f) = dom f by MESFUNC1:def 6;
 for x being Element of C st x in dom f holds (-f).x = ((-1)(#)f).x
  proof
    let x be Element of C;
    assume
A4: x in dom f;
then  ((-1)(#) f).x=(R_EAL -1)*(f.x) by A2,MESFUNC1:def 6;
then  ((-1)(#)f).x =(-(R_EAL 1))*(f.x) by SUPINF_2:3
      .= -(R_EAL 1)*(f.x) by XXREAL_3:103
      .= -(f.x) by XXREAL_3:92;
    hence thesis by A1,A4,MESFUNC1:def 7;
  end;
  hence thesis by A1,A2,PARTFUN1:34;
end;

theorem Th12:
  for C being non empty set, f being PartFunc of C,ExtREAL, r be Real
  st f is real-valued holds r(#)f is real-valued
proof
  let C be non empty set;
  let f be PartFunc of C,ExtREAL;
  let r be Real;
  assume
A1: f is real-valued;
   for x being Element of C st x in dom(r(#)f) holds |.(r(#)f).x .| < +infty
  proof
    let x be Element of C;
    assume
A3: x in dom(r(#)f);
then  x in dom f by MESFUNC1:def 6;
then A5: |. f.x .| < +infty by A1,Def1;
then  -(+infty) < f.x by EXTREAL2:58;
then A7: -infty < f.x by XXREAL_3:def 3;
 f.x < +infty by A5,EXTREAL2:58;
    then reconsider y = f.x as Real by A7,XXREAL_0:14;
A9: -infty < R_EAL(r*y) by XXREAL_0:12;
A10: R_EAL(r*y) < +infty by XXREAL_0:9;
A11: -infty < R_EAL r * R_EAL y by A9,EXTREAL1:38;
A12: R_EAL r * R_EAL y = (r(#)f).x by A3,MESFUNC1:def 6;
then A13: -(+infty) < (r(#)f).x by A11,XXREAL_3:def 3;
 (r(#)f).x < +infty by A10,A12,EXTREAL1:38;
    hence thesis by A13,EXTREAL2:59;
  end;
  hence thesis by Def1;
end;

theorem
  for f,g,A st f is real-valued & g is real-valued & f is_measurable_on A &
  g is_measurable_on A & A c= dom g holds f-g is_measurable_on A
proof
  let f,g,A;
  assume that
A1: f is real-valued and
A2: g is real-valued and
A3: f is_measurable_on A and
A4: g is_measurable_on A & A c= dom g;
A5: (-1)(#)g is real-valued by A2,Th12;
A6: (-1)(#)g is_measurable_on A by A4,MESFUNC1:41;
A7: -g is real-valued by A5,Th11;
 -g is_measurable_on A by A6,Th11;
then  f+(-g) is_measurable_on A by A1,A3,A7,Th7;
  hence thesis by Th9;
end;

begin  ::definitions of extended real valued functions max+(f) and max-(f) ::
       :: and their basic properties                                       ::

definition
  let C be non empty set, f be PartFunc of C,ExtREAL;
  deffunc F(Element of C) = max(f.$1,0.);
  func max+(f) -> PartFunc of C,ExtREAL means
  :Def2:
  dom it = dom f &
  for x be Element of C st x in dom it holds it.x = max(f.x,0.);
  existence
  proof
    defpred P[Element of C] means $1 in dom f;
    consider F be PartFunc of C,ExtREAL such that
A1: for c being Element of C holds c in dom F iff P[c] and
A2: for c being Element of C st c in dom F holds F.c = F(c) from SEQ_1:sch 3;
    take F;
    thus dom F=dom f
    proof
      thus dom F c= dom f
      proof
        let x be set;
        assume x in dom F;
        hence thesis by A1;
      end;
      let x be set;
      assume x in dom f;
      hence thesis by A1;
    end;
    let c be Element of C;
    assume c in dom F;
    hence thesis by A2;
  end;
  uniqueness
  proof
    set X = dom f;
    thus for F,G being PartFunc of C,ExtREAL st
    (dom F=X & for c be Element of C st c in dom F holds F.c = F(c)) &
    (dom G=X & for c be Element of C st c in dom G holds G.c = F(c))
    holds F = G from SEQ_1:sch 4;
  end;
  deffunc F(Element of C) = max(-(f.$1),0.);
  func max-(f) -> PartFunc of C,ExtREAL means
  :Def3:
  dom it = dom f &
  for x be Element of C st x in dom it holds it.x = max(-(f.x),0.);
  existence
  proof
    defpred P[Element of C] means $1 in dom f;
    consider F be PartFunc of C,ExtREAL such that
A6: for c being Element of C holds c in dom F iff P[c] and
A7: for c being Element of C st c in dom F holds F.c = F(c) from SEQ_1:sch 3;
    take F;
    thus dom F=dom f
    proof
      thus dom F c= dom f
      proof
        let x be set;
        assume x in dom F;
        hence thesis by A6;
      end;
      let x be set;
      assume x in dom f;
      hence thesis by A6;
    end;
    let c be Element of C;
    assume c in dom F;
    hence thesis by A7;
  end;
  uniqueness
  proof
    set X = dom f;
    thus for F,G being PartFunc of C,ExtREAL st
    (dom F=X & for c be Element of C st c in dom F holds F.c = F(c)) &
    (dom G=X & for c be Element of C st c in dom G holds G.c = F(c))
    holds F = G from SEQ_1:sch 4;
  end;
end;

theorem Th14:
  for C being non empty set, f being PartFunc of C,ExtREAL,
  x being Element of C holds 0. <= (max+(f)).x
proof
  let C be non empty set;
  let f be PartFunc of C,ExtREAL;
  let x be Element of C;
A1: dom max+f = dom f by Def2;
  per cases;
  suppose
 x in dom f;
then  (max+(f).x) = max(f.x,0.) by A1,Def2;
    hence thesis by XXREAL_0:25;
  end;
  suppose
 not x in dom f;
    hence thesis by A1,FUNCT_1:def 4;
  end;
end;

theorem Th15:
  for C being non empty set, f being PartFunc of C,ExtREAL,
  x being Element of C holds 0. <= (max-(f)).x
proof
  let C be non empty set;
  let f be PartFunc of C,ExtREAL;
  let x be Element of C;
A1: dom max-f = dom f by Def3;
  per cases;
  suppose
 x in dom f;
then  (max-(f).x) = max(-f.x,0.) by A1,Def3;
    hence thesis by XXREAL_0:25;
  end;
  suppose
 not x in dom f;
    hence thesis by A1,FUNCT_1:def 4;
  end;
end;

theorem
  for C being non empty set, f being PartFunc of C,ExtREAL holds
  max-(f) = max+(-f)
proof
  let C be non empty set;
  let f be PartFunc of C,ExtREAL;
A1: dom(max-(f)) = dom f by Def3
    .= dom (-f) by MESFUNC1:def 7;
then A2: dom(max-(f)) = dom(max+(-f)) by Def2;
   for
 x being Element of C st x in dom(max-(f)) holds max-(f).x = max+(-f).x
  proof
    let x be Element of C;
    assume
A4: x in dom (max-(f));
    then  max-
(f).x = max(-(f.x),0.) & -(f.x) = (-f).x by A1,Def3,MESFUNC1:def 7;
    hence thesis by A2,A4,Def2;
  end;
  hence thesis by A2,PARTFUN1:34;
end;

theorem Th17:
for C being non empty set, f being PartFunc of C,ExtREAL, x being Element of C
  st 0. < max+(f).x holds max-(f).x = 0.
proof
  let C be non empty set;
  let f be PartFunc of C,ExtREAL;
  let x be Element of C;
A1: dom max+f = dom f by Def2;
  per cases;
  suppose
A2: x in dom f;
    assume
A3: 0. < max+(f).x;
A4: x in dom(max+(f)) by A2,Def2;
A5: x in dom(max-(f)) by A2,Def3;
 max+(f).x = max(f.x,0.) by A4,Def2;
then  not (f.x <= 0. & 0. <= 0.) by A3,XXREAL_0:28;
then  max(-(f.x),0.) = 0. by XXREAL_0:def 10;
    hence thesis by A5,Def3;
  end;
  suppose
 not x in dom f;
    hence thesis by A1,FUNCT_1:def 4;
  end;
end;

theorem
  for C being non empty set, f being PartFunc of C,ExtREAL,
  x being Element of C st 0. < max-(f).x holds max+(f).x = 0.
proof
  let C be non empty set;
  let f be PartFunc of C,ExtREAL;
  let x be Element of C;
A1: dom max-f = dom f by Def3;
  per cases;
  suppose
A2: x in dom f;
    assume
A3: 0. < max-(f).x;
A4: x in dom(max-(f)) by A2,Def3;
A5: x in dom(max+(f)) by A2,Def2;
 max-(f).x = max(-(f.x),0.) by A4,Def3;
then  -(-(f.x)) < -0. by A3,XXREAL_0:28;
then  max(f.x,0.) = 0. by XXREAL_0:def 10;
    hence thesis by A5,Def2;
  end;
  suppose
 not x in dom f;
    hence thesis by A1,FUNCT_1:def 4;
  end;
end;

theorem Th19:
  for C being non empty set, f being PartFunc of C,ExtREAL holds
  dom f = dom (max+(f)-max-(f)) & dom f = dom (max+(f)+max-(f))
proof
  let C be non empty set;
  let f be PartFunc of C,ExtREAL;
A1: dom (max+(f)) = dom f & dom (max-(f)) = dom f by Def2,Def3;
 (max+(f))"{+infty} misses (max-(f))"{+infty}
  proof
    assume not (max+(f))"{+infty} misses (max-(f))"{+infty};
    then consider x1 being set such that
A4: x1 in (max+(f))"{+infty} and
A5: x1 in (max-(f))"{+infty} by XBOOLE_0:3;
    reconsider x1 as Element of C by A4;
A6: max+(f).x1 in {+infty} by A4,FUNCT_1:def 13;
A7: max-(f).x1 in {+infty} by A5,FUNCT_1:def 13;
A8: max+(f).x1 = +infty by A6,TARSKI:def 1;
 max-(f).x1 = +infty by A7,TARSKI:def 1;
    hence contradiction by A8,Th17;
  end;
then A10: (max+(f))"{+infty} /\ (max-(f))"{+infty} = {} by XBOOLE_0:def 7;
 (max+(f))"{-infty} misses (max-(f))"{-infty}
  proof
    assume not (max+(f))"{-infty} misses (max-(f))"{-infty};
    then consider x1 being set such that
A13: x1 in (max+(f))"{-infty} and x1 in (max-(f))"{-infty} by XBOOLE_0:3;
    reconsider x1 as Element of C by A13;
 max+(f).x1 in {-infty} by A13,FUNCT_1:def 13;
then  max+(f).x1 = -infty by TARSKI:def 1;
    hence contradiction by Th14;
  end;
then A16: (max+(f))"{-infty} /\ (max-(f))"{-infty} = {} by XBOOLE_0:def 7;
 (max+(f))"{+infty} misses (max-(f))"{-infty}
  proof
    assume not (max+(f))"{+infty} misses (max-(f))"{-infty};
    then consider x1 being set such that
A19: x1 in (max+(f))"{+infty} and
A20: x1 in (max-(f))"{-infty} by XBOOLE_0:3;
    reconsider x1 as Element of C by A19;
 max-(f).x1 in {-infty} by A20,FUNCT_1:def 13;
then  max-(f).x1 = -infty by TARSKI:def 1;
    hence contradiction by Th15;
  end;
then A23: (max+(f))"{+infty} /\ (max-(f))"{-infty} = {} by XBOOLE_0:def 7;
 (max+(f))"{-infty} misses (max-(f))"{+infty}
  proof
    assume not (max+(f))"{-infty} misses (max-(f))"{+infty};
    then consider x1 being set such that
A26: x1 in (max+(f))"{-infty} and x1 in (max-(f))"{+infty} by XBOOLE_0:3;
    reconsider x1 as Element of C by A26;
 max+(f).x1 in {-infty} by A26,FUNCT_1:def 13;
then  max+(f).x1 = -infty by TARSKI:def 1;
    hence contradiction by Th14;
  end;
then A29: (max+(f))"{-infty} /\ (max-(f))"{+infty} = {} by XBOOLE_0:def 7;
 dom (max+(f)-max-(f)) = (dom f /\ dom f)\({}\/{}) by A1,A10,A16,
  MESFUNC1:def 4;
  hence thesis by A1,A23,A29,MESFUNC1:def 3;
end;

theorem Th20:
for C being non empty set, f being PartFunc of C,ExtREAL, x being Element of C
  holds
  (max+(f).x = f.x or max+(f).x = 0.) & (max-(f).x = -(f.x) or max-(f).x = 0.)
proof
  let C be non empty set;
  let f be PartFunc of C,ExtREAL;
  let x be Element of C;
A1: dom max-f = dom f & dom max+f = dom f by Def2,Def3;
  per cases;
  suppose
A2: x in dom f;
then A3: x in dom(max+(f)) by Def2;
A4: x in dom(max-(f)) by A2,Def3;
A5: max+(f).x = max(f.x,0.) by A3,Def2;
 max-(f).x = max(-(f.x),0.) by A4,Def3;
    hence thesis by A5,XXREAL_0:16;
  end;
  suppose
 not x in dom f;
    hence thesis by A1,FUNCT_1:def 4;
  end;
end;

theorem Th21:
for C being non empty set, f being PartFunc of C,ExtREAL, x being Element of C
  st max+(f).x = f.x holds max-(f).x = 0.
proof
  let C be non empty set;
  let f be PartFunc of C,ExtREAL;
  let x be Element of C;
A1: dom max-f = dom f by Def3;
  per cases;
  suppose
A2: x in dom f;
    assume
A3: max+(f).x = f.x;
A4: x in dom(max+(f)) by A2,Def2;
A5: x in dom(max-(f)) by A2,Def3;
A6: max+(f).x = max(f.x,0.) by A4,Def2;
A7: max-(f).x = max(-(f.x),0.) by A5,Def3;
 0. <= f.x by A3,A6,XXREAL_0:def 10;
    hence thesis by A7,XXREAL_0:def 10;
  end;
  suppose
 not x in dom f;
    hence thesis by A1,FUNCT_1:def 4;
  end;
end;

theorem Th22:
for C being non empty set, f being PartFunc of C,ExtREAL, x being Element of C
  st x in dom f & max+(f).x = 0. holds max-(f).x = -(f.x)
proof
  let C be non empty set;
  let f be PartFunc of C,ExtREAL;
  let x be Element of C;
  assume that
A1: x in dom f and
A2: max+(f).x = 0.;
A3: x in dom(max+(f)) by A1,Def2;
A4: x in dom(max-(f)) by A1,Def3;
A5: max+(f).x = max(f.x,0.) by A3,Def2;
A6: max-(f).x = max(-(f.x),0.) by A4,Def3;
 f.x <= 0. by A2,A5,XXREAL_0:def 10;
  hence thesis by A6,XXREAL_0:def 10;
end;

theorem
  for C being non empty set, f being PartFunc of C,ExtREAL,
  x being Element of C st max-(f).x = -(f.x) holds max+(f).x = 0.
proof
  let C be non empty set;
  let f be PartFunc of C,ExtREAL;
  let x be Element of C;
A1: dom max+f = dom f by Def2;
  per cases;
  suppose
A2: x in dom f;
    assume
A3: max-(f).x = -(f.x);
A4: x in dom(max+(f)) by A2,Def2;
A5: x in dom(max-(f)) by A2,Def3;
A6: max+(f).x = max(f.x,0.) by A4,Def2;
 max-(f).x = max(-(f.x),0.) by A5,Def3;
then  -(-(f.x)) <= -0. by A3,XXREAL_0:def 10;
    hence thesis by A6,XXREAL_0:def 10;
  end;
  suppose
 not x in dom f;
    hence thesis by A1,FUNCT_1:def 4;
  end;
end;

theorem
  for C being non empty set, f being PartFunc of C,ExtREAL,
  x being Element of C st x in dom f & max-(f).x = 0. holds max+(f).x = f.x
proof
  let C be non empty set;
  let f be PartFunc of C,ExtREAL;
  let x be Element of C;
  assume that
A1: x in dom f and
A2: max-(f).x = 0.;
A3: x in dom(max+(f)) by A1,Def2;
A4: x in dom(max-(f)) by A1,Def3;
A5: max+(f).x = max(f.x,0.) by A3,Def2;
 max-(f).x = max(-(f.x),0.) by A4,Def3;
then  -0. <= -(-(f.x)) by A2,XXREAL_0:def 10;
  hence thesis by A5,XXREAL_0:def 10;
end;

theorem
  for C being non empty set, f being PartFunc of C,ExtREAL holds
  f = max+(f) - max-(f)
proof
  let C be non empty set;
  let f be PartFunc of C,ExtREAL;
A1: dom f = dom(max+(f)-max-(f)) by Th19;
   for x being Element of C st x in dom f holds f.x = (max+(f) - max-(f)).x
  proof
    let x be Element of C;
    assume
A3: x in dom f;
then A4: (max+(f) - max-(f)).x = max+(f).x - max-(f).x by A1,MESFUNC1:def 4;
    per cases by Th20;
    suppose
A5:   max+(f).x = f.x;
then    max-(f).x = 0. by Th21;
then    -max-(f).x = 0;
      hence thesis by A4,A5,XXREAL_3:4;
    end;
    suppose
A8:   max+(f).x = 0.;
then    max-(f).x = -(f.x) by A3,Th22;
      hence thesis by A4,A8,XXREAL_3:4;
    end;
  end;
  hence thesis by A1,PARTFUN1:34;
end;

theorem
  for C being non empty set, f being PartFunc of C,ExtREAL holds
  |.f.| = max+(f) + max-(f)
proof
  let C be non empty set;
  let f be PartFunc of C,ExtREAL;
A1: dom f = dom(max+(f)+max-(f)) by Th19;
A2: dom f = dom |.f.| by MESFUNC1:def 10;
 for x being Element of C st x in dom f holds
  |.f.| .x = (max+(f) + max-(f)).x
  proof
    let x be Element of C;
    assume
A4: x in dom f;
 now per cases by Th20;
      suppose
A6:     max+(f).x = f.x;
then A7:     max+(f).x + max-(f).x = f.x + 0. by Th21
          .= f.x by XXREAL_3:4;
     x in dom(max+(f)) by A4,Def2;
then      max+(f).x = max(f.x,0.) by Def2;
then     0. <= f.x by A6,XXREAL_0:def 10;
then     f.x = |. f.x .| by EXTREAL1:def 3
          .= |.f.| .x by A2,A4,MESFUNC1:def 10;
        hence thesis by A1,A4,A7,MESFUNC1:def 3;
      end;
      suppose
A12:    max+(f).x = 0.;
then A13:    max+(f).x + max-(f).x = 0. + -(f.x) by A4,Th22
          .= -(f.x) by XXREAL_3:4;
    x in dom(max+(f)) by A4,Def2;
then     max+(f).x = max(f.x,0.) by Def2;
then     f.x <= 0. by A12,XXREAL_0:def 10;
then     -(f.x) = |. f.x .| by EXTREAL2:55
          .= |.f.| .x by A2,A4,MESFUNC1:def 10;
        hence thesis by A1,A4,A13,MESFUNC1:def 3;
      end;
    end;
    hence thesis;
  end;
  hence thesis by A1,A2,PARTFUN1:34;
end;

begin  :: Measurability of max+(f), max-(f) and |.f.|

theorem
  f is_measurable_on A implies max+(f) is_measurable_on A
proof
  assume
A1: f is_measurable_on A;
 for r be real number holds A /\ less_dom(max+(f),R_EAL r) in S
  proof
    let r be real number;
    reconsider r as Real by XREAL_0:def 1;
 now per cases;
      suppose
A4:     0 < r;
     for x being set st x in less_dom(max+(f),R_EAL r) holds
        x in less_dom(f,R_EAL r)
        proof
          let x be set;
          assume
A6:       x in less_dom(max+(f),R_EAL r);
then A7:       x in dom max+(f) by MESFUNC1:def 12;
A8:       max+(f).x < R_EAL r by A6,MESFUNC1:def 12;
          reconsider x as Element of X by A6;
A9:       max(f.x,0.) < R_EAL r by A7,A8,Def2;
then A10:      f.x <= R_EAL r by XXREAL_0:30;
      f.x <> R_EAL r
          proof
            assume
A12:        f.x = R_EAL r;
then         max(f.x,0.) = 0. by A9,XXREAL_0:16;
            hence contradiction by A9,A12,XXREAL_0:def 10;
          end;
then A14:      f.x < R_EAL r by A10,XXREAL_0:1;
      x in dom f by A7,Def2;
          hence thesis by A14,MESFUNC1:def 12;
        end;
then A16:    less_dom(max+(f),R_EAL r) c= less_dom(f,R_EAL r) by
        TARSKI:def 3;
    for x being set st x in less_dom(f,R_EAL r) holds
        x in less_dom(max+(f),R_EAL r)
        proof
          let x be set;
          assume
A18:      x in less_dom(f,R_EAL r);
then A19:      x in dom f by MESFUNC1:def 12;
A20:      f.x < R_EAL r by A18,MESFUNC1:def 12;
          reconsider x as Element of X by A18;
A21:      x in dom (max+(f)) by A19,Def2;
      now per cases;
            suppose
          0. <= f.x;
then           max(f.x,0.) = f.x by XXREAL_0:def 10;
then           max+(f).x = f.x by A21,Def2;
              hence thesis by A20,A21,MESFUNC1:def 12;
            end;
            suppose
          not 0. <= f.x;
then           max(f.x,0.) = 0. by XXREAL_0:def 10;
then           max+(f).x = 0. by A21,Def2;
              hence thesis by A4,A21,MESFUNC1:def 12;
            end;
          end;
          hence thesis;
        end;
then
    less_dom(f,R_EAL r) c= less_dom(max+(f),R_EAL r) by TARSKI:def 3;
then     less_dom
(max+(f),R_EAL r) = less_dom(f,R_EAL r) by A16,XBOOLE_0:def 10;
        hence thesis by A1,MESFUNC1:def 17;
      end;
      suppose
A31:    r <= 0;
    for x being Element of X holds not x in less_dom(max+(f),R_EAL r)
        proof
          let x be Element of X;
          assume
A33:      x in less_dom(max+(f),R_EAL r);
then A34:      x in dom(max+(f)) by MESFUNC1:def 12;
A35:      max+(f).x < R_EAL r by A33,MESFUNC1:def 12;
      max+(f).x = max(f.x,0.) by A34,Def2;
          hence contradiction by A31,A35,XXREAL_0:25;
        end;
then     less_dom(max+(f),R_EAL r) = {} by SUBSET_1:10;
        hence thesis by PROB_1:10;
      end;
    end;
    hence thesis;
  end;
  hence thesis by MESFUNC1:def 17;
end;

theorem
  f is_measurable_on A & A c= dom f implies max-(f) is_measurable_on A
proof
  assume
A1: f is_measurable_on A & A c= dom f;
 for r be real number holds A /\ less_dom(max-(f),R_EAL r) in S
  proof
    let r be real number;
    reconsider r as Real by XREAL_0:def 1;
 now per cases;
      suppose
A4:     0 < r;
     (-1)(#)f is_measurable_on A by A1,MESFUNC1:41;
then A6:     -f is_measurable_on A by Th11;
     for x being set st x in less_dom(max-(f),R_EAL r) holds
        x in less_dom(-f,R_EAL r)
        proof
          let x be set;
          assume
A8:       x in less_dom(max-(f),R_EAL r);
then A9:       x in dom max-(f) by MESFUNC1:def 12;
A10:      max-(f).x < R_EAL r by A8,MESFUNC1:def 12;
          reconsider x as Element of X by A8;
A11:      max(-(f.x),0.) < R_EAL r by A9,A10,Def3;
then A12:      -(f.x) <= R_EAL r by XXREAL_0:30;
      -(f.x) <> R_EAL r
          proof
            assume
A14:        -(f.x) = R_EAL r;
then         max(-(f.x),0.) = 0. by A11,XXREAL_0:16;
            hence contradiction by A11,A14,XXREAL_0:def 10;
          end;
then A16:      -(f.x) < R_EAL r by A12,XXREAL_0:1;
      x in dom f by A9,Def3;
then A18:      x in dom -f by MESFUNC1:def 7;
then       (-f).x = -(f.x) by MESFUNC1:def 7;
          hence thesis by A16,A18,MESFUNC1:def 12;
        end;
then A20:    less_dom(max-(f),R_EAL r) c= less_dom(-f,R_EAL r) by
        TARSKI:def 3;
    for x being set st x in less_dom(-f,R_EAL r) holds
        x in less_dom(max-(f),R_EAL r)
        proof
          let x be set;
          assume
A22:      x in less_dom(-f,R_EAL r);
then A23:      x in dom -f by MESFUNC1:def 12;
A24:      (-f).x < R_EAL r by A22,MESFUNC1:def 12;
          reconsider x as Element of X by A22;
      x in dom f by A23,MESFUNC1:def 7;
then A26:      x in dom (max-(f)) by Def3;
      now per cases;
            suppose
          0. <= -(f.x);
then           max(-(f.x),0.) = -(f.x) by XXREAL_0:def 10;
then           max-(f).x = -(f.x) by A26,Def3
                .= (-f).x by A23,MESFUNC1:def 7;
              hence thesis by A24,A26,MESFUNC1:def 12;
            end;
            suppose
          not 0. <= -(f.x);
then           max(-(f.x),0.) = 0. by XXREAL_0:def 10;
then           max-(f).x = 0. by A26,Def3;
              hence thesis by A4,A26,MESFUNC1:def 12;
            end;
          end;
          hence thesis;
        end;
then
    less_dom(-f,R_EAL r) c= less_dom(max-(f),R_EAL r) by TARSKI:def 3;
then     less_dom(max-(f),R_EAL r) = less_dom(-f,R_EAL r) by A20,
        XBOOLE_0:def 10;
        hence thesis by A6,MESFUNC1:def 17;
      end;
      suppose
A36:    r <= 0;
    for x being Element of X holds not x in less_dom(max-(f),R_EAL r)
        proof
          let x be Element of X;
          assume
A38:      x in less_dom(max-(f),R_EAL r);
then A39:      x in dom(max-(f)) by MESFUNC1:def 12;
A40:      max-(f).x < R_EAL r by A38,MESFUNC1:def 12;
      max-(f).x = max(-(f.x),0.) by A39,Def3;
          hence contradiction by A36,A40,XXREAL_0:25;
        end;
then     less_dom(max-(f),R_EAL r) = {} by SUBSET_1:10;
        hence thesis by PROB_1:10;
      end;
    end;
    hence thesis;
  end;
  hence thesis by MESFUNC1:def 17;
end;

theorem
  for f,A st f is_measurable_on A & A c= dom f holds |.f.| is_measurable_on A
proof
  let f,A;
  assume
A1: f is_measurable_on A & A c= dom f;
 for r be real number holds A /\ less_dom(|.f.|,R_EAL r) in S
  proof
    let r be real number;
    reconsider r as Real by XREAL_0:def 1;
 for x being set st x in less_dom(|.f.|,R_EAL r) holds
    x in less_dom(f,R_EAL r) /\ great_dom(f,R_EAL -r)
    proof
      let x be set;
      assume
A4:   x in less_dom(|.f.|,R_EAL r);
then A5:   x in dom |.f.| by MESFUNC1:def 12;
A6:   |.f.| .x < R_EAL r by A4,MESFUNC1:def 12;
      reconsider x as Element of X by A4;
A7:   x in dom f by A5,MESFUNC1:def 10;
A8:   |. f.x .| < R_EAL r by A5,A6,MESFUNC1:def 10;
then A9:   -(R_EAL r) < f.x by EXTREAL2:58;
A10:  f.x < R_EAL r by A8,EXTREAL2:58;
A11:  -(R_EAL r) = -r by SUPINF_2:3;
A12:  x in less_dom(f,R_EAL r) by A7,A10,MESFUNC1:def 12;
  x in great_dom(f,R_EAL -r) by A7,A9,A11,MESFUNC1:def 14;
      hence thesis by A12,XBOOLE_0:def 4;
    end;
    then
A14: less_dom(|.f.|,R_EAL r) c= less_dom(f,R_EAL r) /\ great_dom(f,R_EAL
    -r) by TARSKI:def 3;
 for x being set st x in less_dom(f,R_EAL r) /\
    great_dom(f,R_EAL -r) holds x in less_dom(|.f.|,R_EAL r)
    proof
      let x be set;
      assume
A16:  x in less_dom(f,R_EAL r) /\ great_dom(f,R_EAL -r);
then A17:  x in less_dom(f,R_EAL r) by XBOOLE_0:def 4;
A18:  x in great_dom(f,R_EAL -r) by A16,XBOOLE_0:def 4;
A19:  x in dom f by A17,MESFUNC1:def 12;
A20:  f.x < R_EAL r by A17,MESFUNC1:def 12;
A21:  R_EAL -r < f.x by A18,MESFUNC1:def 14;
      reconsider x as Element of X by A16;
A22:  x in dom |.f.| by A19,MESFUNC1:def 10;
  -(R_EAL r) = -r by SUPINF_2:3;
then   |. f.x .| < R_EAL r by A20,A21,EXTREAL2:59;
then   |.f.| .x < R_EAL r by A22,MESFUNC1:def 10;
      hence thesis by A22,MESFUNC1:def 12;
    end;
then  less_dom(f,R_EAL r) /\ great_dom(f,R_EAL -r) c= less_dom(|.f.|,
    R_EAL r) by TARSKI:def 3;
then
A27: less_dom(|.f.|,R_EAL r) = less_dom(f,R_EAL r) /\ great_dom(f,R_EAL -r) by
    A14,XBOOLE_0:def 10;
 A /\ great_dom(f,R_EAL -r) /\ less_dom(f,R_EAL r) in S by A1,MESFUNC1:36;
    hence thesis by A27,XBOOLE_1:16;
  end;
  hence thesis by MESFUNC1:def 17;
end;

begin

definition
  let A,X be set;
  redefine func chi(A,X) -> PartFunc of X,ExtREAL;
  coherence
  proof
 now
      let x be set;
      assume
A2:   x in rng chi(A,X);
   now per cases by A2,MESFUNC1:def 8,TARSKI:def 2;
        suppose
       x=0.;
          hence x in ExtREAL;
        end;
        suppose
       x=1.;
          hence x in ExtREAL;
        end;
      end;
      hence x in ExtREAL;
    end;
    then  dom
 chi(A,X) =X & rng chi(A,X) c= ExtREAL by FUNCT_3:def 3,TARSKI:def 3;
    hence chi(A,X) is PartFunc of X,ExtREAL by RELSET_1:11;
  end;
end;

canceled;

theorem
  chi(A,X) is real-valued
proof
 for x st x in dom chi(A,X) holds |.chi(A,X).x.| < +infty
  proof
    let x;
    assume x in dom chi(A,X);
 now per cases;
      suppose
     x in A;
then      chi(A,X).x = 1. by FUNCT_3:def 3,MESFUNC1:def 8;
then      |.chi(A,X).x.| = 1. by EXTREAL1:def 3,MESFUNC1:def 8;
        hence thesis by MESFUNC1:def 8,XXREAL_0:9;
      end;
      suppose
     not x in A;
then      chi(A,X).x = 0. by FUNCT_3:def 3;
        hence thesis by EXTREAL1:def 3;
      end;
    end;
    hence thesis;
  end;
  hence thesis by Def1;
end;

theorem
  chi(A,X) is_measurable_on B
proof
 for r be real number holds B /\ less_eq_dom(chi(A,X),R_EAL r) in S
  proof
    let r be real number;
    reconsider r as Real by XREAL_0:def 1;
 now per cases;
      suppose
A3:     r >= 1;
             for
 x being set st x in X holds x in less_eq_dom(chi(A,X),R_EAL r)
        proof
          let x be set;
          assume
A5:       x in X;
then A6:       x in dom chi(A,X) by FUNCT_3:def 3;
          reconsider x as Element of X by A5;
       chi(A,X).x <= 1.
          proof
         now per cases;
              suppose
             x in A;
                hence thesis by FUNCT_3:def 3,MESFUNC1:def 8;
              end;
              suppose
            not x in A;
                hence thesis by FUNCT_3:def 3,MESFUNC1:def 8;
              end;
            end;
            hence thesis;
          end;
then       chi(A,X).x <= R_EAL r by A3,MESFUNC1:def 8,XXREAL_0:2;
          hence thesis by A6,MESFUNC1:def 13;
        end;
then     X c= less_eq_dom(chi(A,X),R_EAL r) by TARSKI:def 3;
then     less_eq_dom(chi(A,X),R_EAL r) = X by XBOOLE_0:def 10;
then     less_eq_dom(chi(A,X),R_EAL r) in S by PROB_1:11;
        hence thesis by FINSUB_1:def 2;
      end;
      suppose
A15:    0 <= r & r < 1;
            for
 x being set st x in less_eq_dom(chi(A,X),R_EAL r) holds x in X\A
        proof
          let x be set;
          assume
A17:      x in less_eq_dom(chi(A,X),R_EAL r);
          then reconsider x as Element of X;
      chi(A,X).x <= R_EAL r by A17,MESFUNC1:def 13;
then       not x in A by A15,FUNCT_3:def 3;
          hence thesis by XBOOLE_0:def 5;
        end;
then A20:    less_eq_dom(chi(A,X),R_EAL r) c= X\A by TARSKI:def 3;
            for
 x being set st x in X\A holds x in less_eq_dom(chi(A,X),R_EAL r)
        proof
          let x be set;
          assume
A22:      x in X\A;
then A23:      x in X;
A24:      not x in A by A22,XBOOLE_0:def 5;
          reconsider x as Element of X by A22;
A25:      chi(A,X).x = 0. by A24,FUNCT_3:def 3;
      x in dom chi(A,X) by A23,FUNCT_3:def 3;
          hence thesis by A15,A25,MESFUNC1:def 13;
        end;
then     X\A c= less_eq_dom(chi(A,X),R_EAL r) by TARSKI:def 3;
then A28:    less_eq_dom(chi(A,X),R_EAL r) = X\A by A20,XBOOLE_0:def 10;
    X in S by PROB_1:11;
then     less_eq_dom(chi(A,X),R_EAL r) in S by A28,MEASURE1:20;
        hence thesis by FINSUB_1:def 2;
      end;
      suppose
A31:    r < 0;
    for x holds not x in less_eq_dom(chi(A,X),R_EAL r)
        proof
          assume ex x st x in less_eq_dom(chi(A,X),R_EAL r);
          then consider x such that
A34:      x in less_eq_dom(chi(A,X),R_EAL r);
      0. <= chi(A,X).x
          proof
        now per cases;
              suppose
            x in A;
                hence thesis by FUNCT_3:def 3;
              end;
              suppose
            not x in A;
                hence thesis by FUNCT_3:def 3;
              end;
            end;
            hence thesis;
          end;
          hence contradiction by A31,A34,MESFUNC1:def 13;
        end;
then     less_eq_dom(chi(A,X),R_EAL r) = {} by SUBSET_1:10;
        hence thesis by PROB_1:10;
      end;
    end;
    hence thesis;
  end;
  hence thesis by MESFUNC1:32;
end;

begin  :: Definition and measurability of simple function

registration
  let X be set;
  let S be SigmaField of X;
  cluster disjoint_valued FinSequence of S;
  existence
  proof
    reconsider A = {} as Element of S by PROB_1:10;
    reconsider p = Seg 1 --> A as Function of Seg 1, S;
A1: dom p = Seg 1 by FUNCOP_1:19;
    then reconsider p as FinSequence by FINSEQ_1:def 2;
 rng p c= S by RELAT_1:def 19;
    then reconsider p as FinSequence of S by FINSEQ_1:def 4;
A3: for n,m being set st n <> m holds p.n misses p.m
    proof
      let n,m be set;
      assume n <> m;
   p.n = {}
      proof
        per cases;
        suppose
       n in dom p;
          hence thesis by A1,FUNCOP_1:13;
        end;
        suppose
       not n in dom p;
          hence thesis by FUNCT_1:def 4;
        end;
      end;
      hence thesis by XBOOLE_1:65;
    end;
    take p;
    thus thesis by A3,PROB_2:def 3;
  end;
end;

definition
  let X be set;
  let S be SigmaField of X;
  mode Finite_Sep_Sequence of S is disjoint_valued FinSequence of S;
end;

theorem Th33:
  for F being Function st F is Finite_Sep_Sequence of S holds
  ex G being Sep_Sequence of S st union rng F = union rng G &
  (for n st n in dom F holds F.n = G.n) &
  for m st not m in dom F holds G.m = {}
proof
  let F be Function;
  defpred P[set,set] means
  ($1 in dom F implies F.$1 = $2) & (not $1 in dom F implies $2 = {});
  assume
A1: F is Finite_Sep_Sequence of S;
A2: for x1 being set st x1 in NAT ex y1 being set st y1 in S & P[x1,y1]
  proof
    let x1 be set;
    assume x1 in NAT;
    then reconsider x1 as Element of NAT;
    per cases;
    suppose
A4:   x1 in dom F;
then A5:   F.x1 in rng F by FUNCT_1:def 5;
A6:   rng F c= S by A1,FINSEQ_1:def 4;
      take F.x1;
      thus thesis by A4,A5,A6;
    end;
    suppose
A7:   not x1 in dom F;
      take {};
      thus thesis by A7,PROB_1:10;
    end;
  end;
  consider G being Function of NAT,S such that
A8: for x1 being set st x1 in NAT holds P[x1,G.x1] from FUNCT_2:sch 1(A2);
 for n,m being set st n <> m holds G.n misses G.m
  proof
    let n,m be set;
    assume
A10: n <> m;
    per cases;
    suppose
A11:  n in NAT & m in NAT;
  now per cases;
        suppose
      n in dom F & m in dom F;
then       G.n = F.n & G.m = F.m by A8,A11;
          hence thesis by A1,A10,PROB_2:def 3;
        end;
        suppose
A15:      not n in dom F or not m in dom F;
      now per cases by A15;
            suppose
          not n in dom F;
then           G.n = {} by A8,A11;
              hence thesis by XBOOLE_1:65;
            end;
            suppose
          not m in dom F;
then           G.m = {} by A8,A11;
              hence thesis by XBOOLE_1:65;
            end;
          end;
          hence thesis;
        end;
      end;
      hence thesis;
    end;
    suppose
  not (n in NAT & m in NAT);
then   not n in dom G or not m in dom G;
then   G.n = {} or G.m = {} by FUNCT_1:def 4;
      hence thesis by XBOOLE_1:65;
    end;
  end;
  then reconsider G as Sep_Sequence of S by PROB_2:def 3;
  take G;
 for x1 being set st x1 in union rng F holds x1 in union rng G
  proof
    let x1 be set;
    assume x1 in union rng F;
    then consider Y being set such that
A26: x1 in Y and
A27: Y in rng F by TARSKI:def 4;
    consider k being set such that
A28: k in dom F and
A29: Y = F.k by A27,FUNCT_1:def 5;
 dom F c= NAT by A1,RELAT_1:def 18;
    then reconsider k as Element of NAT by A28;
A31: F.k = G.k by A8,A28;
 G.k in rng G by FUNCT_2:6;
    hence thesis by A26,A29,A31,TARSKI:def 4;
  end;
then A33: union rng F c= union rng G by TARSKI:def 3;
 for x1 being set st x1 in union rng G holds x1 in union rng F
  proof
    let x1 be set;
    assume x1 in union rng G;
    then consider Y being set such that
A36: x1 in Y and
A37: Y in rng G by TARSKI:def 4;
    consider k being set such that
A38: k in dom G and
A39: Y = G.k by A37,FUNCT_1:def 5;
    reconsider k as Element of NAT by A38;
A40: k in dom F by A8,A36,A39;
A41: F.k = G.k by A8,A36,A39;
 F.k in rng F by A40,FUNCT_1:def 5;
    hence thesis by A36,A39,A41,TARSKI:def 4;
  end;
then  union rng G c= union rng F by TARSKI:def 3;
  hence union rng F = union rng G by A33,XBOOLE_0:def 10;
  hereby
    let n;
 n in NAT by ORDINAL1:def 13;
    hence n in dom F implies F.n = G.n by A8;
  end;
  let m;
 m in NAT by ORDINAL1:def 13;
  hence thesis by A8;
end;

theorem
  for F being Function st F is Finite_Sep_Sequence of S holds union rng F in S
proof
  let F be Function;
  assume F is Finite_Sep_Sequence of S;
then  ex G being Sep_Sequence of S st union rng F = union rng G
  &( for n st n in dom F holds F.n = G.n)& for m st not m in dom F holds G.m =
  {} by Th33;
  hence thesis;
end;

definition
  let X be non empty set;
  let S be SigmaField of X;
  let f be PartFunc of X,ExtREAL;
  canceled;
  pred f is_simple_func_in S means
  :Def5:
  f is real-valued &
  ex F being Finite_Sep_Sequence of S st (dom f = union rng F &
  for n being Nat,x,y being Element of X st
  n in dom F & x in F.n & y in F.n holds f.x = f.y);
end;

theorem
  f is real-valued implies rng f is Subset of REAL
proof
  assume
A1: f is real-valued;
 rng f c= REAL
  proof
    let y be set;
    assume
A3: y in rng f;
    then reconsider y as R_eal;
    consider x being set such that
A4: x in dom f and
A5: y = f.x by A3,FUNCT_1:def 5;
    reconsider x as Element of X by A4;
A6: |. f.x .| < +infty by A1,A4,Def1;
then  -(+infty) < y by A5,EXTREAL2:58;
then A8: -infty < y by XXREAL_3:def 3;
 y < +infty by A5,A6,EXTREAL2:58;
    hence thesis by A8,XXREAL_0:14;
  end;
  hence thesis;
end;

theorem
  for F being Relation st F is Finite_Sep_Sequence of S holds
  F|(Seg n) is Finite_Sep_Sequence of S
proof
  let F be Relation;
  assume
A1: F is Finite_Sep_Sequence of S;
  then reconsider G = F|(Seg n) as FinSequence of S by FINSEQ_1:23;
  reconsider F as FinSequence of S by A1;
 for k,m being set st k <> m holds G.k misses G.m
  proof
    let k,m be set;
    assume
A3: k <> m;
    per cases;
    suppose
   k in dom G & m in dom G;
then    G.k = F.k & G.m = F.m by FUNCT_1:70;
      hence thesis by A1,A3,PROB_2:def 3;
    end;
    suppose
   not (k in dom G & m in dom G);
then    G.k = {} or G.m = {} by FUNCT_1:def 4;
      hence thesis by XBOOLE_1:65;
    end;
  end;
  hence thesis by PROB_2:def 3;
end;

theorem
  f is_simple_func_in S implies f is_measurable_on A
proof
  assume
A1: f is_simple_func_in S;
  then consider F being Finite_Sep_Sequence of S such that
A2: dom f = union rng F and
A3: for n being Nat,x,y being Element of X st
  n in dom F & x in F.n & y in F.n holds f.x = f.y by Def5;
  reconsider F as FinSequence of S;
  defpred P[Nat] means
  $1 <= len F implies f|(union rng(F|(Seg($1)))) is_measurable_on A;
A4: P[0]
  proof
    assume
A5: 0 <= len F;
    reconsider z = 0 as Nat;
    reconsider G = F|Seg z as FinSequence of S by FINSEQ_1:23;
 len G = 0 by A5,FINSEQ_1:21;
then  G = {};
then A8: dom(f|union rng G) = dom f /\ {} by RELAT_1:60,90,ZFMISC_1:2
      .= {};
 for r be real number holds A /\ less_dom(f|union rng G,R_EAL r) in S
    proof
      let r be real number;
  for x1 being set st x1 in less_dom(f|union rng G,R_EAL r) holds
      x1 in dom(f|union rng G) by MESFUNC1:def 12;
then   less_dom(f|union rng G,R_EAL r)c=dom(f|union rng G) by
      TARSKI:def 3;
then   less_dom(f|union rng G,R_EAL r) = {} by A8,XBOOLE_1:3;
      hence thesis by PROB_1:10;
    end;
    hence thesis by MESFUNC1:def 17;
  end;
A13: for m st P[m] holds P[m+1]
  proof
    let m;
    assume
A14: m <= len F implies f|(union rng(F|(Seg m))) is_measurable_on A;
    reconsider m as Element of NAT by ORDINAL1:def 13;
 m+1 <= len F implies f|(union rng(F|(Seg(m+1)))) is_measurable_on A
    proof
      assume
A16:  m+1 <= len F;
A17:  m <= m+1 by NAT_1:11;
  for r be real number holds
      A /\ less_dom(f|union rng(F|(Seg(m+1))),R_EAL r) in S
      proof
        let r be real number;
    now per cases;
          suppose
A20:        F.(m+1) = {};
        less_dom(f|union rng(F|(Seg m)),R_EAL r)
            = less_dom(f|union rng(F|(Seg(m+1))),R_EAL r)
            proof
              reconsider G1 = F|(Seg m) as FinSequence of S by FINSEQ_1:23;
          1 <= m+1 by NAT_1:11;
then           m+1 in Seg len F by A16,FINSEQ_1:3;
then           m+1 in dom F by FINSEQ_1:def 3;
then           F|(Seg(m+1)) = G1^<*{}*> by A20,FINSEQ_5:11;
then           rng (F|(Seg(m+1))) = rng G1 \/ rng <*{}*> by FINSEQ_1:44
                .= rng G1 \/ {{}} by FINSEQ_1:56;
then           union rng (F|(Seg(m+1))) = union rng G1 \/ union {{}} by
ZFMISC_1:96
                .= union rng G1 \/ {} by ZFMISC_1:31
                .= union rng G1;
              hence thesis;
            end;
            hence thesis by A14,A16,A17,MESFUNC1:def 17,XXREAL_0:2;
          end;
          suppose
A28:        F.(m+1) <> {};
            reconsider G1 = F|(Seg m) as FinSequence of S by FINSEQ_1:23;
        1 <= m+1 by NAT_1:11;
then         m+1 in Seg len F by A16,FINSEQ_1:3;
then A31:        m+1 in dom F by FINSEQ_1:def 3;
then A32:        F.(m+1) in rng F by FUNCT_1:def 5;
then         F.(m+1) in S;
            then reconsider F1=F.(m+1) as Subset of X;
            consider x such that
A34:        x in F1 by A28,SUBSET_1:10;
        F|(Seg(m+1)) = G1^<*(F.(m+1))*> by A31,FINSEQ_5:11;
then
        rng (F|(Seg(m+1))) = rng G1 \/ rng <*(F.(m+1))*> by FINSEQ_1:44
              .= rng G1 \/ {F.(m+1)} by FINSEQ_1:56;
then A37:        union rng (F|(Seg(m+1))) = union rng G1 \/
            union {F.(m+1)} by ZFMISC_1:96
              .= union rng G1 \/ F.(m+1) by ZFMISC_1:31;
A38:        x in dom f by A2,A32,A34,TARSKI:def 4;
        f is real-valued by A1,Def5;
then A40:        |. f.x .| < +infty by A38,Def1;
then         -(+infty) < f.x by EXTREAL2:58;
then A42:        -infty < f.x by XXREAL_3:def 3;
        f.x < +infty by A40,EXTREAL2:58;
            then reconsider r1 = f.x as Real by A42,XXREAL_0:14;
        now per cases;
              suppose
A45:            r <= r1;
                            for
 x1 being set st x1 in less_dom(f|union rng(F|(Seg(m+1)))
                , R_EAL r)
                holds x1 in less_dom(f|union rng(F|(Seg m)),R_EAL r)
                proof
                  let x1 be set;
                  assume
A47:              x1 in less_dom(f|union rng(F|(Seg(m+1))),R_EAL r);
                  then A48:              x1
 in dom(f|union rng(F|(Seg(m+1)) )) by MESFUNC1:def 12;
then               x1 in dom f /\ union rng(F|(Seg(m+1))) by
                  RELAT_1:90;
then               x1 in (dom f /\ union rng G1) \/ (dom f /\
                  F.(m+1)) by A37,XBOOLE_1:23;
                  then A51:              x1
 in dom f /\ union rng G1 or x1 in dom f /\ F.(m+1) by
                  XBOOLE_0:def 3;
                  reconsider x1 as Element of X by A47;
A52:              (f|union rng(F|(Seg(m+1)))).x1 < R_EAL r
                  by A47,MESFUNC1:def 12;
A53:              ( f|union rng(F|(Seg(m+1)))).x1 = f.x1 by A48,FUNCT_1:70;
                  set m1 = m+1;
              not x1 in dom(f|F1)
                  proof
                    assume x1 in dom(f|F1);
then                 x1 in dom f /\ F1 by RELAT_1:90;
then                 x1 in F.m1 by XBOOLE_0:def 4;
                    hence contradiction by A3,A31,A34,A45,A52,A53;
                  end;
then A58:              x1 in dom(f|union rng G1) by A51,
                  RELAT_1:90;
then               (
f|union rng(F|(Seg(m+1)))).x1 = (f|union rng G1).x1 by A53,FUNCT_1:70;
                  hence thesis by A52,A58,MESFUNC1:def 12;
                end;
                then
A60:            less_dom(f|union rng(F|(Seg(m+1))), R_EAL r)
                c= less_dom(f|union rng(F|(Seg m)),R_EAL r) by
                TARSKI:def 3;
            for
 x1 being set st x1 in less_dom(f|union rng(F|(Seg m)),R_EAL r)
                holds x1 in less_dom(f|union rng(F|(Seg(m+1))),R_EAL r)
                proof
                  let x1 be set;
                  assume
A62:              x1 in less_dom(f|union rng(F|(Seg m)),R_EAL r);
then A63:              x1 in dom(f|union rng(F|(Seg m))) by
                  MESFUNC1:def 12;
then A64:              x1 in dom f /\ union rng G1 by RELAT_1:90;
then A65:              x1 in union rng G1 by XBOOLE_0:def 4;
A66:              x1 in dom f by A64,XBOOLE_0:def 4;
              x1 in union rng (F|(Seg(m+1))) by A37,A65,XBOOLE_0:def 3;
then               x1
 in dom f /\ union rng (F|(Seg(m+1))) by A66,XBOOLE_0:def 4;
                  then A69:              x1
 in dom(f|union rng(F|(Seg(m+1)) )) by RELAT_1:90;
                  reconsider x1 as Element of X by A62;
A70:              (
                  f|union rng(F|(Seg m))).x1 < R_EAL r by A62,MESFUNC1:def 12;
              (f|union rng(F|(Seg m))).x1 = f.x1 by A63,FUNCT_1:70;
then
              (f|union rng(F|(Seg m))).x1 = (f|union rng(F|(Seg(m+1)))).x1
                  by A69,FUNCT_1:70;
                  hence thesis by A69,A70,MESFUNC1:def 12;
                end;
then             less_dom(f|union rng(F|(Seg m)),R_EAL r)
c= less_dom(f|union rng(F|(Seg(m+1))),R_EAL r) by TARSKI:def 3;
then             less_dom
(f|union rng(F|(Seg(m+1))),R_EAL r) = less_dom(f|union rng(F|(Seg
                m)),R_EAL r) by A60,XBOOLE_0:def 10;
                hence thesis by A14,A16,A17,MESFUNC1:def 17,XXREAL_0:2;
              end;
              suppose
A75:            r1 < r;
            for
 x1 being set st x1 in less_dom(f|union rng(F|(Seg(m+1))),R_EAL r)
                holds x1 in less_dom(f|union rng(F|(Seg m)),R_EAL r) \/ F.(m+1)
                proof
                  let x1 be set;
                  assume
A77:              x1 in less_dom(f|union rng(F|(Seg(m+1))),R_EAL r);
                  then A78:              x1
 in dom(f|union rng(F|(Seg(m+1)) )) by MESFUNC1:def 12;
then               x1 in dom f /\ union rng(F|(Seg(m+1))) by
                  RELAT_1:90;
then A80:              x1 in (dom f /\ union rng G1) \/ (
                  dom f /\ F.(m+1)) by A37,XBOOLE_1:23;
              now per cases by A80,XBOOLE_0:def 3;
                    suppose
A82:                  x1 in dom f /\ union rng G1;
                      then reconsider x1 as Element of X;
A83:                  x1 in dom(f|union rng G1) by A82,RELAT_1:90;
then A84:                  (f|union rng G1).x1 = f.x1 by
                      FUNCT_1:70;
A85:                  (f|union rng(F|(Seg(m+1)))).x1 < R_EAL r
                      by A77,MESFUNC1:def 12;
                                        (
f|union rng(F|(Seg(m+1)))).x1 = (f|union rng G1).x1
                      by A78,A84,FUNCT_1:70;
then                   x1 in less_dom(f|union rng(F|(Seg m)),R_EAL r)
                      by A83,A85,MESFUNC1:def 12;
                      hence thesis by XBOOLE_0:def 3;
                    end;
                    suppose
                  x1 in dom f /\ F.(m+1);
then                   x1 in F.(m+1) by XBOOLE_0:def 4;
                      hence thesis by XBOOLE_0:def 3;
                    end;
                  end;
                  hence thesis;
                end;
                then
A90:            less_dom(f|union rng(F|(Seg(m+1))), R_EAL r)
                c= less_dom(f|union rng(F|(Seg m)),R_EAL r) \/
                F.(m+1) by TARSKI:def 3;
            for x1 being set st
                x1 in less_dom(f|union rng(F|(Seg m)),R_EAL r) \/ F.(m+1) holds
                x1 in less_dom(f|union rng(F|(Seg(m+1))),R_EAL r)
                proof
                  let x1 be set;
                  assume
A92:              x1 in less_dom(f|union rng(F|(Seg m)),R_EAL r)\/ F.(m+1);
              now per cases by A92,XBOOLE_0:def 3;
                    suppose
A94:                  x1 in less_dom(f|union rng(F|(Seg m)),R_EAL r);
                      then A95:                  x1
 in dom(f|union rng(F|( Seg m))) by MESFUNC1:def 12;
then A96:                  x1 in dom f /\ union rng G1 by RELAT_1:90;
then A97:                  x1 in union rng G1 by XBOOLE_0:def 4;
A98:                  x1 in dom f by A96,XBOOLE_0:def 4;
                  x1 in union rng (F|(Seg(m+1))) by A37,A97,XBOOLE_0:def 3;
then                  x1
 in dom f /\ union rng (F|(Seg(m+1))) by A98,XBOOLE_0:def 4;
                      then A101:                 x1
 in dom(f|union rng(F|( Seg(m+1)))) by RELAT_1:90;
                      reconsider x1 as Element of X by A94;
A102:                 (f|union rng(F|(Seg m))).x1 < R_EAL r
                      by A94,MESFUNC1:def 12;
                 (f|union rng(F|(Seg m))).x1 = f.x1 by A95,
                      FUNCT_1:70;
then                  (f|union rng(F|(Seg m))).x1 =
                      (f|union rng(F|(Seg(m+1)))).x1 by A101,FUNCT_1:70;
                      hence thesis by A101,A102,MESFUNC1:def 12;
                    end;
                    suppose
A105:                 x1 in F.(m+1);
                      then A106:                 x1
 in union rng (F|(Seg(m+ 1))) by A37,XBOOLE_0:def 3;
A107:                 x1 in dom f by A2,A32,A105,TARSKI:def 4;
then                  x1 in dom f /\ union rng (F|(Seg(m+1)))
                      by A106,XBOOLE_0:def 4;
                      then A109:                 x1
 in dom(f|union rng (F|( Seg(m+1)))) by RELAT_1:90;
                      reconsider x1 as Element of X by A107;
A110:                 f.x1 = R_EAL r1 by A3,A31,A34,A105;
                      reconsider y = f.x1 as R_eal;
                 y = (f|union rng (F|(Seg(m+1)))).x1 by A109,
                      FUNCT_1:70;
                      hence thesis by A75,A109,A110,MESFUNC1:def 12;
                    end;
                  end;
                  hence thesis;
                end;
then            less_dom(f|union rng(F|(Seg m)),R_EAL r) \/ F.(m+1)
c= less_dom(f|union rng(F|(Seg(m+1))),R_EAL r) by TARSKI:def 3;
then            less_dom
(f|union rng(F|(Seg(m+1))),R_EAL r) = less_dom(f|union rng(F|(Seg
                m)),R_EAL r) \/ F.(m+1) by A90,XBOOLE_0:def 10;
                then A114:           A
 /\ less_dom(f|union rng(F|(Seg(m+1))), R_EAL r)
= (A /\ less_dom(f|union rng(F|(Seg m)),R_EAL r)) \/ (A /\ F.(m+1))
                by XBOOLE_1:23;
                           A
 /\ less_dom(f|union rng(F|(Seg m)),R_EAL r) in S & A /\ F.(m+1) in S by
                A14,A16,A17,A32,FINSUB_1:def 2,MESFUNC1:def 17,XXREAL_0:2;
                hence thesis by A114,FINSUB_1:def 1;
              end;
            end;
            hence thesis;
          end;
        end;
        hence thesis;
      end;
      hence thesis by MESFUNC1:def 17;
    end;
    hence thesis;
  end;
A116: for n being Nat holds P[n] from NAT_1:sch 2(A4,A13);
 F|(Seg len F) = F by FINSEQ_3:55;
then  f|(dom f) is_measurable_on A by A2,A116;
  hence thesis by RELAT_1:97;
end;
