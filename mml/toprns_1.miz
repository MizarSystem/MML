:: Sequences in $R^n$
::  by Agnieszka Sakowicz , Jaros{\l}aw Gryko and Adam Grabowski
::
:: Received May 10, 1994
:: Copyright (c) 1994 Association of Mizar Users

environ

 vocabularies NUMBERS, SUBSET_1, SEQ_1, NAT_1, EUCLID, REAL_1, PRE_TOPC,
      FUNCT_1, RELAT_1, VALUED_0, TARSKI, STRUCT_0, SUPINF_2, ARYTM_3, ARYTM_1,
      COMPLEX1, VALUED_1, CARD_1, XXREAL_0, XXREAL_2, SEQ_2, ORDINAL2;
 notations TARSKI, SUBSET_1, NUMBERS, RELAT_1, FUNCT_1, VALUED_1, COMPLEX1,
      REAL_1, PRE_TOPC, XXREAL_0, NAT_1, SEQ_1, STRUCT_0, RLVECT_1, EUCLID;
 constructors XXREAL_0, REAL_1, NAT_1, COMPLEX1, MONOID_0, EUCLID, SEQ_1,
      RELSET_1, VALUED_0, BINOP_2;
 registrations ORDINAL1, RELSET_1, XXREAL_0, XREAL_0, MEMBERED, STRUCT_0,
      MONOID_0, EUCLID, XBOOLE_0, VALUED_1, FUNCT_2, NUMBERS, VALUED_0;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions STRUCT_0;
 theorems FUNCT_1, FUNCT_2, TARSKI, EUCLID, SEQ_1, NAT_1, XBOOLE_0, NORMSP_1,
      XCMPLX_0, XREAL_1, COMPLEX1, XXREAL_0;
 schemes SEQ_1, NAT_1, FUNCT_2;

begin

definition
  let N be Element of NAT;
  mode Real_Sequence of N is sequence of TOP-REAL N;
end;

reserve N,n,m,k,n1,n2 for Element of NAT;
reserve q,r,r1,r2 for Real;
reserve x,y for set;
reserve w,w1,w2,g,g1,g2 for Point of TOP-REAL N;
reserve seq,seq1,seq2,seq3,seq' for Real_Sequence of N;

canceled;

theorem Th2:
  for f being Function holds f is Real_Sequence of N iff (dom f=NAT
  & for n holds f.n is Point of TOP-REAL N)
proof
  let f be Function;
  thus f is Real_Sequence of N implies (dom f=NAT & for n holds f.n is Point
  of TOP-REAL N) by NORMSP_1:17;
  assume that
A1: dom f=NAT and
A2: for n holds f.n is Point of TOP-REAL N;
  for x holds x in NAT implies f.x is Point of TOP-REAL N by A2;
  hence thesis by A1,NORMSP_1:17;
end;

definition
  let N;
  let IT be Real_Sequence of N;
  attr IT is non-zero means
  :Def1:
  rng IT c= NonZero TOP-REAL N;
end;

theorem Th3:
  seq is non-zero iff for x st x in NAT holds seq.x<>0.TOP-REAL N
proof
  thus seq is non-zero implies for x st x in NAT holds seq.x<>0.TOP-REAL N
  proof
    assume
    seq is non-zero;
    then
A1: rng seq c= NonZero TOP-REAL N by Def1;
    let x;
    assume
    x in NAT;
    then x in dom seq by Th2;
    then seq.x in rng seq by FUNCT_1:def 5;
    then not seq.x in {0.TOP-REAL N} by A1,XBOOLE_0:def 5;
    hence thesis by TARSKI:def 1;
  end;
  assume
A2: for x st x in NAT holds seq.x<>0.TOP-REAL N;
  now
    let y;
    assume
    y in rng seq;
    then consider x such that
A3: x in dom seq and
A4: seq.x=y by FUNCT_1:def 5;
A5: x in NAT by A3,NORMSP_1:17;
    then y<>0.TOP-REAL N by A2,A4;
    then
A6: not y in {0.TOP-REAL N} by TARSKI:def 1;
    y is Point of TOP-REAL N by A4,A5,NORMSP_1:17;
    hence y in NonZero TOP-REAL N by A6,XBOOLE_0:def 5;
  end;
  then rng seq c= NonZero TOP-REAL N by TARSKI:def 3;
  hence thesis by Def1;
end;

theorem Th4:
  seq is non-zero iff for n holds seq.n<>0.TOP-REAL N
proof
  thus seq is non-zero implies for n holds seq.n<>0.TOP-REAL N by Th3;
  assume
  for n holds seq.n<>0.TOP-REAL N;
  then for x holds x in NAT implies seq.x<>0.TOP-REAL N;
  hence thesis by Th3;
end;

definition
  let N,seq1,seq2;
  func seq1 + seq2 -> Real_Sequence of N means
  :Def2:
  for n holds it.n = seq1.
  n + seq2.n;
  existence
  proof
    deffunc U(Element of NAT) = seq1.$1 + seq2.$1;
    thus ex s being Real_Sequence of N st for n holds s.n = U(n) from FUNCT_2:
    sch 4;
  end;
  uniqueness
  proof
    let seq,seq' such that
A1: for n holds seq.n=seq1.n+seq2.n and
A2: for n holds seq'.n=seq1.n+seq2.n;
    now
      let n;
      seq.n=seq1.n+seq2.n by A1;
      hence seq.n=seq'.n by A2;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

definition
  let r,N,seq;
  func r*seq -> Real_Sequence of N means
  : Def3:
  for n holds it.n = r*seq.n;
  existence
  proof
    deffunc U(Element of NAT) = r * seq.$1;
    thus ex s being Real_Sequence of N st for n holds s.n = U(n) from FUNCT_2:
    sch 4;
  end;
  uniqueness
  proof
    let seq1,seq2 such that
A1: for n holds seq1.n=r*seq.n and
A2: for n holds seq2.n=r*seq.n;
    now
      let n;
      seq1.n=r*seq.n by A1;
      hence seq1.n=seq2.n by A2;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

definition
  let N,seq;
  func - seq -> Real_Sequence of N means
  :Def4:
  for n holds it.n = -seq.n;
  existence
  proof
    take seq1=(-1)*seq;
    let n;
    thus seq1.n=(-1)*seq.n by Def3
      .=-(seq.n) by EUCLID:43;
  end;
  uniqueness
  proof
    let seq1,seq2 such that
A1: for n holds seq1.n=-seq.n and
A2: for n holds seq2.n=-seq.n;
    now
      let n;
      seq1.n=-seq.n by A1;
      hence seq1.n=seq2.n by A2;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

definition
  let N,seq1,seq2;
  func seq1-seq2 -> Real_Sequence of N equals
  seq1 +- seq2;
  correctness;
end;

definition
  canceled;
  let N,seq;
  func |.seq.| -> Real_Sequence means
  :Def7:
  for n holds it.n=|.seq.n.|;
  existence
  proof
    deffunc U(Element of NAT) = |.seq.$1.|;
    thus ex s being Real_Sequence st for n holds s.n= U(n) from SEQ_1:sch 1;
  end;
  uniqueness
  proof
    let seq8,seq9 be Real_Sequence such that
A1: for n holds seq8.n=|.seq.n.| and
A2: for n holds seq9.n=|.seq.n.|;
    now
      let n;
      seq9.n=|.seq.n.| by A2;
      hence seq8.n=seq9.n by A1;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

canceled 3;

theorem Th8:
  abs(r)*|.w.| = |.r*w.|
proof
  reconsider w2=w as Element of REAL N by EUCLID:25;
  thus abs(r)*|.w.| = |.r*w2.| by EUCLID:14
    .= |.r*w.| by EUCLID:69;
end;

theorem
  |.r*seq.| = abs(r)(#)|.seq.|
proof
  now
    let n;
    thus |.r*seq.|.n=|.(r*seq).n.| by Def7
      .=|.r*(seq.n).| by Def3
      .=abs(r)*|.seq.n.| by Th8
      .=abs(r)*(|.seq.|).n by Def7
      .=(abs(r)(#)|.seq.|).n by SEQ_1:13;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem
  seq1 + seq2 = seq2 + seq1
proof
  now
    let n;
    thus (seq1+seq2).n = seq2.n + seq1.n by Def2
      .= (seq2 + seq1).n by Def2;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th11:
  (seq1 + seq2) + seq3 = seq1 + (seq2 + seq3)
proof
  now
    let n;
    thus ((seq1+seq2)+seq3).n = (seq1+seq2).n+ seq3.n by Def2
      .= seq1.n+seq2.n+seq3.n by Def2
      .=seq1.n+(seq2.n+seq3.n) by EUCLID:30
      .=seq1.n+(seq2+seq3).n by Def2
      .=(seq1+(seq2+seq3)).n by Def2;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th12:
  -seq = (-1)*seq
proof
  now
    let n;
    thus ((-1)*seq).n=(-1)*seq.n by Def3
      .=-seq.n by EUCLID:43
      .=(-seq).n by Def4;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th13:
  r*(seq1 + seq2) = r*seq1 + r*seq2
proof
  now
    let n;
    thus (r*(seq1 + seq2)).n=r*(seq1+seq2).n by Def3
      .=r*(seq1.n+seq2.n) by Def2
      .=r*seq1.n+r*seq2.n by EUCLID:36
      .=(r*seq1).n+r*seq2.n by Def3
      .=(r*seq1).n+(r*seq2).n by Def3
      .=((r*seq1)+(r*seq2)).n by Def2;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th14:
  (r*q)*seq = r*(q*seq)
proof
  now
    let n;
    thus ((r*q)*seq).n=(r*q)*seq.n by Def3
      .=r*(q*seq.n) by EUCLID:34
      .=r*(q*seq).n by Def3
      .=(r*(q*seq)).n by Def3;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th15:
  r*(seq1 - seq2) = r*seq1 - r*seq2
proof
  thus r*(seq1-seq2)=r*seq1+r*(-seq2) by Th13
    .=r*seq1+r*((-1)*seq2) by Th12
    .=r*seq1+((-1)*r)*seq2 by Th14
    .=r*seq1+(-1)*(r*seq2) by Th14
    .=r*seq1-(r*seq2) by Th12;
end;

theorem
  seq1 - (seq2 + seq3) = seq1 - seq2 - seq3
proof
  thus seq1-(seq2+seq3)=seq1+(-1)*(seq2+seq3) by Th12
    .=seq1+((-1)*seq2+(-1)*seq3) by Th13
    .=seq1+(-seq2+(-1)*seq3) by Th12
    .=seq1+(-seq2+-seq3) by Th12
    .=seq1-seq2-seq3 by Th11;
end;

theorem Th17:
  1*seq=seq
proof
  now
    let n;
    thus (1*seq).n=1*seq.n by Def3
      .=seq.n by EUCLID:33;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th18:
  -(-seq) = seq
proof
  thus -(-seq)=(-1)*(-seq) by Th12
    .=(-1)*((-1)*seq) by Th12
    .=((-1)*(-1))*seq by Th14
    .=seq by Th17;
end;

theorem
  seq1 - (-seq2) = seq1 + seq2 by Th18;

theorem
  seq1 - (seq2 - seq3) = seq1 - seq2 + seq3
proof
  thus seq1-(seq2-seq3)=seq1+(-1)*(seq2-seq3) by Th12
    .=seq1+((-1)*seq2-((-1)*seq3)) by Th15
    .=seq1+(-seq2-((-1)*seq3)) by Th12
    .=seq1+(-seq2-(-seq3)) by Th12
    .=seq1+(-seq2+seq3) by Th18
    .=seq1-seq2+seq3 by Th11;
end;

theorem
  seq1 + (seq2 - seq3) = seq1 + seq2 - seq3 by Th11;

theorem Th22:
  r <> 0 & seq is non-zero implies r*seq is non-zero
proof
  assume that
A1: r<>0 and
A2: seq is non-zero and
A3: not r*seq is non-zero;
  consider n1 such that
A4: (r*seq).n1=0.TOP-REAL N by A3,Th4;
A5: seq.n1 <> 0.TOP-REAL N by A2,Th4;
  r*seq.n1=0.TOP-REAL N by A4,Def3;
  hence contradiction by A1,A5,EUCLID:35;
end;

theorem
  seq is non-zero implies -seq is non-zero
proof
  assume
  seq is non-zero;
  then (-1)*seq is non-zero by Th22;
  hence thesis by Th12;
end;

theorem Th24:
  |.0.TOP-REAL N.| = 0
proof
  thus |.0.TOP-REAL N.| = |.0*N.| by EUCLID:74
    .= 0 by EUCLID:10;
end;

theorem Th25:
  |.w.| = 0 implies w = 0.TOP-REAL N
proof
  reconsider s = w as Element of REAL N by EUCLID:25;
  assume
  |.w.| = 0;
  then s = 0*N by EUCLID:11
    .= 0.TOP-REAL N by EUCLID:74;
  hence thesis;
end;

theorem
  |.w.| >= 0;

theorem
  |.-w.| = |.w.| by EUCLID:75;

theorem Th28:
  |.w1 - w2.| = |.w2 - w1.|
proof
  thus |.w1 - w2.| = |.-(w1 - w2).| by EUCLID:75
    .= |.w2 - w1.| by EUCLID:48;
end;

Lm1: |.w1 - w2.| = 0 implies w1 = w2
proof
  assume
  |.w1 - w2.| = 0;
  then w1 - w2 = 0.TOP-REAL N by Th25;
  hence thesis by EUCLID:47;
end;

Lm2: w1 = w2 implies |.w1 - w2.| = 0
proof
  assume
  w1 = w2;
  then |.w1 - w2.| = |.0.TOP-REAL N.| by EUCLID:46
    .= 0 by Th24;
  hence thesis;
end;

theorem
  |.w1 - w2.| = 0 iff w1 = w2 by Lm1,Lm2;

theorem Th30:
  |.w1 + w2.| <= |.w1.| + |.w2.|
proof
  reconsider s1 = w1, s2 = w2 as Element of REAL N by EUCLID:25;
  w1 + w2 = s1 + s2 by EUCLID:68;
  hence thesis by EUCLID:15;
end;

theorem
  |.w1 - w2.| <= |.w1.| + |.w2.|
proof
  reconsider s1 = w1, s2 = w2 as Element of REAL N by EUCLID:25;
  w1 - w2 = s1 - s2 by EUCLID:73;
  hence thesis by EUCLID:16;
end;

theorem
  |.w1.| - |.w2.| <= |.w1 + w2.|
proof
  reconsider s1 = w1, s2 = w2 as Element of REAL N by EUCLID:25;
  w1 + w2 = s1 + s2 by EUCLID:68;
  hence thesis by EUCLID:17;
end;

theorem Th33:
  |.w1.| - |.w2.| <= |.w1 - w2.|
proof
  reconsider s1 = w1, s2 = w2 as Element of REAL N by EUCLID:25;
  w1 - w2 = s1 - s2 by EUCLID:73;
  hence thesis by EUCLID:18;
end;

theorem
  w1 <> w2 implies |.w1 - w2.| > 0
proof
  reconsider s1 = w1, s2 = w2 as Element of REAL N by EUCLID:25;
  s1 - s2 = w1 - w2 by EUCLID:73;
  hence thesis by EUCLID:20;
end;

theorem
  |.w1 - w2.| <= |.w1 - w.| + |.w - w2.|
proof
  0.TOP-REAL N = w - w by EUCLID:46
    .= -w + w by EUCLID:45;
  then |.w1 - w2.| = |.w1 + ((-w) + w) - w2.| by EUCLID:31
    .= |.w1 + (-w) + w - w2.| by EUCLID:30
    .= |.(w1 - w) + w - w2.| by EUCLID:45
    .= |.(w1 - w) + (w - w2).| by EUCLID:49;
  hence thesis by Th30;
end;

canceled 3;

definition
  let N;
  let IT be Real_Sequence of N;
  attr IT is bounded means
  :Def8:
  ex r st for n holds |.IT.n.| < r;
end;

theorem Th39:
  for n ex r st (0<r & for m st m<=n holds |.seq.m.| < r)
proof
  defpred X[Element of NAT] means ex r1 st 0<r1 & for m st m<=$1 holds |.seq.m
  .|<r1;
A1: for n st X[n] holds X[n+1]
  proof
    let n;
    given r1 such that
A2: 0<r1 and
A3: for m st m<=n holds |.seq.m.|<r1;
A4: now
      assume
A5:   r1<=|.seq.(n+1).|;
      take r=|.seq.(n+1).|+1;
      thus 0<r;
      let m such that
A6:   m<=n+1;
A7:   now
        assume
        m<=n;
        then |.seq.m.|<r1 by A3;
        then |.seq.m.|<|.seq.(n+1).| by A5,XXREAL_0:2;
        then |.seq.m.|+0<r by XREAL_1:10;
        hence |.seq.m.|<r;
      end;
      now
        assume
        m=n+1;
        then |.seq.m.|+0<r by XREAL_1:10;
        hence |.seq.m.|<r;
      end;
      hence |.seq.m.|<r by A6,A7,NAT_1:8;
    end;
    now
      assume
A8:   |.seq.(n+1).|<=r1;
      take r=r1+1;
      thus 0<r by A2;
      let m such that
A9:   m<=n+1;
A10:  now
        assume
        m<=n;
        then
A11:    |.seq.m.|<r1 by A3;
        r1+0<r by XREAL_1:10;
        hence |.seq.m.|<r by A11,XXREAL_0:2;
      end;
      now
A12:    r1+0<r by XREAL_1:10;
        assume
        m=n+1;
        hence |.seq.m.|<r by A8,A12,XXREAL_0:2;
      end;
      hence |.seq.m.|<r by A9,A10,NAT_1:8;
    end;
    hence thesis by A4;
  end;
A13: X[0]
  proof
    take r=|.seq.0 .|+1;
    thus 0<r;
    let m;
    assume
    m<=0;
    then 0=m by NAT_1:2;
    then |.seq.m.|+0<r by XREAL_1:10;
    hence thesis;
  end;
  thus for n holds X[n] from NAT_1:sch 1(A13,A1);
end;

definition
  let N;
  let IT be Real_Sequence of N;
  attr IT is convergent means
  :Def9:
  ex g st for r st 0<r ex n st for m st n<=
  m holds |.IT.m-g.| < r;
end;

definition
  let N,seq;
  assume
A1: seq is convergent;
  func lim seq -> Point of TOP-REAL N means
  :Def10:
  for r st 0<r ex n st for m
  st n<=m holds |.seq.m-it.| < r;
  existence by A1,Def9;
  uniqueness
  proof
    given g1,g2 such that
A2: for r st 0<r ex n st for m st n<=m holds |.seq.m-g1.|<r and
A3: for r st 0<r ex n st for m st n<=m holds |.seq.m-g2.|<r and
A4: g1<>g2;
A5: now
      assume
      |.g1-g2.|=0;
      then 0.TOP-REAL N+g2=g1-g2+g2 by Th25;
      then g2=g1-g2+g2 by EUCLID:31
        .=g1-(g2-g2) by EUCLID:51
        .=g1-0.TOP-REAL N by EUCLID:46
        .=g1+ -0.TOP-REAL N by EUCLID:45
        .=g1+(-1)*0.TOP-REAL N by EUCLID:43
        .=g1+0.TOP-REAL N by EUCLID:32
        .=g1 by EUCLID:31;
      hence contradiction by A4;
    end;
    then consider n1 such that
A6: for m st n1<=m holds |.seq.m-g1.|<|.g1-g2.|/4 by A2,XREAL_1:226;
    consider n2 such that
A7: for m st n2<=m holds |.seq.m-g2.|<|.g1-g2.|/4 by A3,A5,XREAL_1:
    226;
A8: |.seq.(n1+n2)-g2.|<|.g1-g2.|/4 by A7,NAT_1:12;
    |.seq.(n1+n2)-g1.|<|.g1-g2.|/4 by A6,NAT_1:12;
    then
A9: |.seq.(n1+n2)-g2.|+|.seq.(n1+n2)-g1.|<|.g1-g2.|/4+|.g1-g2.|/4 by
    A8,XREAL_1:10;
    |.g1-g2.|=|.g1-g2+0.TOP-REAL N.| by EUCLID:31
      .=|.g1-g2+(-1)*0.TOP-REAL N.| by EUCLID:32
      .= |.g1-g2+-0.TOP-REAL N.| by EUCLID:43
      .= |.g1-g2-0.TOP-REAL N.| by EUCLID:45
      .=|.g1-g2-(seq.(n1+n2)-seq.(n1+n2)).| by EUCLID:46
      .=|.g1-g2-seq.(n1+n2)+seq.(n1+n2).| by EUCLID:51
      .=|.g1-(seq.(n1+n2)+g2)+seq.(n1+n2).| by EUCLID:50
      .=|.g1-seq.(n1+n2)-g2+seq.(n1+n2).| by EUCLID:50
      .=|.g1-seq.(n1+n2)-(g2-seq.(n1+n2)).| by EUCLID:51
      .=|.g1-seq.(n1+n2)+-(g2-seq.(n1+n2)).| by EUCLID:45
      .=|.g1-seq.(n1+n2)+(seq.(n1+n2)-g2).| by EUCLID:48
      .=|.-(seq.(n1+n2)-g1)+(seq.(n1+n2)-g2).| by EUCLID:48;
    then |.g1-g2.|<=|.-(seq.(n1+n2)-g1).|+|.seq.(n1+n2)-g2.| by Th30;
    then
A10: |.g1-g2.|<=|.seq.(n1+n2)-g1.|+|.seq.(n1+n2)-g2.| by EUCLID:75;
    |.g1-g2.|/2 <|.g1-g2.| by A5,XREAL_1:218;
    hence contradiction by A9,A10,XXREAL_0:2;
  end;
end;

canceled;

theorem Th41:
  seq is convergent & seq' is convergent implies seq + seq' is
  convergent
proof
  assume that
A1: seq is convergent and
A2: seq' is convergent;
  consider g1 such that
A3: for r st 0<r ex n st for m st n<=m holds |.seq.m-g1.|<r by A1,Def9;
  consider g2 such that
A4: for r st 0<r ex n st for m st n<=m holds |.seq'.m-g2.|<r by A2,Def9;
  take g=g1+g2;
  let r;
  assume
A5: 0<r;
  then consider n1 such that
A6: for m st n1<=m holds |.seq.m-g1.|<r/2 by A3,XREAL_1:217;
  consider n2 such that
A7: for m st n2<=m holds |.seq'.m-g2.|<r/2 by A4,A5,XREAL_1:217;
  take k=n1+n2;
  let m;
  assume
A8: k<=m;
  n2<=k by NAT_1:12;
  then n2<=m by A8,XXREAL_0:2;
  then
A9: |.seq'.m-g2.|<r/2 by A7;
A10: |.(seq+seq').m-g.|=|.seq.m+seq'.m-(g1+g2).| by Def2
    .=|.seq.m+(seq'.m-(g1+g2)).| by EUCLID:49
    .=|.seq.m+((-(g1+g2))+seq'.m).| by EUCLID:45
    .=|.seq.m+-(g1+g2)+seq'.m.| by EUCLID:30
    .=|.seq.m-(g1+g2)+seq'.m.| by EUCLID:45
    .=|.seq.m-g1-g2+seq'.m.| by EUCLID:50
    .=|.seq.m-g1+-g2+seq'.m.| by EUCLID:45
    .=|.seq.m-g1+(seq'.m+-g2).| by EUCLID:30
    .=|.seq.m-g1+(seq'.m-g2).| by EUCLID:45;
A11: |.seq.m-g1+(seq'.m-g2).|<=|.seq.m-g1.|+|.seq'.m-g2.| by Th30;
  n1<=n1+n2 by NAT_1:12;
  then n1<=m by A8,XXREAL_0:2;
  then |.seq.m-g1.|<r/2 by A6;
  then |.seq.m-g1.|+|.seq'.m-g2.|<r/2+r/2 by A9,XREAL_1:10;
  hence thesis by A10,A11,XXREAL_0:2;
end;

theorem Th42:
  seq is convergent & seq' is convergent implies lim (seq + seq')=
  (lim seq)+(lim seq')
proof
  assume that
A1: seq is convergent and
A2: seq' is convergent;
A3: now
    let r;
    assume
    0<r;
    then
A4: 0<r/2 by XREAL_1:217;
    then consider n1 such that
A5: for m st n1<=m holds |.seq.m-lim (seq).|<r/2 by A1,Def10;
    consider n2 such that
A6: for m st n2<=m holds |.seq'.m-lim (seq').|<r/2 by A2,A4,Def10;
    take k=n1+n2;
    let m such that
A7: k<=m;
    n2<=k by NAT_1:12;
    then n2<=m by A7,XXREAL_0:2;
    then
A8: |.seq'.m-(lim seq').|<r/2 by A6;
A9: |.((seq+seq').m)-((lim seq)+(lim seq')).| =|.((seq+seq').m)-(lim seq)
    -(lim seq').| by EUCLID:50
      .=|.seq.m+seq'.m-(lim seq)-(lim seq').| by Def2
      .=|.seq.m+(seq'.m-(lim seq))-(lim seq').| by EUCLID:49
      .=|.seq.m+(-(lim seq)+seq'.m)-(lim seq').| by EUCLID:45
      .=|.seq.m+-(lim seq)+seq'.m-(lim seq').| by EUCLID:30
      .=|.seq.m-(lim seq)+seq'.m-(lim seq').| by EUCLID:45
      .=|.seq.m-(lim seq)+(seq'.m-(lim seq')).| by EUCLID:49;
A10: |.seq.m-(lim seq)+(seq'.m-(lim seq')).|<= |.seq.m-(lim seq).|+|.seq'.
    m-(lim seq').| by Th30;
    n1<=n1+n2 by NAT_1:12;
    then n1<=m by A7,XXREAL_0:2;
    then |.seq.m-lim(seq).|<r/2 by A5;
    then |.seq.m-(lim seq).|+|.seq'.m-(lim seq').|<r/2+r/2 by
    A8, XREAL_1
    :10;
    hence |.((seq+seq').m)-((lim seq)+(lim seq')).|<r by A9,A10,XXREAL_0:2;
  end;
  seq+seq' is convergent by A1,A2,Th41;
  hence thesis by A3,Def10;
end;

theorem Th43:
  seq is convergent implies r*seq is convergent
proof
  assume
  seq is convergent;
  then consider g1 such that
A1: for q st 0<q ex n st for m st n<=m holds |.seq.m-g1.|<q by Def9;
  set g=r*g1;
A2: now
A3: 0/abs(r)=0;
    assume
A4: r<>0;
    then
A5: 0<abs(r) by COMPLEX1:133;
    let q;
    assume
    0<q;
    then consider n1 such that
A6: for m st n1<=m holds |.seq.m-g1.|<q/abs(r) by A1,A5,A3,
    XREAL_1:76;
    take k=n1;
    let m;
    assume
    k<=m;
    then
A7: |.seq.m-g1.|<q/abs(r) by A6;
A8: 0<abs(r) by A4,COMPLEX1:133;
A9: abs(r)*(q/abs(r))=abs(r)*(abs(r)"*q) by XCMPLX_0:def 9
      .=abs(r)*abs(r)"*q
      .=1*q by A8,XCMPLX_0:def 7
      .=q;
    |.((r*seq).m)-g.|=|.r*seq.m-r*g1.| by Def3
      .=|.r*(seq.m-g1).| by EUCLID:53
      .=abs(r)*|.seq.m-g1.| by Th8;
    hence |.((r*seq).m)-g.|<q by A5,A7,A9,XREAL_1:70;
  end;
  now
    assume
A10: r=0;
    let q such that
A11: 0<q;
    take k=0;
    let m such that
    k<=m;
    |.((r*seq).m)-g.|=|.((0 *seq).m)-0.TOP-REAL N.| by A10,EUCLID:33
      .=|.0.TOP-REAL N-((0 * seq).m).| by Th28
      .=|.0.TOP-REAL N+-((0 * seq).m).| by EUCLID:45
      .=|.-((0 * seq).m).| by EUCLID:31
      .=|.(0 * seq).m.| by EUCLID:75
      .=|.0 * seq.m.| by Def3
      .=|.0.TOP-REAL N.| by EUCLID:33
      .=0 by Th24;
    hence |.((r*seq).m)-g.|<q by A11;
  end;
  hence thesis by A2,Def9;
end;

theorem Th44:
  seq is convergent implies lim(r*seq)=r*(lim seq)
proof
A1: now
    assume
A2: r=0;
    let q such that
A3: 0<q;
    take k=0;
    let m such that
    k<=m;
    r*(lim seq)=0.TOP-REAL N by A2,EUCLID:33;
    then |.((r*seq).m)-r*(lim seq).|=|.0 * seq.m-0.TOP-REAL N.| by A2,Def3
      .=|.0.TOP-REAL N-0 * seq.m.| by Th28
      .=|.0.TOP-REAL N+-0 * seq.m.| by EUCLID:45
      .=|.-0 * seq.m.| by EUCLID:31
      .=|.0 * seq.m.| by EUCLID:75
      .=|.0.TOP-REAL N.| by EUCLID:33
      .=0 by Th24;
    hence |.((r*seq).m)-r*(lim seq).|<q by A3;
  end;
  assume
A4: seq is convergent;
A5: now
A6: 0/abs(r)=0;
    assume
A7: r<>0;
    then
A8: 0<abs(r) by COMPLEX1:133;
    let q;
    assume
    0<q;
    then 0<q/abs(r) by A8,A6,XREAL_1:76;
    then consider n1 such that
A9: for m st n1<=m holds |.seq.m-(lim seq).|<q/abs(r) by A4,Def10;
    take k=n1;
    let m;
    assume
    k<=m;
    then
A10: |.seq.m-(lim seq).|<q/abs(r) by A9;
A11: 0<>abs(r) by A7,COMPLEX1:133;
A12: abs(r)*(q/abs(r))=abs(r)*(abs(r)"*q) by XCMPLX_0:def 9
      .=abs(r)*abs(r)"*q
      .=1*q by A11,XCMPLX_0:def 7
      .=q;
    |.((r*seq).m)-r*(lim seq).|=|.r*seq.m-r*(lim seq).| by Def3
      .=|.r*(seq.m-(lim seq)).| by EUCLID:53
      .=abs(r)*|.seq.m-(lim seq).| by Th8;
    hence |.((r*seq).m)-r*(lim seq).|<q by A8,A10,A12,XREAL_1:70;
  end;
  r*seq is convergent by A4,Th43;
  hence thesis by A1,A5,Def10;
end;

theorem Th45:
  seq is convergent implies - seq is convergent
proof
  assume
  seq is convergent;
  then (-1)*seq is convergent by Th43;
  hence thesis by Th12;
end;

theorem Th46:
  seq is convergent implies lim(-seq)=-(lim seq)
proof
  assume
  seq is convergent;
  then lim ((-1)*seq)=(-1)*(lim seq) by Th44
    .=-(1*(lim seq)) by EUCLID:44
    .=-(lim seq) by EUCLID:33;
  hence thesis by Th12;
end;

theorem
  seq is convergent & seq' is convergent implies seq - seq' is
  convergent
proof
  assume that
A1: seq is convergent and
A2: seq' is convergent;
  -seq' is convergent by A2,Th45;
  hence thesis by A1,Th41;
end;

theorem
  seq is convergent & seq' is convergent implies lim(seq - seq')=(lim
  seq)-(lim seq')
proof
  assume that
A1: seq is convergent and
A2: seq' is convergent;
  - seq' is convergent by A2,Th45;
  hence lim(seq - seq')=(lim seq)+(lim(- seq')) by A1,Th42
    .=(lim seq)+-(lim seq') by A2,Th46
    .=(lim seq)-(lim seq') by EUCLID:45;
end;

canceled;

theorem
  seq is convergent implies seq is bounded
proof
  assume
  seq is convergent;
  then consider g such that
A1: for r st 0<r ex n st for m st n<=m holds |.seq.m-g.|<r by Def9;
  consider n1 such that
A2: for m st n1<=m holds |.seq.m-g.|<1 by A1;
A3: now
    take r=|.g.|+1;
    thus 0<r;
    let m;
A4: |.seq.m.|-|.g.|+|.g.|=|.seq.m.|;
    assume
    n1<=m;
    then
A5: |.seq.m-g.|<1 by A2;
    |.seq.m.|-|.g.|<=|.seq.m-g.| by Th33;
    then |.seq.m.|-|.g.|<1 by A5,XXREAL_0:2;
    hence |.seq.m.|<r by A4,XREAL_1:8;
  end;
  now
    consider r2 such that
A6: 0<r2 and
A7: for m st m<=n1 holds |.seq.m.|<r2 by Th39;
    consider r1 such that
A8: 0<r1 and
A9: for m st n1<=m holds |.seq.m.|<r1 by A3;
    take r=r1+r2;
    thus 0<r by A8,A6;
    let m;
A10: 0+r2<r by A8,XREAL_1:10;
A11: now
      assume
      m<=n1;
      then |.seq.m.|<r2 by A7;
      hence |.seq.m.|<r by A10,XXREAL_0:2;
    end;
A12: r1+0<r by A6,XREAL_1:10;
    now
      assume
      n1<=m;
      then |.seq.m.|<r1 by A9;
      hence |.seq.m.|<r by A12,XXREAL_0:2;
    end;
    hence |.seq.m.|<r by A11;
  end;
  hence thesis by Def8;
end;

theorem
  seq is convergent implies ((lim seq) <> 0.TOP-REAL N implies ex n st
  for m st n<=m holds |.(lim seq).|/2 < |.seq.m.|)
proof
  assume that
A1: seq is convergent and
A2: (lim seq)<>0.TOP-REAL N;
  0 <> |.(lim seq).| by A2,Th25;
  then 0<|.(lim seq).|/2 by XREAL_1:217;
  then consider n1 such that
A3: for m st n1<=m holds |.seq.m-(lim seq).|<|.(lim seq).|/2 by A1,
  Def10;
  take n=n1;
  let m;
  assume
  n<=m;
  then
A4: |.seq.m-(lim seq).|<|.(lim seq).|/2 by A3;
  |.(lim seq).|-|.seq.m.|<=|.(lim seq)-seq.m.| & |.(lim seq)-seq.m.|=|.seq
  .m-( lim seq).| by Th28,Th33;
  then
A5: |.(lim seq).|-|.seq.m.|<|.(lim seq).|/2 by A4,XXREAL_0:2;
  |.(lim seq).|/2+(|.seq.m.|-|.(lim seq).|/2) =|.seq.m.| & |.(lim seq).|-
  |.seq .m.|+(|.seq.m.|-|.(lim seq).|/2) =|.(lim seq).|/2;
  hence thesis by A5,XREAL_1:8;
end;

