:: The Relevance of Measure and Probability and Definition of Completeness
:: of Probability
::  by Bo Zhang , Hiroshi Yamazaki and Yatsuka Nakamura
::
:: Received November 23, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies RELAT_1, PROB_1, SEQ_2, ARYTM_1, ORDINAL2, BOOLE, SUBSET_1,
      PROB_2, SETFAM_1, TARSKI, LATTICES, SERIES_1, MEASURE3, PROB_3, RLVECT_1,
      FUNCT_1, PROB_4, MEASURE1, SUPINF_2, SUPINF_1, MEASURE6, ARYTM;
 notations TARSKI, XBOOLE_0, REAL_1, RELAT_1, CARD_3, ORDINAL1, MEASURE3,
      SUPINF_2, XCMPLX_0, XXREAL_0, NAT_1, SEQ_2, SETFAM_1, FUNCT_1, RELSET_1,
      SUBSET_1, NUMBERS, SUPINF_1, PARTFUN1, FUNCT_2, PROB_1, PROB_3, MEASURE1,
      SEQM_3, SERIES_1, RINFSUP1, ZFMISC_1, MEASURE6, PROB_2;
 constructors PARTFUN1, DOMAIN_1, REAL_1, NAT_1, SEQM_3, SERIES_1, MEASURE3,
      MEASURE6, PARTFUN3, KURATO_2, RINFSUP1, PROB_3, VALUED_1, SEQ_1;
 registrations XBOOLE_0, SUBSET_1, RELAT_1, ORDINAL1, FUNCT_2, NUMBERS,
      XXREAL_0, XREAL_0, NAT_1, MEMBERED, SEQ_1, MEASURE1, MEASURE3, VALUED_0,
      VALUED_1;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, XBOOLE_0, SERIES_1, SUBSET_1, VALUED_1;
 theorems FUNCT_1, FUNCT_2, SEQ_1, SEQ_2, XREAL_1, SUPINF_1, NAT_1, TARSKI,
      XBOOLE_0, XBOOLE_1, PROB_2, RINFSUP1, SUPINF_2, SEQ_4, SEQM_3, CARD_3,
      SETLIM_1, PROB_1, SERIES_1, PROB_3, MEASURE1, MEASURE3, MEASURE6,
      ZFMISC_1, XXREAL_0, ORDINAL1, NUMBERS;
 schemes FUNCT_2, NAT_1, PARTFUN1, XBOOLE_0, KURATO_2;

begin

reserve n,m,k for Element of NAT,
  x,X for set,
  A1 for SetSequence of X,
  Si for SigmaField of X,
  XSeq for SetSequence of Si;

reserve Omega for non empty set,
  Sigma for SigmaField of Omega,
  ASeq for SetSequence of Sigma,
  P for Probability of Sigma;

Lm1: for A,B,C being set holds
C c= B implies A \ C = (A \ B) \/ (A /\ (B \ C))
proof
  let A,B,C be set;
  assume
A1: C c= B;
  thus A \ C c= (A \ B) \/ (A /\ (B \ C))
  proof
    let x be set;
    assume x in A \ C;
    then (x in A & not x in B) or (x in A & x in B & not x in C)
    by XBOOLE_0:def 4;
    then x in A \ B or (x in A & x in B \ C) by XBOOLE_0:def 4;
    then x in A \ B or x in A /\ (B \ C) by XBOOLE_0:def 3;
    hence thesis by XBOOLE_0:def 2;
  end;
  let x be set;
  assume x in (A \ B) \/ (A /\ (B \ C));
  then x in A \ B or x in (A /\ (B \ C)) by XBOOLE_0:def 2;
  then x in A \ B or (x in A & x in B \ C) by XBOOLE_0:def 3;
  then x in A & not x in C by A1,XBOOLE_0:def 4;
  hence thesis by XBOOLE_0:def 4;
end;

definition
  let X,Si,XSeq,n;
  redefine func XSeq.n -> Element of Si;
  coherence by PROB_1:def 9;
end;

theorem Th1:
  rng XSeq c= Si
proof
  let x such that
A1: x in rng XSeq;
  consider n such that
A2: x = XSeq.n by A1,SETLIM_1:7;
  thus thesis by A2;
end;

theorem Th2:
  for f being Function holds
  f is SetSequence of Si iff f is Function of NAT,Si
proof
  let f be Function;
  thus f is SetSequence of Si implies f is Function of NAT,Si
  proof
    assume f is SetSequence of Si;
    then reconsider f as SetSequence of Si;
A1: rng f c= Si by Th1;
    dom f = NAT by FUNCT_2:def 1;
    hence thesis by A1,FUNCT_2:4;
  end;
  assume
A2: f is Function of NAT,Si;
  then
A3: for n holds f.n in Si by FUNCT_2:7;
  reconsider f as SetSequence of X by A2,FUNCT_2:9;
  f is SetSequence of Si by A3,PROB_1:def 9;
  hence thesis;
end;

scheme LambdaSigmaSSeq { X() -> set, Si() -> SigmaField of X(),
  F(set) -> Element of Si() } :
  ex f being SetSequence of Si() st for n holds f.n = F(n)
proof
  set Y = Si();
  ex f being SetSequence of X() st for n holds f.n = F(n) from KURATO_2:sch 1;
  then consider f be SetSequence of X() such that
A1: for n holds f.n = F(n);
  for n holds f.n in Si()
  proof
    let n;
    f.n = F(n) by A1;
    hence thesis;
  end;
  then reconsider f as SetSequence of Si() by PROB_1:def 9;
  take f;
  thus thesis by A1;
end;

registration
  let X;
  cluster disjoint_valued SetSequence of X;
  existence
  proof
    reconsider Y = {} as Subset of X by XBOOLE_1:2;
    deffunc F(Element of NAT) = Y;
    ex f being SetSequence of X st for n holds f.n = F(n) from KURATO_2:sch 1;
    then consider A1 being SetSequence of X such that
A1: for n holds A1.n = {};
    take A1;
    for m,n being Nat st m <> n holds A1.m misses A1.n
    proof
      let m,n be Nat;
      m in NAT & n in NAT by ORDINAL1:def 13;
      then A1.m = {} & A1.n = {} by A1;
      hence thesis by XBOOLE_1:65;
    end;
    hence thesis by PROB_3:def 4;
  end;
end;

registration
  let X,Si;
  cluster disjoint_valued SetSequence of Si;
  existence
  proof
    reconsider MSi = Si as SigmaField of X;
    consider f being disjoint_valued Function of NAT,MSi;
    reconsider f as SetSequence of Si by Th2;
    take f;
    thus thesis;
  end;
end;

theorem Th3:
  for A, B being Subset of X holds
  ex A1 st A1.0 = A & A1.1 = B & for n st n > 1 holds A1.n = {}
proof
  let A,B be Subset of X;
  defpred P[set] means $1 = 0;
  deffunc F(set) = A;
  deffunc G(set) = {};
  ex f being Function st dom f = NAT &
  for x st x in NAT holds (P[x] implies f.x=F(x)) &
  (not P[x] implies f.x=G(x)) from PARTFUN1:sch 1;
  then consider f1 being Function such that
A1: dom f1 = NAT & for x st x in NAT holds
  (x = 0 implies f1.x = A) & (x <> 0 implies f1.x = {});
  defpred P[set] means $1 <> 1;
  deffunc F(set) = f1.$1;
  deffunc G(set) = B;
  ex f being Function st dom f = NAT & for x st x in NAT holds
  (P[x] implies f.x=F(x)) & (not P[x] implies f.x=G(x)) from PARTFUN1:sch 1;
  then consider f2 being Function such that
A2: dom f2 = NAT & for x st x in NAT holds
  (x <> 1 implies f2.x = f1.x) & (not x <> 1 implies f2.x = B);
A3: A in bool X & B in bool X;
  {} c= X by XBOOLE_1:2;
  then
A4: {} in bool X;
  for x st x in NAT holds f2.x in bool X
  proof
    let x;
    assume
A5: x in NAT;
    per cases;
    suppose x <> 1;
      then
A6:   f2.x = f1.x by A2,A5;
      per cases;
      suppose x <> 0;
        hence thesis by A1,A4,A5,A6;
      end;
      suppose x = 0;
        then f1.x = A by A1;
        hence thesis by A6;
      end;
    end;
    suppose x = 1;
      hence thesis by A2,A3;
    end;
  end;
  then reconsider f2 as SetSequence of X by A2,FUNCT_2:5;
A7: f2.0 = A & f2.1 = B & for n st n > 1 holds f2.n = {}
  proof
    thus f2.0 = f1.0 by A2
      .= A by A1;
    thus f2.1 = B by A2;
    let n such that
A8: n > 1;
    thus f2.n = f1.n by A2,A8
      .= {} by A1,A8;
  end;
  take f2;
  thus thesis by A7;
end;

theorem Th4:
  for A, B being Subset of X st
  A misses B & (A1.0 = A & A1.1 = B & for n st n > 1 holds A1.n = {}) holds
  A1 is disjoint_valued & Union A1 = A \/ B
proof
  let A,B be Subset of X;
  assume that
A1: A misses B and
A2: A1.0 = A & A1.1 = B & for n st n > 1 holds A1.n = {};
  thus A1 is disjoint_valued
  proof
    for m,n being Nat st m <> n holds A1.m misses A1.n
    proof
      let m,n be Nat such that
A3:   m <> n;
A4:   m in NAT & n in NAT by ORDINAL1:def 13;
      per cases;
      suppose
A5:     m = 0;
        then n >= 1 by A3,NAT_1:14;
        then
A6:     n + 1 > 1 by NAT_1:13;
        per cases by A6,NAT_1:22;
        suppose n > 1;
          then A1.n = {} by A2,A4;
          hence thesis by XBOOLE_1:65;
        end;
        suppose n = 1;
          hence thesis by A1,A2,A5;
        end;
      end;
      suppose
A7:     m <> 0 & m = 1;
        n >= 1 or n <= 1;
        then
A8:     n + 1 > 1 or n < 1 + 1 by NAT_1:13;
        per cases by A3,A7,A8,NAT_1:14,22;
        suppose n > 1;
          then A1.n = {} by A2,A4;
          hence thesis by XBOOLE_1:65;
        end;
        suppose n = 0;
          hence thesis by A1,A2,A7;
        end;
      end;
      suppose
A9:     m <> 0 & m <> 1;
        then m >= 1 by NAT_1:14;
        then m +1 > 1 by NAT_1:13;
        then A1.m = {} by A2,A4,A9,NAT_1:22;
        hence thesis by XBOOLE_1:65;
      end;
    end;
    hence thesis by PROB_3:def 4;
  end;
  thus Union A1 = A \/ B
  proof
    thus Union A1 c= A \/ B
    proof
      let x;
      assume x in Union A1;
      then consider n such that
A10:  x in A1.n by PROB_1:25;
      not n > 1 by A2,A10;
      then
A11:  n < 1 + 1 by NAT_1:13;
      per cases by A11,NAT_1:14,22;
      suppose n = 0;
        hence thesis by A2,A10,XBOOLE_0:def 2;
      end;
      suppose n = 1;
        hence thesis by A2,A10,XBOOLE_0:def 2;
      end;
    end;
    let x;
    assume x in A \/ B;
    then x in A1.0 or x in A1.1 by A2,XBOOLE_0:def 2;
    hence thesis by PROB_1:25;
  end;
end;

theorem Th5:
  for S being non empty set holds S is SigmaField of X iff S c= bool X &
  (for A1 being SetSequence of X st
  (for n holds A1.n in S) holds Union A1 in S) &
  (for A being Subset of X st A in S holds A` in S)
proof
  let S be non empty set;
  thus S is SigmaField of X implies S c= bool X &
  (for A1 being SetSequence of X st
  (for n holds A1.n in S) holds Union A1 in S) &
  (for A being Subset of X st A in S holds A` in S)
  proof
    assume S is SigmaField of X;
    then reconsider S as SigmaField of X;
    (for A1 being SetSequence of X st
    (for n holds A1.n in S) holds Union A1 in S)
    proof
      let A1 be SetSequence of X such that
A1:   for n holds A1.n in S;
      reconsider A1 as SetSequence of S by A1,PROB_1:def 9;
      Union A1 in S by PROB_1:46;
      hence thesis;
    end;
    hence thesis by PROB_1:32;
  end;
  assume
A2: S c= bool X & (for A1 being SetSequence of X st
  (for n holds A1.n in S) holds Union A1 in S) &
  (for A being Subset of X st A in S holds A` in S);
  (for A1 being SetSequence of X st
  (for n holds A1.n in S) holds Intersection A1 in S)
  proof
    let A1 be SetSequence of X such that
A3: for n holds A1.n in S;
A4: (for n holds A1.n in S) implies (for n holds (Complement A1).n in S)
    proof
      assume
A5:   for n holds A1.n in S;
      for n holds (Complement A1).n in S
      proof
        let n;
        A1.n in S by A5;
        then (A1.n)` in S by A2;
        hence thesis by PROB_1:def 4;
      end;
      hence thesis;
    end;
    (for n holds A1.n in S) implies (Union Complement A1)` in S
    proof
      assume for n holds A1.n in S;
      then Union Complement A1 in S by A2,A4;
      hence thesis by A2;
    end;
    hence thesis by A3,PROB_1:def 5;
  end;
  hence thesis by A2,PROB_1:32;
end;

theorem Th6:
  for A,B being Event of Sigma holds P.(A \ B) = P.(A \/ B) - P.B
proof
  let A,B be Event of Sigma;
  P.(A \/ B) - P.B = P.B + P.(A \ B) - P.B by PROB_1:72;
  hence thesis;
end;

theorem Th7:
  for A,B being Event of Sigma st A c= B & P.B = 0 holds P.A = 0
proof
  let A,B be Event of Sigma such that
A1: A c= B & P.B = 0;
  P.A <= 0 by A1,PROB_1:70;
  hence thesis by PROB_1:def 13;
end;

theorem Th8:
  (for n holds P.(ASeq.n) = 0) iff P.(Union ASeq) = 0
proof
  hereby
    assume
A1: for n holds P.(ASeq.n) = 0;
    for n holds Partial_Sums(P * ASeq).n = 0
    proof
      defpred P[Element of NAT] means Partial_Sums(P * ASeq).$1 = 0;
A2:   P[0]
      proof
        thus Partial_Sums(P * ASeq).0 = (P * ASeq).0 by SERIES_1:def 1
          .= P.(ASeq.0) by PROB_2:11
          .= 0 by A1;
      end;
A3:   for k st P[k] holds P[k+1]
      proof
        let k such that
A4:     P[k];
        thus Partial_Sums(P * ASeq).(k+1)
        = Partial_Sums(P * ASeq).k + (P * ASeq).(k+1) by SERIES_1:def 1
          .= 0 + P.(ASeq.(k+1)) by A4,PROB_2:11
          .= 0 by A1;
      end;
      thus for k holds P[k] from NAT_1:sch 1(A2,A3);
    end;
    then for n st 0 <= n holds Partial_Sums(P * ASeq).n = 0;
    then
A5: Partial_Sums(P * ASeq) is convergent &
    lim Partial_Sums(P * ASeq) = 0 by PROB_1:3;
A6: P.(Union ASeq) <= 0
    proof
      @Partial_Diff_Union ASeq is disjoint_valued by PROB_3:41;
      then
A7:   Partial_Sums(P * @Partial_Diff_Union ASeq) is convergent by PROB_3:50;
      now
        let n;
        (@Partial_Diff_Union ASeq).n c= ASeq.n by PROB_3:37;
        hence (P * @Partial_Diff_Union ASeq).n <= (P * ASeq).n by PROB_3:5;
      end;
      then for n holds (Partial_Sums(P * @Partial_Diff_Union ASeq)).n
      <= (Partial_Sums(P * ASeq)).n by SERIES_1:17;
      then Sum (P * @Partial_Diff_Union ASeq) <= 0 by A5,A7,SEQ_2:32;
      then P.(Union @Partial_Diff_Union ASeq) <= 0 by PROB_3:41,51;
      hence thesis by PROB_3:40;
    end;
    P.(Union ASeq) >= 0
    proof
      Union ASeq is Event of Sigma by PROB_1:58;
      hence thesis by PROB_1:def 13;
    end;
    hence P.(Union ASeq) = 0 by A6;
  end;
  assume
A8: P.(Union ASeq) = 0;
  hereby
    let n;
    reconsider Y1 = ASeq.n as Event of Sigma by PROB_1:def 10;
    reconsider Y2 = Union ASeq as Event of Sigma by PROB_1:58;
    Y1 c= Y2
    proof
      ASeq.n in rng ASeq by SETLIM_1:7;
      then ASeq.n c= union rng ASeq by ZFMISC_1:92;
      hence thesis by CARD_3:def 4;
    end;
    hence P.(ASeq.n) = 0 by A8,Th7;
  end;
end;

theorem Th9:
  (for A being set st A in rng ASeq holds P.A = 0) iff P.(union rng ASeq) = 0
proof
  hereby
    assume
A1: for A being set st A in rng ASeq holds P.A = 0;
    for n holds P.(ASeq.n) = 0
    proof
      let n;
      ASeq.n in rng ASeq by SETLIM_1:7;
      hence thesis by A1;
    end;
    then P.(Union ASeq) = 0 by Th8;
    hence P.(union rng ASeq) = 0 by CARD_3:def 4;
  end;
  assume P.(union rng ASeq) = 0;
  then
A2: P.(Union ASeq) = 0 by CARD_3:def 4;
  hereby
    let A be set such that
A3: A in rng ASeq;
    consider n1 being Element of NAT such that
A4: A = ASeq.n1 by A3,SETLIM_1:7;
    thus P.A = 0 by A2,A4,Th8;
  end;
end;

theorem Th10:
  for seq being Function of NAT,REAL,
  Eseq being Function of NAT,ExtREAL st seq = Eseq holds
  Partial_Sums seq = Ser Eseq
proof
  let seq be Function of NAT,REAL, Eseq be Function of NAT,ExtREAL such that
A1: seq = Eseq;
  reconsider Ps = Partial_Sums seq as Function of NAT, ExtREAL
  by FUNCT_2:9,NUMBERS:31;
  defpred P[Element of NAT] means Ps.$1 = (Ser Eseq).$1;
A2: P[0]
  proof
    thus Ps.0 = Eseq.0 by A1,SERIES_1:def 1
      .= (Ser Eseq).0 by SUPINF_2:63;
  end;
A3: for k st P[k] holds P[k+1]
  proof
    let k such that
A4: Ps.k = (Ser Eseq).k;
    Ps.k = (Partial_Sums seq).k;
    then reconsider Psk = Ps.k as Real;
A5: Ps.(k+1) = Psk + seq.(k+1) by SERIES_1:def 1;
    (Ser Eseq).(k+1) = (Ser Eseq).k + Eseq.(k+1) by SUPINF_2:63;
    hence thesis by A1,A4,A5,SUPINF_2:1;
  end;
  for k holds P[k] from NAT_1:sch 1(A2,A3);
  hence thesis by FUNCT_2:113;
end;

theorem Th11:
  for seq being Function of NAT,REAL, Eseq being Function of NAT,ExtREAL st
  seq = Eseq & seq is bounded_above holds sup seq = sup rng Eseq
proof
  let seq be Function of NAT,REAL, Eseq be Function of NAT,ExtREAL such that
A1: seq = Eseq & seq is bounded_above;
A2: dom Eseq = NAT & dom seq = NAT by FUNCT_2:def 1;
  reconsider s = sup seq as R_eal by XXREAL_0:def 1;
  for x being ext-real number holds x in rng Eseq implies x <= s
  proof
    let x be ext-real number;
    assume x in rng Eseq;
    then consider n1 being set such that
A3: n1 in NAT & Eseq.n1 = x by A2,FUNCT_1:def 5;
    reconsider n1 as Element of NAT by A3;
    thus thesis by A1,A3,RINFSUP1:7;
  end;
  then
A4: s is majorant of rng Eseq by SUPINF_1:def 9;
  then
A5: rng Eseq is bounded_above by SUPINF_1:def 11;
A6: rng Eseq <> {-infty}
  proof
    assume rng Eseq = {-infty};
    then reconsider k1 = -infty as Element of rng Eseq by TARSKI:def 1;
    consider n1 being set such that
A7: n1 in NAT & Eseq.n1 = k1 by A2,FUNCT_1:def 5;
    reconsider n1 as Element of NAT by A7;
    seq.n1 = k1 by A1,A7;
    hence contradiction;
  end;
  then
A8: sup rng Eseq in REAL by A5,SUPINF_1:83;
A9: sup rng Eseq <= s by A4,SUPINF_1:def 16;
  reconsider Eseq1=Eseq as Function of NAT,[#]ExtREAL;
  s <= sup rng Eseq
  proof
A10: sup rng Eseq is majorant of rng Eseq by SUPINF_1:def 16;
    reconsider r1=sup rng Eseq as Real by A5,A6,SUPINF_1:83;
    for n holds seq.n <= r1
    proof
      let n;
A11:  seq.n = Eseq.n by A1;
      Eseq.n in rng Eseq by A2,FUNCT_1:12;
      then Eseq.n <= sup rng Eseq by A10,SUPINF_1:def 9;
      then consider r2,r3 being Real such that
A12:  r2 = Eseq.n & r3 = sup rng Eseq & r2 <= r3 by A8,A11;
      thus thesis by A1,A12;
    end;
    hence thesis by RINFSUP1:9;
  end;
  hence thesis by A9,XXREAL_0:1;
end;

theorem Th12:
  for seq being Function of NAT,REAL, Eseq being Function of NAT,ExtREAL st
  seq = Eseq & seq is bounded_below holds inf seq = inf rng Eseq
proof
  let seq be Function of NAT,REAL, Eseq be Function of NAT,ExtREAL such that
A1: seq = Eseq & seq is bounded_below;
A2: dom Eseq = NAT & dom seq = NAT by FUNCT_2:def 1;
  reconsider s=inf seq as R_eal by XXREAL_0:def 1;
  for x being ext-real number holds x in rng Eseq implies s <= x
  proof
    let x be ext-real number;
    assume x in rng Eseq;
    then consider n1 being set such that
A3: n1 in NAT & Eseq.n1 = x by A2,FUNCT_1:def 5;
    reconsider n1 as Element of NAT by A3;
    thus thesis by A1,A3,RINFSUP1:8;
  end;
  then
A4: s is minorant of rng Eseq by SUPINF_1:def 10;
  then
A5: rng Eseq is bounded_below by SUPINF_1:def 12;
A6: rng Eseq <> {+infty}
  proof
    assume rng Eseq = {+infty};
    then reconsider k1 = +infty as Element of rng Eseq by TARSKI:def 1;
    consider n1 being set such that
A7: n1 in NAT & Eseq.n1 = k1 by A2,FUNCT_1:def 5;
    reconsider n1 as Element of NAT by A7;
    seq.n1 = k1 by A1,A7;
    hence contradiction;
  end;
  then
A8: inf rng Eseq in REAL by A5,SUPINF_1:84;
A9: s <= inf rng Eseq by A4,SUPINF_1:def 17;
  reconsider Eseq1=Eseq as Function of NAT,[#]ExtREAL;
  inf rng Eseq <= s
  proof
A10: inf rng Eseq is minorant of rng Eseq by SUPINF_1:def 17;
    reconsider r1=inf rng Eseq as Real by A5,A6,SUPINF_1:84;
    for n holds r1 <= seq.n
    proof
      let n;
A11:  seq.n = Eseq.n by A1;
      Eseq.n in rng Eseq by A2,FUNCT_1:12;
      then inf rng Eseq <= Eseq.n by A10,SUPINF_1:def 10;
      then consider r2,r3 being Real such that
A12:  r2 = inf rng Eseq & r3 = Eseq.n & r2 <= r3 by A8,A11;
      thus thesis by A1,A12;
    end;
    hence thesis by RINFSUP1:10;
  end;
  hence thesis by A9,XXREAL_0:1;
end;

theorem Th13:
  for seq being Function of NAT,REAL, Eseq being Function of NAT,ExtREAL st
  seq = Eseq & seq is nonnegative summable holds Sum seq = SUM Eseq
proof
  let seq be Function of NAT,REAL, Eseq be Function of NAT,ExtREAL such that
A1: seq = Eseq & seq is nonnegative summable;
  Partial_Sums seq is convergent by A1,SERIES_1:def 2;
  then Partial_Sums seq is bounded by SEQ_2:27;
  then
A2: Partial_Sums seq is bounded_above by SEQ_2:def 5;
  then sup Partial_Sums seq = sup rng Ser Eseq by A1,Th10,Th11;
  then
A3: sup Partial_Sums seq = SUM Eseq by SUPINF_2:def 23;
  for n holds seq.n >= 0 by A1,RINFSUP1:def 3;
  then Partial_Sums seq is non-decreasing by SERIES_1:19;
  hence thesis by A2,A3,RINFSUP1:24;
end;

theorem Th14:
  P is sigma_Measure of Sigma
proof
  set Z = Sigma;
  reconsider P1 = P as Function of Z,ExtREAL by FUNCT_2:9,NUMBERS:31;
  for x being R_eal holds x in rng P1 implies 0. <= x
  proof
    let x be R_eal;
A1: dom P1 = Sigma by FUNCT_2:def 1;
    assume x in rng P1;
    then consider y being set such that
A2: y in Sigma & P1.y = x by A1,FUNCT_1:def 5;
    reconsider y1=y as Event of Sigma by A2,PROB_1:def 10;
    0 <= P.y1 by PROB_1:def 13;
    hence thesis by A2;
  end;
  then rng P1 is nonnegative by SUPINF_2:def 15;
  then
A3: P1 is nonnegative by SUPINF_2:def 22;
A4: P1.{} = 0. by PROB_1:64;
  for F being Sep_Sequence of Z holds SUM(P1 * F) = P1.(union rng F)
  proof
    let F be Sep_Sequence of Z;
    reconsider F2=F as disjoint_valued SetSequence of Sigma by Th2;
A5: P.(Union F2) = Sum(P * F2) by PROB_3:51;
    Sum(P * F2) = SUM(P1 * F)
    proof
      Partial_Sums(P * F2) is convergent by PROB_3:50;
      then
A6:   P * F2 is summable by SERIES_1:def 2;
      for n holds (P * F2).n >= 0 by PROB_3:4;
      then P * F2 is nonnegative by RINFSUP1:def 3;
      hence thesis by A6,Th13;
    end;
    hence thesis by A5,CARD_3:def 4;
  end;
  hence thesis by A3,A4,MEASURE1:def 11;
end;

definition
  let Omega,Sigma,P;
  func P2M(P) -> sigma_Measure of Sigma equals

  P;
  coherence by Th14;
end;

theorem Th15:
  for X being non empty set, S being SigmaField of X,
  M being sigma_Measure of S st M.X = R_EAL(1) holds M is Probability of S
proof
  let X be non empty set, S be SigmaField of X,
  M be sigma_Measure of S such that
A1: M.X = R_EAL(1);
A2: for A being Element of S holds M.A <= R_EAL(1)
  proof
    let A be Element of S;
    reconsider X as Element of S by PROB_1:11;
    M.A <= M.X by MEASURE1:62;
    hence thesis by A1;
  end;
A3: dom M = S by FUNCT_2:def 1;
A4: M is nonnegative by MEASURE1:def 11;
A5: for x st x in S holds M.x in REAL
  proof
    let x such that
A6: x in S;
    reconsider x as Element of S by A6;
A7: R_EAL(1) is Real & 0 is Real by MEASURE6:def 1;
A8: 0. <= M.x by A4,MEASURE1:def 4;
    M.x <= R_EAL(1) by A2;
    then M.x is Real by A7,A8,MEASURE6:16;
    hence thesis;
  end;
  then reconsider P1 = M as Function of S,REAL by A3,FUNCT_2:5;
  reconsider P1 as Function of S,REAL;
A9: for A being Event of S holds 0 <= P1.A
  proof
    let A be Event of S;
    reconsider A as Element of S by PROB_1:def 10;
    reconsider A1 = A as Element of S;
A10: M.A1 in REAL & 0 in REAL by A5;
    0. <= M.A1 by A4,MEASURE1:def 4;
    then consider p,q being Real such that
A11: p = 0. & q = M.A1 & p <= q by A10;
    thus thesis by A11;
  end;
A12: P1.X = 1 by A1,MEASURE6:def 1;
A13: for A,B being Event of S st A misses B holds P1.(A \/ B) = P1.A + P1.B
  proof
    let A,B be Event of S such that
A14: A misses B;
    reconsider A, B as Element of S by PROB_1:def 10;
    reconsider A2 = A, B2 = B as Element of S;
    P1.(A \/ B) = M.A2 + M.B2 by A14,MEASURE1:61
      .= P1.A + P1.B by SUPINF_2:1;
    hence thesis;
  end;
  for ASeq being SetSequence of S st ASeq is non-increasing holds
  (P1 * ASeq is convergent & lim (P1 * ASeq) = P1.Intersection ASeq)
  proof
    let ASeq be SetSequence of S such that
A15: ASeq is non-increasing;
    reconsider F = ASeq as Function of NAT,S by Th2;
A16: for n being Element of NAT holds F.(n+1) c= F.n by A15,PROB_2:14;
A17: M.(F.0) <+infty by A5,XXREAL_0:9;
    for n holds 0 <= (P1 * ASeq).n
    proof
      let n;
      reconsider A = ASeq.n as Event of S by PROB_1:def 10;
A18:  0 <= P1.A by A9;
      dom (P1 * ASeq) = NAT by SEQ_1:3;
      hence thesis by A18,FUNCT_1:22;
    end;
    then
A19: P1*ASeq is bounded_below by RINFSUP1:10;
A20: P1*ASeq is non-increasing
    proof
      now
        let n;
        dom (M*F) = NAT by FUNCT_2:def 1;
        then
A21:    (M*F).n = M.(F.n) & (M*F).(n+1) = M.(F.(n+1)) by FUNCT_1: 22;
        then
A22:    (M*F).(n+1) is Real & (M*F).n is Real by A5;
        F.(n+1) c= F.n by A15,PROB_2:14;
        then consider p,q being Real such that
A23:    p = (M*F).(n+1) & q = (M*F).n & p <= q by A21,A22,MEASURE1:62;
        thus (P1*ASeq).(n+1) <= (P1*ASeq).n by A23;
      end;
      hence thesis by SEQM_3:def 14;
    end;
    lim (P1*ASeq) = inf(rng (M*F))
    proof
      thus lim (P1*ASeq) = inf (P1*ASeq) by A19,A20,RINFSUP1:25
        .= inf(rng (M*F)) by A19,Th12;
    end;
    then lim (P1 * ASeq) = M.(meet rng F) by A16,A17,MEASURE3:14
      .= P1.Intersection ASeq by SETLIM_1:13;
    hence thesis by A19,A20,SEQ_4:52;
  end;
  hence thesis by A9,A12,A13,PROB_1:def 13;
end;

definition
  let X be non empty set, S be SigmaField of X, M be sigma_Measure of S;
  assume
A1: M.X = R_EAL(1);
  func M2P(M) -> Probability of S equals

  M;
  coherence by A1,Th15;
end;

Lm2: A1 is non-decreasing implies for n holds (Partial_Union A1).n = A1.n
proof
  assume
A1: A1 is non-decreasing;
  defpred P[Element of NAT] means (Partial_Union A1).$1 = A1.$1;
A2: P[0] by PROB_3:def 2;
A3: for k st P[k] holds P[k+1]
  proof
    let k such that
A4: P[k];
A5: A1.k c= A1.(k+1) by A1,PROB_2:15;
    thus (Partial_Union A1).(k+1) = A1.k \/ A1.(k+1) by A4,PROB_3:def 2
      .= A1.(k+1) by A5,XBOOLE_1:12;
  end;
  thus for k holds P[k] from NAT_1:sch 1(A2,A3);
end;

theorem Th16:
  A1 is non-decreasing implies Partial_Union A1 = A1
proof
  assume A1 is non-decreasing;
  then for n holds (Partial_Union A1).n = A1.n by Lm2;
  hence thesis by FUNCT_2:113;
end;

theorem Th17:
  A1 is non-decreasing implies (Partial_Diff_Union A1).0 = A1.0 &
  for n holds (Partial_Diff_Union A1).(n+1) = A1.(n+1) \ A1.n
proof
  assume
A1: A1 is non-decreasing;
  thus (Partial_Diff_Union A1).0 = A1.0 by PROB_3:def 3;
  thus for n holds (Partial_Diff_Union A1).(n+1) = A1.(n+1) \ A1.n
  proof
    let n;
    thus (Partial_Diff_Union A1).(n+1)
    = A1.(n+1) \ (Partial_Union A1).n by PROB_3:def 3
      .= A1.(n+1) \ A1.n by A1,Lm2;
  end;
end;

theorem
  A1 is non-decreasing implies
  for n holds A1.(n+1) = (Partial_Diff_Union A1).(n+1) \/ A1.n
proof
  assume
A1: A1 is non-decreasing;
  thus for n holds A1.(n+1) = (Partial_Diff_Union A1).(n+1) \/ A1.n
  proof
    let n;
A2: A1.n c= A1.(n+1) by A1,PROB_2:15;
    thus (Partial_Diff_Union A1).(n+1) \/ A1.n
    = (A1.(n+1) \ (Partial_Union A1).n) \/ A1.n by PROB_3:def 3
      .= (A1.(n+1) \ A1.n) \/ A1.n by A1,Lm2
      .= A1.(n+1) \/ A1.n by XBOOLE_1:39
      .= A1.(n+1) by A2,XBOOLE_1:12;
  end;
end;

theorem Th19:
  A1 is non-decreasing implies
  for n holds (Partial_Diff_Union A1).(n+1) misses A1.n
proof
  assume
A1: A1 is non-decreasing;
  let n;
  (Partial_Diff_Union A1).(n+1) = A1.(n+1) \ A1.n by A1,Th17;
  hence thesis by XBOOLE_1:79;
end;

theorem
  XSeq is non-decreasing implies @Partial_Union XSeq = XSeq
proof
  assume
A1: XSeq is non-decreasing;
  @Partial_Union XSeq = Partial_Union XSeq by PROB_3:def 6;
  hence thesis by A1,Th16;
end;

theorem Th21:
  XSeq is non-decreasing implies (@Partial_Diff_Union XSeq).0 = XSeq.0 &
  for n holds (@Partial_Diff_Union XSeq).(n+1) = XSeq.(n+1) \ XSeq.n
proof
  assume
A1: XSeq is non-decreasing;
  @Partial_Diff_Union XSeq = Partial_Diff_Union XSeq by PROB_3:def 7;
  hence thesis by A1,Th17;
end;

theorem Th22:
  XSeq is non-decreasing implies
  for n holds (@Partial_Diff_Union XSeq).(n+1) misses XSeq.n
proof
  assume
A1: XSeq is non-decreasing;
  @Partial_Diff_Union XSeq = Partial_Diff_Union XSeq by PROB_3:def 7;
  hence thesis by A1,Th19;
end;

definition
  let Omega,Sigma,P;
  pred P is_complete Sigma means
  :Def3:
  for A being Subset of Omega for B being set st B in Sigma holds
  (A c= B & P.B=0 implies A in Sigma);
end;

theorem
  P is_complete Sigma iff P2M(P) is_complete Sigma
proof
  hereby
    assume P is_complete Sigma;
    then for A being Subset of Omega for B being set st B in Sigma
    holds (A c= B & (P2M P).B = 0. implies A in Sigma) by Def3;
    hence P2M(P) is_complete Sigma by MEASURE3:def 2;
  end;
  assume P2M(P) is_complete Sigma;
  then for A being Subset of Omega for B being set st B in Sigma
  holds (A c= B & P.B=0 implies A in Sigma) by MEASURE3:def 2;
  hence thesis by Def3;
end;

definition
  let Omega,Sigma,P;
  mode thin of P -> Subset of Omega means
    :Def4:
    ex A being set st A in Sigma & it c= A & P.A = 0;
  existence
  proof
    reconsider B = {} as Subset of Omega by XBOOLE_1:2;
    take B;
    take A={};
    thus A in Sigma by PROB_1:42;
    thus B c= A;
    thus thesis by PROB_1:64;
  end;
end;

theorem Th24:
  for Y being Subset of Omega holds Y is thin of P iff Y is thin of P2M(P)
proof
  let Y be Subset of Omega;
  hereby
    assume Y is thin of P;
    then consider A be set such that
A1: A in Sigma & Y c= A & P.A = 0 by Def4;
    thus Y is thin of P2M(P) by A1,MEASURE3:def 3;
  end;
  assume Y is thin of P2M(P);
  then consider B be set such that
A2: B in Sigma & Y c= B & (P2M(P)).B = 0. by MEASURE3:def 3;
  thus thesis by A2,Def4;
end;

theorem Th25:
  {} is thin of P
proof
  {} c= {} & P.{} = 0 & {} in Sigma by PROB_1:42,64;
  hence {} is thin of P by Def4;
end;

theorem Th26:
  for B1,B2 being set st B1 in Sigma & B2 in Sigma holds
  for C1,C2 being thin of P holds B1 \/ C1 = B2 \/ C2 implies P.B1 = P.B2
proof
  let B1,B2 be set;
  assume
A1: B1 in Sigma & B2 in Sigma;
  let C1,C2 be thin of P;
  assume
A2: B1 \/ C1 = B2 \/ C2;
  consider D1 being set such that
A3: D1 in Sigma & C1 c= D1 & P.D1 = 0 by Def4;
  consider D2 being set such that
A4: D2 in Sigma & C2 c= D2 & P.D2 = 0 by Def4;
A5: B1 c= B2 \/ C2 & B2 \/ C2 c= B2 \/ D2 by A2,A4,XBOOLE_1:7,9;
A6: B2 c= B1 \/ C1 & B1 \/ C1 c= B1 \/ D1 by A2,A3,XBOOLE_1:7,9;
  reconsider B1,B2,D1,D2 as Event of Sigma by A1,A3,A4,PROB_1:def 10;
A7: P.B1 <= P.(B2 \/ D2) by A5,PROB_1:70,XBOOLE_1:1;
  P.(B2 \/ D2) <= P.B2 + P.D2 by PROB_1:75;
  then
A8: P.B1 <= P.B2 by A4,A7,XXREAL_0:2;
A9: P.B2 <= P.(B1 \/ D1) by A6,PROB_1:70,XBOOLE_1:1;
  P.(B1 \/ D1) <= P.B1 + P.D1 by PROB_1:75;
  then P.B2 <= P.B1 by A3,A9,XXREAL_0:2;
  hence thesis by A8,XXREAL_0:1;
end;

definition
  let Omega,Sigma,P;
  func COM(Sigma,P) -> non empty Subset-Family of Omega means
  :Def5:
  for A being set holds A in it iff
  (ex B being set st B in Sigma & ex C being thin of P st A = B \/ C);
  existence
  proof
    defpred P[set] means for A being set st A = $1 holds
    ex B being set st B in Sigma & ex C being thin of P st A = B \/ C;
    consider D being set such that
A1: for y being set holds y in D iff y in bool Omega & P[y]
    from XBOOLE_0:sch 1;
A2: D c= bool Omega
    proof
      let x be set;
      assume x in D;
      hence x in bool Omega by A1;
    end;
A3: for A being set holds (A in D iff
    (ex B being set st (B in Sigma & ex C being thin of P st A = B \/ C)))
    proof
      let A be set;
A4:   A in D iff (A in bool Omega & for y being set holds (y = A implies
      (ex B being set st
      (B in Sigma & ex C being thin of P st y = B \/ C)))) by A1;
      (ex B being set st (B in Sigma & ex C being thin of P st A = B \/ C))
      implies A in D
      proof
        assume
A5:     ex B being set st (B in Sigma & ex C being thin of P st A = B \/ C);
        then consider B being set such that
A6:     B in Sigma & ex C being thin of P st A = B \/ C;
        A c= Omega by A6,XBOOLE_1:8;
        hence thesis by A4,A5;
      end;
      hence thesis by A1;
    end;
A7: {} is thin of P by Th25;
A8: {} c= Omega by XBOOLE_1:2;
    for A being set st A = {} holds
    ex B being set st B in Sigma & ex C being thin of P st A = B \/ C
    proof
      let A be set;
      assume
A9:   A = {};
      consider B being set such that
A10:  B = {} & B in Sigma by PROB_1:42;
      consider C being thin of P such that
A11:  C = {} by A7;
      A = B \/ C by A9,A10,A11;
      hence thesis by A10;
    end;
    then reconsider D as non empty Subset-Family of Omega by A1,A2,A8;
    take D;
    thus thesis by A3;
  end;
  uniqueness
  proof
    let D1,D2 be non empty Subset-Family of Omega such that
A12: for A being set holds (A in D1 iff
    (ex B being set st (B in Sigma & ex C being thin of P st A = B \/ C))) and
A13: for A being set holds (A in D2 iff
    (ex B being set st (B in Sigma & ex C being thin of P st A = B \/ C)));
    for A being set holds A in D1 iff A in D2
    proof
      let A be set;
      thus A in D1 implies A in D2
      proof
        assume A in D1;
        then ex B being set st (B in Sigma & ex C being thin of P st A = B \/
        C) by A12;
        hence thesis by A13;
      end;
      assume A in D2;
      then ex B being set st (B in Sigma & ex C being thin of P st A = B \/ C)
      by A13;
      hence thesis by A12;
    end;
    hence thesis by TARSKI:2;
  end;
end;

theorem Th27:
  for C being thin of P holds C in COM(Sigma,P)
proof
  let C be thin of P;
  reconsider B = {} as Element of Sigma by PROB_1:42;
  B \/ C in COM(Sigma,P) by Def5;
  hence thesis;
end;

theorem Th28:
  COM(Sigma,P) = COM(Sigma,P2M P)
proof
A1: COM(Sigma,P) c= COM(Sigma,P2M P)
  proof
    let x such that
A2: x in COM(Sigma,P);
    consider B being set such that
A3: B in Sigma & ex C being thin of P st x = B \/ C by A2,Def5;
    consider C being thin of P such that
A4: x = B \/ C by A3;
    reconsider C1=C as thin of P2M(P) by Th24;
    x = B \/ C1 by A4;
    hence thesis by A3,MEASURE3:def 4;
  end;
  COM( Sigma,P2M P) c= COM(Sigma,P)
  proof
    let x such that
A5: x in COM( Sigma,P2M P);
    consider B being set such that
A6: B in Sigma & ex C being thin of P2M(P) st x = B \/ C by A5,MEASURE3:def 4;
    consider C being thin of P2M(P) such that
A7: x = B \/ C by A6;
    reconsider C1=C as thin of P by Th24;
    x = B \/ C1 by A7;
    hence thesis by A6,Def5;
  end;
  hence thesis by A1,XBOOLE_0:def 10;
end;

definition
  let Omega,Sigma,P;
  let A be Element of COM(Sigma,P);
  func P_COM2M_COM(A) -> Element of COM(Sigma,P2M(P)) equals

  A;
  correctness by Th28;
end;

theorem Th29:
  Sigma c= COM(Sigma,P)
proof
  let A be set such that
A1: A in Sigma;
  reconsider C={} as thin of P by Th25;
  A = A \/ C;
  hence thesis by A1,Def5;
end;

definition
  let Omega,Sigma,P;
  let A be Element of COM(Sigma,P);
  func ProbPart(A) -> non empty Subset-Family of Omega means
  :Def7:
  for B being set holds (B in it iff
  (B in Sigma & B c= A & A \ B is thin of P));
  existence
  proof
    defpred P[set] means
    for t being set holds t = $1 implies t in Sigma & t c= A &
    A \ t is thin of P;
    consider D being set such that
A1: for t being set holds t in D iff t in bool Omega & P[t]
    from XBOOLE_0:sch 1;
A2: for B being set holds B in D iff B in Sigma & B c= A & A \ B is thin of P
    proof
      let B be set;
      B in Sigma & B c= A & A \ B is thin of P implies B in D
      proof
        assume
A3:     B in Sigma & B c= A & A \ B is thin of P;
        then for t being set holds t = B implies
        (t in Sigma & t c= A & A \ t is thin of P);
        hence thesis by A1,A3;
      end;
      hence thesis by A1;
    end;
    D <> {}
    proof
      consider B being set such that
A4:   B in Sigma & ex C being thin of P st A = B \/ C by Def5;
      consider C being thin of P such that
A5:   A = B \/ C by A4;
      consider E being set such that
A6:   E in Sigma & C c= E & P.E = 0 by Def4;
      A \ B = C \ B by A5,XBOOLE_1:40;
      then A \ B c= C by XBOOLE_1:36;
      then A \ B c= E by A6,XBOOLE_1:1;
      then
A7:   A \ B is thin of P by A6,Def4;
      B c= A by A5,XBOOLE_1:7;
      hence thesis by A2,A4,A7;
    end;
    then consider x being set such that
A8: x in D by XBOOLE_0:def 1;
    D c= bool Omega
    proof
      let B be set;
      assume B in D;
      then B in Sigma by A2;
      hence thesis;
    end;
    then reconsider D as non empty Subset-Family of Omega by A8;
    take D;
    thus thesis by A2;
  end;
  uniqueness
  proof
    let D1,D2 be non empty Subset-Family of Omega such that
A9: for B being set holds B in D1 iff (B in Sigma & B c= A &
    A \ B is thin of P) and
A10: for B being set holds B in D2 iff (B in Sigma & B c= A &
    A \ B is thin of P);
    for B being set holds B in D1 iff B in D2
    proof
      let B be set;
      thus B in D1 implies B in D2
      proof
        assume B in D1;
        then B in Sigma & B c= A & A \ B is thin of P by A9;
        hence thesis by A10;
      end;
      assume B in D2;
      then B in Sigma & B c= A & A \ B is thin of P by A10;
      hence thesis by A9;
    end;
    hence thesis by TARSKI:2;
  end;
end;

theorem
  for A being Element of COM(Sigma,P) holds
  ProbPart(A) = MeasPart(P_COM2M_COM(A))
proof
  let A be Element of COM(Sigma,P);
A1: ProbPart(A) c= MeasPart(P_COM2M_COM(A))
  proof
    let x be set such that
A2: x in ProbPart(A);
A3: x in Sigma & x c= A & A \ x is thin of P by A2,Def7;
    then P_COM2M_COM(A) \ x is thin of P2M(P) by Th24;
    hence thesis by A3,MEASURE3:def 5;
  end;
  MeasPart(P_COM2M_COM(A)) c= ProbPart(A)
  proof
    let x be set such that
A4: x in MeasPart(P_COM2M_COM A);
A5: x in Sigma & x c= P_COM2M_COM(A) &
    P_COM2M_COM(A) \ x is thin of P2M(P) by A4,MEASURE3:def 5;
    then A \ x is thin of P by Th24;
    hence thesis by A5,Def7;
  end;
  hence thesis by A1,XBOOLE_0:def 10;
end;

theorem
  for A being Element of COM(Sigma,P) holds
  for A1,A2 being set st A1 in ProbPart(A) & A2 in ProbPart(A) holds
  P.A1 = P.A2
proof
  let A be Element of COM(Sigma,P);
  let A1,A2 be set such that
A1: A1 in ProbPart(A) & A2 in ProbPart(A);
A2: A1 in Sigma & A1 c= A & (A \ A1) is thin of P by A1,Def7;
A3: A2 in Sigma & A2 c= A & (A \ A2) is thin of P by A1,Def7;
  reconsider C1 = A \ A1, C2 = A \ A2 as thin of P by A1,Def7;
  A1 \/ C1 = A by A2,XBOOLE_1:45
    .= A2 \/ C2 by A3,XBOOLE_1:45;
  hence thesis by A2,A3,Th26;
end;

theorem Th32:
  for F being Function of NAT,COM(Sigma,P) holds
  ex BSeq being SetSequence of Sigma st for n holds BSeq.n in ProbPart(F.n)
proof
  let F be Function of NAT,COM(Sigma,P);
  defpred P[Element of NAT, set] means
  for n being Element of NAT for y being set holds
  (n = $1 & y = $2 implies y in ProbPart(F.n));
A1: for t being Element of NAT ex A being Element of Sigma st P[t,A]
  proof
    let t be Element of NAT;
    consider A being Element of ProbPart(F.t);
    reconsider A as Element of Sigma by Def7;
    take A;
    thus thesis;
  end;
  ex G being Function of NAT,Sigma st
  for t being Element of NAT holds P[t,G.t] from FUNCT_2:sch 3(A1);
  then consider G being Function of NAT,Sigma such that
A2: for t being Element of NAT holds
  for n being Element of NAT for y being set holds
  (n = t & y = G.t implies y in ProbPart(F.n));
A3: for n holds G.n in Sigma;
  reconsider BSeq = G as SetSequence of Omega by FUNCT_2:9;
  reconsider BSeq as SetSequence of Sigma by A3,PROB_1:def 9;
  take BSeq;
  thus thesis by A2;
end;

theorem Th33:
  for F being Function of NAT,COM(Sigma,P),
  BSeq being SetSequence of Sigma holds
  ex CSeq being SetSequence of Omega st for n holds CSeq.n = F.n \ BSeq.n
proof
  let F be Function of NAT,COM(Sigma,P), G be SetSequence of Sigma;
  defpred P[Element of NAT, set] means
  for n being Element of NAT for y being set holds
  (n = $1 & y = $2 implies y = F.n \ G.n);
A1: for t being Element of NAT ex A being Element of bool Omega st P[t,A]
  proof
    let t be Element of NAT;
    F.t is Element of COM(Sigma,P);
    then reconsider A = F.t \ G.t as Element of bool Omega by XBOOLE_1:1;
    take A;
    thus thesis;
  end;
  ex H being Function of NAT,bool Omega st
  for t being Element of NAT holds P[t,H.t] from FUNCT_2:sch 3(A1);
  then consider H being Function of NAT,bool Omega such that
A2: for t being Element of NAT holds
  for n being Element of NAT for y being set holds
  n = t & y = H.t implies y = F.n \ G.n;
  take H;
  thus thesis by A2;
end;

theorem Th34:
  for BSeq being SetSequence of Omega st
  (for n holds BSeq.n is thin of P) holds
  ex CSeq being SetSequence of Sigma st
  (for n holds BSeq.n c= CSeq.n & P.(CSeq.n) = 0)
proof
  let BSeq be SetSequence of Omega;
  assume
A1: for n holds BSeq.n is thin of P;
  defpred P[Element of NAT, set] means
  for n being Element of NAT for y being set holds
  (n = $1 & y = $2 implies y in Sigma & BSeq.n c= y & P.y = 0);
A2: for t being Element of NAT ex A being Element of Sigma st P[t,A]
  proof
    let t be Element of NAT;
    BSeq.t is thin of P by A1;
    then consider A being set such that
A3: A in Sigma & BSeq.t c= A & P.A = 0 by Def4;
    reconsider A as Element of Sigma by A3;
    take A;
    thus thesis by A3;
  end;
  ex G being Function of NAT,Sigma st
  for t being Element of NAT holds P[t,G.t] from FUNCT_2:sch 3(A2);
  then consider G being Function of NAT,Sigma such that
A4: for t being Element of NAT holds
  for n being Element of NAT for y being set holds
  (n = t & y = G.t implies y in Sigma & BSeq.n c= y & P.y = 0);
A5: for n holds G.n in Sigma;
  reconsider CSeq = G as SetSequence of Omega by FUNCT_2:9;
  reconsider CSeq as SetSequence of Sigma by A5,PROB_1:def 9;
  take CSeq;
  thus thesis by A4;
end;

theorem Th35:
  for D being non empty Subset-Family of Omega holds
  (for A being set holds (A in D iff
  ex B being set st B in Sigma & ex C being thin of P st A = B \/ C))
  implies D is SigmaField of Omega
proof
  let D be non empty Subset-Family of Omega;
  assume
A1: for A being set holds A in D iff
  (ex B being set st B in Sigma & ex C being thin of P st A = B \/ C);
A2: for A being Subset of Omega holds A in D implies A` in D
  proof
    let A be Subset of Omega;
    assume
A3: A in D;
    ex Q being set st Q in Sigma & ex W being thin of P st Omega \ A = Q \/ W
    proof
      consider B being set such that
A4:   B in Sigma & ex C being thin of P st A = B \/ C by A1,A3;
      consider C being thin of P such that
A5:   A = B \/ C by A4;
      consider G being set such that
A6:   G in Sigma & C c= G & P.G = 0 by Def4;
      reconsider B as Subset of Omega by A4;
      set H = Omega \ B;
A7:   B` in Sigma by A4,PROB_1:def 1;
A8:   Omega \ A = H \ C by A5,XBOOLE_1:41;
      set Q = H \ G;
A9:   ex W being thin of P st Omega \ A = Q \/ W
      proof
        set W = H /\ (G \ C);
        W c= H by XBOOLE_1:17;
        then reconsider W as Subset of Omega by XBOOLE_1:1;
        reconsider W as thin of P by A6,Def4;
        take W;
        thus thesis by A6,A8,Lm1;
      end;
      take Q;
      thus thesis by A6,A7,A9,PROB_1:44;
    end;
    hence thesis by A1;
  end;
  for A1 being SetSequence of Omega st
  (for n holds A1.n in D) holds Union A1 in D
  proof
    let A1 be SetSequence of Omega;
    assume
A10: for n holds A1.n in D;
    reconsider F = A1 as Function of NAT,bool Omega;
A11: Union A1 = union rng A1 by CARD_3:def 4;
A12: dom F = NAT by FUNCT_2:def 1;
A13: for n holds
    ex B being set st B in Sigma & ex C being thin of P st F.n = B \/ C
    proof
      let n;
      F.n in D by A10;
      hence thesis by A1;
    end;
    for n holds F.n in COM(Sigma,P)
    proof
      let n;
      ex B being set st (B in Sigma & ex C being thin of P st F.n = B \/ C)
      by A13;
      hence thesis by Def5;
    end;
    then for n being set st n in NAT holds F.n in COM(Sigma,P);
    then reconsider F as Function of NAT,COM(Sigma,P) by A12,FUNCT_2:5;
    consider BSeq being SetSequence of Sigma such that
A14: for n holds BSeq.n in ProbPart(F.n) by Th32;
A15: for n being Element of NAT holds BSeq.n in Sigma & BSeq.n c= F.n &
    F.n \ BSeq.n is thin of P
    proof
      let n be Element of NAT;
      BSeq.n in ProbPart(F.n) by A14;
      hence thesis by Def7;
    end;
    consider CSeq being SetSequence of Omega such that
A16: for n holds CSeq.n = F.n \ BSeq.n by Th33;
    for n holds CSeq.n is thin of P
    proof
      let n be Element of NAT;
      F.n \ BSeq.n is thin of P by A15;
      hence thesis by A16;
    end;
    then consider DSeq being SetSequence of Sigma such that
A17: for n holds CSeq.n c= DSeq.n & P.(DSeq.n) = 0 by Th34;
    ex B being set st B in Sigma & ex C being thin of P st Union A1 = B \/ C
    proof
      set B = union rng BSeq;
A18:  union rng BSeq c= union rng F
      proof
        let x be set;
        assume x in union rng BSeq;
        then consider Z being set such that
A19:    x in Z & Z in rng BSeq by TARSKI:def 4;
        dom BSeq = NAT by FUNCT_2:def 1;
        then consider n being set such that
A20:    n in NAT & Z = BSeq.n by A19,FUNCT_1:def 5;
        reconsider n as Element of NAT by A20;
        set P = F.n;
A21:    BSeq.n c= P by A15;
        ex P being set st P in rng F & x in P
        proof
          take P;
          thus thesis by A12,A19,A20,A21,FUNCT_1:def 5;
        end;
        hence thesis by TARSKI:def 4;
      end;
A22:  ex C being thin of P st Union A1 = B \/ C
      proof
        set C = Union A1 \ B;
A23:    Union A1 = C \/ union rng F /\ union rng BSeq by A11,XBOOLE_1:51
          .= B \/ C by A18,XBOOLE_1:28;
        C is thin of P
        proof
A24:      C c= union rng CSeq
          proof
            let x be set;
            assume x in C;
            then
A25:        x in union rng F & not x in union rng BSeq by A11, XBOOLE_0:def 4;
            then consider Z being set such that
A26:        x in Z & Z in rng F by TARSKI:def 4;
            consider n being set such that
A27:        n in NAT & Z = F.n by A12,A26,FUNCT_1:def 5;
            reconsider n as Element of NAT by A27;
            not x in BSeq.n
            proof
              assume
A28:          x in BSeq.n;
              dom BSeq = NAT by FUNCT_2:def 1;
              then BSeq.n in rng BSeq by FUNCT_1:def 5;
              hence thesis by A25,A28,TARSKI:def 4;
            end;
            then
A29:        x in F.n \ BSeq.n by A26,A27,XBOOLE_0:def 4;
            ex Z being set st x in Z & Z in rng CSeq
            proof
A30:          dom CSeq = NAT by FUNCT_2:def 1;
              take CSeq.n;
              thus thesis by A16,A29,A30,FUNCT_1:def 5;
            end;
            hence thesis by TARSKI:def 4;
          end;
          union rng CSeq c= union rng DSeq
          proof
            let x be set;
            assume x in union rng CSeq;
            then consider Z being set such that
A31:        x in Z & Z in rng CSeq by TARSKI:def 4;
            dom CSeq = NAT by FUNCT_2:def 1;
            then consider n being set such that
A32:        n in NAT & Z = CSeq.n by A31,FUNCT_1:def 5;
            reconsider n as Element of NAT by A32;
A33:        x in CSeq.n & CSeq.n c= DSeq.n by A17,A31,A32;
            n in dom DSeq by A32,FUNCT_2:def 1;
            then DSeq.n in rng DSeq by FUNCT_1:def 5;
            hence thesis by A33,TARSKI:def 4;
          end;
          then
A34:      C c= union rng DSeq by A24,XBOOLE_1:1;
          for A being set holds A in rng DSeq implies P.A = 0
          proof
            let A be set;
            assume
A35:        A in rng DSeq;
A36:        rng DSeq c= Sigma by Th1;
            dom DSeq = NAT by FUNCT_2:def 1;
            then
A37:        ex n being set st n in NAT & A = DSeq.n by A35, FUNCT_1:def 5;
            reconsider A as Element of Sigma by A35,A36;
            thus thesis by A17,A37;
          end;
          then
A38:      P.(union rng DSeq) = 0 by Th9;
          Union DSeq in Sigma by PROB_1:46;
          then union rng DSeq in Sigma by CARD_3:def 4;
          hence thesis by A34,A38,Def4;
        end;
        then consider C being thin of P such that
A39:    Union A1 = B \/ C by A23;
        take C;
        thus thesis by A39;
      end;
      take B;
      Union BSeq in Sigma by PROB_1:46;
      hence thesis by A22,CARD_3:def 4;
    end;
    hence thesis by A1;
  end;
  hence thesis by A2,Th5;
end;

definition
  let Omega,Sigma,P;
  redefine func COM(Sigma,P) -> SigmaField of Omega;
  coherence
  proof
    for A being set holds (A in COM(Sigma,P) iff
    (ex B being set st B in Sigma & ex C being thin of P st A=B \/ C))
    by Def5;
    hence thesis by Th35;
  end;
end;

definition
  let Omega,Sigma,P;
  redefine mode thin of P -> Event of COM(Sigma,P);
  coherence
  proof
    for C being thin of P holds C is Event of COM(Sigma,P)
    proof
      let C be thin of P;
      C in COM(Sigma,P) by Th27;
      hence thesis by PROB_1:def 10;
    end;
    hence thesis;
  end;
end;

theorem Th36:
  for A being set holds (A in COM(Sigma,P) iff
  ex A1,A2 being set st A1 in Sigma & A2 in Sigma &
  A1 c= A & A c= A2 & P.(A2 \ A1) = 0)
proof
  let A be set;
  thus A in COM(Sigma,P) implies
  ex A1,A2 being set st A1 in Sigma & A2 in Sigma &
  A1 c= A & A c= A2 & P.(A2 \ A1) = 0
  proof
    assume A in COM(Sigma,P);
    then consider B being set such that
A1: B in Sigma & ex C being thin of P st A = B \/ C by Def5;
    consider C being thin of P such that
A2: A = B \/ C by A1;
    consider D being set such that
A3: D in Sigma & C c= D & P.D = 0 by Def4;
    reconsider D as Event of Sigma by A3,PROB_1:def 10;
    reconsider B as Event of Sigma by A1,PROB_1:def 10;
    reconsider E = D \/ B as Event of Sigma;
A4: E in Sigma by PROB_1:def 10;
A5: A c= E by A2,A3,XBOOLE_1:9;
A6: P.(E \ B) = P.(D \ B) by XBOOLE_1:40
      .= P.(D \/ B) - P.B by Th6;
    P.(D \/ B) <= P.D + P.B by PROB_1:75;
    then P.(E \ B) <= 0 by A3,A6,XREAL_1:49;
    then P.(E \ B) = 0 by PROB_1:def 13;
    hence thesis by A1,A4,A5,XBOOLE_1:7;
  end;
  thus (ex A1,A2 being set st A1 in Sigma & A2 in Sigma &
  A1 c= A & A c= A2 & P.(A2 \ A1) = 0) implies A in COM(Sigma,P)
  proof
    given A1,A2 being set such that
A7: A1 in Sigma & A2 in Sigma & A1 c= A & A c= A2 & P.(A2 \ A1) = 0;
    reconsider A1 as Element of Sigma by A7;
    reconsider A2 as Element of Sigma by A7;
    set C = A \ A1;
A8: A = A1 \/ C by A7,XBOOLE_1:45;
    C is thin of P
    proof
      reconsider D = A2 \ A1 as Element of Sigma;
A9:   C c= D
      proof
        let x such that
A10:    x in C;
        x in A & not x in A1 by A10,XBOOLE_0:def 4;
        hence thesis by A7,XBOOLE_0:def 4;
      end;
      then C c= Omega by XBOOLE_1:1;
      hence thesis by A7,A9,Def4;
    end;
    hence thesis by A8,Def5;
  end;
end;

theorem
  for C being non empty Subset-Family of Omega holds
  (for A being set holds (A in C iff
  ex A1,A2 being set st A1 in Sigma & A2 in Sigma &
  A1 c= A & A c= A2 & P.(A2 \ A1) = 0)) implies C = COM(Sigma,P)
proof
  let C be non empty Subset-Family of Omega;
  assume
A1: for A being set holds (A in C iff
  ex A1,A2 being set st A1 in Sigma & A2 in Sigma &
  A1 c= A & A c= A2 & P.(A2 \ A1) = 0);
  now
    let A be set;
    A in C iff ex A1,A2 being set st A1 in Sigma & A2 in Sigma &
    A1 c= A & A c= A2 & P.(A2 \ A1) = 0 by A1;
    hence A in C iff A in COM(Sigma,P) by Th36;
  end;
  hence thesis by TARSKI:2;
end;

definition
  let Omega,Sigma,P;
  func COM(P) -> Probability of COM(Sigma,P) means
  :Def8:
  for B being set st B in Sigma for C being thin of P holds it.(B \/ C) = P.B;
  existence
  proof
    defpred P[set,set] means for x,y being set st x in COM(Sigma,P) holds
    (x = $1 & y = $2 implies (for B being set st B in Sigma
    for C being thin of P holds (x = B \/ C implies y = P.B)));
A1: for x being set st x in COM(Sigma,P) ex y being set st y in REAL & P[x,y]
    proof
      let x be set;
      assume x in COM(Sigma,P);
      then consider B being set such that
A2:   B in Sigma & ex C being thin of P st x = B \/ C by Def5;
      take P.B;
      thus thesis by A2,Th26,FUNCT_2:7;
    end;
    consider comP being Function of COM(Sigma,P),REAL such that
A3: for x being set st x in COM(Sigma,P) holds P[x,comP.x]
    from FUNCT_2:sch 1(A1);
A4: for x being Element of COM(Sigma,P) holds comP.x >= 0
    proof
      let x be Element of COM(Sigma,P);
      consider B being set such that
A5:   B in Sigma & ex C being thin of P st x = B \/ C by Def5;
      reconsider B as Element of Sigma by A5;
A6:   comP.x = P.B by A3,A5;
      for A being set st A in Sigma holds P.A >= 0
      proof
        let A be set;
        assume A in Sigma;
        then A is Event of Sigma by PROB_1:def 10;
        hence thesis by PROB_1:def 13;
      end;
      hence thesis by A6;
    end;
A7: for A being Event of COM(Sigma,P) holds comP.A >= 0
    proof
      let A be Event of COM(Sigma,P);
      A in COM(Sigma,P) by PROB_1:def 10;
      hence thesis by A4;
    end;
A8: for B being set st B in Sigma
    for C being thin of P holds comP.(B \/ C) = P.B
    proof
      let B be set;
      assume
A9:   B in Sigma;
      let C be thin of P;
      B \/ C in COM(Sigma,P) by A9,Def5;
      hence thesis by A3,A9;
    end;
A10: {} is thin of P by Th25;
    reconsider C = {} as thin of P by Th25;
    set B = {};
    {} = B \/ C;
    then
A11: comP.{} = P.{} by A8,PROB_1:42
      .= 0 by PROB_1:64;
A12: comP.Omega = 1
    proof
      thus comP.Omega = comP.(B \/ Omega)
        .= P.Omega by A8,A10,PROB_1:43
        .= 1 by PROB_1:def 13;
    end;
A13: for BSeq being SetSequence of COM(Sigma,P) st BSeq is disjoint_valued
    holds Sum(comP * BSeq) = comP.(Union BSeq)
    proof
      let BSeq be SetSequence of COM(Sigma,P) such that
A14:  BSeq is disjoint_valued;
      reconsider F = BSeq as Function of NAT,bool Omega;
A15:  dom BSeq = NAT by FUNCT_2:def 1;
      for n being set st n in NAT holds F.n in COM(Sigma,P) by PROB_1:def 9;
      then reconsider F as Function of NAT,COM(Sigma,P) by A15,FUNCT_2:5;
      consider CSeq being SetSequence of Sigma such that
A16:  for n holds CSeq.n in ProbPart(F.n) by Th32;
A17:  for n being Element of NAT holds CSeq.n in Sigma & CSeq.n c= F.n &
      F.n \ CSeq.n is thin of P
      proof
        let n be Element of NAT;
        CSeq.n in ProbPart(F.n) by A16;
        hence thesis by Def7;
      end;
      consider DSeq being SetSequence of Omega such that
A18:  for n holds DSeq.n = F.n \ CSeq.n by Th33;
      for n being Element of NAT holds DSeq.n is thin of P
      proof
        let n be Element of NAT;
        F.n \ CSeq.n is thin of P by A17;
        hence thesis by A18;
      end;
      then consider ESeq being SetSequence of Sigma such that
A19:  for n holds DSeq.n c= ESeq.n & P.(ESeq.n) = 0 by Th34;
      consider B being set such that
A20:  B = union rng CSeq;
      Union CSeq in Sigma by PROB_1:46;
      then reconsider B as Element of Sigma by A20,CARD_3:def 4;
A21:  B c= union rng F
      proof
        let x be set;
        assume x in B;
        then consider Z being set such that
A22:    x in Z & Z in rng CSeq by A20,TARSKI:def 4;
        dom CSeq = NAT by FUNCT_2:def 1;
        then consider n being set such that
A23:    n in NAT & Z = CSeq.n by A22,FUNCT_1:def 5;
        reconsider n as Element of NAT by A23;
        set P = F.n;
A24:    CSeq.n c= P by A17;
        ex P being set st P in rng F & x in P
        proof
          take P;
          thus thesis by A15,A22,A23,A24,FUNCT_1:def 5;
        end;
        hence thesis by TARSKI:def 4;
      end;
A25:  ex C being thin of P st union rng F = B \/ C
      proof
        set C = union rng F \ B;
A26:    union rng F = C \/ union rng F /\ B by XBOOLE_1:51
          .= B \/ C by A21,XBOOLE_1:28;
        reconsider C as Subset of Omega;
        C is thin of P
        proof
A27:      C c= union rng DSeq
          proof
            let x be set;
            assume x in C;
            then
A28:        x in union rng F & not x in union rng CSeq by A20, XBOOLE_0:def 4;
            then consider Z being set such that
A29:        x in Z & Z in rng F by TARSKI:def 4;
            consider n being set such that
A30:        n in NAT & Z = F.n by A15,A29,FUNCT_1:def 5;
            reconsider n as Element of NAT by A30;
            not x in CSeq.n
            proof
              assume
A31:          x in CSeq.n;
              dom CSeq = NAT by FUNCT_2:def 1;
              then CSeq.n in rng CSeq by FUNCT_1:def 5;
              hence thesis by A28,A31,TARSKI:def 4;
            end;
            then
A32:        x in F.n \ CSeq.n by A29,A30,XBOOLE_0:def 4;
            ex Z being set st x in Z & Z in rng DSeq
            proof
A33:          dom DSeq = NAT by FUNCT_2:def 1;
              take DSeq.n;
              thus thesis by A18,A32,A33,FUNCT_1:def 5;
            end;
            hence thesis by TARSKI:def 4;
          end;
          union rng DSeq c= union rng ESeq
          proof
            let x be set;
            assume x in union rng DSeq;
            then consider Z being set such that
A34:        x in Z & Z in rng DSeq by TARSKI:def 4;
            dom DSeq = NAT by FUNCT_2:def 1;
            then consider n being set such that
A35:        n in NAT & Z = DSeq.n by A34,FUNCT_1:def 5;
            reconsider n as Element of NAT by A35;
A36:        x in DSeq.n & DSeq.n c= ESeq.n by A19,A34,A35;
            n in dom ESeq by A35,FUNCT_2:def 1;
            then ESeq.n in rng ESeq by FUNCT_1:def 5;
            hence thesis by A36,TARSKI:def 4;
          end;
          then
A37:      C c= union rng ESeq by A27,XBOOLE_1:1;
          Union ESeq in Sigma by PROB_1:46;
          then
A38:      union rng ESeq in Sigma by CARD_3:def 4;
          P.(union rng ESeq) = 0
          proof
            for A being set holds A in rng ESeq implies P.A = 0
            proof
              let A be set;
              assume
A39:          A in rng ESeq;
              dom ESeq = NAT by FUNCT_2:def 1;

then ex n being set st n in NAT & A = ESeq.n by A39,FUNCT_1:def 5;
              hence thesis by A19;
            end;
            hence thesis by Th9;
          end;
          hence thesis by A37,A38,Def4;
        end;
        then consider C being thin of P such that
A40:    union rng F = B \/ C by A26;
        take C;
        thus thesis by A40;
      end;
      for n,m being set st n <> m holds CSeq.n misses CSeq.m
      proof
        let n,m be set;
        assume n <> m;
        then F.n misses F.m by A14,PROB_2:def 3;
        then
A41:    F.n /\ F.m = {} by XBOOLE_0:def 7;
A42:    dom F = NAT by FUNCT_2:def 1
          .= dom CSeq by FUNCT_2:def 1;
        for n being set holds CSeq.n c= F.n
        proof
          let n be set;
          per cases;
          suppose n in dom F;
            hence thesis by A17;
          end;
          suppose
A43:        not n in dom F;
            then F.n = {} by FUNCT_1:def 4
              .= CSeq.n by A42,A43,FUNCT_1:def 4;
            hence thesis;
          end;
        end;
        then CSeq.n c= F.n & CSeq.m c= F.m;
        then CSeq.n /\ CSeq.m = {} by A41,XBOOLE_1:3,27;
        hence thesis by XBOOLE_0:def 7;
      end;
      then
A44:  CSeq is disjoint_valued by PROB_2:def 3;
      Sum(comP*F) = comP.(Union F)
      proof
A45:    for n holds comP.(F.n) = P.(CSeq.n)
        proof
          let n;
A46:      CSeq.n in Sigma & CSeq.n c= F.n & F.n \ CSeq.n is thin of P by A17;
          then consider C being thin of P such that
A47:      C = F.n \ CSeq.n;
          F.n = (F.n /\ CSeq.n) \/ (F.n \ CSeq.n) by XBOOLE_1:51
            .= CSeq.n \/ C by A46,A47,XBOOLE_1:28;
          hence thesis by A8;
        end;
        for x st x in NAT holds (comP*F).x = (P*CSeq).x
        proof
          let x such that
A48:      x in NAT;
          reconsider x as Element of NAT by A48;
          (comP*F).x = comP.(F.x) by FUNCT_2:21
            .= P.(CSeq.x) by A45
            .= (P*CSeq).x by FUNCT_2:21;
          hence thesis;
        end;
        then
A49:    Sum(comP*F) = Sum(P*CSeq) by SEQ_1:8;
        consider C1 being thin of P such that
A50:    union rng F = B \/ C1 by A25;
        comP.(union rng F) = P.(union rng CSeq) by A8,A20,A50;
        then comP.(Union F) = P.(union rng CSeq) by CARD_3:def 4
          .= P.(Union CSeq) by CARD_3:def 4;
        hence thesis by A44,A49,PROB_3:51;
      end;
      hence thesis;
    end;
A51: for A,B being Event of COM(Sigma,P) st A misses B holds
    comP.(A \/ B)= comP.A + comP.B
    proof
      let A,B be Event of COM(Sigma,P) such that
A52:  A misses B;
      consider BSeq being SetSequence of Omega such that
A53:  BSeq.0 = A & BSeq.1 = B & for n st n > 1 holds BSeq.n = {} by Th3;
      for n holds BSeq.n in COM(Sigma,P)
      proof
        let n;
        per cases;
        suppose n = 0;
          hence thesis by A53,PROB_1:def 10;
        end;
        suppose n <> 0;
          then n >= 1 by NAT_1:14;
          then
A54:      n + 1 > 1 by NAT_1:13;
          per cases by A54,NAT_1:22;
          suppose n > 1;
            then BSeq.n = {} by A53;
            hence thesis by PROB_1:42;
          end;
          suppose n = 1;
            hence thesis by A53,PROB_1:def 10;
          end;
        end;
      end;
      then reconsider BSeq as SetSequence of COM(Sigma,P) by PROB_1:def 9;
A55:  BSeq is disjoint_valued & Union BSeq = A \/ B by A52,A53,Th4;
      for m st 2<=m holds Partial_Sums(comP * BSeq).m = comP.A + comP.B
      proof
A56:    Partial_Sums(comP * BSeq).0 = (comP * BSeq).0 by SERIES_1:def 1
          .= comP.A by A53,FUNCT_2:21;
        0+1=1;
        then
A57:    Partial_Sums(comP * BSeq).1
        = Partial_Sums(comP * BSeq).0 + (comP * BSeq).1 by SERIES_1:def 1
          .= comP.A + comP.B by A53,A56,FUNCT_2:21;
A58:    for n holds Partial_Sums(comP * BSeq).(2+n) = comP.A + comP.B
        proof
          defpred P[Element of NAT] means Partial_Sums(comP * BSeq).(2+$1)
          = comP.A + comP.B;
A59:      P[0]
          proof
            2=1+1;
            hence Partial_Sums(comP * BSeq).(2+0)
            = Partial_Sums(comP * BSeq).1+(comP * BSeq).2 by SERIES_1:def 1
              .= comP.A + comP.B + comP.(BSeq.2) by A57,FUNCT_2:21
              .= comP.A + comP.B + comP.{} by A53
              .= comP.A + comP.B by A11;
          end;
A60:      for k st P[k] holds P[k+1]
          proof
            let k such that
A61:        P[k];
A62:        2+k+1 >1
            proof
              2+k+1 > 0+1 by XREAL_1:10;
              hence thesis;
            end;
            thus Partial_Sums(comP * BSeq).(2+(k+1))
            = Partial_Sums(comP * BSeq).(2+k) + (comP * BSeq).(2+k+1)
            by SERIES_1:def 1
              .= comP.A + comP.B + comP.(BSeq.(2+k+1)) by A61,FUNCT_2:21
              .= comP.A + comP.B + comP.{} by A53,A62
              .= comP.A + comP.B by A11;
          end;
          thus for k holds P[k] from NAT_1:sch 1(A59,A60);
        end;
        let m such that
A63:    2 <= m;
        consider k being Nat such that
A64:    m=2+k by A63,NAT_1:10;
        k in NAT by ORDINAL1:def 13;
        hence thesis by A58,A64;
      end;
      then Sum(comP * BSeq) = comP.A + comP.B by PROB_1:3;
      hence thesis by A13,A55;
    end;
A65: for A,B being Event of COM(Sigma,P) st A c= B holds
    comP.(B \ A) = comP.B - comP.A
    proof
      let A,B be Event of COM(Sigma,P);
      assume
A66:  A c= B;
      comP.A + comP.(B \ A) = comP.(A \/ (B \ A)) by A51,XBOOLE_1:79
        .= comP.B by A66,XBOOLE_1:45;
      hence thesis;
    end;
A67: for A,B being Event of COM(Sigma,P) st A c= B holds comP.A <= comP.B
    proof
      let A,B be Event of COM(Sigma,P);
      assume A c= B;
      then comP.(B \ A) = comP.B - comP.A by A65;
      then 0 <= comP.B - comP.A by A7;
      then 0 + comP.A <= comP.B by XREAL_1:21;
      hence thesis;
    end;
A68: for A being Event of COM(Sigma,P) holds comP.A <= 1
    proof
      let A be Event of COM(Sigma,P);
      reconsider B = Omega as Event of COM(Sigma,P) by PROB_1:53;
      comP.A <= comP.B by A67;
      hence thesis by A12;
    end;
A69: for BSeq being SetSequence of COM(Sigma,P) for n holds comP.(BSeq.n) <= 1
    proof
      let BSeq be SetSequence of COM(Sigma,P);
      let n;
      BSeq.n is Event of COM(Sigma,P) by PROB_1:def 10;
      hence thesis by A68;
    end;
A70: for BSeq being SetSequence of COM(Sigma,P) for n holds (comP*BSeq).n <= 1
    proof
      let BSeq be SetSequence of COM(Sigma,P);
      let n;
      (comP*BSeq).n = comP.(BSeq.n) by FUNCT_2:21;
      hence thesis by A69;
    end;
A71: for BSeq being SetSequence of COM(Sigma,P) st BSeq is non-decreasing
    holds comP * BSeq is non-decreasing
    proof
      let BSeq be SetSequence of COM(Sigma,P) such that
A72:  BSeq is non-decreasing;
      for n,m st n <= m holds (comP * BSeq).n <= (comP * BSeq).m
      proof
        let n,m such that
A73:    n <= m;
A74:    BSeq.n is Event of COM(Sigma,P) & BSeq.m is Event of COM(Sigma,P)
        by PROB_1:def 10;
        BSeq.n c= BSeq.m by A72,A73,PROB_1:def 7;
        then comP.(BSeq.n) <= comP.(BSeq.m) by A67,A74;
        then (comP * BSeq).n <= comP.(BSeq.m) by FUNCT_2:21;
        hence thesis by FUNCT_2:21;
      end;
      hence thesis by SEQM_3:12;
    end;
A75: for BSeq being SetSequence of COM(Sigma,P) st BSeq is non-decreasing
    holds comP * BSeq is bounded_above & comP * BSeq is convergent
    proof
      let BSeq be SetSequence of COM(Sigma,P) such that
A76:  BSeq is non-decreasing;
      for n holds (comP * BSeq).n < 2
      proof
        let n;
        (comP * BSeq).n + 0 < 1 + 1 by A70,XREAL_1:10;
        hence thesis;
      end;
      then comP * BSeq is bounded_above by SEQ_2:def 3;
      hence thesis by A71,A76,SEQ_4:51;
    end;
    for BSeq being SetSequence of COM(Sigma,P) st BSeq is non-decreasing
    holds (comP * BSeq is convergent & lim (comP * BSeq) = comP.Union BSeq)
    proof
      let BSeq be SetSequence of COM(Sigma,P) such that
A77:  BSeq is non-decreasing;
      set CSeq=@Partial_Diff_Union(BSeq);
A78:  comP.Union BSeq = comP.Union CSeq by PROB_3:40
        .= Sum(comP * CSeq) by A13,PROB_3:41
        .= lim Partial_Sums(comP * CSeq);
      for n holds Partial_Sums(comP * CSeq).n = (comP * BSeq).n
      proof
        defpred P[Element of NAT] means
        Partial_Sums(comP * CSeq).$1=(comP * BSeq).$1;
A79:    P[0]
        proof
          thus Partial_Sums(comP * CSeq).0 = (comP * CSeq).0 by SERIES_1:def 1
            .= comP.(CSeq.0) by FUNCT_2:21
            .= comP.(BSeq.0) by A77,Th21
            .= (comP * BSeq).0 by FUNCT_2:21;
        end;
A80:    for k st P[k] holds P[k+1]
        proof
          let k such that
A81:      P[k];
A82:      BSeq.k is Event of COM(Sigma,P) &
          CSeq.(k+1) is Event of COM(Sigma,P) by PROB_1:def 10;
A83:      BSeq.k c= BSeq.(k+1) by A77,PROB_2:15;
          thus Partial_Sums(comP * CSeq).(k+1)
          =(comP * BSeq).k + (comP * CSeq).(k+1) by A81,SERIES_1:def 1
            .=comP.(BSeq.k) + (comP * CSeq).(k+1) by FUNCT_2:21
            .=comP.(BSeq.k) + comP.(CSeq.(k+1)) by FUNCT_2:21
            .=comP.(BSeq.k \/ CSeq.(k+1)) by A51,A77,A82,Th22
            .=comP.(BSeq.k \/ (BSeq.(k+1) \ BSeq.k)) by A77,Th21
            .=comP.(BSeq.k \/ BSeq.(k+1)) by XBOOLE_1:39
            .=comP.(BSeq.(k+1)) by A83,XBOOLE_1:12
            .=(comP * BSeq).(k+1) by FUNCT_2:21;
        end;
        thus for k holds P[k] from NAT_1:sch 1(A79,A80);
      end;
      hence thesis by A75,A77,A78,FUNCT_2:113;
    end;
    then reconsider comP as Probability of COM(Sigma,P)
    by A7,A12,A51,PROB_2:20;
    take comP;
    thus thesis by A8;
  end;
  uniqueness
  proof
    let P1,P2 be Probability of COM(Sigma,P) such that
A84: (for B being set st B in Sigma
    for C being thin of P holds P1.(B \/ C) = P.B) and
A85: (for B being set st B in Sigma
    for C being thin of P holds P2.(B \/ C) = P.B);
    for x being set holds x in COM(Sigma,P) implies P1.x = P2.x
    proof
      let x be set;
      assume x in COM(Sigma,P);
      then consider B being set such that
A86:  B in Sigma & ex C being thin of P st x = B \/ C by Def5;
      P1.x = P.B by A84,A86
        .= P2.x by A85,A86;
      hence thesis;
    end;
    hence thesis by FUNCT_2:18;
  end;
end;

theorem
  COM(P) = COM(P2M P)
proof
  set Y = COM(P);
  reconsider Y1=P2M(Y) as sigma_Measure of COM(Sigma,P2M(P)) by Th28;
  for B being set st B in Sigma
  for C being thin of P2M(P) holds Y1.(B \/ C) = (P2M(P)).B
  proof
    let B be set such that
A1: B in Sigma;
    let C be thin of P2M(P);
    reconsider C1=C as thin of P by Th24;
    Y.(B \/ C1) = P.B by A1,Def8;
    hence thesis;
  end;
  hence thesis by MEASURE3:def 6;
end;

theorem
  COM(P) is_complete COM(Sigma,P)
proof
  for A being Subset of Omega for B being set st B in COM(Sigma,P) &
  A c= B & (COM P).B = 0 holds A in COM(Sigma,P)
  proof
    let A be Subset of Omega;
    let B be set;
    assume
A1: B in COM(Sigma,P);
    assume
A2: A c= B & (COM P).B = 0;
    ex B1 being set st (B1 in Sigma & ex C1 being thin of P st A = B1 \/ C1 )
    proof
      consider B2 being set such that
A3:   B2 in Sigma & ex C2 being thin of P st B = B2 \/ C2 by A1,Def5;
      consider C2 being thin of P such that
A4:   B = B2 \/ C2 by A3;
A5:   P.B2 = 0 by A2,A3,Def8;
      consider D2 being set such that
A6:   D2 in Sigma & C2 c= D2 & P.D2 = 0 by Def4;
      set C1 = (A /\ B2) \/ (A /\ C2);
A7:   A = A /\ (B2 \/ C2) by A2,A4,XBOOLE_1:28
        .= {} \/ C1 by XBOOLE_1:23;
      set O = B2 \/ D2;
      A /\ B2 c= B2 & A /\ C2 c= C2 by XBOOLE_1:17;
      then
A8:   A /\ B2 c= B2 & A /\ C2 c= D2 by A6,XBOOLE_1:1;
A9:   C1 is thin of P
      proof
        ex O being set st O in Sigma & C1 c= O & P.O = 0
        proof
          reconsider O1 = O as Element of Sigma by A3,A6,PROB_1:41;
A10:      O1 is Event of Sigma by PROB_1:def 10;
          reconsider B2,D2 as Element of Sigma by A3,A6;
          B2 is Event of Sigma & D2 is Event of Sigma by PROB_1:def 10;
          then
A11:      P.(B2 \/ D2) <= 0 + 0 by A5,A6,PROB_1:75;
          take O;
          thus thesis by A8,A10,A11,PROB_1:def 13,XBOOLE_1:13;
        end;
        hence thesis by Def4;
      end;
      take {};
      thus thesis by A7,A9,PROB_1:42;
    end;
    hence thesis by Def5;
  end;
  hence thesis by Def3;
end;

theorem Th40:
  for A being Event of Sigma holds P.A = (COM P).A
proof
  let A be Event of Sigma;
A1: A in Sigma by PROB_1:def 10;
  reconsider C = {} as thin of P by Th25;
  thus P.A = (COM P).(A \/ C) by A1,Def8
    .= (COM P).A;
end;

theorem Th41:
  for C being thin of P holds (COM P).C = 0
proof
  let C be thin of P;
  consider A being set such that
A1: A in Sigma & C c= A & P.A = 0 by Def4;
  reconsider A as Event of Sigma by A1,PROB_1:def 10;
A2: (COM P).A = 0 by A1,Th40;
  Sigma c= COM(Sigma,P) by Th29;
  then reconsider A as Event of COM(Sigma,P) by A1,PROB_1:def 10;
  (COM P).C <= (COM P).A by A1,PROB_1:70;
  hence thesis by A2,PROB_1:def 13;
end;

theorem
  for A being Element of COM(Sigma,P), B being set st B in ProbPart(A) holds
  P.B = (COM P).A
proof
  let A be Element of COM(Sigma,P), B be set such that
A1: B in ProbPart(A);
A2: B in Sigma & B c= A & A \ B is thin of P by A1,Def7;
  reconsider C = A \ B as thin of P by A1,Def7;
A3: A = B \/ C by A2,XBOOLE_1:45;
  reconsider A as Event of COM(Sigma,P) by PROB_1:def 10;
  Sigma c= COM(Sigma,P) by Th29;
  then reconsider B as Event of COM(Sigma,P) by A2,PROB_1:def 10;
  B misses C by XBOOLE_1:79;
  then
A4: (COM P).A = (COM P).B + (COM P).C by A3,PROB_1:def 13
    .= (COM P).B + 0 by Th41
    .= (COM P).B;
  reconsider B as Event of Sigma by A2,PROB_1:def 10;
  (COM P).A = P.B by A4,Th40;
  hence thesis;
end;

