:: Propositional Calculus for Boolean Valued Functions, {VIII}
::  by Shunichi Kobayashi
::
:: Received September 25, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies XBOOLE_0, SUBSET_1, FUNCT_2, MARGREL1, FUNCT_1, XBOOLEAN,
      RELAT_1, TARSKI, BVFUNC_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, XBOOLEAN, MARGREL1, RELAT_1, FUNCT_1,
      FUNCT_2, BVFUNC_1;
 constructors BVFUNC_1, RELSET_1;
 registrations FUNCT_1, FUNCT_2, XBOOLEAN, MARGREL1, XBOOLE_0;
 requirements SUBSET, BOOLE;
 definitions TARSKI, MARGREL1;
 theorems FUNCT_1, FUNCT_2, MARGREL1, BVFUNC_1, BVFUNC_4, BVFUNC_8, BVFUNC25,
      XBOOLEAN;
 schemes FUNCT_1;

begin

reserve Y for non empty set,
  a,b,c for Element of Funcs(Y,BOOLEAN);

definition
  let p,q be boolean-valued Function;
  func p 'nand' q -> Function means
  :Def1:
  dom it = dom p /\ dom q & for x
  being set st x in dom it holds it.x = (p.x) 'nand' (q.x);
  existence
  proof
    deffunc F(set) = (p.$1) 'nand' (q.$1);
    consider s being Function such that
A1: dom s = dom p /\ dom q & for x being set st x in dom p /\ dom q
    holds s.x = F(x) from FUNCT_1:sch 3;
    take s;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let s1,s2 be Function such that
A2: dom s1 = dom p /\ dom q and
A3: for x being set st x in dom s1 holds s1.x = (p.x) 'nand' (q.x) and
A4: dom s2 = dom p /\ dom q and
A5: for x being set st x in dom s2 holds s2.x = (p.x) 'nand' (q.x);
    for x being set st x in dom s1 holds s1.x = s2.x
    proof
      let x be set;
      assume
A6:   x in dom s1;
      then s1.x = (p.x) 'nand' (q.x) by A3;
      hence thesis by A2,A4,A5,A6;
    end;
    hence thesis by A2,A4,FUNCT_1:9;
  end;
  commutativity;
  func p 'nor' q -> Function means
  :Def2:
  dom it = dom p /\ dom q & for x
  being set st x in dom it holds it.x = (p.x) 'nor' (q.x);
  existence
  proof
    deffunc F(set) = (p.$1) 'nor' (q.$1);
    consider s being Function such that
A7: dom s = dom p /\ dom q & for x being set st x in dom p /\ dom q
    holds s.x = F(x) from FUNCT_1:sch 3;
    take s;
    thus thesis by A7;
  end;
  uniqueness
  proof
    let s1,s2 be Function such that
A8: dom s1 = dom p /\ dom q and
A9: for x being set st x in dom s1 holds s1.x = (p.x) 'nor' (q.x) and
A10: dom s2 = dom p /\ dom q and
A11: for x being set st x in dom s2 holds s2.x = (p.x) 'nor' (q.x);
    for x being set st x in dom s1 holds s1.x = s2.x
    proof
      let x be set;
      assume
A12:  x in dom s1;
      then s1.x = (p.x) 'nor' (q.x) by A9;
      hence thesis by A8,A10,A11,A12;
    end;
    hence thesis by A8,A10,FUNCT_1:9;
  end;
  commutativity;
end;

registration
  let p,q be boolean-valued Function;
  cluster p 'nand' q -> boolean-valued;
  coherence
  proof
    let x be set;
    assume x in rng(p 'nand' q);
    then consider y being set such that
A1: y in dom(p 'nand' q) & x = (p 'nand' q).y by FUNCT_1:def 5;
    x = (p.y) 'nand' (q.y) by A1,Def1;
    then x = FALSE or x = TRUE by XBOOLEAN:def 3;
    hence thesis;
  end;
  cluster p 'nor' q -> boolean-valued;
  coherence
  proof
    let x be set;
    assume x in rng(p 'nor' q);
    then consider y being set such that
A2: y in dom(p 'nor' q) & x = (p 'nor' q).y by FUNCT_1:def 5;
    x = (p.y) 'nor' (q.y) by A2,Def2;
    then x = FALSE or x = TRUE by XBOOLEAN:def 3;
    hence thesis;
  end;
end;

definition
  let A be non empty set;
  let p,q be Element of Funcs(A,BOOLEAN);
  redefine func p 'nand' q -> Element of Funcs(A,BOOLEAN) means
  :Def3:
  for x being Element of A holds it.x = (p.x) 'nand' (q.x);
  coherence
  proof
    ( ex f being Function st p = f & dom f = A & rng f c= BOOLEAN)& ex f
    being Function st q = f & dom f = A & rng f c= BOOLEAN by FUNCT_2:def 2;
    then
A1: dom(p 'nand' q) = A /\ A by Def1
      .= A;
    rng(p 'nand' q) c= BOOLEAN by MARGREL1:def 17;
    hence thesis by A1,FUNCT_2:def 2;
  end;
  compatibility
  proof
    let IT be Element of Funcs(A,BOOLEAN);
A2: dom IT = A by FUNCT_2:def 1;
    hereby
      assume
A3:   IT = p 'nand' q;
      let x be Element of A;
      dom p = A & dom q = A by FUNCT_2:def 1;
      then dom(p 'nand' q) = A /\ A by Def1
        .= A;
      hence IT.x = (p.x) 'nand' (q.x) by A3,Def1;
    end;
A4: dom q = A by FUNCT_2:def 1;
A5: dom IT = A /\ A by FUNCT_2:def 1
      .= dom p /\ dom q by A4,FUNCT_2:def 1;
    assume for x being Element of A holds IT.x = (p.x) 'nand' (q.x);
    then
    for x being set st x in dom IT holds IT.x = (p.x) 'nand' (q.x) by A2;
    hence thesis by A5,Def1;
  end;
  redefine func p 'nor' q -> Element of Funcs(A,BOOLEAN) means
  :Def4:
  for x being Element of A holds it.x = (p.x) 'nor' (q.x);
  coherence
  proof
    ( ex f being Function st p = f & dom f = A & rng f c= BOOLEAN)& ex f
    being Function st q = f & dom f = A & rng f c= BOOLEAN by FUNCT_2:def 2;
    then
A6: dom(p 'nor' q) = A /\ A by Def2
      .= A;
    rng(p 'nor' q) c= BOOLEAN by MARGREL1:def 17;
    hence thesis by A6,FUNCT_2:def 2;
  end;
  compatibility
  proof
    let IT be Element of Funcs(A,BOOLEAN);
A7: dom IT = A by FUNCT_2:def 1;
    hereby
      assume
A8:   IT = p 'nor' q;
      let x be Element of A;
      dom p = A & dom q = A by FUNCT_2:def 1;
      then dom(p 'nor' q) = A /\ A by Def2
        .= A;
      hence IT.x = (p.x) 'nor' (q.x) by A8,Def2;
    end;
A9: dom q = A by FUNCT_2:def 1;
A10: dom IT = A /\ A by FUNCT_2:def 1
      .= dom p /\ dom q by A9,FUNCT_2:def 1;
    assume for x being Element of A holds IT.x = (p.x) 'nor' (q.x);
    then
    for x being set st x in dom IT holds IT.x = (p.x) 'nor' (q.x) by A7;
    hence thesis by A10,Def2;
  end;
end;

definition
  let Y;
  let a,b be Element of Funcs(Y,BOOLEAN);
  redefine func a 'nand' b ->Element of Funcs(Y,BOOLEAN);
  coherence
  proof
    a 'nand' b is Element of Funcs(Y,BOOLEAN);
    hence thesis;
  end;
  redefine func a 'nor' b ->Element of Funcs(Y,BOOLEAN);
  coherence
  proof
    a 'nor' b is Element of Funcs(Y,BOOLEAN);
    hence thesis;
  end;
end;

theorem Th1:
  a 'nand' b = 'not' (a '&' b)
proof
  consider k3 being Function such that
A1: a 'nand' b=k3 and
A2: dom k3=Y and
  rng k3 c= BOOLEAN by FUNCT_2:def 2;
  consider k4 being Function such that
A3: 'not' (a '&' b)=k4 and
A4: dom k4=Y and
  rng k4 c= BOOLEAN by FUNCT_2:def 2;
  for x being Element of Y holds (a 'nand' b).x = ('not' (a '&' b)).x
  proof
    let x be Element of Y;
    thus ('not' (a '&' b)).x = 'not' (a '&' b).x by MARGREL1:def 20
      .= 'not' (a.x '&' b.x) by MARGREL1:def 21
      .= 'not' ('not' (a.x 'nand' b.x)) by BVFUNC_1:49
      .= (a 'nand' b).x by Def3;
  end;
  then for u being set st u in Y holds k3.u=k4.u by A1,A3;
  hence thesis by A1,A2,A3,A4,FUNCT_1:9;
end;

theorem Th2:
  a 'nor' b = 'not' (a 'or' b)
proof
  consider k3 being Function such that
A1: a 'nor' b=k3 and
A2: dom k3=Y and
  rng k3 c= BOOLEAN by FUNCT_2:def 2;
  consider k4 being Function such that
A3: 'not' (a 'or' b)=k4 and
A4: dom k4=Y and
  rng k4 c= BOOLEAN by FUNCT_2:def 2;
  for x being Element of Y holds (a 'nor' b).x = ('not' (a 'or' b)).x
  proof
    let x be Element of Y;
    thus ('not' (a 'or' b)).x = 'not' (a 'or' b).x by MARGREL1:def 20
      .= 'not' ((a).x 'or' (b).x) by BVFUNC_1:def 7
      .= 'not' 'not' (a.x 'nor' b.x) by BVFUNC_1:56
      .= (a 'nor' b).x by Def4;
  end;
  then for u being set st u in Y holds k3.u=k4.u by A1,A3;
  hence thesis by A1,A2,A3,A4,FUNCT_1:9;
end;

theorem Th3:
  I_el(Y) 'nand' a = 'not' a
proof
  I_el(Y) 'nand' a = 'not' (I_el(Y) '&' a) by Th1;
  hence thesis by BVFUNC_1:9;
end;

theorem Th4:
  O_el(Y) 'nand' a = I_el(Y)
proof
  O_el(Y) 'nand' a = 'not' (O_el(Y) '&' a) & O_el(Y) '&' a = O_el(Y) by
  Th1, BVFUNC_1:8;
  hence thesis by BVFUNC_1:5;
end;

theorem
  O_el(Y) 'nand' O_el(Y) = I_el(Y) & O_el(Y) 'nand' I_el(Y) = I_el(Y) &
  I_el(Y) 'nand' I_el(Y) = O_el(Y)
proof
  thus O_el(Y) 'nand' O_el(Y) = I_el(Y) by Th4;
  thus O_el(Y) 'nand' I_el(Y) = I_el(Y) by Th4;
  thus I_el(Y) 'nand' I_el(Y) = 'not' I_el(Y) by Th3
    .= O_el(Y) by BVFUNC_1:5;
end;

theorem
  a 'nand' a = 'not' a & 'not' (a 'nand' a) = a
proof
  a 'nand' a = 'not' (a '&' a) by Th1
    .= 'not' a;
  hence thesis;
end;

theorem
  'not' (a 'nand' b) = a '&' b
proof
  'not' (a 'nand' b) = 'not' 'not' (a '&' b) by Th1;
  hence thesis;
end;

theorem
  a 'nand' 'not' a = I_el(Y) & 'not' (a 'nand' 'not' a) = O_el(Y)
proof
  a 'nand' 'not' a = 'not' (a '&' 'not' a) by Th1
    .= 'not' O_el(Y) by BVFUNC_4:5
    .= I_el(Y) by BVFUNC_1:5;
  hence thesis by BVFUNC_1:5;
end;

theorem Th9:
  a 'nand' (b '&' c) = 'not' (a '&' b '&' c)
proof
  a 'nand' (b '&' c) = 'not' (a '&' (b '&' c)) by Th1;
  hence thesis by BVFUNC_1:7;
end;

theorem Th10:
  a 'nand' (b '&' c) = (a '&' b) 'nand' c
proof
  (a '&' b) 'nand' c = 'not' (a '&' b '&' c) by Th1;
  hence thesis by Th9;
end;

theorem Th11:
  a 'nand' (b 'or' c) = 'not' (a '&' b) '&' 'not' (a '&' c)
proof
  thus a 'nand' (b 'or' c) = 'not' (a '&' (b 'or' c)) by Th1
    .= 'not' (a '&' b 'or' a '&' c) by BVFUNC_1:15
    .= 'not' (a '&' b) '&' 'not' (a '&' c) by BVFUNC_1:16;
end;

theorem
  a 'nand' (b 'xor' c) = (a '&' b) 'eqv' (a '&' c)
proof
  thus a 'nand' (b 'xor' c) = 'not' (a '&' (b 'xor' c)) by Th1
    .= 'not' ((a '&' b) 'xor' (a '&' c)) by BVFUNC25:11
    .= 'not' 'not' ((a '&' b) 'eqv' (a '&' c)) by BVFUNC_8:12
    .= (a '&' b) 'eqv' (a '&' c);
end;

theorem
  a 'nand' (b 'nand' c) = 'not' a 'or' (b '&' c) & a 'nand' (b 'nand' c)
  = a 'imp' (b '&' c)
proof
  a 'nand' (b 'nand' c) = 'not' (a '&' (b 'nand' c)) by Th1
    .= 'not' (a '&' 'not' (b '&' c)) by Th1
    .= 'not' a 'or' 'not' 'not' (b '&' c) by BVFUNC_1:17
    .= 'not' a 'or' (b '&' c);
  hence thesis by BVFUNC_4:8;
end;

theorem
  a 'nand' (b 'nor' c) = 'not' a 'or' b 'or' c & a 'nand' (b 'nor' c) =
  a 'imp' (b 'or' c)
proof
A1: a 'nand' (b 'nor' c) = 'not' (a '&' (b 'nor' c)) by Th1
    .= 'not' (a '&' 'not' (b 'or' c)) by Th2
    .= 'not' a 'or' 'not' 'not' (b 'or' c) by BVFUNC_1:17
    .= 'not' a 'or' b 'or' c by BVFUNC_1:11;
  then a 'nand' (b 'nor' c) = 'not' a 'or' (b 'or' c) by BVFUNC_1:11;
  hence thesis by A1,BVFUNC_4:8;
end;

theorem
  a 'nand' (b 'eqv' c) = a 'imp' (b 'xor' c)
proof
  a 'nand' (b 'eqv' c) = 'not' (a '&' (b 'eqv' c)) by Th1
    .= ('not' a) 'or' 'not' (b 'eqv' c) by BVFUNC_1:17
    .= ('not' a) 'or' 'not' 'not' (b 'xor' c) by BVFUNC25:12
    .= ('not' a) 'or' (b 'xor' c);
  hence thesis by BVFUNC_4:8;
end;

theorem
  a 'nand' (a '&' b) = a 'nand' b
proof
  a 'nand' (a '&' b) = (a '&' a) 'nand' b by Th10;
  hence thesis;
end;

theorem
  a 'nand' (a 'or' b) = 'not' a '&' 'not' (a '&' b)
proof
  thus a 'nand' (a 'or' b) = 'not' (a '&' a) '&' 'not' (a '&' b) by Th11
    .= 'not' a '&' 'not' (a '&' b);
end;

theorem
  a 'nand' (a 'eqv' b) = a 'imp' (a 'xor' b)
proof
  a 'nand' (a 'eqv' b) = 'not' (a '&' (a 'eqv' b)) by Th1
    .= 'not' a 'or' 'not' (a 'eqv' b) by BVFUNC_1:17
    .= 'not' a 'or' 'not' 'not' (a 'xor' b) by BVFUNC25:12
    .= 'not' a 'or' (a 'xor' b);
  hence thesis by BVFUNC_4:8;
end;

theorem
  a 'nand' (a 'nand' b) = 'not' a 'or' b & a 'nand' (a 'nand' b) = a 'imp' b
proof
  a 'nand' (a 'nand' b) = 'not' (a '&' (a 'nand' b)) by Th1
    .= 'not' (a '&' 'not' (a '&' b)) by Th1
    .= ('not' a) 'or' ('not' 'not' (a '&' b)) by BVFUNC_1:17
    .= ('not' a 'or' a) '&' ('not' a 'or' b) by BVFUNC_1:14
    .= I_el(Y) '&' ('not' a 'or' b) by BVFUNC_4:6
    .= 'not' a 'or' b by BVFUNC_1:9;
  hence thesis by BVFUNC_4:8;
end;

theorem
  a 'nand' (a 'nor' b) = I_el(Y)
proof
  thus a 'nand' (a 'nor' b) = 'not' (a '&' (a 'nor' b)) by Th1
    .= 'not' (a '&' 'not' (a 'or' b)) by Th2
    .= ('not' a) 'or' ('not' 'not' (a 'or' b)) by BVFUNC_1:17
    .= ('not' a) 'or' a 'or' b by BVFUNC_1:11
    .= I_el(Y) 'or' b by BVFUNC_4:6
    .= I_el(Y) by BVFUNC_1:13;
end;

theorem
  a 'nand' (a 'eqv' b) = 'not' a 'or' 'not' b
proof
  thus a 'nand' (a 'eqv' b) = 'not' (a '&' (a 'eqv' b)) by Th1
    .= 'not' (a '&' 'not' (a 'xor' b)) by BVFUNC25:12
    .= ('not' a) 'or' ('not' 'not' (a 'xor' b)) by BVFUNC_1:17
    .= ('not' a) 'or' ((a 'or' b) '&' ('not' a 'or' 'not' b)) by BVFUNC_8:13
    .= (('not' a) 'or' (a 'or' b)) '&' (('not' a) 'or' ('not' a 'or' 'not' b
  )) by BVFUNC_1:14
    .= (('not' a) 'or' a 'or' b) '&' (('not' a) 'or' ('not' a 'or' 'not' b))
  by BVFUNC_1:11
    .= (I_el(Y) 'or' b) '&' (('not' a) 'or' ('not' a 'or' 'not' b)) by
  BVFUNC_4:6
    .= I_el(Y) '&' (('not' a) 'or' ('not' a 'or' 'not' b)) by BVFUNC_1:13
    .= ('not' a) 'or' ('not' a 'or' 'not' b) by BVFUNC_1:9
    .= ('not' a) 'or' 'not' a 'or' 'not' b by BVFUNC_1:11
    .= 'not' a 'or' 'not' b;
end;

theorem
  a '&' b = (a 'nand' b) 'nand' (a 'nand' b)
proof
  thus (a 'nand' b) 'nand' (a 'nand' b) = 'not' ((a 'nand' b) '&' (a 'nand' b)
  ) by Th1
    .= (a '&' b) 'or' 'not' 'not' (a '&' b) by Th1
    .= (a '&' b);
end;

theorem
  (a 'nand' b) 'nand' (a 'nand' c) = a '&' (b 'or' c)
proof
  thus (a 'nand' b) 'nand' (a 'nand' c) = 'not' ((a 'nand' b) '&' (a 'nand' c)
  ) by Th1
    .= 'not' ('not' (a '&' b) '&' (a 'nand' c)) by Th1
    .= 'not' ('not' (a '&' b) '&' 'not' (a '&' c)) by Th1
    .= 'not' 'not' (a '&' b) 'or' 'not' 'not' (a '&' c) by BVFUNC_1:17
    .= a '&' (b 'or' c) by BVFUNC_1:15;
end;

theorem Th24:
  a 'nand' (b 'imp' c) = ('not' a 'or' b) '&' 'not' (a '&' c)
proof
  thus a 'nand' (b 'imp' c) = 'not' (a '&' (b 'imp' c)) by Th1
    .= 'not' (a '&' ('not' b 'or' c)) by BVFUNC_4:8
    .= 'not' ((a '&' 'not' b) 'or' (a '&' c)) by BVFUNC_1:15
    .= 'not' (a '&' 'not' b) '&' 'not' (a '&' c) by BVFUNC_1:16
    .= (('not' a) 'or' ('not' 'not' b)) '&' 'not' (a '&' c) by BVFUNC_1:17
    .= ('not' a 'or' b) '&' 'not' (a '&' c);
end;

theorem
  a 'nand' (a 'imp' b) = 'not' (a '&' b)
proof
  thus a 'nand' (a 'imp' b) = ('not' a 'or' a) '&' 'not' (a '&' b) by Th24
    .= I_el(Y) '&' 'not' (a '&' b) by BVFUNC_4:6
    .= 'not' (a '&' b) by BVFUNC_1:9;
end;

theorem Th26:
  I_el(Y) 'nor' a = O_el(Y)
proof
  thus I_el(Y) 'nor' a = 'not' (I_el(Y) 'or' a) by Th2
    .= 'not' I_el(Y) by BVFUNC_1:13
    .= O_el(Y) by BVFUNC_1:5;
end;

theorem Th27:
  O_el(Y) 'nor' a = 'not' a
proof
  thus O_el(Y) 'nor' a = 'not' (O_el(Y) 'or' a) by Th2
    .= 'not' a by BVFUNC_1:12;
end;

theorem
  O_el(Y) 'nor' O_el(Y) = I_el(Y) & O_el(Y) 'nor' I_el(Y) = O_el(Y) &
  I_el(Y) 'nor' I_el(Y) = O_el(Y)
proof
  thus O_el(Y) 'nor' O_el(Y) = 'not' O_el(Y) by Th27
    .= I_el(Y) by BVFUNC_1:5;
  thus O_el(Y) 'nor' I_el(Y) = O_el(Y) by Th26;
  thus thesis by Th26;
end;

theorem
  a 'nor' a = 'not' a & 'not' (a 'nor' a) = a
proof
  a 'nor' a = 'not' (a 'or' a) by Th2
    .= 'not' a;
  hence thesis;
end;

theorem
  'not' (a 'nor' b) = a 'or' b
proof
  'not' (a 'nor' b) = 'not' 'not' (a 'or' b) by Th2;
  hence thesis;
end;

theorem
  a 'nor' 'not' a = O_el(Y) & 'not' (a 'nor' 'not' a) = I_el(Y)
proof
  a 'nor' 'not' a = 'not' (a 'or' 'not' a) by Th2
    .= 'not' I_el(Y) by BVFUNC_4:6
    .= O_el(Y) by BVFUNC_1:5;
  hence thesis by BVFUNC_1:5;
end;

theorem
  'not' a '&' (a 'xor' b) = 'not' a '&' b
proof
  thus ('not' a) '&' (a 'xor' b) = ('not' a) '&' (('not' a '&' b) 'or' (a '&'
  'not' b)) by BVFUNC_4:9
    .= (('not' a) '&' ('not' a '&' b)) 'or' (('not' a) '&' (a '&' 'not' b))
  by BVFUNC_1:15
    .= ('not' a '&' 'not' a '&' b) 'or' (('not' a) '&' (a '&' 'not' b)) by
  BVFUNC_1:7
    .= ('not' a '&' b) 'or' ('not' a '&' a '&' 'not' b) by BVFUNC_1:7
    .= ('not' a '&' b) 'or' (O_el(Y) '&' 'not' b) by BVFUNC_4:5
    .= ('not' a '&' b) 'or' O_el(Y) by BVFUNC_1:8
    .= 'not' a '&' b by BVFUNC_1:12;
end;

theorem Th33:
  a 'nor' (b '&' c) = 'not' (a 'or' b) 'or' 'not' (a 'or' c)
proof
  a 'nor' (b '&' c) = 'not' (a 'or' (b '&' c)) by Th2
    .= 'not' ((a 'or' b) '&' (a 'or' c)) by BVFUNC_1:14;
  hence thesis by BVFUNC_1:17;
end;

theorem Th34:
  a 'nor' (b 'or' c) = 'not' (a 'or' b 'or' c)
proof
  thus a 'nor' (b 'or' c) = 'not' (a 'or' (b 'or' c)) by Th2
    .= 'not' (a 'or' b 'or' c) by BVFUNC_1:11;
end;

theorem Th35:
  a 'nor' (b 'eqv' c) = ('not' a) '&' (b 'xor' c)
proof
  thus a 'nor' (b 'eqv' c) = 'not' (a 'or' (b 'eqv' c)) by Th2
    .= ('not' a) '&' ('not' (b 'eqv' c)) by BVFUNC_1:16
    .= ('not' a) '&' ('not' 'not' (b 'xor' c)) by BVFUNC25:12
    .= ('not' a) '&' (b 'xor' c);
end;

theorem Th36:
  a 'nor' (b 'imp' c) = 'not' a '&' b '&' 'not' c
proof
  thus a 'nor' (b 'imp' c) = 'not' (a 'or' (b 'imp' c)) by Th2
    .= ('not' a) '&' ('not' (b 'imp' c)) by BVFUNC_1:16
    .= ('not' a) '&' (b '&' 'not' c) by BVFUNC25:1
    .= 'not' a '&' b '&' 'not' c by BVFUNC_1:7;
end;

theorem Th37:
  a 'nor' (b 'nand' c) = 'not' a '&' b '&' c
proof
  thus a 'nor' (b 'nand' c) = 'not' (a 'or' (b 'nand' c)) by Th2
    .= 'not' (a 'or' 'not' (b '&' c)) by Th1
    .= 'not' a '&' 'not' 'not' (b '&' c) by BVFUNC_1:16
    .= 'not' a '&' b '&' c by BVFUNC_1:7;
end;

theorem Th38:
  a 'nor' (b 'nor' c) = 'not' a '&' (b 'or' c)
proof
  thus a 'nor' (b 'nor' c) = 'not' (a 'or' (b 'nor' c)) by Th2
    .= 'not' (a 'or' 'not' (b 'or' c)) by Th2
    .= 'not' a '&' 'not' 'not' (b 'or' c) by BVFUNC_1:16
    .= 'not' a '&' (b 'or' c);
end;

theorem
  a 'nor' (a '&' b) = 'not' (a '&' (a 'or' b))
proof
  thus a 'nor' (a '&' b) = 'not' (a 'or' a) 'or' 'not' (a 'or' b) by Th33
    .= 'not' (a '&' (a 'or' b)) by BVFUNC_1:17;
end;

theorem
  a 'nor' (a 'or' b) = 'not' (a 'or' b)
proof
  thus a 'nor' (a 'or' b) = 'not' (a 'or' a 'or' b) by Th34
    .= 'not' (a 'or' b);
end;

theorem
  a 'nor' (a 'eqv' b) = 'not' a '&' b
proof
  thus a 'nor' (a 'eqv' b) = ('not' a) '&' (a 'xor' b) by Th35
    .= ('not' a) '&' (('not' a '&' b) 'or' (a '&' 'not' b)) by BVFUNC_4:9
    .= ('not' a) '&' ('not' a '&' b) 'or' ('not' a) '&' (a '&' 'not' b) by
  BVFUNC_1:15
    .= ('not' a) '&' ('not' a '&' b) 'or' ('not' a '&' a '&' 'not' b) by
  BVFUNC_1:7
    .= ('not' a) '&' ('not' a '&' b) 'or' (O_el(Y) '&' 'not' b) by BVFUNC_4:
  5
    .= ('not' a) '&' ('not' a '&' b) 'or' O_el(Y) by BVFUNC_1:8
    .= ('not' a) '&' ('not' a '&' b) by BVFUNC_1:12
    .= ('not' a) '&' ('not' a) '&' b by BVFUNC_1:7
    .= 'not' a '&' b;
end;

theorem
  a 'nor' (a 'imp' b) = O_el(Y)
proof
  thus a 'nor' (a 'imp' b) = 'not' a '&' a '&' 'not' b by Th36
    .= O_el(Y) '&' 'not' b by BVFUNC_4:5
    .= O_el(Y) by BVFUNC_1:8;
end;

theorem
  a 'nor' (a 'nand' b) = O_el(Y)
proof
  thus a 'nor' (a 'nand' b) = 'not' a '&' a '&' b by Th37
    .= O_el(Y) '&' b by BVFUNC_4:5
    .= O_el(Y) by BVFUNC_1:8;
end;

theorem
  a 'nor' (a 'nor' b) = 'not' a '&' b
proof
  thus a 'nor' (a 'nor' b) = 'not' a '&' (a 'or' b) by Th38
    .= ('not' a '&' a) 'or' ('not' a '&' b) by BVFUNC_1:15
    .= O_el(Y) 'or' ('not' a '&' b) by BVFUNC_4:5
    .= 'not' a '&' b by BVFUNC_1:12;
end;

theorem
  O_el(Y) 'eqv' O_el(Y) = I_el(Y)
proof
  thus O_el(Y) 'eqv' O_el(Y) = 'not' (O_el(Y) 'xor' O_el(Y)) by BVFUNC25:12
    .= 'not' O_el(Y) by BVFUNC25:13
    .= I_el(Y) by BVFUNC_1:5;
end;

theorem
  O_el(Y) 'eqv' I_el(Y) = O_el(Y)
proof
  thus O_el(Y) 'eqv' I_el(Y) = 'not' (O_el(Y) 'xor' I_el(Y)) by BVFUNC25:12
    .= 'not' (O_el(Y) 'xor' 'not' O_el(Y)) by BVFUNC_1:5
    .= 'not' I_el(Y) by BVFUNC25:14
    .= O_el(Y) by BVFUNC_1:5;
end;

theorem
  I_el(Y) 'eqv' I_el(Y) = I_el(Y)
proof
  thus I_el(Y) 'eqv' I_el(Y) = 'not' (I_el(Y) 'xor' I_el(Y)) by BVFUNC25:12
    .= 'not' 'not' I_el(Y) by BVFUNC_8:14
    .= I_el(Y);
end;

theorem
  a 'eqv' a = I_el(Y) & 'not' (a 'eqv' a) = O_el(Y)
proof
  a 'eqv' a = 'not' (a 'xor' a) by BVFUNC25:12
    .= 'not' O_el(Y) by BVFUNC25:13
    .= I_el(Y) by BVFUNC_1:5;
  hence thesis by BVFUNC_1:5;
end;

theorem
  a 'eqv' (a 'or' b) = a 'or' 'not' b
proof
  thus a 'eqv' (a 'or' b) = 'not' (a 'xor' (a 'or' b)) by BVFUNC25:12
    .= 'not' (('not' a '&' (a 'or' b)) 'or' (a '&' 'not' (a 'or' b))) by
  BVFUNC_4:9
    .= 'not' (('not' a '&' a 'or' 'not' a '&' b) 'or' (a '&' 'not' (a 'or' b
  ))) by BVFUNC_1:15
    .= 'not' ((O_el(Y) 'or' 'not' a '&' b) 'or' (a '&' 'not' (a 'or' b))) by
  BVFUNC_4:5
    .= 'not' (('not' a '&' b) 'or' (a '&' 'not' (a 'or' b))) by BVFUNC_1:12
    .= 'not' (('not' a '&' b) 'or' (a '&' ('not' a '&' 'not' b))) by
  BVFUNC_1:16
    .= 'not' (('not' a '&' b) 'or' (a '&' 'not' a '&' 'not' b)) by BVFUNC_1:
  7
    .= 'not' (('not' a '&' b) 'or' (O_el(Y) '&' 'not' b)) by BVFUNC_4:5
    .= 'not' (('not' a '&' b) 'or' O_el(Y)) by BVFUNC_1:8
    .= 'not' ('not' a '&' b) by BVFUNC_1:12
    .= 'not' 'not' a 'or' 'not' b by BVFUNC_1:17
    .= a 'or' 'not' b;
end;

theorem Th50:
  a '&' (b 'nand' c) = (a '&' 'not' b) 'or' (a '&' 'not' c)
proof
  thus a '&' (b 'nand' c) =a '&' 'not' (b '&' c) by Th1
    .=a '&' ('not' b 'or' 'not' c) by BVFUNC_1:17
    .=(a '&' 'not' b) 'or' (a '&' 'not' c) by BVFUNC_1:15;
end;

theorem Th51:
  a 'or' (b 'nand' c) = a 'or' 'not' b 'or' 'not' c
proof
  thus a 'or' (b 'nand' c) =a 'or' 'not' (b '&' c) by Th1
    .=a 'or' ('not' b 'or' 'not' c) by BVFUNC_1:17
    .=a 'or' 'not' b 'or' 'not' c by BVFUNC_1:11;
end;

theorem Th52:
  a 'xor' (b 'nand' c) = ('not' a '&' 'not' (b '&' c)) 'or' (a '&' b '&' c)
proof
  thus a 'xor' (b 'nand' c) =a 'xor' 'not' (b '&' c) by Th1
    .=('not' a '&' 'not' (b '&' c)) 'or' (a '&' 'not' 'not' (b '&' c)) by
  BVFUNC_4:9
    .=('not' a '&' 'not' (b '&' c)) 'or' (a '&' b '&' c) by BVFUNC_1:7;
end;

theorem Th53:
  a 'eqv' (b 'nand' c) = (a '&' 'not' (b '&' c)) 'or' ('not' a '&' b '&' c)
proof
  thus a 'eqv' (b 'nand' c) =a 'eqv' 'not' (b '&' c) by Th1
    .=(a '&' 'not' (b '&' c)) 'or' ('not' a '&' 'not' 'not' (b '&' c)) by
  BVFUNC_8:19
    .=(a '&' 'not' (b '&' c)) 'or' ('not' a '&' b '&' c) by BVFUNC_1:7;
end;

theorem Th54:
  a 'imp' (b 'nand' c) = 'not' (a '&' b '&' c)
proof
  thus a 'imp' (b 'nand' c) =a 'imp' 'not' (b '&' c) by Th1
    .='not' a 'or' 'not' (b '&' c) by BVFUNC_4:8
    .='not' (a '&' (b '&' c)) by BVFUNC_1:17
    .='not' (a '&' b '&' c) by BVFUNC_1:7;
end;

theorem Th55:
  a 'nor' (b 'nand' c) = 'not' (a 'or' 'not' b 'or' 'not' c)
proof
  thus a 'nor' (b 'nand' c) =a 'nor' 'not' (b '&' c) by Th1
    .='not' (a 'or' 'not' (b '&' c)) by Th2
    .='not' (a 'or' ('not' b 'or' 'not' c)) by BVFUNC_1:17
    .='not' (a 'or' 'not' b 'or' 'not' c) by BVFUNC_1:11;
end;

theorem
  a '&' (a 'nand' b) = a '&' 'not' b
proof
  thus a '&' (a 'nand' b) =(a '&' 'not' a) 'or' (a '&' 'not' b) by Th50
    .=O_el(Y) 'or' (a '&' 'not' b) by BVFUNC_4:5
    .=a '&' 'not' b by BVFUNC_1:12;
end;

theorem
  a 'or' (a 'nand' b) = I_el(Y)
proof
  thus a 'or' (a 'nand' b) =a 'or' 'not' a 'or' 'not' b by Th51
    .=I_el(Y) 'or' 'not' b by BVFUNC_4:6
    .=I_el(Y) by BVFUNC_1:13;
end;

theorem
  a 'xor' (a 'nand' b) = 'not' a 'or' b
proof
  thus a 'xor' (a 'nand' b) =('not' a '&' 'not' (a '&' b)) 'or' (a '&' a '&' b
  ) by Th52
    .=('not' a 'or' (a '&' b)) '&' ('not' (a '&' b) 'or' (a '&' b)) by
  BVFUNC_1:14
    .=('not' a 'or' (a '&' b)) '&' I_el(Y) by BVFUNC_4:6
    .='not' a 'or' (a '&' b) by BVFUNC_1:9
    .=('not' a 'or' a) '&' ('not' a 'or' b) by BVFUNC_1:14
    .=I_el(Y) '&' ('not' a 'or' b) by BVFUNC_4:6
    .='not' a 'or' b by BVFUNC_1:9;
end;

theorem
  a 'eqv' (a 'nand' b) = a '&' 'not' b
proof
  thus a 'eqv' (a 'nand' b) =(a '&' 'not' (a '&' b)) 'or' ('not' a '&' a '&' b
  ) by Th53
    .=(a '&' 'not' (a '&' b)) 'or' (O_el(Y) '&' b) by BVFUNC_4:5
    .=(a '&' 'not' (a '&' b)) 'or' O_el(Y) by BVFUNC_1:8
    .=(a '&' 'not' (a '&' b)) by BVFUNC_1:12
    .=(a '&' ('not' a 'or' 'not' b)) by BVFUNC_1:17
    .=(a '&' 'not' a) 'or' (a '&' 'not' b) by BVFUNC_1:15
    .=O_el(Y) 'or' (a '&' 'not' b) by BVFUNC_4:5
    .=a '&' 'not' b by BVFUNC_1:12;
end;

theorem
  a 'imp' (a 'nand' b) = 'not' (a '&' b)
proof
  thus a 'imp' (a 'nand' b) ='not' (a '&' a '&' b) by Th54
    .='not' (a '&' b);
end;

theorem
  a 'nor' (a 'nand' b) = O_el(Y)
proof
  thus a 'nor' (a 'nand' b) ='not' (a 'or' 'not' a 'or' 'not' b) by Th55
    .='not' (I_el(Y) 'or' 'not' b) by BVFUNC_4:6
    .='not' I_el(Y) by BVFUNC_1:13
    .=O_el(Y) by BVFUNC_1:5;
end;

theorem Th62:
  a '&' (b 'nor' c) = a '&' 'not' b '&' 'not' c
proof
  thus a '&' (b 'nor' c) =a '&' 'not' (b 'or' c) by Th2
    .=a '&' ('not' b '&' 'not' c) by BVFUNC_1:16
    .=a '&' 'not' b '&' 'not' c by BVFUNC_1:7;
end;

theorem Th63:
  a 'or' (b 'nor' c) = (a 'or' 'not' b) '&' (a 'or' 'not' c)
proof
  thus a 'or' (b 'nor' c) =a 'or' 'not' (b 'or' c) by Th2
    .=a 'or' ('not' b '&' 'not' c) by BVFUNC_1:16
    .=(a 'or' 'not' b) '&' (a 'or' 'not' c) by BVFUNC_1:14;
end;

theorem Th64:
  a 'xor' (b 'nor' c) = (a 'or' 'not' (b 'or' c)) '&' ('not' a 'or' b 'or' c)
proof
  thus a 'xor' (b 'nor' c) =a 'xor' 'not' (b 'or' c) by Th2
    .=(a 'or' 'not' (b 'or' c)) '&' ('not' a 'or' 'not' 'not' (b 'or' c)) by
  BVFUNC_8:13
    .=(a 'or' 'not' (b 'or' c)) '&' ('not' a 'or' b 'or' c) by BVFUNC_1:11;
end;

theorem Th65:
  a 'eqv' (b 'nor' c) = (a 'or' b 'or' c) '&' ('not' a 'or' 'not' (b 'or' c))
proof
  thus a 'eqv' (b 'nor' c) =a 'eqv' 'not' (b 'or' c) by Th2
    .=(a 'or' 'not' 'not' (b 'or' c)) '&' ('not' a 'or' 'not' (b 'or' c)) by
  BVFUNC_8:18
    .=(a 'or' b 'or' c) '&' ('not' a 'or' 'not' (b 'or' c)) by BVFUNC_1:11;
end;

theorem Th66:
  a 'imp' (b 'nor' c) = 'not' (a '&' (b 'or' c))
proof
  thus a 'imp' (b 'nor' c) =a 'imp' 'not' (b 'or' c) by Th2
    .='not' a 'or' 'not' (b 'or' c) by BVFUNC_4:8
    .='not' (a '&' (b 'or' c)) by BVFUNC_1:17;
end;

theorem Th67:
  a 'nand' (b 'nor' c) = 'not' a 'or' b 'or' c
proof
  thus a 'nand' (b 'nor' c) =a 'nand' 'not' (b 'or' c) by Th2
    .='not' (a '&' 'not' (b 'or' c)) by Th1
    .='not' a 'or' 'not' 'not' (b 'or' c) by BVFUNC_1:17
    .='not' a 'or' b 'or' c by BVFUNC_1:11;
end;

theorem
  a '&' (a 'nor' b) = O_el(Y)
proof
  thus a '&' (a 'nor' b) =a '&' 'not' a '&' 'not' b by Th62
    .=O_el(Y) '&' 'not' b by BVFUNC_4:5
    .=O_el(Y) by BVFUNC_1:8;
end;

theorem
  a 'or' (a 'nor' b) = a 'or' 'not' b
proof
  thus a 'or' (a 'nor' b) =(a 'or' 'not' a) '&' (a 'or' 'not' b) by Th63
    .=I_el(Y) '&' (a 'or' 'not' b) by BVFUNC_4:6
    .=a 'or' 'not' b by BVFUNC_1:9;
end;

theorem
  a 'xor' (a 'nor' b) = a 'or' 'not' b
proof
  thus a 'xor' (a 'nor' b) =(a 'or' 'not' (a 'or' b)) '&' ('not' a 'or' a 'or'
  b) by Th64
    .=(a 'or' 'not' (a 'or' b)) '&' (I_el(Y) 'or' b) by BVFUNC_4:6
    .=(a 'or' 'not' (a 'or' b)) '&' I_el(Y) by BVFUNC_1:13
    .=a 'or' 'not' (a 'or' b) by BVFUNC_1:9
    .=a 'or' ('not' a '&' 'not' b) by BVFUNC_1:16
    .=(a 'or' 'not' a) '&' (a 'or' 'not' b) by BVFUNC_1:14
    .=I_el(Y) '&' (a 'or' 'not' b) by BVFUNC_4:6
    .=a 'or' 'not' b by BVFUNC_1:9;
end;

theorem
  a 'eqv' (a 'nor' b) = 'not' a '&' b
proof
  thus a 'eqv' (a 'nor' b) =(a 'or' a 'or' b) '&' ('not' a 'or' 'not' (a 'or'
  b)) by Th65
    .=((a 'or' b) '&' 'not' a) 'or' ((a 'or' b) '&' 'not' (a 'or' b)) by
  BVFUNC_1:15
    .=((a 'or' b) '&' 'not' a) 'or' O_el(Y) by BVFUNC_4:5
    .=(a 'or' b) '&' 'not' a by BVFUNC_1:12
    .=(a '&' 'not' a) 'or' (b '&' 'not' a) by BVFUNC_1:15
    .=O_el(Y) 'or' (b '&' 'not' a) by BVFUNC_4:5
    .='not' a '&' b by BVFUNC_1:12;
end;

theorem
  a 'imp' (a 'nor' b) = 'not' (a 'or' a '&' b)
proof
  thus a 'imp' (a 'nor' b) ='not' (a '&' (a 'or' b)) by Th66
    .='not' (a '&' a 'or' a '&' b) by BVFUNC_1:15
    .='not' (a 'or' a '&' b);
end;

theorem
  a 'nand' (a 'nor' b) = I_el(Y)
proof
  thus a 'nand' (a 'nor' b) ='not' a 'or' a 'or' b by Th67
    .=I_el(Y) 'or' b by BVFUNC_4:6
    .=I_el(Y) by BVFUNC_1:13;
end;
