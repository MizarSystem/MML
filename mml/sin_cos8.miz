:: Formulas and Identities of Hyperbolic Functions
::  by Pacharapokin Chanapat and Hiroshi Yamazaki
:: 
:: Received November 7, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies ARYTM, ARYTM_3, SIN_COS, ARYTM_1, SQUARE_1, FUNCT_1, ORDINAL2,
      SIN_COS2, GROUP_1, SIN_COS5;
 notations ORDINAL1, XCMPLX_0, XREAL_0, REAL_1, NAT_1, SQUARE_1, PREPOWER,
      SEQ_1, SIN_COS, SIN_COS2, SIN_COS5;
 constructors NAT_1, REAL_1, RFUNCT_1, SQUARE_1, PREPOWER, BINOP_2, SIN_COS,
      SIN_COS2, SIN_COS5;
 registrations XREAL_0, MEMBERED, SQUARE_1, SIN_COS;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 theorems XREAL_1, SQUARE_1, SIN_COS, NEWTON, XCMPLX_1, SIN_COS2, SIN_COS5,
      POLYEQ_2, SIN_COS7, TAYLOR_1;

begin :: Hyperbolic sine, hyperbolic cosine and hyperbolic tangent

 reserve x, y, z, w for real number;
 reserve n for Nat;

Na3:
  cosh x >= 1 & cosh 0 = 1 & sinh 0 = 0
proof
  cosh.x >= 1 by SIN_COS2:37;
  hence thesis by SIN_COS2:def 2,SIN_COS2:def 4,SIN_COS2:15,SIN_COS2:16;
end;

TH3: :: SIN_COS5:46
  x >= 0 implies sinh x >= 0 by SIN_COS5:46;

Na1:
  sinh x = (exp(x)-exp(-x))/2 &
  cosh x = (exp(x)+exp(-x))/2 &
  tanh x = (exp(x)-exp(-x))/(exp(x)+exp(-x))
proof 
A1: sinh x = sinh.x by SIN_COS2:def 2
         .=(exp.x-exp.-x)/2 by SIN_COS2:def 1
         .=(exp(x)-exp.-x)/2 by SIN_COS:def 27
         .=(exp(x)-exp(-x))/2 by SIN_COS:def 27;
A2: cosh x = cosh.x by SIN_COS2:def 4
         .=(exp.x+exp.-x)/2 by SIN_COS2:def 3
         .=(exp(x)+exp.-x)/2 by SIN_COS:def 27
         .=(exp(x)+exp(-x))/2 by SIN_COS:def 27;
    tanh x = tanh.x by SIN_COS2:def 6
         .= sinh.x/cosh.x by SIN_COS2:17
         .= sinh(x)/cosh.x by SIN_COS2:def 2
         .= (exp(x)-exp(-x))/2/((exp(x)+exp(-x))/2) by A2,A1,SIN_COS2:def 4;
  hence thesis by XCMPLX_1:55,A1,A2;
end;
  
Na2:
  (cosh x)^2-(sinh x)^2 = 1 &
  cosh(x)*cosh(x)-sinh(x)*sinh(x) = 1
proof
A1: (cosh x)^2-(sinh x)^2
          = (cosh x)^2-(sinh.x)^2 by SIN_COS2:def 2
         .= (cosh.x)^2-(sinh.x)^2 by SIN_COS2:def 4
         .= 1 by SIN_COS2:14;
  cosh(x)*cosh(x)-sinh(x)*sinh(x)
          = (cosh x)^2-sinh(x)*sinh(x) by SQUARE_1:def 3
         .= (cosh x)^2-(sinh x)^2 by SQUARE_1:def 3;
  hence thesis by A1;
end;
    
theorem Na4:
  tanh x = sinh(x)/cosh(x) & tanh 0 = 0
proof
A1: tanh(x) = tanh.x by SIN_COS2:def 6
         .= sinh.x/cosh.x by SIN_COS2:17
         .= sinh(x)/cosh.x by SIN_COS2:def 2
         .= sinh(x)/cosh(x) by SIN_COS2:def 4;
  tanh 0 = tanh.0 by SIN_COS2:def 6
         .= sinh.0/cosh.0 by SIN_COS2:17
         .= 0 by SIN_COS2:16;
   hence thesis by A1;
 end;

Na6:
  x <> 0 implies sinh x <> 0 & tanh x <> 0
proof assume A1: x <> 0;
A2: cosh(x) <> 0 by Na3;
A3: exp(x) <> 1 by A1,SIN_COS7:29;
    exp(x) > 0 by SIN_COS:60;then
    1/exp(x) <> exp(x) by A3,XCMPLX_1:200;then
    (exp(x) - exp(-x))/2 <> 0 by TAYLOR_1:4;then
    sinh(x) <> 0 by Na1;then
    sinh(x)/cosh(x) <> 0 by A2,XCMPLX_1:50;
    hence thesis by Na4;
end;
 
Na7:
  y-z <> 0 implies sinh(y/2-z/2) <> 0
proof assume y-z <> 0;then
  (y-z)/2 <> 0;
  hence thesis by Na6;
end;

Na8:
  y+z <> 0 implies sinh(y/2+z/2) <> 0
proof assume y+z <> 0;then
  (y+z)/2 <> 0;
  hence thesis by Na6;
end; 

Na9:
  (sinh x)^2 = 1/2*(cosh(2*x) - 1) & (cosh x)^2 = 1/2*(cosh(2*x) + 1)
proof
A1: (sinh x)^2 = (sinh.x)^2 by SIN_COS2:def 2
         .=1/2*(cosh.(2*x)-1) by SIN_COS2:18
         .=1/2*(cosh(2*x)-1) by SIN_COS2:def 4;
    (cosh x)^2 = (cosh.x)^2 by SIN_COS2:def 4
         .=1/2*(cosh.(2*x)+1) by SIN_COS2:18;
   hence thesis by A1,SIN_COS2:def 4;
 end;

Na10:
  sinh(-x) = -sinh x & cosh(-x) = cosh(x) &
  tanh(-x) = -tanh x & coth(-x) = -coth(x) &
  sech(-x) = sech x & cosech(-x) = -cosech(x)
proof
A1: sinh(-x) = sinh.-x by SIN_COS2:def 2
         .= -sinh.x by SIN_COS2:19
         .= -sinh x by SIN_COS2:def 2;
A2: cosh(-x) = cosh.-x by SIN_COS2:def 4
         .= cosh.x by SIN_COS2:19
         .= cosh x by SIN_COS2:def 4;
A3: tanh(-x) = tanh.-x by SIN_COS2:def 6
         .= -tanh.x by SIN_COS2:19
         .= -tanh x by SIN_COS2:def 6;
A4: coth(-x) = cosh(x)/-sinh(x) by A1,A2,SIN_COS5:def 1
         .= -(cosh(x)/sinh(x)) by XCMPLX_1:189
         .= -coth x by SIN_COS5:def 1;
A5: sech(-x) = 1/cosh(x) by A2,SIN_COS5:def 2
         .= sech x by SIN_COS5:def 2;
    cosech(-x) = 1/(-sinh(x)) by A1,SIN_COS5:def 3
         .= -(1/sinh x) by XCMPLX_1:189
         .= -cosech x by SIN_COS5:def 3;
  hence thesis by A1,A2,A3,A4,A5;
end;

theorem TH1:
  sinh x = 1/cosech x & cosh x = 1/sech x & tanh x = 1/coth x
proof
A1: sinh x = 1/(1/sinh x) by XCMPLX_1:56
         .= 1/cosech x by SIN_COS5:def 3;
A2: cosh x = 1/(1/cosh x) by XCMPLX_1:56
         .= 1/sech(x) by SIN_COS5:def 2;
    tanh x = sinh(x)/cosh(x) by Na4
         .= 1/(cosh(x)/sinh(x)) by XCMPLX_1:57
         .= 1/coth x by SIN_COS5:def 1;
  hence thesis by A1,A2;
end;

Na11:
  exp(x)+exp(-x) >= 2
proof
A1:exp.x >= 0 by SIN_COS:59;
A2: exp(x) >= 0 by A1,SIN_COS:def 27;
A3: exp.-x >= 0 by SIN_COS:59;
A4: exp(-x) >= 0 by A3,SIN_COS:def 27;
  2*sqrt(exp(x)*exp(-x))
     =2*sqrt(exp(x)*exp.-x) by SIN_COS:def 27
    .=2*sqrt(exp.x*exp.-x) by SIN_COS:def 27
    .=2*sqrt(exp.(x+-x)) by SIN_COS2:12
    .=2*1 by SIN_COS:56,def 27,SQUARE_1:83;
   hence thesis by A2,A4,SIN_COS2:1;
 end;

theorem TH2:
  sech x <= 1 & 0 < sech x & sech 0 = 1
proof
A1:sech 0 = 1/1 by Na3,SIN_COS5:def 2  .= 1;
  0 < cosh.x by SIN_COS2:15;then
  0 < cosh x by SIN_COS2:def 4;then
A2:0 < 1/cosh(x) by XREAL_1:141;
  exp(x)+exp(-x) >= 2 by Na11;then
  2/2 >= 2/(exp(x)+exp(-x)) by XREAL_1:185;
  hence thesis by SIN_COS5:36,A1,A2,SIN_COS5:def 2;
end;

theorem
  x >= 0 implies tanh x >= 0
proof assume A0: x >= 0;
A1: cosh(x) >= 1 by Na3;
    per cases by A0;
  suppose x > 0;then
    sinh(x) >= 0 by SIN_COS5:46;then
    sinh(x)/cosh(x) >= 0 by A1,XREAL_1:138;
    hence tanh(x) >= 0 by Na4;
  end;
  suppose x = 0;
    hence tanh(x) >= 0 by Na4;
  end;
end;
 
theorem
  cosh x = 1/sqrt (1-(tanh x)^2) &
  sinh x = tanh(x)/sqrt (1-(tanh x)^2)
proof
A1: (sech x)^2 = (sech x)^2+(tanh x)^2-(tanh x)^2
       .= 1-(tanh x)^2 by SIN_COS5:38;
A2: sech x > 0 by TH2;
A3: cosh x = 1/(1/cosh x) by XCMPLX_1:56
       .= 1/sech(x) by SIN_COS5:def 2
       .= 1/sqrt (1-(tanh x)^2) by A1,A2,SQUARE_1:89;
A4: cosh x <> 0 by Na3;
    sinh x = sinh(x)/cosh(x)*cosh(x) by A4,XCMPLX_1:88
       .= tanh(x)*(1/sqrt (1-(tanh x)^2)) by A3,Na4
       .= tanh(x)/sqrt (1-(tanh x)^2) by XCMPLX_1:100;
    hence thesis by A3;
end;

theorem
  (cosh(x)+sinh(x)) |^ n = cosh(n*x)+sinh(n*x) &
  (cosh(x)-sinh(x)) |^ n = cosh(n*x)-sinh(n*x)
proof
  A1: (cosh(x) + sinh(x)) |^ n
          = (cosh.x+sinh(x)) |^ n by SIN_COS2:def 4
         .= (cosh.x+sinh.x) |^ n by SIN_COS2:def 2
         .= cosh.(n*x)+sinh.(n*x) by SIN_COS2:29
         .= cosh(n*x)+sinh.(n*x) by SIN_COS2:def 4
         .= cosh(n*x)+sinh(n*x) by SIN_COS2:def 2;
  (cosh(x) - sinh(x)) |^ n
          = (cosh(x)+-sinh(x)) |^ n
         .= (cosh(x)+sinh(-x)) |^ n by Na10
         .= (cosh.x+sinh(-x)) |^ n by SIN_COS2:def 4
         .= (cosh.x+sinh.-x) |^ n by SIN_COS2:def 2
         .= (cosh.-x+sinh.-x) |^ n by SIN_COS2:19
         .= cosh.(n*-x)+sinh.(-n*x) by SIN_COS2:29
         .= cosh.(n*x)+sinh.(-n*x) by SIN_COS2:19
         .= cosh.(n*x)+-sinh.(n*x) by SIN_COS2:19
         .= cosh.(n*x)-sinh.(n*x)
         .= cosh(n*x)-sinh.(n*x) by SIN_COS2:def 4
         .= cosh(n*x)-sinh(n*x) by SIN_COS2:def 2;
  hence thesis by A1;
end;

theorem TH5:
  exp(x) = cosh(x)+sinh(x) &
  exp(-x) = cosh(x)-sinh(x) &
  exp(x) = (cosh(x/2)+sinh(x/2))/(cosh(x/2)-sinh(x/2)) &
  exp(-x) = (cosh(x/2)-sinh(x/2))/(cosh(x/2)+sinh(x/2)) &
  exp(x) = (1+tanh(x/2))/(1-tanh(x/2)) &
  exp(-x) = (1-tanh(x/2))/(1+tanh(x/2))
proof 
A1: exp(x) = (exp(x)+exp(-x))/2+(exp(x)-exp(-x))/2
         .= cosh(x)+(exp(x)-exp(-x))/2 by Na1
         .= cosh(x)+sinh(x) by Na1;
A2: exp(-x) = (exp(x)+exp(-x))/2-(exp(x)-exp(-x))/2
         .= cosh(x)-(exp(x)-exp(-x))/2 by Na1
         .= cosh(x)-sinh(x) by Na1;
A3: exp(x/2)>0 by SIN_COS:60;
A4: exp(-x/2)>0 by SIN_COS:60;
A5: exp(x) = exp(x/2+x/2)
         .= exp(x/2)*exp(x/2) by SIN_COS:55
         .= exp(x/2)*exp(-x/2)*(exp(x/2)/exp(-x/2)) by A4,XCMPLX_1:91
         .= exp(x/2+-x/2)*(exp(x/2)/exp(-x/2)) by SIN_COS:55
         .= ((exp(x/2)+exp(-x/2))/2+(exp(x/2)-exp(-x/2))/2)/exp(-x/2)
            by SIN_COS:56
         .= (cosh(x/2)+(exp(x/2)-exp(-x/2))/2)/exp(-x/2) by Na1
         .= (cosh(x/2)+sinh(x/2))/((exp(-x/2)+exp(x/2))/2
            -(exp(x/2)-exp(-x/2))/2) by Na1
         .= (cosh(x/2)+sinh(x/2))/((exp(x/2)+exp(-x/2))/2-sinh(x/2)) by Na1
         .= (cosh(x/2)+sinh(x/2))/(cosh(x/2)-sinh(x/2)) by Na1;
A6: exp(-x) = exp(-x/2+-x/2)
         .= exp(-x/2)*exp(-x/2) by SIN_COS:55
         .= exp(-x/2)*exp(x/2)*(exp(-x/2)/exp(x/2)) by A3,XCMPLX_1:91
         .= exp(-x/2+x/2)*(exp(-x/2)/exp(x/2)) by SIN_COS:55
         .= ((exp(x/2)+exp(-x/2))/2-(exp(x/2)-exp(-x/2))/2)/exp(x/2)
            by SIN_COS:56
         .= ((exp(x/2)+exp(-x/2))/2-sinh(x/2))/exp(x/2) by Na1
         .= (cosh(x/2)-sinh(x/2))/((exp(x/2)+exp(-x/2))/2
            +(exp(x/2)/2-exp(-x/2)/2)) by Na1
         .= (cosh(x/2)-sinh(x/2))/(cosh(x/2)+(exp(x/2)-exp(-x/2))/2)
            by Na1
         .= (cosh(x/2)-sinh(x/2))/(cosh(x/2)+sinh(x/2)) by Na1;
A7: cosh(x/2) <> 0 by Na3;
A8: exp(x) = (cosh(x/2)+sinh(x/2))/cosh(x/2)
            /((cosh(x/2)-sinh(x/2))/cosh(x/2)) by A5,A7,XCMPLX_1:55
         .= (cosh(x/2)/cosh(x/2)+sinh(x/2)/cosh(x/2))
            /((cosh(x/2)-sinh(x/2))/cosh(x/2)) by XCMPLX_1:63
         .= (1+sinh(x/2)/cosh(x/2))/((cosh(x/2)-sinh(x/2))/cosh(x/2))
            by A7,XCMPLX_1:60
         .= (1+tanh(x/2))/((cosh(x/2)-sinh(x/2))/cosh(x/2)) by Na4
         .= (1+tanh(x/2))/(cosh(x/2)/cosh(x/2)-sinh(x/2)/cosh(x/2))
            by XCMPLX_1:121
         .= (1+tanh(x/2))/(1-sinh(x/2)/cosh(x/2)) by A7,XCMPLX_1:60
         .= (1+tanh(x/2))/(1-tanh(x/2)) by Na4;
  exp(-x) = (cosh(x/2)-sinh(x/2))/cosh(x/2)
            /((cosh(x/2)+sinh(x/2))/cosh(x/2)) by A6,A7,XCMPLX_1:55
         .= (cosh(x/2)/cosh(x/2)-sinh(x/2)/cosh(x/2))
            /((cosh(x/2)+sinh(x/2))/cosh(x/2)) by XCMPLX_1:121
         .= (1-sinh(x/2)/cosh(x/2))/((cosh(x/2)+sinh(x/2))/cosh(x/2))
            by A7,XCMPLX_1:60
         .= (1-tanh(x/2))/((cosh(x/2)+sinh(x/2))/cosh(x/2)) by Na4
         .= (1-tanh(x/2))/(cosh(x/2)/cosh(x/2)+sinh(x/2)/cosh(x/2))
            by XCMPLX_1:63
         .= (1-tanh(x/2))/(1+sinh(x/2)/cosh(x/2)) by A7,XCMPLX_1:60
         .= (1-tanh(x/2))/(1+tanh(x/2)) by Na4;
  hence thesis by A1,A2,A5,A6,A8;
end;

theorem
  x <> 0 implies
  exp(x) = (coth(x/2)+1)/(coth(x/2)-1) &
  exp(-x) = (coth(x/2)-1)/(coth(x/2)+1)
  proof
    assume x <> 0; then
A0: x/2 <> 0;
A1: tanh(x/2) <> 0 by A0,Na6;
A2: coth(x/2)+1 = 1/(1/coth(x/2))+1 by XCMPLX_1:56
    .= 1/tanh(x/2)+1 by TH1
    .= 1/tanh(x/2)+tanh(x/2)/tanh(x/2) by A1,XCMPLX_1:60
    .= (1+tanh(x/2))/tanh(x/2) by XCMPLX_1:63;
A3: coth(x/2)-1 = 1/(1/coth(x/2))-1 by XCMPLX_1:56
    .= 1/tanh(x/2)-1 by TH1
    .= 1/tanh(x/2)-tanh(x/2)/tanh(x/2) by A1,XCMPLX_1:60
    .= (1-tanh(x/2))/tanh(x/2) by XCMPLX_1:121;
A4: exp(x) = (1+tanh(x/2))/(1-tanh(x/2)) by TH5
    .= (coth(x/2)+1)/(coth(x/2)-1)by A1,A2,A3,XCMPLX_1:55;
    exp(-x) = (1-tanh(x/2))/(1+tanh(x/2)) by TH5
    .= (coth(x/2)-1)/(coth(x/2)+1)by A1,A2,A3,XCMPLX_1:55;
    hence thesis by A4;
  end;

theorem
  (cosh(x)+sinh x)/(cosh(x)-sinh(x)) = (1+tanh(x))/(1-tanh x)
  proof
A2: exp(2*x)
    = (cosh((2*x)/2)+sinh((2*x)/2))/(cosh((2*x)/2)-sinh((2*x)/2)) by TH5
    .= (cosh(x)+sinh(x))/(cosh(x)-sinh(x));
    exp(2*x) = (1+tanh(x))/(1-tanh((2*x)/2)) by TH5
    .= (1+tanh(x))/(1-tanh(x));
    hence thesis by A2;
  end;

Na12:
  cosh(y+z) = cosh(y)*cosh(z)+sinh(y)*sinh(z) &
  cosh(y-z) = cosh(y)*cosh(z)-sinh(y)*sinh(z) &
  sinh(y+z) = sinh(y)*cosh(z)+cosh(y)*sinh(z) &
  sinh(y-z) = sinh(y)*cosh(z)-cosh(y)*sinh(z) &
  tanh(y+z) = (tanh(y)+tanh(z))/(1+tanh(y)*tanh(z)) &
  tanh(y-z) = (tanh(y)-tanh(z))/(1-tanh(y)*tanh(z))
  proof 
A1: cosh(y)*cosh(z)+sinh(y)*sinh(z)
         = cosh.y*cosh(z)+sinh(y)*sinh(z) by SIN_COS2:def 4
        .= cosh.y*cosh.z+sinh(y)*sinh(z) by SIN_COS2:def 4
        .= cosh.y*cosh.z+sinh.y*sinh(z) by SIN_COS2:def 2
        .= cosh.y*cosh.z+sinh.y*sinh.z by SIN_COS2:def 2
        .= cosh.(y+z) by SIN_COS2:20
        .= cosh(y+z) by SIN_COS2:def 4;
A2: cosh(y)*cosh(z)-sinh(y)*sinh(z)
         = cosh.y*cosh(z)-sinh(y)*sinh(z) by SIN_COS2:def 4
        .= cosh.y*cosh.z-sinh(y)*sinh(z) by SIN_COS2:def 4
        .= cosh.y*cosh.z-sinh.y*sinh(z) by SIN_COS2:def 2
        .= cosh.y*cosh.z-sinh.y*sinh.z by SIN_COS2:def 2
        .= cosh.(y-z) by SIN_COS2:20
        .= cosh(y-z) by SIN_COS2:def 4;
A3: sinh(y)*cosh(z)+cosh(y)*sinh(z)
         = sinh.y*cosh(z)+cosh(y)*sinh(z) by SIN_COS2:def 2
        .= sinh.y*cosh(z)+cosh(y)*sinh.z by SIN_COS2:def 2
        .= sinh.y*cosh.z+cosh(y)*sinh.z by SIN_COS2:def 4
        .= sinh.y*cosh.z+cosh.y*sinh.z by SIN_COS2:def 4
        .= sinh.(y+z) by SIN_COS2:21
        .= sinh(y+z) by SIN_COS2:def 2;
A4: sinh(y)*cosh(z)-cosh(y)*sinh(z)
         = sinh.y*cosh(z)-cosh(y)*sinh(z) by SIN_COS2:def 2
        .= sinh.y*cosh(z)-cosh(y)*sinh.z by SIN_COS2:def 2
        .= sinh.y*cosh.z-cosh(y)*sinh.z by SIN_COS2:def 4
        .= sinh.y*cosh.z-cosh.y*sinh.z by SIN_COS2:def 4
        .= sinh.(y-z) by SIN_COS2:21
        .= sinh(y-z) by SIN_COS2:def 2;
A5: tanh(y+z) = tanh.(y+z) by SIN_COS2:def 6
        .= (tanh.y+tanh.z)/(1+tanh.y*tanh.z) by SIN_COS2:22
        .= (tanh(y)+tanh.z)/(1+tanh.y*tanh.z)
           by SIN_COS2:def 6
        .= (tanh(y)+tanh(z))/(1+tanh.y*tanh.z)
           by SIN_COS2:def 6
        .= (tanh(y)+tanh(z))/(1+tanh(y)*tanh.z) by SIN_COS2:def 6
        .= (tanh(y)+tanh(z))/(1+tanh(y)*tanh(z)) by SIN_COS2:def 6;
  tanh(y-z) = tanh.(y-z) by SIN_COS2:def 6
        .= (tanh.y-tanh.z)/(1-tanh.y*tanh.z) by SIN_COS2:22
        .= (tanh(y)-tanh.z)/(1-tanh.y*tanh.z)
           by SIN_COS2:def 6
        .= (tanh(y)-tanh(z))/(1-tanh.y*tanh.z)
           by SIN_COS2:def 6
        .= (tanh(y)-tanh(z))/(1-tanh(y)*tanh.z) by SIN_COS2:def 6
        .= (tanh(y)-tanh(z))/(1-tanh(y)*tanh(z)) by SIN_COS2:def 6;
  hence thesis by A1,A2,A3,A4,A5;
end;
 
theorem
  y <> 0 implies
  coth(y)+tanh(z) = cosh(y+z)/(sinh(y)*cosh(z)) &
  coth(y)-tanh(z) = cosh(y-z)/(sinh(y)*cosh(z))
proof assume A0: y <> 0;
A1: sinh(y) <> 0 by A0,Na6;
A2: cosh(z) <> 0 by Na3;
A3: coth(y)+tanh(z) = cosh(y)/sinh(y)+tanh(z) by SIN_COS5:def 1
         .= cosh(y)*cosh(z)/(sinh(y)*cosh z)+tanh z
            by A2,XCMPLX_1:92
         .= cosh(y)*cosh(z)/(sinh(y)*cosh z)+sinh(z)/cosh(z) by Na4
         .= cosh(y)*cosh(z)/(sinh(y)*cosh z)
            +sinh(y)*sinh(z)/(sinh(y)*cosh z) by A1,XCMPLX_1:92
         .= (cosh(y)*cosh(z)+sinh(y)*sinh(z))/(sinh(y)*cosh(z))
            by XCMPLX_1:63
         .= cosh(y+z)/(sinh(y)*cosh(z)) by Na12;
  coth(y)-tanh(z) = cosh(y)/sinh(y)-tanh(z) by SIN_COS5:def 1
         .= cosh(y)*cosh(z)/(sinh(y)*cosh(z))-tanh(z) by A2,XCMPLX_1:92
         .= cosh(y)*cosh(z)/(sinh(y)*cosh(z))-sinh(z)/cosh(z) by Na4
         .= cosh(y)*cosh(z)/(sinh(y)*cosh(z))
            -sinh(y)*sinh(z)/(sinh(y)*cosh(z)) by A1,XCMPLX_1:92
         .= (cosh(y)*cosh(z)-sinh(y)*sinh(z))/(sinh(y)*cosh(z))
            by XCMPLX_1:121
         .= cosh(y-z)/(sinh(y)*cosh(z)) by Na12;
  hence thesis by A3;
end;

theorem TH9:
  sinh(y)*sinh(z) = 1/2*(cosh(y+z)-cosh(y-z)) &
  sinh(y)*cosh(z) = 1/2*(sinh(y+z)+sinh(y-z)) &
  cosh(y)*sinh(z) = 1/2*(sinh(y+z)-sinh(y-z)) &
  cosh(y)*cosh(z) = 1/2*(cosh(y+z)+cosh(y-z))
proof 
A1: sinh(y)*sinh(z)
  = 1/2*(cosh(y)*cosh(z)+sinh(y)*sinh(z)-(cosh(y)*cosh(z)-sinh(y)*sinh(z)))
  .= 1/2 * (cosh(y+z)-(cosh(y)*cosh(z)-sinh(y)*sinh(z))) by Na12
  .= 1/2 * (cosh(y+z)-cosh(y-z)) by Na12;
A2: sinh(y)*cosh(z)
  = 1/2*(sinh(y)*cosh(z)+cosh(y)*sinh(z)+(sinh(y)*cosh(z)-cosh(y)*sinh(z)))
  .= 1/2*(sinh(y+z)+(sinh(y)*cosh(z)-cosh(y)*sinh(z))) by Na12
  .= 1/2*(sinh(y+z)+sinh(y-z)) by Na12;
A3: cosh(y)*sinh(z)
  = 1/2*(sinh(y)*cosh(z)+cosh(y)*sinh(z)-(sinh(y)*cosh(z)-cosh(y)*sinh(z)))
  .= 1/2*(sinh(y+z)-(sinh(y)*cosh(z)-cosh(y)*sinh(z))) by Na12
  .= 1/2*(sinh(y+z)-sinh(y-z)) by Na12;
  cosh(y)*cosh(z)
  = 1/2*(cosh(y)*cosh(z)+sinh(y)*sinh(z)+(cosh(y)*cosh(z)-sinh(y)*sinh(z)))
  .= 1/2*(cosh(y+z)+(cosh(y)*cosh(z)-sinh(y)*sinh(z))) by Na12
  .= 1/2*(cosh(y+z)+cosh(y-z)) by Na12;
  hence thesis by A1,A2,A3;
end;

theorem
  (sinh y)^2-(cosh z)^2 = sinh(y+z)*sinh(y-z)-1
proof
  (sinh y)^2-(cosh z)^2 = (sinh y)^2-(cosh z)^2*1
  .= (sinh y)^2-(cosh z)^2*((cosh y)^2-(sinh y)^2) by Na2
  .= (sinh y)^2+((cosh z)^2*(sinh y)^2
  -(cosh y)^2*((cosh z)^2-(sinh z)^2+(sinh z)^2))
  .= (sinh y)^2+((cosh z)^2*(sinh y)^2
  -(cosh y)^2*(1+(sinh z)^2)) by Na2
  .= (sinh y)^2+(((cosh z)^2*(sinh y)^2
  -(cosh y)^2*(sinh z)^2)-(cosh y)^2*1)
  .= (sinh y)^2+(((sinh(y)*cosh z)^2-(cosh y)^2*(sinh z)^2)
  -(cosh y)^2) by SQUARE_1:68
  .= (sinh y)^2+(((sinh(y)*cosh z)^2-(cosh(y)*sinh z)^2)
  -(cosh y)^2) by SQUARE_1:68
  .= (sinh y)^2+((sinh(y)*cosh(z)+cosh(y)*sinh(z))
  *(sinh(y)*cosh(z)-cosh(y)*sinh z)-(cosh y)^2) by SQUARE_1:67
  .= (sinh y)^2+(sinh(y+z)
  *(sinh(y)*cosh(z)-cosh(y)*sinh(z))-(cosh y)^2) by Na12
  .= sinh(y+z)*sinh(y-z)-(cosh y)^2+(sinh y)^2 by Na12
  .= sinh(y+z)*sinh(y-z)-((cosh y)^2-(sinh y)^2)
  .= sinh(y+z)*sinh(y-z)-1 by Na2;
  hence thesis;
end;

Na13:
  sinh(y)+sinh(z) = 2*sinh(y/2+z/2)*cosh(y/2-z/2) &
  sinh(y)-sinh(z) = 2*sinh(y/2-z/2)*cosh(y/2+z/2) &
  cosh(y)+cosh(z) = 2*cosh(y/2+z/2)*cosh(y/2-z/2) &
  cosh(y)-cosh(z) = 2*sinh(y/2-z/2)*sinh(y/2+z/2) &
  tanh(y)+tanh(z) = sinh(y+z)/(cosh(y)*cosh(z)) &
  tanh(y)-tanh(z) = sinh(y-z)/(cosh(y)*cosh(z))
proof
  A1: 2*sinh(y/2+z/2)*cosh(y/2-z/2)
          = 2*sinh.(y/2+z/2)*cosh(y/2-z/2) by SIN_COS2:def 2
         .= 2*sinh.(y/2+z/2)*cosh.(y/2-z/2) by SIN_COS2:def 4
         .= sinh.y+sinh.z by SIN_COS2:26
         .= sinh(y)+sinh.z by SIN_COS2:def 2
         .= sinh(y)+sinh(z) by SIN_COS2:def 2;
A2: 2*sinh(y/2-z/2)*cosh(y/2+z/2)
          = 2*sinh.(y/2-z/2)*cosh(y/2+z/2) by SIN_COS2:def 2
         .= 2*sinh.(y/2-z/2)*cosh.(y/2+z/2) by SIN_COS2:def 4
         .= sinh.y-sinh.z by SIN_COS2:26
         .= sinh(y)-sinh.z by SIN_COS2:def 2
         .= sinh(y)-sinh(z) by SIN_COS2:def 2;
A3: 2*cosh(y/2+z/2)*cosh(y/2-z/2)
          = 2*cosh.(y/2+z/2)*cosh(y/2-z/2) by SIN_COS2:def 4
         .= 2*cosh.(y/2+z/2)*cosh.(y/2-z/2) by SIN_COS2:def 4
         .= cosh.y+cosh.z by SIN_COS2:27
         .= cosh(y)+cosh.z by SIN_COS2:def 4
         .= cosh(y)+cosh(z) by SIN_COS2:def 4;
A4: 2*sinh(y/2-z/2)*sinh(y/2+z/2)
          = 2*sinh.(y/2-z/2)*sinh(y/2+z/2) by SIN_COS2:def 2
         .= 2*sinh.(y/2-z/2)*sinh.(y/2+z/2) by SIN_COS2:def 2
         .= cosh.y-cosh.z by SIN_COS2:27
         .= cosh(y)-cosh.z by SIN_COS2:def 4
         .= cosh(y)-cosh(z) by SIN_COS2:def 4;
A5: sinh(y+z)/(cosh(y)*cosh(z))
          = sinh.(y+z)/(cosh(y)*cosh(z)) by SIN_COS2:def 2
         .= sinh.(y+z)/(cosh.y*cosh(z)) by SIN_COS2:def 4
         .= sinh.(y+z)/(cosh.y*cosh.z) by SIN_COS2:def 4
         .= tanh.y+tanh.z by SIN_COS2:28
         .= tanh(y)+tanh.z by SIN_COS2:def 6
         .= tanh(y)+tanh(z) by SIN_COS2:def 6;
  sinh(y-z)/(cosh(y)*cosh(z))
          = sinh.(y-z)/(cosh(y)*cosh(z)) by SIN_COS2:def 2
         .= sinh.(y-z)/(cosh.y*cosh(z)) by SIN_COS2:def 4
         .= sinh.(y-z)/(cosh.y*cosh.z) by SIN_COS2:def 4
         .= tanh.y-tanh.z by SIN_COS2:28
         .= tanh(y)-tanh.z by SIN_COS2:def 6
         .= tanh(y)-tanh(z) by SIN_COS2:def 6;
  hence thesis by A1,A2,A3,A4,A5;
end;

theorem
  (sinh(y)-sinh z)^2-(cosh(y)-cosh z)^2 = 4*(sinh((y-z)/2))^2 &
  (cosh(y)+cosh z)^2-(sinh(y)+sinh z)^2 = 4*(cosh((y-z)/2))^2
proof
A1: (sinh(y)-sinh z)^2-(cosh(y)-cosh z)^2
  = (2*sinh(y/2-z/2)*cosh(y/2+z/2))^2-(cosh(y)-cosh z)^2 by Na13
  .= (2*cosh(y/2+z/2)*sinh(y/2-z/2))^2
  -(2*sinh(y/2-z/2)*sinh((y+z)/2))^2 by Na13
  .= (2*cosh(y/2+z/2)*sinh(y/2-z/2))^2 - (2*(sinh(y/2-z/2)*sinh((y+z)/2)))^2
  .= (2*cosh(y/2+z/2)*sinh(y/2-z/2))^2 - (2*(sinh(y/2+z/2)*sinh((y-z)/2)))^2
  .= (2*cosh(y/2+z/2)*sinh(y/2-z/2))^2 - (2*sinh(y/2+z/2)*sinh((y-z)/2))^2
  .= (2*(cosh(y/2+z/2)*sinh(y/2-z/2)))^2 - (2*sinh(y/2+z/2)*sinh(y/2-z/2))^2
  .= 2^2*(cosh(y/2+z/2)*sinh(y/2-z/2))^2
  - (2*(sinh(y/2+z/2)*sinh(y/2-z/2)))^2 by SQUARE_1:68
  .= 2^2*(cosh(y/2+z/2)*sinh(y/2-z/2))^2
  - 2^2*(sinh(y/2+z/2)*sinh(y/2-z/2))^2 by SQUARE_1:68
  .= 2*2*(cosh(y/2+z/2)*sinh(y/2-z/2))^2
  - 2^2*(sinh(y/2+z/2)*sinh(y/2-z/2))^2 by SQUARE_1:def 3
  .= 4*(sinh(y/2-z/2)*cosh(y/2+z/2))^2
  - 2*2*(sinh(y/2+z/2)*sinh(y/2-z/2))^2 by SQUARE_1:def 3
  .= 4*((sinh(y/2-z/2))^2*(cosh(y/2+z/2))^2)
  - 4*(sinh(y/2-z/2)*sinh(y/2+z/2))^2 by SQUARE_1:68
  .= 4*((sinh(y/2-z/2))^2*(cosh(y/2+z/2))^2
  - (sinh(y/2-z/2)*sinh(y/2+z/2))^2)
  .= 4*((sinh(y/2-z/2))^2*(cosh(y/2+z/2))^2
  - (sinh(y/2-z/2))^2*(sinh(y/2+z/2))^2) by SQUARE_1:68
  .= 4*((sinh(y/2-z/2))^2*((cosh(y/2+z/2))^2 - (sinh(y/2+z/2))^2))
  .= 4*((sinh(y/2-z/2))^2*1) by Na2
  .= 4*(sinh((y-z)/2))^2;
  (cosh(y)+cosh(z))^2-(sinh(y)+sinh(z))^2
  = (2*cosh(y/2+z/2)*cosh(y/2-z/2))^2-(sinh(y)+sinh(z))^2 by Na13
  .= (2*(cosh(y/2+z/2)*cosh(y/2-z/2)))^2
  - (2*sinh(y/2+z/2)*cosh(y/2-z/2))^2 by Na13
  .= 2^2*(cosh(y/2+z/2)*cosh(y/2-z/2))^2
  - (2*(sinh(y/2+z/2)*cosh(y/2-z/2)))^2 by SQUARE_1:68
  .= 2^2*(cosh(y/2+z/2)*cosh(y/2-z/2))^2
  - 2^2*(sinh(y/2+z/2)*cosh(y/2-z/2))^2 by SQUARE_1:68
  .= 2^2*((cosh(y/2+z/2)*cosh(y/2-z/2))^2 - (sinh(y/2+z/2)*cosh(y/2-z/2))^2)
  .= 2*2*((cosh(y/2-z/2)*cosh(y/2+z/2))^2
  - (sinh(y/2+z/2)*cosh(y/2-z/2))^2) by SQUARE_1:def 3
  .= 4*((cosh(y/2-z/2))^2*(cosh(y/2+z/2))^2
  - (cosh(y/2-z/2)*sinh(y/2+z/2))^2) by SQUARE_1:68
  .= 4*((cosh(y/2-z/2))^2*(cosh(y/2+z/2))^2
  - (cosh(y/2-z/2))^2*(sinh(y/2+z/2))^2) by SQUARE_1:68
  .= 4*((cosh(y/2-z/2))^2 * ((cosh(y/2+z/2))^2 - (sinh(y/2+z/2))^2))
  .= 4*((cosh(y/2-z/2))^2*1) by Na2
  .= 4*(cosh((y-z)/2))^2;
  hence thesis by A1;
end;

theorem
  (sinh(y)+sinh(z))/(sinh(y)-sinh(z)) = tanh((y+z)/2)*coth((y-z)/2)
proof 
  (sinh(y)+sinh(z))/(sinh(y)-sinh(z))
  = 2*sinh(y/2+z/2)*cosh(y/2-z/2)/(sinh(y)-sinh(z)) by Na13
  .= 2*sinh(y/2+z/2)*cosh(y/2-z/2) / (2*sinh(y/2-z/2)*cosh(y/2+z/2)) by Na13
  .= 2*sinh(y/2+z/2)*cosh(y/2-z/2) / (2*cosh(y/2+z/2)*sinh(y/2-z/2))
  .= 2*sinh(y/2+z/2)/(2*cosh(y/2+z/2))
  * (cosh(y/2-z/2)/sinh(y/2-z/2)) by XCMPLX_1:77
  .= 2*sinh(y/2+z/2)/(2*cosh(y/2+z/2)) * coth(y/2-z/2) by SIN_COS5:def 1
  .= 2/2*(sinh(y/2+z/2)/cosh(y/2+z/2)) * coth(y/2-z/2) by XCMPLX_1:77
  .= tanh((y+z)/2) * coth((y-z)/2) by Na4;
  hence thesis;
end;
 
theorem
  (cosh(y)+cosh(z))/(cosh(y)-cosh(z)) = coth((y+z)/2)*coth((y-z)/2)
proof 
  (cosh(y)+cosh(z))/(cosh(y)-cosh(z))
  = 2*cosh(y/2+z/2)*cosh(y/2-z/2)/(cosh(y)-cosh(z)) by Na13
  .= 2*cosh(y/2+z/2)*cosh(y/2-z/2)/(2*sinh(y/2-z/2)*sinh((y+z)/2)) by Na13
  .= 2*cosh(y/2+z/2)*cosh(y/2-z/2)/(2*(sinh(y/2-z/2)*sinh((y+z)/2)))
  .= 2*cosh(y/2+z/2)*cosh(y/2-z/2)/(2*(sinh(y/2+z/2)*sinh((y-z)/2)))
  .= 2*cosh(y/2+z/2)*cosh(y/2-z/2)/(2*sinh(y/2+z/2)*sinh((y-z)/2))
  .= 2*cosh(y/2+z/2)/(2*sinh(y/2+z/2))
  * (cosh(y/2-z/2)/sinh(y/2-z/2)) by XCMPLX_1:77
  .= 2*cosh(y/2+z/2)/(2*sinh(y/2+z/2))*coth(y/2-z/2) by SIN_COS5:def 1
  .= 2/2*(cosh(y/2+z/2)/sinh(y/2+z/2))*coth(y/2-z/2) by XCMPLX_1:77
  .= coth((y+z)/2)*coth((y-z)/2) by SIN_COS5:def 1;
  hence thesis;
end;

theorem
  y-z <> 0 implies
  (sinh(y)+sinh(z))/(cosh(y)+cosh(z)) = (cosh(y)-cosh(z))/(sinh(y)-sinh(z))
  proof
    assume y-z <> 0;then
A1: sinh(y/2-z/2) <> 0 by Na7;
A2: cosh(y/2-z/2) <> 0 by Na3;
    (sinh(y)+sinh(z))/(cosh(y)+cosh(z))
    = 2*sinh(y/2+z/2)*cosh(y/2-z/2)/(cosh(y)+cosh(z)) by Na13
    .= 2*sinh(y/2+z/2)*cosh(y/2-z/2)/(2*cosh(y/2+z/2)*cosh(y/2-z/2)) by Na13
    .= 2*sinh(y/2+z/2)/(2*cosh(y/2+z/2))
    * (cosh(y/2-z/2)/cosh(y/2-z/2)) by XCMPLX_1:77
    .= 2*sinh(y/2+z/2)/(2*cosh(y/2+z/2))*1 by A2,XCMPLX_1:60
    .= 2*sinh((y+z)/2)*sinh((y-z)/2)
    / (2*cosh(y/2+z/2)*sinh(y/2-z/2)) by A1,XCMPLX_1:92
    .= 2*sinh((y-z)/2)*sinh((y+z)/2)/(2*cosh(y/2+z/2)*sinh(y/2-z/2))
    .= (cosh(y)-cosh(z))/(2*sinh(y/2-z/2)*cosh(y/2+z/2)) by Na13 
    .= (cosh(y)-cosh(z))/(sinh(y)-sinh(z)) by Na13;
    hence thesis;
  end;

theorem
  y+z <> 0 implies
  (sinh(y)-sinh(z))/(cosh(y)+cosh(z)) = (cosh(y)-cosh(z))/(sinh(y)+sinh(z))
  proof
    assume
A0: y+z <> 0;
A1: sinh(y/2+z/2) <> 0 by A0,Na8;
A2: cosh(y/2+z/2) <> 0 by Na3;
    (sinh(y)-sinh(z))/(cosh(y)+cosh(z))
    = 2*sinh(y/2-z/2)*cosh(y/2+z/2)/(cosh(y)+cosh(z)) by Na13
    .= 2*cosh(y/2+z/2)*sinh(y/2-z/2)/(2*cosh(y/2+z/2)*cosh(y/2-z/2)) by Na13
    .= 2*cosh(y/2+z/2)/(2*cosh(y/2+z/2))
    * (sinh(y/2-z/2)/cosh(y/2-z/2)) by XCMPLX_1:77
    .= cosh(y/2+z/2)/cosh(y/2+z/2)
    *(sinh(y/2-z/2)/cosh(y/2-z/2)) by XCMPLX_1:92
    .= 1*(sinh(y/2-z/2)/cosh(y/2-z/2)) by A2,XCMPLX_1:60
    .= sinh(y/2+z/2)*sinh(y/2-z/2)
    / (sinh(y/2+z/2)*cosh(y/2-z/2)) by A1,XCMPLX_1:92
    .= 2*(sinh(y/2+z/2)*sinh(y/2-z/2))
    /(2*(sinh(y/2+z/2)*cosh(y/2-z/2))) by XCMPLX_1:92
    .= 2*(sinh((y-z)/2)*sinh((y+z)/2)) / (2*sinh(y/2+z/2)*cosh(y/2-z/2))
    .= 2*sinh((y-z)/2)*sinh((y+z)/2) / (2*sinh(y/2+z/2)*cosh(y/2-z/2))
    .= (cosh(y)-cosh(z))/(2*sinh(y/2+z/2)*cosh(y/2-z/2)) by Na13
    .= (cosh(y)-cosh(z))/(sinh(y)+sinh(z)) by Na13;
    hence thesis;
  end;

theorem
  (sinh(y)+sinh(z))/(cosh(y)+cosh(z)) = tanh(y/2+z/2) &
  (sinh(y)-sinh(z))/(cosh(y)+cosh(z)) = tanh(y/2-z/2)
  proof
A1: cosh(y/2-z/2) <> 0 by Na3;
A2: (sinh(y)+sinh(z))/(cosh(y)+cosh(z))
    = 2*sinh(y/2+z/2)*cosh(y/2-z/2)/(cosh(y)+cosh(z)) by Na13
    .= 2*sinh(y/2+z/2)*cosh(y/2-z/2)/(2*cosh(y/2+z/2)*cosh(y/2-z/2)) by Na13
    .= 2*sinh(y/2+z/2)/(2*cosh(y/2+z/2))*(cosh(y/2-z/2)/cosh(y/2-z/2))
    by XCMPLX_1:77
    .= 2*sinh(y/2+z/2)/(2*cosh(y/2+z/2))*1 by A1,XCMPLX_1:60
    .= 2/2*(sinh(y/2+z/2)/cosh(y/2+z/2)) by XCMPLX_1:77
    .= tanh(y/2+z/2) by Na4;
A3: cosh(y/2+z/2) <> 0 by Na3;
    (sinh(y)-sinh(z))/(cosh(y)+cosh(z))
    = 2*sinh(y/2-z/2)*cosh(y/2+z/2)/(cosh(y)+cosh(z)) by Na13
    .= 2*(sinh(y/2-z/2)*cosh(y/2+z/2))/(2*cosh(y/2+z/2)*cosh(y/2-z/2))
    by Na13
    .= 2*(sinh(y/2-z/2)*cosh(y/2+z/2))/(2*(cosh(y/2+z/2)*cosh(y/2-z/2)))
    .= cosh(y/2+z/2)*sinh(y/2-z/2)/(cosh(y/2+z/2)*cosh(y/2-z/2))
    by XCMPLX_1:92
    .= cosh(y/2+z/2)/cosh(y/2+z/2)*(sinh(y/2-z/2)/cosh(y/2-z/2))
    by XCMPLX_1:77
    .= 1*(sinh(y/2-z/2)/cosh(y/2-z/2)) by A3,XCMPLX_1:60
    .= tanh(y/2-z/2) by Na4;
    hence thesis by A2;
  end;

theorem
  (tanh(y)+tanh(z))/(tanh(y)-tanh(z)) = sinh(y+z)/sinh(y-z)
  proof 
    cosh(y) <> 0 & cosh(z) <> 0 by Na3;then
A1: cosh(y)*cosh(z) <> 0 by XCMPLX_1:6;
    (tanh(y)+tanh(z))/(tanh(y)-tanh(z))
    = sinh(y+z)/(cosh(y)*cosh(z))/(tanh(y)-tanh(z)) by Na13
    .= sinh(y+z)/(cosh(y)*cosh(z))/(sinh(y-z)/(cosh(y)*cosh(z))) by Na13
    .= sinh(y+z)/sinh(y-z) by A1,XCMPLX_1:55;
    hence thesis;
  end;

na14:
  1+(cosh(x)+cosh(x)) <> 0
  proof
    cosh.x > 0 by SIN_COS2:15;then
    cosh x > 0 by SIN_COS2:def 4; then
    cosh(x)+cosh(x) > 0+0 by XREAL_1:10;
    hence thesis by XREAL_1:10;
  end;

Na14:
  cosh(x)+1 > 0 & cosh(x)-1 >= 0 & cosh(x)+2+cosh(x) <> 0
  proof
    cosh.x > 0 by SIN_COS2:15;then
A1: cosh x > 0 by SIN_COS2:def 4;
    cosh(x) >= 1 by Na3;then
A3: cosh(x)-1 >= 1-1 by XREAL_1:15;
    cosh(x)+cosh(x) > 0+0 by A1,XREAL_1:10;
    hence thesis by A1,A3,XREAL_1:10;
  end;

theorem
  (sinh(y-z)+sinh(y)+sinh(y+z))/(cosh(y-z)+cosh(y)+cosh(y+z)) = tanh(y)
  proof
A1: 1+(cosh(z)+cosh(z)) <> 0 by na14;
    (sinh(y-z)+sinh(y)+sinh(y+z))/(cosh(y-z)+cosh(y)+cosh(y+z))
    = (sinh(y)*cosh(z)-cosh(y)*sinh(z)+sinh(y)+sinh(y+z))
    /(cosh(y-z)+cosh(y)+cosh(y+z)) by Na12
    .= (sinh(y)+(sinh(y)*cosh(z)-cosh(y)*sinh(z))
    +(sinh(y)*cosh(z)+cosh(y)*sinh(z)))/(cosh(y)+cosh(y-z)+cosh(y+z)) by Na12
    .= sinh(y)*(1+(cosh(z)+cosh(z)))
    / (cosh(y)+(cosh(y)*cosh(z)-sinh(y)*sinh(z))+cosh(y+z)) by Na12
    .= sinh(y)*(1+(cosh(z)+cosh(z)))
    /(cosh(y)+(cosh(y)*cosh(z)-sinh(y)*sinh(z))
    +(cosh(y)*cosh(z)+sinh(y)*sinh(z))) by Na12
    .= sinh(y)*(1+(cosh(z)+cosh(z)))/(cosh(y)*(1+(cosh(z)+cosh(z))))
    .= sinh(y)/cosh(y)
    * ((1+(cosh(z)+cosh(z)))/(1+(cosh(z)+cosh(z)))) by XCMPLX_1:77
    .= sinh(y)/cosh(y)*1 by A1,XCMPLX_1:60
    .= tanh(y) by Na4;
    hence thesis;
  end;

Na15:
  sinh(y)*cosh(z)/(cosh(y)*cosh(z)) = tanh(y) &
  sinh(y)*cosh(z) = tanh(y)*(cosh(y)*cosh(z)) &
  sinh(y) = tanh(y)*cosh(y) &
  sinh(y)*sinh(z) = tanh(y)*tanh(z)*(cosh(y)*cosh(z))
  proof
A1: cosh y <> 0 & cosh z <> 0 by Na3; then
A2: cosh(y)*cosh(z) <> 0 by XCMPLX_1:6;
A3: sinh(y)*cosh(z)/(cosh(y)*cosh(z))
    = sinh(y)/cosh(y) by A1,XCMPLX_1:92
    .= tanh(y) by Na4;
A5: sinh(y) = sinh(y)/cosh(y)*cosh(y) by A1,XCMPLX_1:88
    .= tanh(y)*cosh(y) by Na4;
    sinh(y)*sinh(z)
    = sinh(y)*sinh(z)/(cosh(y)*cosh(z))*(cosh(y)*cosh(z)) by A2,XCMPLX_1:88
    .= sinh(y)/cosh(y)*(sinh(z)/cosh(z))*(cosh(y)*cosh(z)) by XCMPLX_1:77
    .= tanh(y)*(sinh(z)/cosh(z))*(cosh(y)*cosh(z)) by Na4
    .= tanh(y)*tanh(z)*(cosh(y)*cosh(z)) by Na4;
    hence thesis by A3,A5;
  end;

theorem TH19:
  sinh(y+z+w) = (tanh(y)+tanh(z)+tanh(w)+tanh(y)*tanh(z)*tanh w)
     * cosh(y)*cosh(z)*cosh(w) &
  cosh(y+z+w) = (1+tanh(y)*tanh(z)+tanh(z)*tanh(w)+tanh(w)*tanh y)
     * cosh(y)*cosh(z)*cosh(w) &
  tanh(y+z+w) = (tanh(y)+tanh(z)+tanh(w)+tanh(y)*tanh(z)*tanh w)
     / (1+tanh(z)*tanh(w)+tanh(w)*tanh(y)+tanh(y)*tanh z)
  proof
A1: cosh(y) <> 0 & cosh(z) <> 0 & cosh(w) <> 0 by Na3; then
    cosh(y)*cosh(z) <> 0 by XCMPLX_1:6;then
A2: cosh(y)*cosh(z)*cosh(w) <> 0 by A1,XCMPLX_1:6;
A3: sinh(y+z+w) = sinh(y+z)*cosh(w)+cosh(y+z)*sinh(w) by Na12
    .= (sinh(y)*cosh(z)+cosh(y)*sinh(z))*cosh(w)+cosh(y+z)*sinh(w) by Na12
    .= (sinh(y)*cosh(z)+cosh(y)*sinh(z))*cosh(w)
    +(cosh(y)*cosh(z)+sinh(y)*sinh(z))*sinh(w) by Na12
    .= (tanh(y)*(cosh(y)*cosh(z))+cosh(y)*sinh(z))*cosh(w)
    + (cosh(y)*cosh(z)+sinh(y)*sinh(z))*sinh(w) by Na15
    .= (tanh(y)*(cosh(y)*cosh(z))+tanh(z)*(cosh(y)*cosh(z)))*cosh(w)
    + (cosh(y)*cosh(z)+sinh(y)*sinh(z))*sinh(w) by Na15
    .= (tanh(y)+tanh(z))*(cosh(y)*cosh(z)*cosh(w))
    + (cosh(y)*(cosh(z)*sinh(w))+sinh(y)*sinh(z)*sinh(w))
    .= (tanh(y)+tanh(z))*(cosh(y)*cosh(z)*cosh(w))
    + (cosh(y)*(tanh(w)*(cosh(z)*cosh(w)))+sinh(y)*sinh(z)*sinh(w)) by Na15
    .= (tanh(y)+tanh(z))*(cosh(y)*cosh(z)*cosh(w))
    + (tanh(w)*(cosh(y)*cosh(z))*cosh(w)
    + tanh(y)*cosh(y)*sinh(z)*sinh(w)) by Na15
    .= (tanh(y)+tanh(z))*(cosh(y)*cosh(z)*cosh(w))
    + (tanh(w)*(cosh(y)*cosh(z))*cosh(w)
    + tanh(y)*cosh(y)*(tanh(z)*cosh(z))*sinh(w)) by Na15
    .= (tanh(y)+tanh(z))*(cosh(y)*cosh(z)*cosh(w))
    +(tanh(w)*(cosh(y)*cosh(z))*cosh(w)
    +tanh(y)*tanh(z)*(cosh(y)*cosh(z))*(tanh(w)*cosh(w))) by Na15
    .= (tanh(y)+tanh(z)+tanh(w)+tanh(y)*tanh(z)*tanh(w))
    *cosh(y)*cosh(z)*cosh(w);
A4: cosh(y+z+w) = cosh(y+z)*cosh(w)+sinh(y+z)*sinh(w) by Na12
    .= (cosh(y)*cosh(z)+sinh(y)*sinh(z))*cosh(w)+sinh(y+z)*sinh(w) by Na12
    .= cosh(y)*cosh(z)*cosh(w)+sinh(y)*sinh(z)*cosh(w)
    + (sinh(y)*cosh(z)+cosh(y)*sinh(z))*sinh(w) by Na12
    .= cosh(y)*cosh(z)*cosh(w)+tanh(y)*tanh(z)*(cosh(y)*cosh(z))*cosh(w)
    + (sinh(y)*sinh(w)*cosh(z)+sinh(z)*sinh(w)*cosh(y)) by Na15
    .= cosh(y)*cosh(z)*cosh(w) + tanh(y)*tanh(z)*(cosh(y)*cosh(z))*cosh(w)
    + (tanh(w)*tanh(y)*(cosh(w)*cosh(y))*cosh(z)+sinh(z)*sinh(w)*cosh(y))
    by Na15
    .= (1+tanh(y)*tanh(z))*(cosh(y)*cosh(z)*cosh(w))
    + (tanh(z)*tanh(w)*(cosh(z)*cosh(w))*cosh(y)
    + tanh(w)*tanh(y)*(cosh(y)*cosh(z)*cosh(w))) by Na15
    .= (1+tanh(y)*tanh(z)+tanh(z)*tanh(w)+tanh(w)*tanh(y))
    *cosh(y)*cosh(z)*cosh(w);
    tanh(y+z+w) = ((tanh(y)+tanh(z)+tanh(w)+tanh(y)*tanh(z)*tanh(w))
    *(cosh(y)*cosh(z)*cosh(w)))
    /((1+tanh(y)*tanh(z)+tanh(z)*tanh(w)+tanh(w)*tanh(y))
    *(cosh(y)*cosh(z)*cosh(w))) by A4,A3,Na4
    .= (tanh(y)+tanh(z)+tanh(w)+tanh(y)*tanh(z)*tanh(w))
    /(1+tanh(y)*tanh(z)+tanh(z)*tanh(w)+tanh(w)*tanh(y)) by A2,XCMPLX_1:92;
    hence thesis by A3,A4;
  end;

theorem
  cosh(2*y)+cosh(2*z)+cosh(2*w)+cosh(2*(y+z+w))
  = 4*cosh(z+w)*cosh(w+y)*cosh(y+z)
  proof  
    cosh(2*y)+cosh(2*z)+cosh(2*w)+cosh(2*(y+z+w))
    = 2*(1/2*(cosh(2*(y+z+w))+cosh(2*y))+1/2*(cosh(2*w)+cosh(2*z)))
    .= 2*(1/2*(2*cosh(2*(y+z+w)/2+2*y/2)*cosh(2*(y+z+w)/2-2*y/2))
    + 1/2*(cosh(2*w)+cosh(2*z))) by Na13
    .= 2*(1/2*(2*cosh((y+z+w)+y)*cosh((y+(z+w))-y))
    + 1/2*(cosh(2*w)+cosh(2*z)))
    .= 2*(1/2*2*(cosh((y+z+w)+y)*cosh((y+(z+w))-y))
    + 1/2*(cosh(2*w)+cosh(2*z)))
    .= 2*((cosh(z+w)*cosh((y+z+w)+y))
    + 1/2*(2*cosh((2*w)/2+2*z/2)*cosh(2*w/2-2*z/2))) by Na13
    .= 2*(cosh(z+w)*cosh((y+z+w)+y)+1/2*(2*cosh(w+z))*cosh(w-z))
    by XCMPLX_1:4
    .= 2*(cosh(z+w)*(cosh(2*y+(z+w))+cosh(w-z))) by XCMPLX_1:8
    .= 2*(cosh(z+w)
    *(2*cosh((2*y+(z+w))/2+(w-z)/2)*cosh((2*y+(z+w))/2-(w-z)/2))) by Na13
    .= 2*(cosh(z+w)*(2*cosh(y+w)*cosh(2/2*(z+y))))
    .= 2*(cosh(z+w)*(2*(cosh(y+w)*cosh(2/2*(z+y)))))
    .= 2*(cosh(z+w)*2*(cosh(y+w)*cosh(z+y))) by XCMPLX_1:4
    .= 2*(2*cosh(z+w))*(cosh(w+y)*cosh(z+y)) by XCMPLX_1:4
    .= 2*2*cosh(z+w)*(cosh(w+y)*cosh(y+z))
    .= 4*cosh(z+w)*cosh(w+y)*cosh(y+z);
    hence thesis;
  end;

theorem
  sinh(y)*sinh(z)*sinh(z-y) + sinh(z)*sinh(w)*sinh(w-z)
  + sinh(w)*sinh(y)*sinh(y-w) + (sinh(z-y)*sinh(w-z)*sinh(y-w)) = 0
  proof
    sinh(y)*sinh(z)*sinh(z-y) + sinh(z)*sinh(w)*sinh(w-z)
    + sinh(w)*sinh(y)*sinh(y-w) + (sinh(z-y)*sinh(w-z)*sinh(y-w))
    = (1/2*(cosh(y+z)-cosh(y-z)))*sinh(z-y)
    + sinh(z)*sinh(w)*sinh(w-z)
    + sinh(w)*sinh(y)*sinh(y-w)
    + sinh(z-y)*sinh(w-z)*sinh(y-w) by TH9
    .= 1/2*(cosh(y+z)*sinh(z-y)-cosh(y-z)*sinh(z-y))
    + sinh(z)*sinh(w)*sinh(w-z)
    + sinh(w)*sinh(y)*sinh(y-w)
    + sinh(z-y)*sinh(w-z)*sinh(y-w)
    .= 1/2*(1/2*(sinh(y+z+(z-y))-sinh(y+z-(z-y)))-cosh(y-z)*sinh(z-y))
    + sinh(z)*sinh(w)*sinh(w-z)
    + sinh(w)*sinh(y)*sinh(y-w)
    + sinh(z-y)*sinh(w-z)*sinh(y-w) by TH9
    .= 1/2*(1/2*(sinh(2*z)-sinh(y+y))-1/2*(sinh(y-z+-(y-z))-sinh(y-z-(z-y))))
    +sinh(z)*sinh(w)*sinh(w-z)
    +sinh(w)*sinh(y)*sinh(y-w)
    +sinh(z-y)*sinh(w-z)*sinh(y-w) by TH9
    .= 1/2*(1/2*(sinh(2*z)-sinh(2*y))-1/2*(sinh.0-sinh(2*(y-z))))
    + sinh(z)*sinh(w)*sinh(w-z)
    + sinh(w)*sinh(y)*sinh(y-w)
    + sinh(z-y)*sinh(w-z)*sinh(y-w) by SIN_COS2:def 2
    .= 1/2*(1/2*(sinh(2*z)-sinh(2*y)-(0-sinh(2*(y-z)))))
    +(sinh(z)*sinh(w))*sinh(w-z)
    + sinh(w)*sinh(y)*sinh(y-w)
    + sinh(z-y)*sinh(w-z)*sinh(y-w) by SIN_COS2:16
    .= 1/2*(1/2*(sinh(2*z)-sinh(2*y)--sinh(2*(y-z))))
    + 1/2*(cosh(z+w)-cosh(z-w))*sinh(w-z)
    + sinh(w)*sinh(y)*sinh(y-w)
    + sinh(z-y)*sinh(w-z)*sinh(y-w) by TH9
    .= 1/2*(1/2*(sinh(2*z)-sinh(2*y)--sinh(2*(y-z)))
    + (cosh(z+w)*sinh(w-z)-cosh(z-w)*sinh(w-z)))
    + sinh(w)*sinh(y)*sinh(y-w)
    + sinh(z-y)*sinh(w-z)*sinh(y-w)
    .= 1/2*(1/2*(sinh(2*z)-sinh(2*y)--sinh(2*(y-z)))
    + (1/2*(sinh(z+w+(w-z))-sinh(z+w-(w-z))) - cosh(z-w)*sinh(w-z)))
    + sinh(w)*sinh(y)*sinh(y-w)
    + sinh(z-y)*sinh(w-z)*sinh(y-w) by TH9
    .= 1/2*(1/2*(sinh(2*z)-sinh(2*y)--sinh(2*(y-z)))
    + (1/2*(sinh(2*w)-sinh(2*z)) - 1/2*(sinh(z-w+(w-z))-sinh(z-w-(w-z)))))
    + sinh(w)*sinh(y)*sinh(y-w)
    + sinh(z-y)*sinh(w-z)*sinh(y-w) by TH9
    .= 1/2*(1/2*(sinh(2*z)-sinh(2*y)--sinh(2*(y-z)))
    + (1/2*(sinh(2*w)-sinh(2*z)) - 1/2*(sinh.0-sinh(z-w-(w-z)))))
    + sinh(w)*sinh(y)*sinh(y-w)
    + sinh(z-y)*sinh(w-z)*sinh(y-w) by SIN_COS2:def 2
    .= 1/2*(1/2*(sinh(2*z)-sinh(2*y)--sinh(2*(y-z)))
    + (1/2*(sinh(2*w)-sinh(2*z)) - 1/2*(0-sinh(z-w-(w-z)))))
    + sinh(w)*sinh(y)*sinh(y-w)
    + sinh(z-y)*sinh(w-z)*sinh(y-w) by SIN_COS2:16
    .= 1/2*(1/2*(sinh(2*z)-sinh(2*y)--sinh(2*(y-z)))
    + (1/2*(sinh(2*w)-sinh(2*z)) - 1/2*(-sinh(z-w-(w-z)))))
    + sinh(w)*sinh(y)*sinh(y-w)
    + sinh(z-y)*sinh(w-z)*sinh(y-w)
    .= 1/2*(1/2*(sinh(2*z)-sinh(2*y)--sinh(2*(y-z)))
    + (1/2*(sinh(2*w)-sinh(2*z)) - 1/2*(-sinh(2*(z-w)))))
    + sinh(w)*sinh(y)*sinh(y-w)
    + sinh(z-y)*sinh(w-z)*sinh(y-w)
    .= 1/2*(1/2*(sinh(2*z)-sinh(2*y)--sinh(2*(y-z))
    + (sinh(2*w)-sinh(2*z)--sinh(2*(z-w)))))
    + 1/2*(cosh(w+y)-cosh(w-y))*sinh(y-w)
    + sinh(z-y)*sinh(w-z)*sinh(y-w) by TH9
    .= 1/2*(1/2*(sinh(2*z)-sinh(2*y)--sinh(2*(y-z))
    + (sinh(2*w)-sinh(2*z)--sinh(2*(z-w))))
    + (cosh(w+y)*sinh(y-w)-cosh(w-y)*sinh(y-w)))
    + sinh(z-y)*sinh(w-z)*sinh(y-w)
    .= 1/2*(1/2*(sinh(2*z)-sinh(2*y)--sinh(2*(y-z))
    + (sinh(2*w)-sinh(2*z)--sinh(2*(z-w))))
    + (1/2*(sinh(w+y+(y-w))-sinh(w+y-(y-w)))-cosh(w-y)*sinh(y-w)))
    + sinh(z-y)*sinh(w-z)*sinh(y-w) by TH9
    .= 1/2*(1/2*(sinh(2*z)-sinh(2*y)--sinh(2*(y-z))
    + (sinh(2*w)-sinh(2*z)--sinh(2*(z-w))))
    + (1/2*(sinh(2*y)-sinh(2*w))-1/2*(sinh(w-y +-(w-y))-sinh(w-y--(w-y)))))
    + sinh(z-y)*sinh(w-z)*sinh(y-w) by TH9
    .= 1/2*(1/2*(sinh(2*z)-sinh(2*y)--sinh(2*(y-z))
    + (sinh(2*w)-sinh(2*z)--sinh(2*(z-w))))
    + (1/2*(sinh(2*y)-sinh(2*w)) - 1/2*(sinh.0 - sinh(2*(w-y)))))
    + sinh(z-y)*sinh(w-z)*sinh(y-w) by SIN_COS2:def 2
    .= 1/2*(1/2*(sinh(2*z)-sinh(2*y)--sinh(2*(y-z))
    + (sinh(2*w)-sinh(2*z)--sinh(2*(z-w))))
    + (1/2*(sinh(2*y)-sinh(2*w))-1/2*(0-sinh(2*(w-y)))))
    + sinh(z-y)*sinh(w-z)*sinh(y-w) by SIN_COS2:16
    .= 1/2*(1/2*(sinh(2*(y-z))+sinh(2*(z-w))+sinh(2*(w-y))))
    + 1/2*(cosh(z-y+-(z-w)) - cosh(z-y-(w-z)))*sinh(y-w) by TH9
    .= 1/2*(1/2*(sinh(2*(y-z))+sinh(2*(z-w))+sinh(2*(w-y))))
    + 1/2*(cosh(w-y)-cosh(z-y-(w-z)))*sinh(y-w)
    .= 1/2*(1/2*(sinh(2*(y-z))+sinh(2*(z-w))+sinh(2*(w-y)))
    + (cosh(w-y)*sinh(y-w)-cosh(z-y-(w-z))*sinh(y-w)))
    .= 1/2*(1/2*(sinh(2*(y-z))+sinh(2*(z-w))+sinh(2*(w-y)))
    + (1/2*(sinh(w-y+-(w-y))-sinh(w-y-(y-w)))-cosh(z-y-(w-z))*sinh(y-w)))
    by TH9
    .= 1/2*(1/2*(sinh(2*(y-z))+sinh(2*(z-w))+sinh(2*(w-y)))
    + (1/2*(sinh.0-sinh(w-y-(y-w)))-cosh(z-y-(w-z))*sinh(y-w)))
    by SIN_COS2:def 2
    .= 1/2*(1/2*(sinh(2*(y-z))+sinh(2*(z-w))+sinh(2*(w-y)))
    + (1/2*(0-sinh(w-y-(y-w)))-cosh(z-y-(w-z))*sinh(y-w))) by SIN_COS2:16
    .= 1/2*(1/2*(sinh(2*(y-z))+sinh(2*(z-w))+sinh(2*(w-y)))
    + (1/2*(-sinh(w-y-(y-w)))-cosh(z-y-(w-z))*sinh(y-w)))
    .= 1/2*(1/2*(sinh(2*(y-z))+sinh(2*(z-w))+sinh(2*(w-y)))
    + (1/2*-sinh(2*(w-y))-1/2*(sinh(z-y+(z-w)+(y-w))-sinh(z-y+(z-w)-(y-w)))))
    by TH9
    .= 1/2*(1/2*(sinh(2*(y-z))+sinh(2*(z-w))+sinh(2*(w-y)))
    + (1/2*-sinh(2*(w-y))-1/2*(sinh(2*(z-w))-sinh(2*(z-y)))))
    .= 1/2*(1/2*(sinh(2*(y-z))+sinh(2*(z-w))+sinh(2*(w-y)))
    + 1/2*(sinh(2*(z-y))-sinh(2*(z-w))+-sinh(2*(w-y))))
    .= 1/2*(1/2*(sinh(2*(y-z))+sinh(2*(z-w))+sinh(2*(w-y)))
    + 1/2*((sinh(-(2*(y-z)))-sinh(2*(z-w)))+-sinh(2*(w-y))))
    .= 1/2*(1/2*(sinh(2*(y-z))+sinh(2*(z-w))+sinh(2*(w-y)))
    + 1/2*(-sinh(2*(y-z))-sinh(2*(z-w))+-sinh(2*(w-y)))) by Na10;
    hence thesis;
end;

theorem TH22:
  x >= 0 implies sinh(x/2) = sqrt ((cosh(x)-1)/2)
  proof assume x >= 0;then
A2: x/2 >= 0 by XREAL_1:138;
A3: sinh(x/2) >= 0 by A2,TH3;
    sinh(x/2) = sqrt (sinh(x/2))^2 by A3,SQUARE_1:89
    .= sqrt (sinh.(x/2))^2 by SIN_COS2:def 2
    .= sqrt (1/2*(cosh.(2*(x/2))-1)) by SIN_COS2:18
    .= sqrt ((cosh.x-1)/2) by XCMPLX_1:100
    .= sqrt ((cosh(x)-1)/2) by SIN_COS2:def 4;
    hence thesis;
  end;

theorem TH23:
  x < 0 implies sinh(x/2) = - sqrt ((cosh(x)-1)/2)
  proof assume x < 0;then
A2: x/2 < 0 by XREAL_1:143;
A3: sinh(x/2) <= 0 by A2,SIN_COS5:47;
    sinh(x/2) = --sinh(x/2)
    .= - sqrt (sinh(x/2))^2 by A3,SQUARE_1:90
    .= - sqrt (sinh.(x/2))^2 by SIN_COS2:def 2
    .= - sqrt (1/2*(cosh.(2*(x/2))-1)) by SIN_COS2:18
    .= - sqrt ((cosh.x-1)/2) by XCMPLX_1:100
    .= - sqrt ((cosh(x)-1)/2) by SIN_COS2:def 4;
    hence thesis;
  end;

theorem Na16:
  sinh(2*x) = 2*sinh(x)*cosh(x) &
  cosh(2*x) = 2*(cosh x)^2-1 &
  tanh(2*x) = (2*tanh x)/(1+(tanh x)^2)
  proof
A1: sinh(2*x) = sinh.(2*x) by SIN_COS2:def 2
    .= 2*sinh.x*cosh.x by SIN_COS2:23
    .= 2*sinh(x)*cosh.x by SIN_COS2:def 2
    .= 2*sinh(x)*cosh(x) by SIN_COS2:def 4;
A2: cosh(2*x) = cosh.(2*x) by SIN_COS2:def 4
    .= 2*(cosh.x)^2-1 by SIN_COS2:23
    .= 2*(cosh x)^2-1 by SIN_COS2:def 4;
    tanh(2*x) = tanh.(2*x) by SIN_COS2:def 6
    .= (2*tanh.x)/(1+(tanh.x)^2) by SIN_COS2:23
    .= (2*tanh x)/(1+(tanh.x)^2) by SIN_COS2:def 6
    .= (2*tanh x)/(1+(tanh x)^2) by SIN_COS2:def 6;
    hence thesis by A1,A2;
  end;

theorem TH24:
  sinh(2*x) = 2*tanh(x)/(1-(tanh x)^2) &
  sinh(3*x) = sinh(x)*(4*(cosh x)^2-1) &
  sinh(3*x) = 3*sinh(x)-2*sinh(x)*(1-cosh(2*x)) &
  cosh(2*x) = 1+2*(sinh x)^2 &
  cosh(2*x) = (cosh x)^2+(sinh x)^2 &
  cosh(2*x) = (1+(tanh x)^2)/(1-(tanh x)^2) &
  cosh(3*x) = cosh(x)*(4*(sinh x)^2+1) &
  tanh(3*x) = (3*tanh x+(tanh x)|^3)/(1+3*(tanh x)^2)
proof
A1: cosh x <> 0 by Na3;
A2: sinh(2*x) = 2*sinh(x)*cosh(x) by Na16
  .= 2*sinh(x)*cosh(x)*(cosh(x)/cosh(x)) by A1,XCMPLX_1:89
  .= 2*sinh(x)*cosh(x)*cosh(x)/cosh(x) by XCMPLX_1:75
  .= 2*sinh(x)*(cosh(x)*cosh(x))/cosh(x)
  .= 2*sinh(x)/cosh(x)*(cosh(x)*cosh(x)) by XCMPLX_1:75
  .= 2*(sinh(x)/cosh(x))*(cosh(x)*cosh(x)) by XCMPLX_1:75
  .= 2*tanh(x)*(cosh(x)*cosh(x)) by Na4
  .= 2*tanh(x)/(1/(cosh(x)*cosh x)) by XCMPLX_1:101
  .= 2*tanh(x)/(((cosh x)^2-(sinh x)^2)/(cosh(x)*cosh x)) by Na2
  .= 2*tanh(x)/((cosh(x)*cosh(x)-(sinh x)^2)/(cosh(x)*cosh x))
  by SQUARE_1:def 3
  .= 2*tanh(x)/((cosh(x)*cosh(x)-sinh(x)*sinh x)/(cosh(x)*cosh x))
  by SQUARE_1:def 3
  .= 2*tanh(x)
  / (cosh(x)*cosh(x)/(cosh(x)*cosh(x)) - sinh(x)*sinh(x)/(cosh(x)*cosh x))
  by XCMPLX_1:121
  .= 2*tanh(x)
  / (cosh(x)/cosh(x)*(cosh(x)/cosh(x)) - sinh(x)*sinh(x)/(cosh(x)*cosh x))
  by XCMPLX_1:77
  .= 2*tanh(x) / (1*(cosh(x)/cosh(x)) - sinh(x)*sinh(x)/(cosh(x)*cosh x))
  by A1,XCMPLX_1:60
  .= 2*tanh(x) / (1-sinh(x)*sinh(x)/(cosh(x)*cosh x)) by A1,XCMPLX_1:60
  .= 2*tanh(x) / (1-sinh(x)/cosh(x)*(sinh(x)/cosh x)) by XCMPLX_1:77
  .= 2*tanh(x) / (1-tanh(x)*(sinh(x)/cosh x)) by Na4
  .= 2*tanh(x) / (1-tanh(x)*tanh x) by Na4
  .= 2*tanh(x) / (1-(tanh x)^2) by SQUARE_1:def 3;
A3: sinh(3*x) = 4*(sinh(x))|^(2+1)+3*sinh x by SIN_COS5:43
  .= 4*((sinh x)|^2*sinh x)+3*sinh x by NEWTON:11
  .= 4*(sinh x)|^2*sinh(x)+3*sinh x
  .= sinh(x)*(4*(sinh x)|^(1+1)+3) by XCMPLX_1:8
  .= sinh(x)*(4*((sinh x)|^1*sinh x)+3) by NEWTON:11
  .= sinh(x)*(4*(sinh(x)*sinh x)+3) by NEWTON:10
  .= sinh(x)*(4*((cosh x)^2-((cosh x)^2-(sinh x)^2))+3) by SQUARE_1:def 3
  .= sinh(x)*(4*((cosh x)^2-1)+3) by Na2
  .= sinh(x)*(4*(cosh x)^2-1);
A4: sinh(3*x) = sinh(x)*(4*((cosh x)^2-(sinh x)^2+(sinh x)^2))-sinh x by A3
         .= sinh(x)*(4*(1+(sinh x)^2))-sinh x by Na2
         .= (4*sinh(x)+sinh(x)*(2*((cosh x)^2-((cosh x)^2-(sinh x)^2))
            +2*(sinh x)^2))-sinh x
         .= (4*sinh(x)+sinh(x)*(2*((cosh x)^2-1)+2*(sinh x)^2))-sinh x
            by Na2
         .= (4*sinh(x)+sinh(x)*(2*((cosh x)^2+(sinh x)^2-1)))-sinh x
         .= (4*sinh(x)+sinh(x)*(2*((cosh(x)*cosh x)
            +(sinh x)^2-1)))-sinh x by SQUARE_1:def 3
         .= (4*sinh(x)+sinh(x)*(2*((cosh(x)*cosh(x)+sinh(x)*sinh x)-1)))
            -sinh(x) by SQUARE_1:def 3
         .= (4*sinh(x)+sinh(x)*(2*(cosh(x+x)-1)))-sinh(x) by Na12
         .= 4*sinh(x)-sinh(x)+sinh(x)*(2*(cosh(x+x)-1))
         .= (4-1)*sinh(x)+sinh(x)*(2*(cosh(2*x)-1))
         .= 3*sinh(x)-2*sinh(x)*(1-cosh(2*x));
A5: cosh(2*x) = 2*((cosh x)^2-(sinh x)^2+(sinh x)^2)-1 by Na16
         .= 2*(1+(sinh x)^2)-1 by Na2
         .= 1+2*(sinh x)^2;
A6: cosh(2*x) = 2*(cosh x)^2-1 by Na16
         .= 2*(cosh x)^2-((cosh x)^2-(sinh x)^2) by Na2
         .= (cosh x)^2+(sinh x)^2;
      cosh x <> 0 by Na3;then
      cosh(x)*cosh(x) <> 0 by XCMPLX_1:6;then
B1: (cosh x)^2 <> 0 by SQUARE_1:def 3;
A8: cosh(2*x) = (1/sech x)^2+(sinh x)^2 by TH1,A6
         .= 1/(sech x)^2+(sinh x)^2*1 by SQUARE_1:59,SQUARE_1:69
         .= 1/(sech x)^2+((sinh x)^2/(cosh x)^2)*(cosh x)^2
            by B1,XCMPLX_1:88
         .= 1/(sech x)^2+(sinh x/cosh x)^2*(cosh x)^2 by SQUARE_1:69
         .= 1/(sech x)^2+(tanh x)^2*(cosh x)^2 by Na4
         .= 1/(sech x)^2+(tanh x)^2/(1^2/(cosh x)^2)
            by SQUARE_1:59,XCMPLX_1:101
         .= 1/(sech x)^2+(tanh x)^2/(1/cosh x)^2
            by SQUARE_1:69
         .= 1/(sech x)^2+(tanh x)^2/(sech x)^2 by SIN_COS5:def 2
         .= (1+(tanh x)^2)/((sech x)^2+(tanh x)^2-(tanh x)^2)
            by XCMPLX_1:63
         .= (1+(tanh x)^2)/(1-(tanh x)^2) by SIN_COS5:38;
  A9: cosh(3*x) = 4*(cosh x)|^(2+1)-3*cosh x by SIN_COS5:44
         .= 4*((cosh x)|^2*cosh x)-3*cosh x by NEWTON:11
         .= (4*(cosh x)|^2)*cosh(x)-3*cosh x
         .= (4*(cosh x)|^(1+1)-3)*cosh x by XCMPLX_1:40
         .= (4*((cosh x)|^1*cosh x)-3)*cosh x by NEWTON:11
         .= (4*(cosh(x)*cosh x)-3)*cosh x by NEWTON:10
         .= (4*((cosh x)^2-(sinh x)^2+(sinh x)^2)-3)*cosh x by SQUARE_1:def 3
         .= (4*(1+(sinh x)^2)-3)*cosh x by Na2
         .= cosh(x)*(4*(sinh x)^2+1);
  tanh(3*x) = tanh(x+x+x)
         .= (tanh(x)+tanh(x)+tanh(x)+tanh(x)*tanh(x)*tanh x)
            /(1+tanh(x)*tanh(x)+tanh(x)*tanh(x)+tanh(x)*tanh x) by TH19
         .= (3*tanh(x)+(tanh(x))|^1*tanh(x)*tanh x)/(1+tanh(x)*tanh(x)
            +tanh(x)*tanh(x)+tanh(x)*tanh x) by NEWTON:10
         .= (3*tanh(x)+(tanh(x))|^(1+1)*tanh x)/(1+tanh(x)*tanh x
            +tanh(x)*tanh(x)+tanh(x)*tanh x) by NEWTON:11
         .= (3*tanh(x)+(tanh x)|^(1+1+1))/(1+tanh(x)*tanh x
            +tanh(x)*tanh(x)+tanh(x)*tanh(x)) by NEWTON:11
         .= (3*tanh(x)+(tanh x)|^3)/(1+3*(tanh x*tanh x))
         .= (3*tanh(x)+(tanh x)|^3)/(1+3*(tanh x)^2) by SQUARE_1:def 3;
  hence thesis by A2,A3,A4,A5,A6,A8,A9;
end;

theorem
  (sinh(5*x)+2*sinh(3*x)+sinh x)/(sinh(7*x)+2*sinh(5*x)+sinh(3*x))
  = sinh(3*x)/sinh(5*x)
proof 
A1: (cosh(2*x)+2)+cosh(2*x) <> 0 by Na14;
  (sinh(5*x)+2*sinh(3*x)+sinh(x))/(sinh(7*x)+2*sinh(5*x)+sinh(3*x))
          = (sinh(3*x+2*x)+2*sinh(3*x)+sinh(x))
            /(sinh((5+2)*x)+2*sinh(5*x)+sinh(3*x))
         .= ((sinh(3*x)*cosh(2*x)+cosh(3*x)*sinh(2*x))+2*sinh(3*x)
            +sinh(x))/(sinh((5+2)*x)+2*sinh(5*x)+sinh(3*x)) by Na12
         .= ((sinh(3*x)*cosh(2*x)+sinh(3*x)*2)+(cosh(3*x)*sinh(2*x)
            +sinh(x)))/(sinh(5*x+2*x)+2*sinh(5*x)+sinh(3*x))
         .= (sinh(3*x)*(cosh(2*x)+2)+(cosh(2*x+1*x)*sinh(2*x)+sinh(x)))
            /(sinh(5*x+2*x)+2*sinh(5*x)+sinh(3*x))
         .= (sinh(3*x)*(cosh(2*x)+2)+((cosh(2*x)*cosh(x)
            +sinh(2*x)*sinh(x))*sinh(2*x)+sinh(x)))
            /(sinh(5*x+2*x)+2*sinh(5*x)+sinh(3*x)) by Na12
         .= (sinh(3*x)*(cosh(2*x)+2)+(cosh(2*x)*cosh(x)*sinh(2*x)
            +((sinh(2*x)*sinh(2*x))*sinh(x)+sinh(x))))
            /(sinh(5*x+2*x)+2*sinh(5*x)+sinh(3*x))
         .= (sinh(3*x)*(cosh(2*x)+2)+(cosh(2*x)*cosh(x)*sinh(2*x)
            +((sinh(2*x))^2*sinh(x)+1*sinh(x))))
            /(sinh(5*x+2*x)+2*sinh(5*x)+sinh(3*x)) by SQUARE_1:def 3
         .= (sinh(3*x)*(cosh(2*x)+2)+(cosh(2*x)*cosh(x)*sinh(2*x)
            +((sinh(2*x))^2+1)*sinh(x)))
            /(sinh(5*x+2*x)+2*sinh(5*x)+sinh(3*x))
         .= (sinh(3*x)*(cosh(2*x)+2)+(cosh(2*x)*cosh(x)*sinh(2*x)
            +(((cosh(2*x))^2-(sinh(2*x))^2)+(sinh(2*x))^2)*sinh(x)))
            /(sinh(5*x+2*x)+2*sinh(5*x)+sinh(3*x)) by Na2
         .= (sinh(3*x)*(cosh(2*x)+2)+(cosh(2*x)*(cosh(x)*sinh(2*x))
            +(cosh(2*x)*cosh(2*x))*sinh(x)))
            /(sinh(5*x+2*x)+2*sinh(5*x)+sinh(3*x)) by SQUARE_1:def 3
         .= (sinh(3*x)*(cosh(2*x)+2)+(cosh(2*x)*(sinh(2*x)*cosh(x)
            +cosh(2*x)*sinh(x))))/(sinh(5*x+2*x)+2*sinh(5*x)+sinh(3*x))
         .= (sinh(3*x)*(cosh(2*x)+2)+(cosh(2*x)*sinh(2*x+1*x)))
            /(sinh(5*x+2*x)+2*sinh(5*x)+sinh(3*x)) by Na12
         .= sinh(3*x)*((cosh(2*x)+2)+cosh(2*x))
            /(sinh(5*x+2*x)+sinh(5*x)*2+sinh(3*x)) by XCMPLX_1:8
         .= sinh(3*x)*((cosh(2*x)+2)+cosh(2*x))/((sinh(5*x)*cosh(2*x)
            +cosh(5*x)*sinh(2*x))+sinh(5*x)*2+sinh(3*x)) by Na12
         .= sinh(3*x)*((cosh(2*x)+2)+cosh(2*x))/(sinh(5*x)*cosh(2*x)
            +(sinh(5*x)*2+cosh(3*x+2*x)*sinh(2*x))+sinh(3*x))
            by XCMPLX_1:1
         .= sinh(3*x)*((cosh(2*x)+2)+cosh(2*x))/(sinh(5*x)*(cosh(2*x)+2)
            +(cosh(3*x+2*x)*sinh(2*x)+sinh(3*x)))
         .= sinh(3*x)*((cosh(2*x)+2)+cosh(2*x))/(sinh(5*x)*(cosh(2*x)+2)
            +((cosh(3*x)*cosh(2*x)+sinh(3*x)*sinh(2*x))
            *sinh(2*x)+sinh(3*x))) by Na12
         .= sinh(3*x)*((cosh(2*x)+2)+cosh(2*x))/(sinh(5*x)*(cosh(2*x)+2)
            +(cosh(3*x)*cosh(2*x)*sinh(2*x)
            +sinh(3*x)*(sinh(2*x)*sinh(2*x)+1)))
         .= sinh(3*x)*((cosh(2*x)+2)+cosh(2*x))/(sinh(5*x)*(cosh(2*x)+2)
            +(cosh(3*x)*cosh(2*x)*sinh(2*x)+sinh(3*x)*((sinh(2*x))^2+1)))
            by SQUARE_1:def 3
         .= sinh(3*x)*((cosh(2*x)+2)+cosh(2*x))/(sinh(5*x)*(cosh(2*x)+2)
            +(cosh(3*x)*cosh(2*x)*sinh(2*x)+sinh(3*x)*(((cosh(2*x))^2
            -(sinh(2*x))^2)+(sinh(2*x))^2))) by Na2
         .= sinh(3*x)*((cosh(2*x)+2)+cosh(2*x))/(sinh(5*x)*(cosh(2*x)+2)
            +(cosh(2*x)*(cosh(3*x)*sinh(2*x))
            +(cosh(2*x)*cosh(2*x))*sinh(3*x))) by SQUARE_1:def 3
         .= sinh(3*x)*((cosh(2*x)+2)+cosh(2*x))/(sinh(5*x)*(cosh(2*x)+2)
            +cosh(2*x)*(sinh(2*x)*cosh(3*x)+cosh(2*x)*sinh(3*x)))
         .= sinh(3*x)*((cosh(2*x)+2)+cosh(2*x))/(sinh(5*x)*(cosh(2*x)+2)
            +cosh(2*x)*sinh(2*x+3*x)) by Na12
         .= sinh(3*x)*((cosh(2*x)+2)+cosh(2*x))
            /(sinh(5*x)*(cosh(2*x)+2)+sinh(5*x)*cosh(2*x))
         .= sinh(3*x)*((cosh(2*x)+2)+cosh(2*x))
            /(sinh(5*x)*((cosh(2*x)+2)+cosh(2*x)))
         .= sinh(3*x)/sinh(5*x) by A1,XCMPLX_1:92;
  hence thesis;
end;

theorem
  x >= 0 implies tanh(x/2) = sqrt((cosh(x)-1)/(cosh(x)+1))
proof assume A1: x >= 0;
  cosh(x)+1 > 0 by Na14;then
A2: (cosh(x)+1)/2 > 0 by XREAL_1:141;
  cosh(x)-1 >= 0 by Na14;then
A3: (cosh(x)-1)/2 >= 0/2 by XREAL_1:74;
  tanh(x/2) = sinh(x/2)/cosh(x/2) by Na4
         .= sqrt ((cosh(x)-1)/2)/cosh(x/2) by A1,TH22
         .= sqrt ((cosh(x)-1)/2)/sqrt((cosh(x)+1)/2) by SIN_COS5:48
         .= sqrt (((cosh(x)-1)/2)/((cosh(x)+1)/2)) by A2,A3,SQUARE_1:99
         .= sqrt ((cosh(x)-1)/(cosh(x)+1)) by XCMPLX_1:55;
  hence thesis;
end;

theorem
  x < 0 implies tanh(x/2) = -sqrt((cosh(x)-1)/(cosh(x)+1))
proof assume A1: x < 0;
  cosh(x)+1 > 0 by Na14;then
A2: (cosh(x)+1)/2 > 0 by XREAL_1:141;
  cosh(x)-1 >= 0 by Na14;then
A3: (cosh(x)-1)/2 >= 0/2 by XREAL_1:74;
  tanh(x/2) = sinh(x/2)/cosh(x/2) by Na4
         .= (-sqrt ((cosh(x)-1)/2))/cosh(x/2) by A1,TH23
         .= -sqrt ((cosh(x)-1)/2)/cosh(x/2) by XCMPLX_1:188
         .= -sqrt ((cosh(x)-1)/2)/sqrt((cosh(x)+1)/2) by SIN_COS5:48
         .= -sqrt (((cosh(x)-1)/2)/((cosh(x)+1)/2)) by A2,A3,SQUARE_1:99
         .= -sqrt ((cosh(x)-1)/(cosh(x)+1)) by XCMPLX_1:55;
  hence thesis;
end;

theorem
  (sinh x)|^3 = (sinh(3*x)-3*sinh x)/4 &
  (sinh x)|^4 = (cosh(4*x)-4*cosh(2*x)+3)/8 &
  (sinh x)|^5 = (sinh(5*x)-5*sinh(3*x)+10*sinh(x))/16 &
  (sinh x)|^6 = (cosh(6*x)-6*cosh(4*x)+15*cosh(2*x)-10)/32 &
  (sinh x)|^7 = (sinh(7*x)-7*sinh(5*x)+21*sinh(3*x)-35*sinh(x))/64 &
  (sinh x)|^8 = (cosh(8*x)-8*cosh(6*x)+28*cosh(4*x)-56*cosh(2*x)+35)/128
proof
A1: (sinh x)|^3 = (4*(sinh(x))|^3+3*sinh(x)-3*sinh(x))/4
         .= (sinh(3*x)-3*sinh(x))/4 by SIN_COS5:43;
A2: (sinh x)|^4 = (sinh(3*x)-3*sinh(x))/4*sinh(x) by A1,POLYEQ_2:4
         .= (sinh(3*x)*sinh(x)-3*(sinh(x)*sinh(x)))/4
         .= (1/2*(cosh(3*x+1*x)-cosh(3*x-1*x))-3*(sinh(x)*sinh(x)))/4 by TH9
         .= (1/2*(cosh(4*x)-cosh(2*x))-(sinh x)^2*3)/4 by SQUARE_1:def 3
         .= (1/2*(cosh(4*x)-cosh(2*x))-1/2*(cosh(2*x)-1)*3)/4 by Na9
         .= (cosh(4*x)-4*cosh(2*x)+3)/8;
A3: (sinh x)|^5 = (sinh(x))|^(4+1)
         .= (cosh(4*x)-4*cosh(2*x)+3)/8*sinh(x) by A2,NEWTON:11
         .= (cosh(4*x)*sinh(x)-4*(cosh(2*x)*sinh(x))+3*sinh(x))/8
         .= (1/2*(sinh(4*x+1*x)-sinh(4*x-1*x))-4*(cosh(2*x)*sinh(x))
            +3*sinh(x))/8 by TH9
         .= (1/2*(sinh(5*x)-sinh(3*x))-4*(1/2*(sinh(2*x+1*x)
            -sinh(2*x-1*x)))+3*sinh(x))/8 by TH9
         .= (1/2*(sinh(5*x)-sinh(3*x))-1/2*((sinh(3*x)-sinh(1*x))*4)
            +3*sinh(x))/8 by XCMPLX_1:4
         .= (1/2*(sinh(5*x)-sinh(3*x)-(sinh(3*x)-sinh(1*x))*4)
            +3*sinh(x))/8
         .= (1/2*(sinh(5*x)-sinh(3*x)-(sinh(3*x)*4-sinh(x)*4))
            +3*sinh(x))/8 by XCMPLX_1:40
         .= (sinh(5*x)-5*sinh(3*x)+10*sinh(x))/16;
A4: (sinh x)|^6 = (sinh x)|^(5+1)
         .= (sinh(5*x)-5*sinh(3*x)+10*sinh(x))/16*sinh(x) by A3,NEWTON:11
         .= (sinh(5*x)*sinh(x)-5*(sinh(3*x)*sinh(x))
            +10*(sinh(x)*sinh(x)))/16
         .= (1/2*(cosh(5*x+1*x)-cosh(5*x-1*x))-5*(sinh(3*x)*sinh(x))
            +10*(sinh(x)*sinh(x)))/16 by TH9
         .= (1/2*(cosh(6*x)-cosh(4*x))-1/2*(cosh(3*x+1*x)
            -cosh(3*x-1*x))*5+10*(sinh(x)*sinh(x)))/16 by TH9
         .= (1/2*(cosh(6*x)-cosh(4*x)-(cosh(3*x+1*x)
            -cosh(3*x-1*x))*5)+10*(sinh(x)*sinh(x)))/16
         .= (1/2*(cosh(6*x)-cosh(4*x)-(cosh(4*x)*5-cosh(2*x)*5))
            +10*(sinh(x)*sinh(x)))/16 by XCMPLX_1:40
         .= (1/2*(cosh(6*x)-6*cosh(4*x)+cosh(2*x)*5)+10*(sinh x)^2)/16
            by SQUARE_1:def 3
         .= (1/2*(cosh(6*x)-6*cosh(4*x)+cosh(2*x)*5)+1/2*(cosh(2*x)-1)*10)/16
            by Na9
         .= (cosh(6*x)-6*cosh(4*x)+15*cosh(2*x)-10)/32;
A5: (sinh x)|^7 = (sinh x)|^(6+1)
         .= (cosh(6*x)-6*cosh(4*x)+15*cosh(2*x)-10)/32*sinh(x)
            by A4,NEWTON:11
         .= ((cosh(6*x)*sinh(x)-6*cosh(4*x)*sinh(x)
            +15*cosh(2*x)*sinh(x)-10*sinh(x)))/32
         .= (1/2*(sinh(6*x+1*x)-sinh(6*x-1*x))-6*cosh(4*x)*sinh(x)
            +15*cosh(2*x)*sinh(x)-10*sinh(x))/32 by TH9
         .= (1/2*(sinh(7*x)-sinh(5*x))-6*(cosh(4*x)*sinh(x))
            +15*cosh(2*x)*sinh(x)-10*sinh(x))/32
         .= (1/2*(sinh(7*x)-sinh(5*x))-6*(1/2*(sinh(4*x+1*x)-sinh(4*x-1*x)))
            +15*cosh(2*x)*sinh(x)-10*sinh(x))/32 by TH9
         .= (1/2*(sinh(7*x)-sinh(5*x))-1/2*(sinh(5*x)-sinh(3*x))*6
            +15*cosh(2*x)*sinh(x)-10*sinh(x))/32
         .= (1/2*(sinh(7*x)-7*sinh(5*x)+sinh(3*x)*6)
            +15*(cosh(2*x)*sinh(x))-10*sinh(x))/32
         .= (1/2*(sinh(7*x)-7*sinh(5*x)+sinh(3*x)*6)+15
            *(1/2*(sinh(2*x+1*x)-sinh(2*x-1*x)))-10*sinh(x))/32 by TH9
         .= (1/2*(sinh(7*x)-7*sinh(5*x)+sinh(3*x)*6)
            +1/2*((sinh(3*x)-sinh(1*x))*15)-10*sinh(x))/32 by XCMPLX_1:4
         .= (1/2*(sinh(7*x)-7*sinh(5*x)+sinh(3*x)*6
            +(sinh(3*x)*15-sinh(1*x)*15))-10*sinh(x))/32
         .= (1/2*((sinh(7*x)-7*sinh(5*x)+sinh(3*x)*6)
            +(sinh(3*x)*15-sinh(x)*15))-10*sinh(x))/32
         .= (sinh(7*x)-7*sinh(5*x)+21*sinh(3*x)-35*sinh(x))/64;
  (sinh x)|^8 = (sinh x)|^(7+1)
         .= (sinh(7*x)-7*sinh(5*x)+21*sinh(3*x)-35*sinh(x))/64*sinh(x)
            by A5,NEWTON:11
         .= (sinh(7*x)*sinh(x)-7*(sinh(5*x)*sinh x)
            +21*(sinh(3*x)*sinh(x))-35*(sinh(x)*sinh x))/64
         .= (1/2*(cosh(7*x+1*x)-cosh(7*x-1*x))-7*(sinh(5*x)*sinh(x))
            +21*(sinh(3*x)*sinh(x))-35*(sinh(x)*sinh x))/64 by TH9
         .= (1/2*(cosh(8*x)-cosh(6*x))-(1/2*(cosh(5*x+1*x)
            -cosh(5*x-1*x))*7)+21*(sinh(3*x)*sinh x)
            -35*(sinh(x)*sinh(x)))/64 by TH9
         .= (1/2*(cosh(8*x)-cosh(6*x))-1/2*((cosh(6*x)-cosh(4*x))*7)
            +21*(sinh(3*x)*sinh(x))-35*(sinh(x)*sinh x))/64 by XCMPLX_1:4
         .= (1/2*(cosh(8*x)-8*cosh(6*x)+cosh(4*x)*7)+1/2*(cosh(3*x+1*x)
            -cosh(3*x-1*x))*21-35*(sinh(x)*sinh x))/64 by TH9
         .= (1/2*(cosh(8*x)-8*cosh(6*x)+cosh(4*x)*7+(cosh(3*x+1*x)
            -cosh(3*x-1*x))*21)-35*(sinh(x)*sinh x))/64
         .= (1/2*(cosh(8*x)-8*cosh(6*x)+cosh(4*x)*7+(cosh(4*x)*21
            -cosh(2*x)*21))-35*(sinh(x)*sinh x))/64 by XCMPLX_1:40
         .= (1/2*(cosh(8*x)-8*cosh(6*x)+28*cosh(4*x)+-cosh(2*x)*21)
            -35*(sinh x)^2)/64 by SQUARE_1:def 3
         .= (1/2*(cosh(8*x)-8*cosh(6*x)+28*cosh(4*x)+-cosh(2*x)*21)
            -1/2*(cosh(2*x)-1)*35)/64 by Na9
         .= (cosh(8*x)-8*cosh(6*x)+28*cosh(4*x)-56*cosh(2*x)+35)/128;
  hence thesis by A1,A2,A3,A4,A5;
end;

theorem
  (cosh x)|^3 = (cosh(3*x)+3*cosh x)/4 &
  (cosh x)|^4 = (cosh(4*x)+4*cosh(2*x)+3)/8 &
  (cosh x)|^5 = (cosh(5*x)+5*cosh(3*x)+10*cosh x)/16 &
  (cosh x)|^6 = (cosh(6*x)+6*cosh(4*x)+15*cosh(2*x)+10)/32 &
  (cosh x)|^7 = (cosh(7*x)+7*cosh(5*x)+21*cosh(3*x)+35*cosh x)/64 &
  (cosh x)|^8 = (cosh(8*x)+8*cosh(6*x)+28*cosh(4*x)+56*cosh(2*x)+35)/128
  proof
A1: (cosh x)|^3 = (cosh x)|^(1+1+1)
    .= (cosh x)|^(1+1)*cosh(x) by NEWTON:11
    .= (cosh x)|^1*cosh(x)*cosh(x) by NEWTON:11
    .= (1+3)/4*cosh(x)*cosh(x)*cosh(x) by NEWTON:10
    .= ((cosh x)^2*cosh(x) + 3*(cosh(x)*((cosh x)^2-(sinh x)^2
       +(sinh x)^2)))/4 by SQUARE_1:def 3
    .= ((cosh x)^2*cosh(x)+3*(cosh(x)*(1+(sinh x)^2)))/4 by Na2
    .= (cosh(x)*(cosh(x)*cosh(x))+((1+2)*(cosh(x)*(sinh x)^2)+3*cosh x))/4
    by SQUARE_1:def 3
    .= (cosh(x)*(cosh(x)*cosh(x)+(sinh x)^2)+2*(cosh(x)*(sinh x)^2)
    + 3*cosh x)/4
    .= (cosh(x)*(cosh(x)*cosh(x)+sinh(x)*sinh x)+2*(cosh(x)*(sinh x)^2)
    + 3*cosh x)/4 by SQUARE_1:def 3
    .= (cosh(x)*cosh(x+x)+2*(cosh(x)*(sinh x)^2) + 3*cosh x)/4 by Na12
    .= (cosh(x)*cosh(x+x)+2*(cosh(x)*(sinh(x)*sinh x))
    + 3*cosh x)/4 by SQUARE_1:def 3
    .= (cosh(x)*cosh(2*x)+2*sinh(x)*cosh(x)*sinh(x)+3*cosh(x))/4
    .= (cosh(2*x)*cosh(x)+sinh(2*x)*sinh(x)+3*cosh(x))/4 by Na16
    .= (cosh(x+x+x)+3*cosh x)/4 by Na12
    .= (cosh(3*x)+3*cosh x)/4;
A2: (cosh x)|^4 = (cosh(3*x)+3*cosh x)/4*cosh(x) by A1,POLYEQ_2:4
    .= (cosh(3*x)*cosh(x)+3*(cosh(x)*cosh x))/4
    .= (1/2*(cosh(3*x+1*x)+cosh(3*x-x))+3*(cosh(x)*cosh x))/4 by TH9
    .= (1/2*(cosh(4*x)+cosh(2*x))+3*(1/2*(cosh(x+x)+cosh(x-x))))/4 by TH9
    .= (1/2*(cosh(4*x)+cosh(2*x))+1/2*(cosh(2*x)+1)*3)/4
    by SIN_COS2:def 4,SIN_COS2:15
    .= (cosh(4*x)+4*cosh(2*x)+3)/8;
A3: (cosh x)|^5 = (cosh x)|^(4+1)
    .= (cosh(4*x)+4*cosh(2*x)+3)/8*cosh(x) by A2,NEWTON:11
    .= (cosh(4*x)*cosh(x)+4*(cosh(2*x)*cosh(x))+3*cosh(x))/8
    .= (1/2*(cosh(4*x+1*x)+cosh(4*x-x)) + 4*(cosh(2*x)*cosh(x))
    + 3*cosh(x))/8 by TH9
    .= (1/2*(cosh(5*x)+cosh(3*x)) + 4*(1/2*(cosh(2*x+1*x)+cosh(2*x-x)))
    + 3*cosh(x))/8 by TH9
    .= (1/2*(cosh(5*x)+cosh(3*x)) + 1/2*((cosh((2+1)*x)+cosh(1*x))*4)
    + 3*cosh(x))/8 by XCMPLX_1:4
    .= (1/2*(cosh(5*x)+cosh(3*x)+(cosh(3*x)+cosh(x))*4)
    + 3*cosh(x))/8 by XCMPLX_1:8
    .= (cosh(5*x)+5*cosh(3*x)+10*cosh(x))/16;
A4: (cosh x)|^6 = (cosh x)|^(5+1)
    .= (cosh(5*x)+5*cosh(3*x)+10*cosh(x))/16*cosh(x) by A3,NEWTON:11
    .= (cosh(5*x)*cosh(x)+5*(cosh(3*x)*cosh(x))+10*(cosh(x)*cosh(x)))/16
    .= (1/2*(cosh(5*x+x)+cosh(5*x-x))+5*(cosh(3*x)*cosh(x))
    + 10*(cosh(x)*cosh x))/16 by TH9
    .= (1/2*(cosh(5*x+x)+cosh(5*x-x)) + 1/2*(cosh(3*x+x)+cosh(3*x-x))*5
    + 10*(cosh(x)*cosh x))/16 by TH9
    .= (1/2*(cosh(5*x+x)+cosh(5*x-x)) + 1/2*((cosh(3*x+x)+cosh(3*x-x))*5)
    + 10*(cosh(x)*cosh x))/16
    .= (1/2*(cosh(6*x)+cosh(4*x)+(cosh(4*x)+cosh(2*x))*5)
    + 10*(cosh(x)*cosh x))/16 by XCMPLX_1:8
    .= (1/2*(cosh(6*x)+6*cosh(4*x)+cosh(2*x)*5)
    + 10*(cosh x)^2)/16 by SQUARE_1:def 3
    .= (1/2*(cosh(6*x)+6*cosh(4*x)+cosh(2*x)*5)
    + 1/2*(cosh(2*x)+1)*10)/16 by Na9
    .= (cosh(6*x)+6*cosh(4*x)+15*cosh(2*x)+10)/32;
A5: (cosh x)|^7 = (cosh(x))|^(6+1)
    .= (cosh(6*x)+6*cosh(4*x)+15*cosh(2*x)+10)/32*cosh(x) by A4,NEWTON:11
    .= (cosh(6*x)*cosh(x) + 6*(cosh(4*x)*cosh(x))
    + 15*(cosh(2*x)*cosh(x)) + 10*cosh(x))/32
    .= (1/2*(cosh(6*x+1*x)+cosh(6*x-1*x)) + 6*(cosh(4*x)*cosh(x))
    + 15*(cosh(2*x)*cosh(x)) + 10*cosh(x))/32 by TH9
    .= (1/2*(cosh(7*x)+cosh(5*x)) + 1/2*(cosh(4*x+x)+cosh(4*x-x))*6
    + 15*(cosh(2*x)*cosh(x)) + 10*cosh(x))/32 by TH9
    .= (1/2*(cosh(7*x)+cosh(5*x)) + 1/2*((cosh(5*x)+cosh((4-1)*x))*6)
    + 15*(cosh(2*x)*cosh(x)) + 10*cosh(x))/32 by XCMPLX_1:4
    .= (1/2*(cosh(7*x)+cosh(5*x)+(cosh(5*x)+cosh(3*x))*6)
    + 15*(cosh(2*x)*cosh(x)) + 10*cosh(x))/32 by XCMPLX_1:8
    .= (1/2*(cosh(7*x)+7*cosh(5*x)+cosh(3*x)*6)
    + 1/2*(cosh(2*x+x)+cosh(2*x-x))*15 + 10*cosh(x))/32 by TH9
    .= (1/2*(cosh(7*x)+7*cosh(5*x)+cosh(3*x)*6)
    + 1/2*((cosh(3*x)+cosh((2-1)*x))*15) + 10*cosh(x))/32 by XCMPLX_1:4
    .= (1/2*(cosh(7*x)+7*cosh(5*x)+cosh(3*x)*6+(cosh(3*x)+cosh(1*x))*15)
    + 10*cosh x)/32 by XCMPLX_1:8
    .= (1/2*(cosh(7*x)+7*cosh(5*x)+cosh(3*x)*6+(cosh(3*x)*15+cosh(x)*15))
    + 10*cosh x)/32 by XCMPLX_1:8
    .= (cosh(7*x)+7*cosh(5*x)+21*cosh(3*x)+35*cosh(x))/64;
    (cosh x)|^8 = (cosh x)|^(7+1)
    .= (cosh(7*x)+7*cosh(5*x)+21*cosh(3*x)+35*cosh(x))/64*cosh(x)
    by A5,NEWTON:11
    .= ((cosh(7*x)+7*cosh(5*x)+21*cosh(3*x))*cosh x+35*(cosh(x)*cosh x))/64
    .= (cosh(7*x)*cosh(x) + 7*cosh(5*x)*cosh(x)
    + 21*cosh(3*x)*cosh(x) + 35*(cosh x)^2)/64 by SQUARE_1:def 3
    .= (1/2*(cosh(7*x+1*x)+cosh(7*x-x)) + 7*cosh(5*x)*cosh(x)
    + 21*cosh(3*x)*cosh(x) + 35*(cosh x)^2)/64 by TH9
    .= (1/2*(cosh(8*x)+cosh(6*x)) + 7*(cosh(5*x)*cosh(x))
    + 21*cosh(3*x)*cosh(x) + 35*(cosh x)^2)/64
    .= (1/2*(cosh(8*x)+cosh(6*x)) + 7*(1/2*(cosh(5*x+1*x)+cosh(5*x-1*x)))
    + 21*cosh(3*x)*cosh(x) + 35*(cosh x)^2)/64 by TH9
    .= (1/2*(cosh(8*x)+cosh(6*x)) + 1/2*((cosh(6*x)+cosh(4*x))*7)
    + 21*cosh(3*x)*cosh(x) + 35*(cosh x)^2)/64 by XCMPLX_1:4
    .= (1/2*(cosh(8*x)+8*cosh(6*x)+cosh(4*x)*7)
    + 21*(cosh(3*x)*cosh x) + 35*(cosh x)^2)/64
    .= (1/2*(cosh(8*x)+8*cosh(6*x)+cosh(4*x)*7)
    + 21*(1/2*(cosh(3*x+1*x)+cosh(3*x-x))) + 35*(cosh x)^2)/64 by TH9
    .= (1/2*(cosh(8*x)+8*cosh(6*x)+cosh(4*x)*7)
    + 1/2*(cosh(4*x)+cosh(2*x))*21 + 35*(cosh x)^2)/64
    .= (1/2*(cosh(8*x)+8*cosh(6*x)+28*cosh(4*x)+cosh(2*x)*21)
    + 1/2*(cosh(2*x)+1)*35)/64 by Na9
    .= (cosh(8*x)+8*cosh(6*x)+28*cosh(4*x)+56*cosh(2*x)+35)/128;
    hence thesis by A1,A2,A3,A4,A5;
  end;

theorem
  cosh(2*y)+cos(2*z) = 2+2*((sinh y)^2-(sin z)^2) &
  cosh(2*y)-cos(2*z) = 2*((sinh y)^2+(sin z)^2)
  proof
A1: cosh(2*y)+cos(2*z)
    = 1+2*(sinh y)^2+cos(2*z) by TH24
    .= 1+2*(sinh y)^2+(1-2*(sin z)^2) by SIN_COS5:7
    .= 2+2*((sinh y)^2-(sin z)^2);
    cosh(2*y)-cos(2*z) = 1+2*(sinh y)^2-cos(2*z) by TH24
    .= 1+2*(sinh y)^2-(1-2*(sin z)^2) by SIN_COS5:7
    .= 2*((sinh y)^2+(sin z)^2);
    hence thesis by A1;
  end;
