:: On $T_{1}$ Reflex of Topological Space
::  by Adam Naumowicz and Mariusz {\L}api\'nski
::
:: Received March 7, 1998
:: Copyright (c) 1998 Association of Mizar Users

environ

 vocabularies FUNCT_1, RELAT_1, PRE_TOPC, EQREL_1, BORSUK_1, TARSKI, BOOLE,
      SUBSET_1, SETFAM_1, PROB_1, URYSOHN1, ORDINAL2, T_1TOPSP, NAT_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, RELAT_1, FUNCT_1, RELSET_1,
      PARTFUN1, FUNCT_2, SETFAM_1, DOMAIN_1, STRUCT_0, PRE_TOPC, EQREL_1,
      TOPS_2, URYSOHN1, BORSUK_1;
 constructors SETFAM_1, TOPS_2, BORSUK_1, URYSOHN1;
 registrations XBOOLE_0, SUBSET_1, FUNCT_1, RELSET_1, FUNCT_2, EQREL_1,
      STRUCT_0, PRE_TOPC, BORSUK_1;
 requirements BOOLE, SUBSET;
 definitions TARSKI, PRE_TOPC, XBOOLE_0, STRUCT_0;
 theorems PRE_TOPC, TARSKI, SETFAM_1, EQREL_1, URYSOHN1, BORSUK_1, FUNCT_2,
      FUNCT_1, TOPS_2, RELSET_1, RELAT_1, XBOOLE_0, XBOOLE_1;
 schemes FUNCT_1;

begin

reserve y,v,w for set;

canceled;

theorem Th2:
 for T being non empty TopSpace,
     A being non empty a_partition of the carrier of T,
     y being Subset of space A holds (Proj(A))"y = union y
      proof
       let T be non empty TopSpace;
       let A be non empty a_partition of the carrier of T;
       let y be Subset of space A;
       reconsider y as Subset of A by BORSUK_1:def 10;
            (Proj(A))"y = (proj A)"y by BORSUK_1:def 11
                      .= union y by BORSUK_1:31;
       hence thesis;
      end;

canceled 2;

theorem Th5:
 for T being non empty TopSpace,
     S being non empty a_partition of the carrier of T,
     A being Subset of space S,
     B being Subset of T holds
     B = union A implies (A is closed iff B is closed)
      proof
       let T be non empty TopSpace;
       let S be non empty a_partition of the carrier of T;
       let A be Subset of space S;
       let B be Subset of T;
       assume
A1:   B = union A;
       reconsider C = A as Subset of S by BORSUK_1:def 10;
A2:    [#](T) \ union A = (union S) \ (union C) by EQREL_1:def 6
                     .= union (S \ A) by EQREL_1:52
                     .= union ([#](space S) \ A) by BORSUK_1:def 10;
       thus A is closed implies B is closed
        proof
         assume A is closed;
then A3:       [#](space S) \ A is open by PRE_TOPC:def 6;
         reconsider om = [#](space S) \ A as Subset of S by BORSUK_1:def 10;
            om in the topology of space S by A3,PRE_TOPC:def 5;
         then [#](T) \ B in the topology of T by A1,A2,BORSUK_1:69;
         then [#](T) \ B is open by PRE_TOPC:def 5;
         hence thesis by PRE_TOPC:def 6;
        end;
       thus B is closed implies A is closed
        proof
         assume B is closed;
         then [#](T) \ B is open by PRE_TOPC:def 6;
         then A4:       [#](T) \ union A in the topology of T by A1,
PRE_TOPC:def 5;
         reconsider om = [#](space S) \ A as Subset of S by BORSUK_1:def 10;
              om in the topology of space S by A2,A4,BORSUK_1:69;
         then [#](space S) \ A is open by PRE_TOPC:def 5;
         hence thesis by PRE_TOPC:def 6;
        end;
      end;

definition
  canceled 4;
end;

canceled 5;

::reserve F for Part-Family of X;

:: Families of partitions of topological spaces

reserve T for non empty TopSpace;

theorem Th11:
 { A where A is a_partition of the carrier of T : A is closed } is
                                        Part-Family of the carrier of T
  proof
   set S = { A where A is a_partition of the carrier of T : A is closed };
   A1: S c= bool bool the carrier of T
   proof
     let B be set; assume B in S;
     then consider A being a_partition of the carrier of T such that
A2:   B = A and A is closed;
     thus thesis by A2;
   end;
        now let B be set;
   assume B in { A where A is a_partition of the carrier of T : A is closed };
   then consider A being a_partition of the carrier of T such that
A3: B = A and A is closed;
   thus B is a_partition of the carrier of T by A3;
   end;
   hence thesis by A1,EQREL_1:def 9,EQREL_1:def 10;
  end;

definition
 let T;
 func Closed_Partitions T -> non empty Part-Family of the carrier of T equals
  { A where A is a_partition of the carrier of T : A is closed };
 coherence
  proof
   set F = { A where A is a_partition of the carrier of T : A is closed };
   reconsider ct = {the carrier of T} as
                            a_partition of the carrier of T by EQREL_1:48;
        for A being Subset of T st A in ct holds A is closed
    proof
     let A be Subset of T; assume A in ct;
     then A = [#](T) by TARSKI:def 1;
     hence thesis;
    end;
  then ct is closed by TOPS_2:def 2;
  then ct in F; hence thesis by Th11;
 end;
end;

:: T_1 reflex of a topological space

definition
 let T be non empty TopSpace;
 func T_1-reflex T -> TopSpace equals
    space (Intersection Closed_Partitions T);
 correctness;
end;

registration let T;
 cluster T_1-reflex T -> strict non empty;
 coherence;
end;

theorem Th12:
 for T being non empty TopSpace holds T_1-reflex T is being_T1
  proof
   let T be non empty TopSpace;
        now
    let p be Point of T_1-reflex T;
    A1: the carrier of T_1-reflex T = Intersection Closed_Partitions T
                                    by BORSUK_1:def 10;
    then A2:  p in Intersection Closed_Partitions T;
    consider x being Element of T such that
A3:  p = EqClass(x,Intersection Closed_Partitions T) by A1,EQREL_1:51;
A4:  p = meet { EqClass(x,S) where S is a_partition of the carrier of T :
    S in Closed_Partitions T } by A3,EQREL_1:def 11;
    reconsider q=p as Subset of T by A3;
A5:  { EqClass(x,S) where S is a_partition of the carrier of T :
               S in Closed_Partitions T } is non empty
     proof
      consider Y being set such that
A6:   Y in Closed_Partitions T by XBOOLE_0:def 1;
      reconsider Y as a_partition of the carrier of T by A6,EQREL_1:def 10;
           EqClass(x,Y) in
      {EqClass(x,S) where S is a_partition of the carrier of T
      : S in Closed_Partitions T} by A6;
      hence thesis;
     end;
      { EqClass(x,S) where S is a_partition of the carrier of T :
               S in Closed_Partitions T } c= bool the carrier of T
     proof
      let Z be set;
      assume Z in { EqClass(x,S) where S is a_partition of the carrier of T
                  : S in Closed_Partitions T };
      then consider Y being a_partition of the carrier of T such that
A7:   Z = EqClass(x,Y) & Y in Closed_Partitions T;
      thus thesis by A7;
     end;
    then reconsider m = { EqClass(x,S) where S is a_partition of the carrier of
T :
    S in Closed_Partitions T } as non empty Subset-Family of T by A5;
    reconsider m as non empty Subset-Family of T;
      for A being Subset of T st A in m holds A is closed
     proof
      let A be Subset of T;
      assume A in m;
      then consider S being a_partition of the carrier of T such that
A8:    A = EqClass(x,S) & S in Closed_Partitions T;
      consider B being a_partition of the carrier of T such that
A9:    S = B & B is closed by A8;
           A in S by A8,EQREL_1:def 8;
      hence thesis by A9,TOPS_2:def 2;
     end;
    then q is closed by A4,PRE_TOPC:44;
    then [#](T) \ q is open by PRE_TOPC:def 6;
    then [#](T) \ p in the topology of T by PRE_TOPC:def 5;
    then A10:  union((Intersection Closed_Partitions T) \ {p}) in the topology
of T
                                         by A2,EQREL_1:53;
    reconsider I = (Intersection Closed_Partitions T) \ {p} as
    Subset of (Intersection Closed_Partitions T) by XBOOLE_1:36;
A11:  I in {A where A is Subset of (Intersection Closed_Partitions T) :
    union A in the topology of T} by A10;
        A12: the topology of space(Intersection Closed_Partitions T) =
    {A where A is Subset of
    (Intersection Closed_Partitions T) : union A in the topology of T}
              by BORSUK_1:def 10;
    reconsider I as Subset of
    space(Intersection Closed_Partitions T)
              by BORSUK_1:def 10;
    reconsider I as Subset of T_1-reflex T;
         I = ([#] T_1-reflex T) \ {p} by BORSUK_1:def 10;
    then ([#] T_1-reflex T) \ {p} is open by A11,A12,PRE_TOPC:def 5;
    hence {p} is closed by PRE_TOPC:def 6;
   end;
   hence thesis by URYSOHN1:27;
  end;

registration let T;
 cluster T_1-reflex T -> being_T1;
 coherence by Th12;
end;

registration
  cluster being_T1 non empty TopSpace;
  existence
  proof
    consider T being non empty TopSpace;
    take T_1-reflex T;
    thus thesis;
  end;
end;

definition let T be non empty TopSpace;
  func T_1-reflect T -> continuous Function of T,T_1-reflex T equals
    Proj Intersection Closed_Partitions T;
  correctness;
end;

theorem Th13:
 for T,T1 being non empty TopSpace,f being continuous Function of T,T1 holds
   T1 is being_T1 implies
   ({f"{z} where z is Element of T1 : z in rng f}
   is a_partition of the carrier of T) &
   (for A being Subset of T st
   A in {f"{z} where z is Element of T1 : z in rng f} holds
   A is closed)
    proof
     let T,T1 be non empty TopSpace;
     let f be continuous Function of T,T1;
A1: dom f = the carrier of T & rng f c= the carrier of T1
      by FUNCT_2:def 1;
     assume
A2: T1 is being_T1;
     thus {f"{z} where z is Element of T1 : z in rng f}
      is a_partition of the carrier of T
       proof
         {f"{z} where z is Element of T1 : z in rng f} c=
        bool the carrier of T
         proof
          let y;
          assume y in {f"{z} where z is Element of T1 : z in rng f};
          then consider z being Element of T1 such that
A3:        y = f"{z} & z in rng f;
          thus thesis by A3;
         end;
        then reconsider fz = {f"{z} where z is Element of T1 : z in rng f}
          as Subset-Family of T;
        reconsider fz as Subset-Family of T;
A4:     union fz = the carrier of T
         proof
          thus union fz c= the carrier of T;
          thus the carrier of T c= union fz
           proof
            let y;
            assume
A5:          y in the carrier of T;
            consider z being set such that
A6:          z = f.y;
A7:       z in rng f by A1,A5,A6,FUNCT_1:def 5;
            then reconsider z as Element of T1;
A8:          f"{z} in fz by A7;
                 f.y in {f.y} by TARSKI:def 1;
            then y in f"{z} by A1,A5,A6,FUNCT_1:def 13;
            hence thesis by A8,TARSKI:def 4;
           end;
         end;
             for A being Subset of T st
             A in fz holds A <> {} &
             for B being Subset of T st
             B in fz holds A = B or A misses B
         proof
          let A be Subset of T;
          assume A in fz;
          then consider z being Element of T1 such that
A9:        A = f"{z} & z in rng f;
          consider y being set such that
A10:        y in dom f & z = f.y by A9,FUNCT_1:def 5;
               f.y in {f.y} by TARSKI:def 1;
          hence A <> {} by A9,A10,FUNCT_1:def 13;
          let B be Subset of T;
          assume B in fz;
          then consider w being Element of T1 such that
A11:        B = f"{w} & w in rng f;
          thus A = B or A misses B
           proof
                 now
             assume not A misses B;
             then consider v being set such that
A12:           v in A & v in B by XBOOLE_0:3;
                  f.v in {z} & f.v in {w} by A9,A11,A12,FUNCT_1:def 13;
             then f.v = z & f.v = w by TARSKI:def 1;
             hence A = B by A9,A11;
            end;
            hence thesis;
           end;
         end;
        hence thesis by A4,EQREL_1:def 6;
       end;
     thus for A being Subset of T st
      A in {f"{z} where z is Element of T1 : z in rng f} holds A is closed
       proof
        let A be Subset of T;
        assume A in {f"{z} where z is Element of T1 : z in rng f};
        then consider z being Element of T1 such that
A13:    A = f"{z} & z in rng f;
         {z} is closed by A2,URYSOHN1:27;
        hence thesis by A13,PRE_TOPC:def 12;
       end;
    end;

theorem Th14:
 for T,T1 being non empty TopSpace,f being continuous Function of T,T1 holds
   T1 is being_T1 implies for w for x being Element of T holds
    w = EqClass(x,(Intersection Closed_Partitions T)) implies w c= f"{f.x}
     proof
      let T,T1 be non empty TopSpace;
      let f be continuous Function of T,T1;
A1: dom f = the carrier of T & rng f c= the carrier of T1
      by FUNCT_2:def 1;
      assume
A2:  T1 is being_T1;
      let w be set;
      let x be Element of T;
      assume
A3:   w = EqClass(x,(Intersection Closed_Partitions T));
      let y be set;
      assume
A4:    y in w;
      reconsider fz = {f"{z} where z is Element of T1 : z in rng f}
       as a_partition of the carrier of T by A2,Th13;
           for A being Subset of T st A in fz holds A is closed by A2,Th13;
      then fz is closed by TOPS_2:def 2;
      then fz in {B where B is a_partition of the carrier of T : B is closed};
then A5:   EqClass(x,fz) in
      {EqClass(x,S) where S is a_partition of the carrier of T :
      S in Closed_Partitions T};
A6:  f"{f.x} = EqClass(x,fz)
       proof
         f.x in {f.x} by TARSKI:def 1;
        then A7:     x in f"{f.x} by A1,FUNCT_1:def 13;
A8:     f.x in rng f by A1,FUNCT_1:def 5;
        reconsider fx = f.x as Element of T1;
         f"{fx} in fz by A8;
        hence thesis by A7,EQREL_1:def 8;
       end;
       EqClass(x,(Intersection Closed_Partitions T)) =
      meet{EqClass(x,S) where S is a_partition of the carrier of T :
      S in Closed_Partitions T} by EQREL_1:def 11;
      hence y in f"{f.x} by A3,A4,A5,A6,SETFAM_1:def 1;
     end;

theorem Th15:
 for T,T1 being non empty TopSpace,f being continuous Function of T,T1 holds
   T1 is being_T1 implies
    for w st w in the carrier of T_1-reflex T ex z being Element of T1
    st z in rng f & w c= f"{z}
     proof
      let T,T1 be non empty TopSpace;
      let f be continuous Function of T,T1;
      assume
A1:  T1 is being_T1;
      let w be set;
      assume
       w in the carrier of T_1-reflex T;
      then w in Intersection (Closed_Partitions T) by BORSUK_1:def 10;
      then consider x being Element of T such that
A2:    w = EqClass(x,Intersection (Closed_Partitions T)) by EQREL_1:51;
      reconsider x as Element of T;
A3: dom f = the carrier of T & rng f c= the carrier of T1
      by FUNCT_2:def 1;
      reconsider fx = f.x as Element of T1;
      take fx;
      thus thesis by A1,A2,A3,Th14,FUNCT_1:def 5;
     end;

:: The theorem on factorization

theorem Th16:
 for T,T1 being non empty TopSpace,f being continuous Function of T,T1 holds
   T1 is being_T1 implies
    ex h being continuous Function of T_1-reflex T, T1 st
     f = h*T_1-reflect T
      proof
       let T,T1 be non empty TopSpace;
       let f be continuous Function of T,T1;
       assume
A1:   T1 is being_T1;
       then reconsider fx = {f"{x} where x is Element of T1 : x in rng f}
        as a_partition of the carrier of T by Th13;
       set g = T_1-reflect T;
A2:  dom f = the carrier of T & rng f c= the carrier of T1
       by FUNCT_2:def 1;
A3:  dom g = the carrier of T & rng g c= the carrier of T_1-reflex T
       by FUNCT_2:def 1;
       defpred X[set,set] means for z being Element of T1 holds
       (z in rng f & $1 c= f"{z}) implies $2 = f"{z};
A4:    for y,w,v st y in the carrier of T_1-reflex T & X[y,w] & X[y,v]
       holds w = v
        proof
         let y,w,v;
         assume
A5:       y in the carrier of T_1-reflex T &
(for z being Element of T1 holds (z in rng f & y c= f"{z}) implies w = f"{z})
         &
(for z being Element of T1 holds (z in rng f & y c= f"{z}) implies v = f"{z});
         then y in Intersection(Closed_Partitions T) by BORSUK_1:def 10;
         then consider x being Element of T such that
A6:       y = EqClass(x,Intersection(Closed_Partitions T)) by EQREL_1:51;
         reconsider x as Element of T;
         reconsider fix = f.x as Element of T1;
              fix in rng f & y c= f"{fix} by A1,A2,A6,Th14,FUNCT_1:def 5;
         then w = f"{fix} & v = f"{fix} by A5;
         hence w = v;
        end;
A7:    for y st y in the carrier of T_1-reflex T ex w st X[y,w]
        proof
         let y;
         assume y in the carrier of T_1-reflex T;
         then y in Intersection(Closed_Partitions T) by BORSUK_1:def 10;
         then consider x being Element of T such that
A8:       y = EqClass(x,Intersection(Closed_Partitions T)) by EQREL_1:51;
         reconsider x as Element of T;
         set w = f"{f.x};
         take w;
         let z be Element of T1;
         assume
A9:      z in rng f & y c= f"{z};
         then A10:      f"{z} in fx;
A11:      f.x in rng f by A2,FUNCT_1:def 5;
         reconsider fix = f.x as Element of T1;
              f"{fix} in fx by A11;
         then A12:     w misses f"{z} or w = f"{z} by A10,EQREL_1:def 6;
A13:     y c= w & y c= f"{z} by A1,A8,A9,Th14;
              y is non empty by A8,EQREL_1:def 8;
         then consider z1 being set such that
A14:      z1 in y by XBOOLE_0:def 1;
         thus w = f"{z} by A12,A13,A14,XBOOLE_0:3;
        end;
       consider h1 being Function such that
A15:     dom h1 = the carrier of T_1-reflex T &
       for y st y in the carrier of T_1-reflex T
           holds X[y,h1.y] from FUNCT_1:sch 2(A4,A7);
        defpred X[set,set] means for z being Element of T1 holds
       (z in rng f & $1 = f"{z}) implies $2 = z;
A16:    for y,w,v st y in fx & X[y,w] & X[y,v]
       holds w = v
        proof
         let y,w,v;
         assume
A17: y in fx &
   (for z being Element of T1 holds (z in rng f & y = f"{z}) implies w = z)
    &
   (for z being Element of T1 holds (z in rng f & y = f"{z}) implies v = z);
         then consider z1 being Element of T1 such that
A18:      y = f"{z1} & z1 in rng f;
              w = z1 & v = z1 by A17,A18;
         hence thesis;
        end;
A19:    for y st y in fx ex w st X[y,w]
        proof
         let y be set;
         assume y in fx;
         then consider w being Element of T1 such that
A20:       y = f"{w} & w in rng f;
         take w;
         let z be Element of T1;
         assume
A21:     z in rng f & y = f"{z};
              now
          assume
A22:       z <> w;
          consider v being set such that
A23:        v in dom f & z = f.v by A21,FUNCT_1:def 5;
               z in {z} by TARSKI:def 1;
          then v in f"{w} by A20,A21,A23,FUNCT_1:def 13;
          then f.v in {w} by FUNCT_1:def 13;
          hence contradiction by A22,A23,TARSKI:def 1;
         end;
         hence w = z;
        end;
       consider h2 being Function such that
A24:     dom h2 = fx &
       for y st y in fx holds X[y,h2.y] from FUNCT_1:sch 2(A16,A19);
       set h = h2*h1;
A25:  dom h = the carrier of T_1-reflex T
        proof
         thus dom h c= the carrier of T_1-reflex T by A15,RELAT_1:44;
         let z be set;
         assume
A26:     z in the carrier of T_1-reflex T;
         then consider w being Element of T1 such that
A27:       w in rng f & z c= f"{w} by A1,Th15;
              h1.z = f"{w} by A15,A26,A27;
         then h1.z in dom h2 by A24,A27;
         hence z in dom h by A15,A26,FUNCT_1:21;
        end;
A28:  rng h c= rng h2
        proof
         let z be set;
         thus thesis by FUNCT_1:25;
        end;
         rng h2 c= the carrier of T1
        proof
         let y;
         assume y in rng h2;
         then consider w being set such that
A29:       w in dom h2 & y = h2.w by FUNCT_1:def 5;
         consider x being Element of T1 such that
A30:       w = f"{x} & x in rng f by A24,A29;
              h2.w = x by A24,A29,A30;
         hence y in the carrier of T1 by A29;
        end;
       then A31:    rng h c= the carrier of T1 by A28,XBOOLE_1:1;
A32:  dom (h*g) = the carrier of T
        proof
         thus dom (h*g) c= the carrier of T by A3,RELAT_1:44;
         let y be set;
         assume
A33:       y in the carrier of T;
         then g.y in rng g by A3,FUNCT_1:def 5;
         hence y in dom (h*g) by A3,A25,A33,FUNCT_1:21;
        end;
A34:    f = h*g
        proof
           for x being set st x in dom f holds f.x = (h*g).x
          proof
           let x be set;
           assume
A35:         x in dom f;
           then g.x in rng g by A3,FUNCT_1:def 5;
           then g.x in the carrier of T_1-reflex T;
           then g.x in Intersection (Closed_Partitions T) by BORSUK_1:def 10;
           then consider y being Element of T such that
A36:         g.x = EqClass(y,Intersection (Closed_Partitions T)) by EQREL_1:51;
           reconsider x as Element of T by A35;
            g = proj (Intersection Closed_Partitions T) by BORSUK_1:def 11;
           then A37:       x in g.x by BORSUK_1:def 1;
                x in EqClass(x,Intersection (Closed_Partitions T))
               by EQREL_1:def 8;
           then A38:       EqClass(x,Intersection (Closed_Partitions T)) meets
           EqClass(y,Intersection (Closed_Partitions T)) by A36,A37,XBOOLE_0:3;
           reconsider fix = f.x as Element of T1;
                g.x = EqClass(x,Intersection (Closed_Partitions T)) by A36,A38,
EQREL_1:50;
           then A39:       fix in rng f & g.x c= f"{fix} by A1,A35,Th14,
FUNCT_1:def 5;
then A40:      f"{fix} in fx;
                (h*g).x = (h2*h1).(g.x) by A32,FUNCT_1:22
                  .= h2.(h1.(g.x)) by A15,FUNCT_1:23
                  .= h2.(f"{fix}) by A15,A39
                  .= f.x by A24,A39,A40;
           hence thesis;
          end;
        hence thesis by A2,A32,FUNCT_1:9;
       end;
       reconsider h as Function of
       the carrier of T_1-reflex T,the carrier of T1
                    by A25,A31,FUNCT_2:def 1,RELSET_1:11;
       reconsider h as Function of T_1-reflex T,T1;
         h is continuous
        proof
         let y be Subset of T1;
         assume
A41:     y is closed;
         reconsider hy = h"y as Subset of
           space Intersection(Closed_Partitions T);
              union hy c= the carrier of T
          proof
           let z1 be set;
           assume z1 in union hy;
           then consider z2 being set such that
A42:        z1 in z2 & z2 in hy by TARSKI:def 4;
                z2 in the carrier of space Intersection(Closed_Partitions T) by
A42;
           then z2 in Intersection(Closed_Partitions T) by BORSUK_1:def 10;
           hence thesis by A42;
          end;
         then reconsider uhy = union hy as Subset of T;
              (h*g)"y is closed by A34,A41,PRE_TOPC:def 12;
         then g"(h"y) is closed by RELAT_1:181;
         then uhy is closed by Th2; hence h"y is closed by Th5;
        end;
       then reconsider h as continuous Function of T_1-reflex T,T1;
       take h;
       thus thesis by A34;
      end;

definition
 let T,S be non empty TopSpace;
 let f be continuous Function of T,S;
  func T_1-reflex f -> continuous Function of T_1-reflex T, T_1-reflex S means
      (T_1-reflect S) * f = it * (T_1-reflect T);
existence
 proof
   reconsider g = (T_1-reflect S) * f as continuous Function of T,T_1-reflex S
      by TOPS_2:58;
    ex h being continuous Function of T_1-reflex T, T_1-reflex S st
     g = h*T_1-reflect T by Th16;
  hence ex h being continuous Function of T_1-reflex T, T_1-reflex S
    st (T_1-reflect S) * f = h * (T_1-reflect T);
 end;
uniqueness
 proof
  let g1,g2 be continuous Function of T_1-reflex T, T_1-reflex S;
  assume that
A1: (T_1-reflect S) * f = g1 * (T_1-reflect T) and
A2: (T_1-reflect S) * f = g2 * (T_1-reflect T);
A3: dom g1 = the carrier of (T_1-reflex T) &
   dom g2 = the carrier of (T_1-reflex T) by FUNCT_2:def 1;
       now
   let x be set;
   assume A4: x in dom g1;
   then A5: x in the carrier of T_1-reflex T;
A6: the carrier of T_1-reflex T =
 (Intersection Closed_Partitions T) by BORSUK_1:def 10;
   then consider y being Element of T such that
A7: x = EqClass(y,(Intersection (Closed_Partitions T))) by A4,EQREL_1:51;
 A8: dom (T_1-reflect T) = the carrier of T &
   rng (T_1-reflect T) c= the carrier of T_1-reflex T
        by FUNCT_2:def 1;
   reconsider y as Element of T;
        T_1-reflect T = proj (Intersection Closed_Partitions T)
                               by BORSUK_1:def 11;
   then A9: y in (T_1-reflect T).y & y in x by A7,EQREL_1:def 8,BORSUK_1:def 1;
   set ty=(T_1-reflect T).y;
        ty in (Intersection Closed_Partitions T) by A6;
   then A10: ty misses x or ty = x by A5,A6,EQREL_1:def 6;
   hence g2.x = (g2 * (T_1-reflect T)).y by A8,A9,FUNCT_1:23,XBOOLE_0:3
            .= g1.x by A1,A2,A8,A9,A10,FUNCT_1:23,XBOOLE_0:3;
  end;
  hence g1=g2 by A3,FUNCT_1:9;
 end;
end;

