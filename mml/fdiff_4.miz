:: Several Differentiable Formulas of Special Functions
::  by Yan Zhang and Xiquan Liang
::
:: Received July 6, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies PRE_TOPC, BOOLE, FUNCT_1, ARYTM_1, ARYTM_3, RELAT_1, ORDINAL2,
      PARTFUN1, SEQ_1, SIN_COS, FDIFF_1, PREPOWER, POWER, GROUP_1, SQUARE_1,
      TAYLOR_1, FINSEQ_1, SIN_COS4;
 notations TARSKI, XBOOLE_0, SUBSET_1, RELAT_1, PARTFUN1, PARTFUN2, RCOMP_1,
      XREAL_0, NUMBERS, REAL_1, NAT_1, SEQ_1, FDIFF_1, PREPOWER, POWER,
      TAYLOR_1, SIN_COS, RFUNCT_1, SQUARE_1, SIN_COS4;
 constructors REAL_1, NAT_1, PARTFUN1, PARTFUN2, LIMFUNC1, RCOMP_1, TAYLOR_1,
      SERIES_1, PREPOWER, FDIFF_2, SIN_COS, RFUNCT_1, SIN_COS4;
 registrations RELSET_1, XREAL_0, MEMBERED, RCOMP_1, INT_1, SEQ_1;
 definitions SQUARE_1;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;
 theorems FDIFF_2, TAYLOR_1, XBOOLE_1, XCMPLX_1, SEQ_1, FDIFF_1, FUNCT_1,
      TARSKI, PREPOWER, RFUNCT_1, SQUARE_1, XREAL_1, XCMPLX_0, NEWTON, SIN_COS,
      SIN_COS5, SIN_COS4, WSIERP_1;

begin

 reserve c,y,X for set;
 reserve x,r,a,b,c for Real;
 reserve n for Nat;
 reserve Z for open Subset of REAL;
 reserve f,f1,f2 for PartFunc of REAL,REAL;

:: 1 f.x=ln(a+x)
theorem Th1:
  Z c= dom ((log_ number_e)*f) &
  (for x st x in Z holds f.x = a+x & f.x > 0) implies
    (log_ number_e)*f is_differentiable_on Z &
    for x st x in Z holds (((log_ number_e)*f)`|Z).x = 1/(a+x)
proof
  assume A1: Z c= dom ((log_ number_e)*f) &
  for x st x in Z holds f.x = a+x & f.x > 0;
  for y st y in Z holds y in dom f by FUNCT_1:21,A1;
  then A2:Z c= dom f by TARSKI:def 3;
  A3:for x st x in Z holds f.x = 1*x+a by A1;
  then A4:f is_differentiable_on Z &
  for x st x in Z holds (f`|Z).x = 1 by FDIFF_1:31,A2;
  A5:for x st x in Z holds (log_ number_e)*f is_differentiable_in x
  proof
    let x;
    assume A6:x in Z;
    then A7:f is_differentiable_in x by FDIFF_1:16,A4;
    f.x >0 by A1,A6;
    hence (log_ number_e)*f is_differentiable_in x by TAYLOR_1:20,A7;
  end;
  then A8:(log_ number_e)*f is_differentiable_on Z by A1,FDIFF_1:16;
  for x st x in Z holds (((log_ number_e)*f)`|Z).x = 1/(a+x)
  proof
    let x;
    assume A9:x in Z;
    then A10:f is_differentiable_in x by FDIFF_1:16,A4;
A11: f.x = a+x & f.x >0 by A1,A9;
    diff((log_ number_e)*f,x) =diff(f,x)/( f.x) by TAYLOR_1:20,A11,A10
      .=(f`|Z).x/( f.x) by A4,A9,FDIFF_1:def 8
      .=1/(a+x) by FDIFF_1:31,A2,A3,A9,A11;
    hence thesis by A9,A8,FDIFF_1:def 8;
  end;
  hence thesis by A1,FDIFF_1:16,A5;
end;

:: 2 f.x=ln(x-a)
theorem Th2:
  Z c= dom ((log_ number_e)*f) &
  (for x st x in Z holds f.x = x-a & f.x >0) implies
    (log_ number_e)*f is_differentiable_on Z &
    for x st x in Z holds (((log_ number_e)*f)`|Z).x = 1/(x-a)
proof
  assume A1:Z c= dom ((log_ number_e)*f) &
  for x st x in Z holds f.x = x-a & f.x >0;
  for y st y in Z holds y in dom f by FUNCT_1:21,A1;
  then A2:Z c= dom f by TARSKI:def 3;
A3:for x st x in Z holds f.x = 1*x+-a
  proof let x;
    assume A4:x in Z;
    1*x+-a=1*x-a;
    hence f.x = 1*x+-a by A1,A4;
  end;
  then A5:f is_differentiable_on Z &
  for x st x in Z holds (f`|Z).x = 1 by FDIFF_1:31,A2;
A6:for x st x in Z holds (log_ number_e)*f is_differentiable_in x
  proof
    let x;
    assume A7:x in Z;
    then A8:f is_differentiable_in x by FDIFF_1:16,A5;
    f.x >0 by A1,A7;
    hence (log_ number_e)*f is_differentiable_in x by TAYLOR_1:20,A8;
  end;
  then A9:(log_ number_e)*f is_differentiable_on Z by A1,FDIFF_1:16;
  for x st x in Z holds (((log_ number_e)*f)`|Z).x = 1/(x-a)
  proof
    let x;
    assume A10:x in Z;
    then A11:f is_differentiable_in x by FDIFF_1:16,A5;
A12: f.x = x-a & f.x >0 by A1,A10;
    diff((log_ number_e)*f,x) =diff(f,x)/( f.x) by TAYLOR_1:20,A12,A11
      .=(f`|Z).x/( f.x) by A5,A10,FDIFF_1:def 8
      .=1/(x-a) by A3,FDIFF_1:31,A2,A10,A12;
    hence (((log_ number_e)*f)`|Z).x = 1/(x-a) by A10,A9,FDIFF_1:def 8;
  end;
  hence thesis by A1,FDIFF_1:16,A6;
end;

:: 3 f.x=-ln(a-x)
theorem
  Z c= dom (-((log_ number_e)*f)) &
  (for x st x in Z holds f.x =a-x & f.x >0) implies
    - ((log_ number_e)*f) is_differentiable_on Z &
    for x st x in Z holds ((-((log_ number_e)*f))`|Z).x = 1/(a-x)
proof
  assume A1:Z c= dom (-((log_ number_e)*f)) &
  (for x st x in Z holds f.x =a-x & f.x > 0);
A2:-((log_ number_e)*f) = (-1)(#)((log_ number_e)*f) by RFUNCT_1:38;
A3:Z c= dom ((-1)(#)((log_ number_e)*f)) &
  (for x st x in Z holds f.x =a-x & f.x >0) by A1,RFUNCT_1:38;
  now let y;
    assume y in Z;
    then y in dom ( (-1)(#)((log_ number_e)*f)) by A1,A2;
    hence y in dom ((log_ number_e)*f) by SEQ_1:def 6;
  end;
  then A4:Z c=dom ((log_ number_e)*f) by TARSKI:def 3;
  for y st y in Z holds y in dom f by FUNCT_1:21,A4;
  then A5:Z c= dom f by TARSKI:def 3;
A6:for x st x in Z holds f.x =(-1)*x+a
  proof
    let x;
    assume x in Z;
    then f.x =a-x by A1;
    hence f.x =(-1)*x+a;
  end;
  then A7:f is_differentiable_on Z &
  for x st x in Z holds (f`|Z).x = -1 by FDIFF_1:31,A5;
  now let x;
    assume A8:x in Z;
    then A9:f is_differentiable_in x by FDIFF_1:16,A7;
    f.x >0 by A1,A8;
    hence (log_ number_e)*f is_differentiable_in x by TAYLOR_1:20,A9;
  end;
  then A10:(log_ number_e)*f is_differentiable_on Z by A4,FDIFF_1:16;
  then A11:(-1)(#)((log_ number_e)*f) is_differentiable_on Z &
  for x st x in Z holds (((-1)(#)((log_ number_e)*f))`|Z).x
     =(-1)*diff((log_ number_e)*f,x) by FDIFF_1:28,A3;
  for x st x in Z holds ((-((log_ number_e)*f))`|Z).x =1/(a-x)
  proof
    let x;
    assume A12:x in Z;
    then A13: f.x =a-x & f.x >0 by A1;
    f is_differentiable_in x by FDIFF_1:16,A7,A12;
    then A14: diff((log_ number_e)*f,x) =diff(f,x)/( f.x) by TAYLOR_1:20,A13
        .=(f`|Z).x/( f.x) by A7,A12,FDIFF_1:def 8
        .=(-1)/(a-x) by A6,FDIFF_1:31,A5,A12,A13;
    (((-1)(#)((log_ number_e)*f))`|Z).x
         =(-1)*((-1)/(a-x)) by A14,FDIFF_1:28,A3,A10,A12
        .=(-1)*(-1)/(a-x) by XCMPLX_1:75
        .=1/(a-x);
    hence ((-((log_ number_e)*f))`|Z).x =1/(a-x) by RFUNCT_1:38;
  end;
  hence thesis by A11,RFUNCT_1:38;
end;

:: 4 f.x=x-a*ln(a+x)
theorem
  Z c= dom (id Z - a(#)f) & f = (log_ number_e)*f1 &
  (for x st x in Z holds f1.x=a+x & f1.x>0) implies
  (id Z - a(#)f) is_differentiable_on Z &
  for x st x in Z holds ((id Z - a(#)f)`|Z).x = x/(a+x)
  proof
    assume A1:Z c= dom (id Z - a(#)f) & f = (log_ number_e)*f1 &
    (for x st x in Z holds f1.x=a+x & f1.x>0);
    then Z c= dom id Z /\ dom (a(#)f) by SEQ_1:def 4;
    then A2:Z c= dom id Z & Z c= dom (a(#)f) by XBOOLE_1:18;
    then A3:Z c= dom ((log_ number_e)*f1) by A1,SEQ_1:def 6;
A4: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;
    then A5:id Z is_differentiable_on Z & for x st x in Z
    holds ((id Z)`|Z).x = 1 by FDIFF_1:31,A2;
A6: f is_differentiable_on Z & for x st x in Z
    holds (f`|Z).x = 1/(a+x) by A3,Th1,A1;
    then A7:a(#)f is_differentiable_on Z & for x st x in Z holds
    ((a(#)f)`|Z).x =a*diff(f,x) by A2,FDIFF_1:28;
A8: for x st x in Z holds ((a(#)f)`|Z).x =a/(a+x)
    proof
      let x;
      assume A9:x in Z;
      hence ((a(#)f)`|Z).x=a*diff(f,x) by A6,A2,FDIFF_1:28
      .=a*(f`|Z).x by A9,FDIFF_1:def 8,A6
      .=a*(1/(a+x)) by A3,Th1,A1,A9
      .=a/(a+x) by XCMPLX_1:100;
    end;
    for x st x in Z holds ((id Z - a(#)f)`|Z).x = x/(a+x)
    proof
      let x;
      assume A10:x in Z;
      then A11:f1.x=a+x & f1.x>0 by A1;
      ((id Z - a(#)f)`|Z).x= diff(id Z,x) - diff(a(#)f,x)
      by A5,A7,A1,FDIFF_1:27,A10
      .=((id Z)`|Z).x - diff(a(#)f,x) by A5,A10,FDIFF_1:def 8
      .=((id Z)`|Z).x - ((a(#)f)`|Z).x by A10,A7,FDIFF_1:def 8
      .=1 - ((a(#)f)`|Z).x by FDIFF_1:31,A2,A4,A10
      .=1 - a/(a+x) by A8,A10
      .=(1*(a+x) - a)/(a+x) by XCMPLX_1:128,A11
      .=x/(a+x);
      hence thesis;
    end;
    hence thesis by A5,A7,A1,FDIFF_1:27;
  end;

:: 5 f.x=(2*a)*ln(a+x)-x
theorem
  Z c= dom ((2*a)(#)f - id Z) & f = (log_ number_e)*f1 &
  (for x st x in Z holds f1.x=a+x & f1.x>0) implies
  ((2*a)(#)f - id Z) is_differentiable_on Z &
  for x st x in Z holds (((2*a)(#)f - id Z)`|Z).x = (a-x)/(a+x)
  proof
    assume A1:Z c= dom ((2*a)(#)f - id Z) & f = (log_ number_e)*f1 &
    (for x st x in Z holds f1.x=a+x & f1.x>0);
    then Z c= dom ((2*a)(#)f) /\ dom id Z by SEQ_1:def 4;
    then A2:Z c= dom id Z & Z c= dom ((2*a)(#)f) by XBOOLE_1:18;
    then A3:Z c= dom ((log_ number_e)*f1) by A1,SEQ_1:def 6;
A4: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;
    then A5:id Z is_differentiable_on Z & for x st x in Z
    holds ((id Z)`|Z).x = 1 by FDIFF_1:31,A2;
A6: f is_differentiable_on Z & for x st x in Z
    holds (f`|Z).x = 1/(a+x) by A3,Th1,A1;
    then A7:(2*a)(#)f is_differentiable_on Z & for x st x in Z holds
    (((2*a)(#)f)`|Z).x =(2*a)*diff(f,x) by A2,FDIFF_1:28;
A8: for x st x in Z holds (((2*a)(#)f)`|Z).x =(2*a)/(a+x)
    proof
      let x;
      assume A9:x in Z;
      hence (((2*a)(#)f)`|Z).x =(2*a)*diff(f,x) by A6,A2,FDIFF_1:28
      .=(2*a)*(f`|Z).x by A9,FDIFF_1:def 8,A6
      .=(2*a)*(1/(a+x)) by A3,Th1,A1,A9
      .=(2*a)/(a+x) by XCMPLX_1:100;
    end;
    for x st x in Z holds (((2*a)(#)f - id Z)`|Z).x =(a-x)/(a+x)
    proof
      let x;
      assume A10:x in Z;
      then A11:f1.x=a+x & f1.x>0 by A1;
      (((2*a)(#)f - id Z)`|Z).x = diff((2*a)(#)f,x) - diff(id Z,x)
      by A5,A7,A1,FDIFF_1:27,A10
      .=diff((2*a)(#)f,x)-((id Z)`|Z).x by A5,A10,FDIFF_1:def 8
      .=(((2*a)(#)f)`|Z).x-((id Z)`|Z).x by A10,A7,FDIFF_1:def 8
      .=(((2*a)(#)f)`|Z).x - 1 by FDIFF_1:31,A2,A4,A10
      .=(2*a)/(a+x) - 1 by A8,A10
      .=(2*a-1*(a+x))/(a+x) by XCMPLX_1:127,A11
      .=(a-x)/(a+x);
      hence thesis;
    end;
    hence thesis by A5,A7,A1,FDIFF_1:27;
  end;

:: 6 f.x=x-(2*a)*ln(x+a)
theorem
  Z c= dom (id Z - (2*a)(#)f) & f = (log_ number_e)*f1 &
  (for x st x in Z holds f1.x=x+a & f1.x>0) implies
  (id Z - (2*a)(#)f) is_differentiable_on Z &
  for x st x in Z holds ((id Z -(2*a)(#)f)`|Z).x = (x-a)/(x+a)
  proof
    assume A1:Z c= dom (id Z - (2*a)(#)f) & f = (log_ number_e)*f1 &
    (for x st x in Z holds f1.x=x+a & f1.x>0);
    then Z c= dom id Z /\ dom ((2*a)(#)f) by SEQ_1:def 4;
    then A2:Z c= dom id Z & Z c= dom ((2*a)(#)f) by XBOOLE_1:18;
    then A3:Z c= dom ((log_ number_e)*f1) by A1,SEQ_1:def 6;
A4: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;
    then A5:id Z is_differentiable_on Z & for x st x in Z
    holds ((id Z)`|Z).x = 1 by FDIFF_1:31,A2;
A6: for x st x in Z holds f1.x=a+x & f1.x>0 by A1;
    then A7:f is_differentiable_on Z & for x st x in Z
    holds (f`|Z).x = 1/(x+a) by A1,A3,Th1;
    then A8:(2*a)(#)f is_differentiable_on Z & for x st x in Z holds
    (((2*a)(#)f)`|Z).x =(2*a)*diff(f,x) by A2,FDIFF_1:28;
A9: for x st x in Z holds (((2*a)(#)f)`|Z).x =(2*a)/(x+a)
    proof
      let x;
      assume A10:x in Z;
      hence (((2*a)(#)f)`|Z).x =(2*a)*diff(f,x) by A2,FDIFF_1:28,A7
      .=(2*a)*(f`|Z).x by A10,FDIFF_1:def 8,A7
      .=(2*a)*(1/(x+a)) by A1,A3,Th1,A6,A10
      .=(2*a)/(x+a) by XCMPLX_1:100;
    end;
    for x st x in Z holds ((id Z - (2*a)(#)f)`|Z).x = (x-a)/(x+a)
    proof
      let x;
      assume A11:x in Z;
      then A12:f1.x=x+a & f1.x>0 by A1;
      ((id Z - (2*a)(#)f)`|Z).x = diff(id Z,x) - diff((2*a)(#)f,x)
      by A5,A8,A1,FDIFF_1:27,A11
      .=((id Z)`|Z).x - diff((2*a)(#)f,x) by A5,A11,FDIFF_1:def 8
      .=((id Z)`|Z).x - (((2*a)(#)f)`|Z).x by A11,A8,FDIFF_1:def 8
      .=1 - (((2*a)(#)f)`|Z).x by FDIFF_1:31,A2,A4,A11
      .=1 - (2*a)/(x+a) by A9,A11
      .=(1*(x+a) - (2*a))/(x+a) by XCMPLX_1:128,A12
      .=(x-a)/(x+a);
      hence thesis;
    end;
    hence thesis by A5,A8,A1,FDIFF_1:27;
  end;

:: 7 f.x=x + (2*a)*ln(x-a)
theorem
  Z c= dom (id Z + (2*a)(#)f) & f = (log_ number_e)*f1 &
  (for x st x in Z holds f1.x=x-a & f1.x>0) implies
  (id Z + (2*a)(#)f) is_differentiable_on Z &
  for x st x in Z holds ((id Z +(2*a)(#)f)`|Z).x = (x+a)/(x-a)
  proof
    assume A1:Z c= dom (id Z + (2*a)(#)f) & f = (log_ number_e)*f1 &
    (for x st x in Z holds f1.x=x-a & f1.x>0);
    then Z c= dom id Z /\ dom ((2*a)(#)f) by SEQ_1:def 3;
    then A2:Z c= dom id Z & Z c= dom ((2*a)(#)f) by XBOOLE_1:18;
    then A3:Z c= dom ((log_ number_e)*f1) by A1,SEQ_1:def 6;
A4: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;
    then A5:id Z is_differentiable_on Z & for x st x in Z
    holds ((id Z)`|Z).x = 1 by FDIFF_1:31,A2;
A6: f is_differentiable_on Z & for x st x in Z
    holds (f`|Z).x = 1/(x-a) by A1,A3,Th2;
    then A7:(2*a)(#)f is_differentiable_on Z & for x st x in Z holds
    (((2*a)(#)f)`|Z).x =(2*a)*diff(f,x) by A2,FDIFF_1:28;
A8: for x st x in Z holds (((2*a)(#)f)`|Z).x =(2*a)/(x-a)
    proof
      let x;
      assume A9:x in Z;
      hence (((2*a)(#)f)`|Z).x =(2*a)*diff(f,x) by A6,A2,FDIFF_1:28
      .=(2*a)*(f`|Z).x by A9,FDIFF_1:def 8,A6
      .=(2*a)*(1/(x-a)) by A1,A3,Th2,A9
      .=(2*a)/(x-a) by XCMPLX_1:100;
    end;
    for x st x in Z holds ((id Z + (2*a)(#)f)`|Z).x = (x+a)/(x-a)
    proof
      let x;
      assume A10:x in Z;
      then A11:f1.x=x-a & f1.x>0 by A1;
      ((id Z + (2*a)(#)f)`|Z).x = diff(id Z,x) + diff((2*a)(#)f,x)
      by A5,A7,A1,FDIFF_1:26,A10
      .=((id Z)`|Z).x + diff((2*a)(#)f,x) by A5,A10,FDIFF_1:def 8
      .=((id Z)`|Z).x + (((2*a)(#)f)`|Z).x by A10,A7,FDIFF_1:def 8
      .=1 + (((2*a)(#)f)`|Z).x by FDIFF_1:31,A2,A4,A10
      .=1 + (2*a)/(x-a) by A8,A10
      .=(1*(x-a) + (2*a))/(x-a) by XCMPLX_1:114,A11
      .=(x+a)/(x-a);
      hence thesis;
    end;
    hence thesis by A5,A7,A1,FDIFF_1:26;
  end;

:: 8 f.x=x+ (a-b)*ln(x+b)
theorem
  Z c= dom (id Z + (a-b)(#)f) & f = (log_ number_e)*f1 &
  (for x st x in Z holds f1.x=x+b & f1.x>0) implies
  (id Z + (a-b)(#)f) is_differentiable_on Z &
  for x st x in Z holds ((id Z +(a-b)(#)f)`|Z).x = (x+a)/(x+b)
  proof
    assume A1:Z c= dom (id Z + (a-b)(#)f) & f = (log_ number_e)*f1 &
    (for x st x in Z holds f1.x=x+b & f1.x>0);
    then Z c= dom id Z /\ dom ((a-b)(#)f) by SEQ_1:def 3;
    then A2:Z c= dom id Z & Z c= dom ((a-b)(#)f) by XBOOLE_1:18;
    then A3:Z c= dom ((log_ number_e)*f1) by A1,SEQ_1:def 6;
A4: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;
    then A5:id Z is_differentiable_on Z & for x st x in Z
    holds ((id Z)`|Z).x = 1 by FDIFF_1:31,A2;
A6: for x st x in Z holds f1.x=b+x & f1.x>0 by A1;
    then A7:f is_differentiable_on Z & for x st x in Z
    holds (f`|Z).x = 1/(x+b) by A1,A3,Th1;
    then A8:(a-b)(#)f is_differentiable_on Z & for x st x in Z holds
    (((a-b)(#)f)`|Z).x =(a-b)*diff(f,x) by A2,FDIFF_1:28;
A9: for x st x in Z holds (((a-b)(#)f)`|Z).x =(a-b)/(x+b)
    proof
      let x;
      assume A10:x in Z;
      hence (((a-b)(#)f)`|Z).x =(a-b)*diff(f,x) by A7,A2,FDIFF_1:28
      .=(a-b)*(f`|Z).x by A10,FDIFF_1:def 8,A7
      .=(a-b)*(1/(x+b)) by A1,A3,Th1,A6,A10
      .=(a-b)/(x+b) by XCMPLX_1:100;
    end;
    for x st x in Z holds ((id Z + (a-b)(#)f)`|Z).x = (x+a)/(x+b)
    proof
      let x;
      assume A11:x in Z;
      then A12:f1.x=x+b & f1.x>0 by A1;
      ((id Z + (a-b)(#)f)`|Z).x = diff(id Z,x) + diff((a-b)(#)f,x)
      by A5,A8,A1,FDIFF_1:26,A11
      .=((id Z)`|Z).x + diff((a-b)(#)f,x) by A5,A11,FDIFF_1:def 8
      .=((id Z)`|Z).x + (((a-b)(#)f)`|Z).x by A11,A8,FDIFF_1:def 8
      .=1 + (((a-b)(#)f)`|Z).x by FDIFF_1:31,A2,A4,A11
      .=1 + (a-b)/(x+b) by A9,A11
      .=(1*(x+b) + (a-b))/(x+b) by XCMPLX_1:114,A12
      .=(x+a)/(x+b);
      hence thesis;
    end;
    hence thesis by A5,A8,A1,FDIFF_1:26;
  end;

:: 9 f.x=x+ (a+b)*ln(x-b)
theorem
  Z c= dom (id Z + (a+b)(#)f) & f = (log_ number_e)*f1 &
  (for x st x in Z holds f1.x=x-b & f1.x>0) implies
  (id Z + (a+b)(#)f) is_differentiable_on Z &
  for x st x in Z holds ((id Z +(a+b)(#)f)`|Z).x = (x+a)/(x-b)
  proof
    assume A1:Z c= dom (id Z + (a+b)(#)f) & f = (log_ number_e)*f1 &
    (for x st x in Z holds f1.x=x-b & f1.x>0);
    then Z c= dom id Z /\ dom ((a+b)(#)f) by SEQ_1:def 3;
    then A2:Z c= dom id Z & Z c= dom ((a+b)(#)f) by XBOOLE_1:18;
    then A3:Z c= dom ((log_ number_e)*f1) by A1,SEQ_1:def 6;
A4: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;
    then A5:id Z is_differentiable_on Z & for x st x in Z
    holds ((id Z)`|Z).x = 1 by FDIFF_1:31,A2;
A6: f is_differentiable_on Z & for x st x in Z
    holds (f`|Z).x = 1/(x-b) by A3,Th2,A1;
    then A7:(a+b)(#)f is_differentiable_on Z & for x st x in Z holds
    (((a+b)(#)f)`|Z).x =(a+b)*diff(f,x) by A2,FDIFF_1:28;
A8: for x st x in Z holds (((a+b)(#)f)`|Z).x =(a+b)/(x-b)
    proof
      let x;
      assume A9:x in Z;
      hence (((a+b)(#)f)`|Z).x =(a+b)*diff(f,x) by A6,A2,FDIFF_1:28
      .=(a+b)*(f`|Z).x by A9,FDIFF_1:def 8,A6
      .=(a+b)*(1/(x-b)) by A3,Th2,A1,A9
      .=(a+b)/(x-b) by XCMPLX_1:100;
    end;
    for x st x in Z holds ((id Z + (a+b)(#)f)`|Z).x = (x+a)/(x-b)
    proof
      let x;
      assume A10:x in Z;
      then A11:f1.x=x-b & f1.x>0 by A1;
      ((id Z + (a+b)(#)f)`|Z).x = diff(id Z,x) + diff((a+b)(#)f,x)
      by A5,A7,A1,FDIFF_1:26,A10
      .=((id Z)`|Z).x + diff((a+b)(#)f,x) by A5,A10,FDIFF_1:def 8
      .=((id Z)`|Z).x + (((a+b)(#)f)`|Z).x by A10,A7,FDIFF_1:def 8
      .=1 + (((a+b)(#)f)`|Z).x by FDIFF_1:31,A2,A4,A10
      .=1 + (a+b)/(x-b) by A8,A10
      .=(1*(x-b) + (a+b))/(x-b) by XCMPLX_1:114,A11
      .=(x+a)/(x-b);
      hence thesis;
    end;
    hence thesis by A5,A7,A1,FDIFF_1:26;
  end;

:: 10 f.x=x- (a+b)*ln(x+b)
theorem
  Z c= dom (id Z - (a+b)(#)f) & f = (log_ number_e)*f1 &
  (for x st x in Z holds f1.x=x+b & f1.x>0) implies
  (id Z - (a+b)(#)f) is_differentiable_on Z &
  for x st x in Z holds ((id Z -(a+b)(#)f)`|Z).x = (x-a)/(x+b)
  proof
    assume A1:Z c= dom (id Z - (a+b)(#)f) & f = (log_ number_e)*f1 &
    (for x st x in Z holds f1.x=x+b & f1.x>0);
    then Z c= dom id Z /\ dom ((a+b)(#)f) by SEQ_1:def 4;
    then A2:Z c= dom id Z & Z c= dom ((a+b)(#)f) by XBOOLE_1:18;
    then A3:Z c= dom ((log_ number_e)*f1) by A1,SEQ_1:def 6;
A4: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;
    then A5:id Z is_differentiable_on Z & for x st x in Z
    holds ((id Z)`|Z).x = 1 by FDIFF_1:31,A2;
A6: for x st x in Z holds f1.x=b+x & f1.x>0 by A1;
    then A7:f is_differentiable_on Z & for x st x in Z
    holds (f`|Z).x = 1/(x+b) by A1,A3,Th1;
    then A8:(a+b)(#)f is_differentiable_on Z & for x st x in Z holds
    (((a+b)(#)f)`|Z).x =(a+b)*diff(f,x) by A2,FDIFF_1:28;
A9: for x st x in Z holds (((a+b)(#)f)`|Z).x =(a+b)/(x+b)
    proof
      let x;
      assume A10:x in Z;
      hence (((a+b)(#)f)`|Z).x=(a+b)*diff(f,x) by A7,A2,FDIFF_1:28
      .=(a+b)*(f`|Z).x by A10,FDIFF_1:def 8,A7
      .=(a+b)*(1/(x+b)) by A1,A3,Th1,A6,A10
      .=(a+b)/(x+b) by XCMPLX_1:100;
    end;
    for x st x in Z holds ((id Z - (a+b)(#)f)`|Z).x = (x-a)/(x+b)
    proof
      let x;
      assume A11:x in Z;
      then A12:f1.x=x+b & f1.x>0 by A1;
      ((id Z - (a+b)(#)f)`|Z).x = diff(id Z,x) - diff((a+b)(#)f,x)
      by A5,A8,A1,FDIFF_1:27,A11
      .=((id Z)`|Z).x - diff((a+b)(#)f,x) by A5,A11,FDIFF_1:def 8
      .=((id Z)`|Z).x - (((a+b)(#)f)`|Z).x by A11,A8,FDIFF_1:def 8
      .=1 - (((a+b)(#)f)`|Z).x by FDIFF_1:31,A2,A4,A11
      .=1 - (a+b)/(x+b) by A9,A11
      .=(1*(x+b) - (a+b))/(x+b) by XCMPLX_1:128,A12
      .=(x-a)/(x+b);
      hence thesis;
    end;
    hence thesis by A5,A8,A1,FDIFF_1:27;
  end;

:: 11 f.x=x+ (b-a)*ln(x-b)
theorem
  Z c= dom (id Z + (b-a)(#)f) & f = (log_ number_e)*f1 &
  (for x st x in Z holds f1.x=x-b & f1.x>0) implies
  (id Z + (b-a)(#)f) is_differentiable_on Z &
  for x st x in Z holds ((id Z +(b-a)(#)f)`|Z).x = (x-a)/(x-b)
  proof
    assume A1:Z c= dom (id Z + (b-a)(#)f) & f = (log_ number_e)*f1 &
    (for x st x in Z holds f1.x=x-b & f1.x>0);
    then Z c= dom id Z /\ dom ((b-a)(#)f) by SEQ_1:def 3;
    then A2:Z c= dom id Z & Z c= dom ((b-a)(#)f) by XBOOLE_1:18;
    then A3:Z c= dom ((log_ number_e)*f1) by A1,SEQ_1:def 6;
A4: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;
    then A5:id Z is_differentiable_on Z & for x st x in Z
    holds ((id Z)`|Z).x = 1 by FDIFF_1:31,A2;
A6: f is_differentiable_on Z & for x st x in Z
    holds (f`|Z).x = 1/(x-b) by A1,A3,Th2;
    then A7:(b-a)(#)f is_differentiable_on Z & for x st x in Z holds
    (((b-a)(#)f)`|Z).x =(b-a)*diff(f,x) by A2,FDIFF_1:28;
A8: for x st x in Z holds (((b-a)(#)f)`|Z).x =(b-a)/(x-b)
    proof
      let x;
      assume A9:x in Z;
      hence (((b-a)(#)f)`|Z).x =(b-a)*diff(f,x) by A6,A2,FDIFF_1:28
      .=(b-a)*(f`|Z).x by A9,FDIFF_1:def 8,A6
      .=(b-a)*(1/(x-b)) by A1,A3,Th2,A9
      .=(b-a)/(x-b) by XCMPLX_1:100;
    end;
    for x st x in Z holds ((id Z + (b-a)(#)f)`|Z).x = (x-a)/(x-b)
    proof
      let x;
      assume A10:x in Z;
      then A11:f1.x=x-b & f1.x>0 by A1;
      ((id Z + (b-a)(#)f)`|Z).x = diff(id Z,x) + diff((b-a)(#)f,x)
      by A5,A7,A1,FDIFF_1:26,A10
      .=((id Z)`|Z).x + diff((b-a)(#)f,x) by A5,A10,FDIFF_1:def 8
      .=((id Z)`|Z).x + (((b-a)(#)f)`|Z).x by A10,A7,FDIFF_1:def 8
      .=1 + (((b-a)(#)f)`|Z).x by FDIFF_1:31,A2,A4,A10
      .=1 + (b-a)/(x-b) by A8,A10
      .=(1*(x-b) + (b-a))/(x-b) by XCMPLX_1:114,A11
      .=(x-a)/(x-b);
      hence thesis;
    end;
    hence thesis by A5,A7,A1,FDIFF_1:26;
  end;

:: 12 f.x=a+b*x+c*x^|2
theorem Th12:
  Z c= dom (f1+c(#)f2) & (for x st x in Z holds f1.x=a+b*x) & f2=#Z 2
  implies f1+c(#)f2 is_differentiable_on Z &
  for x st x in Z holds ((f1+c(#)f2)`|Z).x = b+2*c*x
  proof
    assume A1:Z c= dom (f1+c(#)f2) & (for x st x in Z holds f1.x=a+b*x)
    & f2=#Z 2;
    then Z c= dom f1 /\ dom (c(#)f2) by SEQ_1:def 3;
    then A2:Z c= dom f1 & Z c= dom (c(#)f2) by XBOOLE_1:18;
    then A3:Z c= dom f2 by SEQ_1:def 6;
A4: for x st x in Z holds f1.x=b*x+a by A1;
    then A5:f1 is_differentiable_on Z & for x st x in Z
    holds (f1`|Z).x = b by FDIFF_1:31,A2;
A6: f2 is_differentiable_on Z
    proof
      for x st x in Z holds f2 is_differentiable_in x by A1,TAYLOR_1:2;
      hence f2 is_differentiable_on Z by A3,FDIFF_1:16;
    end;
    then A7:(c(#)f2) is_differentiable_on Z by FDIFF_1:28,A2;
A8: for x st x in Z holds (f2`|Z).x=2*x
     proof
       let x;
       assume A9:x in Z;
       2 * (x #Z (2-1))=2 * x by PREPOWER:45;
       then diff(f2,x) =2 * x by TAYLOR_1:2,A1;
       hence (f2`|Z).x =2 * x by A6,A9,FDIFF_1:def 8;
     end;
A10: for x st x in Z holds ((c(#)f2)`|Z).x=2*c*x
     proof
       let x;
       assume A11:x in Z;
       hence ((c(#)f2)`|Z).x=c*diff(f2,x) by A2,A6,FDIFF_1:28
       .=c*(f2`|Z).x by A11,A6,FDIFF_1:def 8
       .=c*(2*x) by A8,A11
       .=2*c*x;
     end;
     for x st x in Z holds ((f1+c(#)f2)`|Z).x = b+2*c*x
     proof
       let x;
       assume A12:x in Z;
       ((f1+c(#)f2)`|Z).x =diff(f1,x) + diff(c(#)f2,x)
       by FDIFF_1:26,A1,A7,A5,A12
       .=(f1`|Z).x+ diff((c(#)f2),x) by A12,A5,FDIFF_1:def 8
       .=(f1`|Z).x+((c(#)f2)`|Z).x by A12,A7,FDIFF_1:def 8
       .=b+((c(#)f2)`|Z).x by FDIFF_1:31,A2,A4,A12
       .=b+2*c*x by A10,A12;
       hence thesis;
     end;
     hence thesis by A7,A5,A1,FDIFF_1:26;
   end;

:: 13 f.x=ln(a+b*x+c*x |^2)
theorem Th13:
  Z c= dom ((log_ number_e)*(f1+c(#)f2)) & f2=#Z 2 &
  (for x st x in Z holds f1.x=a+b*x & (f1+c(#)f2).x >0) implies
  ((log_ number_e)*(f1+c(#)f2)) is_differentiable_on Z & for x st x in Z holds
  (((log_ number_e)*(f1+c(#)f2))`|Z).x = (b+2*c*x)/(a+b*x+c*x |^2)
  proof
    assume A1: Z c= dom ((log_ number_e)*(f1+c(#)f2)) & f2=#Z 2 &
    (for x st x in Z holds f1.x=a+b*x & (f1+c(#)f2).x >0);
    for y st y in Z holds y in dom (f1+c(#)f2) by FUNCT_1:21,A1;
    then A2:Z c= dom (f1+c(#)f2) by TARSKI:def 3;
    then Z c= dom f1 /\ dom (c(#)f2) by SEQ_1:def 3;
    then A3: Z c= dom f1 & Z c= dom (c(#)f2) by XBOOLE_1:18;
A4: (for x st x in Z holds f1.x=a+b*x) & f2=#Z 2 by A1;
    then A5:(f1+c(#)f2) is_differentiable_on Z &
    for x st x in Z holds ((f1+c(#)f2)`|Z).x = b+2*c*x by Th12,A2;
A6: for x st x in Z holds (log_ number_e)*(f1+c(#)f2) is_differentiable_in x
    proof
      let x;
      assume A7:x in Z;
      then A8:(f1+c(#)f2) is_differentiable_in x by FDIFF_1:16,A5;
      (f1+c(#)f2).x >0 by A1,A7;
      hence (log_ number_e)*(f1+c(#)f2) is_differentiable_in x
      by TAYLOR_1:20,A8;
    end;
    then A9:(log_ number_e)*(f1+c(#)f2) is_differentiable_on Z by A1,FDIFF_1:16
;
    for x st x in Z holds
    (((log_ number_e)*(f1+c(#)f2))`|Z).x = (b+2*c*x)/(a+b*x+c*x |^2)
    proof
      let x;
      assume A10:x in Z;
      then A11: x in dom (f1+c(#)f2) by FUNCT_1:21,A1;
A12:   (f1+c(#)f2) is_differentiable_in x by FDIFF_1:16,A5,A10;
A13:   f1.x=a+b*x & ( f1+c(#)f2).x >0 by A1,A10;
A14:  ( f1+c(#)f2).x=f1.x + (c(#)f2).x by SEQ_1:def 3,A11
      .=f1.x +c*f2.x by A3,A10, SEQ_1:def 6
      .=a+b*x +c*(f2.x) by A1,A10
      .=a+b*x +c*(x #Z 2) by A1,TAYLOR_1:def 1
      .=a+b*x +c*(x |^2) by PREPOWER:46;
      diff((log_ number_e)*( f1+c(#)f2),x)
      =diff(( f1+c(#)f2),x)/(( f1+c(#)f2).x) by TAYLOR_1:20,A13,A12
      .=(( f1+c(#)f2)`|Z).x/(( f1+c(#)f2).x) by A5,A10,FDIFF_1:def 8
      .=(b+2*c*x )/(a+b*x+c*(x |^2)) by A14,Th12,A2,A4,A10;
      hence (((log_ number_e)*( f1+c(#)f2))`|Z).x = (b+2*c*x)/(a+b*x+c*x |^2)
      by A10,A9,FDIFF_1:def 8;
    end;
    hence thesis by A1,FDIFF_1:16,A6;
  end;

:: 14 f.x=1/(a+x)
theorem Th14:
  Z c= dom f & (for x st x in Z holds f.x = a+x & f.x<>0) implies
  f^ is_differentiable_on Z & for x st x in Z holds
  ( (f^)`|Z).x= -1/(a+x)^2
  proof
    assume that A1: Z c= dom f and
A2: for x st x in Z holds f.x = a+x & f.x<>0;
A3: for x st x in Z holds f.x = 1*x+a by A2;
    then A4:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = 1 by FDIFF_1:31,A1;
A5: for x st x in Z holds f.x<>0 by A2;
    then A6:f^ is_differentiable_on Z by A4,FDIFF_2:22;
    now let x;
      assume A7:x in Z;
A8:  f.x<>0 by A2,A7;
A9:   f is_differentiable_in x by A7,A4,FDIFF_1:16;
      ( (f^)`|Z).x= diff(f^,x) by A7,A6,FDIFF_1:def 8
      .= - diff(f,x)/( f.x)^2 by A8,FDIFF_2:15,A9
      .=- (f`|Z).x/( f.x)^2 by A7,A4,FDIFF_1:def 8
      .=-1/( f.x)^2 by FDIFF_1:31,A1,A3,A7
      .=-1/(a+x)^2 by A2,A7;
      hence ( (f^)`|Z).x=-1/(a+x)^2;
    end;
    hence thesis by A4,FDIFF_2:22,A5;
  end;

:: 15 f.x=-1/(a+x)
theorem
  Z c= dom ((-1)(#)(f^)) & (for x st x in Z holds f.x=a+x & f.x <>0)
  implies (-1)(#)(f^) is_differentiable_on Z &
  for x st x in Z holds (((-1)(#)(f^))`|Z).x = 1/(a+x)^2
  proof
    assume that A1:Z c= dom ((-1)(#)(f^)) and
A2: for x st x in Z holds f.x=a+x & f.x <>0;
A3: Z c= dom (f^) by SEQ_1:def 6,A1;
    dom (f^) c= dom f by RFUNCT_1:11;
    then A4:Z c= dom f by XBOOLE_1:1,A3;
    then A5:f^ is_differentiable_on Z & for x st x in Z holds
    ((f^)`|Z).x= -1/(a+x)^2 by A2,Th14;
    now let x;
      assume A6:x in Z;
      hence (((-1)(#)(f^))`|Z).x =(-1)*diff((f^),x) by A5,A1,FDIFF_1:28
      .=(-1)*((f^)`|Z).x by A6,A5,FDIFF_1:def 8
      .=(-1)*(-1/(a+x)^2) by A6,A2,Th14,A4
      .=1/(a+x)^2;
    end;
    hence thesis by A5,A1,FDIFF_1:28;
  end;

:: 16 f.x=1/(a-x)
theorem
  Z c= dom f & (for x st x in Z holds f.x = a-x & f.x<>0) implies
  f^ is_differentiable_on Z & for x st x in Z holds
  ( (f^)`|Z).x= 1/(a-x)^2
  proof
    assume that A1: Z c= dom f and
A2: for x st x in Z holds f.x = a-x & f.x<>0;
A3: for x st x in Z holds f.x =(-1)*x+a
    proof
      let x;
      assume x in Z;
      then f.x =a-x by A2;
      hence thesis;
    end;
    then A4:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = -1 by FDIFF_1:31,A1;
A5: for x st x in Z holds f.x<>0 by A2;
    then A6:f^ is_differentiable_on Z by A4,FDIFF_2:22;
    now let x;
      assume A7:x in Z;
A8:   f.x<>0 by A2,A7;
A9:   f is_differentiable_in x by A7,A4,FDIFF_1:16;
      ( (f^)`|Z).x= diff(f^,x) by A7,A6,FDIFF_1:def 8
      .= - diff(f,x)/( f.x)^2 by A8,FDIFF_2:15,A9
      .=- (f`|Z).x/( f.x)^2 by A7,A4,FDIFF_1:def 8
      .=-(-1)/( f.x)^2 by FDIFF_1:31,A1,A3,A7
      .=-(-1/( f.x)^2) by XCMPLX_1:188
      .=1/(a-x)^2 by A2,A7;
      hence ( (f^)`|Z).x=1/(a-x)^2;
    end;
    hence thesis by A4,FDIFF_2:22,A5;
  end;

:: 17 f.x=a^2+x^|2
theorem Th17:
  Z c= dom (f1+f2) & (for x st x in Z holds f1.x=a^2) & f2=#Z 2 implies
  f1+f2 is_differentiable_on Z &
  for x st x in Z holds ((f1+f2)`|Z).x = 2*x
  proof
    assume that A1:Z c= dom (f1+f2) and
A2: (for x st x in Z holds f1.x=a^2) and
A3: f2=#Z 2;
A4: Z c= dom (f1+1(#)f2) by A1,RFUNCT_1:33;
A5: for x st x in Z holds f1.x=a^2 + 0*x by A2;
    then A6:f1+1(#)f2 is_differentiable_on Z &
    for x st x in Z holds ((f1+1(#)f2)`|Z).x = 0+2*1*x by Th12,A3,A4;
    for x st x in Z holds ((f1+f2)`|Z).x = 2*x
    proof
      let x;
      assume A7:x in Z;
      ((f1+f2)`|Z).x=((f1+1(#)f2)`|Z).x by RFUNCT_1:33
      .= 0+2*1*x by Th12,A3,A4,A5,A7
      .= 2*x;
      hence thesis;
    end;
    hence thesis by A6,RFUNCT_1:33;
  end;

:: 18 f.x=ln(a^2+x|^2)
theorem
  Z c= dom ((log_ number_e)*(f1+f2)) & f2=#Z 2 &
  (for x st x in Z holds f1.x=a^2 & (f1+f2).x >0) implies
  ((log_ number_e)*(f1+f2)) is_differentiable_on Z & for x st x in Z holds
  (((log_ number_e)*(f1+f2))`|Z).x = (2*x)/(a^2+x|^2)
  proof
    assume that A1: Z c= dom ((log_ number_e)*(f1+f2)) and A2:f2=#Z 2 and
A3: (for x st x in Z holds f1.x=a^2 & (f1+f2).x >0);
A4: f2=1(#)f2 by RFUNCT_1:33;
A5: Z c= dom ((log_ number_e)*(f1+1(#)f2)) by A1,RFUNCT_1:33;
A6: for x st x in Z holds f1.x=a^2+0*x & (f1+1(#)f2).x >0 by A4,A3;
    then A7:((log_ number_e)*(f1+1(#)f2)) is_differentiable_on Z &
    for x st x in Z holds
    (((log_ number_e)*(f1+1(#)f2))`|Z).x = (0+2*1*x)/(a^2+0*x+1*x |^2)
    by Th13,A5,A2;
    for x st x in Z holds
    (((log_ number_e)*(f1+f2))`|Z).x = (2*x)/(a^2+x |^2)
    proof
      let x;
      assume A8:x in Z;
      (((log_ number_e)*(f1+f2))`|Z).x
      =(((log_ number_e)*(f1+1(#)f2))`|Z).x by RFUNCT_1:33
      .= (0+2*1*x)/(a^2+0*x+1*x |^2) by Th13,A5,A2,A6,A8
      .=(2*x)/(a^2+x |^2);
      hence thesis;
    end;
    hence thesis by A7,RFUNCT_1:33;
  end;

:: 19 f.x=-ln(a^2-x|^2)
theorem
  Z c= dom -((log_ number_e)*(f1-f2)) & f2=#Z 2 &
  (for x st x in Z holds f1.x=a^2 & (f1-f2).x >0) implies
  -((log_ number_e)*(f1-f2)) is_differentiable_on Z &
  for x st x in Z holds
  ((-((log_ number_e)*(f1-f2)))`|Z).x = 2*x/(a^2-x |^2)
  proof
    assume that A1:Z c= dom -((log_ number_e)*(f1-f2)) and A2:f2=#Z 2 and
A3: for x st x in Z holds f1.x=a^2 & (f1-f2).x >0;
A4: f1+(-1)(#)f2=f1+-f2 by RFUNCT_1:38
    .=f1-f2 by RFUNCT_1:40;
    then A5:Z c= dom ((log_ number_e)*(f1+(-1)(#)f2)) by A1,SEQ_1:def 7;
A6: for x st x in Z holds f1.x=a^2+0*x & ( f1+(-1)(#)f2).x >0 by A3,A4;
A7: Z c= dom ((-1)(#)((log_ number_e)*(f1+(-1)(#)f2)))
    by A1,A4,RFUNCT_1:38;
A8: (log_ number_e)*(f1+(-1)(#)f2) is_differentiable_on Z & for x st x in Z
    holds (((log_ number_e)*(f1+(-1)(#)f2))`|Z).x
    = (0+2*(-1)*x)/(a^2+0*x+(-1)*x |^2) by Th13,A5,A6,A2;
    then A9:(-1)(#)((log_ number_e)*(f1+(-1)(#)f2)) is_differentiable_on Z
    & for x st x in Z holds (((-1)(#)((log_ number_e)*(f1+(-1)(#)f2)))`|Z).x
    =(-1)*diff(((log_ number_e)*(f1+(-1)(#)f2)),x) by FDIFF_1:28,A7;
    for x st x in Z holds ((-((log_ number_e)*(f1-f2)))`|Z).x =2*x/(a^2-x |^2)
    proof
      let x;
      assume A10:x in Z;
      ((-((log_ number_e)*(f1-f2)))`|Z).x
      =(((-1)(#)((log_ number_e)*(f1+(-1)(#)f2)))`|Z).x by A4,RFUNCT_1:38
      .=(-1)*diff(((log_ number_e)*(f1+(-1)(#)f2)),x) by A10,FDIFF_1:28,A7,A8
      .=(-1)*(((log_ number_e)*(f1+(-1)(#)f2))`|Z).x by A10,A8,FDIFF_1:def 8
      .=(-1)*((0+2*(-1)*x)/(a^2+0*x+(-1)*x |^2)) by Th13,A5,A6,A2,A10
      .=(-1)*(2*(-1)*x)/(a^2+(-1)*x |^2) by XCMPLX_1:75
      .=2*x/(a^2-x |^2);
      hence thesis;
    end;
    hence thesis by A4,RFUNCT_1:38,A9;
  end;

:: 20 f.x=a+x|^3
theorem Th20:
  Z c= dom (f1+f2) & (for x st x in Z holds f1.x=a) & f2=#Z 3
  implies f1+f2 is_differentiable_on Z &
  for x st x in Z holds ((f1+f2)`|Z).x = 3*x |^2
  proof
    assume A1:Z c= dom (f1+f2) & (for x st x in Z holds f1.x=a) & f2=#Z 3;
    then Z c= dom f1 /\ dom f2 by SEQ_1:def 3;
    then A2:Z c= dom f1 & Z c= dom f2 by XBOOLE_1:18;
A3: for x st x in Z holds f1.x = 0*x+a by A1;
    then A4:f1 is_differentiable_on Z &
    for x st x in Z holds (f1`|Z).x = 0 by FDIFF_1:31,A2;
    for x st x in Z holds f2 is_differentiable_in x by A1,TAYLOR_1:2;
    then A5:f2 is_differentiable_on Z by FDIFF_1:16,A2;
A6: for x st x in Z holds (f2`|Z).x = 3 * x #Z (3-1)
    proof
      let x;
      assume A7:x in Z;
      diff(f2,x) = 3 * x #Z (3-1) by A1,TAYLOR_1:2;
      hence (f2`|Z).x =3 * x #Z (3-1) by A7,A5,FDIFF_1:def 8;
    end;
    for x st x in Z holds ((f1+f2)`|Z).x = 3*x|^2
    proof
      let x;
      assume A8:x in Z;
      then ((f1+f2)`|Z).x=diff(f1,x)+diff(f2,x) by A4,A5,FDIFF_1:26,A1;
      hence ((f1+f2)`|Z).x=(f1`|Z).x +diff(f2,x) by A4,A8,FDIFF_1:def 8
      .=(f1`|Z).x +(f2`|Z).x by A5,A8,FDIFF_1:def 8
      .=0+(f2`|Z).x by FDIFF_1:31,A2,A3,A8
      .=3 * x #Z (3-1) by A6,A8
      .=3*x |^2 by PREPOWER:46;
    end;
    hence thesis by A4,A5,FDIFF_1:26,A1;
  end;

:: 21 f.x=ln(a+x|^3)
theorem
  Z c= dom ((log_ number_e)*(f1+f2)) & f2=#Z 3 &
  (for x st x in Z holds f1.x=a & (f1+f2).x >0) implies
  ((log_ number_e)*(f1+f2)) is_differentiable_on Z & for x st x in Z holds
  (((log_ number_e)*(f1+f2))`|Z).x = (3*x |^2)/(a+x |^3)
  proof
    assume that A1: Z c= dom ((log_ number_e)*(f1+f2)) and A2:f2=#Z 3 and
A3: for x st x in Z holds f1.x=a & (f1+f2).x >0;
    for y st y in Z holds y in dom (f1+f2) by FUNCT_1:21,A1;
    then A4:Z c= dom (f1+f2) by TARSKI:def 3;
A5: for x st x in Z holds f1.x=a by A3;
    then A6:f1+f2 is_differentiable_on Z &
    for x st x in Z holds ((f1+f2)`|Z).x = 3*x |^2 by A2,A4,Th20;
A7: for x st x in Z holds (log_ number_e)*(f1+f2) is_differentiable_in x
    proof
      let x;
      assume A8:x in Z;
      then A9:f1+f2 is_differentiable_in x by A6,FDIFF_1:16;
      (f1+f2).x >0 by A8,A3;
      hence (log_ number_e)*(f1+f2) is_differentiable_in x
      by TAYLOR_1:20,A9;
    end;
    then A10:(log_ number_e)*(f1+f2) is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds
    (((log_ number_e)*(f1+f2))`|Z).x = (3*x |^2)/(a+x |^3)
    proof
      let x;
      assume A11:x in Z;
      then A12:f1+f2 is_differentiable_in x & ((f1+f2)`|Z).x = 3*x |^2
      by A6,FDIFF_1:16;
A13:   (f1+f2).x >0 by A11,A3;
A14:  x in dom (f1+f2) by A11,FUNCT_1:21,A1;
      diff((log_ number_e)*(f1+f2),x) =diff((f1+f2),x)/((f1+f2).x)
      by TAYLOR_1:20,A13,A12
      .=((f1+f2)`|Z).x/((f1+f2).x) by A6,A11,FDIFF_1:def 8
      .=(3*x |^2)/((f1+f2).x) by A5,A2,A4,Th20,A11
      .=(3*x |^2)/(f1.x+f2.x) by SEQ_1:def 3,A14
      .=(3*x |^2)/(a+f2.x) by A3,A11
      .=(3*x |^2)/(a+x #Z 3) by A2,TAYLOR_1:def 1
      .=(3*x |^2)/(a+x |^3 ) by PREPOWER:46;
      hence thesis by A10,A11,FDIFF_1:def 8;
    end;
    hence thesis by A1,FDIFF_1:16,A7;
  end;

:: 22 f.x=ln((a+x)/(a-x))
theorem
  Z c= dom ((log_ number_e)*(f1/f2)) &
  (for x st x in Z holds f1.x=a+x & f1.x >0 & f2.x=a-x & f2.x >0) implies
  (log_ number_e)*(f1/f2) is_differentiable_on Z & for x st x in Z holds
  (((log_ number_e)*(f1/f2))`|Z).x = (2*a)/(a^2-x^2)
  proof
    assume A1:Z c= dom ((log_ number_e)*(f1/f2)) & (for x st x in Z holds
    f1.x=a+x & f1.x >0 & f2.x=a-x & f2.x >0);
    for y st y in Z holds y in dom (f1/f2) by FUNCT_1:21,A1;
    then Z c= dom (f1/f2) by TARSKI:def 3;
    then Z c= dom f1 /\ (dom f2 \ f2"{0}) by RFUNCT_1:def 4;
    then A2:Z c= dom f1 & Z c= dom f2 \ f2"{0} by XBOOLE_1:18;
    dom (f2^) c= dom f2 by RFUNCT_1:11;
    then dom f2 \ f2"{0} c= dom f2 by RFUNCT_1:def 8;
    then A3:Z c= dom f2 by XBOOLE_1:1,A2;
A4: for x st x in Z holds f1.x=1*x+a by A1;
    then A5:f1 is_differentiable_on Z & for x st x in Z holds (f1`|Z).x = 1
    by FDIFF_1:31,A2;
A6: for x st x in Z holds f2.x =(-1)*x+a
    proof
      let x;
      assume x in Z;
      then f2.x =a-x by A1;
      hence thesis;
    end;
    then A7:f2 is_differentiable_on Z &
    for x st x in Z holds (f2`|Z).x = -1 by FDIFF_1:31,A3;
    for x st x in Z holds f2.x <> 0 by A1;
    then A8:f1/f2 is_differentiable_on Z by A5,A7,FDIFF_2:21;
A9: for x st x in Z holds ((f1/f2)`|Z).x = (2*a)/(a-x)^2
    proof
      let x;
      assume A10:x in Z;
      then A11:f1 is_differentiable_in x by A5,FDIFF_1:16;
A12:  f2 is_differentiable_in x by A7,A10,FDIFF_1:16;
A13:  f2.x <>0 & f1.x=a+x & f2.x=a-x by A1,A10;
      then diff(f1/f2,x)
      =(diff(f1,x) * f2.x - diff(f2,x) * f1.x)/(f2.x)^2 by FDIFF_2:14,A11,A12
      .=((f1`|Z).x * f2.x-diff(f2,x) * f1.x)/(f2.x)^2 by A5,A10,FDIFF_1:def 8
      .= ((f1`|Z).x * f2.x-(f2`|Z).x * f1.x)/(f2.x)^2 by A7,A10,FDIFF_1:def 8
      .=(1* f2.x-(f2`|Z).x * f1.x)/(f2.x)^2 by A10,FDIFF_1:31,A2,A4
      .=(1* f2.x-(-1)* f1.x)/(f2.x)^2 by A10,FDIFF_1:31,A3,A6
      .=(2*a)/(a-x)^2 by A13;
      hence ((f1/f2)`|Z).x = (2*a)/(a-x)^2 by FDIFF_1:def 8,A8,A10;
    end;
A14: for x st x in Z holds (f1/f2).x >0
    proof
      let x;
      assume A15:x in Z;
      then A16:x in dom (f1/f2) by FUNCT_1:21,A1;
A17:   f1.x >0 & f2.x >0 by A1,A15;
      (f1/f2).x=f1.x * (f2.x)" by A16,RFUNCT_1:def 4
      .=f1.x / f2.x by XCMPLX_0:def 9;
      hence thesis by A17,XREAL_1:141;
    end;
A18: for x st x in Z holds (log_ number_e)*(f1/f2) is_differentiable_in x
    proof
      let x;
      assume A19:x in Z;
      then A20:f1/f2 is_differentiable_in x by FDIFF_1:16,A8;
      (f1/f2).x >0 by A14,A19;
      hence (log_ number_e)*(f1/f2) is_differentiable_in x by TAYLOR_1:20,A20;
    end;
    then A21:(log_ number_e)*(f1/f2) is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds (((log_ number_e)*(f1/f2))`|Z).x = (2*a)/(a^2-x^2)
    proof
      let x;
      assume A22:x in Z;
      then A23:(f1/f2) is_differentiable_in x by FDIFF_1:16,A8;
A24:  f2.x=a-x & f2.x>0 by A1,A22;
A25:  x in dom (f1/f2) by FUNCT_1:21,A1,A22;
A26:  (f1/f2).x >0 & f1.x=a+x & f2.x=a-x by A1,A22,A14;
      diff((log_ number_e)*(f1/f2),x) =diff((f1/f2),x)/((f1/f2).x)
      by TAYLOR_1:20,A26,A23
      .=((f1/f2)`|Z).x/((f1/f2).x) by A8,A22,FDIFF_1:def 8
      .=((2*a)/(a-x)^2)/((f1/f2).x) by A9,A22
      .=((2*a)/(a-x)^2)/(f1.x * (f2.x)") by A25,RFUNCT_1:def 4
      .=((2*a)/(a-x)^2)/((a+x)/(a-x)) by A26,XCMPLX_0:def 9
      .=((2*a)/((a-x)*(a-x)))/((a+x)/(a-x))
      .=((2*a)/(a-x)/(a-x))/((a+x)/(a-x)) by XCMPLX_1:79
      .=((2*a)/(a-x))/((a+x)/(a-x))/(a-x) by XCMPLX_1:48
      .=(2*a)/(a+x)/(a-x) by A24,XCMPLX_1:55
      .=(2*a)/((a+x)*(a-x)) by XCMPLX_1:79
      .=(2*a)/(a^2-x^2);
      hence thesis by A22,A21,FDIFF_1:def 8;
    end;
    hence thesis by A1,FDIFF_1:16,A18;
  end;

:: 23 f.x=ln((x-a)/(x+a))
theorem
  Z c= dom ((log_ number_e)*(f1/f2)) & (for x st x in Z holds
  f1.x=x-a & f1.x >0 & f2.x=x+a & f2.x >0) implies
  (log_ number_e)*(f1/f2) is_differentiable_on Z & for x st x in Z holds
  (((log_ number_e)*(f1/f2))`|Z).x = (2*a)/(x^2-a^2)
  proof
    assume A1:Z c= dom ((log_ number_e)*(f1/f2)) & (for x st x in Z holds
    f1.x=x-a & f1.x >0 & f2.x=x+a & f2.x >0);
    for y st y in Z holds y in dom (f1/f2) by FUNCT_1:21,A1;
    then Z c= dom (f1/f2) by TARSKI:def 3;
    then Z c= dom f1 /\ (dom f2 \ f2"{0}) by RFUNCT_1:def 4;
    then A2:Z c= dom f1 & Z c= dom f2 \ f2"{0} by XBOOLE_1:18;
    dom (f2^) c= dom f2 by RFUNCT_1:11;
    then dom f2 \ f2"{0} c= dom f2 by RFUNCT_1:def 8;
    then A3:Z c= dom f2 by XBOOLE_1:1,A2;
A4: for x st x in Z holds f1.x = 1*x+-a
    proof
      let x;
      assume A5:x in Z;
      1*x+-a=1*x-a;
      hence thesis by A1,A5;
    end;
    then A6:f1 is_differentiable_on Z &
    for x st x in Z holds (f1`|Z).x = 1 by FDIFF_1:31,A2;
A7: for x st x in Z holds f2.x = 1*x+a by A1;
    then A8:f2 is_differentiable_on Z &
    for x st x in Z holds (f2`|Z).x = 1 by FDIFF_1:31,A3;
    for x st x in Z holds f2.x <> 0 by A1;
    then A9:f1/f2 is_differentiable_on Z by A6,A8,FDIFF_2:21;
A10: for x st x in Z holds ((f1/f2)`|Z).x = (2*a)/(x+a)^2
    proof
      let x;
      assume A11:x in Z;
      then A12:f1 is_differentiable_in x by A6,FDIFF_1:16;
A13:  f2 is_differentiable_in x by A8,A11,FDIFF_1:16;
A14:  f2.x <>0 & f1.x=x-a & f2.x=x+a by A1,A11;
      then diff(f1/f2,x)
      =(diff(f1,x) * f2.x - diff(f2,x) * f1.x)/(f2.x)^2 by FDIFF_2:14,A12,A13
      .=((f1`|Z).x * f2.x-diff(f2,x) * f1.x)/(f2.x)^2 by A6,A11,FDIFF_1:def 8
      .= ((f1`|Z).x * f2.x-(f2`|Z).x * f1.x)/(f2.x)^2 by A8,A11,FDIFF_1:def 8
      .=(1* f2.x-(f2`|Z).x * f1.x)/(f2.x)^2 by A11,FDIFF_1:31,A2,A4
      .=(1* f2.x-1* f1.x)/(f2.x)^2 by A11,FDIFF_1:31,A3,A7
      .=(2*a)/(x+a)^2 by A14;
      hence ((f1/f2)`|Z).x = (2*a)/(x+a)^2 by FDIFF_1:def 8,A9,A11;
    end;
A15: for x st x in Z holds (f1/f2).x >0
    proof
      let x;
      assume A16:x in Z;
      then A17:x in dom (f1/f2) by FUNCT_1:21,A1;
A18:   f1.x >0 & f2.x >0 by A1,A16;
      (f1/f2).x=f1.x * (f2.x)" by A17,RFUNCT_1:def 4
      .=f1.x / f2.x by XCMPLX_0:def 9;
      hence thesis by XREAL_1:141,A18;
    end;
A19:  for x st x in Z holds (log_ number_e)*(f1/f2) is_differentiable_in x
    proof
      let x;
      assume A20:x in Z;
      then A21:f1/f2 is_differentiable_in x by FDIFF_1:16,A9;
      (f1/f2).x >0 by A15,A20;
      hence (log_ number_e)*(f1/f2) is_differentiable_in x by TAYLOR_1:20,A21;
    end;
    then A22:(log_ number_e)*(f1/f2) is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds (((log_ number_e)*(f1/f2))`|Z).x = (2*a)/(x^2-a^2)
    proof
      let x;
      assume A23:x in Z;
      then A24:(f1/f2) is_differentiable_in x by FDIFF_1:16,A9;
A25:  f2.x=x+a & f2.x>0 by A1,A23;
A26:  x in dom (f1/f2) by FUNCT_1:21,A1,A23;
A27:  (f1/f2).x >0 & f1.x=x-a & f2.x=x+a by A1,A23,A15;
      diff((log_ number_e)*(f1/f2),x) =diff((f1/f2),x)/((f1/f2).x)
      by TAYLOR_1:20,A27,A24
      .=((f1/f2)`|Z).x/((f1/f2).x) by A9,A23,FDIFF_1:def 8
      .=((2*a)/(x+a)^2)/((f1/f2).x) by A10,A23
      .=((2*a)/(x+a)^2)/(f1.x * (f2.x)") by A26,RFUNCT_1:def 4
      .=((2*a)/(x+a)^2)/((x-a)/(x+a)) by A27,XCMPLX_0:def 9
      .=((2*a)/((x+a)*(x+a)))/((x-a)/(x+a))
      .=((2*a)/(x+a)/(x+a))/((x-a)/(x+a)) by XCMPLX_1:79
      .=((2*a)/(x+a))/((x-a)/(x+a))/(x+a) by XCMPLX_1:48
      .=(2*a)/(x-a)/(x+a) by A25,XCMPLX_1:55
      .=(2*a)/((x-a)*(x+a)) by XCMPLX_1:79
      .=(2*a)/(x^2-a^2);
      hence thesis by A23,A22,FDIFF_1:def 8;
    end;
    hence thesis by A19,A1,FDIFF_1:16;
  end;

:: 24 f.x=ln((x-a)/(x-b))
theorem Th24:
  Z c= dom ((log_ number_e)*(f1/f2)) & (for x st x in Z holds
  f1.x=x-a & f1.x >0 & f2.x=x-b & f2.x >0) implies
  (log_ number_e)*(f1/f2) is_differentiable_on Z & for x st x in Z holds
  (((log_ number_e)*(f1/f2))`|Z).x = (a-b)/((x-a)*(x-b))
  proof
    assume A1:Z c= dom ((log_ number_e)*(f1/f2)) & (for x st x in Z holds
    f1.x=x-a & f1.x >0 & f2.x=x-b & f2.x >0);
    for y st y in Z holds y in dom (f1/f2) by FUNCT_1:21,A1;
    then Z c= dom (f1/f2) by TARSKI:def 3;
    then Z c= dom f1 /\ (dom f2 \ f2"{0}) by RFUNCT_1:def 4;
    then A2:Z c= dom f1 & Z c= dom f2 \ f2"{0} by XBOOLE_1:18;
    dom (f2^) c= dom f2 by RFUNCT_1:11;
    then dom f2 \ f2"{0} c= dom f2 by RFUNCT_1:def 8;
    then A3:Z c= dom f2 by XBOOLE_1:1,A2;
A4: for x st x in Z holds f1.x = 1*x+-a & f2.x = 1*x+-b
    proof
      let x;
      assume A5:x in Z;
      1*x+-a=1*x-a & 1*x+-b=1*x-b;
      hence thesis by A1,A5;
    end;
    then A6:for x st x in Z holds f1.x = 1*x+-a;
    then A7:f1 is_differentiable_on Z & for x st x in Z holds
    (f1`|Z).x = 1 by FDIFF_1:31,A2;
A8: for x st x in Z holds f2.x = 1*x+-b by A4;
    then A9: f2 is_differentiable_on Z &
    for x st x in Z holds (f2`|Z).x = 1 by FDIFF_1:31,A3;
    for x st x in Z holds f2.x <> 0 by A1;
    then A10:f1/f2 is_differentiable_on Z by A7,A9,FDIFF_2:21;
A11: for x st x in Z holds ((f1/f2)`|Z).x = (a-b)/(x-b)^2
    proof
      let x;
      assume A12:x in Z;
      then A13:f1 is_differentiable_in x by A7,FDIFF_1:16;
A14:  f2 is_differentiable_in x by A9,A12,FDIFF_1:16;
A15:  f2.x <>0 & f1.x=x-a & f2.x=x-b by A1,A12;
      then diff(f1/f2,x)
      =(diff(f1,x) * f2.x - diff(f2,x) * f1.x)/(f2.x)^2 by FDIFF_2:14,A13,A14
      .=((f1`|Z).x * f2.x-diff(f2,x) * f1.x)/(f2.x)^2 by A7,A12,FDIFF_1:def 8
      .= ((f1`|Z).x * f2.x-(f2`|Z).x * f1.x)/(f2.x)^2 by A9,A12,FDIFF_1:def 8
      .=(1* f2.x-(f2`|Z).x * f1.x)/(f2.x)^2 by A12,FDIFF_1:31,A2,A6
      .=(1* f2.x-1* f1.x)/(f2.x)^2 by A12,FDIFF_1:31,A3,A8
      .=(a-b)/(x-b)^2 by A15;
      hence ((f1/f2)`|Z).x = (a-b)/(x-b)^2 by FDIFF_1:def 8,A10,A12;
    end;
A16: for x st x in Z holds (f1/f2).x >0
    proof
      let x;
      assume A17:x in Z;
      then A18:x in dom (f1/f2) by FUNCT_1:21,A1;
A19:   f1.x >0 & f2.x >0 by A1,A17;
      (f1/f2).x=f1.x * (f2.x)" by A18,RFUNCT_1:def 4
      .=f1.x / f2.x by XCMPLX_0:def 9;
      hence thesis by XREAL_1:141,A19;
    end;
A20: for x st x in Z holds (log_ number_e)*(f1/f2) is_differentiable_in x
    proof
      let x;
      assume A21:x in Z;
      then A22:f1/f2 is_differentiable_in x by FDIFF_1:16,A10;
      (f1/f2).x >0 by A16,A21;
      hence (log_ number_e)*(f1/f2) is_differentiable_in x by TAYLOR_1:20,A22;
    end;
    then A23:(log_ number_e)*(f1/f2) is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds
      (((log_ number_e)*(f1/f2))`|Z).x = (a-b)/((x-a)*(x-b))
    proof
      let x;
      assume A24:x in Z;
      then A25:(f1/f2) is_differentiable_in x by FDIFF_1:16,A10;
A26:  f2.x=x-b & f2.x>0 by A1,A24;
A27:  x in dom (f1/f2) by FUNCT_1:21,A1,A24;
A28:  (f1/f2).x >0 & f1.x=x-a & f2.x=x-b by A1,A24,A16;
      diff((log_ number_e)*(f1/f2),x) =diff((f1/f2),x)/((f1/f2).x)
      by TAYLOR_1:20,A28,A25
      .=((f1/f2)`|Z).x/((f1/f2).x) by A10,A24,FDIFF_1:def 8
      .=((a-b)/(x-b)^2)/((f1/f2).x) by A11,A24
      .=((a-b)/(x-b)^2)/(f1.x * (f2.x)") by A27,RFUNCT_1:def 4
      .=((a-b)/(x-b)^2)/((x-a)/(x-b)) by A28,XCMPLX_0:def 9
      .=((a-b)/((x-b)*(x-b)))/((x-a)/(x-b))
      .=((a-b)/(x-b)/(x-b))/((x-a)/(x-b)) by XCMPLX_1:79
      .=((a-b)/(x-b))/((x-a)/(x-b))/(x-b) by XCMPLX_1:48
      .=(a-b)/(x-a)/(x-b) by A26,XCMPLX_1:55
      .=(a-b)/((x-a)*(x-b)) by XCMPLX_1:79;
      hence thesis by A24,A23,FDIFF_1:def 8;
    end;
    hence thesis by A1,FDIFF_1:16,A20;
  end;

:: 25 f.x=(1/(a-b))*ln((x-a)/(x-b))
theorem
  Z c= dom ((1/(a-b))(#)f) & f=(log_ number_e)*(f1/f2) & (for x st
  x in Z holds f1.x=x-a & f1.x >0 & f2.x=x-b & f2.x >0 & a-b<>0)
  implies (1/(a-b))(#)f is_differentiable_on Z &
  for x st x in Z holds (((1/(a-b))(#)f)`|Z).x =1/((x-a)*(x-b))
  proof
    assume A1:Z c= dom ((1/(a-b))(#)f) & f=(log_ number_e)*(f1/f2) &
    (for x st x in Z holds f1.x=x-a & f1.x >0 & f2.x=x-b & f2.x >0 & a-b<>0);
    then A2:Z c= dom ((1/(a-b))(#)f) & f=(log_ number_e)*(f1/f2) &
    (for x st x in Z holds f1.x=x-a & f1.x >0 & f2.x=x-b & f2.x >0);
A3: Z c= dom f by SEQ_1:def 6,A1;
    then A4:f is_differentiable_on Z & for x
    st x in Z holds (f`|Z).x =(a-b)/((x-a)*(x-b)) by Th24,A2;
    for x st x in Z holds (((1/(a-b))(#)f)`|Z).x =1/((x-a)*(x-b))
    proof
      let x;
      assume A5:x in Z;
      then A6:a-b<>0 by A1;
      (((1/(a-b))(#)f)`|Z).x =(1/(a-b))*diff(f,x) by A4,A1,FDIFF_1:28,A5
      .=(1/(a-b))*(f`|Z).x by A5,A4,FDIFF_1:def 8
      .=(1/(a-b))*((a-b)/((x-a)*(x-b))) by Th24,A2,A3,A5
      .=(1/(a-b))*(a-b)/((x-a)*(x-b)) by XCMPLX_1:75
      .=1/((x-a)*(x-b)) by XCMPLX_1:107,A6;
      hence thesis;
    end;
    hence thesis by A1,FDIFF_1:28,A4;
  end;

:: 26 f.x=ln((x-a)/x|^2)
theorem
  Z c= dom ((log_ number_e)*(f1/f2)) & f2=#Z 2 & (for x st x in Z holds
  f1.x=x-a & f1.x >0 & f2.x >0 & x <>0) implies
  (log_ number_e)*(f1/f2) is_differentiable_on Z & for x st x in Z holds
  (((log_ number_e)*(f1/f2))`|Z).x = (2*a-x)/(x*(x-a))
  proof
    assume A1:Z c= dom ((log_ number_e)*(f1/f2)) & f2=#Z 2 &
    (for x st x in Z holds f1.x=x-a & f1.x >0 & f2.x >0 & x <>0);
    for y st y in Z holds y in dom (f1/f2) by FUNCT_1:21,A1;
    then Z c= dom (f1/f2) by TARSKI:def 3;
    then Z c= dom f1 /\ (dom f2 \ f2"{0}) by RFUNCT_1:def 4;
    then A2:Z c= dom f1 & Z c= dom f2 \ f2"{0} by XBOOLE_1:18;
    dom (f2^) c= dom f2 by RFUNCT_1:11;
    then dom f2 \ f2"{0} c= dom f2 by RFUNCT_1:def 8;
    then A3:Z c= dom f2 by XBOOLE_1:1,A2;
A4: for x st x in Z holds f1.x = 1*x+-a
    proof
      let x;
      assume A5:x in Z;
      1*x+-a=1*x-a;
      hence thesis by A1,A5;
    end;
    then A6:f1 is_differentiable_on Z & for x st x in Z holds
    (f1`|Z).x = 1 by FDIFF_1:31,A2;
A7: for x st x in Z holds f2 is_differentiable_in x by A1,TAYLOR_1:2;
    then A8:f2 is_differentiable_on Z by A3,FDIFF_1:16;
A9: for x st x in Z holds (f2`|Z).x=2*x
     proof
       let x;
       assume A10:x in Z;
       2 * (x #Z (2-1))=2 * x by PREPOWER:45;
       then diff(f2,x) =2 * x by TAYLOR_1:2,A1;
       hence thesis by A8,A10,FDIFF_1:def 8;
     end;
A11:  f2 is_differentiable_on Z & for x st x in Z holds (f2`|Z).x=2*x
     by A3,FDIFF_1:16,A7,A9;
     for x st x in Z holds f2.x <> 0 by A1;
     then A12:f1/f2 is_differentiable_on Z by A6,A11,FDIFF_2:21;
A13:  for x st x in Z holds ((f1/f2)`|Z).x = (2*a-x)/x |^3
     proof
       let x;
       assume A14:x in Z;
A15:    f1 is_differentiable_in x by A6,A14,FDIFF_1:16;
A16:   f2 is_differentiable_in x by A1,TAYLOR_1:2;
A17:   x<>0 by A1,A14;
A18:   f2.x=x #Z 2 by A1,TAYLOR_1:def 1
       .=x |^2 by PREPOWER:46;
       then f2.x <>0 & f1.x=x-a & f2.x=x |^2 by A1,A14;
       then diff(f1/f2,x)
       =(diff(f1,x) * f2.x - diff(f2,x) * f1.x)/(f2.x)^2 by FDIFF_2:14,A15,A16
       .=((f1`|Z).x * f2.x-diff(f2,x) * f1.x)/(f2.x)^2 by A6,A14,FDIFF_1:def 8
       .= ((f1`|Z).x * f2.x-(f2`|Z).x * f1.x)/(f2.x)^2 by A11,A14,FDIFF_1:def 8
       .=(1* f2.x-(f2`|Z).x * f1.x)/(f2.x)^2 by A14,FDIFF_1:31,A2,A4
       .=(1* f2.x-(2*x)* f1.x)/(f2.x)^2 by A14,A9
       .=((x |^(1+1))-(2*x)*(x-a))/(x |^2)^2 by A18,A1,A14
       .=((x |^1*x)-(2*x)*(x-a))/(x |^2)^2 by NEWTON:11
       .=((x *x)-(2*x)*(x-a))/(x |^2)^2 by NEWTON:10
       .=(x *(2*a-x))/((x |^2)*(x |^2))
       .=(x *(2*a-x))/(x |^(2+2)) by NEWTON:13
       .=(x *(2*a-x))/(x |^(3+1))
       .=(x *(2*a-x))/(x |^3 *x) by NEWTON:11
       .=(2*a-x)/x |^3 by XCMPLX_1:92,A17;
       hence ((f1/f2)`|Z).x = (2*a-x)/x |^3 by FDIFF_1:def 8,A12,A14;
     end;
A19:  for x st x in Z holds (f1/f2).x >0
     proof
       let x;
       assume A20:x in Z;
       then A21:x in dom (f1/f2) by FUNCT_1:21,A1;
A22:     f1.x >0 & f2.x >0 by A1,A20;
       (f1/f2).x=f1.x * (f2.x)" by A21,RFUNCT_1:def 4
       .=f1.x / f2.x by XCMPLX_0:def 9;
       hence thesis by XREAL_1:141,A22;
     end;
A23:  for x st x in Z holds (log_ number_e)*(f1/f2) is_differentiable_in x
     proof
       let x;
       assume A24:x in Z;
       then A25:f1/f2 is_differentiable_in x by FDIFF_1:16,A12;
       (f1/f2).x >0 by A19,A24;
       hence (log_ number_e)*(f1/f2) is_differentiable_in x by TAYLOR_1:20,A25;
     end;
     then A26:(log_ number_e)*(f1/f2) is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds (((log_ number_e)*(f1/f2))`|Z).x = (2*a-x)/(x*(x-a))
     proof
       let x;
       assume A27:x in Z;
       then A28:(f1/f2) is_differentiable_in x by FDIFF_1:16,A12;
A29:    f2.x=x #Z 2 by A1,TAYLOR_1:def 1
       .=x |^2 by PREPOWER:46;
A30:   x |^2>0 by A29,A1,A27;
A31:   x in dom (f1/f2) by FUNCT_1:21,A1,A27;
A32:   (f1/f2).x >0 & f1.x=x-a & f2.x=x |^2 by A1,A27,A19,A29;
       diff((log_ number_e)*(f1/f2),x) =diff((f1/f2),x)/((f1/f2).x)
       by TAYLOR_1:20,A32,A28
       .=((f1/f2)`|Z).x/((f1/f2).x) by A12,A27,FDIFF_1:def 8
       .=((2*a-x)/x |^3)/((f1/f2).x) by A13,A27
       .=((2*a-x)/x |^3)/(f1.x * (f2.x)") by A31,RFUNCT_1:def 4
       .=((2*a-x)/x |^(2+1))/((x-a)/x |^2) by A32,XCMPLX_0:def 9
       .=((2*a-x)/(x |^2*x))/((x-a)/x |^2) by NEWTON:11
       .=(2*a-x)/x |^2/x/((x-a)/x |^2) by XCMPLX_1:79
       .=((2*a-x)/x |^2)/((x-a)/x |^2)/x by XCMPLX_1:48
       .=(2*a-x)/(x-a)/x by A30,XCMPLX_1:55
       .=(2*a-x)/(x*(x-a)) by XCMPLX_1:79;
       hence thesis by A27,A26,FDIFF_1:def 8;
     end;
     hence thesis by A1,FDIFF_1:16,A23;
   end;

:: irrational function
:: 27 f.x=(a+x) #R (3/2)
theorem Th27:
  Z c= dom (( #R (3/2))*f) & (for x st x in Z holds f.x=a+x & f.x>0)
  implies ( #R (3/2))*f is_differentiable_on Z &
  for x st x in Z holds ((( #R (3/2))*f)`|Z).x =(3/2)* (a+x) #R (1/2)
  proof
    assume A1:Z c= dom (( #R (3/2))*f) &
    (for x st x in Z holds f.x=a+x & f.x>0);
    for y st y in Z holds y in dom f by FUNCT_1:21,A1;
    then A2:Z c= dom f by TARSKI:def 3;
A3: for x st x in Z holds f.x = 1*x+a by A1;
    then A4:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = 1 by FDIFF_1:31,A2;
A5: for x st x in Z holds ( #R (3/2))*f is_differentiable_in x
    proof
      let x;
      assume A6:x in Z;
      then A7:f is_differentiable_in x by FDIFF_1:16,A4;
     f.x >0 by A1,A6;
      hence ( #R (3/2))*f is_differentiable_in x by TAYLOR_1:22,A7;
    end;
    then A8:( #R (3/2))*f is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds ((( #R (3/2))*f)`|Z).x =(3/2)*(a+x) #R (1/2)
    proof
      let x;
      assume A9:x in Z;
      then A10:f is_differentiable_in x by FDIFF_1:16,A4;
A11:  f.x = a+x & f.x >0 by A1,A9;
      diff(( #R (3/2)) *f,x) = (3/2)*( ( f.x) #R (3/2-1)) * diff(f,x)
      by TAYLOR_1:22,A11,A10
      .= (3/2)*( ( f.x) #R (3/2-1))*(f`|Z).x by A4,A9,FDIFF_1:def 8
      .= (3/2)*( (a+x) #R (3/2-1))*1 by FDIFF_1:31,A2,A3,A9,A11
      .=(3/2)* (a+x) #R (1/2);
      hence ((( #R (3/2))*f)`|Z).x =(3/2)* (a+x) #R (1/2)
        by A9,A8,FDIFF_1:def 8;
    end;
    hence thesis by A1,FDIFF_1:16,A5;
  end;

:: 28 f.x=(a+x) #R (2/3)
theorem
  Z c= dom ((2/3)(#)(( #R (3/2))*f)) &
    (for x st x in Z holds f.x=a+x & f.x>0) implies
      (2/3)(#)(( #R (3/2))*f) is_differentiable_on Z &
  for x st x in Z holds (((2/3)(#)(( #R (3/2))*f))`|Z).x =(a+x) #R (1/2)
  proof
    assume A1:Z c= dom ((2/3)(#)(( #R (3/2))*f)) &
    (for x st x in Z holds f.x=a+x & f.x>0);
    then A2:Z c= dom (( #R (3/2))*f) by SEQ_1:def 6;
    then A3:(( #R (3/2))*f) is_differentiable_on Z & for x
    st x in Z holds ((( #R (3/2))*f)`|Z).x =(3/2)* (a+x) #R (1/2) by Th27,A1;
    for x st x in Z holds (((2/3)(#)(( #R (3/2))*f))`|Z).x =(a+x) #R (1/2)
    proof
      let x;
      assume A4:x in Z;
      hence (((2/3)(#)(( #R (3/2))*f))`|Z).x =(2/3)*diff((( #R (3/2))*f),x)
        by A1,FDIFF_1:28,A3
      .=(2/3)*((( #R (3/2))*f)`|Z).x by A4,A3,FDIFF_1:def 8
      .=(2/3)*((3/2)* (a+x) #R (1/2)) by Th27,A1,A2,A4
      .=(a+x) #R (1/2);
    end;
    hence thesis by A3,A1,FDIFF_1:28;
  end;

:: 29 f.x=(-2/3)*(a+x) #R (2/3)
theorem
  Z c= dom ((-2/3)(#)(( #R (3/2))*f)) & (for x st x in Z holds f.x=a-x & f.x>0)
  implies (-2/3)(#)(( #R (3/2))*f) is_differentiable_on Z &
  for x st x in Z holds (((-2/3)(#)(( #R (3/2))*f))`|Z).x =(a-x) #R (1/2)
  proof
    assume A1:Z c= dom ((-2/3)(#)(( #R (3/2))*f)) &
    (for x st x in Z holds f.x=a-x & f.x>0);
    then A2:Z c= dom (( #R (3/2))*f) by SEQ_1:def 6;
    for y st y in Z holds y in dom f by FUNCT_1:21,A2;
    then A3:Z c= dom f by TARSKI:def 3;
A4: for x st x in Z holds f.x =(-1)*x+a
    proof
      let x;
      assume x in Z;
      then f.x =a-x by A1;
      hence thesis;
    end;
    then A5:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = -1 by FDIFF_1:31,A3;
    now let x;
      assume A6:x in Z;
      then A7:f is_differentiable_in x by FDIFF_1:16,A5;
      f.x >0 by A1,A6;
      hence ( #R (3/2))*f is_differentiable_in x by TAYLOR_1:22,A7;
    end;
    then A8:( #R (3/2))*f is_differentiable_on Z by A2,FDIFF_1:16;
    for x st x in Z holds (((-2/3)(#)(( #R (3/2))*f))`|Z).x =(a-x) #R (1/2)
    proof
      let x;
      assume A9:x in Z;
      then A10:f is_differentiable_in x by FDIFF_1:16,A5;
A11:   f.x = a-x & f.x >0 by A1,A9;
      (((-2/3)(#)(( #R (3/2))*f))`|Z).x =(-2/3)*diff((( #R (3/2))*f),x)
      by A1,FDIFF_1:28,A8,A9
      .=(-2/3)* ((3/2)*( ( f.x) #R (3/2-1)) * diff(f,x)) by TAYLOR_1:22,A11,A10
      .=(-2/3)* ((3/2)*( ( f.x) #R (3/2-1))*(f`|Z).x) by A5,A9,FDIFF_1:def 8
      .=(-2/3)* ((3/2)*( (a-x) #R (3/2-1))*(-1)) by FDIFF_1:31,A3,A4,A9,A11
      .=(a-x) #R (1/2);
      hence thesis;
    end;
    hence thesis by A8,A1,FDIFF_1:28;
  end;

:: 30 f.x=2*(a+x) #R (1/2)
theorem
  Z c= dom ((2(#)(( #R (1/2))*f))) & (for x st x in Z holds f.x=a+x & f.x>0)
  implies 2(#)(( #R (1/2))*f) is_differentiable_on Z &
  for x st x in Z holds ((2(#)(( #R (1/2))*f))`|Z).x =(a+x) #R (-1/2)
  proof
    assume A1:Z c= dom ((2(#)(( #R (1/2))*f))) &
    (for x st x in Z holds f.x=a+x & f.x>0);
    then A2:Z c= dom (( #R (1/2))*f) &
    (for x st x in Z holds f.x=a+x & f.x>0) by SEQ_1:def 6;
    for y st y in Z holds y in dom f by FUNCT_1:21,A2;
    then A3:Z c= dom f by TARSKI:def 3;
A4: for x st x in Z holds f.x = 1*x+a by A1;
    then A5:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = 1 by FDIFF_1:31,A3;
    now let x;
      assume A6:x in Z;
      then A7:f is_differentiable_in x by FDIFF_1:16,A5;
      f.x >0 by A1,A6;
      hence ( #R (1/2))*f is_differentiable_in x by TAYLOR_1:22,A7;
    end;
    then A8:( #R (1/2))*f is_differentiable_on Z by A2,FDIFF_1:16;
    for x st x in Z holds ((2(#)(( #R (1/2))*f))`|Z).x =(a+x) #R (-1/2)
    proof
      let x;
      assume A9:x in Z;
      then A10:f is_differentiable_in x by FDIFF_1:16,A5;
A11:   f.x = a+x & f.x >0 by A1,A9;
      ((2(#)(( #R (1/2))*f))`|Z).x =2*diff((( #R (1/2))*f),x)
      by A8,A1,FDIFF_1:28,A9
      .=2*((1/2)*( ( f.x) #R (1/2-1)) * diff(f,x)) by TAYLOR_1:22,A11,A10
      .=2*((1/2)*( ( f.x) #R (1/2-1))*(f`|Z).x) by A5,A9,FDIFF_1:def 8
      .=2*((1/2)*( (a+x) #R (1/2-1))*1) by FDIFF_1:31,A3,A4,A9,A11
      .= (a+x) #R (-1/2);
      hence ((2(#)(( #R (1/2))*f))`|Z).x =(a+x) #R (-1/2);
    end;
    hence thesis by A1,FDIFF_1:28,A8;
  end;

:: 31 f.x=(-2)*(a-x)#R (1/2)
theorem
  Z c= dom (((-2)(#)(( #R (1/2))*f))) &
    (for x st x in Z holds f.x=a-x & f.x>0)
  implies (-2)(#)(( #R (1/2))*f) is_differentiable_on Z &
  for x st x in Z holds (((-2)(#)(( #R (1/2))*f))`|Z).x =(a-x) #R (-1/2)
  proof
    assume A1:Z c= dom (((-2)(#)(( #R (1/2))*f))) &
    (for x st x in Z holds f.x=a-x & f.x>0);
    then A2:Z c= dom (( #R (1/2))*f) &
    (for x st x in Z holds f.x=a-x & f.x>0) by SEQ_1:def 6;
    for y st y in Z holds y in dom f by FUNCT_1:21,A2;
    then A3:Z c= dom f by TARSKI:def 3;
A4: for x st x in Z holds f.x =(-1)*x+a
    proof
      let x;
      assume x in Z;
      then f.x =a-x by A1;
      hence thesis;
    end;
    then A5:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = -1 by FDIFF_1:31,A3;
    now let x;
      assume A6:x in Z;
      then A7:f is_differentiable_in x by FDIFF_1:16,A5;
      f.x >0 by A1,A6;
      hence ( #R (1/2))*f is_differentiable_in x by TAYLOR_1:22,A7;
    end;
    then A8:( #R (1/2))*f is_differentiable_on Z by A2,FDIFF_1:16;
    for x st x in Z holds (((-2)(#)(( #R (1/2))*f))`|Z).x =(a-x) #R (-1/2)
    proof
      let x;
      assume A9:x in Z;
      then A10:f is_differentiable_in x by FDIFF_1:16,A5;
A11:   f.x = a-x & f.x >0 by A1,A9;
      (((-2)(#)(( #R (1/2))*f))`|Z).x =(-2)*diff((( #R (1/2))*f),x)
      by A1,FDIFF_1:28,A8,A9
      .=(-2)*((1/2)*( ( f.x) #R (1/2-1)) * diff(f,x)) by TAYLOR_1:22,A11,A10
      .=(-2)*((1/2)*( ( f.x) #R (1/2-1))*(f`|Z).x) by A5,A9,FDIFF_1:def 8
      .=(-2)*((1/2)*( (a-x) #R (1/2-1))*(-1)) by FDIFF_1:31,A3,A4,A9,A11
      .= (a-x) #R (-1/2);
      hence (((-2)(#)(( #R (1/2))*f))`|Z).x =(a-x) #R (-1/2);
    end;
    hence thesis by A1,FDIFF_1:28,A8;
  end;

:: 32 f.x=(2/(3*b))*((a+b*x) #R (3/2))
theorem
  Z c= dom ((2/(3*b))(#)(( #R (3/2))*f)) & (for x st x in Z holds f.x=a+b*x &
  b<>0 & f.x>0) implies (2/(3*b))(#)(( #R (3/2))*f) is_differentiable_on Z &
  for x st x in Z holds (((2/(3*b))(#)(( #R (3/2))*f))`|Z).x =(a+b*x) #R (1/2)
  proof
    assume
    A1:Z c= dom ((2/(3*b))(#)(( #R (3/2))*f)) &
    (for x st x in Z holds f.x=a+b*x & b<>0 & f.x>0);
    then A2:Z c= dom (( #R (3/2))*f) by SEQ_1:def 6;
    for y st y in Z holds y in dom f by FUNCT_1:21,A2;
    then A3:Z c= dom f by TARSKI:def 3;
A4: for x st x in Z holds f.x = b*x+a by A1;
    then A5:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = b by FDIFF_1:31,A3;
    now let x;
      assume A6:x in Z;
      then A7:f is_differentiable_in x by FDIFF_1:16,A5;
      f.x >0 by A1,A6;
      hence ( #R (3/2))*f is_differentiable_in x by TAYLOR_1:22,A7;
    end;
    then A8:( #R (3/2))*f is_differentiable_on Z by A2,FDIFF_1:16;
    for x st x in Z holds
      (((2/(3*b))(#)(( #R (3/2))*f))`|Z).x =(a+b*x) #R (1/2)
    proof
      let x;
      assume A9:x in Z;
      then A10:f is_differentiable_in x by FDIFF_1:16,A5;
      b<>0 by A1,A9;
      then b>0 or b<0;
      then A11:3*b <>0;
A12:   f.x = a+b*x & f.x >0 by A1,A9;
      (((2/(3*b))(#)(( #R (3/2))*f))`|Z).x =(2/(3*b))*diff((( #R (3/2))*f),x)
      by A8,A1,FDIFF_1:28,A9
      .=(2/(3*b))*((3/2)*( ( f.x) #R (3/2-1)) * diff(f,x))
      by TAYLOR_1:22,A12,A10
      .=(2/(3*b))*((3/2)*( ( f.x) #R (3/2-1))*(f`|Z).x) by A5,A9,FDIFF_1:def 8
      .=(2/(3*b))*((3/2)*( (a+b*x) #R (3/2-1))*b) by FDIFF_1:31,A3,A4,A9,A12
      .=(2/(3*b))*((3*b)/2)*( (a+b*x) #R (3/2-1))
      .=1*( (a+b*x) #R (3/2-1)) by A11,XCMPLX_1:113
      .=(a+b*x) #R (1/2);
      hence thesis;
    end;
    hence thesis by A8,A1,FDIFF_1:28;
  end;

:: 33 f.x=(-2/(3*b))*((a-b*x) #R (3/2))
theorem
  Z c= dom ((-2/(3*b))(#)(( #R (3/2))*f)) &
  (for x st x in Z holds f.x=a-b*x & b<>0 & f.x>0) implies
  (-2/(3*b))(#)(( #R (3/2))*f) is_differentiable_on Z &
  for x st x in Z holds
    (((-2/(3*b))(#)(( #R (3/2))*f))`|Z).x =(a-b*x) #R (1/2)
  proof
    assume A1:Z c= dom ((-2/(3*b))(#)(( #R (3/2))*f)) &
    (for x st x in Z holds f.x=a-b*x & b<>0 & f.x>0);
    then A2:Z c= dom (( #R (3/2))*f) by SEQ_1:def 6;
    for y st y in Z holds y in dom f by FUNCT_1:21,A2;
    then A3:Z c= dom f by TARSKI:def 3;
A4: for x st x in Z holds f.x =(-b)*x+a
    proof
      let x;
      assume x in Z;
      then f.x =a-b*x by A1;
      hence thesis;
    end;
    then A5:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = -b by FDIFF_1:31,A3;
    now let x;
      assume A6:x in Z;
      then A7:f is_differentiable_in x by FDIFF_1:16,A5;
      f.x >0 by A1,A6;
      hence ( #R (3/2))*f is_differentiable_in x by TAYLOR_1:22,A7;
    end;
    then A8:( #R (3/2))*f is_differentiable_on Z by A2,FDIFF_1:16;
    for x st x in Z holds
      (((-2/(3*b))(#)(( #R (3/2))*f))`|Z).x =(a-b*x) #R (1/2)
    proof
      let x;
      assume A9:x in Z;
      then A10:f is_differentiable_in x by FDIFF_1:16,A5;
      b<>0 by A1,A9;
      then b>0 or b<0;
      then A11:3*b <>0;
A12:   f.x = a-b*x & f.x >0 by A1,A9;
      (((-2/(3*b))(#)(( #R (3/2))*f))`|Z).x =(-2/(3*b))*diff((( #R (3/2))*f),x)
      by A8,A1,FDIFF_1:28,A9
      .=(-2/(3*b))* ((3/2)*( ( f.x) #R (3/2-1)) * diff(f,x))
        by TAYLOR_1:22,A12,A10
      .=(-2/(3*b))* ((3/2)*( ( f.x) #R (3/2-1))*(f`|Z).x)
        by A5,A9,FDIFF_1:def 8
      .=(-2/(3*b))* ((3/2)*( (a-b*x) #R (3/2-1))*(-b))
        by FDIFF_1:31,A3,A4,A9,A12
      .=(2/(3*b))* ((3*b)/2)*( (a-b*x) #R (3/2-1))
      .=1*( (a-b*x) #R (3/2-1)) by A11,XCMPLX_1:113
      .=(a-b*x) #R (1/2);
      hence thesis;
    end;
    hence thesis by A8,A1,FDIFF_1:28;
  end;

:: 34 f.x=(a^2+x|^2) #R (1/2)
theorem
  Z c= dom (( #R (1/2))*f) & f=f1+f2 & f2=#Z 2 &
  (for x st x in Z holds f1.x=a^2 & f.x>0)
  implies ( #R (1/2))*f is_differentiable_on Z &
  for x st x in Z holds ((( #R (1/2))*f)`|Z).x =x * (a^2+x |^2) #R (-1/2)
  proof
    assume A1:Z c= dom (( #R (1/2))*f) & f=f1+f2 & f2=#Z 2 &
    (for x st x in Z holds f1.x=a^2 & f.x>0);
    for y st y in Z holds y in dom f by FUNCT_1:21,A1;
    then A2:Z c= dom (f1+f2) & f2=#Z 2 &
    (for x st x in Z holds f1.x=a^2 ) by A1,TARSKI:def 3;
    then A3:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = 2*x by A1,Th17;
A4: for x st x in Z holds ( #R (1/2))*f is_differentiable_in x
    proof
      let x;
      assume A5:x in Z;
      then A6:f is_differentiable_in x by FDIFF_1:16,A3;
      f.x >0 by A1,A5;
      hence ( #R (1/2))*f is_differentiable_in x by TAYLOR_1:22,A6;
    end;
    then A7:( #R (1/2))*f is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds ((( #R (1/2))*f)`|Z).x =x * (a^2+x |^2) #R (-1/2)
    proof
      let x;
      assume A8:x in Z;
      then A9: x in dom (f1+f2) by A1,FUNCT_1:21;
A10:   f is_differentiable_in x by FDIFF_1:16,A3,A8;
A11:   ( f1+f2).x=f1.x + f2.x by SEQ_1:def 3,A9
      .=a^2 +(f2.x) by A1,A8
      .=a^2 +(x #Z 2) by A1,TAYLOR_1:def 1
      .=a^2 +(x |^2) by PREPOWER:46;
      then A12: f.x = a^2+x |^2 & f.x >0 by A1,A8;
      diff(( #R (1/2)) *f,x) = (1/2)*( ( f.x) #R (1/2-1)) * diff(f,x)
      by TAYLOR_1:22,A10,A12
      .= (1/2)*( ( f.x) #R (1/2-1))*(f`|Z).x by A3,A8,FDIFF_1:def 8
      .= (1/2)*( (a^2+x |^2) #R (1/2-1))*(2*x) by A1,Th17,A2,A8,A11
      .=(1/2)*(2*x)*( (a^2+x |^2) #R (1/2-1))
      .=x* (a^2+x |^2) #R (-1/2);
      hence ((( #R (1/2))*f)`|Z).x =x* (a^2+x |^2) #R (-1/2)
      by A8,A7,FDIFF_1:def 8;
    end;
    hence thesis by A1,FDIFF_1:16,A4;
  end;

:: 35 f.x=-(a^2-x|^2) #R (1/2)
theorem
  Z c= dom -(( #R (1/2))*f) & f=f1-f2 & f2=#Z 2 &
  (for x st x in Z holds f1.x=a^2 & f.x >0) implies
  -(( #R (1/2))*f) is_differentiable_on Z &
  for x st x in Z holds
  ((-(( #R (1/2))*f))`|Z).x = x* (a^2-x |^2) #R (-1/2)
  proof
    assume A1:Z c= dom -(( #R (1/2))*f) & f=f1-f2 & f2=#Z 2 &
    (for x st x in Z holds f1.x=a^2 & f.x >0);
A2: -(( #R (1/2))*f) = (-1)(#)(( #R (1/2))*f) by RFUNCT_1:38;
A3: Z c= dom (( #R (1/2))*f) by A1,SEQ_1:def 7;
A4: for y st y in Z holds y in dom f by FUNCT_1:21,A3;
A5: f1+(-1)(#)f2=f1+-f2 by RFUNCT_1:38
    .=f by A1, RFUNCT_1:40;
    then A6:Z c= dom ((f1+(-1)(#)f2)) by TARSKI:def 3,A4;
A7: f2=#Z 2 & for x st x in Z holds f1.x=a^2+0*x by A1;
    then A8:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x =0+2*(-1)*x by A5,Th12,A6;
    now let x;
      assume A9:x in Z;
      then A10:f is_differentiable_in x by FDIFF_1:16,A8;
      f.x >0 by A1,A9;
      hence ( #R (1/2))*f is_differentiable_in x by TAYLOR_1:22,A10;
    end;
    then A11:( #R (1/2))*f is_differentiable_on Z by A3,FDIFF_1:16;
    for x st x in Z holds
      (((-1)(#)(( #R (1/2))*f))`|Z).x =x* (a^2-x |^2) #R (-1/2)
    proof
      let x;
      assume A12:x in Z;
      then A13: x in dom (f1-f2) by FUNCT_1:21,A3,A1;
A14:   f is_differentiable_in x by FDIFF_1:16,A8,A12;
A15:   ( f1-f2).x=f1.x - f2.x by SEQ_1:def 4,A13
      .=a^2 -(f2.x) by A1,A12
      .=a^2 -(x #Z 2) by A1,TAYLOR_1:def 1
      .=a^2 -(x |^2) by PREPOWER:46;
      then A16: f.x = a^2-x |^2 & f.x >0 by A1,A12;
      (((-1)(#)(( #R (1/2))*f))`|Z).x =(-1)*diff((( #R (1/2))*f),x)
      by A1,FDIFF_1:28,A2,A11,A12
      .=(-1)*((1/2)*( ( f.x) #R (1/2-1)) * diff(f,x))
      by TAYLOR_1:22,A16,A14
      .=(-1)*((1/2)*( ( f.x) #R (1/2-1))*(f`|Z).x) by A8,A12,FDIFF_1:def 8
      .=(-1)*((1/2)*( ( f.x) #R (1/2-1))*(0+2*(-1)*x))
        by A5,Th12,A6,A7,A12
      .=x*( ( f.x) #R (1/2-1))
      .=x* (a^2-x |^2) #R (-1/2) by A1,A15;
      hence thesis;
    end;
    hence thesis by A1,FDIFF_1:28,A2,A11;
  end;

:: 36 f.x=(x+x|^2) #R (1/2)
theorem
  Z c= dom ((2(#)(( #R (1/2))*f))) & f=f1+f2 & f2=#Z 2 &
  (for x st x in Z holds f1.x=x & f.x>0)
  implies 2(#)(( #R (1/2))*f) is_differentiable_on Z &
  for x st x in Z holds
    ((2(#)(( #R (1/2))*f))`|Z).x =(2*x+1)*(x |^2+x) #R (-1/2)
  proof
    assume that A1:Z c= dom ((2(#)(( #R (1/2))*f))) and
A2: f=f1+f2 and
A3: f2=#Z 2 and
A4: for x st x in Z holds f1.x=x & f.x>0;
A5: Z c= dom (( #R (1/2))*f) by SEQ_1:def 6,A1;
A6: for y st y in Z holds y in dom f by FUNCT_1:21,A5;
A7: f2=1(#)f2 by RFUNCT_1:33;
    then A8:Z c= dom (f1+1(#)f2) by TARSKI:def 3,A6,A2;
    for x st x in Z holds f1.x=0+1*x by A4;
    then A9:f1+f2 is_differentiable_on Z &
    for x st x in Z holds ((f1+f2)`|Z).x = 1+2*1*x by A7,Th12,A3,A8;
    now let x;
      assume A10:x in Z;
      then A11:f is_differentiable_in x by FDIFF_1:16,A2,A9;
      f.x >0 by A4,A10;
      hence ( #R (1/2))*f is_differentiable_in x by TAYLOR_1:22,A11;
    end;
    then A12:( #R (1/2))*f is_differentiable_on Z by A5,FDIFF_1:16;
    for x st x in Z holds
      ((2(#)(( #R (1/2))*f))`|Z).x =(2*x+1)*(x |^2+x) #R (-1/2)
    proof
      let x;
      assume A13:x in Z;
      then A14: x in dom (f1+f2) by FUNCT_1:21,A5,A2;
A15:   f is_differentiable_in x by FDIFF_1:16,A2,A9,A13;
A16:   ( f1+f2).x=f1.x + f2.x by SEQ_1:def 3,A14
      .=x +(f2.x) by A4,A13
      .=x +(x #Z 2) by A3,TAYLOR_1:def 1
      .=x +x |^2 by PREPOWER:46;
      then A17: f.x = x+x |^2 & f.x >0 by A4,A2,A13;
      ((2(#)(( #R (1/2))*f))`|Z).x =2*diff((( #R (1/2))*f),x)
        by A1,FDIFF_1:28,A12,A13
      .=2*((1/2)*( ( f.x) #R (1/2-1)) * diff(f,x)) by TAYLOR_1:22,A17,A15
      .=2*((1/2)*( ( f.x) #R (1/2-1))*(f`|Z).x) by A2,A9,A13,FDIFF_1:def 8
      .=2*(1/2)*( ( f.x) #R (1/2-1))*(f`|Z).x
      .=(2*x+1)* (x |^2+x) #R (-1/2) by A2,A9,A13,A16;
      hence thesis;
    end;
    hence thesis by A12,A1,FDIFF_1:28;
  end;

:: trigonometric functions
:: 37 f.x=sin(a*x+b)
theorem
  Z c= dom (sin*f) & (for x st x in Z holds f.x=a*x+b)
  implies sin*f is_differentiable_on Z &
  for x st x in Z holds ((sin*f)`|Z).x = a* cos.(a*x+b)
  proof
    assume that A1:Z c= dom (sin*f) and
A2: for x st x in Z holds f.x=a*x+b;
    for y st y in Z holds y in dom f by FUNCT_1:21,A1;
    then A3:Z c= dom f by TARSKI:def 3;
    then A4:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = a by A2,FDIFF_1:31;
A5: for x st x in Z holds sin*f is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then A6: f is_differentiable_in x by FDIFF_1:16,A4;
      sin is_differentiable_in f.x by SIN_COS:69;
      hence sin*f is_differentiable_in x by A6,FDIFF_2:13;
    end;
    then A7:sin*f is_differentiable_on Z by FDIFF_1:16,A1;
    for x st x in Z holds ((sin*f)`|Z).x = a* cos.(a*x+b)
    proof
      let x;
      assume A8:x in Z;
      then A9: f is_differentiable_in x by FDIFF_1:16,A4;
      sin is_differentiable_in f.x by SIN_COS:69;
      then diff(sin*f,x) = diff(sin, f.x)*diff(f,x) by A9,FDIFF_2:13
      .=cos.( f.x)*diff(f,x) by SIN_COS:69
      .=cos.(a*x+b)*diff(f,x) by A2,A8
      .=cos.(a*x+b)*(f`|Z).x by A4,FDIFF_1:def 8,A8
      .=a* cos.(a*x+b) by A3,A2,FDIFF_1:31,A8;
      hence thesis by FDIFF_1:def 8,A7,A8;
    end;
    hence thesis by FDIFF_1:16,A1,A5;
  end;

:: 38 f.x=sin(a*x+b)
theorem
  Z c= dom (cos*f) & (for x st x in Z holds f.x=a*x+b)
  implies cos*f is_differentiable_on Z &
  for x st x in Z holds ((cos*f)`|Z).x = -a* sin.(a*x+b)
  proof
    assume that A1:Z c= dom (cos*f) and
A2: for x st x in Z holds f.x=a*x+b;
    for y st y in Z holds y in dom f by FUNCT_1:21,A1;
    then A3:Z c= dom f by TARSKI:def 3;
    then A4:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = a by A2,FDIFF_1:31;
A5: for x st x in Z holds cos*f is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then A6: f is_differentiable_in x by FDIFF_1:16,A4;
      cos is_differentiable_in f.x by SIN_COS:68;
      hence cos*f is_differentiable_in x by A6,FDIFF_2:13;
    end;
    then A7:cos*f is_differentiable_on Z by FDIFF_1:16,A1;
    for x st x in Z holds ((cos*f)`|Z).x = -a* sin.(a*x+b)
    proof
      let x;
      assume A8:x in Z;
      then A9: f is_differentiable_in x by FDIFF_1:16,A4;
A10:   diff(cos, f.x)=-sin.( f.x) by SIN_COS:68;
      cos is_differentiable_in f.x by SIN_COS:68;
      then diff(cos*f,x) = diff(cos, f.x)*diff(f,x) by A9,FDIFF_2:13
      .=-sin.( f.x)*diff(f,x) by A10
      .=-sin.(a*x+b)*diff(f,x) by A2,A8
      .=-sin.(a*x+b)*(f`|Z).x by A4,FDIFF_1:def 8,A8
      .=-a*sin.(a*x+b) by A2,FDIFF_1:31,A3,A8;
      hence thesis by FDIFF_1:def 8,A7,A8;
    end;
    hence thesis by FDIFF_1:16,A1,A5;
  end;

:: 39 f.x=1/cos.x
theorem
  (for x st x in Z holds cos.x<>0) implies
  cos^ is_differentiable_on Z & for x st x in Z holds
  ( (cos^)`|Z).x= sin.x/(cos.x)^2
  proof
    assume
A1: for x st x in Z holds cos.x<>0;
A2: cos is_differentiable_on Z by SIN_COS:72,FDIFF_1:34;
    then A3:cos^ is_differentiable_on Z by FDIFF_2:22,A1;
    for x st x in Z holds ( (cos^)`|Z).x= sin.x/(cos.x)^2
    proof
      let x;
      assume A4:x in Z;
A5:   cos.x<>0 by A1,A4;
A6:   cos is_differentiable_in x by SIN_COS:68;
      ( (cos^)`|Z).x= diff(cos^,x) by A3,A4,FDIFF_1:def 8
      .= - diff(cos,x)/(cos.x)^2 by A5,FDIFF_2:15,A6
      .=- (-sin.x)/(cos.x)^2 by SIN_COS:68
      .=- -(sin.x/(cos.x)^2) by XCMPLX_1:188
      .=sin.x/(cos.x)^2;
      hence thesis;
    end;
    hence thesis by FDIFF_2:22,A1,A2;
  end;

:: 40 f.x=1/sin.x
theorem
  (for x st x in Z holds sin.x<>0) implies
  sin^ is_differentiable_on Z &
  for x st x in Z holds ( (sin^)`|Z).x= -cos.x/(sin.x)^2
  proof
    assume
A1: for x st x in Z holds sin.x<>0;
A2: sin is_differentiable_on Z by SIN_COS:73,FDIFF_1:34;
A3: sin^ is_differentiable_on Z by A2,FDIFF_2:22,A1;
    for x st x in Z holds ( (sin^)`|Z).x= -cos.x/(sin.x)^2
    proof
      let x;
      assume A4:x in Z;
A5:   sin.x<>0 by A1,A4;
A6:   sin is_differentiable_in x by A4,A2,FDIFF_1:16;
      ( (sin^)`|Z).x= diff(sin^,x) by A3,A4,FDIFF_1:def 8
      .= - diff(sin,x)/(sin.x)^2 by A5,FDIFF_2:15,A6
      .=- cos.x/(sin.x)^2 by SIN_COS:69;
      hence thesis;
    end;
    hence thesis by A2,FDIFF_2:22,A1;
  end;

:: 41 f.x=sin.x*cos.x
theorem
  Z c= dom (sin(#)cos) implies sin(#)cos is_differentiable_on Z &
  for x st x in Z holds ((sin(#)cos)`|Z).x = cos(2*x)
  proof
    assume A1: Z c= dom (sin(#)cos);
A2: sin is_differentiable_on Z by SIN_COS:73,FDIFF_1:34;
A3: cos is_differentiable_on Z by SIN_COS:72,FDIFF_1:34;
    now let x;
      assume x in Z;
      hence ((sin(#)cos)`|Z).x =(cos.x)*diff(sin,x) + (sin.x)*diff(cos,x)
      by A3,A2,A1,FDIFF_1:29
      .=cos.x * cos.x + (sin.x)*diff(cos,x) by SIN_COS:69
      .=cos.x * cos.x + (sin.x)*(-sin.x) by SIN_COS:68
      .=cos.x * cos.x -sin.x*sin.x
      .=(cos.x)^2-sin.x*sin.x
      .=(cos.x)^2-(sin.x)^2
      .=(cos(x))^2-(sin.x)^2 by SIN_COS:def 23
      .=(cos(x))^2-(sin(x))^2 by SIN_COS:def 21
      .=cos(2*x) by SIN_COS5:7;
    end;
    hence thesis by A3,A2,A1,FDIFF_1:29;
  end;

:: 42 f.x=ln(cos.x)
theorem
  Z c= dom ((log_ number_e)*cos) & (for x st x in Z holds cos.x >0)
  implies (log_ number_e)*cos is_differentiable_on Z &
  for x st x in Z holds (((log_ number_e)*cos)`|Z).x =- tan(x)
  proof
    assume that A1:Z c= dom ((log_ number_e)*cos) and
A2: for x st x in Z holds cos.x >0;
A3: cos is_differentiable_on Z by SIN_COS:72,FDIFF_1:34;
A4: for x st x in Z holds (log_ number_e)*cos is_differentiable_in x
    proof
      let x;
      assume A5:x in Z;
      then A6:cos is_differentiable_in x by FDIFF_1:16,A3;
      cos.x >0 by A2,A5;
      hence thesis by TAYLOR_1:20,A6;
    end;
    then A7:(log_ number_e)*cos is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds (((log_ number_e)*cos)`|Z).x =- tan(x)
    proof
      let x;
      assume A8:x in Z;
      then A9:cos is_differentiable_in x by FDIFF_1:16,A3;
      cos.x >0 by A2,A8;
      then diff((log_ number_e)*cos,x) =diff(cos,x)/(cos.x)
      by TAYLOR_1:20,A9
      .=(-sin.x)/cos.x by SIN_COS:68
      .=- (sin.x/cos.x) by XCMPLX_1:188
      .=- (sin(x)/cos.x) by SIN_COS:def 21
      .=- (sin(x)/cos(x)) by SIN_COS:def 23
      .=- tan(x) by SIN_COS4:def 1;
      hence thesis by A8,A7,FDIFF_1:def 8;
    end;
    hence thesis by A4,A1,FDIFF_1:16;
  end;

:: 43 f.x=ln(sin.x)
theorem
  Z c= dom ((log_ number_e)*sin) & (for x st x in Z holds sin.x >0)
  implies (log_ number_e)*sin is_differentiable_on Z &
  for x st x in Z holds (((log_ number_e)*sin)`|Z).x = cot(x)
  proof
    assume that A1:Z c= dom ((log_ number_e)*sin) and
A2: for x st x in Z holds sin.x >0;
A3: sin is_differentiable_on Z by SIN_COS:73,FDIFF_1:34;
A4: for x st x in Z holds (log_ number_e)*sin is_differentiable_in x
    proof
      let x;
      assume A5:x in Z;
      then A6:sin is_differentiable_in x by FDIFF_1:16,A3;
      sin.x >0 by A2,A5;
      hence (log_ number_e)*sin is_differentiable_in x by TAYLOR_1:20,A6;
    end;
    then A7:(log_ number_e)*sin is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds (((log_ number_e)*sin)`|Z).x =cot(x)
    proof
      let x;
      assume A8:x in Z;
      then A9:sin is_differentiable_in x by FDIFF_1:16,A3;
      sin.x >0 by A2,A8;
      then diff((log_ number_e)*sin,x) =diff(sin,x)/(sin.x)
      by TAYLOR_1:20,A9
      .=cos.x/sin.x by SIN_COS:69
      .=cos(x)/sin.x by SIN_COS:def 23
      .=cos(x)/sin(x) by SIN_COS:def 21
      .=cot(x) by SIN_COS4:def 2;
      hence thesis by A8,A7,FDIFF_1:def 8;
    end;
    hence thesis by A4,A1,FDIFF_1:16;
  end;

:: 44 f.x=-x*cos.x
theorem Th44:
  Z c= dom ((-id Z)(#)cos) implies (-id Z)(#)cos is_differentiable_on Z &
  for x st x in Z holds (((-id Z)(#)cos)`|Z).x =-cos.x+x*sin.x
  proof
    assume A1:Z c= dom ((-id Z)(#)cos);
    then Z c= dom (-id Z) /\ dom cos by SEQ_1:def 5;
    then A2:Z c= dom (-id Z) & Z c= dom cos by XBOOLE_1:18;
A3: for x st x in Z holds (-id Z).x =(-1)*x +0
    proof
      let x;
      assume A4:x in Z;
      then (-id Z).x = -(id Z.x) by SEQ_1:def 7,A2
      .=-x by FUNCT_1:35,A4
      .=(-1)*x +0;
      hence thesis;
    end;
    then A5:-id Z is_differentiable_on Z & for x st x in Z
    holds ((-id Z)`|Z).x = -1 by FDIFF_1:31,A2;
A6: cos is_differentiable_on Z by SIN_COS:72,FDIFF_1:34;
    now let x;
      assume A7:x in Z;
      hence (((-id Z)(#)cos)`|Z).x
      = (cos.x)*diff((-id Z),x) + ((-id Z).x)*diff(cos,x)
        by A5,A1,FDIFF_1:29,A6
      .=(cos.x)*((-id Z)`|Z).x+ ((-id Z).x)*diff(cos,x)
        by A5,A7,FDIFF_1:def 8
      .=(cos.x)*(-1)+ ((-id Z).x)*diff(cos,x) by A7,FDIFF_1:31,A2,A3
      .=(cos.x)*(-1)+((-id Z).x)*(-sin.x) by SIN_COS:68
      .=-cos.x+((-1)*x +0)*(-sin.x) by A3,A7
      .=-cos.x+x*sin.x;
    end;
    hence thesis by A5,A1,FDIFF_1:29,A6;
  end;

:: f.x=x*sin.x
theorem Th45:
  Z c= dom ((id Z)(#)sin) implies (id Z)(#)sin is_differentiable_on Z &
  for x st x in Z holds (((id Z)(#)sin)`|Z).x =sin.x+x*cos.x
  proof
    assume A1:Z c= dom ((id Z)(#)sin);
    then Z c= dom (id Z) /\ dom sin by SEQ_1:def 5;
    then A2:Z c= dom (id Z) & Z c= dom sin by XBOOLE_1:18;
A3: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;
    then A4:id Z is_differentiable_on Z & for x st x in Z
    holds ((id Z)`|Z).x = 1 by FDIFF_1:31,A2;
A5: sin is_differentiable_on Z by SIN_COS:73,FDIFF_1:34;
    now let x;
      assume A6:x in Z;
      hence (((id Z)(#)sin)`|Z).x
      = (sin.x)*diff((id Z),x) + ((id Z).x)*diff(sin,x) by A4,A1,FDIFF_1:29,A5
      .=(sin.x)*((id Z)`|Z).x+ ((id Z).x)*diff(sin,x) by A4,A6,FDIFF_1:def 8
      .=(sin.x)*1+ ((id Z).x)*diff(sin,x) by A6,FDIFF_1:31,A2,A3
      .=(sin.x)*1+((id Z).x)*(cos.x) by SIN_COS:69
      .=sin.x+ x*cos.x by FUNCT_1:35,A6;
    end;
    hence thesis by A4,A1,FDIFF_1:29,A5;
  end;

:: 46 f.x=-x*cos.x+sin.x
theorem
  Z c= dom ((-id Z)(#)cos+sin) implies
    (-id Z)(#)cos+sin is_differentiable_on Z &
  for x st x in Z holds (((-id Z)(#)cos+sin)`|Z).x =x*sin.x
  proof
    assume A1:Z c= dom ((-id Z)(#)cos+sin);
    then Z c= dom ((-id Z)(#)cos) /\ dom sin by SEQ_1:def 3;
    then A2:Z c= dom ((-id Z)(#)cos) & Z c= dom sin by XBOOLE_1:18;
    then A3:(-id Z)(#)cos is_differentiable_on Z &
    for x st x in Z holds (((-id Z)(#)cos)`|Z).x =-cos.x+x*sin.x by Th44;
A4: sin is_differentiable_on Z by SIN_COS:73,FDIFF_1:34;
    now let x;
      assume A5:x in Z;
      hence (((-id Z)(#)cos+sin)`|Z).x = diff((-id Z)(#)cos,x) + diff(sin,x)
      by A3,A1,A4,FDIFF_1:26
      .=(((-id Z)(#)cos)`|Z).x + diff(sin,x) by FDIFF_1:def 8,A3,A5
      .=-cos.x+x*sin.x+ diff(sin,x) by Th44,A2,A5
      .=-cos.x+x*sin.x+cos.x by SIN_COS:69
      .=x*sin.x;
    end;
    hence thesis by A3,A1,A4,FDIFF_1:26;
  end;

:: 47 f.x=x*sin.x+cos.x
theorem
  Z c= dom ((id Z)(#)sin+cos) implies (id Z)(#)sin+cos is_differentiable_on Z &
  for x st x in Z holds (((id Z)(#)sin+cos)`|Z).x =x*cos.x
  proof
    assume A1:Z c= dom ((id Z)(#)sin+cos);
    then Z c= dom ((id Z)(#)sin) /\ dom cos by SEQ_1:def 3;
    then A2:Z c= dom ((id Z)(#)sin) & Z c= dom cos by XBOOLE_1:18;
    then A3:(id Z)(#)sin is_differentiable_on Z &
    for x st x in Z holds (((id Z)(#)sin)`|Z).x =sin.x+ x*cos.x by Th45;
A4: cos is_differentiable_on Z by SIN_COS:72,FDIFF_1:34;
    now let x;
      assume A5:x in Z;
      hence (((id Z)(#)sin+cos)`|Z).x = diff((id Z)(#)sin,x) + diff(cos,x)
      by A3,A1,A4,FDIFF_1:26
      .=(((id Z)(#)sin)`|Z).x + diff(cos,x) by FDIFF_1:def 8,A3,A5
      .=sin.x+ x*cos.x+ diff(cos,x) by Th45,A2,A5
      .=sin.x+ x*cos.x+(-sin.x) by SIN_COS:68
      .=x*cos.x;
    end;
    hence thesis by A3,A1,A4,FDIFF_1:26;
  end;

:: 48 f.x=2*(sin #R (1/2))
theorem
  Z c= dom ((2(#)(( #R (1/2))*sin))) & (for x st x in Z holds sin.x>0)
  implies 2(#)(( #R (1/2))*sin) is_differentiable_on Z &
  for x st x in Z holds ((2(#)(( #R (1/2))*sin))`|Z).x =cos.x*(sin.x) #R (-1/2)
  proof
    assume that A1:Z c= dom ((2(#)(( #R (1/2))*sin))) and
A2: for x st x in Z holds sin.x>0;
A3: Z c= dom (( #R (1/2))*sin) by SEQ_1:def 6,A1;
    now let x;
      assume A4:x in Z;
A5:   sin is_differentiable_in x by SIN_COS:69;
      sin.x >0 by A2,A4;
      hence ( #R (1/2))*sin is_differentiable_in x by TAYLOR_1:22,A5;
    end;
    then A6:( #R (1/2))*sin is_differentiable_on Z by A3,FDIFF_1:16;
    for x st x in Z holds
      ((2(#)(( #R (1/2))*sin))`|Z).x =cos.x*(sin.x) #R (-1/2)
    proof
      let x;
      assume A7:x in Z;
A8:   sin is_differentiable_in x by SIN_COS:69;
A9:   sin.x >0 by A2,A7;
      ((2(#)(( #R (1/2))*sin))`|Z).x =2*diff((( #R (1/2))*sin),x)
      by A6,A1,FDIFF_1:28,A7
      .=2*((1/2)*( (sin.x) #R (1/2-1)) * diff(sin,x)) by TAYLOR_1:22,A9,A8
      .=2*((1/2)*( (sin.x) #R (1/2-1)) *cos.x) by SIN_COS:69
      .=( (sin.x) #R (1/2-1)) *cos.x
      .=cos.x*(sin.x) #R (-1/2);
      hence ((2(#)(( #R (1/2))*sin))`|Z).x =cos.x*(sin.x) #R (-1/2);
    end;
    hence thesis by A1,FDIFF_1:28,A6;
  end;

theorem Th49:
  Z c= dom ((1/2)(#)(( #Z 2)*sin))
  implies (1/2)(#)(( #Z 2)*sin) is_differentiable_on Z &
  for x st x in Z holds (((1/2)(#)(( #Z 2)*sin))`|Z).x =sin.x*cos.x
  proof
    assume A1:Z c= dom ((1/2)(#)(( #Z 2)*sin)); then
A2: Z c= dom (( #Z 2)*sin) by SEQ_1:def 6;
    now let x;
      assume x in Z;
      sin is_differentiable_in x by SIN_COS:69;
      hence ( #Z 2)*sin is_differentiable_in x by TAYLOR_1:3;
    end;
    then A3:( #Z 2)*sin is_differentiable_on Z by A2,FDIFF_1:16;
    for x st x in Z holds (((1/2)(#)(( #Z 2)*sin))`|Z).x =sin.x*cos.x
    proof
      let x;
      assume A4:x in Z;
A5:   sin is_differentiable_in x by SIN_COS:69;
      (((1/2)(#)(( #Z 2)*sin))`|Z).x =(1/2)*diff((( #Z 2)*sin),x)
      by A1,FDIFF_1:28,A3,A4
      .=(1/2)*(2*( (sin.x) #Z (2-1)) * diff(sin,x)) by TAYLOR_1:3,A5
      .=(1/2)*(2*( (sin.x) #Z (2-1)) *cos.x) by SIN_COS:69
      .=( (sin.x) #Z (2-1)) *cos.x
      .=sin.x*cos.x by PREPOWER:45;
      hence thesis;
    end;
    hence thesis by A1,FDIFF_1:28,A3;
  end;

:: 50 f.x=sin.x+(1/2)*(sin.x #Z 2)
theorem
  Z c= dom (sin+(1/2)(#)(( #Z 2)*sin)) &
  (for x st x in Z holds sin.x>0 & sin.x<1) implies
  sin+(1/2)(#)(( #Z 2)*sin) is_differentiable_on Z &
  for x st x in Z holds
    ((sin+(1/2)(#)(( #Z 2)*sin))`|Z).x =(cos.x)|^3/(1-sin.x)
  proof
    assume that A1:Z c= dom (sin+(1/2)(#)(( #Z 2)*sin)) and
A2: for x st x in Z holds sin.x>0 & sin.x<1;
    Z c= dom ((1/2)(#)(( #Z 2)*sin)) /\ dom sin by SEQ_1:def 3,A1;
    then A3:Z c= dom ((1/2)(#)(( #Z 2)*sin)) & Z c= dom sin by XBOOLE_1:18;
    then A4:(1/2)(#)(( #Z 2)*sin) is_differentiable_on Z &
    for x st x in Z holds
      (((1/2)(#)(( #Z 2)*sin))`|Z).x =sin.x*cos.x by Th49;
A5: sin is_differentiable_on Z by SIN_COS:73,FDIFF_1:34;
    now let x;
      assume A6:x in Z;
      then sin.x<1 by A2;
      then A7:1-sin.x>0 by XREAL_1:52;
      ((sin+(1/2)(#)(( #Z 2)*sin))`|Z).x
      = diff(sin,x) + diff((1/2)(#)(( #Z 2)*sin),x)
        by A4,A1,FDIFF_1:26,A5,A6
      .=cos.x + diff((1/2)(#)(( #Z 2)*sin),x) by SIN_COS:69
      .=cos.x +(((1/2)(#)(( #Z 2)*sin))`|Z).x by FDIFF_1:def 8,A4,A6
      .=cos.x +sin.x*cos.x by Th49,A3,A6
      .=cos.x*(1+sin.x)*(1-sin.x)/(1-sin.x) by A7,XCMPLX_1:90
      .=cos.x*((1+sin.x)*(1-sin.x))/(1-sin.x)
      .=cos.x*(1-(sin.x)^2)/(1-sin.x)
      .=cos.x*(1-(sin(x))^2)/(1-sin.x) by SIN_COS:def 21
      .=cos.x*(1-(sin(x)*sin(x)))/(1-sin.x)
      .=cos.x*(cos(x)*cos(x))/(1-sin.x) by SIN_COS4:7
      .=cos.x*((cos(x))|^2)/(1-sin.x) by WSIERP_1:2
      .=(cos.x*((cos.x)|^2))/(1-sin.x) by SIN_COS:def 23
      .=((cos.x)|^(2+1))/(1-sin.x) by NEWTON:11
      .=(cos.x)|^3/(1-sin.x);
      hence ((sin+(1/2)(#)(( #Z 2)*sin))`|Z).x=(cos.x)|^3/(1-sin.x);
    end;
    hence thesis by A4,A1,FDIFF_1:26,A5;
  end;

:: 51 f.x=(1/2)*(sin.x #Z 2)-cos.x
theorem
  Z c= dom ((1/2)(#)(( #Z 2)*sin)-cos) & (for x st x in Z holds sin.x>0
  & cos.x<1) implies ((1/2)(#)(( #Z 2)*sin)-cos) is_differentiable_on Z &
  for x st x in Z holds
  ((((1/2)(#)(( #Z 2)*sin)-cos))`|Z).x =(sin.x)|^3/(1-cos.x)
  proof
    assume that A1:Z c= dom ((1/2)(#)(( #Z 2)*sin)-cos) and
A2: for x st x in Z holds sin.x>0 & cos.x<1;
    Z c= dom ((1/2)(#)(( #Z 2)*sin)) /\ dom cos by SEQ_1:def 4,A1;
    then A3:Z c= dom ((1/2)(#)(( #Z 2)*sin)) & Z c= dom cos by XBOOLE_1:18;
    then A4:(1/2)(#)(( #Z 2)*sin) is_differentiable_on Z &
    for x st x in Z holds
      (((1/2)(#)(( #Z 2)*sin))`|Z).x =sin.x*cos.x by Th49;
A5: cos is_differentiable_on Z by SIN_COS:72,FDIFF_1:34;
    now let x;
      assume A6:x in Z;
      then cos.x<1 by A2;
      then A7:1-cos.x>0 by XREAL_1:52;
      (((1/2)(#)(( #Z 2)*sin)-cos)`|Z).x
      = diff((1/2)(#)(( #Z 2)*sin),x) - diff(cos,x) by A4,A1,FDIFF_1:27,A5,A6
      .=diff((1/2)(#)(( #Z 2)*sin),x)-(-sin.x) by SIN_COS:68
      .=(((1/2)(#)(( #Z 2)*sin))`|Z).x-(-sin.x) by FDIFF_1:def 8,A4,A6
      .=sin.x*cos.x--sin.x by Th49,A3,A6
      .=sin.x*(1+cos.x)*(1-cos.x)/(1-cos.x) by A7,XCMPLX_1:90
      .=sin.x*((1+cos.x)*(1-cos.x))/(1-cos.x)
      .=sin.x*(1-(cos.x)^2)/(1-cos.x)
      .=sin.x*(1-(cos(x))^2)/(1-cos.x) by SIN_COS:def 23
      .=sin.x*(1-(cos(x)*cos(x)))/(1-cos.x)
      .=sin.x*(sin(x)*sin(x))/(1-cos.x) by SIN_COS4:6
      .=sin.x*((sin(x))|^2)/(1-cos.x) by WSIERP_1:2
      .=(sin.x*((sin.x)|^2))/(1-cos.x) by SIN_COS:def 21
      .=((sin.x)|^(2+1))/(1-cos.x) by NEWTON:11
      .=(sin.x)|^3/(1-cos.x);
      hence (((1/2)(#)(( #Z 2)*sin)-cos)`|Z).x=(sin.x)|^3/(1-cos.x);
    end;
    hence thesis by A4,A1,FDIFF_1:27,A5;
  end;

:: 52 f.x=sin.x-(1/2)*(sin #Z 2)
theorem
  Z c= dom (sin-(1/2)(#)(( #Z 2)*sin)) & (for x st x in Z holds sin.x>0
  & sin.x>-1) implies sin-(1/2)(#)(( #Z 2)*sin) is_differentiable_on Z &
  for x st x in Z holds
    ((sin-(1/2)(#)(( #Z 2)*sin))`|Z).x =(cos.x)|^3/(1+sin.x)
  proof
    assume that A1:Z c= dom (sin-(1/2)(#)(( #Z 2)*sin)) and
A2: for x st x in Z holds sin.x>0 & sin.x>-1;
    Z c= dom ((1/2)(#)(( #Z 2)*sin)) /\ dom sin by SEQ_1:def 4,A1;
    then A3:Z c= dom ((1/2)(#)(( #Z 2)*sin)) & Z c= dom sin by XBOOLE_1:18;
    then A4:(1/2)(#)(( #Z 2)*sin) is_differentiable_on Z &
    for x st x in Z holds
      (((1/2)(#)(( #Z 2)*sin))`|Z).x =sin.x*cos.x by Th49;
A5: sin is_differentiable_on Z by SIN_COS:73,FDIFF_1:34;
    now let x;
      assume A6:x in Z;
      then sin.x>-1 by A2;
      then A7:sin.x--1>0 by XREAL_1:52;
      ((sin-(1/2)(#)(( #Z 2)*sin))`|Z).x
      = diff(sin,x) - diff((1/2)(#)(( #Z 2)*sin),x)
        by A4,A1,FDIFF_1:27,A5,A6
      .=cos.x - diff((1/2)(#)(( #Z 2)*sin),x) by SIN_COS:69
      .=cos.x -(((1/2)(#)(( #Z 2)*sin))`|Z).x by FDIFF_1:def 8,A4,A6
      .=cos.x -sin.x*cos.x by A3,Th49,A6
      .=cos.x*(1-sin.x)*(1+sin.x)/(1+sin.x) by A7,XCMPLX_1:90
      .=cos.x*((1+sin.x)*(1-sin.x))/(1+sin.x)
      .=cos.x*(1-(sin.x)^2)/(1+sin.x)
      .=cos.x*(1-(sin(x))^2)/(1+sin.x) by SIN_COS:def 21
      .=cos.x*(1-(sin(x)*sin(x)))/(1+sin.x)
      .=cos.x*(cos(x)*cos(x))/(1+sin.x) by SIN_COS4:7
      .=cos.x*((cos(x))|^2)/(1+sin.x) by WSIERP_1:2
      .=(cos.x*((cos.x)|^2))/(1+sin.x) by SIN_COS:def 23
      .=((cos.x)|^(2+1))/(1+sin.x) by NEWTON:11
      .=(cos.x)|^3/(1+sin.x);
      hence ((sin-(1/2)(#)(( #Z 2)*sin))`|Z).x=(cos.x)|^3/(1+sin.x);
    end;
    hence thesis by A4,A1,FDIFF_1:27,A5;
  end;

:: 53 f.x=-cos.x-(1/2)*(sin.x #Z 2)
theorem
  Z c= dom (-cos-(1/2)(#)(( #Z 2)*sin)) &
  (for x st x in Z holds sin.x>0 & cos.x>-1) implies
  -cos-(1/2)(#)(( #Z 2)*sin) is_differentiable_on Z &
  for x st x in Z holds
    ((-cos-(1/2)(#)(( #Z 2)*sin))`|Z).x =(sin.x)|^3/(1+cos.x)
  proof
    assume that A1:Z c= dom (-cos-(1/2)(#)(( #Z 2)*sin)) and
A2: for x st x in Z holds sin.x>0 & cos.x>-1;
    Z c= dom ((1/2)(#)(( #Z 2)*sin)) /\ dom (-cos) by SEQ_1:def 4,A1;
    then A3:Z c= dom ((1/2)(#)(( #Z 2)*sin)) & Z c= dom (-cos) by XBOOLE_1:18;
    then A4:(1/2)(#)(( #Z 2)*sin) is_differentiable_on Z &
    for x st x in Z holds
      (((1/2)(#)(( #Z 2)*sin))`|Z).x =sin.x*cos.x by Th49;
A5: (-1)(#)cos=-cos by RFUNCT_1:38;
A6: cos is_differentiable_on Z by SIN_COS:72,FDIFF_1:34;
A7: -cos is_differentiable_on Z & for x st x in Z holds
    ((-cos)`|Z).x =(-1)*diff(cos,x) by A5,A3,FDIFF_1:28,A6;
    now let x;
      assume A8:x in Z;
      then cos.x>-1 by A2;
      then A9:cos.x--1>0 by XREAL_1:52;
      ((-cos-(1/2)(#)(( #Z 2)*sin))`|Z).x
      =diff((-cos),x)-diff((1/2)(#)(( #Z 2)*sin),x) by A7,A4,A1,FDIFF_1:27,A8
      .=((-cos)`|Z).x-diff((1/2)(#)(( #Z 2)*sin),x) by FDIFF_1:def 8,A7,A8
      .=(-1)*diff(cos,x)-diff((1/2)(#)(( #Z 2)*sin),x)
      by A5,A3,FDIFF_1:28,A6,A8
      .=(-1)*(-sin.x)-diff((1/2)(#)(( #Z 2)*sin),x) by SIN_COS:68
      .=sin.x-(((1/2)(#)(( #Z 2)*sin))`|Z).x by FDIFF_1:def 8,A4,A8
      .=sin.x-sin.x*cos.x by Th49,A3,A8
      .=sin.x*(1-cos.x)*(1+cos.x)/(1+cos.x) by A9,XCMPLX_1:90
      .=sin.x*((1+cos.x)*(1-cos.x))/(1+cos.x)
      .=sin.x*(1-(cos.x)^2)/(1+cos.x)
      .=sin.x*(1-(cos(x))^2)/(1+cos.x) by SIN_COS:def 23
      .=sin.x*(1-(cos(x)*cos(x)))/(1+cos.x)
      .=sin.x*(sin(x)*sin(x))/(1+cos.x) by SIN_COS4:6
      .=sin.x*((sin(x))|^2)/(1+cos.x) by WSIERP_1:2
      .=(sin.x*((sin.x)|^2))/(1+cos.x) by SIN_COS:def 21
      .=((sin.x)|^(2+1))/(1+cos.x) by NEWTON:11
      .=(sin.x)|^3/(1+cos.x);
      hence ((-cos-(1/2)(#)(( #Z 2)*sin))`|Z).x=(sin.x)|^3/(1+cos.x);
    end;
    hence thesis by A7,A4,A1,FDIFF_1:27;
  end;

:: 54 f.x=(1/n)*(sin.x #Z n)
theorem
  Z c= dom ((1/n)(#)(( #Z n)*sin)) & n>0
  implies (1/n)(#)(( #Z n)*sin) is_differentiable_on Z &
  for x st x in Z holds (((1/n)(#)(( #Z n)*sin))`|Z).x =(sin.x) #Z (n-1) *cos.x
  proof
    assume that A1:Z c= dom ((1/n)(#)(( #Z n)*sin)) and
A2: n>0;
A3: Z c= dom (( #Z n)*sin) by SEQ_1:def 6,A1;
    now let x;
      assume x in Z;
      sin is_differentiable_in x by SIN_COS:69;
      hence ( #Z n)*sin is_differentiable_in x by TAYLOR_1:3;
    end;
    then A4:( #Z n)*sin is_differentiable_on Z by A3,FDIFF_1:16;
    for x st x in Z holds
      (((1/n)(#)(( #Z n)*sin))`|Z).x =(sin.x) #Z (n-1) *cos.x
    proof
      let x;
      assume A5:x in Z;
A6:   sin is_differentiable_in x by SIN_COS:69;
      (((1/n)(#)(( #Z n)*sin))`|Z).x =(1/n)*diff((( #Z n)*sin),x)
        by A4,A1,FDIFF_1:28,A5
      .=(1/n)*(n*( (sin.x) #Z (n-1)) * diff(sin,x)) by TAYLOR_1:3,A6
      .=(1/n)*(n*( (sin.x) #Z (n-1)) *cos.x) by SIN_COS:69
      .=((1/n)*n)*( (sin.x) #Z (n-1)) *cos.x
      .=((n")*n)*( (sin.x) #Z (n-1)) *cos.x by XCMPLX_1:217
      .=1*( (sin.x) #Z (n-1)) *cos.x by A2,XCMPLX_0:def 7
      .=(sin.x) #Z (n-1) *cos.x;
      hence thesis;
    end;
    hence thesis by A4,A1,FDIFF_1:28;
  end;

:: exponential function
:: 55 f.x=exp.x*(x-1)
theorem
  Z c= dom (exp(#)f) & (for x st x in Z holds f.x=x-1) implies
  exp(#)f is_differentiable_on Z &
  for x st x in Z holds ((exp(#)f)`|Z).x = x*(exp.x)
  proof
    assume that A1:Z c= dom (exp(#)f) and
A2: for x st x in Z holds f.x=x-1;
    Z c= dom f /\ dom exp by SEQ_1:def 5,A1;
    then A3:Z c= dom f by XBOOLE_1:18;
A4: for x st x in Z holds f.x = 1*x+-1
    proof let x;
      assume A5:x in Z;
      1*x+-1=1*x-1;
      hence thesis by A2,A5;
    end;
    then A6:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = 1 by FDIFF_1:31,A3;
A7: exp is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
    now let x;
      assume A8:x in Z;
      hence ((exp(#)f)`|Z).x = (f.x)*diff(exp,x) + (exp.x)*diff(f,x)
      by A1,A6,A7,FDIFF_1:29
      .=(x-1)*diff(exp,x)+ (exp.x)*diff(f,x) by A2,A8
      .=(x-1)*(exp.x)+ (exp.x)*diff(f,x) by TAYLOR_1:16
      .=(x-1)*(exp.x)+(exp.x)*(f`|Z).x by A8,A6,FDIFF_1:def 8
      .=(x-1)*(exp.x)+(exp.x)*1 by A8,FDIFF_1:31,A3,A4
      .=x*(exp.x);
    end;
    hence thesis by A1,A6,FDIFF_1:29,A7;
  end;

:: 56 f.x=ln(exp.x/(exp.x+1))
theorem
  Z c= dom ((log_ number_e)*(exp/(exp+f))) & (for x st x in Z holds f.x=1)
  implies (log_ number_e)*(exp/(exp+f)) is_differentiable_on Z &
  for x st x in Z holds (((log_ number_e)*(exp/(exp+f)))`|Z).x = 1/(exp.x+1)
  proof
    assume that A1:Z c= dom ((log_ number_e)*(exp/(exp+f))) and
A2: for x st x in Z holds f.x=1;
    for y st y in Z holds y in dom (exp/(exp+f)) by FUNCT_1:21,A1;
    then Z c= dom (exp/(exp+f)) by TARSKI:def 3;
    then Z c= dom exp /\ (dom (exp+f) \ (exp+f)"{0}) by RFUNCT_1:def 4;
    then A3:Z c= dom exp & Z c= dom (exp+f) \ (exp+f)"{0} by XBOOLE_1:18;
    dom ((exp+f)^) c= dom (exp+f) by RFUNCT_1:11;
    then dom (exp+f) \ (exp+f)"{0} c= dom (exp+f) by RFUNCT_1:def 8;
    then A4:Z c= dom (exp+f) by XBOOLE_1:1,A3;
    then Z c= dom exp /\ dom f by SEQ_1:def 3;
    then A5:Z c= dom f by XBOOLE_1:18;
A6: for x st x in Z holds f.x=0*x+1 by A2;
    then A7:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A5;
A8: exp is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
    then A9:exp+f is_differentiable_on Z & for x st x in Z holds
    ((exp+f)`|Z).x = diff(exp,x) + diff(f,x) by A7,A4,FDIFF_1:26;
A10: for x st x in Z holds ((exp+f)`|Z).x =exp.x
     proof
       let x;
       assume A11:x in Z;
       hence ((exp+f)`|Z).x = diff(exp,x) + diff(f,x) by A8,A7,A4,FDIFF_1:26
       .=exp.x+ diff(f,x) by SIN_COS:70
       .=exp.x+(f`|Z).x by A11,FDIFF_1:def 8,A7
       .=exp.x+0 by A11,FDIFF_1:31,A5,A6
       .=exp.x;
     end;
A12:  for x st x in Z holds (exp+f).x>0
     proof
       let x;
       assume A13:x in Z;
       then A14:(exp+f).x=exp.x + f.x by SEQ_1:def 3,A4
         .=exp.x +1 by A2,A13;
       exp.x>0 by SIN_COS:59;
       hence (exp+f).x>0 by A14,XREAL_1:36;
     end;
     then for x st x in Z holds (exp+f).x<>0;
     then A15:exp/(exp+f) is_differentiable_on Z by A8,A9,FDIFF_2:21;
A16: for x st x in Z holds ((exp/(exp+f))`|Z).x =exp.x/(exp.x +1)^2
     proof
       let x;
       assume A17:x in Z;
A18:   exp is_differentiable_in x by SIN_COS:70;
A19:   exp+f is_differentiable_in x by A9,A17,FDIFF_1:16;
A20:    (exp+f).x=exp.x + f.x by SEQ_1:def 3,A17,A4
       .=exp.x +1 by A2,A17;
       then (exp+f).x <>0 & (exp+f).x=exp.x +1 by A12,A17;
       then diff(exp/(exp+f),x)
       =(diff(exp,x) * (exp+f).x - diff((exp+f),x) * exp.x)/((exp+f).x)^2
         by FDIFF_2:14,A18,A19
       .=(exp.x* (exp+f).x-diff((exp+f),x) * exp.x)/((exp+f).x)^2 by SIN_COS:70
       .=(exp.x*(exp.x +1)-((exp+f)`|Z).x * exp.x)/(exp.x +1)^2
         by A17,A20,A9,FDIFF_1:def 8
       .=(exp.x*(exp.x +1)-exp.x * exp.x)/(exp.x +1)^2 by A10,A17
       .=exp.x/(exp.x +1)^2;
       hence ((exp/(exp+f))`|Z).x =exp.x/(exp.x +1)^2 by A15,A17,FDIFF_1:def 8;
     end;
A21: for x st x in Z holds (exp/(exp+f)).x >0
     proof
       let x;
       assume A22:x in Z;
       then x in dom (exp/(exp+f)) by FUNCT_1:21,A1;
       then A23:(exp/(exp+f)).x=exp.x*((exp+f).x)" by RFUNCT_1:def 4
       .=exp.x*(1/(exp+f).x) by XCMPLX_1:217
       .=exp.x/(exp+f).x by XCMPLX_1:100;
A24:    (exp+f).x>0 by A12,A22;
       exp.x>0 by SIN_COS:59;
       hence (exp/(exp+f)).x >0 by A23,XREAL_1:141,A24;
     end;
A25:  for x st x in Z holds (log_ number_e)*(exp/(exp+f)) is_differentiable_in
x
     proof
       let x;
       assume A26:x in Z;
       then A27:exp/(exp+f) is_differentiable_in x by FDIFF_1:16,A15;
       (exp/(exp+f)).x >0 by A21,A26;
       hence thesis by TAYLOR_1:20,A27;
     end;
     then A28:(log_ number_e)*(exp/(exp+f)) is_differentiable_on Z
       by A1,FDIFF_1:16;
     for x st x in Z holds
       (((log_ number_e)*(exp/(exp+f)))`|Z).x = 1/(exp.x+1)
     proof
       let x;
       assume A29:x in Z;
       x in dom (exp/(exp+f)) by FUNCT_1:21,A1,A29;
       then A30:(exp/(exp+f)).x=(exp.x)*((exp+f).x)" by RFUNCT_1:def 4
       .=(exp.x)*(1/(exp+f).x) by XCMPLX_1:217
       .=(exp.x)/(exp+f).x by XCMPLX_1:100
       .=(exp.x)/(exp.x + f.x) by SEQ_1:def 3,A4,A29
       .=(exp.x)/(exp.x +1) by A2,A29;
A31:    (exp/(exp+f)) is_differentiable_in x by FDIFF_1:16,A15,A29;
A32:   (exp/(exp+f)).x >0 by A29,A21;
A33:   exp.x/(exp.x +1)>0 by A30,A29,A21;
       diff((log_ number_e)*(exp/(exp+f)),x)
       =diff((exp/(exp+f)),x)/((exp/(exp+f)).x) by TAYLOR_1:20,A31,A32
       .=((exp/(exp+f))`|Z).x/((exp/(exp+f)).x) by A15,A29,FDIFF_1:def 8
       .=(exp.x/(exp.x +1)^2)/(exp.x/(exp.x +1)) by A30,A16,A29
       .=(exp.x/((exp.x +1)*(exp.x +1)))/(exp.x/(exp.x +1))
       .=(exp.x/(exp.x +1)/(exp.x +1))/(exp.x/(exp.x +1)) by XCMPLX_1:79
       .=(exp.x/(exp.x +1))/(exp.x/(exp.x +1))/(exp.x +1) by XCMPLX_1:48
       .=1/(exp.x +1) by A33,XCMPLX_1:60;
       hence thesis by A29,A28,FDIFF_1:def 8;
     end;
     hence thesis by A1,FDIFF_1:16,A25;
   end;

:: 57 f.x=ln((exp.x-1)/exp.x)
theorem
  Z c= dom ((log_ number_e)*((exp-f)/exp)) &
  (for x st x in Z holds f.x=1 & (exp-f).x>0) implies
    (log_ number_e)*((exp-f)/exp) is_differentiable_on Z &
    for x st x in Z holds (((log_ number_e)*((exp-f)/exp))`|Z).x = 1/(exp.x-1)
  proof
    assume that
A1: Z c= dom ((log_ number_e)*((exp-f)/exp)) and
A2: for x st x in Z holds f.x=1 & (exp-f).x>0;
    for y st y in Z holds y in dom ((exp-f)/exp) by FUNCT_1:21,A1;
    then Z c= dom ((exp-f)/exp) by TARSKI:def 3;
    then Z c= dom (exp-f) /\ (dom exp \ (exp)"{0}) by RFUNCT_1:def 4;
    then A3:Z c= dom (exp-f) & Z c= dom exp \ (exp)"{0} by XBOOLE_1:18;
    then Z c= dom exp /\ dom f by SEQ_1:def 4;
    then A4:Z c= dom f by XBOOLE_1:18;
A5: for x st x in Z holds f.x=0*x+1 by A2;
    then A6:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A4;
A7: exp is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
    then A8:exp-f is_differentiable_on Z & for x st x in Z holds
    ((exp-f)`|Z).x = diff(exp,x) - diff(f,x) by A6,A3,FDIFF_1:27;
A9:for x st x in Z holds ((exp-f)`|Z).x =exp.x
    proof
      let x;
      assume A10:x in Z;
      hence ((exp-f)`|Z).x = diff(exp,x) - diff(f,x) by A7,A6,A3,FDIFF_1:27
          .= exp.x- diff(f,x) by SIN_COS:70
          .= exp.x-(f`|Z).x by A10,FDIFF_1:def 8,A6
          .= exp.x-0 by A10,FDIFF_1:31,A4,A5
          .= exp.x;
    end;
    for x st x in Z holds exp.x<>0 by SIN_COS:59;
    then A11:(exp-f)/exp is_differentiable_on Z by A7,A8,FDIFF_2:21;
A12:for x st x in Z holds (((exp-f)/exp)`|Z).x=1/exp.x
    proof
      let x;
      assume A13:x in Z;
A14:  exp is_differentiable_in x by SIN_COS:70;
A15:  exp-f is_differentiable_in x by A8,A13,FDIFF_1:16;
A16:   (exp-f).x=exp.x - f.x by SEQ_1:def 4,A13,A3
            .=exp.x -1 by A2,A13;
      then A17:exp.x <>0 & (exp-f).x=exp.x-1 by SIN_COS:59;
      then diff((exp-f)/exp,x)
         =(diff((exp-f),x) * exp.x - diff(exp,x) *(exp-f).x)/(exp.x)^2
           by FDIFF_2:14,A14,A15
        .=(((exp-f)`|Z).x* exp.x - diff(exp,x) *(exp-f).x)/(exp.x)^2
           by A8,A13,FDIFF_1:def 8
        .=(exp.x* exp.x - diff(exp,x) *(exp-f).x)/(exp.x)^2 by A9,A13
        .=(exp.x* exp.x - exp.x*(exp.x-1))/(exp.x)^2 by SIN_COS:70,A16
        .=(exp.x)/((exp.x)*(exp.x))
        .=(exp.x)/(exp.x)/(exp.x) by XCMPLX_1:79
        .=1/exp.x by A17,XCMPLX_1:60;
      hence (((exp-f)/exp)`|Z).x=1/(exp.x) by A11,A13,FDIFF_1:def 8;
    end;
A18:for x st x in Z holds ((exp-f)/exp).x >0
    proof
      let x;
      assume A19:x in Z;
      then x in dom ((exp-f)/exp) by FUNCT_1:21,A1;
      then A20:((exp-f)/exp).x=(exp-f).x*(exp.x)" by RFUNCT_1:def 4
        .=(exp-f).x*(1/exp.x) by XCMPLX_1:217
        .=(exp-f).x/exp.x by XCMPLX_1:100;
A21:   (exp-f).x>0 by A2,A19;
      exp.x>0 by SIN_COS:59;
      hence ((exp-f)/exp).x >0 by A20,XREAL_1:141,A21;
    end;
A22: for x st x in Z holds (log_ number_e)*((exp-f)/exp)
       is_differentiable_in x
    proof
      let x;
      assume A23:x in Z;
      then A24:(exp-f)/exp is_differentiable_in x by FDIFF_1:16,A11;
      ((exp-f)/exp).x >0 by A18,A23;
      hence (log_ number_e)*((exp-f)/exp) is_differentiable_in x
        by TAYLOR_1:20,A24;
    end;
then A25:(log_ number_e)*((exp-f)/exp) is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds (((log_ number_e)*((exp-f)/exp))`|Z).x = 1/(exp.x-1)
    proof
      let x;
      assume A26:x in Z;
      x in dom ((exp-f)/exp) by FUNCT_1:21,A1,A26;
      then A27:((exp-f)/exp).x=(exp-f).x*(exp.x)" by RFUNCT_1:def 4
        .=(exp-f).x*(1/exp.x) by XCMPLX_1:217
        .=(exp-f).x/exp.x by XCMPLX_1:100
        .=(exp.x-f.x)/exp.x by SEQ_1:def 4,A3,A26
        .=(exp.x-1)/exp.x by A2,A26;
A28:   ((exp-f)/exp) is_differentiable_in x by FDIFF_1:16,A11,A26;
A29:  ((exp-f)/exp).x >0 by A26,A18;
A30:  exp.x>0 by SIN_COS:59;
      diff((log_ number_e)*((exp-f)/exp),x)
        =diff(((exp-f)/exp),x)/(((exp-f)/exp).x) by TAYLOR_1:20,A28,A29
       .=(((exp-f)/exp)`|Z).x/(((exp-f)/exp).x) by A11,A26,FDIFF_1:def 8
       .=(1/exp.x)/((exp.x-1)/exp.x) by A27,A12,A26
       .=1/(exp.x-1) by A30,XCMPLX_1:55;
      hence thesis by A26,A25,FDIFF_1:def 8;
    end;
    hence thesis by A1,FDIFF_1:16,A22;
  end;
