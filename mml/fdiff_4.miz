:: Several Differentiable Formulas of Special Functions
::  by Yan Zhang and Xiquan Liang
::
:: Received July 6, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies PRE_TOPC, BOOLE, FUNCT_1, ARYTM_1, ARYTM_3, RELAT_1, ORDINAL2,
      PARTFUN1, SEQ_1, SIN_COS, FDIFF_1, PREPOWER, POWER, GROUP_1, SQUARE_1,
      TAYLOR_1, FINSEQ_1, SIN_COS4;
 notations TARSKI, XBOOLE_0, SUBSET_1, RELAT_1, PARTFUN1, PARTFUN2, RCOMP_1,
      XREAL_0, NUMBERS, REAL_1, NAT_1, SEQ_1, FDIFF_1, PREPOWER, POWER,
      TAYLOR_1, SIN_COS, RFUNCT_1, SQUARE_1, SIN_COS4;
 constructors REAL_1, NAT_1, PARTFUN1, PARTFUN2, LIMFUNC1, RCOMP_1, TAYLOR_1,
      SERIES_1, PREPOWER, FDIFF_2, SIN_COS, RFUNCT_1, SIN_COS4;
 registrations RELSET_1, XREAL_0, MEMBERED, RCOMP_1, INT_1, SEQ_1;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;
 theorems FDIFF_2, TAYLOR_1, XBOOLE_1, XCMPLX_1, SEQ_1, FDIFF_1, FUNCT_1,
      TARSKI, PREPOWER, RFUNCT_1, SQUARE_1, XREAL_1, XCMPLX_0, NEWTON, SIN_COS,
      SIN_COS5, SIN_COS4, WSIERP_1;

begin

 reserve c,y,X for set;
 reserve x,r,a,b,c for Real;
 reserve n for Nat;
 reserve Z for open Subset of REAL;
 reserve f,f1,f2 for PartFunc of REAL,REAL;

:: 1 f.x=ln(a+x)
theorem th1:
  Z c= dom ((log_ number_e)*f) &
  (for x st x in Z holds f.x = a+x & f.x > 0) implies
    (log_ number_e)*f is_differentiable_on Z &
    for x st x in Z holds (((log_ number_e)*f)`|Z).x = 1/(a+x)
proof
  assume A1: Z c= dom ((log_ number_e)*f) &
  for x st x in Z holds f.x = a+x & f.x > 0;
  for y st y in Z holds y in dom f by FUNCT_1:21,A1;
  then A2:Z c= dom f by TARSKI:def 3;
  B1:for x st x in Z holds f.x = 1*x+a by A1;
  then A3:f is_differentiable_on Z &
  for x st x in Z holds (f`|Z).x = 1 by FDIFF_1:31,A2;
  B2:for x st x in Z holds (log_ number_e)*f is_differentiable_in x
  proof
    let x;
    assume A4:x in Z;
    then A5:f is_differentiable_in x by FDIFF_1:16,A3;
    f.x >0 by A1,A4;
    hence (log_ number_e)*f is_differentiable_in x by TAYLOR_1:20,A5;
  end;
  then A:(log_ number_e)*f is_differentiable_on Z by A1,FDIFF_1:16;
  for x st x in Z holds (((log_ number_e)*f)`|Z).x = 1/(a+x)
  proof
    let x;
    assume A4:x in Z;
    then A5:f is_differentiable_in x by FDIFF_1:16,A3;
A7: f.x = a+x & f.x >0 by A1,A4;
    diff((log_ number_e)*f,x) =diff(f,x)/( f.x) by TAYLOR_1:20,A7,A5
      .=(f`|Z).x/( f.x) by A3,A4,FDIFF_1:def 8
      .=1/(a+x) by FDIFF_1:31,A2,B1,A4,A7;
    hence thesis by A4,A,FDIFF_1:def 8;
  end;
  hence thesis by A1,FDIFF_1:16,B2;
end;

:: 2 f.x=ln(x-a)
theorem th2:
  Z c= dom ((log_ number_e)*f) &
  (for x st x in Z holds f.x = x-a & f.x >0) implies
    (log_ number_e)*f is_differentiable_on Z &
    for x st x in Z holds (((log_ number_e)*f)`|Z).x = 1/(x-a)
proof
  assume A1:Z c= dom ((log_ number_e)*f) &
  for x st x in Z holds f.x = x-a & f.x >0;
  for y st y in Z holds y in dom f by FUNCT_1:21,A1;
  then A2:Z c= dom f by TARSKI:def 3;
B1:for x st x in Z holds f.x = 1*x+-a
  proof let x;
    assume A4:x in Z;
    1*x+-a=1*x-a;
    hence f.x = 1*x+-a by A1,A4;
  end;
  then A3:f is_differentiable_on Z &
  for x st x in Z holds (f`|Z).x = 1 by FDIFF_1:31,A2;
B2:for x st x in Z holds (log_ number_e)*f is_differentiable_in x
  proof
    let x;
    assume A4:x in Z;
    then A5:f is_differentiable_in x by FDIFF_1:16,A3;
    f.x >0 by A1,A4;
    hence (log_ number_e)*f is_differentiable_in x by TAYLOR_1:20,A5;
  end;
  then A:(log_ number_e)*f is_differentiable_on Z by A1,FDIFF_1:16;
  for x st x in Z holds (((log_ number_e)*f)`|Z).x = 1/(x-a)
  proof
    let x;
    assume A4:x in Z;
    then A5:f is_differentiable_in x by FDIFF_1:16,A3;
A7: f.x = x-a & f.x >0 by A1,A4;
    diff((log_ number_e)*f,x) =diff(f,x)/( f.x) by TAYLOR_1:20,A7,A5
      .=(f`|Z).x/( f.x) by A3,A4,FDIFF_1:def 8
      .=1/(x-a) by B1,FDIFF_1:31,A2,A4,A7;
    hence (((log_ number_e)*f)`|Z).x = 1/(x-a) by A4,A,FDIFF_1:def 8;
  end;
  hence thesis by A1,FDIFF_1:16,B2;
end;

:: 3 f.x=-ln(a-x)
theorem
  Z c= dom (-((log_ number_e)*f)) &
  (for x st x in Z holds f.x =a-x & f.x >0) implies
    - ((log_ number_e)*f) is_differentiable_on Z &
    for x st x in Z holds ((-((log_ number_e)*f))`|Z).x = 1/(a-x)
proof
  assume B1:Z c= dom (-((log_ number_e)*f)) &
  (for x st x in Z holds f.x =a-x & f.x > 0);
B2:-((log_ number_e)*f) = (-1)(#)((log_ number_e)*f) by RFUNCT_1:38;
A1:Z c= dom ((-1)(#)((log_ number_e)*f)) &
  (for x st x in Z holds f.x =a-x & f.x >0) by B1,RFUNCT_1:38;
  now let y;
    assume y in Z;
    then y in dom ( (-1)(#)((log_ number_e)*f)) by B1,B2;
    hence y in dom ((log_ number_e)*f) by SEQ_1:def 6;
  end;
  then A10:Z c=dom ((log_ number_e)*f) by TARSKI:def 3;
  for y st y in Z holds y in dom f by FUNCT_1:21,A10;
  then A2:Z c= dom f by TARSKI:def 3;
B4:for x st x in Z holds f.x =(-1)*x+a
  proof
    let x;
    assume x in Z;
    then f.x =a-x by B1;
    hence f.x =(-1)*x+a;
  end;
  then A3:f is_differentiable_on Z &
  for x st x in Z holds (f`|Z).x = -1 by FDIFF_1:31,A2;
  now let x;
    assume A4:x in Z;
    then A5:f is_differentiable_in x by FDIFF_1:16,A3;
    f.x >0 by B1,A4;
    hence (log_ number_e)*f is_differentiable_in x by TAYLOR_1:20,A5;
  end;
  then B5:(log_ number_e)*f is_differentiable_on Z by A10,FDIFF_1:16;
  then A12:(-1)(#)((log_ number_e)*f) is_differentiable_on Z &
  for x st x in Z holds (((-1)(#)((log_ number_e)*f))`|Z).x
     =(-1)*diff((log_ number_e)*f,x) by FDIFF_1:28,A1;
  for x st x in Z holds ((-((log_ number_e)*f))`|Z).x =1/(a-x)
  proof
    let x;
    assume A4:x in Z;
    then A7: f.x =a-x & f.x >0 by B1;
    f is_differentiable_in x by FDIFF_1:16,A3,A4;
    then A8: diff((log_ number_e)*f,x) =diff(f,x)/( f.x) by TAYLOR_1:20,A7
        .=(f`|Z).x/( f.x) by A3,A4,FDIFF_1:def 8
        .=(-1)/(a-x) by B4,FDIFF_1:31,A2,A4,A7;
    (((-1)(#)((log_ number_e)*f))`|Z).x
         =(-1)*((-1)/(a-x)) by A8,FDIFF_1:28,A1,B5,A4
        .=(-1)*(-1)/(a-x) by XCMPLX_1:75
        .=1/(a-x);
    hence ((-((log_ number_e)*f))`|Z).x =1/(a-x) by RFUNCT_1:38;
  end;
  hence thesis by A12,RFUNCT_1:38;
end;

:: 4 f.x=x-a*ln(a+x)
theorem
  Z c= dom (id Z - a(#)f) & f = (log_ number_e)*f1 &
  (for x st x in Z holds f1.x=a+x & f1.x>0) implies
  (id Z - a(#)f) is_differentiable_on Z &
  for x st x in Z holds ((id Z - a(#)f)`|Z).x = x/(a+x)
  proof
    assume A1:Z c= dom (id Z - a(#)f) & f = (log_ number_e)*f1 &
    (for x st x in Z holds f1.x=a+x & f1.x>0);
    then Z c= dom id Z /\ dom (a(#)f) by SEQ_1:def 4;
    then A2:Z c= dom id Z & Z c= dom (a(#)f) by XBOOLE_1:18;
    then A4:Z c= dom ((log_ number_e)*f1) by A1,SEQ_1:def 6;
B6: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;
    then A3:id Z is_differentiable_on Z & for x st x in Z
    holds ((id Z)`|Z).x = 1 by FDIFF_1:31,A2;
A7: f is_differentiable_on Z & for x st x in Z
    holds (f`|Z).x = 1/(a+x) by A4,th1,A1;
    then A8:a(#)f is_differentiable_on Z & for x st x in Z holds
    ((a(#)f)`|Z).x =a*diff(f,x) by A2,FDIFF_1:28;
A9: for x st x in Z holds ((a(#)f)`|Z).x =a/(a+x)
    proof
      let x;
      assume B4:x in Z;
      hence ((a(#)f)`|Z).x=a*diff(f,x) by A7,A2,FDIFF_1:28
      .=a*(f`|Z).x by B4,FDIFF_1:def 8,A7
      .=a*(1/(a+x)) by A4,th1,A1,B4
      .=a/(a+x) by XCMPLX_1:100;
    end;
    for x st x in Z holds ((id Z - a(#)f)`|Z).x = x/(a+x)
    proof
      let x;
      assume B4:x in Z;
      then B5:f1.x=a+x & f1.x>0 by A1;
      ((id Z - a(#)f)`|Z).x= diff(id Z,x) - diff(a(#)f,x)
      by A3,A8,A1,FDIFF_1:27,B4
      .=((id Z)`|Z).x - diff(a(#)f,x) by A3,B4,FDIFF_1:def 8
      .=((id Z)`|Z).x - ((a(#)f)`|Z).x by B4,A8,FDIFF_1:def 8
      .=1 - ((a(#)f)`|Z).x by FDIFF_1:31,A2,B6,B4
      .=1 - a/(a+x) by A9,B4
      .=(1*(a+x) - a)/(a+x) by XCMPLX_1:128,B5
      .=x/(a+x);
      hence thesis;
    end;
    hence thesis by A3,A8,A1,FDIFF_1:27;
  end;

:: 5 f.x=(2*a)*ln(a+x)-x
theorem
  Z c= dom ((2*a)(#)f - id Z) & f = (log_ number_e)*f1 &
  (for x st x in Z holds f1.x=a+x & f1.x>0) implies
  ((2*a)(#)f - id Z) is_differentiable_on Z &
  for x st x in Z holds (((2*a)(#)f - id Z)`|Z).x = (a-x)/(a+x)
  proof
    assume A1:Z c= dom ((2*a)(#)f - id Z) & f = (log_ number_e)*f1 &
    (for x st x in Z holds f1.x=a+x & f1.x>0);
    then Z c= dom ((2*a)(#)f) /\ dom id Z by SEQ_1:def 4;
    then A2:Z c= dom id Z & Z c= dom ((2*a)(#)f) by XBOOLE_1:18;
    then A4:Z c= dom ((log_ number_e)*f1) by A1,SEQ_1:def 6;
B1: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;
    then A3:id Z is_differentiable_on Z & for x st x in Z
    holds ((id Z)`|Z).x = 1 by FDIFF_1:31,A2;
A7: f is_differentiable_on Z & for x st x in Z
    holds (f`|Z).x = 1/(a+x) by A4,th1,A1;
    then A8:(2*a)(#)f is_differentiable_on Z & for x st x in Z holds
    (((2*a)(#)f)`|Z).x =(2*a)*diff(f,x) by A2,FDIFF_1:28;
A9: for x st x in Z holds (((2*a)(#)f)`|Z).x =(2*a)/(a+x)
    proof
      let x;
      assume B4:x in Z;
      hence (((2*a)(#)f)`|Z).x =(2*a)*diff(f,x) by A7,A2,FDIFF_1:28
      .=(2*a)*(f`|Z).x by B4,FDIFF_1:def 8,A7
      .=(2*a)*(1/(a+x)) by A4,th1,A1,B4
      .=(2*a)/(a+x) by XCMPLX_1:100;
    end;
    for x st x in Z holds (((2*a)(#)f - id Z)`|Z).x =(a-x)/(a+x)
    proof
      let x;
      assume B4:x in Z;
      then B5:f1.x=a+x & f1.x>0 by A1;
      (((2*a)(#)f - id Z)`|Z).x = diff((2*a)(#)f,x) - diff(id Z,x)
      by A3,A8,A1,FDIFF_1:27,B4
      .=diff((2*a)(#)f,x)-((id Z)`|Z).x by A3,B4,FDIFF_1:def 8
      .=(((2*a)(#)f)`|Z).x-((id Z)`|Z).x by B4,A8,FDIFF_1:def 8
      .=(((2*a)(#)f)`|Z).x - 1 by FDIFF_1:31,A2,B1,B4
      .=(2*a)/(a+x) - 1 by A9,B4
      .=(2*a-1*(a+x))/(a+x) by XCMPLX_1:127,B5
      .=(a-x)/(a+x);
      hence thesis;
    end;
    hence thesis by A3,A8,A1,FDIFF_1:27;
  end;

:: 6 f.x=x-(2*a)*ln(x+a)
theorem
  Z c= dom (id Z - (2*a)(#)f) & f = (log_ number_e)*f1 &
  (for x st x in Z holds f1.x=x+a & f1.x>0) implies
  (id Z - (2*a)(#)f) is_differentiable_on Z &
  for x st x in Z holds ((id Z -(2*a)(#)f)`|Z).x = (x-a)/(x+a)
  proof
    assume A1:Z c= dom (id Z - (2*a)(#)f) & f = (log_ number_e)*f1 &
    (for x st x in Z holds f1.x=x+a & f1.x>0);
    then Z c= dom id Z /\ dom ((2*a)(#)f) by SEQ_1:def 4;
    then A2:Z c= dom id Z & Z c= dom ((2*a)(#)f) by XBOOLE_1:18;
    then A4:Z c= dom ((log_ number_e)*f1) by A1,SEQ_1:def 6;
B2: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;
    then A3:id Z is_differentiable_on Z & for x st x in Z
    holds ((id Z)`|Z).x = 1 by FDIFF_1:31,A2;
B1: for x st x in Z holds f1.x=a+x & f1.x>0 by A1;
    then A7:f is_differentiable_on Z & for x st x in Z
    holds (f`|Z).x = 1/(x+a) by A1,A4,th1;
    then A8:(2*a)(#)f is_differentiable_on Z & for x st x in Z holds
    (((2*a)(#)f)`|Z).x =(2*a)*diff(f,x) by A2,FDIFF_1:28;
A9: for x st x in Z holds (((2*a)(#)f)`|Z).x =(2*a)/(x+a)
    proof
      let x;
      assume B4:x in Z;
      hence (((2*a)(#)f)`|Z).x =(2*a)*diff(f,x) by A2,FDIFF_1:28,A7
      .=(2*a)*(f`|Z).x by B4,FDIFF_1:def 8,A7
      .=(2*a)*(1/(x+a)) by A1,A4,th1,B1,B4
      .=(2*a)/(x+a) by XCMPLX_1:100;
    end;
    for x st x in Z holds ((id Z - (2*a)(#)f)`|Z).x = (x-a)/(x+a)
    proof
      let x;
      assume B4:x in Z;
      then B5:f1.x=x+a & f1.x>0 by A1;
      ((id Z - (2*a)(#)f)`|Z).x = diff(id Z,x) - diff((2*a)(#)f,x)
      by A3,A8,A1,FDIFF_1:27,B4
      .=((id Z)`|Z).x - diff((2*a)(#)f,x) by A3,B4,FDIFF_1:def 8
      .=((id Z)`|Z).x - (((2*a)(#)f)`|Z).x by B4,A8,FDIFF_1:def 8
      .=1 - (((2*a)(#)f)`|Z).x by FDIFF_1:31,A2,B2,B4
      .=1 - (2*a)/(x+a) by A9,B4
      .=(1*(x+a) - (2*a))/(x+a) by XCMPLX_1:128,B5
      .=(x-a)/(x+a);
      hence thesis;
    end;
    hence thesis by A3,A8,A1,FDIFF_1:27;
  end;

:: 7 f.x=x + (2*a)*ln(x-a)
theorem
  Z c= dom (id Z + (2*a)(#)f) & f = (log_ number_e)*f1 &
  (for x st x in Z holds f1.x=x-a & f1.x>0) implies
  (id Z + (2*a)(#)f) is_differentiable_on Z &
  for x st x in Z holds ((id Z +(2*a)(#)f)`|Z).x = (x+a)/(x-a)
  proof
    assume A1:Z c= dom (id Z + (2*a)(#)f) & f = (log_ number_e)*f1 &
    (for x st x in Z holds f1.x=x-a & f1.x>0);
    then Z c= dom id Z /\ dom ((2*a)(#)f) by SEQ_1:def 3;
    then A2:Z c= dom id Z & Z c= dom ((2*a)(#)f) by XBOOLE_1:18;
    then A4:Z c= dom ((log_ number_e)*f1) by A1,SEQ_1:def 6;
B1: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;
    then A3:id Z is_differentiable_on Z & for x st x in Z
    holds ((id Z)`|Z).x = 1 by FDIFF_1:31,A2;
A7: f is_differentiable_on Z & for x st x in Z
    holds (f`|Z).x = 1/(x-a) by A1,A4,th2;
    then A8:(2*a)(#)f is_differentiable_on Z & for x st x in Z holds
    (((2*a)(#)f)`|Z).x =(2*a)*diff(f,x) by A2,FDIFF_1:28;
A9: for x st x in Z holds (((2*a)(#)f)`|Z).x =(2*a)/(x-a)
    proof
      let x;
      assume B4:x in Z;
      hence (((2*a)(#)f)`|Z).x =(2*a)*diff(f,x) by A7,A2,FDIFF_1:28
      .=(2*a)*(f`|Z).x by B4,FDIFF_1:def 8,A7
      .=(2*a)*(1/(x-a)) by A1,A4,th2,B4
      .=(2*a)/(x-a) by XCMPLX_1:100;
    end;
    for x st x in Z holds ((id Z + (2*a)(#)f)`|Z).x = (x+a)/(x-a)
    proof
      let x;
      assume B4:x in Z;
      then B5:f1.x=x-a & f1.x>0 by A1;
      ((id Z + (2*a)(#)f)`|Z).x = diff(id Z,x) + diff((2*a)(#)f,x)
      by A3,A8,A1,FDIFF_1:26,B4
      .=((id Z)`|Z).x + diff((2*a)(#)f,x) by A3,B4,FDIFF_1:def 8
      .=((id Z)`|Z).x + (((2*a)(#)f)`|Z).x by B4,A8,FDIFF_1:def 8
      .=1 + (((2*a)(#)f)`|Z).x by FDIFF_1:31,A2,B1,B4
      .=1 + (2*a)/(x-a) by A9,B4
      .=(1*(x-a) + (2*a))/(x-a) by XCMPLX_1:114,B5
      .=(x+a)/(x-a);
      hence thesis;
    end;
    hence thesis by A3,A8,A1,FDIFF_1:26;
  end;

:: 8 f.x=x+ (a-b)*ln(x+b)
theorem
  Z c= dom (id Z + (a-b)(#)f) & f = (log_ number_e)*f1 &
  (for x st x in Z holds f1.x=x+b & f1.x>0) implies
  (id Z + (a-b)(#)f) is_differentiable_on Z &
  for x st x in Z holds ((id Z +(a-b)(#)f)`|Z).x = (x+a)/(x+b)
  proof
    assume A1:Z c= dom (id Z + (a-b)(#)f) & f = (log_ number_e)*f1 &
    (for x st x in Z holds f1.x=x+b & f1.x>0);
    then Z c= dom id Z /\ dom ((a-b)(#)f) by SEQ_1:def 3;
    then A2:Z c= dom id Z & Z c= dom ((a-b)(#)f) by XBOOLE_1:18;
    then A4:Z c= dom ((log_ number_e)*f1) by A1,SEQ_1:def 6;
B1: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;
    then A3:id Z is_differentiable_on Z & for x st x in Z
    holds ((id Z)`|Z).x = 1 by FDIFF_1:31,A2;
B2: for x st x in Z holds f1.x=b+x & f1.x>0 by A1;
    then A7:f is_differentiable_on Z & for x st x in Z
    holds (f`|Z).x = 1/(x+b) by A1,A4,th1;
    then A8:(a-b)(#)f is_differentiable_on Z & for x st x in Z holds
    (((a-b)(#)f)`|Z).x =(a-b)*diff(f,x) by A2,FDIFF_1:28;
A9: for x st x in Z holds (((a-b)(#)f)`|Z).x =(a-b)/(x+b)
    proof
      let x;
      assume B4:x in Z;
      hence (((a-b)(#)f)`|Z).x =(a-b)*diff(f,x) by A7,A2,FDIFF_1:28
      .=(a-b)*(f`|Z).x by B4,FDIFF_1:def 8,A7
      .=(a-b)*(1/(x+b)) by A1,A4,th1,B2,B4
      .=(a-b)/(x+b) by XCMPLX_1:100;
    end;
    for x st x in Z holds ((id Z + (a-b)(#)f)`|Z).x = (x+a)/(x+b)
    proof
      let x;
      assume B4:x in Z;
      then B5:f1.x=x+b & f1.x>0 by A1;
      ((id Z + (a-b)(#)f)`|Z).x = diff(id Z,x) + diff((a-b)(#)f,x)
      by A3,A8,A1,FDIFF_1:26,B4
      .=((id Z)`|Z).x + diff((a-b)(#)f,x) by A3,B4,FDIFF_1:def 8
      .=((id Z)`|Z).x + (((a-b)(#)f)`|Z).x by B4,A8,FDIFF_1:def 8
      .=1 + (((a-b)(#)f)`|Z).x by FDIFF_1:31,A2,B1,B4
      .=1 + (a-b)/(x+b) by A9,B4
      .=(1*(x+b) + (a-b))/(x+b) by XCMPLX_1:114,B5
      .=(x+a)/(x+b);
      hence thesis;
    end;
    hence thesis by A3,A8,A1,FDIFF_1:26;
  end;

:: 9 f.x=x+ (a+b)*ln(x-b)
theorem
  Z c= dom (id Z + (a+b)(#)f) & f = (log_ number_e)*f1 &
  (for x st x in Z holds f1.x=x-b & f1.x>0) implies
  (id Z + (a+b)(#)f) is_differentiable_on Z &
  for x st x in Z holds ((id Z +(a+b)(#)f)`|Z).x = (x+a)/(x-b)
  proof
    assume A1:Z c= dom (id Z + (a+b)(#)f) & f = (log_ number_e)*f1 &
    (for x st x in Z holds f1.x=x-b & f1.x>0);
    then Z c= dom id Z /\ dom ((a+b)(#)f) by SEQ_1:def 3;
    then A2:Z c= dom id Z & Z c= dom ((a+b)(#)f) by XBOOLE_1:18;
    then A4:Z c= dom ((log_ number_e)*f1) by A1,SEQ_1:def 6;
B1: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;
    then A3:id Z is_differentiable_on Z & for x st x in Z
    holds ((id Z)`|Z).x = 1 by FDIFF_1:31,A2;
A7: f is_differentiable_on Z & for x st x in Z
    holds (f`|Z).x = 1/(x-b) by A4,th2,A1;
    then A8:(a+b)(#)f is_differentiable_on Z & for x st x in Z holds
    (((a+b)(#)f)`|Z).x =(a+b)*diff(f,x) by A2,FDIFF_1:28;
A9: for x st x in Z holds (((a+b)(#)f)`|Z).x =(a+b)/(x-b)
    proof
      let x;
      assume B4:x in Z;
      hence (((a+b)(#)f)`|Z).x =(a+b)*diff(f,x) by A7,A2,FDIFF_1:28
      .=(a+b)*(f`|Z).x by B4,FDIFF_1:def 8,A7
      .=(a+b)*(1/(x-b)) by A4,th2,A1,B4
      .=(a+b)/(x-b) by XCMPLX_1:100;
    end;
    for x st x in Z holds ((id Z + (a+b)(#)f)`|Z).x = (x+a)/(x-b)
    proof
      let x;
      assume B4:x in Z;
      then B5:f1.x=x-b & f1.x>0 by A1;
      ((id Z + (a+b)(#)f)`|Z).x = diff(id Z,x) + diff((a+b)(#)f,x)
      by A3,A8,A1,FDIFF_1:26,B4
      .=((id Z)`|Z).x + diff((a+b)(#)f,x) by A3,B4,FDIFF_1:def 8
      .=((id Z)`|Z).x + (((a+b)(#)f)`|Z).x by B4,A8,FDIFF_1:def 8
      .=1 + (((a+b)(#)f)`|Z).x by FDIFF_1:31,A2,B1,B4
      .=1 + (a+b)/(x-b) by A9,B4
      .=(1*(x-b) + (a+b))/(x-b) by XCMPLX_1:114,B5
      .=(x+a)/(x-b);
      hence thesis;
    end;
    hence thesis by A3,A8,A1,FDIFF_1:26;
  end;

:: 10 f.x=x- (a+b)*ln(x+b)
theorem
  Z c= dom (id Z - (a+b)(#)f) & f = (log_ number_e)*f1 &
  (for x st x in Z holds f1.x=x+b & f1.x>0) implies
  (id Z - (a+b)(#)f) is_differentiable_on Z &
  for x st x in Z holds ((id Z -(a+b)(#)f)`|Z).x = (x-a)/(x+b)
  proof
    assume A1:Z c= dom (id Z - (a+b)(#)f) & f = (log_ number_e)*f1 &
    (for x st x in Z holds f1.x=x+b & f1.x>0);
    then Z c= dom id Z /\ dom ((a+b)(#)f) by SEQ_1:def 4;
    then A2:Z c= dom id Z & Z c= dom ((a+b)(#)f) by XBOOLE_1:18;
    then A4:Z c= dom ((log_ number_e)*f1) by A1,SEQ_1:def 6;
B1: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;
    then A3:id Z is_differentiable_on Z & for x st x in Z
    holds ((id Z)`|Z).x = 1 by FDIFF_1:31,A2;
B2: for x st x in Z holds f1.x=b+x & f1.x>0 by A1;
    then A7:f is_differentiable_on Z & for x st x in Z
    holds (f`|Z).x = 1/(x+b) by A1,A4,th1;
    then A8:(a+b)(#)f is_differentiable_on Z & for x st x in Z holds
    (((a+b)(#)f)`|Z).x =(a+b)*diff(f,x) by A2,FDIFF_1:28;
A9: for x st x in Z holds (((a+b)(#)f)`|Z).x =(a+b)/(x+b)
    proof
      let x;
      assume B4:x in Z;
      hence (((a+b)(#)f)`|Z).x=(a+b)*diff(f,x) by A7,A2,FDIFF_1:28
      .=(a+b)*(f`|Z).x by B4,FDIFF_1:def 8,A7
      .=(a+b)*(1/(x+b)) by A1,A4,th1,B2,B4
      .=(a+b)/(x+b) by XCMPLX_1:100;
    end;
    for x st x in Z holds ((id Z - (a+b)(#)f)`|Z).x = (x-a)/(x+b)
    proof
      let x;
      assume B4:x in Z;
      then B5:f1.x=x+b & f1.x>0 by A1;
      ((id Z - (a+b)(#)f)`|Z).x = diff(id Z,x) - diff((a+b)(#)f,x)
      by A3,A8,A1,FDIFF_1:27,B4
      .=((id Z)`|Z).x - diff((a+b)(#)f,x) by A3,B4,FDIFF_1:def 8
      .=((id Z)`|Z).x - (((a+b)(#)f)`|Z).x by B4,A8,FDIFF_1:def 8
      .=1 - (((a+b)(#)f)`|Z).x by FDIFF_1:31,A2,B1,B4
      .=1 - (a+b)/(x+b) by A9,B4
      .=(1*(x+b) - (a+b))/(x+b) by XCMPLX_1:128,B5
      .=(x-a)/(x+b);
      hence thesis;
    end;
    hence thesis by A3,A8,A1,FDIFF_1:27;
  end;

:: 11 f.x=x+ (b-a)*ln(x-b)
theorem
  Z c= dom (id Z + (b-a)(#)f) & f = (log_ number_e)*f1 &
  (for x st x in Z holds f1.x=x-b & f1.x>0) implies
  (id Z + (b-a)(#)f) is_differentiable_on Z &
  for x st x in Z holds ((id Z +(b-a)(#)f)`|Z).x = (x-a)/(x-b)
  proof
    assume A1:Z c= dom (id Z + (b-a)(#)f) & f = (log_ number_e)*f1 &
    (for x st x in Z holds f1.x=x-b & f1.x>0);
    then Z c= dom id Z /\ dom ((b-a)(#)f) by SEQ_1:def 3;
    then A2:Z c= dom id Z & Z c= dom ((b-a)(#)f) by XBOOLE_1:18;
    then A4:Z c= dom ((log_ number_e)*f1) by A1,SEQ_1:def 6;
B1: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;
    then A3:id Z is_differentiable_on Z & for x st x in Z
    holds ((id Z)`|Z).x = 1 by FDIFF_1:31,A2;
A7: f is_differentiable_on Z & for x st x in Z
    holds (f`|Z).x = 1/(x-b) by A1,A4,th2;
    then A8:(b-a)(#)f is_differentiable_on Z & for x st x in Z holds
    (((b-a)(#)f)`|Z).x =(b-a)*diff(f,x) by A2,FDIFF_1:28;
A9: for x st x in Z holds (((b-a)(#)f)`|Z).x =(b-a)/(x-b)
    proof
      let x;
      assume B4:x in Z;
      hence (((b-a)(#)f)`|Z).x =(b-a)*diff(f,x) by A7,A2,FDIFF_1:28
      .=(b-a)*(f`|Z).x by B4,FDIFF_1:def 8,A7
      .=(b-a)*(1/(x-b)) by A1,A4,th2,B4
      .=(b-a)/(x-b) by XCMPLX_1:100;
    end;
    for x st x in Z holds ((id Z + (b-a)(#)f)`|Z).x = (x-a)/(x-b)
    proof
      let x;
      assume B4:x in Z;
      then A11:f1.x=x-b & f1.x>0 by A1;
      ((id Z + (b-a)(#)f)`|Z).x = diff(id Z,x) + diff((b-a)(#)f,x)
      by A3,A8,A1,FDIFF_1:26,B4
      .=((id Z)`|Z).x + diff((b-a)(#)f,x) by A3,B4,FDIFF_1:def 8
      .=((id Z)`|Z).x + (((b-a)(#)f)`|Z).x by B4,A8,FDIFF_1:def 8
      .=1 + (((b-a)(#)f)`|Z).x by FDIFF_1:31,A2,B1,B4
      .=1 + (b-a)/(x-b) by A9,B4
      .=(1*(x-b) + (b-a))/(x-b) by XCMPLX_1:114,A11
      .=(x-a)/(x-b);
      hence thesis;
    end;
    hence thesis by A3,A8,A1,FDIFF_1:26;
  end;

:: 12 f.x=a+b*x+c*x^|2
theorem th12:
  Z c= dom (f1+c(#)f2) & (for x st x in Z holds f1.x=a+b*x) & f2=#Z 2
  implies f1+c(#)f2 is_differentiable_on Z &
  for x st x in Z holds ((f1+c(#)f2)`|Z).x = b+2*c*x
  proof
    assume A1:Z c= dom (f1+c(#)f2) & (for x st x in Z holds f1.x=a+b*x)
    & f2=#Z 2;
    then Z c= dom f1 /\ dom (c(#)f2) by SEQ_1:def 3;
    then A2:Z c= dom f1 & Z c= dom (c(#)f2) by XBOOLE_1:18;
    then A4:Z c= dom f2 by SEQ_1:def 6;
B1: for x st x in Z holds f1.x=b*x+a by A1;
    then A3:f1 is_differentiable_on Z & for x st x in Z
    holds (f1`|Z).x = b by FDIFF_1:31,A2;
A9: f2 is_differentiable_on Z
    proof
      for x st x in Z holds f2 is_differentiable_in x by A1,TAYLOR_1:2;
      hence f2 is_differentiable_on Z by A4,FDIFF_1:16;
    end;
    then A10:(c(#)f2) is_differentiable_on Z by FDIFF_1:28,A2;
A12: for x st x in Z holds (f2`|Z).x=2*x
     proof
       let x;
       assume A8:x in Z;
       2 * (x #Z (2-1))=2 * x by PREPOWER:45;
       then diff(f2,x) =2 * x by TAYLOR_1:2,A1;
       hence (f2`|Z).x =2 * x by A9,A8,FDIFF_1:def 8;
     end;
A13: for x st x in Z holds ((c(#)f2)`|Z).x=2*c*x
     proof
       let x;
       assume A8:x in Z;
       hence ((c(#)f2)`|Z).x=c*diff(f2,x) by A2,A9,FDIFF_1:28
       .=c*(f2`|Z).x by A8,A9,FDIFF_1:def 8
       .=c*(2*x) by A12,A8
       .=2*c*x;
     end;
     for x st x in Z holds ((f1+c(#)f2)`|Z).x = b+2*c*x
     proof
       let x;
       assume A8:x in Z;
       ((f1+c(#)f2)`|Z).x =diff(f1,x) + diff(c(#)f2,x)
       by FDIFF_1:26,A1,A10,A3,A8
       .=(f1`|Z).x+ diff((c(#)f2),x) by A8,A3,FDIFF_1:def 8
       .=(f1`|Z).x+((c(#)f2)`|Z).x by A8,A10,FDIFF_1:def 8
       .=b+((c(#)f2)`|Z).x by FDIFF_1:31,A2,B1,A8
       .=b+2*c*x by A13,A8;
       hence thesis;
     end;
     hence thesis by A10,A3,A1,FDIFF_1:26;
   end;

:: 13 f.x=ln(a+b*x+c*x |^2)
theorem th13:
  Z c= dom ((log_ number_e)*(f1+c(#)f2)) & f2=#Z 2 &
  (for x st x in Z holds f1.x=a+b*x & (f1+c(#)f2).x >0) implies
  ((log_ number_e)*(f1+c(#)f2)) is_differentiable_on Z & for x st x in Z holds
  (((log_ number_e)*(f1+c(#)f2))`|Z).x = (b+2*c*x)/(a+b*x+c*x |^2)
  proof
    assume A1: Z c= dom ((log_ number_e)*(f1+c(#)f2)) & f2=#Z 2 &
    (for x st x in Z holds f1.x=a+b*x & (f1+c(#)f2).x >0);
    for y st y in Z holds y in dom (f1+c(#)f2) by FUNCT_1:21,A1;
    then A2:Z c= dom (f1+c(#)f2) by TARSKI:def 3;
    then Z c= dom f1 /\ dom (c(#)f2) by SEQ_1:def 3;
    then A11: Z c= dom f1 & Z c= dom (c(#)f2) by XBOOLE_1:18;
B1: (for x st x in Z holds f1.x=a+b*x) & f2=#Z 2 by A1;
    then A3:(f1+c(#)f2) is_differentiable_on Z &
    for x st x in Z holds ((f1+c(#)f2)`|Z).x = b+2*c*x by th12,A2;
B3: for x st x in Z holds (log_ number_e)*(f1+c(#)f2) is_differentiable_in x
    proof
      let x;
      assume A4:x in Z;
      then A5:(f1+c(#)f2) is_differentiable_in x by FDIFF_1:16,A3;
      (f1+c(#)f2).x >0 by A1,A4;
      hence (log_ number_e)*(f1+c(#)f2) is_differentiable_in x
      by TAYLOR_1:20,A5;
    end;
    then A:(log_ number_e)*(f1+c(#)f2) is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds
    (((log_ number_e)*(f1+c(#)f2))`|Z).x = (b+2*c*x)/(a+b*x+c*x |^2)
    proof
      let x;
      assume A4:x in Z;
      then A9: x in dom (f1+c(#)f2) by FUNCT_1:21,A1;
A5:   (f1+c(#)f2) is_differentiable_in x by FDIFF_1:16,A3,A4;
A7:   f1.x=a+b*x & ( f1+c(#)f2).x >0 by A1,A4;
A15:  ( f1+c(#)f2).x=f1.x + (c(#)f2).x by SEQ_1:def 3,A9
      .=f1.x +c*f2.x by A11,A4, SEQ_1:def 6
      .=a+b*x +c*(f2.x) by A1,A4
      .=a+b*x +c*(x #Z 2) by A1,TAYLOR_1:def 1
      .=a+b*x +c*(x |^2) by PREPOWER:46;
      diff((log_ number_e)*( f1+c(#)f2),x)
      =diff(( f1+c(#)f2),x)/(( f1+c(#)f2).x) by TAYLOR_1:20,A7,A5
      .=(( f1+c(#)f2)`|Z).x/(( f1+c(#)f2).x) by A3,A4,FDIFF_1:def 8
      .=(b+2*c*x )/(a+b*x+c*(x |^2)) by A15,th12,A2,B1,A4;
      hence (((log_ number_e)*( f1+c(#)f2))`|Z).x = (b+2*c*x)/(a+b*x+c*x |^2)
      by A4,A,FDIFF_1:def 8;
    end;
    hence thesis by A1,FDIFF_1:16,B3;
  end;

:: 14 f.x=1/(a+x)
theorem th14:
  Z c= dom f & (for x st x in Z holds f.x = a+x & f.x<>0) implies
  f^ is_differentiable_on Z & for x st x in Z holds
  ( (f^)`|Z).x= -1/(a+x)^2
  proof
    assume that A1: Z c= dom f and
A2: for x st x in Z holds f.x = a+x & f.x<>0;
B1: for x st x in Z holds f.x = 1*x+a by A2;
    then A3:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = 1 by FDIFF_1:31,A1;
B2: for x st x in Z holds f.x<>0 by A2;
    then A5:f^ is_differentiable_on Z by A3,FDIFF_2:22;
    now let x;
      assume A6:x in Z;
A7:  f.x<>0 by A2,A6;
A9:   f is_differentiable_in x by A6,A3,FDIFF_1:16;
      ( (f^)`|Z).x= diff(f^,x) by A6,A5,FDIFF_1:def 8
      .= - diff(f,x)/( f.x)^2 by A7,FDIFF_2:15,A9
      .=- (f`|Z).x/( f.x)^2 by A6,A3,FDIFF_1:def 8
      .=-1/( f.x)^2 by FDIFF_1:31,A1,B1,A6
      .=-1/(a+x)^2 by A2,A6;
      hence ( (f^)`|Z).x=-1/(a+x)^2;
    end;
    hence thesis by A3,FDIFF_2:22,B2;
  end;

:: 15 f.x=-1/(a+x)
theorem
  Z c= dom ((-1)(#)(f^)) & (for x st x in Z holds f.x=a+x & f.x <>0)
  implies (-1)(#)(f^) is_differentiable_on Z &
  for x st x in Z holds (((-1)(#)(f^))`|Z).x = 1/(a+x)^2
  proof
    assume that A1:Z c= dom ((-1)(#)(f^)) and
A2: for x st x in Z holds f.x=a+x & f.x <>0;
A3: Z c= dom (f^) by SEQ_1:def 6,A1;
    dom (f^) c= dom f by RFUNCT_1:11;
    then B2:Z c= dom f by XBOOLE_1:1,A3;
    then A6:f^ is_differentiable_on Z & for x st x in Z holds
    ((f^)`|Z).x= -1/(a+x)^2 by A2,th14;
    now let x;
      assume A4:x in Z;
      hence (((-1)(#)(f^))`|Z).x =(-1)*diff((f^),x) by A6,A1,FDIFF_1:28
      .=(-1)*((f^)`|Z).x by A4,A6,FDIFF_1:def 8
      .=(-1)*(-1/(a+x)^2) by A4,A2,th14,B2
      .=1/(a+x)^2;
    end;
    hence thesis by A6,A1,FDIFF_1:28;
  end;

:: 16 f.x=1/(a-x)
theorem
  Z c= dom f & (for x st x in Z holds f.x = a-x & f.x<>0) implies
  f^ is_differentiable_on Z & for x st x in Z holds
  ( (f^)`|Z).x= 1/(a-x)^2
  proof
    assume that A1: Z c= dom f and
A2: for x st x in Z holds f.x = a-x & f.x<>0;
B2: for x st x in Z holds f.x =(-1)*x+a
    proof
      let x;
      assume x in Z;
      then f.x =a-x by A2;
      hence thesis;
    end;
    then A3:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = -1 by FDIFF_1:31,A1;
B3: for x st x in Z holds f.x<>0 by A2;
    then A5:f^ is_differentiable_on Z by A3,FDIFF_2:22;
    now let x;
      assume A6:x in Z;
A7:   f.x<>0 by A2,A6;
B1:   f is_differentiable_in x by A6,A3,FDIFF_1:16;
      ( (f^)`|Z).x= diff(f^,x) by A6,A5,FDIFF_1:def 8
      .= - diff(f,x)/( f.x)^2 by A7,FDIFF_2:15,B1
      .=- (f`|Z).x/( f.x)^2 by A6,A3,FDIFF_1:def 8
      .=-(-1)/( f.x)^2 by FDIFF_1:31,A1,B2,A6
      .=-(-1/( f.x)^2) by XCMPLX_1:188
      .=1/(a-x)^2 by A2,A6;
      hence ( (f^)`|Z).x=1/(a-x)^2;
    end;
    hence thesis by A3,FDIFF_2:22,B3;
  end;

:: 17 f.x=a^2+x^|2
theorem th17:
  Z c= dom (f1+f2) & (for x st x in Z holds f1.x=a^2) & f2=#Z 2 implies
  f1+f2 is_differentiable_on Z &
  for x st x in Z holds ((f1+f2)`|Z).x = 2*x
  proof
    assume that A1:Z c= dom (f1+f2) and
A2: (for x st x in Z holds f1.x=a^2) and
A3: f2=#Z 2;
A5: Z c= dom (f1+1(#)f2) by A1,RFUNCT_1:33;
B1: for x st x in Z holds f1.x=a^2 + 0*x by A2;
    then A6:f1+1(#)f2 is_differentiable_on Z &
    for x st x in Z holds ((f1+1(#)f2)`|Z).x = 0+2*1*x by th12,A3,A5;
    for x st x in Z holds ((f1+f2)`|Z).x = 2*x
    proof
      let x;
      assume A4:x in Z;
      ((f1+f2)`|Z).x=((f1+1(#)f2)`|Z).x by RFUNCT_1:33
      .= 0+2*1*x by th12,A3,A5,B1,A4
      .= 2*x;
      hence thesis;
    end;
    hence thesis by A6,RFUNCT_1:33;
  end;

:: 18 f.x=ln(a^2+x|^2)
theorem
  Z c= dom ((log_ number_e)*(f1+f2)) & f2=#Z 2 &
  (for x st x in Z holds f1.x=a^2 & (f1+f2).x >0) implies
  ((log_ number_e)*(f1+f2)) is_differentiable_on Z & for x st x in Z holds
  (((log_ number_e)*(f1+f2))`|Z).x = (2*x)/(a^2+x|^2)
  proof
    assume that A1: Z c= dom ((log_ number_e)*(f1+f2)) and A2:f2=#Z 2 and
A3: (for x st x in Z holds f1.x=a^2 & (f1+f2).x >0);
A4: f2=1(#)f2 by RFUNCT_1:33;
A5: Z c= dom ((log_ number_e)*(f1+1(#)f2)) by A1,RFUNCT_1:33;
B2: for x st x in Z holds f1.x=a^2+0*x & (f1+1(#)f2).x >0 by A4,A3;
    then A7:((log_ number_e)*(f1+1(#)f2)) is_differentiable_on Z &
    for x st x in Z holds
    (((log_ number_e)*(f1+1(#)f2))`|Z).x = (0+2*1*x)/(a^2+0*x+1*x |^2)
    by th13,A5,A2;
    for x st x in Z holds
    (((log_ number_e)*(f1+f2))`|Z).x = (2*x)/(a^2+x |^2)
    proof
      let x;
      assume B1:x in Z;
      (((log_ number_e)*(f1+f2))`|Z).x
      =(((log_ number_e)*(f1+1(#)f2))`|Z).x by RFUNCT_1:33
      .= (0+2*1*x)/(a^2+0*x+1*x |^2) by th13,A5,A2,B2,B1
      .=(2*x)/(a^2+x |^2);
      hence thesis;
    end;
    hence thesis by A7,RFUNCT_1:33;
  end;

:: 19 f.x=-ln(a^2-x|^2)
theorem
  Z c= dom -((log_ number_e)*(f1-f2)) & f2=#Z 2 &
  (for x st x in Z holds f1.x=a^2 & (f1-f2).x >0) implies
  -((log_ number_e)*(f1-f2)) is_differentiable_on Z &
  for x st x in Z holds
  ((-((log_ number_e)*(f1-f2)))`|Z).x = 2*x/(a^2-x |^2)
  proof
    assume that B1:Z c= dom -((log_ number_e)*(f1-f2)) and B2:f2=#Z 2 and
B3: for x st x in Z holds f1.x=a^2 & (f1-f2).x >0;
A2: f1+(-1)(#)f2=f1+-f2 by RFUNCT_1:38
    .=f1-f2 by RFUNCT_1:40;
    then A3:Z c= dom ((log_ number_e)*(f1+(-1)(#)f2)) by B1,SEQ_1:def 7;
A4: for x st x in Z holds f1.x=a^2+0*x & ( f1+(-1)(#)f2).x >0 by B3,A2;
B5: Z c= dom ((-1)(#)((log_ number_e)*(f1+(-1)(#)f2)))
    by B1,A2,RFUNCT_1:38;
A9: (log_ number_e)*(f1+(-1)(#)f2) is_differentiable_on Z & for x st x in Z
    holds (((log_ number_e)*(f1+(-1)(#)f2))`|Z).x
    = (0+2*(-1)*x)/(a^2+0*x+(-1)*x |^2) by th13,A3,A4,B2;
    then A6:(-1)(#)((log_ number_e)*(f1+(-1)(#)f2)) is_differentiable_on Z
    & for x st x in Z holds (((-1)(#)((log_ number_e)*(f1+(-1)(#)f2)))`|Z).x
    =(-1)*diff(((log_ number_e)*(f1+(-1)(#)f2)),x) by FDIFF_1:28,B5;
    for x st x in Z holds ((-((log_ number_e)*(f1-f2)))`|Z).x =2*x/(a^2-x |^2)
    proof
      let x;
      assume A8:x in Z;
      ((-((log_ number_e)*(f1-f2)))`|Z).x
      =(((-1)(#)((log_ number_e)*(f1+(-1)(#)f2)))`|Z).x by A2,RFUNCT_1:38
      .=(-1)*diff(((log_ number_e)*(f1+(-1)(#)f2)),x) by A8,FDIFF_1:28,B5,A9
      .=(-1)*(((log_ number_e)*(f1+(-1)(#)f2))`|Z).x by A8,A9,FDIFF_1:def 8
      .=(-1)*((0+2*(-1)*x)/(a^2+0*x+(-1)*x |^2)) by th13,A3,A4,B2,A8
      .=(-1)*(2*(-1)*x)/(a^2+(-1)*x |^2) by XCMPLX_1:75
      .=2*x/(a^2-x |^2);
      hence thesis;
    end;
    hence thesis by A2,RFUNCT_1:38,A6;
  end;

:: 20 f.x=a+x|^3
theorem th20:
  Z c= dom (f1+f2) & (for x st x in Z holds f1.x=a) & f2=#Z 3
  implies f1+f2 is_differentiable_on Z &
  for x st x in Z holds ((f1+f2)`|Z).x = 3*x |^2
  proof
    assume A1:Z c= dom (f1+f2) & (for x st x in Z holds f1.x=a) & f2=#Z 3;
    then Z c= dom f1 /\ dom f2 by SEQ_1:def 3;
    then B4:Z c= dom f1 & Z c= dom f2 by XBOOLE_1:18;
B9: for x st x in Z holds f1.x = 0*x+a by A1;
    then A3:f1 is_differentiable_on Z &
    for x st x in Z holds (f1`|Z).x = 0 by FDIFF_1:31,B4;
    for x st x in Z holds f2 is_differentiable_in x by A1,TAYLOR_1:2;
    then B7:f2 is_differentiable_on Z by FDIFF_1:16,B4;
A5: for x st x in Z holds (f2`|Z).x = 3 * x #Z (3-1)
    proof
      let x;
      assume A4:x in Z;
      diff(f2,x) = 3 * x #Z (3-1) by A1,TAYLOR_1:2;
      hence (f2`|Z).x =3 * x #Z (3-1) by A4,B7,FDIFF_1:def 8;
    end;
    for x st x in Z holds ((f1+f2)`|Z).x = 3*x|^2
    proof
      let x;
      assume A4:x in Z;
      then ((f1+f2)`|Z).x=diff(f1,x)+diff(f2,x) by A3,B7,FDIFF_1:26,A1;
      hence ((f1+f2)`|Z).x=(f1`|Z).x +diff(f2,x) by A3,A4,FDIFF_1:def 8
      .=(f1`|Z).x +(f2`|Z).x by B7,A4,FDIFF_1:def 8
      .=0+(f2`|Z).x by FDIFF_1:31,B4,B9,A4
      .=3 * x #Z (3-1) by A5,A4
      .=3*x |^2 by PREPOWER:46;
    end;
    hence thesis by A3,B7,FDIFF_1:26,A1;
  end;

:: 21 f.x=ln(a+x|^3)
theorem
  Z c= dom ((log_ number_e)*(f1+f2)) & f2=#Z 3 &
  (for x st x in Z holds f1.x=a & (f1+f2).x >0) implies
  ((log_ number_e)*(f1+f2)) is_differentiable_on Z & for x st x in Z holds
  (((log_ number_e)*(f1+f2))`|Z).x = (3*x |^2)/(a+x |^3)
  proof
    assume that A1: Z c= dom ((log_ number_e)*(f1+f2)) and B2:f2=#Z 3 and
B3: for x st x in Z holds f1.x=a & (f1+f2).x >0;
    for y st y in Z holds y in dom (f1+f2) by FUNCT_1:21,A1;
    then A2:Z c= dom (f1+f2) by TARSKI:def 3;
B4: for x st x in Z holds f1.x=a by B3;
    then B8:f1+f2 is_differentiable_on Z &
    for x st x in Z holds ((f1+f2)`|Z).x = 3*x |^2 by B2,A2,th20;
B5: for x st x in Z holds (log_ number_e)*(f1+f2) is_differentiable_in x
    proof
      let x;
      assume A4:x in Z;
      then A10:f1+f2 is_differentiable_in x by B8,FDIFF_1:16;
      (f1+f2).x >0 by A4,B3;
      hence (log_ number_e)*(f1+f2) is_differentiable_in x
      by TAYLOR_1:20,A10;
    end;
    then A:(log_ number_e)*(f1+f2) is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds
    (((log_ number_e)*(f1+f2))`|Z).x = (3*x |^2)/(a+x |^3)
    proof
      let x;
      assume A4:x in Z;
      then A10:f1+f2 is_differentiable_in x & ((f1+f2)`|Z).x = 3*x |^2
      by B8,FDIFF_1:16;
A7:   (f1+f2).x >0 by A4,B3;
A12:  x in dom (f1+f2) by A4,FUNCT_1:21,A1;
      diff((log_ number_e)*(f1+f2),x) =diff((f1+f2),x)/((f1+f2).x)
      by TAYLOR_1:20,A7,A10
      .=((f1+f2)`|Z).x/((f1+f2).x) by B8,A4,FDIFF_1:def 8
      .=(3*x |^2)/((f1+f2).x) by B4,B2,A2,th20,A4
      .=(3*x |^2)/(f1.x+f2.x) by SEQ_1:def 3,A12
      .=(3*x |^2)/(a+f2.x) by B3,A4
      .=(3*x |^2)/(a+x #Z 3) by B2,TAYLOR_1:def 1
      .=(3*x |^2)/(a+x |^3 ) by PREPOWER:46;
      hence thesis by A,A4,FDIFF_1:def 8;
    end;
    hence thesis by A1,FDIFF_1:16,B5;
  end;

:: 22 f.x=ln((a+x)/(a-x))
theorem
  Z c= dom ((log_ number_e)*(f1/f2)) &
  (for x st x in Z holds f1.x=a+x & f1.x >0 & f2.x=a-x & f2.x >0) implies
  (log_ number_e)*(f1/f2) is_differentiable_on Z & for x st x in Z holds
  (((log_ number_e)*(f1/f2))`|Z).x = (2*a)/(a^2-x^2)
  proof
    assume A1:Z c= dom ((log_ number_e)*(f1/f2)) & (for x st x in Z holds
    f1.x=a+x & f1.x >0 & f2.x=a-x & f2.x >0);
    for y st y in Z holds y in dom (f1/f2) by FUNCT_1:21,A1;
    then Z c= dom (f1/f2) by TARSKI:def 3;
    then Z c= dom f1 /\ (dom f2 \ f2"{0}) by RFUNCT_1:def 4;
    then A3:Z c= dom f1 & Z c= dom f2 \ f2"{0} by XBOOLE_1:18;
    dom (f2^) c= dom f2 by RFUNCT_1:11;
    then dom f2 \ f2"{0} c= dom f2 by RFUNCT_1:def 8;
    then A4:Z c= dom f2 by XBOOLE_1:1,A3;
B1: for x st x in Z holds f1.x=1*x+a by A1;
    then A5:f1 is_differentiable_on Z & for x st x in Z holds (f1`|Z).x = 1
    by FDIFF_1:31,A3;
B2: for x st x in Z holds f2.x =(-1)*x+a
    proof
      let x;
      assume x in Z;
      then f2.x =a-x by A1;
      hence thesis;
    end;
    then A6:f2 is_differentiable_on Z &
    for x st x in Z holds (f2`|Z).x = -1 by FDIFF_1:31,A4;
    for x st x in Z holds f2.x <> 0 by A1;
    then A7:f1/f2 is_differentiable_on Z by A5,A6,FDIFF_2:21;
A8: for x st x in Z holds ((f1/f2)`|Z).x = (2*a)/(a-x)^2
    proof
      let x;
      assume B4:x in Z;
      then A9:f1 is_differentiable_in x by A5,FDIFF_1:16;
A10:  f2 is_differentiable_in x by A6,B4,FDIFF_1:16;
A11:  f2.x <>0 & f1.x=a+x & f2.x=a-x by A1,B4;
      then diff(f1/f2,x)
      =(diff(f1,x) * f2.x - diff(f2,x) * f1.x)/(f2.x)^2 by FDIFF_2:14,A9,A10
      .=((f1`|Z).x * f2.x-diff(f2,x) * f1.x)/(f2.x)^2 by A5,B4,FDIFF_1:def 8
      .= ((f1`|Z).x * f2.x-(f2`|Z).x * f1.x)/(f2.x)^2 by A6,B4,FDIFF_1:def 8
      .=(1* f2.x-(f2`|Z).x * f1.x)/(f2.x)^2 by B4,FDIFF_1:31,A3,B1
      .=(1* f2.x-(-1)* f1.x)/(f2.x)^2 by B4,FDIFF_1:31,A4,B2
      .=(2*a)/(a-x)^2 by A11;
      hence ((f1/f2)`|Z).x = (2*a)/(a-x)^2 by FDIFF_1:def 8,A7,B4;
    end;
A9: for x st x in Z holds (f1/f2).x >0
    proof
      let x;
      assume B4:x in Z;
      then A8:x in dom (f1/f2) by FUNCT_1:21,A1;
B6:   f1.x >0 & f2.x >0 by A1,B4;
      (f1/f2).x=f1.x * (f2.x)" by A8,RFUNCT_1:def 4
      .=f1.x / f2.x by XCMPLX_0:def 9;
      hence thesis by B6,XREAL_1:141;
    end;
B7: for x st x in Z holds (log_ number_e)*(f1/f2) is_differentiable_in x
    proof
      let x;
      assume B4:x in Z;
      then A5:f1/f2 is_differentiable_in x by FDIFF_1:16,A7;
      (f1/f2).x >0 by A9,B4;
      hence (log_ number_e)*(f1/f2) is_differentiable_in x by TAYLOR_1:20,A5;
    end;
    then A:(log_ number_e)*(f1/f2) is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds (((log_ number_e)*(f1/f2))`|Z).x = (2*a)/(a^2-x^2)
    proof
      let x;
      assume B4:x in Z;
      then A5:(f1/f2) is_differentiable_in x by FDIFF_1:16,A7;
A13:  f2.x=a-x & f2.x>0 by A1,B4;
A12:  x in dom (f1/f2) by FUNCT_1:21,A1,B4;
A11:  (f1/f2).x >0 & f1.x=a+x & f2.x=a-x by A1,B4,A9;
      diff((log_ number_e)*(f1/f2),x) =diff((f1/f2),x)/((f1/f2).x)
      by TAYLOR_1:20,A11,A5
      .=((f1/f2)`|Z).x/((f1/f2).x) by A7,B4,FDIFF_1:def 8
      .=((2*a)/(a-x)^2)/((f1/f2).x) by A8,B4
      .=((2*a)/(a-x)^2)/(f1.x * (f2.x)") by A12,RFUNCT_1:def 4
      .=((2*a)/(a-x)^2)/((a+x)/(a-x)) by A11,XCMPLX_0:def 9
      .=((2*a)/((a-x)*(a-x)))/((a+x)/(a-x)) by SQUARE_1:def 3
      .=((2*a)/(a-x)/(a-x))/((a+x)/(a-x)) by XCMPLX_1:79
      .=((2*a)/(a-x))/((a+x)/(a-x))/(a-x) by XCMPLX_1:48
      .=(2*a)/(a+x)/(a-x) by A13,XCMPLX_1:55
      .=(2*a)/((a+x)*(a-x)) by XCMPLX_1:79
      .=(2*a)/(a^2-x^2) by SQUARE_1:67;
      hence thesis by B4,A,FDIFF_1:def 8;
    end;
    hence thesis by A1,FDIFF_1:16,B7;
  end;

:: 23 f.x=ln((x-a)/(x+a))
theorem
  Z c= dom ((log_ number_e)*(f1/f2)) & (for x st x in Z holds
  f1.x=x-a & f1.x >0 & f2.x=x+a & f2.x >0) implies
  (log_ number_e)*(f1/f2) is_differentiable_on Z & for x st x in Z holds
  (((log_ number_e)*(f1/f2))`|Z).x = (2*a)/(x^2-a^2)
  proof
    assume A1:Z c= dom ((log_ number_e)*(f1/f2)) & (for x st x in Z holds
    f1.x=x-a & f1.x >0 & f2.x=x+a & f2.x >0);
    for y st y in Z holds y in dom (f1/f2) by FUNCT_1:21,A1;
    then Z c= dom (f1/f2) by TARSKI:def 3;
    then Z c= dom f1 /\ (dom f2 \ f2"{0}) by RFUNCT_1:def 4;
    then A3:Z c= dom f1 & Z c= dom f2 \ f2"{0} by XBOOLE_1:18;
    dom (f2^) c= dom f2 by RFUNCT_1:11;
    then dom f2 \ f2"{0} c= dom f2 by RFUNCT_1:def 8;
    then A4:Z c= dom f2 by XBOOLE_1:1,A3;
B1: for x st x in Z holds f1.x = 1*x+-a
    proof
      let x;
      assume A4:x in Z;
      1*x+-a=1*x-a;
      hence thesis by A1,A4;
    end;
    then A5:f1 is_differentiable_on Z &
    for x st x in Z holds (f1`|Z).x = 1 by FDIFF_1:31,A3;
B2: for x st x in Z holds f2.x = 1*x+a by A1;
    then A6:f2 is_differentiable_on Z &
    for x st x in Z holds (f2`|Z).x = 1 by FDIFF_1:31,A4;
    for x st x in Z holds f2.x <> 0 by A1;
    then A7:f1/f2 is_differentiable_on Z by A5,A6,FDIFF_2:21;
A8: for x st x in Z holds ((f1/f2)`|Z).x = (2*a)/(x+a)^2
    proof
      let x;
      assume B4:x in Z;
      then A9:f1 is_differentiable_in x by A5,FDIFF_1:16;
A10:  f2 is_differentiable_in x by A6,B4,FDIFF_1:16;
A11:  f2.x <>0 & f1.x=x-a & f2.x=x+a by A1,B4;
      then diff(f1/f2,x)
      =(diff(f1,x) * f2.x - diff(f2,x) * f1.x)/(f2.x)^2 by FDIFF_2:14,A9,A10
      .=((f1`|Z).x * f2.x-diff(f2,x) * f1.x)/(f2.x)^2 by A5,B4,FDIFF_1:def 8
      .= ((f1`|Z).x * f2.x-(f2`|Z).x * f1.x)/(f2.x)^2 by A6,B4,FDIFF_1:def 8
      .=(1* f2.x-(f2`|Z).x * f1.x)/(f2.x)^2 by B4,FDIFF_1:31,A3,B1
      .=(1* f2.x-1* f1.x)/(f2.x)^2 by B4,FDIFF_1:31,A4,B2
      .=(2*a)/(x+a)^2 by A11;
      hence ((f1/f2)`|Z).x = (2*a)/(x+a)^2 by FDIFF_1:def 8,A7,B4;
    end;
A9: for x st x in Z holds (f1/f2).x >0
    proof
      let x;
      assume A4:x in Z;
      then A8:x in dom (f1/f2) by FUNCT_1:21,A1;
B5:   f1.x >0 & f2.x >0 by A1,A4;
      (f1/f2).x=f1.x * (f2.x)" by A8,RFUNCT_1:def 4
      .=f1.x / f2.x by XCMPLX_0:def 9;
      hence thesis by XREAL_1:141,B5;
    end;
B:  for x st x in Z holds (log_ number_e)*(f1/f2) is_differentiable_in x
    proof
      let x;
      assume A4:x in Z;
      then A5:f1/f2 is_differentiable_in x by FDIFF_1:16,A7;
      (f1/f2).x >0 by A9,A4;
      hence (log_ number_e)*(f1/f2) is_differentiable_in x by TAYLOR_1:20,A5;
    end;
    then A:(log_ number_e)*(f1/f2) is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds (((log_ number_e)*(f1/f2))`|Z).x = (2*a)/(x^2-a^2)
    proof
      let x;
      assume A4:x in Z;
      then A5:(f1/f2) is_differentiable_in x by FDIFF_1:16,A7;
A13:  f2.x=x+a & f2.x>0 by A1,A4;
A12:  x in dom (f1/f2) by FUNCT_1:21,A1,A4;
A11:  (f1/f2).x >0 & f1.x=x-a & f2.x=x+a by A1,A4,A9;
      diff((log_ number_e)*(f1/f2),x) =diff((f1/f2),x)/((f1/f2).x)
      by TAYLOR_1:20,A11,A5
      .=((f1/f2)`|Z).x/((f1/f2).x) by A7,A4,FDIFF_1:def 8
      .=((2*a)/(x+a)^2)/((f1/f2).x) by A8,A4
      .=((2*a)/(x+a)^2)/(f1.x * (f2.x)") by A12,RFUNCT_1:def 4
      .=((2*a)/(x+a)^2)/((x-a)/(x+a)) by A11,XCMPLX_0:def 9
      .=((2*a)/((x+a)*(x+a)))/((x-a)/(x+a)) by SQUARE_1:def 3
      .=((2*a)/(x+a)/(x+a))/((x-a)/(x+a)) by XCMPLX_1:79
      .=((2*a)/(x+a))/((x-a)/(x+a))/(x+a) by XCMPLX_1:48
      .=(2*a)/(x-a)/(x+a) by A13,XCMPLX_1:55
      .=(2*a)/((x-a)*(x+a)) by XCMPLX_1:79
      .=(2*a)/(x^2-a^2) by SQUARE_1:67;
      hence thesis by A4,A,FDIFF_1:def 8;
    end;
    hence thesis by B,A1,FDIFF_1:16;
  end;

:: 24 f.x=ln((x-a)/(x-b))
theorem th24:
  Z c= dom ((log_ number_e)*(f1/f2)) & (for x st x in Z holds
  f1.x=x-a & f1.x >0 & f2.x=x-b & f2.x >0) implies
  (log_ number_e)*(f1/f2) is_differentiable_on Z & for x st x in Z holds
  (((log_ number_e)*(f1/f2))`|Z).x = (a-b)/((x-a)*(x-b))
  proof
    assume A1:Z c= dom ((log_ number_e)*(f1/f2)) & (for x st x in Z holds
    f1.x=x-a & f1.x >0 & f2.x=x-b & f2.x >0);
    for y st y in Z holds y in dom (f1/f2) by FUNCT_1:21,A1;
    then Z c= dom (f1/f2) by TARSKI:def 3;
    then Z c= dom f1 /\ (dom f2 \ f2"{0}) by RFUNCT_1:def 4;
    then A3:Z c= dom f1 & Z c= dom f2 \ f2"{0} by XBOOLE_1:18;
    dom (f2^) c= dom f2 by RFUNCT_1:11;
    then dom f2 \ f2"{0} c= dom f2 by RFUNCT_1:def 8;
    then A4:Z c= dom f2 by XBOOLE_1:1,A3;
B1: for x st x in Z holds f1.x = 1*x+-a & f2.x = 1*x+-b
    proof
      let x;
      assume A4:x in Z;
      1*x+-a=1*x-a & 1*x+-b=1*x-b;
      hence thesis by A1,A4;
    end;
    then B2:for x st x in Z holds f1.x = 1*x+-a;
    then A5:f1 is_differentiable_on Z & for x st x in Z holds
    (f1`|Z).x = 1 by FDIFF_1:31,A3;
B3: for x st x in Z holds f2.x = 1*x+-b by B1;
    then A6: f2 is_differentiable_on Z &
    for x st x in Z holds (f2`|Z).x = 1 by FDIFF_1:31,A4;
    for x st x in Z holds f2.x <> 0 by A1;
    then A7:f1/f2 is_differentiable_on Z by A5,A6,FDIFF_2:21;
A8: for x st x in Z holds ((f1/f2)`|Z).x = (a-b)/(x-b)^2
    proof
      let x;
      assume B4:x in Z;
      then A9:f1 is_differentiable_in x by A5,FDIFF_1:16;
A10:  f2 is_differentiable_in x by A6,B4,FDIFF_1:16;
A11:  f2.x <>0 & f1.x=x-a & f2.x=x-b by A1,B4;
      then diff(f1/f2,x)
      =(diff(f1,x) * f2.x - diff(f2,x) * f1.x)/(f2.x)^2 by FDIFF_2:14,A9,A10
      .=((f1`|Z).x * f2.x-diff(f2,x) * f1.x)/(f2.x)^2 by A5,B4,FDIFF_1:def 8
      .= ((f1`|Z).x * f2.x-(f2`|Z).x * f1.x)/(f2.x)^2 by A6,B4,FDIFF_1:def 8
      .=(1* f2.x-(f2`|Z).x * f1.x)/(f2.x)^2 by B4,FDIFF_1:31,A3,B2
      .=(1* f2.x-1* f1.x)/(f2.x)^2 by B4,FDIFF_1:31,A4,B3
      .=(a-b)/(x-b)^2 by A11;
      hence ((f1/f2)`|Z).x = (a-b)/(x-b)^2 by FDIFF_1:def 8,A7,B4;
    end;
A9: for x st x in Z holds (f1/f2).x >0
    proof
      let x;
      assume A4:x in Z;
      then A8:x in dom (f1/f2) by FUNCT_1:21,A1;
B6:   f1.x >0 & f2.x >0 by A1,A4;
      (f1/f2).x=f1.x * (f2.x)" by A8,RFUNCT_1:def 4
      .=f1.x / f2.x by XCMPLX_0:def 9;
      hence thesis by XREAL_1:141,B6;
    end;
B9: for x st x in Z holds (log_ number_e)*(f1/f2) is_differentiable_in x
    proof
      let x;
      assume A4:x in Z;
      then A5:f1/f2 is_differentiable_in x by FDIFF_1:16,A7;
      (f1/f2).x >0 by A9,A4;
      hence (log_ number_e)*(f1/f2) is_differentiable_in x by TAYLOR_1:20,A5;
    end;
    then A:(log_ number_e)*(f1/f2) is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds
      (((log_ number_e)*(f1/f2))`|Z).x = (a-b)/((x-a)*(x-b))
    proof
      let x;
      assume A4:x in Z;
      then A5:(f1/f2) is_differentiable_in x by FDIFF_1:16,A7;
A13:  f2.x=x-b & f2.x>0 by A1,A4;
A12:  x in dom (f1/f2) by FUNCT_1:21,A1,A4;
A11:  (f1/f2).x >0 & f1.x=x-a & f2.x=x-b by A1,A4,A9;
      diff((log_ number_e)*(f1/f2),x) =diff((f1/f2),x)/((f1/f2).x)
      by TAYLOR_1:20,A11,A5
      .=((f1/f2)`|Z).x/((f1/f2).x) by A7,A4,FDIFF_1:def 8
      .=((a-b)/(x-b)^2)/((f1/f2).x) by A8,A4
      .=((a-b)/(x-b)^2)/(f1.x * (f2.x)") by A12,RFUNCT_1:def 4
      .=((a-b)/(x-b)^2)/((x-a)/(x-b)) by A11,XCMPLX_0:def 9
      .=((a-b)/((x-b)*(x-b)))/((x-a)/(x-b)) by SQUARE_1:def 3
      .=((a-b)/(x-b)/(x-b))/((x-a)/(x-b)) by XCMPLX_1:79
      .=((a-b)/(x-b))/((x-a)/(x-b))/(x-b) by XCMPLX_1:48
      .=(a-b)/(x-a)/(x-b) by A13,XCMPLX_1:55
      .=(a-b)/((x-a)*(x-b)) by XCMPLX_1:79;
      hence thesis by A4,A,FDIFF_1:def 8;
    end;
    hence thesis by A1,FDIFF_1:16,B9;
  end;

:: 25 f.x=(1/(a-b))*ln((x-a)/(x-b))
theorem
  Z c= dom ((1/(a-b))(#)f) & f=(log_ number_e)*(f1/f2) & (for x st
  x in Z holds f1.x=x-a & f1.x >0 & f2.x=x-b & f2.x >0 & a-b<>0)
  implies (1/(a-b))(#)f is_differentiable_on Z &
  for x st x in Z holds (((1/(a-b))(#)f)`|Z).x =1/((x-a)*(x-b))
  proof
    assume B1:Z c= dom ((1/(a-b))(#)f) & f=(log_ number_e)*(f1/f2) &
    (for x st x in Z holds f1.x=x-a & f1.x >0 & f2.x=x-b & f2.x >0 & a-b<>0);
    then A1:Z c= dom ((1/(a-b))(#)f) & f=(log_ number_e)*(f1/f2) &
    (for x st x in Z holds f1.x=x-a & f1.x >0 & f2.x=x-b & f2.x >0);
B2: Z c= dom f by SEQ_1:def 6,B1;
    then A2:f is_differentiable_on Z & for x
    st x in Z holds (f`|Z).x =(a-b)/((x-a)*(x-b)) by th24,A1;
    for x st x in Z holds (((1/(a-b))(#)f)`|Z).x =1/((x-a)*(x-b))
    proof
      let x;
      assume A4:x in Z;
      then A5:a-b<>0 by B1;
      (((1/(a-b))(#)f)`|Z).x =(1/(a-b))*diff(f,x) by A2,B1,FDIFF_1:28,A4
      .=(1/(a-b))*(f`|Z).x by A4,A2,FDIFF_1:def 8
      .=(1/(a-b))*((a-b)/((x-a)*(x-b))) by th24,A1,B2,A4
      .=(1/(a-b))*(a-b)/((x-a)*(x-b)) by XCMPLX_1:75
      .=1/((x-a)*(x-b)) by XCMPLX_1:107,A5;
      hence thesis;
    end;
    hence thesis by B1,FDIFF_1:28,A2;
  end;

:: 26 f.x=ln((x-a)/x|^2)
theorem
  Z c= dom ((log_ number_e)*(f1/f2)) & f2=#Z 2 & (for x st x in Z holds
  f1.x=x-a & f1.x >0 & f2.x >0 & x <>0) implies
  (log_ number_e)*(f1/f2) is_differentiable_on Z & for x st x in Z holds
  (((log_ number_e)*(f1/f2))`|Z).x = (2*a-x)/(x*(x-a))
  proof
    assume A1:Z c= dom ((log_ number_e)*(f1/f2)) & f2=#Z 2 &
    (for x st x in Z holds f1.x=x-a & f1.x >0 & f2.x >0 & x <>0);
    for y st y in Z holds y in dom (f1/f2) by FUNCT_1:21,A1;
    then Z c= dom (f1/f2) by TARSKI:def 3;
    then Z c= dom f1 /\ (dom f2 \ f2"{0}) by RFUNCT_1:def 4;
    then A3:Z c= dom f1 & Z c= dom f2 \ f2"{0} by XBOOLE_1:18;
    dom (f2^) c= dom f2 by RFUNCT_1:11;
    then dom f2 \ f2"{0} c= dom f2 by RFUNCT_1:def 8;
    then A4:Z c= dom f2 by XBOOLE_1:1,A3;
B1: for x st x in Z holds f1.x = 1*x+-a
    proof
      let x;
      assume A4:x in Z;
      1*x+-a=1*x-a;
      hence thesis by A1,A4;
    end;
    then A5:f1 is_differentiable_on Z & for x st x in Z holds
    (f1`|Z).x = 1 by FDIFF_1:31,A3;
B2: for x st x in Z holds f2 is_differentiable_in x by A1,TAYLOR_1:2;
    then A9:f2 is_differentiable_on Z by A4,FDIFF_1:16;
A12: for x st x in Z holds (f2`|Z).x=2*x
     proof
       let x;
       assume A8:x in Z;
       2 * (x #Z (2-1))=2 * x by PREPOWER:45;
       then diff(f2,x) =2 * x by TAYLOR_1:2,A1;
       hence thesis by A9,A8,FDIFF_1:def 8;
     end;
A6:  f2 is_differentiable_on Z & for x st x in Z holds (f2`|Z).x=2*x
     by A4,FDIFF_1:16,B2,A12;
     for x st x in Z holds f2.x <> 0 by A1;
     then A7:f1/f2 is_differentiable_on Z by A5,A6,FDIFF_2:21;
A8:  for x st x in Z holds ((f1/f2)`|Z).x = (2*a-x)/x |^3
     proof
       let x;
       assume A14:x in Z;
A9:    f1 is_differentiable_in x by A5,A14,FDIFF_1:16;
A10:   f2 is_differentiable_in x by A1,TAYLOR_1:2;
A13:   x<>0 by A1,A14;
A11:   f2.x=x #Z 2 by A1,TAYLOR_1:def 1
       .=x |^2 by PREPOWER:46;
       then f2.x <>0 & f1.x=x-a & f2.x=x |^2 by A1,A14;
       then diff(f1/f2,x)
       =(diff(f1,x) * f2.x - diff(f2,x) * f1.x)/(f2.x)^2 by FDIFF_2:14,A9,A10
       .=((f1`|Z).x * f2.x-diff(f2,x) * f1.x)/(f2.x)^2 by A5,A14,FDIFF_1:def 8
       .= ((f1`|Z).x * f2.x-(f2`|Z).x * f1.x)/(f2.x)^2 by A6,A14,FDIFF_1:def 8
       .=(1* f2.x-(f2`|Z).x * f1.x)/(f2.x)^2 by A14,FDIFF_1:31,A3,B1
       .=(1* f2.x-(2*x)* f1.x)/(f2.x)^2 by A14,A12
       .=((x |^(1+1))-(2*x)*(x-a))/(x |^2)^2 by A11,A1,A14
       .=((x |^1*x)-(2*x)*(x-a))/(x |^2)^2 by NEWTON:11
       .=((x *x)-(2*x)*(x-a))/(x |^2)^2 by NEWTON:10
       .=(x *(2*a-x))/((x |^2)*(x |^2)) by SQUARE_1:def 3
       .=(x *(2*a-x))/(x |^(2+2)) by NEWTON:13
       .=(x *(2*a-x))/(x |^(3+1))
       .=(x *(2*a-x))/(x |^3 *x) by NEWTON:11
       .=(2*a-x)/x |^3 by XCMPLX_1:92,A13;
       hence ((f1/f2)`|Z).x = (2*a-x)/x |^3 by FDIFF_1:def 8,A7,A14;
     end;
A9:  for x st x in Z holds (f1/f2).x >0
     proof
       let x;
       assume A4:x in Z;
       then A8:x in dom (f1/f2) by FUNCT_1:21,A1;
B:     f1.x >0 & f2.x >0 by A1,A4;
       (f1/f2).x=f1.x * (f2.x)" by A8,RFUNCT_1:def 4
       .=f1.x / f2.x by XCMPLX_0:def 9;
       hence thesis by XREAL_1:141,B;
     end;
B9:  for x st x in Z holds (log_ number_e)*(f1/f2) is_differentiable_in x
     proof
       let x;
       assume A4:x in Z;
       then A5:f1/f2 is_differentiable_in x by FDIFF_1:16,A7;
       (f1/f2).x >0 by A9,A4;
       hence (log_ number_e)*(f1/f2) is_differentiable_in x by TAYLOR_1:20,A5;
     end;
     then A:(log_ number_e)*(f1/f2) is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds (((log_ number_e)*(f1/f2))`|Z).x = (2*a-x)/(x*(x-a))
     proof
       let x;
       assume A4:x in Z;
       then A5:(f1/f2) is_differentiable_in x by FDIFF_1:16,A7;
B1:    f2.x=x #Z 2 by A1,TAYLOR_1:def 1
       .=x |^2 by PREPOWER:46;
A13:   x |^2>0 by B1,A1,A4;
A12:   x in dom (f1/f2) by FUNCT_1:21,A1,A4;
A11:   (f1/f2).x >0 & f1.x=x-a & f2.x=x |^2 by A1,A4,A9,B1;
       diff((log_ number_e)*(f1/f2),x) =diff((f1/f2),x)/((f1/f2).x)
       by TAYLOR_1:20,A11,A5
       .=((f1/f2)`|Z).x/((f1/f2).x) by A7,A4,FDIFF_1:def 8
       .=((2*a-x)/x |^3)/((f1/f2).x) by A8,A4
       .=((2*a-x)/x |^3)/(f1.x * (f2.x)") by A12,RFUNCT_1:def 4
       .=((2*a-x)/x |^(2+1))/((x-a)/x |^2) by A11,XCMPLX_0:def 9
       .=((2*a-x)/(x |^2*x))/((x-a)/x |^2) by NEWTON:11
       .=(2*a-x)/x |^2/x/((x-a)/x |^2) by XCMPLX_1:79
       .=((2*a-x)/x |^2)/((x-a)/x |^2)/x by XCMPLX_1:48
       .=(2*a-x)/(x-a)/x by A13,XCMPLX_1:55
       .=(2*a-x)/(x*(x-a)) by XCMPLX_1:79;
       hence thesis by A4,A,FDIFF_1:def 8;
     end;
     hence thesis by A1,FDIFF_1:16,B9;
   end;

:: irrational function
:: 27 f.x=(a+x) #R (3/2)
theorem th27:
  Z c= dom (( #R (3/2))*f) & (for x st x in Z holds f.x=a+x & f.x>0)
  implies ( #R (3/2))*f is_differentiable_on Z &
  for x st x in Z holds ((( #R (3/2))*f)`|Z).x =(3/2)* (a+x) #R (1/2)
  proof
    assume A1:Z c= dom (( #R (3/2))*f) &
    (for x st x in Z holds f.x=a+x & f.x>0);
    for y st y in Z holds y in dom f by FUNCT_1:21,A1;
    then A2:Z c= dom f by TARSKI:def 3;
B1: for x st x in Z holds f.x = 1*x+a by A1;
    then A3:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = 1 by FDIFF_1:31,A2;
B2: for x st x in Z holds ( #R (3/2))*f is_differentiable_in x
    proof
      let x;
      assume A4:x in Z;
      then A5:f is_differentiable_in x by FDIFF_1:16,A3;
     f.x >0 by A1,A4;
      hence ( #R (3/2))*f is_differentiable_in x by TAYLOR_1:22,A5;
    end;
    then A:( #R (3/2))*f is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds ((( #R (3/2))*f)`|Z).x =(3/2)*(a+x) #R (1/2)
    proof
      let x;
      assume A4:x in Z;
      then A5:f is_differentiable_in x by FDIFF_1:16,A3;
A7:  f.x = a+x & f.x >0 by A1,A4;
      diff(( #R (3/2)) *f,x) = (3/2)*( ( f.x) #R (3/2-1)) * diff(f,x)
      by TAYLOR_1:22,A7,A5
      .= (3/2)*( ( f.x) #R (3/2-1))*(f`|Z).x by A3,A4,FDIFF_1:def 8
      .= (3/2)*( (a+x) #R (3/2-1))*1 by FDIFF_1:31,A2,B1,A4,A7
      .=(3/2)* (a+x) #R (1/2);
      hence ((( #R (3/2))*f)`|Z).x =(3/2)* (a+x) #R (1/2)
        by A4,A,FDIFF_1:def 8;
    end;
    hence thesis by A1,FDIFF_1:16,B2;
  end;

:: 28 f.x=(a+x) #R (2/3)
theorem
  Z c= dom ((2/3)(#)(( #R (3/2))*f)) &
    (for x st x in Z holds f.x=a+x & f.x>0) implies
      (2/3)(#)(( #R (3/2))*f) is_differentiable_on Z &
  for x st x in Z holds (((2/3)(#)(( #R (3/2))*f))`|Z).x =(a+x) #R (1/2)
  proof
    assume A1:Z c= dom ((2/3)(#)(( #R (3/2))*f)) &
    (for x st x in Z holds f.x=a+x & f.x>0);
    then B1:Z c= dom (( #R (3/2))*f) by SEQ_1:def 6;
    then A2:(( #R (3/2))*f) is_differentiable_on Z & for x
    st x in Z holds ((( #R (3/2))*f)`|Z).x =(3/2)* (a+x) #R (1/2) by th27,A1;
    for x st x in Z holds (((2/3)(#)(( #R (3/2))*f))`|Z).x =(a+x) #R (1/2)
    proof
      let x;
      assume A4:x in Z;
      hence (((2/3)(#)(( #R (3/2))*f))`|Z).x =(2/3)*diff((( #R (3/2))*f),x)
        by A1,FDIFF_1:28,A2
      .=(2/3)*((( #R (3/2))*f)`|Z).x by A4,A2,FDIFF_1:def 8
      .=(2/3)*((3/2)* (a+x) #R (1/2)) by th27,A1,B1,A4
      .=(a+x) #R (1/2);
    end;
    hence thesis by A2,A1,FDIFF_1:28;
  end;

:: 29 f.x=(-2/3)*(a+x) #R (2/3)
theorem
  Z c= dom ((-2/3)(#)(( #R (3/2))*f)) & (for x st x in Z holds f.x=a-x & f.x>0)
  implies (-2/3)(#)(( #R (3/2))*f) is_differentiable_on Z &
  for x st x in Z holds (((-2/3)(#)(( #R (3/2))*f))`|Z).x =(a-x) #R (1/2)
  proof
    assume A1:Z c= dom ((-2/3)(#)(( #R (3/2))*f)) &
    (for x st x in Z holds f.x=a-x & f.x>0);
    then B1:Z c= dom (( #R (3/2))*f) by SEQ_1:def 6;
    for y st y in Z holds y in dom f by FUNCT_1:21,B1;
    then A2:Z c= dom f by TARSKI:def 3;
B2: for x st x in Z holds f.x =(-1)*x+a
    proof
      let x;
      assume x in Z;
      then f.x =a-x by A1;
      hence thesis;
    end;
    then A3:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = -1 by FDIFF_1:31,A2;
    now let x;
      assume A4:x in Z;
      then A5:f is_differentiable_in x by FDIFF_1:16,A3;
      f.x >0 by A1,A4;
      hence ( #R (3/2))*f is_differentiable_in x by TAYLOR_1:22,A5;
    end;
    then B4:( #R (3/2))*f is_differentiable_on Z by B1,FDIFF_1:16;
    for x st x in Z holds (((-2/3)(#)(( #R (3/2))*f))`|Z).x =(a-x) #R (1/2)
    proof
      let x;
      assume A4:x in Z;
      then A5:f is_differentiable_in x by FDIFF_1:16,A3;
A7:   f.x = a-x & f.x >0 by A1,A4;
      (((-2/3)(#)(( #R (3/2))*f))`|Z).x =(-2/3)*diff((( #R (3/2))*f),x)
      by A1,FDIFF_1:28,B4,A4
      .=(-2/3)* ((3/2)*( ( f.x) #R (3/2-1)) * diff(f,x)) by TAYLOR_1:22,A7,A5
      .=(-2/3)* ((3/2)*( ( f.x) #R (3/2-1))*(f`|Z).x) by A3,A4,FDIFF_1:def 8
      .=(-2/3)* ((3/2)*( (a-x) #R (3/2-1))*(-1)) by FDIFF_1:31,A2,B2,A4,A7
      .=(a-x) #R (1/2);
      hence thesis;
    end;
    hence thesis by B4,A1,FDIFF_1:28;
  end;

:: 30 f.x=2*(a+x) #R (1/2)
theorem
  Z c= dom ((2(#)(( #R (1/2))*f))) & (for x st x in Z holds f.x=a+x & f.x>0)
  implies 2(#)(( #R (1/2))*f) is_differentiable_on Z &
  for x st x in Z holds ((2(#)(( #R (1/2))*f))`|Z).x =(a+x) #R (-1/2)
  proof
    assume B1:Z c= dom ((2(#)(( #R (1/2))*f))) &
    (for x st x in Z holds f.x=a+x & f.x>0);
    then A1:Z c= dom (( #R (1/2))*f) &
    (for x st x in Z holds f.x=a+x & f.x>0) by SEQ_1:def 6;
    for y st y in Z holds y in dom f by FUNCT_1:21,A1;
    then A2:Z c= dom f by TARSKI:def 3;
B4: for x st x in Z holds f.x = 1*x+a by B1;
    then A3:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = 1 by FDIFF_1:31,A2;
    now let x;
      assume A4:x in Z;
      then A5:f is_differentiable_in x by FDIFF_1:16,A3;
      f.x >0 by B1,A4;
      hence ( #R (1/2))*f is_differentiable_in x by TAYLOR_1:22,A5;
    end;
    then B2:( #R (1/2))*f is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds ((2(#)(( #R (1/2))*f))`|Z).x =(a+x) #R (-1/2)
    proof
      let x;
      assume A4:x in Z;
      then A5:f is_differentiable_in x by FDIFF_1:16,A3;
A7:   f.x = a+x & f.x >0 by B1,A4;
      ((2(#)(( #R (1/2))*f))`|Z).x =2*diff((( #R (1/2))*f),x)
      by B2,B1,FDIFF_1:28,A4
      .=2*((1/2)*( ( f.x) #R (1/2-1)) * diff(f,x)) by TAYLOR_1:22,A7,A5
      .=2*((1/2)*( ( f.x) #R (1/2-1))*(f`|Z).x) by A3,A4,FDIFF_1:def 8
      .=2*((1/2)*( (a+x) #R (1/2-1))*1) by FDIFF_1:31,A2,B4,A4,A7
      .= (a+x) #R (-1/2);
      hence ((2(#)(( #R (1/2))*f))`|Z).x =(a+x) #R (-1/2);
    end;
    hence thesis by B1,FDIFF_1:28,B2;
  end;

:: 31 f.x=(-2)*(a-x)#R (1/2)
theorem
  Z c= dom (((-2)(#)(( #R (1/2))*f))) &
    (for x st x in Z holds f.x=a-x & f.x>0)
  implies (-2)(#)(( #R (1/2))*f) is_differentiable_on Z &
  for x st x in Z holds (((-2)(#)(( #R (1/2))*f))`|Z).x =(a-x) #R (-1/2)
  proof
    assume B1:Z c= dom (((-2)(#)(( #R (1/2))*f))) &
    (for x st x in Z holds f.x=a-x & f.x>0);
    then A1:Z c= dom (( #R (1/2))*f) &
    (for x st x in Z holds f.x=a-x & f.x>0) by SEQ_1:def 6;
    for y st y in Z holds y in dom f by FUNCT_1:21,A1;
    then A2:Z c= dom f by TARSKI:def 3;
B4: for x st x in Z holds f.x =(-1)*x+a
    proof
      let x;
      assume x in Z;
      then f.x =a-x by B1;
      hence thesis;
    end;
    then A3:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = -1 by FDIFF_1:31,A2;
    now let x;
      assume A4:x in Z;
      then A5:f is_differentiable_in x by FDIFF_1:16,A3;
      f.x >0 by B1,A4;
      hence ( #R (1/2))*f is_differentiable_in x by TAYLOR_1:22,A5;
    end;
    then B2:( #R (1/2))*f is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds (((-2)(#)(( #R (1/2))*f))`|Z).x =(a-x) #R (-1/2)
    proof
      let x;
      assume A4:x in Z;
      then A5:f is_differentiable_in x by FDIFF_1:16,A3;
A7:   f.x = a-x & f.x >0 by B1,A4;
      (((-2)(#)(( #R (1/2))*f))`|Z).x =(-2)*diff((( #R (1/2))*f),x)
      by B1,FDIFF_1:28,B2,A4
      .=(-2)*((1/2)*( ( f.x) #R (1/2-1)) * diff(f,x)) by TAYLOR_1:22,A7,A5
      .=(-2)*((1/2)*( ( f.x) #R (1/2-1))*(f`|Z).x) by A3,A4,FDIFF_1:def 8
      .=(-2)*((1/2)*( (a-x) #R (1/2-1))*(-1)) by FDIFF_1:31,A2,B4,A4,A7
      .= (a-x) #R (-1/2);
      hence (((-2)(#)(( #R (1/2))*f))`|Z).x =(a-x) #R (-1/2);
    end;
    hence thesis by B1,FDIFF_1:28,B2;
  end;

:: 32 f.x=(2/(3*b))*((a+b*x) #R (3/2))
theorem
  Z c= dom ((2/(3*b))(#)(( #R (3/2))*f)) & (for x st x in Z holds f.x=a+b*x &
  b<>0 & f.x>0) implies (2/(3*b))(#)(( #R (3/2))*f) is_differentiable_on Z &
  for x st x in Z holds (((2/(3*b))(#)(( #R (3/2))*f))`|Z).x =(a+b*x) #R (1/2)
  proof
    assume
    A1:Z c= dom ((2/(3*b))(#)(( #R (3/2))*f)) &
    (for x st x in Z holds f.x=a+b*x & b<>0 & f.x>0);
    then B1:Z c= dom (( #R (3/2))*f) by SEQ_1:def 6;
    for y st y in Z holds y in dom f by FUNCT_1:21,B1;
    then A2:Z c= dom f by TARSKI:def 3;
B4: for x st x in Z holds f.x = b*x+a by A1;
    then A3:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = b by FDIFF_1:31,A2;
    now let x;
      assume A4:x in Z;
      then A5:f is_differentiable_in x by FDIFF_1:16,A3;
      f.x >0 by A1,A4;
      hence ( #R (3/2))*f is_differentiable_in x by TAYLOR_1:22,A5;
    end;
    then B2:( #R (3/2))*f is_differentiable_on Z by B1,FDIFF_1:16;
    for x st x in Z holds
      (((2/(3*b))(#)(( #R (3/2))*f))`|Z).x =(a+b*x) #R (1/2)
    proof
      let x;
      assume A4:x in Z;
      then A5:f is_differentiable_in x by FDIFF_1:16,A3;
      b<>0 by A1,A4;
      then b>0 or b<0;
      then A8:3*b <>0;
A7:   f.x = a+b*x & f.x >0 by A1,A4;
      (((2/(3*b))(#)(( #R (3/2))*f))`|Z).x =(2/(3*b))*diff((( #R (3/2))*f),x)
      by B2,A1,FDIFF_1:28,A4
      .=(2/(3*b))*((3/2)*( ( f.x) #R (3/2-1)) * diff(f,x))
      by TAYLOR_1:22,A7,A5
      .=(2/(3*b))*((3/2)*( ( f.x) #R (3/2-1))*(f`|Z).x) by A3,A4,FDIFF_1:def 8
      .=(2/(3*b))*((3/2)*( (a+b*x) #R (3/2-1))*b) by FDIFF_1:31,A2,B4,A4,A7
      .=(2/(3*b))*((3*b)/2)*( (a+b*x) #R (3/2-1))
      .=1*( (a+b*x) #R (3/2-1)) by A8,XCMPLX_1:113
      .=(a+b*x) #R (1/2);
      hence thesis;
    end;
    hence thesis by B2,A1,FDIFF_1:28;
  end;

:: 33 f.x=(-2/(3*b))*((a-b*x) #R (3/2))
theorem
  Z c= dom ((-2/(3*b))(#)(( #R (3/2))*f)) &
  (for x st x in Z holds f.x=a-b*x & b<>0 & f.x>0) implies
  (-2/(3*b))(#)(( #R (3/2))*f) is_differentiable_on Z &
  for x st x in Z holds
    (((-2/(3*b))(#)(( #R (3/2))*f))`|Z).x =(a-b*x) #R (1/2)
  proof
    assume A1:Z c= dom ((-2/(3*b))(#)(( #R (3/2))*f)) &
    (for x st x in Z holds f.x=a-b*x & b<>0 & f.x>0);
    then B1:Z c= dom (( #R (3/2))*f) by SEQ_1:def 6;
    for y st y in Z holds y in dom f by FUNCT_1:21,B1;
    then A2:Z c= dom f by TARSKI:def 3;
B4: for x st x in Z holds f.x =(-b)*x+a
    proof
      let x;
      assume x in Z;
      then f.x =a-b*x by A1;
      hence thesis;
    end;
    then A3:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = -b by FDIFF_1:31,A2;
    now let x;
      assume A4:x in Z;
      then A5:f is_differentiable_in x by FDIFF_1:16,A3;
      f.x >0 by A1,A4;
      hence ( #R (3/2))*f is_differentiable_in x by TAYLOR_1:22,A5;
    end;
    then B2:( #R (3/2))*f is_differentiable_on Z by B1,FDIFF_1:16;
    for x st x in Z holds
      (((-2/(3*b))(#)(( #R (3/2))*f))`|Z).x =(a-b*x) #R (1/2)
    proof
      let x;
      assume A4:x in Z;
      then A5:f is_differentiable_in x by FDIFF_1:16,A3;
      b<>0 by A1,A4;
      then b>0 or b<0;
      then A8:3*b <>0;
A7:   f.x = a-b*x & f.x >0 by A1,A4;
      (((-2/(3*b))(#)(( #R (3/2))*f))`|Z).x =(-2/(3*b))*diff((( #R (3/2))*f),x) 
      by B2,A1,FDIFF_1:28,A4
      .=(-2/(3*b))* ((3/2)*( ( f.x) #R (3/2-1)) * diff(f,x))
        by TAYLOR_1:22,A7,A5
      .=(-2/(3*b))* ((3/2)*( ( f.x) #R (3/2-1))*(f`|Z).x)
        by A3,A4,FDIFF_1:def 8
      .=(-2/(3*b))* ((3/2)*( (a-b*x) #R (3/2-1))*(-b))
        by FDIFF_1:31,A2,B4,A4,A7
      .=(2/(3*b))* ((3*b)/2)*( (a-b*x) #R (3/2-1))
      .=1*( (a-b*x) #R (3/2-1)) by A8,XCMPLX_1:113
      .=(a-b*x) #R (1/2);
      hence thesis;
    end;
    hence thesis by B2,A1,FDIFF_1:28;
  end;

:: 34 f.x=(a^2+x|^2) #R (1/2)
theorem 
  Z c= dom (( #R (1/2))*f) & f=f1+f2 & f2=#Z 2 &
  (for x st x in Z holds f1.x=a^2 & f.x>0)
  implies ( #R (1/2))*f is_differentiable_on Z &
  for x st x in Z holds ((( #R (1/2))*f)`|Z).x =x * (a^2+x |^2) #R (-1/2)
  proof
    assume A1:Z c= dom (( #R (1/2))*f) & f=f1+f2 & f2=#Z 2 &
    (for x st x in Z holds f1.x=a^2 & f.x>0);
    for y st y in Z holds y in dom f by FUNCT_1:21,A1;
    then A10:Z c= dom (f1+f2) & f2=#Z 2 &
    (for x st x in Z holds f1.x=a^2 ) by A1,TARSKI:def 3;
    then A3:f is_differentiable_on Z & 
    for x st x in Z holds (f`|Z).x = 2*x by A1,th17;
B3: for x st x in Z holds ( #R (1/2))*f is_differentiable_in x
    proof
      let x;
      assume A4:x in Z;
      then A5:f is_differentiable_in x by FDIFF_1:16,A3;
      f.x >0 by A1,A4;
      hence ( #R (1/2))*f is_differentiable_in x by TAYLOR_1:22,A5;
    end;
    then A:( #R (1/2))*f is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds ((( #R (1/2))*f)`|Z).x =x * (a^2+x |^2) #R (-1/2) 
    proof
      let x;
      assume A4:x in Z;
      then A9: x in dom (f1+f2) by A1,FUNCT_1:21;
A5:   f is_differentiable_in x by FDIFF_1:16,A3,A4;
B2:   ( f1+f2).x=f1.x + f2.x by SEQ_1:def 3,A9
      .=a^2 +(f2.x) by A1,A4
      .=a^2 +(x #Z 2) by A1,TAYLOR_1:def 1
      .=a^2 +(x |^2) by PREPOWER:46;
      then A7: f.x = a^2+x |^2 & f.x >0 by A1,A4;
      diff(( #R (1/2)) *f,x) = (1/2)*( ( f.x) #R (1/2-1)) * diff(f,x)
      by TAYLOR_1:22,A5,A7
      .= (1/2)*( ( f.x) #R (1/2-1))*(f`|Z).x by A3,A4,FDIFF_1:def 8
      .= (1/2)*( (a^2+x |^2) #R (1/2-1))*(2*x) by A1,th17,A10,A4,B2
      .=(1/2)*(2*x)*( (a^2+x |^2) #R (1/2-1))
      .=x* (a^2+x |^2) #R (-1/2);
      hence ((( #R (1/2))*f)`|Z).x =x* (a^2+x |^2) #R (-1/2) 
      by A4,A,FDIFF_1:def 8;
    end;
    hence thesis by A1,FDIFF_1:16,B3;
  end;

:: 35 f.x=-(a^2-x|^2) #R (1/2)
theorem 
  Z c= dom -(( #R (1/2))*f) & f=f1-f2 & f2=#Z 2 & 
  (for x st x in Z holds f1.x=a^2 & f.x >0) implies
  -(( #R (1/2))*f) is_differentiable_on Z &
  for x st x in Z holds 
  ((-(( #R (1/2))*f))`|Z).x = x* (a^2-x |^2) #R (-1/2)  
  proof
    assume A1:Z c= dom -(( #R (1/2))*f) & f=f1-f2 & f2=#Z 2 & 
    (for x st x in Z holds f1.x=a^2 & f.x >0);
B4: -(( #R (1/2))*f) = (-1)(#)(( #R (1/2))*f) by RFUNCT_1:38;
A2: Z c= dom (( #R (1/2))*f) by A1,SEQ_1:def 7;
B1: for y st y in Z holds y in dom f by FUNCT_1:21,A2;
A5: f1+(-1)(#)f2=f1+-f2 by RFUNCT_1:38
    .=f by A1, RFUNCT_1:40;
    then A6:Z c= dom ((f1+(-1)(#)f2)) by TARSKI:def 3,B1;
B7: f2=#Z 2 & for x st x in Z holds f1.x=a^2+0*x by A1;
    then A8:f is_differentiable_on Z & 
    for x st x in Z holds (f`|Z).x =0+2*(-1)*x by A5,th12,A6;
    now let x;
      assume A4:x in Z;
      then A5:f is_differentiable_in x by FDIFF_1:16,A8;
      f.x >0 by A1,A4;
      hence ( #R (1/2))*f is_differentiable_in x by TAYLOR_1:22,A5;
    end;
    then B5:( #R (1/2))*f is_differentiable_on Z by A2,FDIFF_1:16;
    for x st x in Z holds
      (((-1)(#)(( #R (1/2))*f))`|Z).x =x* (a^2-x |^2) #R (-1/2)
    proof
      let x;
      assume A4:x in Z;
      then A9: x in dom (f1-f2) by FUNCT_1:21,A2,A1;
B8:   f is_differentiable_in x by FDIFF_1:16,A8,A4;
B6:   ( f1-f2).x=f1.x - f2.x by SEQ_1:def 4,A9
      .=a^2 -(f2.x) by A1,A4
      .=a^2 -(x #Z 2) by A1,TAYLOR_1:def 1
      .=a^2 -(x |^2) by PREPOWER:46;
      then A7: f.x = a^2-x |^2 & f.x >0 by A1,A4;
      (((-1)(#)(( #R (1/2))*f))`|Z).x =(-1)*diff((( #R (1/2))*f),x) 
      by A1,FDIFF_1:28,B4,B5,A4 
      .=(-1)*((1/2)*( ( f.x) #R (1/2-1)) * diff(f,x))
      by TAYLOR_1:22,A7,B8
      .=(-1)*((1/2)*( ( f.x) #R (1/2-1))*(f`|Z).x) by A8,A4,FDIFF_1:def 8
      .=(-1)*((1/2)*( ( f.x) #R (1/2-1))*(0+2*(-1)*x))
        by A5,th12,A6,B7,A4
      .=x*( ( f.x) #R (1/2-1))
      .=x* (a^2-x |^2) #R (-1/2) by A1,B6;
      hence thesis;
    end;
    hence thesis by A1,FDIFF_1:28,B4,B5;
  end;

:: 36 f.x=(x+x|^2) #R (1/2)
theorem 
  Z c= dom ((2(#)(( #R (1/2))*f))) & f=f1+f2 & f2=#Z 2 &
  (for x st x in Z holds f1.x=x & f.x>0)
  implies 2(#)(( #R (1/2))*f) is_differentiable_on Z &
  for x st x in Z holds
    ((2(#)(( #R (1/2))*f))`|Z).x =(2*x+1)*(x |^2+x) #R (-1/2)
  proof
    assume that A1:Z c= dom ((2(#)(( #R (1/2))*f))) and
A2: f=f1+f2 and
A3: f2=#Z 2 and
B4: for x st x in Z holds f1.x=x & f.x>0;
B1: Z c= dom (( #R (1/2))*f) by SEQ_1:def 6,A1;
B7: for y st y in Z holds y in dom f by FUNCT_1:21,B1;
A7: f2=1(#)f2 by RFUNCT_1:33;
    then A9:Z c= dom (f1+1(#)f2) by TARSKI:def 3,B7,A2;
    for x st x in Z holds f1.x=0+1*x by B4;
    then B2:f1+f2 is_differentiable_on Z & 
    for x st x in Z holds ((f1+f2)`|Z).x = 1+2*1*x by A7,th12,A3,A9;
    now let x;
      assume A4:x in Z;
      then A5:f is_differentiable_in x by FDIFF_1:16,A2,B2;
      f.x >0 by B4,A4;
      hence ( #R (1/2))*f is_differentiable_in x by TAYLOR_1:22,A5;
    end;
    then B5:( #R (1/2))*f is_differentiable_on Z by B1,FDIFF_1:16;
    for x st x in Z holds
      ((2(#)(( #R (1/2))*f))`|Z).x =(2*x+1)*(x |^2+x) #R (-1/2)
    proof
      let x;
      assume A4:x in Z;
      then A9: x in dom (f1+f2) by FUNCT_1:21,B1,A2;
A5:   f is_differentiable_in x by FDIFF_1:16,A2,B2,A4;
B6:   ( f1+f2).x=f1.x + f2.x by SEQ_1:def 3,A9
      .=x +(f2.x) by B4,A4
      .=x +(x #Z 2) by A3,TAYLOR_1:def 1
      .=x +x |^2 by PREPOWER:46;
      then A7: f.x = x+x |^2 & f.x >0 by B4,A2,A4;
      ((2(#)(( #R (1/2))*f))`|Z).x =2*diff((( #R (1/2))*f),x)
        by A1,FDIFF_1:28,B5,A4
      .=2*((1/2)*( ( f.x) #R (1/2-1)) * diff(f,x)) by TAYLOR_1:22,A7,A5
      .=2*((1/2)*( ( f.x) #R (1/2-1))*(f`|Z).x) by A2,B2,A4,FDIFF_1:def 8
      .=2*(1/2)*( ( f.x) #R (1/2-1))*(f`|Z).x
      .=(2*x+1)* (x |^2+x) #R (-1/2) by A2,B2,A4,B6;
      hence thesis;
    end;
    hence thesis by B5,A1,FDIFF_1:28;
  end;

:: trigonometric functions
:: 37 f.x=sin(a*x+b)
theorem 
  Z c= dom (sin*f) & (for x st x in Z holds f.x=a*x+b)
  implies sin*f is_differentiable_on Z & 
  for x st x in Z holds ((sin*f)`|Z).x = a* cos.(a*x+b) 
  proof
    assume that A1:Z c= dom (sin*f) and 
A2: for x st x in Z holds f.x=a*x+b;
    for y st y in Z holds y in dom f by FUNCT_1:21,A1;
    then B9:Z c= dom f by TARSKI:def 3;
    then A3:f is_differentiable_on Z & 
    for x st x in Z holds (f`|Z).x = a by A2,FDIFF_1:31;
B8: for x st x in Z holds sin*f is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then A6: f is_differentiable_in x by FDIFF_1:16,A3;  
      sin is_differentiable_in f.x by SIN_COS:69;
      hence sin*f is_differentiable_in x by A6,FDIFF_2:13;
    end;
    then A7:sin*f is_differentiable_on Z by FDIFF_1:16,A1;
    for x st x in Z holds ((sin*f)`|Z).x = a* cos.(a*x+b) 
    proof
      let x;
      assume A4:x in Z;
      then A6: f is_differentiable_in x by FDIFF_1:16,A3;
      sin is_differentiable_in f.x by SIN_COS:69;
      then diff(sin*f,x) = diff(sin, f.x)*diff(f,x) by A6,FDIFF_2:13
      .=cos.( f.x)*diff(f,x) by SIN_COS:69
      .=cos.(a*x+b)*diff(f,x) by A2,A4
      .=cos.(a*x+b)*(f`|Z).x by A3,FDIFF_1:def 8,A4
      .=a* cos.(a*x+b) by B9,A2,FDIFF_1:31,A4;
      hence thesis by FDIFF_1:def 8,A7,A4;
    end;
    hence thesis by FDIFF_1:16,A1,B8;
  end;

:: 38 f.x=sin(a*x+b)
theorem 
  Z c= dom (cos*f) & (for x st x in Z holds f.x=a*x+b)
  implies cos*f is_differentiable_on Z & 
  for x st x in Z holds ((cos*f)`|Z).x = -a* sin.(a*x+b) 
  proof
    assume that A1:Z c= dom (cos*f) and
A2: for x st x in Z holds f.x=a*x+b;
    for y st y in Z holds y in dom f by FUNCT_1:21,A1;
    then B1:Z c= dom f by TARSKI:def 3;
    then A3:f is_differentiable_on Z & 
    for x st x in Z holds (f`|Z).x = a by A2,FDIFF_1:31;
B2: for x st x in Z holds cos*f is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then A6: f is_differentiable_in x by FDIFF_1:16,A3;  
      cos is_differentiable_in f.x by SIN_COS:68;
      hence cos*f is_differentiable_in x by A6,FDIFF_2:13;
    end;
    then A7:cos*f is_differentiable_on Z by FDIFF_1:16,A1;
    for x st x in Z holds ((cos*f)`|Z).x = -a* sin.(a*x+b) 
    proof
      let x;
      assume A4:x in Z;
      then A6: f is_differentiable_in x by FDIFF_1:16,A3;
A8:   diff(cos, f.x)=-sin.( f.x) by SIN_COS:68;
      cos is_differentiable_in f.x by SIN_COS:68;
      then diff(cos*f,x) = diff(cos, f.x)*diff(f,x) by A6,FDIFF_2:13
      .=-sin.( f.x)*diff(f,x) by A8
      .=-sin.(a*x+b)*diff(f,x) by A2,A4
      .=-sin.(a*x+b)*(f`|Z).x by A3,FDIFF_1:def 8,A4
      .=-a*sin.(a*x+b) by A2,FDIFF_1:31,B1,A4;
      hence thesis by FDIFF_1:def 8,A7,A4;
    end;
    hence thesis by FDIFF_1:16,A1,B2;
  end;

:: 39 f.x=1/cos.x
theorem
  (for x st x in Z holds cos.x<>0) implies
  cos^ is_differentiable_on Z & for x st x in Z holds
  ( (cos^)`|Z).x= sin.x/(cos.x)^2
  proof
    assume
A2: for x st x in Z holds cos.x<>0;
A3: cos is_differentiable_on Z by SIN_COS:72,FDIFF_1:34;
    then A5:cos^ is_differentiable_on Z by FDIFF_2:22,A2;
    for x st x in Z holds ( (cos^)`|Z).x= sin.x/(cos.x)^2
    proof
      let x;
      assume A6:x in Z;
A7:   cos.x<>0 by A2,A6;
B1:   cos is_differentiable_in x by SIN_COS:68;
      ( (cos^)`|Z).x= diff(cos^,x) by A5,A6,FDIFF_1:def 8
      .= - diff(cos,x)/(cos.x)^2 by A7,FDIFF_2:15,B1
      .=- (-sin.x)/(cos.x)^2 by SIN_COS:68
      .=- -(sin.x/(cos.x)^2) by XCMPLX_1:188
      .=sin.x/(cos.x)^2;
      hence thesis;
    end;
    hence thesis by FDIFF_2:22,A2,A3;
  end;

:: 40 f.x=1/sin.x
theorem
  (for x st x in Z holds sin.x<>0) implies
  sin^ is_differentiable_on Z &
  for x st x in Z holds ( (sin^)`|Z).x= -cos.x/(sin.x)^2
  proof
    assume
A2: for x st x in Z holds sin.x<>0;
A3: sin is_differentiable_on Z by SIN_COS:73,FDIFF_1:34;
A5: sin^ is_differentiable_on Z by A3,FDIFF_2:22,A2;
    for x st x in Z holds ( (sin^)`|Z).x= -cos.x/(sin.x)^2
    proof
      let x;
      assume A6:x in Z;
A7:   sin.x<>0 by A2,A6;
B1:   sin is_differentiable_in x by A6,A3,FDIFF_1:16;
      ( (sin^)`|Z).x= diff(sin^,x) by A5,A6,FDIFF_1:def 8
      .= - diff(sin,x)/(sin.x)^2 by A7,FDIFF_2:15,B1 
      .=- cos.x/(sin.x)^2 by SIN_COS:69;
      hence thesis;
    end;
    hence thesis by A3,FDIFF_2:22,A2;
  end;

:: 41 f.x=sin.x*cos.x
theorem 
  Z c= dom (sin(#)cos) implies sin(#)cos is_differentiable_on Z & 
  for x st x in Z holds ((sin(#)cos)`|Z).x = cos(2*x)
  proof
    assume A1: Z c= dom (sin(#)cos);
A7: sin is_differentiable_on Z by SIN_COS:73,FDIFF_1:34;
A2: cos is_differentiable_on Z by SIN_COS:72,FDIFF_1:34;
    now let x;
      assume x in Z;
      hence ((sin(#)cos)`|Z).x =(cos.x)*diff(sin,x) + (sin.x)*diff(cos,x) 
      by A2,A7,A1,FDIFF_1:29
      .=cos.x * cos.x + (sin.x)*diff(cos,x) by SIN_COS:69
      .=cos.x * cos.x + (sin.x)*(-sin.x) by SIN_COS:68
      .=cos.x * cos.x -sin.x*sin.x
      .=(cos.x)^2-sin.x*sin.x by SQUARE_1:def 3
      .=(cos.x)^2-(sin.x)^2 by SQUARE_1:def 3
      .=(cos(x))^2-(sin.x)^2 by SIN_COS:def 23
      .=(cos(x))^2-(sin(x))^2 by SIN_COS:def 21
      .=cos(2*x) by SIN_COS5:7;
    end;
    hence thesis by A2,A7,A1,FDIFF_1:29;
  end;

:: 42 f.x=ln(cos.x)
theorem      
  Z c= dom ((log_ number_e)*cos) & (for x st x in Z holds cos.x >0)
  implies (log_ number_e)*cos is_differentiable_on Z &
  for x st x in Z holds (((log_ number_e)*cos)`|Z).x =- tan(x) 
  proof
    assume that A1:Z c= dom ((log_ number_e)*cos) and
A2: for x st x in Z holds cos.x >0;
A3: cos is_differentiable_on Z by SIN_COS:72,FDIFF_1:34;
A6: for x st x in Z holds (log_ number_e)*cos is_differentiable_in x
    proof
      let x;
      assume A4:x in Z;
      then A5:cos is_differentiable_in x by FDIFF_1:16,A3;
      cos.x >0 by A2,A4;
      hence thesis by TAYLOR_1:20,A5;
    end;
    then A:(log_ number_e)*cos is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds (((log_ number_e)*cos)`|Z).x =- tan(x) 
    proof
      let x;
      assume A4:x in Z;
      then A5:cos is_differentiable_in x by FDIFF_1:16,A3;
      cos.x >0 by A2,A4;
      then diff((log_ number_e)*cos,x) =diff(cos,x)/(cos.x) 
      by TAYLOR_1:20,A5
      .=(-sin.x)/cos.x by SIN_COS:68
      .=- (sin.x/cos.x) by XCMPLX_1:188
      .=- (sin(x)/cos.x) by SIN_COS:def 21
      .=- (sin(x)/cos(x)) by SIN_COS:def 23
      .=- tan(x) by SIN_COS4:def 1;
      hence thesis by A4,A,FDIFF_1:def 8;
    end;
    hence thesis by A6,A1,FDIFF_1:16;
  end;

:: 43 f.x=ln(sin.x)
theorem      
  Z c= dom ((log_ number_e)*sin) & (for x st x in Z holds sin.x >0)
  implies (log_ number_e)*sin is_differentiable_on Z &
  for x st x in Z holds (((log_ number_e)*sin)`|Z).x = cot(x) 
  proof
    assume that A1:Z c= dom ((log_ number_e)*sin) and
A2: for x st x in Z holds sin.x >0;
A3: sin is_differentiable_on Z by SIN_COS:73,FDIFF_1:34;
A6: for x st x in Z holds (log_ number_e)*sin is_differentiable_in x
    proof
      let x;
      assume A4:x in Z;
      then A5:sin is_differentiable_in x by FDIFF_1:16,A3;
      sin.x >0 by A2,A4;
      hence (log_ number_e)*sin is_differentiable_in x by TAYLOR_1:20,A5;
    end;
    then A:(log_ number_e)*sin is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds (((log_ number_e)*sin)`|Z).x =cot(x) 
    proof
      let x;
      assume A4:x in Z;
      then A5:sin is_differentiable_in x by FDIFF_1:16,A3;
      sin.x >0 by A2,A4;
      then diff((log_ number_e)*sin,x) =diff(sin,x)/(sin.x) 
      by TAYLOR_1:20,A5
      .=cos.x/sin.x by SIN_COS:69
      .=cos(x)/sin.x by SIN_COS:def 23
      .=cos(x)/sin(x) by SIN_COS:def 21
      .=cot(x) by SIN_COS4:def 2;
      hence thesis by A4,A,FDIFF_1:def 8;
    end;
    hence thesis by A6,A1,FDIFF_1:16;
  end;

:: 44 f.x=-x*cos.x
theorem th44:
  Z c= dom ((-id Z)(#)cos) implies (-id Z)(#)cos is_differentiable_on Z & 
  for x st x in Z holds (((-id Z)(#)cos)`|Z).x =-cos.x+x*sin.x
  proof
    assume A1:Z c= dom ((-id Z)(#)cos);
    then Z c= dom (-id Z) /\ dom cos by SEQ_1:def 5;
    then A2:Z c= dom (-id Z) & Z c= dom cos by XBOOLE_1:18; 
A6: for x st x in Z holds (-id Z).x =(-1)*x +0
    proof
      let x;
      assume A4:x in Z;
      then (-id Z).x = -(id Z.x) by SEQ_1:def 7,A2
      .=-x by FUNCT_1:35,A4
      .=(-1)*x +0;
      hence thesis;
    end;
    then A3:-id Z is_differentiable_on Z & for x st x in Z 
    holds ((-id Z)`|Z).x = -1 by FDIFF_1:31,A2;
B1: cos is_differentiable_on Z by SIN_COS:72,FDIFF_1:34;
    now let x;
      assume A4:x in Z;
      hence (((-id Z)(#)cos)`|Z).x 
      = (cos.x)*diff((-id Z),x) + ((-id Z).x)*diff(cos,x)
        by A3,A1,FDIFF_1:29,B1
      .=(cos.x)*((-id Z)`|Z).x+ ((-id Z).x)*diff(cos,x)
        by A3,A4,FDIFF_1:def 8
      .=(cos.x)*(-1)+ ((-id Z).x)*diff(cos,x) by A4,FDIFF_1:31,A2,A6
      .=(cos.x)*(-1)+((-id Z).x)*(-sin.x) by SIN_COS:68
      .=-cos.x+((-1)*x +0)*(-sin.x) by A6,A4
      .=-cos.x+x*sin.x;
    end;
    hence thesis by A3,A1,FDIFF_1:29,B1;
  end;

:: f.x=x*sin.x
theorem th45:
  Z c= dom ((id Z)(#)sin) implies (id Z)(#)sin is_differentiable_on Z & 
  for x st x in Z holds (((id Z)(#)sin)`|Z).x =sin.x+x*cos.x
  proof
    assume A1:Z c= dom ((id Z)(#)sin);
    then Z c= dom (id Z) /\ dom sin by SEQ_1:def 5;
    then A2:Z c= dom (id Z) & Z c= dom sin by XBOOLE_1:18;
B2: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;
    then A3:id Z is_differentiable_on Z & for x st x in Z 
    holds ((id Z)`|Z).x = 1 by FDIFF_1:31,A2;
B1: sin is_differentiable_on Z by SIN_COS:73,FDIFF_1:34;
    now let x;
      assume A4:x in Z;
      hence (((id Z)(#)sin)`|Z).x 
      = (sin.x)*diff((id Z),x) + ((id Z).x)*diff(sin,x) by A3,A1,FDIFF_1:29,B1
      .=(sin.x)*((id Z)`|Z).x+ ((id Z).x)*diff(sin,x) by A3,A4,FDIFF_1:def 8
      .=(sin.x)*1+ ((id Z).x)*diff(sin,x) by A4,FDIFF_1:31,A2,B2
      .=(sin.x)*1+((id Z).x)*(cos.x) by SIN_COS:69
      .=sin.x+ x*cos.x by FUNCT_1:35,A4;
    end;
    hence thesis by A3,A1,FDIFF_1:29,B1;
  end;

:: 46 f.x=-x*cos.x+sin.x
theorem 
  Z c= dom ((-id Z)(#)cos+sin) implies
    (-id Z)(#)cos+sin is_differentiable_on Z &
  for x st x in Z holds (((-id Z)(#)cos+sin)`|Z).x =x*sin.x
  proof
    assume A1:Z c= dom ((-id Z)(#)cos+sin);
    then Z c= dom ((-id Z)(#)cos) /\ dom sin by SEQ_1:def 3;
    then A7:Z c= dom ((-id Z)(#)cos) & Z c= dom sin by XBOOLE_1:18;
    then A3:(-id Z)(#)cos is_differentiable_on Z & 
    for x st x in Z holds (((-id Z)(#)cos)`|Z).x =-cos.x+x*sin.x by th44;
A4: sin is_differentiable_on Z by SIN_COS:73,FDIFF_1:34;
    now let x;
      assume A6:x in Z;
      hence (((-id Z)(#)cos+sin)`|Z).x = diff((-id Z)(#)cos,x) + diff(sin,x) 
      by A3,A1,A4,FDIFF_1:26
      .=(((-id Z)(#)cos)`|Z).x + diff(sin,x) by FDIFF_1:def 8,A3,A6
      .=-cos.x+x*sin.x+ diff(sin,x) by th44,A7,A6
      .=-cos.x+x*sin.x+cos.x by SIN_COS:69
      .=x*sin.x;
    end;
    hence thesis by A3,A1,A4,FDIFF_1:26;
  end;

:: 47 f.x=x*sin.x+cos.x
theorem 
  Z c= dom ((id Z)(#)sin+cos) implies (id Z)(#)sin+cos is_differentiable_on Z & 
  for x st x in Z holds (((id Z)(#)sin+cos)`|Z).x =x*cos.x
  proof
    assume A1:Z c= dom ((id Z)(#)sin+cos);
    then Z c= dom ((id Z)(#)sin) /\ dom cos by SEQ_1:def 3;
    then A7:Z c= dom ((id Z)(#)sin) & Z c= dom cos by XBOOLE_1:18;
    then A3:(id Z)(#)sin is_differentiable_on Z & 
    for x st x in Z holds (((id Z)(#)sin)`|Z).x =sin.x+ x*cos.x by th45;
A4: cos is_differentiable_on Z by SIN_COS:72,FDIFF_1:34;
    now let x;
      assume A6:x in Z;
      hence (((id Z)(#)sin+cos)`|Z).x = diff((id Z)(#)sin,x) + diff(cos,x) 
      by A3,A1,A4,FDIFF_1:26
      .=(((id Z)(#)sin)`|Z).x + diff(cos,x) by FDIFF_1:def 8,A3,A6
      .=sin.x+ x*cos.x+ diff(cos,x) by th45,A7,A6
      .=sin.x+ x*cos.x+(-sin.x) by SIN_COS:68
      .=x*cos.x;
    end;
    hence thesis by A3,A1,A4,FDIFF_1:26;
  end;

:: 48 f.x=2*(sin #R (1/2))
theorem 
  Z c= dom ((2(#)(( #R (1/2))*sin))) & (for x st x in Z holds sin.x>0)  
  implies 2(#)(( #R (1/2))*sin) is_differentiable_on Z &
  for x st x in Z holds ((2(#)(( #R (1/2))*sin))`|Z).x =cos.x*(sin.x) #R (-1/2)
  proof
    assume that A1:Z c= dom ((2(#)(( #R (1/2))*sin))) and
A2: for x st x in Z holds sin.x>0;
A3: Z c= dom (( #R (1/2))*sin) by SEQ_1:def 6,A1;
    now let x;
      assume A4:x in Z;
A5:   sin is_differentiable_in x by SIN_COS:69;
      sin.x >0 by A2,A4;
      hence ( #R (1/2))*sin is_differentiable_in x by TAYLOR_1:22,A5;
    end;
    then A6:( #R (1/2))*sin is_differentiable_on Z by A3,FDIFF_1:16;
    for x st x in Z holds
      ((2(#)(( #R (1/2))*sin))`|Z).x =cos.x*(sin.x) #R (-1/2)
    proof
      let x;
      assume A4:x in Z;
A5:   sin is_differentiable_in x by SIN_COS:69;
A7:   sin.x >0 by A2,A4;
      ((2(#)(( #R (1/2))*sin))`|Z).x =2*diff((( #R (1/2))*sin),x)
      by A6,A1,FDIFF_1:28,A4
      .=2*((1/2)*( (sin.x) #R (1/2-1)) * diff(sin,x)) by TAYLOR_1:22,A7,A5
      .=2*((1/2)*( (sin.x) #R (1/2-1)) *cos.x) by SIN_COS:69
      .=( (sin.x) #R (1/2-1)) *cos.x
      .=cos.x*(sin.x) #R (-1/2);
      hence ((2(#)(( #R (1/2))*sin))`|Z).x =cos.x*(sin.x) #R (-1/2);
    end;
    hence thesis by A1,FDIFF_1:28,A6;
  end;

theorem th49:
  Z c= dom ((1/2)(#)(( #Z 2)*sin))
  implies (1/2)(#)(( #Z 2)*sin) is_differentiable_on Z &
  for x st x in Z holds (((1/2)(#)(( #Z 2)*sin))`|Z).x =sin.x*cos.x
  proof
    assume A1:Z c= dom ((1/2)(#)(( #Z 2)*sin)); then
A3: Z c= dom (( #Z 2)*sin) by SEQ_1:def 6;
    now let x;
      assume x in Z;
      sin is_differentiable_in x by SIN_COS:69;
      hence ( #Z 2)*sin is_differentiable_in x by TAYLOR_1:3;
    end;
    then A6:( #Z 2)*sin is_differentiable_on Z by A3,FDIFF_1:16;
    for x st x in Z holds (((1/2)(#)(( #Z 2)*sin))`|Z).x =sin.x*cos.x 
    proof
      let x;
      assume A4:x in Z;
A5:   sin is_differentiable_in x by SIN_COS:69;
      (((1/2)(#)(( #Z 2)*sin))`|Z).x =(1/2)*diff((( #Z 2)*sin),x) 
      by A1,FDIFF_1:28,A6,A4
      .=(1/2)*(2*( (sin.x) #Z (2-1)) * diff(sin,x)) by TAYLOR_1:3,A5
      .=(1/2)*(2*( (sin.x) #Z (2-1)) *cos.x) by SIN_COS:69
      .=( (sin.x) #Z (2-1)) *cos.x
      .=sin.x*cos.x by PREPOWER:45;
      hence thesis;
    end;
    hence thesis by A1,FDIFF_1:28,A6;
  end;

:: 50 f.x=sin.x+(1/2)*(sin.x #Z 2)
theorem
  Z c= dom (sin+(1/2)(#)(( #Z 2)*sin)) &
  (for x st x in Z holds sin.x>0 & sin.x<1) implies
  sin+(1/2)(#)(( #Z 2)*sin) is_differentiable_on Z &
  for x st x in Z holds
    ((sin+(1/2)(#)(( #Z 2)*sin))`|Z).x =(cos.x)|^3/(1-sin.x)
  proof
    assume that A1:Z c= dom (sin+(1/2)(#)(( #Z 2)*sin)) and
B1: for x st x in Z holds sin.x>0 & sin.x<1;
    Z c= dom ((1/2)(#)(( #Z 2)*sin)) /\ dom sin by SEQ_1:def 3,A1;
    then B3:Z c= dom ((1/2)(#)(( #Z 2)*sin)) & Z c= dom sin by XBOOLE_1:18;
    then A3:(1/2)(#)(( #Z 2)*sin) is_differentiable_on Z & 
    for x st x in Z holds
      (((1/2)(#)(( #Z 2)*sin))`|Z).x =sin.x*cos.x by th49;
B2: sin is_differentiable_on Z by SIN_COS:73,FDIFF_1:34;
    now let x;
      assume A6:x in Z;
      then sin.x<1 by B1;
      then A7:1-sin.x>0 by XREAL_1:52;
      ((sin+(1/2)(#)(( #Z 2)*sin))`|Z).x 
      = diff(sin,x) + diff((1/2)(#)(( #Z 2)*sin),x)
        by A3,A1,FDIFF_1:26,B2,A6
      .=cos.x + diff((1/2)(#)(( #Z 2)*sin),x) by SIN_COS:69
      .=cos.x +(((1/2)(#)(( #Z 2)*sin))`|Z).x by FDIFF_1:def 8,A3,A6
      .=cos.x +sin.x*cos.x by th49,B3,A6
      .=cos.x*(1+sin.x)*(1-sin.x)/(1-sin.x) by A7,XCMPLX_1:90
      .=cos.x*((1+sin.x)*(1-sin.x))/(1-sin.x)
      .=cos.x*(1-(sin.x)^2)/(1-sin.x) by SQUARE_1:59,SQUARE_1:67
      .=cos.x*(1-(sin(x))^2)/(1-sin.x) by SIN_COS:def 21
      .=cos.x*(1-(sin(x)*sin(x)))/(1-sin.x) by SQUARE_1:def 3
      .=cos.x*(cos(x)*cos(x))/(1-sin.x) by SIN_COS4:7
      .=cos.x*((cos(x))|^2)/(1-sin.x) by WSIERP_1:2
      .=(cos.x*((cos.x)|^2))/(1-sin.x) by SIN_COS:def 23
      .=((cos.x)|^(2+1))/(1-sin.x) by NEWTON:11
      .=(cos.x)|^3/(1-sin.x);
      hence ((sin+(1/2)(#)(( #Z 2)*sin))`|Z).x=(cos.x)|^3/(1-sin.x);
    end;
    hence thesis by A3,A1,FDIFF_1:26,B2;
  end;

:: 51 f.x=(1/2)*(sin.x #Z 2)-cos.x
theorem  
  Z c= dom ((1/2)(#)(( #Z 2)*sin)-cos) & (for x st x in Z holds sin.x>0 
  & cos.x<1) implies ((1/2)(#)(( #Z 2)*sin)-cos) is_differentiable_on Z &
  for x st x in Z holds 
  ((((1/2)(#)(( #Z 2)*sin)-cos))`|Z).x =(sin.x)|^3/(1-cos.x)
  proof
    assume that A1:Z c= dom ((1/2)(#)(( #Z 2)*sin)-cos) and
B1: for x st x in Z holds sin.x>0 & cos.x<1;
    Z c= dom ((1/2)(#)(( #Z 2)*sin)) /\ dom cos by SEQ_1:def 4,A1;
    then B3:Z c= dom ((1/2)(#)(( #Z 2)*sin)) & Z c= dom cos by XBOOLE_1:18;
    then A3:(1/2)(#)(( #Z 2)*sin) is_differentiable_on Z & 
    for x st x in Z holds
      (((1/2)(#)(( #Z 2)*sin))`|Z).x =sin.x*cos.x by th49;
B2: cos is_differentiable_on Z by SIN_COS:72,FDIFF_1:34;
    now let x;
      assume A6:x in Z;
      then cos.x<1 by B1;
      then A7:1-cos.x>0 by XREAL_1:52;
      (((1/2)(#)(( #Z 2)*sin)-cos)`|Z).x 
      = diff((1/2)(#)(( #Z 2)*sin),x) - diff(cos,x) by A3,A1,FDIFF_1:27,B2,A6
      .=diff((1/2)(#)(( #Z 2)*sin),x)-(-sin.x) by SIN_COS:68
      .=(((1/2)(#)(( #Z 2)*sin))`|Z).x-(-sin.x) by FDIFF_1:def 8,A3,A6
      .=sin.x*cos.x--sin.x by th49,B3,A6
      .=sin.x*(1+cos.x)*(1-cos.x)/(1-cos.x) by A7,XCMPLX_1:90
      .=sin.x*((1+cos.x)*(1-cos.x))/(1-cos.x)
      .=sin.x*(1-(cos.x)^2)/(1-cos.x) by SQUARE_1:59,SQUARE_1:67
      .=sin.x*(1-(cos(x))^2)/(1-cos.x) by SIN_COS:def 23
      .=sin.x*(1-(cos(x)*cos(x)))/(1-cos.x) by SQUARE_1:def 3
      .=sin.x*(sin(x)*sin(x))/(1-cos.x) by SIN_COS4:6
      .=sin.x*((sin(x))|^2)/(1-cos.x) by WSIERP_1:2
      .=(sin.x*((sin.x)|^2))/(1-cos.x) by SIN_COS:def 21
      .=((sin.x)|^(2+1))/(1-cos.x) by NEWTON:11
      .=(sin.x)|^3/(1-cos.x);
      hence (((1/2)(#)(( #Z 2)*sin)-cos)`|Z).x=(sin.x)|^3/(1-cos.x);
    end;
    hence thesis by A3,A1,FDIFF_1:27,B2;
  end;

:: 52 f.x=sin.x-(1/2)*(sin #Z 2)
theorem  
  Z c= dom (sin-(1/2)(#)(( #Z 2)*sin)) & (for x st x in Z holds sin.x>0 
  & sin.x>-1) implies sin-(1/2)(#)(( #Z 2)*sin) is_differentiable_on Z &
  for x st x in Z holds
    ((sin-(1/2)(#)(( #Z 2)*sin))`|Z).x =(cos.x)|^3/(1+sin.x)
  proof
    assume that A1:Z c= dom (sin-(1/2)(#)(( #Z 2)*sin)) and
B1: for x st x in Z holds sin.x>0 & sin.x>-1;
    Z c= dom ((1/2)(#)(( #Z 2)*sin)) /\ dom sin by SEQ_1:def 4,A1;
    then B3:Z c= dom ((1/2)(#)(( #Z 2)*sin)) & Z c= dom sin by XBOOLE_1:18;
    then A3:(1/2)(#)(( #Z 2)*sin) is_differentiable_on Z &
    for x st x in Z holds
      (((1/2)(#)(( #Z 2)*sin))`|Z).x =sin.x*cos.x by th49;
B2: sin is_differentiable_on Z by SIN_COS:73,FDIFF_1:34;
    now let x;
      assume A6:x in Z;
      then sin.x>-1 by B1;
      then B4:sin.x--1>0 by XREAL_1:52;
      ((sin-(1/2)(#)(( #Z 2)*sin))`|Z).x 
      = diff(sin,x) - diff((1/2)(#)(( #Z 2)*sin),x)
        by A3,A1,FDIFF_1:27,B2,A6
      .=cos.x - diff((1/2)(#)(( #Z 2)*sin),x) by SIN_COS:69
      .=cos.x -(((1/2)(#)(( #Z 2)*sin))`|Z).x by FDIFF_1:def 8,A3,A6
      .=cos.x -sin.x*cos.x by B3,th49,A6
      .=cos.x*(1-sin.x)*(1+sin.x)/(1+sin.x) by B4,XCMPLX_1:90
      .=cos.x*((1+sin.x)*(1-sin.x))/(1+sin.x)
      .=cos.x*(1-(sin.x)^2)/(1+sin.x) by SQUARE_1:59,SQUARE_1:67
      .=cos.x*(1-(sin(x))^2)/(1+sin.x) by SIN_COS:def 21
      .=cos.x*(1-(sin(x)*sin(x)))/(1+sin.x) by SQUARE_1:def 3
      .=cos.x*(cos(x)*cos(x))/(1+sin.x) by SIN_COS4:7
      .=cos.x*((cos(x))|^2)/(1+sin.x) by WSIERP_1:2
      .=(cos.x*((cos.x)|^2))/(1+sin.x) by SIN_COS:def 23
      .=((cos.x)|^(2+1))/(1+sin.x) by NEWTON:11
      .=(cos.x)|^3/(1+sin.x);
      hence ((sin-(1/2)(#)(( #Z 2)*sin))`|Z).x=(cos.x)|^3/(1+sin.x);
    end;
    hence thesis by A3,A1,FDIFF_1:27,B2;
  end;

:: 53 f.x=-cos.x-(1/2)*(sin.x #Z 2)
theorem
  Z c= dom (-cos-(1/2)(#)(( #Z 2)*sin)) &
  (for x st x in Z holds sin.x>0 & cos.x>-1) implies
  -cos-(1/2)(#)(( #Z 2)*sin) is_differentiable_on Z &
  for x st x in Z holds
    ((-cos-(1/2)(#)(( #Z 2)*sin))`|Z).x =(sin.x)|^3/(1+cos.x)
  proof
    assume that A1:Z c= dom (-cos-(1/2)(#)(( #Z 2)*sin)) and
B1: for x st x in Z holds sin.x>0 & cos.x>-1;
    Z c= dom ((1/2)(#)(( #Z 2)*sin)) /\ dom (-cos) by SEQ_1:def 4,A1;
    then B2:Z c= dom ((1/2)(#)(( #Z 2)*sin)) & Z c= dom (-cos) by XBOOLE_1:18;
    then A3:(1/2)(#)(( #Z 2)*sin) is_differentiable_on Z & 
    for x st x in Z holds
      (((1/2)(#)(( #Z 2)*sin))`|Z).x =sin.x*cos.x by th49;
B5: (-1)(#)cos=-cos by RFUNCT_1:38;
B3: cos is_differentiable_on Z by SIN_COS:72,FDIFF_1:34;
A4: -cos is_differentiable_on Z & for x st x in Z holds 
    ((-cos)`|Z).x =(-1)*diff(cos,x) by B5,B2,FDIFF_1:28,B3;
    now let x;
      assume A6:x in Z;
      then cos.x>-1 by B1;
      then B8:cos.x--1>0 by XREAL_1:52;
      ((-cos-(1/2)(#)(( #Z 2)*sin))`|Z).x
      =diff((-cos),x)-diff((1/2)(#)(( #Z 2)*sin),x) by A4,A3,A1,FDIFF_1:27,A6
      .=((-cos)`|Z).x-diff((1/2)(#)(( #Z 2)*sin),x) by FDIFF_1:def 8,A4,A6
      .=(-1)*diff(cos,x)-diff((1/2)(#)(( #Z 2)*sin),x) 
      by B5,B2,FDIFF_1:28,B3,A6
      .=(-1)*(-sin.x)-diff((1/2)(#)(( #Z 2)*sin),x) by SIN_COS:68
      .=sin.x-(((1/2)(#)(( #Z 2)*sin))`|Z).x by FDIFF_1:def 8,A3,A6
      .=sin.x-sin.x*cos.x by th49,B2,A6
      .=sin.x*(1-cos.x)*(1+cos.x)/(1+cos.x) by B8,XCMPLX_1:90
      .=sin.x*((1+cos.x)*(1-cos.x))/(1+cos.x)
      .=sin.x*(1-(cos.x)^2)/(1+cos.x) by SQUARE_1:59,SQUARE_1:67
      .=sin.x*(1-(cos(x))^2)/(1+cos.x) by SIN_COS:def 23
      .=sin.x*(1-(cos(x)*cos(x)))/(1+cos.x) by SQUARE_1:def 3
      .=sin.x*(sin(x)*sin(x))/(1+cos.x) by SIN_COS4:6
      .=sin.x*((sin(x))|^2)/(1+cos.x) by WSIERP_1:2
      .=(sin.x*((sin.x)|^2))/(1+cos.x) by SIN_COS:def 21
      .=((sin.x)|^(2+1))/(1+cos.x) by NEWTON:11
      .=(sin.x)|^3/(1+cos.x);
      hence ((-cos-(1/2)(#)(( #Z 2)*sin))`|Z).x=(sin.x)|^3/(1+cos.x);
    end;
    hence thesis by A4,A3,A1,FDIFF_1:27;
  end;

:: 54 f.x=(1/n)*(sin.x #Z n)
theorem
  Z c= dom ((1/n)(#)(( #Z n)*sin)) & n>0
  implies (1/n)(#)(( #Z n)*sin) is_differentiable_on Z &
  for x st x in Z holds (((1/n)(#)(( #Z n)*sin))`|Z).x =(sin.x) #Z (n-1) *cos.x
  proof
    assume that A1:Z c= dom ((1/n)(#)(( #Z n)*sin)) and
B1: n>0;
A3: Z c= dom (( #Z n)*sin) by SEQ_1:def 6,A1;
    now let x;
      assume x in Z;
      sin is_differentiable_in x by SIN_COS:69;
      hence ( #Z n)*sin is_differentiable_in x by TAYLOR_1:3;
    end;
    then B2:( #Z n)*sin is_differentiable_on Z by A3,FDIFF_1:16;
    for x st x in Z holds
      (((1/n)(#)(( #Z n)*sin))`|Z).x =(sin.x) #Z (n-1) *cos.x
    proof
      let x;
      assume A4:x in Z;
A5:   sin is_differentiable_in x by SIN_COS:69;
      (((1/n)(#)(( #Z n)*sin))`|Z).x =(1/n)*diff((( #Z n)*sin),x) 
        by B2,A1,FDIFF_1:28,A4
      .=(1/n)*(n*( (sin.x) #Z (n-1)) * diff(sin,x)) by TAYLOR_1:3,A5
      .=(1/n)*(n*( (sin.x) #Z (n-1)) *cos.x) by SIN_COS:69
      .=((1/n)*n)*( (sin.x) #Z (n-1)) *cos.x
      .=((n")*n)*( (sin.x) #Z (n-1)) *cos.x by XCMPLX_1:217
      .=1*( (sin.x) #Z (n-1)) *cos.x by B1,XCMPLX_0:def 7
      .=(sin.x) #Z (n-1) *cos.x;
      hence thesis;
    end;
    hence thesis by B2,A1,FDIFF_1:28;
  end;

:: exponential function
:: 55 f.x=exp.x*(x-1)
theorem
  Z c= dom (exp(#)f) & (for x st x in Z holds f.x=x-1) implies
  exp(#)f is_differentiable_on Z &
  for x st x in Z holds ((exp(#)f)`|Z).x = x*(exp.x)
  proof
    assume that A1:Z c= dom (exp(#)f) and
A2: for x st x in Z holds f.x=x-1;
    Z c= dom f /\ dom exp by SEQ_1:def 5,A1;
    then B1:Z c= dom f by XBOOLE_1:18;
B2: for x st x in Z holds f.x = 1*x+-1
    proof let x;
      assume A4:x in Z;
      1*x+-1=1*x-1;
      hence thesis by A2,A4;
    end;
    then A3:f is_differentiable_on Z & 
    for x st x in Z holds (f`|Z).x = 1 by FDIFF_1:31,B1;
A4: exp is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
    now let x;
      assume A6:x in Z;
      hence ((exp(#)f)`|Z).x = (f.x)*diff(exp,x) + (exp.x)*diff(f,x)
      by A1,A3,A4,FDIFF_1:29
      .=(x-1)*diff(exp,x)+ (exp.x)*diff(f,x) by A2,A6
      .=(x-1)*(exp.x)+ (exp.x)*diff(f,x) by TAYLOR_1:16
      .=(x-1)*(exp.x)+(exp.x)*(f`|Z).x by A6,A3,FDIFF_1:def 8
      .=(x-1)*(exp.x)+(exp.x)*1 by A6,FDIFF_1:31,B1,B2
      .=x*(exp.x);
    end;
    hence thesis by A1,A3,FDIFF_1:29,A4;
  end;

:: 56 f.x=ln(exp.x/(exp.x+1))
theorem 
  Z c= dom ((log_ number_e)*(exp/(exp+f))) & (for x st x in Z holds f.x=1)
  implies (log_ number_e)*(exp/(exp+f)) is_differentiable_on Z &
  for x st x in Z holds (((log_ number_e)*(exp/(exp+f)))`|Z).x = 1/(exp.x+1) 
  proof
    assume that A1:Z c= dom ((log_ number_e)*(exp/(exp+f))) and
A2: for x st x in Z holds f.x=1;
    for y st y in Z holds y in dom (exp/(exp+f)) by FUNCT_1:21,A1;
    then Z c= dom (exp/(exp+f)) by TARSKI:def 3;
    then Z c= dom exp /\ (dom (exp+f) \ (exp+f)"{0}) by RFUNCT_1:def 4;
    then A4:Z c= dom exp & Z c= dom (exp+f) \ (exp+f)"{0} by XBOOLE_1:18;
    dom ((exp+f)^) c= dom (exp+f) by RFUNCT_1:11;
    then dom (exp+f) \ (exp+f)"{0} c= dom (exp+f) by RFUNCT_1:def 8; 
    then A5:Z c= dom (exp+f) by XBOOLE_1:1,A4;
    then Z c= dom exp /\ dom f by SEQ_1:def 3;
    then A6:Z c= dom f by XBOOLE_1:18;
B1: for x st x in Z holds f.x=0*x+1 by A2;
    then A7:f is_differentiable_on Z & 
    for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A6;
A8: exp is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
    then A9:exp+f is_differentiable_on Z & for x st x in Z holds 
    ((exp+f)`|Z).x = diff(exp,x) + diff(f,x) by A7,A5,FDIFF_1:26;
A10: for x st x in Z holds ((exp+f)`|Z).x =exp.x
     proof
       let x;
       assume A4:x in Z;
       hence ((exp+f)`|Z).x = diff(exp,x) + diff(f,x) by A8,A7,A5,FDIFF_1:26
       .=exp.x+ diff(f,x) by SIN_COS:70
       .=exp.x+(f`|Z).x by A4,FDIFF_1:def 8,A7
       .=exp.x+0 by A4,FDIFF_1:31,A6,B1
       .=exp.x;
     end;
B1:  for x st x in Z holds (exp+f).x>0
     proof
       let x;
       assume A4:x in Z;
       then A11:(exp+f).x=exp.x + f.x by SEQ_1:def 3,A5
         .=exp.x +1 by A2,A4;
       exp.x>0 by SIN_COS:59;
       hence (exp+f).x>0 by A11,XREAL_1:36;
     end;
     then for x st x in Z holds (exp+f).x<>0;
     then A7:exp/(exp+f) is_differentiable_on Z by A8,A9,FDIFF_2:21;
A12: for x st x in Z holds ((exp/(exp+f))`|Z).x =exp.x/(exp.x +1)^2
     proof
       let x;
       assume A4:x in Z;
A13:   exp is_differentiable_in x by SIN_COS:70;
A14:   exp+f is_differentiable_in x by A9,A4,FDIFF_1:16;
B2:    (exp+f).x=exp.x + f.x by SEQ_1:def 3,A4,A5
       .=exp.x +1 by A2,A4;
       then (exp+f).x <>0 & (exp+f).x=exp.x +1 by B1,A4;
       then diff(exp/(exp+f),x)
       =(diff(exp,x) * (exp+f).x - diff((exp+f),x) * exp.x)/((exp+f).x)^2
         by FDIFF_2:14,A13,A14
       .=(exp.x* (exp+f).x-diff((exp+f),x) * exp.x)/((exp+f).x)^2 by SIN_COS:70
       .=(exp.x*(exp.x +1)-((exp+f)`|Z).x * exp.x)/(exp.x +1)^2
         by A4,B2,A9,FDIFF_1:def 8
       .=(exp.x*(exp.x +1)-exp.x * exp.x)/(exp.x +1)^2 by A10,A4
       .=exp.x/(exp.x +1)^2;
       hence ((exp/(exp+f))`|Z).x =exp.x/(exp.x +1)^2 by A7,A4,FDIFF_1:def 8;
     end;
A13: for x st x in Z holds (exp/(exp+f)).x >0 
     proof
       let x;
       assume A4:x in Z;
       then x in dom (exp/(exp+f)) by FUNCT_1:21,A1;
       then A5:(exp/(exp+f)).x=exp.x*((exp+f).x)" by RFUNCT_1:def 4
       .=exp.x*(1/(exp+f).x) by XCMPLX_1:217
       .=exp.x/(exp+f).x by XCMPLX_1:100;
A6:    (exp+f).x>0 by B1,A4;
       exp.x>0 by SIN_COS:59;
       hence (exp/(exp+f)).x >0 by A5,XREAL_1:141,A6;
     end;
B7:  for x st x in Z holds (log_ number_e)*(exp/(exp+f)) is_differentiable_in x
     proof
       let x;
       assume A4:x in Z;
       then A5:exp/(exp+f) is_differentiable_in x by FDIFF_1:16,A7;
       (exp/(exp+f)).x >0 by A13,A4;
       hence thesis by TAYLOR_1:20,A5;
     end;
     then A:(log_ number_e)*(exp/(exp+f)) is_differentiable_on Z
       by A1,FDIFF_1:16;
     for x st x in Z holds
       (((log_ number_e)*(exp/(exp+f)))`|Z).x = 1/(exp.x+1)
     proof
       let x;
       assume A4:x in Z;
       x in dom (exp/(exp+f)) by FUNCT_1:21,A1,A4;
       then B5:(exp/(exp+f)).x=(exp.x)*((exp+f).x)" by RFUNCT_1:def 4
       .=(exp.x)*(1/(exp+f).x) by XCMPLX_1:217
       .=(exp.x)/(exp+f).x by XCMPLX_1:100
       .=(exp.x)/(exp.x + f.x) by SEQ_1:def 3,A5,A4
       .=(exp.x)/(exp.x +1) by A2,A4;
A6:    (exp/(exp+f)) is_differentiable_in x by FDIFF_1:16,A7,A4;
A14:   (exp/(exp+f)).x >0 by A4,A13;
A11:   exp.x/(exp.x +1)>0 by B5,A4,A13;
       diff((log_ number_e)*(exp/(exp+f)),x)
       =diff((exp/(exp+f)),x)/((exp/(exp+f)).x) by TAYLOR_1:20,A6,A14
       .=((exp/(exp+f))`|Z).x/((exp/(exp+f)).x) by A7,A4,FDIFF_1:def 8 
       .=(exp.x/(exp.x +1)^2)/(exp.x/(exp.x +1)) by B5,A12,A4
       .=(exp.x/((exp.x +1)*(exp.x +1)))/(exp.x/(exp.x +1)) by SQUARE_1:def 3
       .=(exp.x/(exp.x +1)/(exp.x +1))/(exp.x/(exp.x +1)) by XCMPLX_1:79
       .=(exp.x/(exp.x +1))/(exp.x/(exp.x +1))/(exp.x +1) by XCMPLX_1:48
       .=1/(exp.x +1) by A11,XCMPLX_1:60;
       hence thesis by A4,A,FDIFF_1:def 8;
     end;
     hence thesis by A1,FDIFF_1:16,B7;
   end;

:: 57 f.x=ln((exp.x-1)/exp.x)
theorem 
  Z c= dom ((log_ number_e)*((exp-f)/exp)) &
  (for x st x in Z holds f.x=1 & (exp-f).x>0) implies
    (log_ number_e)*((exp-f)/exp) is_differentiable_on Z &
    for x st x in Z holds (((log_ number_e)*((exp-f)/exp))`|Z).x = 1/(exp.x-1)
  proof
    assume that
A1: Z c= dom ((log_ number_e)*((exp-f)/exp)) and
A2: for x st x in Z holds f.x=1 & (exp-f).x>0;
    for y st y in Z holds y in dom ((exp-f)/exp) by FUNCT_1:21,A1;
    then Z c= dom ((exp-f)/exp) by TARSKI:def 3;
    then Z c= dom (exp-f) /\ (dom exp \ (exp)"{0}) by RFUNCT_1:def 4;
    then A4:Z c= dom (exp-f) & Z c= dom exp \ (exp)"{0} by XBOOLE_1:18;
    then Z c= dom exp /\ dom f by SEQ_1:def 4;
    then A6:Z c= dom f by XBOOLE_1:18;
B2: for x st x in Z holds f.x=0*x+1 by A2;
    then A7:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A6;
A8: exp is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
    then A9:exp-f is_differentiable_on Z & for x st x in Z holds
    ((exp-f)`|Z).x = diff(exp,x) - diff(f,x) by A7,A4,FDIFF_1:27;
A10:for x st x in Z holds ((exp-f)`|Z).x =exp.x
    proof
      let x;
      assume B4:x in Z;
      hence ((exp-f)`|Z).x = diff(exp,x) - diff(f,x) by A8,A7,A4,FDIFF_1:27
          .= exp.x- diff(f,x) by SIN_COS:70
          .= exp.x-(f`|Z).x by B4,FDIFF_1:def 8,A7
          .= exp.x-0 by B4,FDIFF_1:31,A6,B2
          .= exp.x;
    end;
    for x st x in Z holds exp.x<>0 by SIN_COS:59;
    then A7:(exp-f)/exp is_differentiable_on Z by A8,A9,FDIFF_2:21;
A12:for x st x in Z holds (((exp-f)/exp)`|Z).x=1/exp.x
    proof
      let x;
      assume A6:x in Z;
A13:  exp is_differentiable_in x by SIN_COS:70;
A14:  exp-f is_differentiable_in x by A9,A6,FDIFF_1:16;
B3:   (exp-f).x=exp.x - f.x by SEQ_1:def 4,A6,A4
            .=exp.x -1 by A2,A6;
      then A11:exp.x <>0 & (exp-f).x=exp.x-1 by SIN_COS:59;
      then diff((exp-f)/exp,x)
         =(diff((exp-f),x) * exp.x - diff(exp,x) *(exp-f).x)/(exp.x)^2
           by FDIFF_2:14,A13,A14
        .=(((exp-f)`|Z).x* exp.x - diff(exp,x) *(exp-f).x)/(exp.x)^2
           by A9,A6,FDIFF_1:def 8
        .=(exp.x* exp.x - diff(exp,x) *(exp-f).x)/(exp.x)^2 by A10,A6
        .=(exp.x* exp.x - exp.x*(exp.x-1))/(exp.x)^2 by SIN_COS:70,B3
        .=(exp.x)/((exp.x)*(exp.x)) by SQUARE_1:def 3
        .=(exp.x)/(exp.x)/(exp.x) by XCMPLX_1:79
        .=1/exp.x by A11,XCMPLX_1:60;
      hence (((exp-f)/exp)`|Z).x=1/(exp.x) by A7,A6,FDIFF_1:def 8;
    end;
A13:for x st x in Z holds ((exp-f)/exp).x >0
    proof
      let x;
      assume A4:x in Z;
      then x in dom ((exp-f)/exp) by FUNCT_1:21,A1;
      then A5:((exp-f)/exp).x=(exp-f).x*(exp.x)" by RFUNCT_1:def 4
        .=(exp-f).x*(1/exp.x) by XCMPLX_1:217
        .=(exp-f).x/exp.x by XCMPLX_1:100;
A6:   (exp-f).x>0 by A2,A4;
      exp.x>0 by SIN_COS:59;
      hence ((exp-f)/exp).x >0 by A5,XREAL_1:141,A6;
    end;
B7: for x st x in Z holds (log_ number_e)*((exp-f)/exp)
       is_differentiable_in x
    proof
      let x;
      assume A4:x in Z;
      then A5:(exp-f)/exp is_differentiable_in x by FDIFF_1:16,A7;
      ((exp-f)/exp).x >0 by A13,A4;
      hence (log_ number_e)*((exp-f)/exp) is_differentiable_in x
        by TAYLOR_1:20,A5;
    end;
then A:(log_ number_e)*((exp-f)/exp) is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds (((log_ number_e)*((exp-f)/exp))`|Z).x = 1/(exp.x-1)
    proof
      let x;
      assume B4:x in Z;
      x in dom ((exp-f)/exp) by FUNCT_1:21,A1,B4;
      then B5:((exp-f)/exp).x=(exp-f).x*(exp.x)" by RFUNCT_1:def 4
        .=(exp-f).x*(1/exp.x) by XCMPLX_1:217
        .=(exp-f).x/exp.x by XCMPLX_1:100
        .=(exp.x-f.x)/exp.x by SEQ_1:def 4,A4,B4
        .=(exp.x-1)/exp.x by A2,B4;
A6:   ((exp-f)/exp) is_differentiable_in x by FDIFF_1:16,A7,B4;
A14:  ((exp-f)/exp).x >0 by B4,A13;
A16:  exp.x>0 by SIN_COS:59;
      diff((log_ number_e)*((exp-f)/exp),x)
        =diff(((exp-f)/exp),x)/(((exp-f)/exp).x) by TAYLOR_1:20,A6,A14
       .=(((exp-f)/exp)`|Z).x/(((exp-f)/exp).x) by A7,B4,FDIFF_1:def 8
       .=(1/exp.x)/((exp.x-1)/exp.x) by B5,A12,B4
       .=1/(exp.x-1) by A16,XCMPLX_1:55;
      hence thesis by B4,A,FDIFF_1:def 8;
    end;
    hence thesis by A1,FDIFF_1:16,B7;
  end;
