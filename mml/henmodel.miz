:: Equivalences of Inconsistency and {H}enkin Models
::  by Patrick Braselmann and Peter Koepke
::
:: Received September 25, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies CQC_LANG, QC_LANG1, FINSEQ_1, RELAT_1, QMAX_1, ZF_LANG, ZF_MODEL,
      BOOLE, FUNCT_1, VALUAT_1, FINSET_1, FUNCT_2, ARYTM_1, HENMODEL, ORDINAL2,
      ORDINAL1, TARSKI, CARD_1, INT_1, WELLORD1, WELLORD2, MARGREL1, ARYTM_3,
      CQC_THE1, MCART_1, CALCUL_1, CALCUL_2, SUBLEMMA, ARYTM;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, SETFAM_1, CALCUL_1, RELAT_1,
      FUNCT_1, ORDINAL1, XCMPLX_0, XXREAL_0, NAT_1, FINSEQ_1, QC_LANG1,
      CQC_LANG, CQC_THE1, VALUAT_1, FINSET_1, RELSET_1, FUNCT_2, FRAENKEL,
      CARD_1, MARGREL1, CQC_SIM1, DOMAIN_1, MCART_1, NUMBERS, FINSEQ_3,
      CALCUL_2, INT_1, WELLORD1, WELLORD2;
 constructors SETFAM_1, WELLORD1, WELLORD2, DOMAIN_1, XXREAL_0, NAT_1, INT_1,
      FINSEQ_3, CQC_SIM1, SUBSTUT2, CALCUL_1, CALCUL_2;
 registrations XBOOLE_0, SUBSET_1, FUNCT_1, ORDINAL1, RELSET_1, FINSET_1,
      XXREAL_0, XREAL_0, INT_1, FINSEQ_1, QC_LANG1, CQC_LANG;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, XBOOLE_0, FUNCT_1, FINSEQ_2;
 theorems TARSKI, FINSEQ_1, FINSEQ_3, FUNCT_1, VALUAT_1, XBOOLE_0, FINSEQ_2,
      ZFMISC_1, RELAT_1, QC_LANG3, XBOOLE_1, NAT_1, MARGREL1, FUNCT_2,
      RELSET_1, FINSET_1, CALCUL_1, CQC_THE1, ORDINAL2, AXIOMS, CALCUL_2,
      CARD_2, INT_1, ORDINAL1, WELLORD1, WELLORD2, CARD_3, QC_LANG1, CQC_LANG,
      MCART_1, SUBSTUT2, XREAL_1, XXREAL_0, FUNCOP_1;
 schemes FUNCT_1, FINSEQ_1, CLASSES1;

begin :: Preliminaries and Equivalences of Inconsistency

reserve a,a1,a2,b,b1,b2,c,d for set,
  X,Y,Z for Subset of CQC-WFF,
  i,j,k,m,n for Element of NAT,
  p,q for Element of CQC-WFF,
  P for QC-pred_symbol of k,
  ll for CQC-variable_list of k,
  f,f1,f2,g for FinSequence of CQC-WFF;

reserve A for non empty finite Subset of NAT;

theorem Th1:
  for f being Function of n,A st
  ((ex m st succ m = n) & f is one-to-one & rng f = A &
  for n,m st m in dom f & n in dom f & n < m holds f.n in f.m) holds
  f.(union n) = union rng f
proof
  let f be Function of n,A such that
A1: (ex m st succ m = n) & f is one-to-one & rng f = A &
  for n,m st m in dom f & n in dom f & n < m holds f.n in f.m;
  thus f.(union n) c= union rng f
  proof
    let a;
    assume
A2: a in f.(union n);
    consider m such that
A3: n = succ m by A1;
A4: a in f.m by A2,A3,ORDINAL2:2;
    dom f = n by FUNCT_2:def 1;
    then f.m in rng f by A3,FUNCT_1:12,ORDINAL1:10;
    hence a in union rng f by A4,TARSKI:def 4;
  end;
  thus union rng f c= f.(union n)
  proof
    let a;
    assume a in union rng f;
    then consider b such that
A5: a in b & b in rng f by TARSKI:def 4;
    consider c such that
A6: c in dom f & f.c = b by A5,FUNCT_1:def 5;
    reconsider i = c as Ordinal by A6;
    reconsider i as Element of NAT by A6,ORDINAL1:19;
    consider m such that
A7: n = succ m by A1;
    i c= m by A6,A7,ORDINAL1:34;
    then i c< m or i = m by XBOOLE_0:def 8;
    then
A8: i in m or i = m by ORDINAL1:21;
    then i in {k : k < m} or i = m by AXIOMS:30;
    then
A9: (ex k st k = i & k < m) or i = m;
A10: dom f = n by FUNCT_2:def 1;
    then
A11: m in dom f by A7,ORDINAL1:10;
    now
      assume
A12:  f.i in f.m;
      f.i in rng f by A8,A10,A11,FUNCT_1:12,ORDINAL1:19;
      then reconsider i1 = f.i as Element of NAT by A1;
      f.m in rng f by A7,A10,FUNCT_1:12,ORDINAL1:10;
      then reconsider i2 = f.m as Element of NAT by A1;
      i1 c= i2 by A12,ORDINAL1:def 2;
      then a in i2 by A5,A6;
      hence thesis by A7,ORDINAL2:2;
    end;
    hence thesis by A1,A5,A6,A7,A9,A11,ORDINAL2:2;
  end;
end;

theorem Th2:
  union A in A & for a st a in A holds (a in union A or a = union A)
proof
  RelIncl A is well-ordering by WELLORD2:9;
  then consider a being Ordinal such that
A1: RelIncl A,RelIncl a are_isomorphic by WELLORD2:14;
  RelIncl a,RelIncl A are_isomorphic by A1,WELLORD1:50;
  then consider F being Function such that
A2: F is_isomorphism_of RelIncl a,RelIncl A by WELLORD1:def 8;
A3: for m,n st m in dom F & n in dom F & n < m holds F.n in F.m
  proof
    let m,n such that
A4: m in dom F & n in dom F & n < m;
    m in field RelIncl a & n in field RelIncl a by A2,A4,WELLORD1:def 7;
    then
A5: m in a & n in a by WELLORD2:def 1;
    n in {i: i < m } by A4;
    then n in m by AXIOMS:30;
    then n <> m & n c= m by ORDINAL1:def 2;
    then [n,m] in RelIncl a by A5,WELLORD2:def 1;
    then
A6: [F.n,F.m] in RelIncl A by A2,WELLORD1:def 7;
    F.n in rng F & F.m in rng F by A4,FUNCT_1:12;
    then F.n in field RelIncl A & F.m in field RelIncl A by A2,WELLORD1:def 7;
    then
A7: F.n in A & F.m in A by WELLORD2:def 1;
    then
A8: F.n c= F.m by A6,WELLORD2:def 1;
    F is one-to-one by A2,WELLORD1:def 7;
    then F.n <> F.m by A4,FUNCT_1:def 8;
    then
A9: F.n c< F.m by A8,XBOOLE_0:def 8;
    reconsider b = F.n as Element of NAT by A7;
    reconsider c = F.m as Element of NAT by A7;
    b in c by A9,ORDINAL1:21;
    hence thesis;
  end;
A10: rng F = field RelIncl A by A2,WELLORD1:def 7;
  then
A11: rng F = A by WELLORD2:def 1;
  consider F1 being Function such that
A12: F1 is_isomorphism_of RelIncl A,RelIncl a by A1,WELLORD1:def 8;
  dom F1 = field RelIncl A by A12,WELLORD1:def 7;
  then
A13: dom F1 = A by WELLORD2:def 1;
  then rng F1 is finite by FINSET_1:26;
  then field RelIncl a is finite by A12,WELLORD1:def 7;
  then
A14: a is finite by WELLORD2:def 1;
  consider b such that
A15: b in dom F1 by A13,XBOOLE_0:def 1;
  F1.b in rng F1 by A15,FUNCT_1:12;
  then field RelIncl a is non empty by A12,WELLORD1:def 7;
  then a is non empty by WELLORD2:def 1;
  then
A16: {} in a by ORDINAL1:26,XBOOLE_1:3;
A17: now
    assume a is being_limit_ordinal;
    then
A18: omega c= a by A16,ORDINAL1:def 12;
    now
      assume omega is finite;
      then Card omega in Card omega by CARD_3:58;
      hence contradiction;
    end;
    hence contradiction by A14,A18;
  end;
  now
    assume
A19: not a in omega;
    now
      assume omega is finite;
      then Card omega in Card omega by CARD_3:58;
      hence contradiction;
    end;
    hence contradiction by A14,A19,FINSET_1:13,ORDINAL1:26;
  end;
  then reconsider a as Element of NAT;
A20: now
    let b be Ordinal;
    assume succ b = a;
    then b in a by ORDINAL1:10;
    hence b is Element of NAT by ORDINAL1:19;
  end;
A21: ex m st succ m = a
  proof
    consider b being Ordinal such that
A22: succ b = a by A17,ORDINAL1:42;
    reconsider b as Element of NAT by A20,A22;
    take b;
    thus thesis by A22;
  end;
  dom F = field RelIncl a by A2,WELLORD1:def 7;
  then
A23: dom F = a by WELLORD2:def 1;
  then reconsider F as Function of a,A by A11,FUNCT_2:3;
  consider m such that
A24: succ m = a by A21;
  (ex m st succ m = a) & F is one-to-one & rng F = A &
  for n,m st m in dom F & n in dom F & n < m holds F.n in F.m
  by A2,A3,A10,A21,WELLORD1:def 7,WELLORD2:def 1;
  then
A25: F.(union a) = union rng F by Th1;
A26: union a = m by A24,ORDINAL2:2;
  hence union A in A by A11,A23,A24,A25,FUNCT_1:12,ORDINAL1:10;
  thus for b st b in A holds (b in union A or b = union A)
  proof
    let b such that
A27: b in A;
    now
      assume
A28:  b <> union A;
      consider c such that
A29:  c in dom F & F.c = b by A11,A27,FUNCT_1:def 5;
      reconsider c as Element of NAT by A29,ORDINAL1:19;
A30:  c in m or c = m by A24,A29,ORDINAL1:13;
A31:  m in dom F by A23,A24,ORDINAL1:10;
      c in {k : k < m} by A11,A24,A25,A28,A29,A30,AXIOMS:30,ORDINAL2:2;
      then ex k st k = c & k < m;
      hence thesis by A3,A11,A25,A26,A29,A31;
    end;
    hence thesis;
  end;
end;

reserve C for non empty set;

theorem Th3:
  for f being Function of NAT,C, X being finite set st
  (for n,m st m in dom f & n in dom f & n < m holds f.n c= f.m) &
  X c= union rng f holds ex k st X c= f.k
proof
  let f be Function of NAT,C, X be finite set such that
A1: (for n,m st m in dom f & n in dom f & n < m
  holds f.n c= f.m) & X c= union rng f;
A2: now
    assume
A3: X is empty;
    take k = 0;
    {} c= f.k by XBOOLE_1:2;
    hence thesis by A3;
  end;
  now
    assume
A4: not X is empty;
    deffunc F(set) = min* {i : $1 in f.i};
    consider g being Function such that
A5: dom g = X & for a st a in X holds g.a = F(a) from FUNCT_1:sch 3;
    reconsider A = rng g as finite set by A5,FINSET_1:26;
A6: now
      let b;
      assume b in A;
      then consider a such that
A7:   a in dom g & g.a = b by FUNCT_1:def 5;
      b = min* {i : a in f.i} by A5,A7;
      hence b in NAT;
    end;
    consider c such that
A8: c in dom g by A4,A5,XBOOLE_0:def 1;
    reconsider A as non empty finite Subset of NAT by A6,A8,FUNCT_1:12
    ,TARSKI:def 3;
    union A in A by Th2;
    then reconsider a = union A as Element of NAT;
    take a;
    thus X c= f.a
    proof
      let b;
      assume
A9:   b in X;
      then consider c such that
A10:  b in c & c in rng f by A1,TARSKI:def 4;
      consider d such that
A11:  d in dom f & f.d = c by A10,FUNCT_1:def 5;
      reconsider d as Element of NAT by A11;
      d in {i : b in f.i} by A10,A11;
      then reconsider Y = {i : b in f.i} as non empty set;
      now
        let a;
        assume a in Y;
        then consider i such that
A12:    i = a & b in f.i;
        thus a in NAT by A12;
      end;
      then reconsider Y as non empty Subset of NAT by TARSKI:def 3;
A13:  g.b = min* Y by A5,A9;
      then reconsider Y' = g.b as Element of NAT;
      Y' in Y & for k st k in Y holds Y' <= k by A13,NAT_1:def 1;
      then consider i such that
A14:  i = g.b & b in f.i;
A15:  Y' in A by A5,A9,FUNCT_1:12;
A16:  dom f = NAT by FUNCT_2:def 1;
      now
        assume Y' in a;
        then Y' in {k : k < a} by AXIOMS:30;
        then ex k st k = Y' & k < a;
        then f.Y' c= f.a by A1,A16;
        hence thesis by A14;
      end;
      hence thesis by A14,A15,Th2;
    end;
  end;
  hence thesis by A2;
end;

definition
  let X,p;
  canceled;
  pred X |- p means
  :Def2:
  ex f st rng f c= X & |- f^<*p*>;
end;

definition
  let X;
  attr X is Consistent means
  :Def3:
  for p holds not (X |- p & X |- 'not' p);
end;

notation
  let X;
  antonym X is Inconsistent for X is Consistent;
end;

definition
  let f be FinSequence of CQC-WFF;
  attr f is Consistent means
  :Def4:
  for p holds not (|- f^<*p*> & |- f^<*'not' p*>);
end;

notation
  let f be FinSequence of CQC-WFF;
  antonym f is Inconsistent for f is Consistent;
end;

theorem Th4:
  X is Consistent & rng g c= X implies g is Consistent
proof
  assume
A1: X is Consistent & rng g c= X;
  now
    assume g is Inconsistent;
    then consider p such that
A2: |- g^<*p*> & |- g^<*'not' p*> by Def4;
    X |- p & X |- 'not' p by A1,A2,Def2;
    hence contradiction by A1,Def3;
  end;
  hence thesis;
end;

theorem Th5:
  |- f^<*p*> implies |- f^g^<*p*>
proof
  assume
A1: |- f^<*p*>;
A2: Ant(f^<*p*>) = f & Ant(f^g^<*p*>) = f^g &
  Suc(f^g^<*p*>) = p by CALCUL_1:5;
  then Suc(f^<*p*>) = Suc(f^g^<*p*>) by CALCUL_1:5;
  hence thesis by A1,A2,CALCUL_1:8,36;
end;

:: Ebb et al, Chapter IV, Lemma 7.2

theorem Th6:
  X is Inconsistent iff for p holds X |- p
proof
  thus X is Inconsistent implies for p holds X |- p
  proof
    assume X is Inconsistent;
    then consider q such that
A1: X |- q & X |- 'not' q by Def3;
    let p;
    consider f1 such that
A2: rng f1 c= X & |- f1^<*q*> by A1,Def2;
    consider f2 such that
A3: rng f2 c= X & |- f2^<*'not' q*> by A1,Def2;
A4: |- f1^f2^<*q*> & |- f1^f2^<*'not' q*> by A2,A3,Th5,CALCUL_2:20;
    take f3 = f1^f2;
    rng f3 = rng f1 \/ rng f2 by FINSEQ_1:44;
    hence thesis by A2,A3,A4,CALCUL_2:25,XBOOLE_1:8;
  end;
  assume for p holds X |- p;
  then X |- VERUM & X |- 'not' VERUM;
  hence thesis by Def3;
end;

:: One direction of Ebb et al, Chapter IV, Lemma 7.4

theorem Th7:
  X is Inconsistent implies ex Y st Y c= X & Y is finite & Y is Inconsistent
proof
  assume X is Inconsistent;
  then consider p such that
A1: X |- p & X |- 'not' p by Def3;
  consider f1 such that
A2: rng f1 c= X & |- f1^<*p*> by A1,Def2;
  consider f2 such that
A3: rng f2 c= X & |- f2^<*'not' p*> by A1,Def2;
A4: |- f1^f2^<*p*> & |- f1^f2^<*'not' p*> by A2,A3,Th5,CALCUL_2:20;
  reconsider Y = rng(f1^f2) as Subset of CQC-WFF;
  take Y;
  Y = rng f1 \/ rng f2 by FINSEQ_1:44;
  hence Y c= X by A2,A3,XBOOLE_1:8;
  Y |- p & Y |- 'not' p by A4,Def2;
  hence thesis by Def3;
end;

Lm1: for f,g be FinSequence holds Seg len(f^g) = Seg len f \/ seq(len f,len g)
proof
  let f,g be FinSequence;
  thus Seg len(f^g) = dom(f^g) by FINSEQ_1:def 3
    .= dom f \/ seq(len f,len g) by CALCUL_2:9
    .= Seg len f \/ seq(len f,len g) by FINSEQ_1:def 3;
end;

theorem
  X \/ {p} |- q implies ex g st rng g c= X & |- g^<*p*>^<*q*>
proof
  assume X \/ {p} |- q;
  then consider f such that
A1: rng f c= X \/ {p} & |- f^<*q*> by Def2;
A2: now
    assume
A3: rng f c= X;
    take f;
    |- f^<*p*>^<*q*> by A1,Th5;
    hence thesis by A3;
  end;
  now
    assume not rng f c= X;
    then consider a such that
A4: a in rng f & not a in X by TARSKI:def 3;
    reconsider B = dom f as finite set;
    reconsider A = f"{p} as finite set;
    set n = card A;
    set g = f - {p};
    set h = g^IdFinS(p,n);
A5: len IdFinS(p,n) = n by FINSEQ_2:69;
    defpred P[Nat,set] means ($1 in Seg len g implies $2 = Sgm(B \ A).$1) &
    ($1 in seq(len g,n) implies $2 = Sgm(A).($1 - len g));
A8: for k be Nat st k in Seg len h ex a st P[k,a]
    proof
      let k be Nat such that k in Seg len h;
      now
        assume
A9:     k in Seg len g;
A10:    Seg len g misses seq(len g,n) by CALCUL_2:8;
        take a = Sgm(B\A).k;
        thus P[k,a] by A9,A10,XBOOLE_0:3;
      end;
      hence thesis;
    end;
    consider F being FinSequence such that
A11: dom F = Seg len h & for k be Nat st k in Seg len h holds P[k,F.k]
    from FINSEQ_1:sch 1(A8);
A12: len h = len g + len IdFinS(p,n) by FINSEQ_1:35
      .= len f - n + len IdFinS(p,n) by FINSEQ_3:66
      .= len f - n + n by FINSEQ_2:69
      .= len f;
    then
A13: dom F = B by A11,FINSEQ_1:def 3;
    reconsider C = B \ A as finite set;
    now
      let b such that
A14:  b in C;
      b in dom f by A14;
      hence b in Seg len f by FINSEQ_1:def 3;
    end;
    then
A15: C c= Seg len f by TARSKI:def 3;
    then
A16: dom Sgm(C) = Seg card C by FINSEQ_3:45;
A17: card C = card B - n by CARD_2:63,RELAT_1:167
      .= card Seg len f - n by FINSEQ_1:def 3
      .= len f - n by FINSEQ_1:78
      .= len g by FINSEQ_3:66;
    A c= B by RELAT_1:167;
    then
A18: A c= Seg len f by FINSEQ_1:def 3;
A19: now
      let i;
      assume
A20:  i in seq(len g,n);
      reconsider j = i-len g as Integer;
A21:  1+len g <= i & i <= n+len g by A20,CALCUL_2:1;
      then
A22:  1 <= j & j <= n by XREAL_1:21,22;
      0 <= j by A21,XREAL_1:21;
      then reconsider j as Element of NAT by INT_1:16;
      j in Seg n by A22,FINSEQ_1:3;
      hence i-len g in dom Sgm(A) by A18,FINSEQ_3:45;
    end;
A23: rng F = B
    proof
      now
        let a such that
A24:    a in rng F;
        consider i being Nat such that
A25:    i in dom F & F.i = a by A24,FINSEQ_2:11;
A26:    i in Seg len g \/ seq(len g,n) by A5,A11,A25,Lm1;
A27:    now
          assume
A28:      i in Seg len g;
          then
A29:      a = Sgm(C).i by A11,A25;
          i in dom Sgm(C) by A15,A17,A28,FINSEQ_3:45;
          then a in rng Sgm(C) by A29,FUNCT_1:12;
          then a in C by A15,FINSEQ_1:def 13;
          hence a in B;
        end;
        now
          assume i in seq(len g,n);
          then
A30:      i-len g in dom Sgm(A) & a = Sgm(A).(i-len g) by A11,A19,A25;
          rng Sgm(A) = A by A18,FINSEQ_1:def 13;
          then
A31:      a in A by A30,FUNCT_1:12;
          A c= B by RELAT_1:167;
          hence a in B by A31;
        end;
        hence a in B by A26,A27,XBOOLE_0:def 2;
      end;
      hence rng F c= B by TARSKI:def 3;
      let a such that
A32:  a in B;
A33:  now
        assume
A34:    a in A;
        rng Sgm(A) = A by A18,FINSEQ_1:def 13;
        then consider i being Nat such that
A35:    i in dom Sgm(A) & Sgm(A).i = a by A34,FINSEQ_2:11;
        reconsider i as Element of NAT by ORDINAL1:def 13;
        set m = i + len g;
        len Sgm(A) = n by A18,FINSEQ_3:44;
        then
A36:    1 <= i & i <= n by A35,FINSEQ_3:27;
        then
A37:    1+len g <= m & m <= n+len g by XREAL_1:8;
        then
A38:    m in seq(len g,n) by CALCUL_2:1;
        i <= m by NAT_1:11;
        then
A39:    1 <= m & m <= len g + len IdFinS(p,n)
        by A36,A37,FINSEQ_2:69,XXREAL_0:2;
        then m <= len h by FINSEQ_1:35;
        then m in dom h by A39,FINSEQ_3:27;
        then
A40:    m in Seg len h by FINSEQ_1:def 3;
        a = Sgm(A).(m - len g) by A35;
        then a = F.m by A11,A38,A40;
        hence thesis by A11,A40,FUNCT_1:12;
      end;
      now
        assume not a in A;
        then
A41:    a in C by A32,XBOOLE_0:def 4;
        now
          let b such that
A42:      b in C;
          b in dom f by A42;
          hence b in Seg len f by FINSEQ_1:def 3;
        end;
        then C c= Seg len f by TARSKI:def 3;
        then a in rng Sgm(C) by A41,FINSEQ_1:def 13;
        then consider i being Nat such that
A43:    i in dom Sgm(C) & Sgm(C).i = a by FINSEQ_2:11;
        len Sgm(C) = len g by A16,A17,FINSEQ_1:def 3;
        then
A44:    1 <= i & i <= len g by A43,FINSEQ_3:27;
        then
A45:    i in Seg len g by FINSEQ_1:3;
        len g <= len h by CALCUL_1:6;
        then i <= len h by A44,XXREAL_0:2;
        then
A46:    i in Seg len h by A44,FINSEQ_1:3;
        then a = F.i by A11,A43,A45;
        hence thesis by A11,A46,FUNCT_1:12;
      end;
      hence thesis by A33;
    end;
    then reconsider F as Relation of B,B by A13,RELSET_1:11;
    B = {} implies B = {};
    then reconsider F as Function of B,B by A13,FUNCT_2:def 1;
    F is one-to-one
    proof
      let a1,a2 such that
A47:  a1 in dom F & a2 in dom F & F.a1 = F.a2;
A48:  a1 in Seg len g \/ seq(len g,n) &
      a2 in Seg len g \/ seq(len g,n) by A5,A11,A47,Lm1;
A49:  now
        assume
A50:    a1 in Seg len g;
A52:    F.a1 = Sgm(C).a1 by A11,A47,A50;
A53:    a1 in dom Sgm(C) by A15,A17,A50,FINSEQ_3:45;
        then F.a1 in rng Sgm(C) by A52,FUNCT_1:12;
        then
A54:    F.a1 in C by A15,FINSEQ_1:def 13;
A55:    now
          assume
A56:      a2 in Seg len g;
A57:      F.a2 = Sgm(C).a2 by A11,A47,A56;
A58:      a2 in dom Sgm(C) by A15,A17,A56,FINSEQ_3:45;
A59:      Sgm(C).a1 = Sgm(C).a2 by A11,A47,A50,A57;
          Sgm(C) is one-to-one by A15,FINSEQ_3:99;
          hence a1 = a2 by A53,A58,A59,FUNCT_1:def 8;
        end;
        now
          assume
A60:      a2 in seq(len g,n);
          then reconsider i = a2 as Element of NAT;
          Sgm(A).(i-len g) in rng Sgm(A) by A19,A60,FUNCT_1:12;
          then F.a2 in rng Sgm(A) by A11,A47,A60;
          then F.a2 in A by A18,FINSEQ_1:def 13;
          hence contradiction by A47,A54,XBOOLE_0:def 4;
        end;
        hence a1 = a2 by A48,A55,XBOOLE_0:def 2;
      end;
      now
        assume
A61:    a1 in seq(len g,n);
        then reconsider i = a1 as Element of NAT;
A62:    i-len g in dom Sgm(A) by A19,A61;
        Sgm(A).(i-len g) in rng Sgm(A) by A19,A61,FUNCT_1:12;
        then F.a1 in rng Sgm(A) by A11,A47,A61;
        then
A63:    F.a1 in A by A18,FINSEQ_1:def 13;
A64:    now
          assume
A65:      a2 in seq(len g,n);
          then reconsider i1 = a2 as Element of NAT;
A66:      F.a2 = Sgm(A).(i1-len g) by A11,A47,A65;
A67:      i1-len g in dom Sgm(A) by A19,A65;
A68:      Sgm(A).(i1-len g) = Sgm(A).(i-len g) by A11,A47,A61,A66;
          Sgm(A) is one-to-one by A18,FINSEQ_3:99;
          then i1-len g+len g = i-len g+len g by A62,A67,A68,FUNCT_1:def 8;
          hence a1 = a2;
        end;
        now
          assume
A69:      a2 in Seg len g;
A70:      F.a2 = Sgm(C).a2 by A11,A47,A69;
          a2 in dom Sgm(C) by A15,A17,A69,FINSEQ_3:45;
          then F.a2 in rng Sgm(C) by A70,FUNCT_1:12;
          then F.a2 in C by A15,FINSEQ_1:def 13;
          hence contradiction by A47,A63,XBOOLE_0:def 4;
        end;
        hence a1 = a2 by A48,A64,XBOOLE_0:def 2;
      end;
      hence thesis by A48,A49,XBOOLE_0:def 2;
    end;
    then reconsider F as Permutation of dom f by A23,FUNCT_2:83;
A71: Per(f,F) = h
    proof
A72:  dom Per(f,F) = B by CALCUL_2:19
        .= dom F by A11,A12,FINSEQ_1:def 3
        .= dom h by A11,FINSEQ_1:def 3;
      now
        let k be Nat such that
A73:    k in dom h;
A74:    k in Seg len h by A73,FINSEQ_1:def 3;
A75:    now
          assume
A76:      k in dom g;
          then
A77:      k in dom Sgm(C) by A16,A17,FINSEQ_1:def 3;
          k in Seg len g by A76,FINSEQ_1:def 3;
          then
A78:      F.k = Sgm(C).k by A11,A74;
          k in dom (F*f) by A72,A73,CALCUL_2:def 2;
          then (F*f).k = f.(Sgm(C).k) by A78,FUNCT_1:22;
          hence Per(f,F).k = f.(Sgm(C).k) by CALCUL_2:def 2
            .= (Sgm(C)*f).k by A77,FUNCT_1:23
            .= g.k by FINSEQ_3:def 1
            .= h.k by A76,FINSEQ_1:def 7;
        end;
        now
          given i be Nat such that
A79:      i in dom IdFinS(p,n) & k = len g + i;
          len IdFinS(p,n) = n by FINSEQ_2:69;
          then
A80:      1 <= i & i <= n by A79,FINSEQ_3:27;
          then 1+len g <= k & k <= n+len g by A79,XREAL_1:8;
          then
A81:      k in seq(len g,n) by CALCUL_2:1;
          then F.k = Sgm(A).(k-len g) by A11,A74;
          then F.k in rng Sgm(A) by A19,A81,FUNCT_1:12;
          then F.k in A by A18,FINSEQ_1:def 13;
          then f.(F.k) in {p} by FUNCT_1:def 13;
          then
A82:      f.(F.k) = p by TARSKI:def 1;
          k in dom (F*f) by A72,A73,CALCUL_2:def 2;
          then (F*f).k = p by A82,FUNCT_1:22;
          then
A83:      Per(f,F).k = p by CALCUL_2:def 2;
          i in Seg n by A80,FINSEQ_1:3;
          hence Per(f,F).k = IdFinS(p,n).i by A83,FUNCOP_1:13
            .= h.k by A79,FINSEQ_1:def 7;
        end;
        hence Per(f,F).k = h.k by A73,A75,FINSEQ_1:38;
      end;
      hence thesis by A72,FINSEQ_1:17;
    end;
    then reconsider h as FinSequence of CQC-WFF;
A84: |- h^<*q*> by A1,A71,CALCUL_2:30;
    a in X or a in {p} by A1,A4,XBOOLE_0:def 2;
    then a = p by A4,TARSKI:def 1;
    then consider i being Nat such that
A85: i in B & f.i = p by A4,FINSEQ_2:11;
    consider j being Nat such that
A86: j in dom F & F.j = i by A23,A85,FINSEQ_2:11;
    (F*f).j = p by A85,A86,FUNCT_1:23;
    then
A87: h.j = p by A71,CALCUL_2:def 2;
A88: now
      assume
A89:  j in dom g;
      then g.j = p by A87,FINSEQ_1:def 7;
      then
A90:  g.j in {p} & g.j in rng g by A89,FUNCT_1:12,TARSKI:def 1;
      rng g = rng f \ {p} by FINSEQ_3:72;
      hence contradiction by A90,XBOOLE_0:def 4;
    end;
    j in dom f by A86;
    then j in dom h by A71,CALCUL_2:19;
    then consider k be Nat such that
A91: k in dom IdFinS(p,n) & j = len g + k by A88,FINSEQ_1:38;
    reconsider g as FinSequence of CQC-WFF by FINSEQ_3:93;
    take g;
    1 <= k & k <= len IdFinS(p,n) by A91,FINSEQ_3:27;
    then 1 <= len IdFinS(p,n) by XXREAL_0:2;
    then 1 <= n by FINSEQ_2:69;
    then
A92: |- g^<*p*>^<*q*> by A84,CALCUL_2:32;
A93: rng g = rng f \ {p} by FINSEQ_3:72;
    rng f \ {p} c= (X \/ {p}) \ {p} by A1,XBOOLE_1:33;
    then
A94: rng g c= X \ {p} by A93,XBOOLE_1:40;
    X \ {p} c= X by XBOOLE_1:36;
    hence thesis by A92,A94,XBOOLE_1:1;
  end;
  hence thesis by A2;
end;

:: Corresponds to Ebb et al, Chapter IV, Lemma 7.6 (a)

theorem
  X |- p iff X \/ {'not' p} is Inconsistent
proof
  thus X |- p implies X \/ {'not' p} is Inconsistent
  proof
    assume X |- p;
    then consider f such that
A1: rng f c= X & |- f^<*p*> by Def2;
A2: |- f^<*'not' p*>^<*p*> by A1,Th5;
    set f1 = f^<*'not' p*>^<*'not' p*>;
A3: Ant(f1) = f^<*'not' p*> & Suc(f1) = 'not' p by CALCUL_1:5;
    then
A4: (Ant(f1)).(len f+1) = Suc(f1) by FINSEQ_1:59;
    1 in Seg 1 by FINSEQ_1:3;
    then 1 in dom <*'not' p*> by FINSEQ_1:55;
    then len f+1 in dom (Ant(f1)) by A3,FINSEQ_1:41;
    then Suc(f1) is_tail_of Ant(f1) by A4,CALCUL_1:def 3;
    then |- f1 by CALCUL_1:33;
    then
A5: not f^<*'not' p*> is Consistent by A2,Def4;
    set f2 = f^<*'not' p*>;
A6: Ant(f2) = f & Suc(f2) = 'not' p by CALCUL_1:5;
    0+1 <= len f2 by CALCUL_1:10;
    then rng f2 = rng f \/ {'not' p} by A6,CALCUL_1:3;
    hence thesis by A1,A5,Th4,XBOOLE_1:9;
  end;
  thus X \/ {'not' p} is Inconsistent implies X |- p
  proof
    assume X \/ {'not' p} is Inconsistent;
    then X \/ {'not' p} |- p by Th6;
    then consider f such that
A7: rng f c= X \/ {'not' p} & |- f^<*p*> by Def2;
    now
      assume not rng f c= X;
      then consider a such that
A8:   a in rng f & not a in X by TARSKI:def 3;
      reconsider B = dom f as finite set;
      reconsider A = f"{'not' p} as finite set;
      set n = card A;
      set g = f - {'not' p};
      set h = g^IdFinS('not' p,n);
A9:   len IdFinS('not' p,n) = n by FINSEQ_2:69;
      defpred P[Nat,set] means ($1 in Seg len g implies $2 = Sgm(B \ A).$1) &
      ($1 in seq(len g,n) implies $2 = Sgm(A).($1 - len g));
A12:  for k be Nat st k in Seg len h ex a st P[k,a]
      proof
        let k be Nat such that k in Seg len h;
        now
          assume
A13:      k in Seg len g;
A14:      Seg len g misses seq(len g,n) by CALCUL_2:8;
          take a = Sgm(B\A).k;
          thus P[k,a] by A13,A14,XBOOLE_0:3;
        end;
        hence thesis;
      end;
      consider F being FinSequence such that
A15:  dom F = Seg len h & for k be Nat st k in Seg len h holds P[k,F.k]
      from FINSEQ_1:sch 1(A12);
A16:  len h = len g + len IdFinS('not' p,n) by FINSEQ_1:35
        .= len f - n + len IdFinS('not' p,n) by FINSEQ_3:66
        .= len f - n + n by FINSEQ_2:69
        .= len f;
      then
A17:  dom F = B by A15,FINSEQ_1:def 3;
      reconsider C = B \ A as finite set;
      now
        let b such that
A18:    b in C;
        b in dom f by A18;
        hence b in Seg len f by FINSEQ_1:def 3;
      end;
      then
A19:  C c= Seg len f by TARSKI:def 3;
      then
A20:  dom Sgm(C) = Seg card C by FINSEQ_3:45;
A21:  card C = card B - n by CARD_2:63,RELAT_1:167
        .= card Seg len f - n by FINSEQ_1:def 3
        .= len f - n by FINSEQ_1:78
        .= len g by FINSEQ_3:66;
      A c= B by RELAT_1:167;
      then
A22:  A c= Seg len f by FINSEQ_1:def 3;
A23:  now
        let i;
        assume
A24:    i in seq(len g,n);
        reconsider j = i-len g as Integer;
A25:    1+len g <= i & i <= n+len g by A24,CALCUL_2:1;
        then
A26:    1 <= j & j <= n by XREAL_1:21,22;
        0 <= j by A25,XREAL_1:21;
        then reconsider j as Element of NAT by INT_1:16;
        j in Seg n by A26,FINSEQ_1:3;
        hence i-len g in dom Sgm(A) by A22,FINSEQ_3:45;
      end;
A27:  rng F = B
      proof
        now
          let a such that
A28:      a in rng F;
          consider i being Nat such that
A29:      i in dom F & F.i = a by A28,FINSEQ_2:11;
A30:      i in Seg len g \/ seq(len g,n) by A9,A15,A29,Lm1;
A31:      now
            assume
A32:        i in Seg len g;
            then
A33:        a = Sgm(C).i by A15,A29;
            i in dom Sgm(C) by A19,A21,A32,FINSEQ_3:45;
            then a in rng Sgm(C) by A33,FUNCT_1:12;
            then a in C by A19,FINSEQ_1:def 13;
            hence a in B;
          end;
          now
            assume i in seq(len g,n);
            then
A34:        i-len g in dom Sgm(A) & a = Sgm(A).(i-len g) by A15,A23,A29;
            rng Sgm(A) = A by A22,FINSEQ_1:def 13;
            then
A35:        a in A by A34,FUNCT_1:12;
            A c= B by RELAT_1:167;
            hence a in B by A35;
          end;
          hence a in B by A30,A31,XBOOLE_0:def 2;
        end;
        hence rng F c= B by TARSKI:def 3;
        let a such that
A36:    a in B;
A37:    now
          assume
A38:      a in A;
          rng Sgm(A) = A by A22,FINSEQ_1:def 13;
          then consider i being Nat such that
A39:      i in dom Sgm(A) & Sgm(A).i = a by A38,FINSEQ_2:11;
          reconsider i as Element of NAT by ORDINAL1:def 13;
          set m = i + len g;
          len Sgm(A) = n by A22,FINSEQ_3:44;
          then
A40:      1 <= i & i <= n by A39,FINSEQ_3:27;
          then
A41:      1+len g <= m & m <= n+len g by XREAL_1:8;
          then
A42:      m in seq(len g,n) by CALCUL_2:1;
          i <= m by NAT_1:11;
          then
A43:      1 <= m & m <= len g + len IdFinS('not' p,n)
          by A40,A41,FINSEQ_2:69,XXREAL_0:2;
          then m <= len h by FINSEQ_1:35;
          then m in dom h by A43,FINSEQ_3:27;
          then
A44:      m in Seg len h by FINSEQ_1:def 3;
          a = Sgm(A).(m - len g) by A39;
          then a = F.m by A15,A42,A44;
          hence thesis by A15,A44,FUNCT_1:12;
        end;
        now
          assume not a in A;
          then
A45:      a in C by A36,XBOOLE_0:def 4;
          now
            let b such that
A46:        b in C;
            b in dom f by A46;
            hence b in Seg len f by FINSEQ_1:def 3;
          end;
          then C c= Seg len f by TARSKI:def 3;
          then a in rng Sgm(C) by A45,FINSEQ_1:def 13;
          then consider i being Nat such that
A47:      i in dom Sgm(C) & Sgm(C).i = a by FINSEQ_2:11;
          len Sgm(C) = len g by A20,A21,FINSEQ_1:def 3;
          then
A48:      1 <= i & i <= len g by A47,FINSEQ_3:27;
          then
A49:      i in Seg len g by FINSEQ_1:3;
          len g <= len h by CALCUL_1:6;
          then i <= len h by A48,XXREAL_0:2;
          then
A50:      i in Seg len h by A48,FINSEQ_1:3;
          then a = F.i by A15,A47,A49;
          hence thesis by A15,A50,FUNCT_1:12;
        end;
        hence thesis by A37;
      end;
      then reconsider F as Relation of B,B by A17,RELSET_1:11;
      B = {} implies B = {};
      then reconsider F as Function of B,B by A17,FUNCT_2:def 1;
      F is one-to-one
      proof
        let a1,a2 such that
A51:    a1 in dom F & a2 in dom F & F.a1 = F.a2;
A52:    a1 in Seg len g \/ seq(len g,n) &
        a2 in Seg len g \/ seq(len g,n) by A9,A15,A51,Lm1;
A53:    now
          assume
A54:      a1 in Seg len g;
A56:      F.a1 = Sgm(C).a1 by A15,A51,A54;
A57:      a1 in dom Sgm(C) by A19,A21,A54,FINSEQ_3:45;
          then F.a1 in rng Sgm(C) by A56,FUNCT_1:12;
          then
A58:      F.a1 in C by A19,FINSEQ_1:def 13;
A59:      now
            assume
A60:        a2 in Seg len g;
A61:        F.a2 = Sgm(C).a2 by A15,A51,A60;
A62:        a2 in dom Sgm(C) by A19,A21,A60,FINSEQ_3:45;
A63:        Sgm(C).a1 = Sgm(C).a2 by A15,A51,A54,A61;
            Sgm(C) is one-to-one by A19,FINSEQ_3:99;
            hence a1 = a2 by A57,A62,A63,FUNCT_1:def 8;
          end;
          now
            assume
A64:        a2 in seq(len g,n);
            then reconsider i = a2 as Element of NAT;
            Sgm(A).(i-len g) in rng Sgm(A) by A23,A64,FUNCT_1:12;
            then F.a2 in rng Sgm(A) by A15,A51,A64;
            then F.a2 in A by A22,FINSEQ_1:def 13;
            hence contradiction by A51,A58,XBOOLE_0:def 4;
          end;
          hence a1 = a2 by A52,A59,XBOOLE_0:def 2;
        end;
        now
          assume
A65:      a1 in seq(len g,n);
          then reconsider i = a1 as Element of NAT;
A66:      i-len g in dom Sgm(A) by A23,A65;
          Sgm(A).(i-len g) in rng Sgm(A) by A23,A65,FUNCT_1:12;
          then F.a1 in rng Sgm(A) by A15,A51,A65;
          then
A67:      F.a1 in A by A22,FINSEQ_1:def 13;
A68:      now
            assume
A69:        a2 in seq(len g,n);
            then reconsider i1 = a2 as Element of NAT;
A70:        F.a2 = Sgm(A).(i1-len g) by A15,A51,A69;
A71:        i1-len g in dom Sgm(A) by A23,A69;
A72:        Sgm(A).(i1-len g) = Sgm(A).(i-len g) by A15,A51,A65,A70;
            Sgm(A) is one-to-one by A22,FINSEQ_3:99;
            then i1-len g = i-len g by A66,A71,A72,FUNCT_1:def 8;
            hence a1 = a2;
          end;
          now
            assume
A73:        a2 in Seg len g;
A74:        F.a2 = Sgm(C).a2 by A15,A51,A73;
            a2 in dom Sgm(C) by A19,A21,A73,FINSEQ_3:45;
            then F.a2 in rng Sgm(C) by A74,FUNCT_1:12;
            then F.a2 in C by A19,FINSEQ_1:def 13;
            hence contradiction by A51,A67,XBOOLE_0:def 4;
          end;
          hence a1 = a2 by A52,A68,XBOOLE_0:def 2;
        end;
        hence thesis by A52,A53,XBOOLE_0:def 2;
      end;
      then reconsider F as Permutation of dom f by A27,FUNCT_2:83;
A75:  Per(f,F) = h
      proof
A76:    dom Per(f,F) = B by CALCUL_2:19
          .= dom F by A15,A16,FINSEQ_1:def 3
          .= dom h by A15,FINSEQ_1:def 3;
        now
          let k be Nat such that
A77:      k in dom h;
A78:      k in Seg len h by A77,FINSEQ_1:def 3;
A79:      now
            assume
A80:        k in dom g;
            then
A81:        k in dom Sgm(C) by A20,A21,FINSEQ_1:def 3;
            k in Seg len g by A80,FINSEQ_1:def 3;
            then
A82:        F.k = Sgm(C).k by A15,A78;
            k in dom (F*f) by A76,A77,CALCUL_2:def 2;
            then (F*f).k = f.(Sgm(C).k) by A82,FUNCT_1:22;
            hence Per(f,F).k = f.(Sgm(C).k) by CALCUL_2:def 2
              .= (Sgm(C)*f).k by A81,FUNCT_1:23
              .= g.k by FINSEQ_3:def 1
              .= h.k by A80,FINSEQ_1:def 7;
          end;
          now
            given i be Nat such that
A83:        i in dom IdFinS('not' p,n) & k = len g + i;
            len IdFinS('not' p,n) = n by FINSEQ_2:69;
            then
A84:        1 <= i & i <= n by A83,FINSEQ_3:27;
            then 1+len g <= k & k <= n+len g by A83,XREAL_1:8;
            then
A85:        k in seq(len g,n) by CALCUL_2:1;
            then F.k = Sgm(A).(k-len g) by A15,A78;
            then F.k in rng Sgm(A) by A23,A85,FUNCT_1:12;
            then F.k in A by A22,FINSEQ_1:def 13;
            then f.(F.k) in {'not' p} by FUNCT_1:def 13;
            then
A86:        f.(F.k) = 'not' p by TARSKI:def 1;
            k in dom (F*f) by A76,A77,CALCUL_2:def 2;
            then (F*f).k = 'not' p by A86,FUNCT_1:22;
            then
A87:        Per(f,F).k = 'not' p by CALCUL_2:def 2;
            i in Seg n by A84,FINSEQ_1:3;
            hence Per(f,F).k = IdFinS('not' p,n).i by A87,FUNCOP_1:13
              .= h.k by A83,FINSEQ_1:def 7;
          end;
          hence Per(f,F).k = h.k by A77,A79,FINSEQ_1:38;
        end;
        hence thesis by A76,FINSEQ_1:17;
      end;
      then reconsider h as FinSequence of CQC-WFF;
A88:  |- h^<*p*> by A7,A75,CALCUL_2:30;
      a in X or a in {'not' p} by A7,A8,XBOOLE_0:def 2;
      then a = 'not' p by A8,TARSKI:def 1;
      then consider i being Nat such that
A89:  i in B & f.i = 'not' p by A8,FINSEQ_2:11;
      consider j being Nat such that
A90:  j in dom F & F.j = i by A27,A89,FINSEQ_2:11;
      (F*f).j = 'not' p by A89,A90,FUNCT_1:23;
      then
A91:  h.j = 'not' p by A75,CALCUL_2:def 2;
A92:  now
        assume
A93:    j in dom g;
        then g.j = 'not' p by A91,FINSEQ_1:def 7;
        then
A94:    g.j in {'not' p} & g.j in rng g by A93,FUNCT_1:12,TARSKI:def 1;
        rng g = rng f \ {'not' p} by FINSEQ_3:72;
        hence contradiction by A94,XBOOLE_0:def 4;
      end;
      j in dom f by A90;
      then j in dom h by A75,CALCUL_2:19;
      then consider k be Nat such that
A95:  k in dom IdFinS('not' p,n) & j = len g + k by A92,FINSEQ_1:38;
      reconsider g as FinSequence of CQC-WFF by FINSEQ_3:93;
      1 <= k & k <= len IdFinS('not' p,n) by A95,FINSEQ_3:27;
      then 1 <= len IdFinS('not' p,n) by XXREAL_0:2;
      then 1 <= n by FINSEQ_2:69;
      then
A96:  |- g^<*'not' p*>^<*p*> by A88,CALCUL_2:32;
      |- g^<*p*>^<*p*> by CALCUL_2:21;
      then
A97:  |- g^<*p*> by A96,CALCUL_2:26;
A98:  rng g = rng f \ {'not' p} by FINSEQ_3:72;
      rng f \ {'not' p} c= (X \/ {'not' p}) \ {'not' p} by A7,XBOOLE_1:33;
      then
A99:  rng g c= X \ {'not' p} by A98,XBOOLE_1:40;
      X \ {'not' p} c= X by XBOOLE_1:36;
      then rng g c= X by A99,XBOOLE_1:1;
      hence thesis by A97,Def2;
    end;
    hence thesis by A7,Def2;
  end;
end;

:: Similar to Ebb et al, Chapter IV, Lemma 7.6 (a)

theorem
  X |- 'not' p iff X \/ {p} is Inconsistent
proof
  thus X |- 'not' p implies X \/ {p} is Inconsistent
  proof
    assume X |- 'not' p;
    then consider f such that
A1: rng f c= X & |- f^<*'not' p*> by Def2;
A2: |- f^<*p*>^<*'not' p*> by A1,Th5;
    set f1 = f^<*p*>^<*p*>;
A3: Ant(f1) = f^<*p*> & Suc(f1) = p by CALCUL_1:5;
    then
A4: (Ant(f1)).(len f+1) = Suc(f1) by FINSEQ_1:59;
    1 in Seg 1 by FINSEQ_1:3;
    then 1 in dom <*p*> by FINSEQ_1:55;
    then len f+1 in dom (Ant(f1)) by A3,FINSEQ_1:41;
    then Suc(f1) is_tail_of Ant(f1) by A4,CALCUL_1:def 3;
    then |- f1 by CALCUL_1:33;
    then
A5: not f^<*p*> is Consistent by A2,Def4;
    set f2 = f^<*p*>;
A6: Ant(f2) = f & Suc(f2) = p by CALCUL_1:5;
    0+1 <= len f2 by CALCUL_1:10;
    then rng f2 = rng f \/ {p} by A6,CALCUL_1:3;
    hence thesis by A1,A5,Th4,XBOOLE_1:9;
  end;
  thus X \/ {p} is Inconsistent implies X |- 'not' p
  proof
    assume X \/ {p} is Inconsistent;
    then X \/ {p} |- 'not' p by Th6;
    then consider f such that
A7: rng f c= X \/ {p} & |- f^<*'not' p*> by Def2;
    now
      assume not rng f c= X;
      then consider a such that
A8:   a in rng f & not a in X by TARSKI:def 3;
      reconsider B = dom f as finite set;
      reconsider A = f"{p} as finite set;
      set n = card A;
      set g = f - {p};
      set h = g^IdFinS(p,n);
A9:   len IdFinS(p,n) = n by FINSEQ_2:69;
      defpred P[Nat,set] means ($1 in Seg len g implies $2 = Sgm(B \ A).$1) &
      ($1 in seq(len g,n) implies $2 = Sgm(A).($1 - len g));
A12:  for k be Nat st k in Seg len h ex a st P[k,a]
      proof
        let k be Nat such that k in Seg len h;
        now
          assume
A13:      k in Seg len g;
A14:      Seg len g misses seq(len g,n) by CALCUL_2:8;
          take a = Sgm(B\A).k;
          thus P[k,a] by A13,A14,XBOOLE_0:3;
        end;
        hence thesis;
      end;
      consider F being FinSequence such that
A15:  dom F = Seg len h & for k be Nat st k in Seg len h holds P[k,F.k]
      from FINSEQ_1:sch 1(A12);
A16:  len h = len g + len IdFinS(p,n) by FINSEQ_1:35
        .= len f - n + len IdFinS(p,n) by FINSEQ_3:66
        .= len f - n + n by FINSEQ_2:69
        .= len f;
      then
A17:  dom F = B by A15,FINSEQ_1:def 3;
      reconsider C = B \ A as finite set;
      now
        let b such that
A18:    b in C;
        b in dom f by A18;
        hence b in Seg len f by FINSEQ_1:def 3;
      end;
      then
A19:  C c= Seg len f by TARSKI:def 3;
      then
A20:  dom Sgm(C) = Seg card C by FINSEQ_3:45;
A21:  card C = card B - n by CARD_2:63,RELAT_1:167
        .= card Seg len f - n by FINSEQ_1:def 3
        .= len f - n by FINSEQ_1:78
        .= len g by FINSEQ_3:66;
      A c= B by RELAT_1:167;
      then
A22:  A c= Seg len f by FINSEQ_1:def 3;
A23:  now
        let i;
        assume
A24:    i in seq(len g,n);
        reconsider j = i-len g as Integer;
A25:    1+len g <= i & i <= n+len g by A24,CALCUL_2:1;
        then
A26:    1 <= j & j <= n by XREAL_1:21,22;
        0 <= j by A25,XREAL_1:21;
        then reconsider j as Element of NAT by INT_1:16;
        j in Seg n by A26,FINSEQ_1:3;
        hence i-len g in dom Sgm(A) by A22,FINSEQ_3:45;
      end;
A27:  rng F = B
      proof
        now
          let a such that
A28:      a in rng F;
          consider i being Nat such that
A29:      i in dom F & F.i = a by A28,FINSEQ_2:11;
A30:      i in Seg len g \/ seq(len g,n) by A9,A15,A29,Lm1;
A31:      now
            assume
A32:        i in Seg len g;
            then
A33:        a = Sgm(C).i by A15,A29;
            i in dom Sgm(C) by A19,A21,A32,FINSEQ_3:45;
            then a in rng Sgm(C) by A33,FUNCT_1:12;
            then a in C by A19,FINSEQ_1:def 13;
            hence a in B;
          end;
          now
            assume i in seq(len g,n);
            then
A34:        i-len g in dom Sgm(A) & a = Sgm(A).(i-len g) by A15,A23,A29;
            rng Sgm(A) = A by A22,FINSEQ_1:def 13;
            then
A35:        a in A by A34,FUNCT_1:12;
            A c= B by RELAT_1:167;
            hence a in B by A35;
          end;
          hence a in B by A30,A31,XBOOLE_0:def 2;
        end;
        hence rng F c= B by TARSKI:def 3;
        let a such that
A36:    a in B;
A37:    now
          assume
A38:      a in A;
          rng Sgm(A) = A by A22,FINSEQ_1:def 13;
          then consider i being Nat such that
A39:      i in dom Sgm(A) & Sgm(A).i = a by A38,FINSEQ_2:11;
          reconsider i as Element of NAT by ORDINAL1:def 13;
          set m = i + len g;
          len Sgm(A) = n by A22,FINSEQ_3:44;
          then
A40:      1 <= i & i <= n by A39,FINSEQ_3:27;
          then
A41:      1+len g <= m & m <= n+len g by XREAL_1:8;
          then
A42:      m in seq(len g,n) by CALCUL_2:1;
          i <= m by NAT_1:11;
          then
A43:      1 <= m & m <= len g + len IdFinS(p,n)
          by A40,A41,FINSEQ_2:69,XXREAL_0:2;
          then m <= len h by FINSEQ_1:35;
          then m in dom h by A43,FINSEQ_3:27;
          then
A44:      m in Seg len h by FINSEQ_1:def 3;
          a = Sgm(A).(m - len g) by A39;
          then a = F.m by A15,A42,A44;
          hence thesis by A15,A44,FUNCT_1:12;
        end;
        now
          assume not a in A;
          then
A45:      a in C by A36,XBOOLE_0:def 4;
          now
            let b such that
A46:        b in C;
            b in dom f by A46;
            hence b in Seg len f by FINSEQ_1:def 3;
          end;
          then C c= Seg len f by TARSKI:def 3;
          then a in rng Sgm(C) by A45,FINSEQ_1:def 13;
          then consider i being Nat such that
A47:      i in dom Sgm(C) & Sgm(C).i = a by FINSEQ_2:11;
          len Sgm(C) = len g by A20,A21,FINSEQ_1:def 3;
          then
A48:      1 <= i & i <= len g by A47,FINSEQ_3:27;
          then
A49:      i in Seg len g by FINSEQ_1:3;
          len g <= len h by CALCUL_1:6;
          then i <= len h by A48,XXREAL_0:2;
          then
A50:      i in Seg len h by A48,FINSEQ_1:3;
          then a = F.i by A15,A47,A49;
          hence thesis by A15,A50,FUNCT_1:12;
        end;
        hence thesis by A37;
      end;
      then reconsider F as Relation of B,B by A17,RELSET_1:11;
      B = {} implies B = {};
      then reconsider F as Function of B,B by A17,FUNCT_2:def 1;
      F is one-to-one
      proof
        let a1,a2 such that
A51:    a1 in dom F & a2 in dom F & F.a1 = F.a2;
A52:    a1 in Seg len g \/ seq(len g,n) &
        a2 in Seg len g \/ seq(len g,n) by A9,A15,A51,Lm1;
A53:    now
          assume
A54:      a1 in Seg len g;
A56:      F.a1 = Sgm(C).a1 by A15,A51,A54;
A57:      a1 in dom Sgm(C) by A19,A21,A54,FINSEQ_3:45;
          then F.a1 in rng Sgm(C) by A56,FUNCT_1:12;
          then
A58:      F.a1 in C by A19,FINSEQ_1:def 13;
A59:      now
            assume
A60:        a2 in Seg len g;
A61:        F.a2 = Sgm(C).a2 by A15,A51,A60;
A62:        a2 in dom Sgm(C) by A19,A21,A60,FINSEQ_3:45;
A63:        Sgm(C).a1 = Sgm(C).a2 by A15,A51,A54,A61;
            Sgm(C) is one-to-one by A19,FINSEQ_3:99;
            hence a1 = a2 by A57,A62,A63,FUNCT_1:def 8;
          end;
          now
            assume
A64:        a2 in seq(len g,n);
            then reconsider i = a2 as Element of NAT;
            Sgm(A).(i-len g) in rng Sgm(A) by A23,A64,FUNCT_1:12;
            then F.a2 in rng Sgm(A) by A15,A51,A64;
            then F.a2 in A by A22,FINSEQ_1:def 13;
            hence contradiction by A51,A58,XBOOLE_0:def 4;
          end;
          hence a1 = a2 by A52,A59,XBOOLE_0:def 2;
        end;
        now
          assume
A65:      a1 in seq(len g,n);
          then reconsider i = a1 as Element of NAT;
A66:      i-len g in dom Sgm(A) by A23,A65;
          Sgm(A).(i-len g) in rng Sgm(A) by A23,A65,FUNCT_1:12;
          then F.a1 in rng Sgm(A) by A15,A51,A65;
          then
A67:      F.a1 in A by A22,FINSEQ_1:def 13;
A68:      now
            assume
A69:        a2 in seq(len g,n);
            then reconsider i1 = a2 as Element of NAT;
A70:        F.a2 = Sgm(A).(i1-len g) by A15,A51,A69;
A71:        i1-len g in dom Sgm(A) by A23,A69;
A72:        Sgm(A).(i1-len g) = Sgm(A).(i-len g) by A15,A51,A65,A70;
            Sgm(A) is one-to-one by A22,FINSEQ_3:99;
            then i1-len g+len g = i-len g+len g by A66,A71,A72,FUNCT_1:def 8;
            hence a1 = a2;
          end;
          now
            assume
A73:        a2 in Seg len g;
A74:        F.a2 = Sgm(C).a2 by A15,A51,A73;
            a2 in dom Sgm(C) by A19,A21,A73,FINSEQ_3:45;
            then F.a2 in rng Sgm(C) by A74,FUNCT_1:12;
            then F.a2 in C by A19,FINSEQ_1:def 13;
            hence contradiction by A51,A67,XBOOLE_0:def 4;
          end;
          hence a1 = a2 by A52,A68,XBOOLE_0:def 2;
        end;
        hence thesis by A52,A53,XBOOLE_0:def 2;
      end;
      then reconsider F as Permutation of dom f by A27,FUNCT_2:83;
A75:  Per(f,F) = h
      proof
A76:    dom Per(f,F) = B by CALCUL_2:19
          .= dom F by A15,A16,FINSEQ_1:def 3
          .= dom h by A15,FINSEQ_1:def 3;
        now
          let k be Nat such that
A77:      k in dom h;
A78:      k in Seg len h by A77,FINSEQ_1:def 3;
A79:      now
            assume
A80:        k in dom g;
            then
A81:        k in dom Sgm(C) by A20,A21,FINSEQ_1:def 3;
            k in Seg len g by A80,FINSEQ_1:def 3;
            then
A82:        F.k = Sgm(C).k by A15,A78;
            k in dom (F*f) by A76,A77,CALCUL_2:def 2;
            then (F*f).k = f.(Sgm(C).k) by A82,FUNCT_1:22;
            hence Per(f,F).k = f.(Sgm(C).k) by CALCUL_2:def 2
              .= (Sgm(C)*f).k by A81,FUNCT_1:23
              .= g.k by FINSEQ_3:def 1
              .= h.k by A80,FINSEQ_1:def 7;
          end;
          now
            given i be Nat such that
A83:        i in dom IdFinS(p,n) & k = len g + i;
            len IdFinS(p,n) = n by FINSEQ_2:69;
            then
A84:        1 <= i & i <= n by A83,FINSEQ_3:27;
            then 1+len g <= k & k <= n+len g by A83,XREAL_1:8;
            then
A85:        k in seq(len g,n) by CALCUL_2:1;
            then F.k = Sgm(A).(k-len g) by A15,A78;
            then F.k in rng Sgm(A) by A23,A85,FUNCT_1:12;
            then F.k in A by A22,FINSEQ_1:def 13;
            then f.(F.k) in {p} by FUNCT_1:def 13;
            then
A86:        f.(F.k) = p by TARSKI:def 1;
            k in dom (F*f) by A76,A77,CALCUL_2:def 2;
            then (F*f).k = p by A86,FUNCT_1:22;
            then
A87:        Per(f,F).k = p by CALCUL_2:def 2;
            i in Seg n by A84,FINSEQ_1:3;
            hence Per(f,F).k = IdFinS(p,n).i by A87,FUNCOP_1:13
              .= h.k by A83,FINSEQ_1:def 7;
          end;
          hence Per(f,F).k = h.k by A77,A79,FINSEQ_1:38;
        end;
        hence thesis by A76,FINSEQ_1:17;
      end;
      then reconsider h as FinSequence of CQC-WFF;
A88:  |- h^<*'not' p*> by A7,A75,CALCUL_2:30;
      a in X or a in {p} by A7,A8,XBOOLE_0:def 2;
      then a = p by A8,TARSKI:def 1;
      then consider i being Nat such that
A89:  i in B & f.i = p by A8,FINSEQ_2:11;
      consider j being Nat such that
A90:  j in dom F & F.j = i by A27,A89,FINSEQ_2:11;
      (F*f).j = p by A89,A90,FUNCT_1:23;
      then
A91:  h.j = p by A75,CALCUL_2:def 2;
A92:  now
        assume
A93:    j in dom g;
        then g.j = p by A91,FINSEQ_1:def 7;
        then
A94:    g.j in {p} & g.j in rng g by A93,FUNCT_1:12,TARSKI:def 1;
        rng g = rng f \ {p} by FINSEQ_3:72;
        hence contradiction by A94,XBOOLE_0:def 4;
      end;
      j in dom f by A90;
      then j in dom h by A75,CALCUL_2:19;
      then consider k be Nat such that
A95:  k in dom IdFinS(p,n) & j = len g + k by A92,FINSEQ_1:38;
      reconsider g as FinSequence of CQC-WFF by FINSEQ_3:93;
      1 <= k & k <= len IdFinS(p,n) by A95,FINSEQ_3:27;
      then 1 <= len IdFinS(p,n) by XXREAL_0:2;
      then 1 <= n by FINSEQ_2:69;
      then
A96:  |- g^<*p*>^<*'not' p*> by A88,CALCUL_2:32;
      |- g^<*'not' p*>^<*'not' p*> by CALCUL_2:21;
      then
A97:  |- g^<*'not' p*> by A96,CALCUL_2:26;
A98:  rng g = rng f \ {p} by FINSEQ_3:72;
      rng f \ {p} c= (X \/ {p}) \ {p} by A7,XBOOLE_1:33;
      then
A99:  rng g c= X \ {p} by A98,XBOOLE_1:40;
      X \ {p} c= X by XBOOLE_1:36;
      then rng g c= X by A99,XBOOLE_1:1;
      hence thesis by A97,Def2;
    end;
    hence thesis by A7,Def2;
  end;
end;

begin :: Unions of Consistent Sets

:: Ebb et al, Chapter IV, Lemma 7.7

theorem
  for f being Function of NAT,bool CQC-WFF st
  (for n,m st m in dom f & n in dom f & n < m holds f.n is Consistent &
  f.n c= f.m) holds (union rng f) is Consistent
proof
  let f be Function of NAT,bool CQC-WFF;
  assume
A1: for n,m st m in dom f & n in dom f & n < m holds
  f.n is Consistent & f.n c= f.m;
  now
    assume not union rng f is Consistent;
    then consider Z such that
A2: Z c= union rng f & Z is finite & Z is Inconsistent by Th7;
    consider p such that
A3: Z |- p & Z |- 'not' p by A2,Def3;
    for n,m st m in dom f & n in dom f & n < m holds f.n c= f.m by A1;
    then consider k such that
A4: Z c= f.k by A2,Th3;
    reconsider Y = f.k as Subset of CQC-WFF;
    consider f1 such that
A5: rng f1 c= Z & |- f1^<*p*> by A3,Def2;
    consider f2 such that
A6: rng f2 c= Z & |- f2^<*'not' p*> by A3,Def2;
A7: |- f1^f2^<*p*> & |- f1^f2^<*'not' p*> by A5,A6,Th5,CALCUL_2:20;
    rng(f1^f2) = rng f1 \/ rng f2 by FINSEQ_1:44;
    then rng(f1^f2) c= Z by A5,A6,XBOOLE_1:8;
    then rng(f1^f2) c= Y by A4,XBOOLE_1:1;
    then Y |- p & Y |- 'not' p by A7,Def2;
    then
A8: not Y is Consistent by Def3;
A9: for n st n in dom f holds f.n is Consistent
    proof
      let n such that
A10:  n in dom f;
      n+1 in NAT;
      then n < n+1 & n+1 in dom f by FUNCT_2:def 1,NAT_1:13;
      hence f.n is Consistent by A1,A10;
    end;
    k in NAT;
    then k in dom f by FUNCT_2:def 1;
    hence contradiction by A8,A9;
  end;
  hence thesis;
end;

begin :: Construction of a Henkin Model

reserve A for non empty set,
  v for Element of Valuations_in A,
  J for interpretation of A;

theorem Th12:
  X is Inconsistent implies for J,v holds not J,v |= X
proof
  assume not X is Consistent;
  then X |- 'not' VERUM by Th6;
  then consider f such that
A1: rng f c= X & |- f^<*'not' VERUM*> by Def2;
  reconsider p = 'not' VERUM as Element of CQC-WFF;
  rng Ant(f^<*p*>) c= X & Suc(f^<*p*>) = p by A1,CALCUL_1:5;
  then p is_formal_provable_from X by A1,CALCUL_1:def 10;
  then
A2: X |= p by CALCUL_1:32;
  let J,v;
  now
    assume J,v |= X;
    then J,v |= 'not' VERUM by A2,CALCUL_1:def 12;
    then not J,v |= VERUM by VALUAT_1:28;
    hence contradiction by VALUAT_1:44;
  end;
  hence thesis;
end;

theorem Th13:
  {VERUM} is Consistent
proof
  consider A,J,v;
  J,v |= VERUM by VALUAT_1:44;
  then for p st p in {VERUM} holds J,v |= p by TARSKI:def 1;
  then J,v |= {VERUM} by CALCUL_1:def 11;
  hence thesis by Th12;
end;

registration
  cluster Consistent Subset of CQC-WFF;
  existence by Th13;
end;

reserve CX for Consistent Subset of CQC-WFF,
  P' for Element of QC-pred_symbols;

definition
  func HCar -> non empty set equals

  bound_QC-variables;
  coherence;
end;

definition
  let P be (Element of QC-pred_symbols),
  ll be CQC-variable_list of (the_arity_of P);
  redefine func P!ll -> Element of CQC-WFF;
  coherence
  proof
    set k = the_arity_of P;
    reconsider P' = P as QC-pred_symbol of k by QC_LANG3:3;
    P'!ll is Element of CQC-WFF;
    hence P!ll is Element of CQC-WFF;
  end;
end;

:: The Henkin Model (Ebb et al, Chapter V, 1)

definition
  let CX;
  mode Henkin_interpretation of CX -> interpretation of HCar means
    :Def6:
    for P being (Element of QC-pred_symbols),
    r being Element of relations_on HCar st it.P = r holds
    for a holds a in r iff ex ll being CQC-variable_list of
    (the_arity_of P) st a = ll & CX |- P!ll;
  existence
  proof
    set A = QC-pred_symbols;
    defpred P[set,set] means ex P' st P' = $1 &
    $2 = {ll where ll is CQC-variable_list of (the_arity_of P') :
    CX |- P'!ll};
A2: for a st a in A ex b st P[a,b]
    proof
      let a such that
A3:   a in A;
      reconsider a as Element of QC-pred_symbols by A3;
      consider b such that
A4:   b = {ll where ll is CQC-variable_list of (the_arity_of a) : CX |- a!ll};
      take b;
      take P' = a;
      thus thesis by A4;
    end;
    consider f being Function such that
A5: dom f = A & for a st a in A holds P[a,f.a] from CLASSES1:sch 1(A2);
A6: for P being (Element of QC-pred_symbols),
    r being Element of relations_on HCar st f.P = r holds
    for a holds a in r iff ex ll being CQC-variable_list of
    (the_arity_of P) st a = ll & CX |- P!ll
    proof
      let P be (Element of QC-pred_symbols),
      r be Element of relations_on HCar such that
A7:   f.P = r;
      let a;
      thus a in r implies ex ll being CQC-variable_list of
      (the_arity_of P) st a = ll & CX |- P!ll
      proof
        assume
A8:     a in r;
        consider P' such that
A9:     P' = P &
        f.P = {ll where ll is CQC-variable_list of (the_arity_of P') :
        CX |- P'!ll} by A5;
        thus thesis by A7,A8,A9;
      end;
      thus (ex ll being CQC-variable_list of
      (the_arity_of P) st a = ll & CX |- P!ll) implies a in r
      proof
        given ll being CQC-variable_list of (the_arity_of P) such that
A10:    a = ll & CX |- P!ll;
        ex P' st P' = P &
        f.P = {l where l is CQC-variable_list of (the_arity_of P') :
        CX |- P'!l} by A5;
        hence thesis by A7,A10;
      end;
    end;
A11: for P being (Element of QC-pred_symbols),
    r being Element of relations_on HCar st f.P = r holds
    r = empty_rel(HCar) or the_arity_of P = the_arity_of r
    proof
      let P be (Element of QC-pred_symbols),
      r be Element of relations_on HCar such that
A12:  f.P = r;
      assume
A13:  not r = empty_rel(HCar);
      then r <> {} by MARGREL1:32;
      then consider a such that
A14:  a in r by XBOOLE_0:def 1;
      consider P' such that
A15:  P' = P & f.P = {ll where ll is CQC-variable_list of (the_arity_of P') :
      CX |- P'!ll} by A5;
      consider ll' being CQC-variable_list of (the_arity_of P') such that
A16:  a = ll' & CX |- P'!ll' by A12,A14,A15;
      rng ll' c= bound_QC-variables by CQC_LANG:def 5;
      then reconsider a as FinSequence of HCar by A16,FINSEQ_1:def 4;
      the_arity_of r = len a by A13,A14,MARGREL1:def 11;
      hence thesis by A15,A16,QC_LANG1:def 8;
    end;
    b in rng f implies b in relations_on HCar
    proof
      assume b in rng f;
      then consider a such that
A17:  a in dom f & b = f.a by FUNCT_1:def 5;
      consider P' such that
A18:  P' = a & f.a = {ll where ll is CQC-variable_list of (the_arity_of P') :
      CX |- P'!ll} by A5,A17;
      c in b implies c in HCar*
      proof
        assume c in b;
        then consider ll being CQC-variable_list of (the_arity_of P')
        such that
A19:    c = ll & CX |- P'!ll by A17,A18;
        rng ll c= bound_QC-variables by CQC_LANG:def 5;
        then ll is FinSequence of HCar by FINSEQ_1:def 4;
        hence c in HCar* by A19,FINSEQ_1:def 11;
      end;
      then
A20:  b c= HCar* by TARSKI:def 3;
      for fin,fin' being FinSequence of HCar st fin in b & fin' in b
      holds len fin = len fin'
      proof
        let fin,fin' be FinSequence of HCar;
        assume
A21:    fin in b & fin' in b;
        then consider ll being CQC-variable_list of (the_arity_of P')
        such that
A22:    fin = ll & CX |- P'!ll by A17,A18;
        consider ll' being CQC-variable_list of (the_arity_of P') such that
A23:    fin' = ll' & CX |- P'!ll' by A17,A18,A21;
        len fin = the_arity_of P' by A22,QC_LANG1:def 8;
        hence thesis by A23,QC_LANG1:def 8;
      end;
      hence thesis by A20,MARGREL1:def 8;
    end;
    then rng f c= relations_on HCar by TARSKI:def 3;
    then reconsider f as Function of A,relations_on HCar by A5,FUNCT_2:4;
    reconsider f as interpretation of HCar by A11,VALUAT_1:def 10;
    take f;
    thus thesis by A6;
  end;
end;

definition
  func valH -> Element of Valuations_in HCar equals

  id bound_QC-variables;
  coherence by VALUAT_1:def 1;
end;

begin :: Some Properties of the Henkin Model

reserve JH for Henkin_interpretation of CX;

theorem Th14:
  valH*'ll = ll
proof
A1: dom (valH*'ll) = dom ll &
  for i st i in dom (valH*'ll) holds valH.(ll.i) = ll.i
  proof
    thus dom (valH*'ll) = dom ll
    proof
      thus dom (valH*'ll) c= dom ll by RELAT_1:44;
      thus dom ll c= dom (valH*'ll)
      proof
        let a such that
A2:     a in dom ll;
        reconsider i = a as Element of NAT by A2;
A3:     ll.i in rng ll by A2,FUNCT_1:12;
        rng ll c= bound_QC-variables by CQC_LANG:def 5;
        then ll.i in bound_QC-variables by A3;
        then ll.i in dom id bound_QC-variables by RELAT_1:71;
        hence thesis by A2,FUNCT_1:21;
      end;
    end;
    thus for i st i in dom (valH*'ll) holds valH.(ll.i) = ll.i
    proof
      let i such that
A4:   i in dom (valH*'ll);
      dom(valH*'ll) c= dom ll by RELAT_1:44;
      then
A5:   ll.i in rng ll by A4,FUNCT_1:12;
      rng ll c= bound_QC-variables by CQC_LANG:def 5;
      hence thesis by A5,FUNCT_1:35;
    end;
  end;
A6: for i st 1 <= i & i <= k holds valH.(ll.i) = ll.i
  proof
    let i such that
A7: 1 <= i & i <= k;
    1 <= i & i <= len (valH*'ll) by A7,VALUAT_1:def 8;
    then i in dom (valH*'ll) by FINSEQ_3:27;
    hence thesis by A1;
  end;
A8: len (valH*'ll) = k & len ll = k by QC_LANG1:def 8,VALUAT_1:def 8;
  then
X: dom ll = Seg k by FINSEQ_1:def 3;
    for i being Nat st i in dom ll holds (valH*'ll).i = ll.i
  proof
    let i be Nat such that
A9: i in dom ll;
    reconsider i as Element of NAT by ORDINAL1:def 13;
A10: 1 <= i & i <= k by A9,FINSEQ_1:3,X;
    then (valH*'ll).i = valH.(ll.i) by VALUAT_1:def 8;
    hence thesis by A6,A10;
  end;
  hence thesis by A8,FINSEQ_2:10;
end;

theorem Th15:
  |- f^<*VERUM*>
proof
  set PR = <*[f^<*VERUM*>,1]*>;
A1: rng PR = {[f^<*VERUM*>,1]} by FINSEQ_1:55;
  now
    let a such that
A2: a in rng PR;
A3: a = [f^<*VERUM*>,1] by A1,A2,TARSKI:def 1;
    f^<*VERUM*> in set_of_CQC-WFF-seq &
    1 in Proof_Step_Kinds by CALCUL_1:def 6,CQC_THE1:43;
    hence a in [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by A3,ZFMISC_1:106;
  end;
  then rng PR c= [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by TARSKI:def 3;
  then reconsider PR as FinSequence of [:set_of_CQC-WFF-seq,Proof_Step_Kinds
  :] by FINSEQ_1:def 4;
  now
    let n be Nat such that
A4: 1 <= n & n <= len PR;
    1 <= n & n <= 1 by A4,FINSEQ_1:56;
    then n = 1 by XXREAL_0:1;
    then PR.n = [f^<*VERUM*>,1] by FINSEQ_1:57;
    then (PR.n)`1 = f^<*VERUM*> & (PR.n)`2 = 1 by MCART_1:7;
    hence PR,n is_a_correct_step by CALCUL_1:def 7;
  end;
  then
A5: PR is a_proof by CALCUL_1:def 8;
  PR.1 = [f^<*VERUM*>,1] by FINSEQ_1:57;
  then PR.(len PR) = [f^<*VERUM*>,1] by FINSEQ_1:57;
  then (PR.(len PR))`1 = f^<*VERUM*> by MCART_1:7;
  hence thesis by A5,CALCUL_1:def 9;
end;

theorem
  JH,valH |= VERUM iff CX |- VERUM
proof
  set f = <*>CQC-WFF;
  rng f c= CX & |- f^<*VERUM*> by Th15,RELAT_1:60,XBOOLE_1:2;
  hence thesis by Def2,VALUAT_1:44;
end;

theorem
  JH,valH |= P!ll iff CX |- P!ll
proof
  thus JH,valH |= P!ll implies CX |- P!ll
  proof
    assume
A1: JH,valH |= P!ll;
    set rel = JH.P;
    reconsider rel as Element of relations_on HCar;
    Valid(P!ll,JH).valH = TRUE by A1,VALUAT_1:def 12;
    then (valH*'ll) in rel by VALUAT_1:16;
    then ll in rel by Th14;
    then consider ll' being CQC-variable_list of (the_arity_of P) such that
A2: ll' = ll & CX |- P!ll' by Def6;
    thus thesis by A2;
  end;
  thus CX |- P!ll implies JH,valH |= P!ll
  proof
    assume
A3: CX |- P!ll;
    P is (QC-pred_symbol of the_arity_of P) by QC_LANG3:3;
    then the_arity_of P = k by SUBSTUT2:3;
    then ll in JH.P by A3,Def6;
    then (valH*'ll) in JH.P by Th14;
    then Valid(P!ll,JH).valH = TRUE by VALUAT_1:16;
    hence thesis by VALUAT_1:def 12;
  end;
end;

