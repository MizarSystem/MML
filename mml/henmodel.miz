:: Equivalences of Inconsistency and {H}enkin Models
::  by Patrick Braselmann and Peter Koepke
::
:: Received September 25, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies CQC_LANG, QC_LANG1, FINSEQ_1, RELAT_1, QMAX_1, ZF_LANG, ZF_MODEL,
      BOOLE, FUNCT_1, VALUAT_1, FINSET_1, FUNCT_2, ARYTM_1, HENMODEL, ORDINAL2,
      ORDINAL1, TARSKI, CARD_1, INT_1, WELLORD1, WELLORD2, MARGREL1, ARYTM_3,
      CQC_THE1, MCART_1, CALCUL_1, CALCUL_2, SUBLEMMA;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, CALCUL_1, XREAL_0, RELAT_1,
      FUNCT_1, ORDINAL1, REAL_1, NAT_1, FINSEQ_1, QC_LANG1, CQC_LANG, CQC_THE1,
      VALUAT_1, FINSET_1, RELSET_1, FUNCT_2, FRAENKEL, CARD_1, MARGREL1,
      CQC_SIM1, DOMAIN_1, MCART_1, NUMBERS, ORDINAL2, FINSEQ_3, CALCUL_2,
      INT_1, WELLORD1, WELLORD2;
 constructors QC_LANG3, DOMAIN_1, CQC_THE1, ARYTM_0, RELAT_1, REAL_1, CQC_THE3,
      RECDEF_1, CQC_SIM1, FINSEQ_1, PARTFUN1, FUNCT_2, MARGREL1, CQC_LANG,
      NAT_1, MCART_1, FUNCT_1, WELLORD2, CARD_1, XREAL_0, MEMBERED, ORDINAL2,
      RELSET_1, XBOOLE_0, ZFMISC_1, XCMPLX_0, ORDINAL1, QC_LANG2, TARSKI,
      CALCUL_1, SETFAM_1, FINSEQ_3, CALCUL_2, INT_1, WELLORD1, ARYTM_2,
      SUBSTUT1, SUBSTUT2, SUBLEMMA;
 registrations FINSEQ_1, RELSET_1, CQC_LANG, ORDINAL2, SUBSET_1, QC_LANG1,
      FUNCT_1, FINSET_1, XREAL_0, INT_1, ORDINAL1;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, XBOOLE_0, FUNCT_1;
 theorems TARSKI, FINSEQ_1, FINSEQ_3, FUNCT_1, VALUAT_1, XBOOLE_0, FINSEQ_2,
      ZFMISC_1, RELAT_1, QC_LANG3, XBOOLE_1, NAT_1, MARGREL1, FUNCT_2,
      RELSET_1, FINSET_1, CALCUL_1, CQC_THE1, ORDINAL2, AXIOMS, XCMPLX_1,
      REAL_1, CALCUL_2, CARD_2, INT_1, ORDINAL1, WELLORD1, WELLORD2, CARD_3,
      QC_LANG1, CQC_LANG, MCART_1, SUBSTUT2;
 schemes FUNCT_1, FINSEQ_1, NAT_1;

begin :: Preliminaries and Equivalences of Inconsistency

 reserve a,a1,a2,b,b1,b2,c,d for set,
         X,Y,Z for Subset of CQC-WFF,
         i,j,k,l,m,n,n1,n2 for Nat,
         p,q for Element of CQC-WFF,
         A for non empty set,
         P for QC-pred_symbol of k,
         ll for CQC-variable_list of k,
         f,f1,f2,g,g1,g2 for FinSequence of CQC-WFF;

definition let D be non empty set, X be Subset of bool D;
  redefine func union X -> Subset of D;
  coherence
  proof
    now let a such that A1: a in union X;
      consider b such that A2: a in b & b in X by A1,TARSKI:def 4;
      thus a in D by A2;
    end;
    hence thesis by TARSKI:def 3;
  end;
end;

 reserve A for non empty finite Subset of NAT;

theorem Th1:
  for f being Function of n,A st
     ((ex m st succ m = n) & f is one-to-one & rng f = A &
     for n,m st m in dom f & n in dom f & n < m holds f.n in f.m) holds
                                                   f.(union n) = union rng f
   proof
     let f be Function of n,A such that
 A1: (ex m st succ m = n) & f is one-to-one & rng f = A &
        for n,m st m in dom f & n in dom f & n < m holds f.n in f.m;
     thus f.(union n) c= union rng f
     proof
       let a;
       assume A2: a in f.(union n);
       consider m such that A3: n = succ m by A1;
A4:    a in f.m by A2,A3,ORDINAL2:2;
       dom f = n by FUNCT_2:def 1; then
       m in dom f by A3,ORDINAL1:10; then
       f.m in rng f by FUNCT_1:12;
       hence a in union rng f by A4,TARSKI:def 4;
     end;
     thus union rng f c= f.(union n)
     proof
       let a;
       assume a in union rng f; then
       consider b such that A5: a in b & b in rng f by TARSKI:def 4;
       consider c such that A6: c in dom f & f.c = b by A5,FUNCT_1:def 5;
       reconsider i = c as Ordinal by A6,ORDINAL1:23;
       reconsider i as Nat by A6,ORDINAL1:19;
       consider m such that A7: n = succ m by A1;
       i c= m by A6,A7,ORDINAL1:34; then
A8:   i c< m or i = m by XBOOLE_0:def 8;
A9:   i in m or i = m by A8,ORDINAL1:21; then
       i in {k : k < m} or i = m by AXIOMS:30; then
A10:   (ex k st k = i & k < m) or i = m;
A11:   dom f = n by FUNCT_2:def 1; then
A12:   m in dom f by A7,ORDINAL1:10;
A13:   i in dom f by A9,A11,A12,ORDINAL1:19;
       now assume A14: f.i in f.m;
         f.i in rng f by A13,FUNCT_1:12; then
         reconsider i1 = f.i as Nat by A1;
         f.m in rng f by A12,FUNCT_1:12; then
         reconsider i2 = f.m as Nat by A1;
         i1 c= i2 by A14,ORDINAL1:def 2; then
         a in i2 by A5,A6;
         hence thesis by A7,ORDINAL2:2;
       end;
       hence thesis by A1,A5,A6,A7,A10,A12,ORDINAL2:2;
     end;
   end;

theorem Th2:
  union A in A & for a st a in A holds (a in union A or a = union A)
   proof
     RelIncl A is well-ordering by WELLORD2:9; then
     consider a being Ordinal such that
A1:     RelIncl A,RelIncl a are_isomorphic by WELLORD2:14;
     RelIncl a,RelIncl A are_isomorphic by A1,WELLORD1:50; then
     consider F being Function such that
A2:     F is_isomorphism_of RelIncl a,RelIncl A by WELLORD1:def 8;
A3:  for m,n st m in dom F & n in dom F & n < m holds F.n in F.m
     proof
       let m,n such that A4: m in dom F & n in dom F & n < m;
       m in field RelIncl a & n in field RelIncl a by A2,A4,WELLORD1:def 7;
       then A5: m in a & n in a by WELLORD2:def 1;
       n in {i: i < m } by A4; then
       n in m by AXIOMS:30; then
       n <> m & n c= m by ORDINAL1:def 2; then
       [n,m] in RelIncl a by A5,WELLORD2:def 1; then
A6:    [F.n,F.m] in RelIncl A by A2,WELLORD1:def 7;
       F.n in rng F & F.m in rng F by A4,FUNCT_1:12; then
       F.n in field RelIncl A & F.m in field RelIncl A by A2,WELLORD1:def 7;
       then A7: F.n in A & F.m in A by WELLORD2:def 1; then
A8:    F.n c= F.m by A6,WELLORD2:def 1;
       F is one-to-one by A2,WELLORD1:def 7; then
       F.n <> F.m by A4,FUNCT_1:def 8; then
A9:   F.n c< F.m by A8,XBOOLE_0:def 8;
       reconsider b = F.n as Nat by A7;
       reconsider c = F.m as Nat by A7;
       b in c by A9,ORDINAL1:21;
       hence thesis;
     end;
     rng F = field RelIncl A by A2,WELLORD1:def 7; then
A10: rng F = A by WELLORD2:def 1;
       consider F1 being Function such that
A11:   F1 is_isomorphism_of RelIncl A,RelIncl a by A1,WELLORD1:def 8;
       dom F1 = field RelIncl A by A11,WELLORD1:def 7; then
A12:   dom F1 = A by WELLORD2:def 1; then
       rng F1 is finite by FINSET_1:26; then
       field RelIncl a is finite by A11,WELLORD1:def 7; then
A13:   a is finite by WELLORD2:def 1;
       consider b such that A14: b in dom F1 by A12,XBOOLE_0:def 1;
       F1.b in rng F1 by A14,FUNCT_1:12; then
       field RelIncl a is non empty by A11,WELLORD1:def 7; then
       a is non empty by WELLORD2:def 1; then
       not a c= {} by XBOOLE_1:3; then
A15:   {} in a by ORDINAL1:26;
       now assume a is being_limit_ordinal; then
A16:     omega c= a by A15,ORDINAL2:19;
         now assume omega is finite; then
           Card omega <` Card omega by CARD_3:58;
           hence contradiction;
         end;
         hence contradiction by A13,A16,FINSET_1:13;
       end; then
A17: ex b being Ordinal st succ b = a by ORDINAL1:42;
     now assume not a in omega; then
A18:   omega c= a by ORDINAL1:26;
       now assume omega is finite; then
           Card omega <` Card omega by CARD_3:58;
           hence contradiction;
         end;
       hence contradiction by A13,A18,FINSET_1:13;
     end; then
     reconsider a as Nat;
A19: now let b be Ordinal;
       assume succ b = a; then
       b in a by ORDINAL1:10;
       hence b is Nat by ORDINAL1:19;
     end;
A20: ex m st succ m = a
     proof
       consider b being Ordinal such that A21: succ b = a by A17;
       reconsider b as Nat by A19,A21;
       take b;
       thus thesis by A21;
     end;
     dom F = field RelIncl a by A2,WELLORD1:def 7; then
A22: dom F = a by WELLORD2:def 1; then
     reconsider F as Function of a,A by A10,FUNCT_2:3;
     consider m such that A23: succ m = a by A20;
     (ex m st succ m = a) & F is one-to-one & rng F = A &
     for n,m st m in dom F & n in dom F & n < m holds F.n in F.m
                by A2,A3,A10,A20,WELLORD1:def 7; then
A24: F.(union a) = union rng F by Th1;
A25: union a = m by A23,ORDINAL2:2; then
     union a in dom F by A22,A23,ORDINAL1:10;
     hence union A in A by A10,A24,FUNCT_1:12;
     thus for b st b in A holds (b in union A or b = union A)
     proof
       let b such that A26: b in A;
       now assume A27: b <> union A;
         consider c such that
  A28:   c in dom F & F.c = b by A10,A26,FUNCT_1:def 5;
         c is Ordinal by A28,ORDINAL1:23; then
         reconsider c as Nat by A28,ORDINAL1:19;
A29:     c in m or c = m by A23,A28,ORDINAL1:13;
A30:     m in dom F by A22,A23,ORDINAL1:10;
         c in {k : k < m}
           by A10,A23,A24,A27,A28,A29,AXIOMS:30,ORDINAL2:2; then
         ex k st k = c & k < m;
         hence thesis by A3,A10,A24,A25,A28,A30;
       end;
       hence thesis;
     end;
   end;

definition let A be set;
  func min* A -> Nat means
  :Def1: (it in A & for k st k in A holds it <= k) if
                                   A is non empty Subset of NAT
          otherwise it = 0;
   existence
   proof
     A is non empty Subset of NAT implies ex n st
                                         n in A & for k st k in A holds n <= k
     proof
       assume A1: A is non empty Subset of NAT;
       consider x being Element of A;
       defpred P[Nat] means $1 in A;
       x is Nat by A1,TARSKI:def 3; then
A2:    ex k st P[k] by A1;
       thus ex k st P[k] & for n st P[n] holds k <= n from NAT_1:sch 5(A2);
     end;
     hence thesis;
   end;
   uniqueness
   proof
     let i,n;
     A is non empty Subset of NAT &
       (i in A & for k st k in A holds i <= k) &
          (n in A & for k st k in A holds n <= k) implies i = n
     proof
       assume A is non empty Subset of NAT &
         (i in A & for k st k in A holds i <= k) &
         (n in A & for k st k in A holds n <= k); then
       i <= n & n <= i;
       hence thesis by AXIOMS:21;
     end;
     hence thesis;
   end;
   consistency;
end;

 reserve C for non empty set;

theorem Th3:
  for f being Function of NAT,C, X being finite set st
    (for n,m st m in dom f & n in dom f & n < m holds f.n c= f.m) &
                                  X c= union rng f holds ex k st X c= f.k
 proof
   let f be Function of NAT,C, X be finite set such that
A1:  (for n,m st m in dom f & n in dom f & n < m
    holds f.n c= f.m) & X c= union rng f;
A2: now assume A3:X is empty;
      take k = 0;
      {} c= f.k by XBOOLE_1:2;
      hence thesis by A3;
    end;
    now assume A4: not X is empty;
      deffunc F(set) = min* {i : $1 in f.i};
      consider g being Function such that A5: dom g = X & for a st a in X holds
                g.a = F(a) from FUNCT_1:sch 3;
    reconsider A = rng g as finite set by A5,FINSET_1:26;
    now let b; assume b in A; then
      consider a such that A6: a in dom g & g.a = b by FUNCT_1:def 5;
      b = min* {i : a in f.i} by A5,A6;
      hence b in NAT;
    end; then
A7: A c= NAT by TARSKI:def 3;
    consider c such that A8: c in dom g by A4,A5,XBOOLE_0:def 1;
    reconsider A as non empty finite Subset of NAT by A7,A8,FUNCT_1:12;
    union A in A by Th2; then
    reconsider a = union A as Nat;
    take a;
    thus X c= f.a
    proof
      let b; assume A9: b in X; then
      consider c such that A10: b in c & c in rng f by A1,TARSKI:def 4;
      consider d such that A11: d in dom f & f.d = c by A10,FUNCT_1:def 5;
      reconsider d as Nat by A11;
      d in {i : b in f.i} by A10,A11; then
      reconsider Y = {i : b in f.i} as non empty set;
      now let a; assume a in Y; then
        consider i such that A12: i = a & b in f.i;
        thus a in NAT by A12;
      end; then
      reconsider Y as non empty Subset of NAT by TARSKI:def 3;
A13:  g.b = min* Y by A5,A9; then
      reconsider Y' = g.b as Nat;
      Y' in Y & for k st k in Y holds Y' <= k by A13,Def1; then
      consider i such that A14: i = g.b & b in f.i;
      Y' in A by A5,A9,FUNCT_1:12; then
A15:  Y' in a or Y' = a by Th2;
A16:  dom f = NAT by FUNCT_2:def 1;
      now assume Y' in a; then
        Y' in {k : k < a} by AXIOMS:30; then
        ex k st k = Y' & k < a; then
        f.Y' c= f.a by A1,A16;
        hence thesis by A14;
      end;
      hence thesis by A14,A15;
    end;
  end;
  hence thesis by A2;
  end;

definition let X,p;
  pred X |- p means :Def2:
    ex f st rng f c= X & |- f^<*p*>;
end;

definition let X;
  attr X is Consistent means
  :Def3: for p holds not (X |- p & X |- 'not' p);
end;

notation let X;
  antonym X is Inconsistent for X is Consistent;
end;

definition let f be FinSequence of CQC-WFF;
  attr f is Consistent means
  :Def4: for p holds not (|- f^<*p*> & |- f^<*'not' p*>);
end;

notation let f be FinSequence of CQC-WFF;
  antonym f is Inconsistent for f is Consistent;
end;

theorem Th4:
  X is Consistent & rng g c= X implies g is Consistent
  proof
    assume A1: X is Consistent & rng g c= X;
    now assume g is Inconsistent; then
      consider p such that A2: |- g^<*p*> & |- g^<*'not' p*> by Def4;
      X |- p & X |- 'not' p by A1,A2,Def2;
      hence contradiction by A1,Def3;
    end;
    hence thesis;
  end;

theorem Th5:
  |- f^<*p*> implies |- f^g^<*p*>
  proof
    assume A1: |- f^<*p*>;
A2: Ant(f^<*p*>) = f & Ant(f^g^<*p*>) = f^g &
    Suc(f^g^<*p*>) = p by CALCUL_1:5; then
A3: Ant(f^<*p*>) is_Subsequence_of Ant(f^g^<*p*>) by CALCUL_1:8;
    Suc(f^<*p*>) = Suc(f^g^<*p*>) by A2,CALCUL_1:5;
    hence thesis by A1,A3,CALCUL_1:36;
  end;

:: Ebb et al, Chapter IV, Lemma 7.2

theorem Th6:
  X is Inconsistent iff for p holds X |- p
  proof
    thus X is Inconsistent implies for p holds X |- p
    proof
      assume X is Inconsistent; then
      consider q such that A1: X |- q & X |- 'not' q by Def3;
      let p;
      consider f1 such that
A2:   rng f1 c= X & |- f1^<*q*> by A1,Def2;
      consider f2 such that
A3:   rng f2 c= X & |- f2^<*'not' q*> by A1,Def2;
A4:   |- f1^f2^<*q*> & |- f1^f2^<*'not' q*> by A2,A3,Th5,CALCUL_2:20;
      take f3 = f1^f2;
      rng f3 = rng f1 \/ rng f2 by FINSEQ_1:44;
      hence thesis by A2,A3,A4,CALCUL_2:25,XBOOLE_1:8;
    end;
    assume for p holds X |- p; then
    X |- VERUM & X |- 'not' VERUM;
    hence thesis by Def3;
  end;

:: One direction of Ebb et al, Chapter IV, Lemma 7.4

theorem Th7:
  X is Inconsistent implies
    ex Y st Y c= X & Y is finite & Y is Inconsistent
  proof
    assume X is Inconsistent; then
    consider p such that A1: X |- p & X |- 'not' p by Def3;
    consider f1 such that A2: rng f1 c= X & |- f1^<*p*> by A1,Def2;
    consider f2 such that A3: rng f2 c= X & |- f2^<*'not' p*> by A1,Def2;
A4: |- f1^f2^<*p*> & |- f1^f2^<*'not' p*> by A2,A3,Th5,CALCUL_2:20;
    reconsider Y = rng(f1^f2) as Subset of CQC-WFF;
    take Y;
    Y = rng f1 \/ rng f2 by FINSEQ_1:44;
    hence Y c= X by A2,A3,XBOOLE_1:8;
    Y |- p & Y |- 'not' p by A4,Def2;
    hence thesis by Def3;
  end;

Lm1:
  for f,g be FinSequence holds
     Seg len(f^g) = Seg len f \/ seq(len f,len g) by CALCUL_2:9;

Lm2:
  Seg n misses seq(n,m) by CALCUL_2:8;

theorem
  X \/ {p} |- q implies ex g st rng g c= X & |- g^<*p*>^<*q*>
  proof
    assume X \/ {p} |- q; then
      consider f such that A1: rng f c= X \/ {p} & |- f^<*q*> by Def2;
A2:   now assume A3: rng f c= X;
        take f;
        |- f^<*p*>^<*q*> by A1,Th5;
        hence thesis by A3; end;
      now assume not rng f c= X; then
        consider a such that
A4:    a in rng f & not a in X by TARSKI:def 3;
        reconsider B = dom f as finite set;
        reconsider A = f"{p} as finite set;
        set n = card A;
        set g = f - {p};
        set h = g^IdFinS(p,n);
A5:   len IdFinS(p,n) = n by FINSEQ_2:69; then
A6:    Seg len h = Seg len g \/ seq(len g,n) by Lm1;
        defpred P[Nat,set] means
         ($1 in Seg len g implies $2 = Sgm(B \ A).$1) &
         ($1 in seq(len g,n) implies $2 = Sgm(A).($1 - len g));
A7:     for k,b1,b2 st k in Seg len h & P[k,b1] & P[k,b2] holds b1=b2
                                               by A6,XBOOLE_0:def 2;
A8:  for k st k in Seg len h ex a st P[k,a]
      proof
        let k such that k in Seg len h;
A9:    now assume A10: k in Seg len g;
A11:     Seg len g misses seq(len g,n) by Lm2;
A12:      not k in seq(len g,n) by A10,A11,XBOOLE_0:3;
          take a = Sgm(B\A).k;
          thus P[k,a] by A12;
        end;
        thus thesis by A9;
      end;
      consider F being FinSequence such that
A13:   dom F = Seg len h & for k st k in Seg len h holds P[k,F.k]
                                             from FINSEQ_1:sch 1(A7,A8);
A14: len h = len g + len IdFinS(p,n) by FINSEQ_1:35
            .= len f - n + len IdFinS(p,n) by FINSEQ_3:66
            .= len f - n + n by FINSEQ_2:69
            .= len f; then
A15:  dom F = B by A13,FINSEQ_1:def 3;
      reconsider C = B \ A as finite set;
      now let b such that A16: b in C;
        C c= dom f by XBOOLE_1:36; then
        b in dom f by A16;
        hence b in Seg len f by FINSEQ_1:def 3;
      end; then
A17:  C c= Seg len f by TARSKI:def 3; then
A18:  dom Sgm(C) = Seg card C by FINSEQ_3:45;
      A c= B by RELAT_1:167; then
A19: card C = card B - n by CARD_2:63
            .= card Seg len f - n by FINSEQ_1:def 3
            .= len f - n by FINSEQ_1:78
            .= len g by FINSEQ_3:66;
      A c= B by RELAT_1:167; then
A20:  A c= Seg len f by FINSEQ_1:def 3;
A21:  now let i; assume A22: i in seq(len g,n);
        reconsider j = i-len g as Integer;
A23:    1+len g <= i & i <= n+len g by A22,CALCUL_2:1; then
A24:    1 <= j & j <= n by REAL_1:84,86;
        0 <= j by A23,REAL_1:84; then
        reconsider j as Nat by INT_1:16;
        j in Seg n by A24,FINSEQ_1:3;
        hence i-len g in dom Sgm(A) by A20,FINSEQ_3:45;
      end;
A25:  rng F = B
      proof
        now let a such that A26: a in rng F;
          consider i such that A27: i in dom F & F.i = a by A26,FINSEQ_2:11;
A28:      i in Seg len g \/ seq(len g,n) by A5,A13,A27,Lm1;
A29:      now assume A30: i in Seg len g; then
A31:        a = Sgm(C).i by A13,A27;
            i in dom Sgm(C) by A17,A19,A30,FINSEQ_3:45; then
            a in rng Sgm(C) by A31,FUNCT_1:12; then
            a in C by A17,FINSEQ_1:def 13;
            hence a in B by XBOOLE_0:def 4;
          end;
          now assume i in seq(len g,n); then
A32:        i-len g in dom Sgm(A) & a = Sgm(A).(i-len g) by A13,A21,A27;
            rng Sgm(A) = A by A20,FINSEQ_1:def 13; then
A33:        a in A by A32,FUNCT_1:12;
            A c= B by RELAT_1:167;
            hence a in B by A33;
          end;
          hence a in B by A28,A29,XBOOLE_0:def 2;
        end;
        hence rng F c= B by TARSKI:def 3;
        let a such that A34: a in B;
A35:    now assume A36: a in A;
          rng Sgm(A) = A by A20,FINSEQ_1:def 13; then
          consider i such that
A37:      i in dom Sgm(A) & Sgm(A).i = a by A36,FINSEQ_2:11;
          set m = i + len g;
          len Sgm(A) = n by A20,FINSEQ_3:44; then
A38:      1 <= i & i <= n by A37,FINSEQ_3:27; then
A39:      1+len g <= m & m <= n+len g by AXIOMS:24; then
A40:      m in seq(len g,n) by CALCUL_2:1;
          i <= m by NAT_1:29; then
A41:      1 <= m & m <= len g + len IdFinS(p,n)
                                by A38,A39,AXIOMS:22,FINSEQ_2:69; then
          m <= len h by FINSEQ_1:35; then
          m in dom h by A41,FINSEQ_3:27; then
A42:      m in Seg len h by FINSEQ_1:def 3;
          a = Sgm(A).(m - len g) by A37; then
          a = F.m by A13,A40,A42;
          hence thesis by A13,A42,FUNCT_1:12;
        end;
        now assume not a in A; then
A43:      a in C by A34,XBOOLE_0:def 4;
          now let b such that A44: b in C;
              C c= dom f by XBOOLE_1:36; then
              b in dom f by A44;
              hence b in Seg len f by FINSEQ_1:def 3;
            end; then
          C c= Seg len f by TARSKI:def 3; then
          a in rng Sgm(C) by A43,FINSEQ_1:def 13; then
          consider i such that
A45:          i in dom Sgm(C) & Sgm(C).i = a by FINSEQ_2:11;
          len Sgm(C) = len g by A18,A19,FINSEQ_1:def 3; then
A46:      1 <= i & i <= len g by A45,FINSEQ_3:27; then
A47:      i in Seg len g by FINSEQ_1:3;
          len g <= len h by CALCUL_1:6; then
          i <= len h by A46,AXIOMS:22; then
A48:      i in Seg len h by A46,FINSEQ_1:3; then
          a = F.i by A13,A45,A47;
          hence thesis by A13,A48,FUNCT_1:12;
        end;
        hence thesis by A35;
      end; then
      reconsider F as Relation of B,B by A15,RELSET_1:11;
      B = {} implies B = {}; then
      reconsider F as Function of B,B by A15,FUNCT_2:def 1;
      F is one-to-one
      proof
        let a1,a2 such that A49: a1 in dom F & a2 in dom F & F.a1 = F.a2;
A50:    a1 in Seg len g \/ seq(len g,n) &
        a2 in Seg len g \/ seq(len g,n) by A5,A13,A49,Lm1;
A51:    now assume A52: a1 in Seg len g; then
A53:      F.a1 = Sgm(C).a1 by A13,A49;
A54:      a1 in dom Sgm(C) by A17,A19,A52,FINSEQ_3:45; then
          F.a1 in rng Sgm(C) by A53,FUNCT_1:12; then
A55:      F.a1 in C by A17,FINSEQ_1:def 13;
A56:      now assume A57: a2 in Seg len g; then
A58:        F.a2 = Sgm(C).a2 by A13,A49;
A59:        a2 in dom Sgm(C) by A17,A19,A57,FINSEQ_3:45;
A60:        Sgm(C).a1 = Sgm(C).a2 by A13,A49,A52,A58;
            Sgm(C) is one-to-one by A17,FINSEQ_3:99;
            hence a1 = a2 by A54,A59,A60,FUNCT_1:def 8;
          end;
          now assume A61: a2 in seq(len g,n); then
            reconsider i = a2 as Nat;
            i-len g in dom Sgm(A) by A21,A61; then
            Sgm(A).(i-len g) in rng Sgm(A) by FUNCT_1:12; then
            F.a2 in rng Sgm(A) by A13,A49,A61; then
            F.a2 in A by A20,FINSEQ_1:def 13;
            hence contradiction by A49,A55,XBOOLE_0:def 4;
          end;
          hence a1 = a2 by A50,A56,XBOOLE_0:def 2;
        end;
        now assume A62: a1 in seq(len g,n); then
          reconsider i = a1 as Nat;
A63:      i-len g in dom Sgm(A) by A21,A62; then
          Sgm(A).(i-len g) in rng Sgm(A) by FUNCT_1:12; then
          F.a1 in rng Sgm(A) by A13,A49,A62; then
A64:      F.a1 in A by A20,FINSEQ_1:def 13;
A65:      now assume A66: a2 in seq(len g,n); then
            reconsider i1 = a2 as Nat;
A67:        F.a2 = Sgm(A).(i1-len g) by A13,A49,A66;
A68:        i1-len g in dom Sgm(A) by A21,A66;
A69:        Sgm(A).(i1-len g) = Sgm(A).(i-len g) by A13,A49,A62,A67;
            Sgm(A) is one-to-one by A20,FINSEQ_3:99; then
            i1-len g+len g = i-len g+len g by A63,A68,A69,FUNCT_1:def 8; then
            i1 = i-len g+len g by XCMPLX_1:27;
            hence a1 = a2;
          end;
          now assume A70: a2 in Seg len g; then
A71:        F.a2 = Sgm(C).a2 by A13,A49;
            a2 in dom Sgm(C) by A17,A19,A70,FINSEQ_3:45; then
            F.a2 in rng Sgm(C) by A71,FUNCT_1:12; then
            F.a2 in C by A17,FINSEQ_1:def 13;
            hence contradiction by A49,A64,XBOOLE_0:def 4;
          end;
          hence a1 = a2 by A50,A65,XBOOLE_0:def 2;
        end;
        hence thesis by A50,A51,XBOOLE_0:def 2;
      end; then
      reconsider F as Permutation of dom f by A25,FUNCT_2:83;
A72:  Per(f,F) = h
      proof
A73:   dom Per(f,F) = B by CALCUL_2:19
                  .= dom F by A13,A14,FINSEQ_1:def 3
                  .= dom h by A13,FINSEQ_1:def 3;
        now let k such that A74: k in dom h;
A75:      k in Seg len h by A74,FINSEQ_1:def 3;
A76:      now assume A77: k in dom g; then
A78:        k in dom Sgm(C) by A18,A19,FINSEQ_1:def 3;
            k in Seg len g by A77,FINSEQ_1:def 3; then
A79:        F.k = Sgm(C).k by A13,A75;
            k in dom (F*f) by A73,A74,CALCUL_2:def 2; then
            (F*f).k = f.(Sgm(C).k) by A79,FUNCT_1:22;
            hence Per(f,F).k = f.(Sgm(C).k) by CALCUL_2:def 2
                           .= (Sgm(C)*f).k by A78,FUNCT_1:23
                           .= g.k by FINSEQ_3:def 1
                           .= h.k by A77,FINSEQ_1:def 7;
          end;
          now given i such that A80: i in dom IdFinS(p,n) & k = len g + i;
            len IdFinS(p,n) = n by FINSEQ_2:69; then
A81:        1 <= i & i <= n by A80,FINSEQ_3:27; then
            1+len g <= k & k <= n+len g by A80,AXIOMS:24; then
A82:       k in seq(len g,n) by CALCUL_2:1; then
A83:       k - len g in dom Sgm(A) by A21;
            F.k = Sgm(A).(k-len g) by A13,A75,A82; then
            F.k in rng Sgm(A) by A83,FUNCT_1:12; then
            F.k in A by A20,FINSEQ_1:def 13; then
            f.(F.k) in {p} by FUNCT_1:def 13; then
A84:       f.(F.k) = p by TARSKI:def 1;
            k in dom (F*f) by A73,A74,CALCUL_2:def 2; then
            (F*f).k = p by A84,FUNCT_1:22; then
A85:       Per(f,F).k = p by CALCUL_2:def 2;
            i in Seg n by A81,FINSEQ_1:3;
            hence Per(f,F).k = IdFinS(p,n).i by A85,FINSEQ_2:70
                           .= h.k by A80,FINSEQ_1:def 7;
          end;
          hence Per(f,F).k = h.k by A74,A76,FINSEQ_1:38;
        end;
        hence thesis by A73,FINSEQ_1:17;
      end; then
        reconsider h as FinSequence of CQC-WFF;
A86:   |- h^<*q*> by A1,A72,CALCUL_2:30;
        a in X or a in {p} by A1,A4,XBOOLE_0:def 2; then
        a = p by A4,TARSKI:def 1; then
        consider i such that
A87:        i in B & f.i = p by A4,FINSEQ_2:11;
        consider j such that
A88:        j in dom F & F.j = i by A25,A87,FINSEQ_2:11;
        (F*f).j = p by A87,A88,FUNCT_1:23; then
A89:   h.j = p by A72,CALCUL_2:def 2;
A90:   now assume A91: j in dom g; then
          g.j = p by A89,FINSEQ_1:def 7; then
A92:     g.j in {p} & g.j in rng g by A91,FUNCT_1:12,TARSKI:def 1;
          rng g = rng f \ {p} by FINSEQ_3:72;
          hence contradiction by A92,XBOOLE_0:def 4;
        end;
        j in dom f by A88; then
        j in dom h by A72,CALCUL_2:19; then
        consider k such that
A93:   k in dom IdFinS(p,n) & j = len g + k by A90,FINSEQ_1:38;
        reconsider g as FinSequence of CQC-WFF by FINSEQ_3:93;
        take g;
        1 <= k & k <= len IdFinS(p,n) by A93,FINSEQ_3:27; then
        1 <= len IdFinS(p,n) by AXIOMS:22; then
        1 <= n by FINSEQ_2:69; then
A94:   |- g^<*p*>^<*q*> by A86,CALCUL_2:32;
A95:   rng g = rng f \ {p} by FINSEQ_3:72;
        rng f \ {p} c= (X \/ {p}) \ {p} by A1,XBOOLE_1:33; then
A96:   rng g c= X \ {p} by A95,XBOOLE_1:40;
        X \ {p} c= X by XBOOLE_1:36; then
        rng g c= X by A96,XBOOLE_1:1;
        hence thesis by A94;
      end;
      hence thesis by A2;
    end;

:: Corresponds to Ebb et al, Chapter IV, Lemma 7.6 (a)

theorem
  X |- p iff X \/ {'not' p} is Inconsistent
  proof
    thus X |- p implies X \/ {'not' p} is Inconsistent
    proof
      assume X |- p; then
      consider f such that A1: rng f c= X & |- f^<*p*> by Def2;
A2:   |- f^<*'not' p*>^<*p*> by A1,Th5;
      set f1 = f^<*'not' p*>^<*'not' p*>;
A3:   Ant(f1) = f^<*'not' p*> & Suc(f1) = 'not' p by CALCUL_1:5; then
A4:   (Ant(f1)).(len f+1) = Suc(f1) by FINSEQ_1:59;
      1 in Seg 1 by FINSEQ_1:3; then
      1 in dom <*'not' p*> by FINSEQ_1:55; then
      len f+1 in dom (Ant(f1)) by A3,FINSEQ_1:41; then
      Suc(f1) is_tail_of Ant(f1) by A4,CALCUL_1:def 3; then
      |- f1 by CALCUL_1:33; then
      not f^<*'not' p*> is Consistent by A2,Def4; then
A5:   not X \/ {'not' p} is Consistent or
      not rng (f^<*'not' p*>) c= X \/ {'not' p} by Th4;
      set f2 = f^<*'not' p*>;
A6:   Ant(f2) = f & Suc(f2) = 'not' p by CALCUL_1:5;
      0+1 <= len f2 by CALCUL_1:10; then
      0 < len f2; then
      rng f2 = rng f \/ {'not' p} by A6,CALCUL_1:3;
      hence thesis by A1,A5,XBOOLE_1:9;
    end;
    thus X \/ {'not' p} is Inconsistent implies X |- p
    proof
      assume X \/ {'not' p} is Inconsistent; then
A7:   X \/ {'not' p} |- p by Th6;
      consider f such that A8: rng f c= X \/ {'not' p} & |- f^<*p*> by A7,Def2;
      now assume not rng f c= X; then
        consider a such that
A9:    a in rng f & not a in X by TARSKI:def 3;
        reconsider B = dom f as finite set;
        reconsider A = f"{'not' p} as finite set;
        set n = card A;
        set g = f - {'not' p};
        set h = g^IdFinS('not' p,n);
A10:   len IdFinS('not' p,n) = n by FINSEQ_2:69; then
A11:    Seg len h = Seg len g \/ seq(len g,n) by Lm1;
        defpred P[Nat,set] means
         ($1 in Seg len g implies $2 = Sgm(B \ A).$1) &
         ($1 in seq(len g,n) implies $2 = Sgm(A).($1 - len g));
A12:     for k,b1,b2 st k in Seg len h & P[k,b1] & P[k,b2] holds b1=b2
                                                   by A11,XBOOLE_0:def 2;
A13:  for k st k in Seg len h ex a st P[k,a]
      proof
        let k such that k in Seg len h;
A14:    now assume A15: k in Seg len g;
A16:     Seg len g misses seq(len g,n) by Lm2;
A17:      not k in seq(len g,n) by A15,A16,XBOOLE_0:3;
          take a = Sgm(B\A).k;
          thus P[k,a] by A17;
        end;
        thus thesis by A14;
      end;
      consider F being FinSequence such that
A18:   dom F = Seg len h & for k st k in Seg len h holds P[k,F.k]
                                             from FINSEQ_1:sch 1(A12,A13);
A19: len h = len g + len IdFinS('not' p,n) by FINSEQ_1:35
            .= len f - n + len IdFinS('not' p,n) by FINSEQ_3:66
            .= len f - n + n by FINSEQ_2:69
            .= len f; then
A20:  dom F = B by A18,FINSEQ_1:def 3;
      reconsider C = B \ A as finite set;
      now let b such that A21: b in C;
        C c= dom f by XBOOLE_1:36; then
        b in dom f by A21;
        hence b in Seg len f by FINSEQ_1:def 3;
      end; then
A22:  C c= Seg len f by TARSKI:def 3; then
A23:  dom Sgm(C) = Seg card C by FINSEQ_3:45;
      A c= B by RELAT_1:167; then
A24: card C = card B - n by CARD_2:63
            .= card Seg len f - n by FINSEQ_1:def 3
            .= len f - n by FINSEQ_1:78
            .= len g by FINSEQ_3:66;
      A c= B by RELAT_1:167; then
A25:  A c= Seg len f by FINSEQ_1:def 3;
A26:  now let i; assume A27: i in seq(len g,n);
        reconsider j = i-len g as Integer;
A28:    1+len g <= i & i <= n+len g by A27,CALCUL_2:1; then
A29:    1 <= j & j <= n by REAL_1:84,86;
        0 <= j by A28,REAL_1:84; then
        reconsider j as Nat by INT_1:16;
        j in Seg n by A29,FINSEQ_1:3;
        hence i-len g in dom Sgm(A) by A25,FINSEQ_3:45;
      end;
A30:  rng F = B
      proof
        now let a such that A31: a in rng F;
          consider i such that A32: i in dom F & F.i = a by A31,FINSEQ_2:11;
A33:      i in Seg len g \/ seq(len g,n) by A10,A18,A32,Lm1;
A34:      now assume A35: i in Seg len g; then
A36:        a = Sgm(C).i by A18,A32;
            i in dom Sgm(C) by A22,A24,A35,FINSEQ_3:45; then
            a in rng Sgm(C) by A36,FUNCT_1:12; then
            a in C by A22,FINSEQ_1:def 13;
            hence a in B by XBOOLE_0:def 4;
          end;
          now assume i in seq(len g,n); then
A37:        i-len g in dom Sgm(A) & a = Sgm(A).(i-len g) by A18,A26,A32;
            rng Sgm(A) = A by A25,FINSEQ_1:def 13; then
A38:        a in A by A37,FUNCT_1:12;
            A c= B by RELAT_1:167;
            hence a in B by A38;
          end;
          hence a in B by A33,A34,XBOOLE_0:def 2;
        end;
        hence rng F c= B by TARSKI:def 3;
        let a such that A39: a in B;
A40:    now assume A41: a in A;
          rng Sgm(A) = A by A25,FINSEQ_1:def 13; then
          consider i such that
A42:      i in dom Sgm(A) & Sgm(A).i = a by A41,FINSEQ_2:11;
          set m = i + len g;
          len Sgm(A) = n by A25,FINSEQ_3:44; then
A43:      1 <= i & i <= n by A42,FINSEQ_3:27; then
A44:      1+len g <= m & m <= n+len g by AXIOMS:24; then
A45:      m in seq(len g,n) by CALCUL_2:1;
          i <= m by NAT_1:29; then
A46:      1 <= m & m <= len g + len IdFinS('not' p,n)
                                by A43,A44,AXIOMS:22,FINSEQ_2:69; then
          m <= len h by FINSEQ_1:35; then
          m in dom h by A46,FINSEQ_3:27; then
A47:      m in Seg len h by FINSEQ_1:def 3;
          a = Sgm(A).(m - len g) by A42; then
          a = F.m by A18,A45,A47;
          hence thesis by A18,A47,FUNCT_1:12;
        end;
        now assume not a in A; then
A48:      a in C by A39,XBOOLE_0:def 4;
          now let b such that A49: b in C;
              C c= dom f by XBOOLE_1:36; then
              b in dom f by A49;
              hence b in Seg len f by FINSEQ_1:def 3;
            end; then
          C c= Seg len f by TARSKI:def 3; then
          a in rng Sgm(C) by A48,FINSEQ_1:def 13; then
          consider i such that
A50:          i in dom Sgm(C) & Sgm(C).i = a by FINSEQ_2:11;
          len Sgm(C) = len g by A23,A24,FINSEQ_1:def 3; then
A51:      1 <= i & i <= len g by A50,FINSEQ_3:27; then
A52:      i in Seg len g by FINSEQ_1:3;
          len g <= len h by CALCUL_1:6; then
          i <= len h by A51,AXIOMS:22; then
A53:      i in Seg len h by A51,FINSEQ_1:3; then
          a = F.i by A18,A50,A52;
          hence thesis by A18,A53,FUNCT_1:12;
        end;
        hence thesis by A40;
      end; then
      reconsider F as Relation of B,B by A20,RELSET_1:11;
      B = {} implies B = {}; then
      reconsider F as Function of B,B by A20,FUNCT_2:def 1;
      F is one-to-one
      proof
        let a1,a2 such that A54: a1 in dom F & a2 in dom F & F.a1 = F.a2;
A55:    a1 in Seg len g \/ seq(len g,n) &
        a2 in Seg len g \/ seq(len g,n) by A10,A18,A54,Lm1;
A56:    now assume A57: a1 in Seg len g; then
A58:      F.a1 = Sgm(C).a1 by A18,A54;
A59:      a1 in dom Sgm(C) by A22,A24,A57,FINSEQ_3:45; then
          F.a1 in rng Sgm(C) by A58,FUNCT_1:12; then
A60:      F.a1 in C by A22,FINSEQ_1:def 13;
A61:      now assume A62: a2 in Seg len g; then
A63:        F.a2 = Sgm(C).a2 by A18,A54;
A64:        a2 in dom Sgm(C) by A22,A24,A62,FINSEQ_3:45;
A65:        Sgm(C).a1 = Sgm(C).a2 by A18,A54,A57,A63;
            Sgm(C) is one-to-one by A22,FINSEQ_3:99;
            hence a1 = a2 by A59,A64,A65,FUNCT_1:def 8;
          end;
          now assume A66: a2 in seq(len g,n); then
            reconsider i = a2 as Nat;
            i-len g in dom Sgm(A) by A26,A66; then
            Sgm(A).(i-len g) in rng Sgm(A) by FUNCT_1:12; then
            F.a2 in rng Sgm(A) by A18,A54,A66; then
            F.a2 in A by A25,FINSEQ_1:def 13;
            hence contradiction by A54,A60,XBOOLE_0:def 4;
          end;
          hence a1 = a2 by A55,A61,XBOOLE_0:def 2;
        end;
        now assume A67: a1 in seq(len g,n); then
          reconsider i = a1 as Nat;
A68:      i-len g in dom Sgm(A) by A26,A67; then
          Sgm(A).(i-len g) in rng Sgm(A) by FUNCT_1:12; then
          F.a1 in rng Sgm(A) by A18,A54,A67; then
A69:      F.a1 in A by A25,FINSEQ_1:def 13;
A70:      now assume A71: a2 in seq(len g,n); then
            reconsider i1 = a2 as Nat;
A72:        F.a2 = Sgm(A).(i1-len g) by A18,A54,A71;
A73:        i1-len g in dom Sgm(A) by A26,A71;
A74:        Sgm(A).(i1-len g) = Sgm(A).(i-len g) by A18,A54,A67,A72;
            Sgm(A) is one-to-one by A25,FINSEQ_3:99; then
            i1-len g = i-len g by A68,A73,A74,FUNCT_1:def 8; then
            i1 = i-len g+len g by XCMPLX_1:27;
            hence a1 = a2;
          end;
          now assume A75: a2 in Seg len g; then
A76:        F.a2 = Sgm(C).a2 by A18,A54;
            a2 in dom Sgm(C) by A22,A24,A75,FINSEQ_3:45; then
            F.a2 in rng Sgm(C) by A76,FUNCT_1:12; then
            F.a2 in C by A22,FINSEQ_1:def 13;
            hence contradiction by A54,A69,XBOOLE_0:def 4;
          end;
          hence a1 = a2 by A55,A70,XBOOLE_0:def 2;
        end;
        hence thesis by A55,A56,XBOOLE_0:def 2;
      end; then
      reconsider F as Permutation of dom f by A30,FUNCT_2:83;
A77:  Per(f,F) = h
      proof
A78:   dom Per(f,F) = B by CALCUL_2:19
                  .= dom F by A18,A19,FINSEQ_1:def 3
                  .= dom h by A18,FINSEQ_1:def 3;
        now let k such that A79: k in dom h;
A80:      k in Seg len h by A79,FINSEQ_1:def 3;
A81:      now assume A82: k in dom g; then
A83:        k in dom Sgm(C) by A23,A24,FINSEQ_1:def 3;
            k in Seg len g by A82,FINSEQ_1:def 3; then
A84:        F.k = Sgm(C).k by A18,A80;
            k in dom (F*f) by A78,A79,CALCUL_2:def 2; then
            (F*f).k = f.(Sgm(C).k) by A84,FUNCT_1:22;
            hence Per(f,F).k = f.(Sgm(C).k) by CALCUL_2:def 2
                           .= (Sgm(C)*f).k by A83,FUNCT_1:23
                           .= g.k by FINSEQ_3:def 1
                           .= h.k by A82,FINSEQ_1:def 7;
          end;
          now given i such that
A85:      i in dom IdFinS('not' p,n) & k = len g + i;
            len IdFinS('not' p,n) = n by FINSEQ_2:69; then
A86:        1 <= i & i <= n by A85,FINSEQ_3:27; then
            1+len g <= k & k <= n+len g by A85,AXIOMS:24; then
A87:       k in seq(len g,n) by CALCUL_2:1; then
A88:       k - len g in dom Sgm(A) by A26;
            F.k = Sgm(A).(k-len g) by A18,A80,A87; then
            F.k in rng Sgm(A) by A88,FUNCT_1:12; then
            F.k in A by A25,FINSEQ_1:def 13; then
            f.(F.k) in {'not' p} by FUNCT_1:def 13; then
A89:       f.(F.k) = 'not' p by TARSKI:def 1;
            k in dom (F*f) by A78,A79,CALCUL_2:def 2; then
            (F*f).k = 'not' p by A89,FUNCT_1:22; then
A90:       Per(f,F).k = 'not' p by CALCUL_2:def 2;
            i in Seg n by A86,FINSEQ_1:3;
            hence Per(f,F).k = IdFinS('not' p,n).i by A90,FINSEQ_2:70
                           .= h.k by A85,FINSEQ_1:def 7;
          end;
          hence Per(f,F).k = h.k by A79,A81,FINSEQ_1:38;
        end;
        hence thesis by A78,FINSEQ_1:17;
      end; then
        reconsider h as FinSequence of CQC-WFF;
A91:   |- h^<*p*> by A8,A77,CALCUL_2:30;
        a in X or a in {'not' p} by A8,A9,XBOOLE_0:def 2; then
        a = 'not' p by A9,TARSKI:def 1; then
        consider i such that
A92:        i in B & f.i = 'not' p by A9,FINSEQ_2:11;
        consider j such that
A93:        j in dom F & F.j = i by A30,A92,FINSEQ_2:11;
        (F*f).j = 'not' p by A92,A93,FUNCT_1:23; then
A94:   h.j = 'not' p by A77,CALCUL_2:def 2;
A95:   now assume A96: j in dom g; then
          g.j = 'not' p by A94,FINSEQ_1:def 7; then
A97:     g.j in {'not' p} & g.j in rng g by A96,FUNCT_1:12,TARSKI:def 1;
          rng g = rng f \ {'not' p} by FINSEQ_3:72;
          hence contradiction by A97,XBOOLE_0:def 4;
        end;
        j in dom f by A93; then
        j in dom h by A77,CALCUL_2:19; then
        consider k such that
A98:      k in dom IdFinS('not' p,n) & j = len g + k by A95,FINSEQ_1:38;
        reconsider g as FinSequence of CQC-WFF by FINSEQ_3:93;
        1 <= k & k <= len IdFinS('not' p,n) by A98,FINSEQ_3:27; then
        1 <= len IdFinS('not' p,n) by AXIOMS:22; then
        1 <= n by FINSEQ_2:69; then
A99:   |- g^<*'not' p*>^<*p*> by A91,CALCUL_2:32;
        |- g^<*p*>^<*p*> by CALCUL_2:21; then
A100:   |- g^<*p*> by A99,CALCUL_2:26;
A101:   rng g = rng f \ {'not' p} by FINSEQ_3:72;
        rng f \ {'not' p} c= (X \/ {'not' p}) \ {'not' p}
          by A8,XBOOLE_1:33; then
A102:   rng g c= X \ {'not' p} by A101,XBOOLE_1:40;
        X \ {'not' p} c= X by XBOOLE_1:36; then
        rng g c= X by A102,XBOOLE_1:1;
        hence thesis by A100,Def2;
      end;
      hence thesis by A8,Def2;
    end;
  end;

:: Similar to Ebb et al, Chapter IV, Lemma 7.6 (a)

theorem
  X |- 'not' p iff X \/ {p} is Inconsistent
  proof
    thus X |- 'not' p implies X \/ {p} is Inconsistent
    proof
      assume X |- 'not' p; then
      consider f such that A1: rng f c= X & |- f^<*'not' p*> by Def2;
A2:   |- f^<*p*>^<*'not' p*> by A1,Th5;
      set f1 = f^<*p*>^<*p*>;
A3:   Ant(f1) = f^<*p*> & Suc(f1) = p by CALCUL_1:5; then
A4:   (Ant(f1)).(len f+1) = Suc(f1) by FINSEQ_1:59;
      1 in Seg 1 by FINSEQ_1:3; then
      1 in dom <*p*> by FINSEQ_1:55; then
      len f+1 in dom (Ant(f1)) by A3,FINSEQ_1:41; then
      Suc(f1) is_tail_of Ant(f1) by A4,CALCUL_1:def 3; then
      |- f1 by CALCUL_1:33; then
      not f^<*p*> is Consistent by A2,Def4; then
A5:   not X \/ {p} is Consistent or
      not rng (f^<*p*>) c= X \/ {p} by Th4;
      set f2 = f^<*p*>;
A6:   Ant(f2) = f & Suc(f2) = p by CALCUL_1:5;
      0+1 <= len f2 by CALCUL_1:10; then
      0 < len f2; then
      rng f2 = rng f \/ {p} by A6,CALCUL_1:3;
      hence thesis by A1,A5,XBOOLE_1:9;
    end;
    thus X \/ {p} is Inconsistent implies X |- 'not' p
    proof
      assume X \/ {p} is Inconsistent; then
      X \/ {p} |- 'not' p by Th6; then
      consider f such that A7: rng f c= X \/ {p} & |- f^<*'not' p*> by Def2;
      now assume not rng f c= X; then
        consider a such that
A8:    a in rng f & not a in X by TARSKI:def 3;
        reconsider B = dom f as finite set;
        reconsider A = f"{p} as finite set;
        set n = card A;
        set g = f - {p};
        set h = g^IdFinS(p,n);
A9:   len IdFinS(p,n) = n by FINSEQ_2:69; then
A10:    Seg len h = Seg len g \/ seq(len g,n) by Lm1;
        defpred P[Nat,set] means
         ($1 in Seg len g implies $2 = Sgm(B \ A).$1) &
         ($1 in seq(len g,n) implies $2 = Sgm(A).($1 - len g));
A11:     for k,b1,b2 st k in Seg len h & P[k,b1] & P[k,b2] holds b1=b2
                                                by A10,XBOOLE_0:def 2;
A12:  for k st k in Seg len h ex a st P[k,a]
      proof
        let k such that k in Seg len h;
A13:    now assume A14: k in Seg len g;
A15:     Seg len g misses seq(len g,n) by Lm2;
A16:      not k in seq(len g,n) by A14,A15,XBOOLE_0:3;
          take a = Sgm(B\A).k;
          thus P[k,a] by A16;
        end;
        thus thesis by A13;
      end;
      consider F being FinSequence such that
A17:   dom F = Seg len h & for k st k in Seg len h holds P[k,F.k]
                                             from FINSEQ_1:sch 1(A11,A12);
A18:  len h = len g + len IdFinS(p,n) by FINSEQ_1:35
            .= len f - n + len IdFinS(p,n) by FINSEQ_3:66
            .= len f - n + n by FINSEQ_2:69
            .= len f; then
A19:  dom F = B by A17,FINSEQ_1:def 3;
      reconsider C = B \ A as finite set;
      now let b such that A20: b in C;
        C c= dom f by XBOOLE_1:36; then
        b in dom f by A20;
        hence b in Seg len f by FINSEQ_1:def 3;
      end; then
A21:  C c= Seg len f by TARSKI:def 3; then
A22:  dom Sgm(C) = Seg card C by FINSEQ_3:45;
      A c= B by RELAT_1:167; then
A23: card C = card B - n by CARD_2:63
            .= card Seg len f - n by FINSEQ_1:def 3
            .= len f - n by FINSEQ_1:78
            .= len g by FINSEQ_3:66;
      A c= B by RELAT_1:167; then
A24:  A c= Seg len f by FINSEQ_1:def 3;
A25:  now let i; assume A26: i in seq(len g,n);
        reconsider j = i-len g as Integer;
A27:    1+len g <= i & i <= n+len g by A26,CALCUL_2:1; then
A28:    1 <= j & j <= n by REAL_1:84,86;
        0 <= j by A27,REAL_1:84; then
        reconsider j as Nat by INT_1:16;
        j in Seg n by A28,FINSEQ_1:3;
        hence i-len g in dom Sgm(A) by A24,FINSEQ_3:45;
      end;
A29:  rng F = B
      proof
        now let a such that A30: a in rng F;
          consider i such that A31: i in dom F & F.i = a by A30,FINSEQ_2:11;
A32:      i in Seg len g \/ seq(len g,n) by A9,A17,A31,Lm1;
A33:      now assume A34: i in Seg len g; then
A35:        a = Sgm(C).i by A17,A31;
            i in dom Sgm(C) by A21,A23,A34,FINSEQ_3:45; then
            a in rng Sgm(C) by A35,FUNCT_1:12; then
            a in C by A21,FINSEQ_1:def 13;
            hence a in B by XBOOLE_0:def 4;
          end;
          now assume i in seq(len g,n); then
A36:        i-len g in dom Sgm(A) & a = Sgm(A).(i-len g) by A17,A25,A31;
            rng Sgm(A) = A by A24,FINSEQ_1:def 13; then
A37:        a in A by A36,FUNCT_1:12;
            A c= B by RELAT_1:167;
            hence a in B by A37;
          end;
          hence a in B by A32,A33,XBOOLE_0:def 2;
        end;
        hence rng F c= B by TARSKI:def 3;
        let a such that A38: a in B;
A39:    now assume A40: a in A;
          rng Sgm(A) = A by A24,FINSEQ_1:def 13; then
          consider i such that
A41:      i in dom Sgm(A) & Sgm(A).i = a by A40,FINSEQ_2:11;
          set m = i + len g;
          len Sgm(A) = n by A24,FINSEQ_3:44; then
A42:      1 <= i & i <= n by A41,FINSEQ_3:27; then
A43:      1+len g <= m & m <= n+len g by AXIOMS:24; then
A44:      m in seq(len g,n) by CALCUL_2:1;
          i <= m by NAT_1:29; then
A45:      1 <= m & m <= len g + len IdFinS(p,n)
                                by A42,A43,AXIOMS:22,FINSEQ_2:69; then
          m <= len h by FINSEQ_1:35; then
          m in dom h by A45,FINSEQ_3:27; then
A46:      m in Seg len h by FINSEQ_1:def 3;
          a = Sgm(A).(m - len g) by A41; then
          a = F.m by A17,A44,A46;
          hence thesis by A17,A46,FUNCT_1:12;
        end;
        now assume not a in A; then
A47:      a in C by A38,XBOOLE_0:def 4;
          now let b such that A48: b in C;
              C c= dom f by XBOOLE_1:36; then
              b in dom f by A48;
              hence b in Seg len f by FINSEQ_1:def 3;
            end; then
          C c= Seg len f by TARSKI:def 3; then
          a in rng Sgm(C) by A47,FINSEQ_1:def 13; then
          consider i such that
A49:          i in dom Sgm(C) & Sgm(C).i = a by FINSEQ_2:11;
          len Sgm(C) = len g by A22,A23,FINSEQ_1:def 3; then
A50:      1 <= i & i <= len g by A49,FINSEQ_3:27; then
A51:      i in Seg len g by FINSEQ_1:3;
          len g <= len h by CALCUL_1:6; then
          i <= len h by A50,AXIOMS:22; then
A52:      i in Seg len h by A50,FINSEQ_1:3; then
          a = F.i by A17,A49,A51;
          hence thesis by A17,A52,FUNCT_1:12;
        end;
        hence thesis by A39;
      end; then
      reconsider F as Relation of B,B by A19,RELSET_1:11;
      B = {} implies B = {}; then
      reconsider F as Function of B,B by A19,FUNCT_2:def 1;
      F is one-to-one
      proof
        let a1,a2 such that A53: a1 in dom F & a2 in dom F & F.a1 = F.a2;
A54:    a1 in Seg len g \/ seq(len g,n) &
        a2 in Seg len g \/ seq(len g,n) by A9,A17,A53,Lm1;
A55:    now assume A56: a1 in Seg len g; then
A57:      F.a1 = Sgm(C).a1 by A17,A53;
A58:      a1 in dom Sgm(C) by A21,A23,A56,FINSEQ_3:45; then
          F.a1 in rng Sgm(C) by A57,FUNCT_1:12; then
A59:      F.a1 in C by A21,FINSEQ_1:def 13;
A60:      now assume A61: a2 in Seg len g; then
A62:        F.a2 = Sgm(C).a2 by A17,A53;
A63:        a2 in dom Sgm(C) by A21,A23,A61,FINSEQ_3:45;
A64:        Sgm(C).a1 = Sgm(C).a2 by A17,A53,A56,A62;
            Sgm(C) is one-to-one by A21,FINSEQ_3:99;
            hence a1 = a2 by A58,A63,A64,FUNCT_1:def 8;
          end;
          now assume A65: a2 in seq(len g,n); then
            reconsider i = a2 as Nat;
            i-len g in dom Sgm(A) by A25,A65; then
            Sgm(A).(i-len g) in rng Sgm(A) by FUNCT_1:12; then
            F.a2 in rng Sgm(A) by A17,A53,A65; then
            F.a2 in A by A24,FINSEQ_1:def 13;
            hence contradiction by A53,A59,XBOOLE_0:def 4;
          end;
          hence a1 = a2 by A54,A60,XBOOLE_0:def 2;
        end;
        now assume A66: a1 in seq(len g,n); then
          reconsider i = a1 as Nat;
A67:      i-len g in dom Sgm(A) by A25,A66; then
          Sgm(A).(i-len g) in rng Sgm(A) by FUNCT_1:12; then
          F.a1 in rng Sgm(A) by A17,A53,A66; then
A68:      F.a1 in A by A24,FINSEQ_1:def 13;
A69:      now assume A70: a2 in seq(len g,n); then
            reconsider i1 = a2 as Nat;
A71:        F.a2 = Sgm(A).(i1-len g) by A17,A53,A70;
A72:        i1-len g in dom Sgm(A) by A25,A70;
A73:        Sgm(A).(i1-len g) = Sgm(A).(i-len g) by A17,A53,A66,A71;
            Sgm(A) is one-to-one by A24,FINSEQ_3:99; then
            i1-len g+len g = i-len g+len g by A67,A72,A73,FUNCT_1:def 8; then
            i1 = i-len g+len g by XCMPLX_1:27;
            hence a1 = a2;
          end;
          now assume A74: a2 in Seg len g; then
A75:        F.a2 = Sgm(C).a2 by A17,A53;
            a2 in dom Sgm(C) by A21,A23,A74,FINSEQ_3:45; then
            F.a2 in rng Sgm(C) by A75,FUNCT_1:12; then
            F.a2 in C by A21,FINSEQ_1:def 13;
            hence contradiction by A53,A68,XBOOLE_0:def 4;
          end;
          hence a1 = a2 by A54,A69,XBOOLE_0:def 2;
        end;
        hence thesis by A54,A55,XBOOLE_0:def 2;
      end; then
      reconsider F as Permutation of dom f by A29,FUNCT_2:83;
A76:  Per(f,F) = h
      proof
A77:   dom Per(f,F) = B by CALCUL_2:19
                  .= dom F by A17,A18,FINSEQ_1:def 3
                  .= dom h by A17,FINSEQ_1:def 3;
        now let k such that A78: k in dom h;
A79:      k in Seg len h by A78,FINSEQ_1:def 3;
A80:      now assume A81: k in dom g; then
A82:        k in dom Sgm(C) by A22,A23,FINSEQ_1:def 3;
            k in Seg len g by A81,FINSEQ_1:def 3; then
A83:        F.k = Sgm(C).k by A17,A79;
            k in dom (F*f) by A77,A78,CALCUL_2:def 2; then
            (F*f).k = f.(Sgm(C).k) by A83,FUNCT_1:22;
            hence Per(f,F).k = f.(Sgm(C).k) by CALCUL_2:def 2
                           .= (Sgm(C)*f).k by A82,FUNCT_1:23
                           .= g.k by FINSEQ_3:def 1
                           .= h.k by A81,FINSEQ_1:def 7;
          end;
          now given i such that A84: i in dom IdFinS(p,n) & k = len g + i;
            len IdFinS(p,n) = n by FINSEQ_2:69; then
A85:        1 <= i & i <= n by A84,FINSEQ_3:27; then
            1+len g <= k & k <= n+len g by A84,AXIOMS:24; then
A86:       k in seq(len g,n) by CALCUL_2:1; then
A87:       k - len g in dom Sgm(A) by A25;
            F.k = Sgm(A).(k-len g) by A17,A79,A86; then
            F.k in rng Sgm(A) by A87,FUNCT_1:12; then
            F.k in A by A24,FINSEQ_1:def 13; then
            f.(F.k) in {p} by FUNCT_1:def 13; then
A88:       f.(F.k) = p by TARSKI:def 1;
            k in dom (F*f) by A77,A78,CALCUL_2:def 2; then
            (F*f).k = p by A88,FUNCT_1:22; then
A89:       Per(f,F).k = p by CALCUL_2:def 2;
            i in Seg n by A85,FINSEQ_1:3;
            hence Per(f,F).k = IdFinS(p,n).i by A89,FINSEQ_2:70
                           .= h.k by A84,FINSEQ_1:def 7;
          end;
          hence Per(f,F).k = h.k by A78,A80,FINSEQ_1:38;
        end;
        hence thesis by A77,FINSEQ_1:17;
      end; then
        reconsider h as FinSequence of CQC-WFF;
A90:   |- h^<*'not' p*> by A7,A76,CALCUL_2:30;
        a in X or a in {p} by A7,A8,XBOOLE_0:def 2; then
        a = p by A8,TARSKI:def 1; then
        consider i such that
A91:   i in B & f.i = p by A8,FINSEQ_2:11;
        consider j such that
A92:   j in dom F & F.j = i by A29,A91,FINSEQ_2:11;
        (F*f).j = p by A91,A92,FUNCT_1:23; then
A93:   h.j = p by A76,CALCUL_2:def 2;
A94:   now assume A95: j in dom g; then
          g.j = p by A93,FINSEQ_1:def 7; then
A96:     g.j in {p} & g.j in rng g by A95,FUNCT_1:12,TARSKI:def 1;
          rng g = rng f \ {p} by FINSEQ_3:72;
          hence contradiction by A96,XBOOLE_0:def 4;
        end;
        j in dom f by A92; then
        j in dom h by A76,CALCUL_2:19; then
        consider k such that
A97:   k in dom IdFinS(p,n) & j = len g + k by A94,FINSEQ_1:38;
        reconsider g as FinSequence of CQC-WFF by FINSEQ_3:93;
        1 <= k & k <= len IdFinS(p,n) by A97,FINSEQ_3:27; then
        1 <= len IdFinS(p,n) by AXIOMS:22; then
        1 <= n by FINSEQ_2:69; then
A98:   |- g^<*p*>^<*'not' p*> by A90,CALCUL_2:32;
        |- g^<*'not' p*>^<*'not' p*> by CALCUL_2:21; then
A99:   |- g^<*'not' p*> by A98,CALCUL_2:26;
A100:   rng g = rng f \ {p} by FINSEQ_3:72;
        rng f \ {p} c= (X \/ {p}) \ {p} by A7,XBOOLE_1:33; then
A101:   rng g c= X \ {p} by A100,XBOOLE_1:40;
        X \ {p} c= X by XBOOLE_1:36; then
        rng g c= X by A101,XBOOLE_1:1;
        hence thesis by A99,Def2;
      end;
      hence thesis by A7,Def2;
    end;
  end;

begin :: Unions of Consistent Sets

:: Ebb et al, Chapter IV, Lemma 7.7

theorem
  for f being Function of NAT,bool CQC-WFF st
    (for n,m st m in dom f & n in dom f & n < m holds f.n is Consistent &
                    f.n c= f.m) holds (union rng f) is Consistent
   proof
     let f be Function of NAT,bool CQC-WFF;
     assume A1: for n,m st m in dom f & n in dom f & n < m holds
                f.n is Consistent & f.n c= f.m;
     now assume not union rng f is Consistent; then
       consider Z such that A2: Z c= union rng f & Z is finite &
                                       Z is Inconsistent by Th7;
       consider p such that A3: Z |- p & Z |- 'not' p by A2,Def3;
       for n,m st m in dom f & n in dom f & n < m holds f.n c= f.m by A1; then
       consider k such that A4: Z c= f.k by A2,Th3;
       reconsider Y = f.k as Subset of CQC-WFF;
       consider f1 such that
A5:    rng f1 c= Z & |- f1^<*p*> by A3,Def2;
      consider f2 such that
A6:   rng f2 c= Z & |- f2^<*'not' p*> by A3,Def2;
A7:   |- f1^f2^<*p*> & |- f1^f2^<*'not' p*> by A5,A6,Th5,CALCUL_2:20;
      rng(f1^f2) = rng f1 \/ rng f2 by FINSEQ_1:44; then
      rng(f1^f2) c= Z by A5,A6,XBOOLE_1:8; then
      rng(f1^f2) c= Y by A4,XBOOLE_1:1; then
      Y |- p & Y |- 'not' p by A7,Def2; then
A8:    not Y is Consistent by Def3;
A9:    for n st n in dom f holds f.n is Consistent
       proof
         let n such that A10: n in dom f;
         n+1 in NAT; then
         n < n+1 & n+1 in dom f by FUNCT_2:def 1,NAT_1:38;
         hence f.n is Consistent by A1,A10;
       end;
       k in NAT; then
       k in dom f by FUNCT_2:def 1;
       hence contradiction by A8,A9;
     end;
     hence thesis;
   end;

begin :: Construction of a Henkin Model

 reserve A for non empty set,
         v,w for Element of Valuations_in A,
         J,J' for interpretation of A;

theorem Th12:
  X is Inconsistent implies for J,v holds not J,v |= X
    proof
      assume not X is Consistent;
      then X |- 'not' VERUM by Th6; then
      consider f such that A1: rng f c= X & |- f^<*'not' VERUM*> by Def2;
      reconsider p = 'not' VERUM as Element of CQC-WFF;
      rng Ant(f^<*p*>) c= X & Suc(f^<*p*>) = p by A1,CALCUL_1:5; then
      p is_formal_provable_from X by A1,CALCUL_1:def 10; then
A2:   X |= p by CALCUL_1:32;
      let J,v;
      now assume J,v |= X; then
        J,v |= 'not' VERUM by A2,CALCUL_1:def 12; then
        not J,v |= VERUM by VALUAT_1:28;
        hence contradiction by VALUAT_1:44;
      end;
     hence thesis;
  end;

theorem Th13:
  {VERUM} is Consistent
 proof
   consider A,J,v;
    J,v |= VERUM by VALUAT_1:44; then
    for p st p in {VERUM} holds J,v |= p by TARSKI:def 1; then
     J,v |= {VERUM} by CALCUL_1:def 11;
     hence thesis by Th12;
  end;

registration
  cluster Consistent Subset of CQC-WFF;
  existence by Th13;
end;

 reserve CX for Consistent Subset of CQC-WFF,
         P',P1,P2 for Element of QC-pred_symbols;

definition
  func HCar -> non empty set equals :Def5:
    bound_QC-variables;
  coherence;
end;

definition let P be (Element of QC-pred_symbols),
               ll be CQC-variable_list of (the_arity_of P);
  redefine func P!ll -> Element of CQC-WFF;
  coherence
  proof
    set k = the_arity_of P;
    reconsider P' = P as QC-pred_symbol of k by QC_LANG3:3;
    P'!ll is Element of CQC-WFF;
    hence P!ll is Element of CQC-WFF;
  end;
end;

:: The Henkin Model (Ebb et al, Chapter V, 1)

definition let CX;
  mode Henkin_interpretation of CX -> interpretation of HCar means
:Def6: for P being (Element of QC-pred_symbols),
      r being Element of relations_on HCar st it.P = r holds
      for a holds a in r iff ex ll being CQC-variable_list of
          (the_arity_of P) st a = ll & CX |- P!ll;
  existence
  proof
    set A = QC-pred_symbols;
    defpred P[set,set] means ex P' st P' = $1 &
           $2 = {ll where ll is CQC-variable_list of (the_arity_of P') :
                 CX |- P'!ll};
A1: for a,b1,b2 st a in A & P[a,b1] & P[a,b2] holds b1 = b2;
A2: for a st a in A ex b st P[a,b]
    proof
      let a such that A3: a in A;
      reconsider a as Element of QC-pred_symbols by A3;
      consider b such that
A4:    b = {ll where ll is CQC-variable_list of (the_arity_of a) :
         CX |- a!ll};
      take b;
      take P' = a;
      thus thesis by A4;
    end;
    consider f being Function such that
A5:    dom f = A & for a st a in A holds P[a,f.a] from FUNCT_1:sch 2(A1,A2);
A6: for P being (Element of QC-pred_symbols),
      r being Element of relations_on HCar st f.P = r holds
      for a holds  a in r iff ex ll being CQC-variable_list of
          (the_arity_of P) st a = ll & CX |- P!ll
    proof
      let P be (Element of QC-pred_symbols),
      r be Element of relations_on HCar such that
A7:  f.P = r;
      let a;
      thus a in r implies ex ll being CQC-variable_list of
          (the_arity_of P) st a = ll & CX |- P!ll
      proof
        assume a in r;
        then A8: a in f.P by A7;
        consider P' such that A9: P' = P &
             f.P = {ll where ll is CQC-variable_list of (the_arity_of P') :
             CX |- P'!ll} by A5;
        thus thesis by A8,A9;
      end;
      thus (ex ll being CQC-variable_list of
          (the_arity_of P) st a = ll & CX |- P!ll) implies a in r
      proof
        given ll being CQC-variable_list of (the_arity_of P) such that
          A10: a = ll & CX |- P!ll;
           ex P' st P' = P &
           f.P = {l where l is CQC-variable_list of (the_arity_of P') :
           CX |- P'!l} by A5;
           hence thesis by A7,A10;
      end;
    end;
A11:  for P being (Element of QC-pred_symbols),
          r being Element of relations_on HCar st f.P = r holds
      r = empty_rel(HCar) or the_arity_of P = the_arity_of r
      proof
        let P be (Element of QC-pred_symbols),
            r be Element of relations_on HCar such that
A12:    f.P = r;
        assume A13: not r = empty_rel(HCar);
        then r <> {} by MARGREL1:32;
        then consider a such that A14: a in r by XBOOLE_0:def 1;
        consider P' such that A15: P' = P &
        f.P = {ll where ll is CQC-variable_list of (the_arity_of P') :
        CX |- P'!ll} by A5;
        consider ll' being  CQC-variable_list of (the_arity_of P')
        such that A16: a = ll' & CX |- P'!ll' by A12,A14,A15;
        rng ll' c= bound_QC-variables by CQC_LANG:def 5;
        then reconsider a as FinSequence of HCar by A16,Def5,FINSEQ_1:def 4;
        the_arity_of r = len a by A13,A14,MARGREL1:def 11;
        hence thesis by A15,A16,QC_LANG1:def 8;
      end;
      b in rng f implies b in relations_on HCar
      proof
        assume b in rng f;
        then consider a such that A17: a in dom f & b = f.a by FUNCT_1:def 5;
        consider P' such that A18: P' = a &
        f.a = {ll where ll is CQC-variable_list of (the_arity_of P') :
         CX |- P'!ll} by A5,A17;
         c in b implies c in HCar*
         proof
           assume c in b;
           then consider ll being CQC-variable_list of (the_arity_of P')
            such that A19: c = ll & CX |- P'!ll by A17,A18;
            rng ll c= bound_QC-variables by CQC_LANG:def 5;
            then ll is FinSequence of HCar by Def5,FINSEQ_1:def 4;
            hence c in HCar* by A19,FINSEQ_1:def 11;
         end; then
A20:     b c= HCar* by TARSKI:def 3;
         for fin,fin' being FinSequence of HCar st fin in b & fin' in b
         holds len fin = len fin'
         proof
           let fin,fin' be FinSequence of HCar;
           assume A21: fin in b & fin' in b;
           then consider ll being  CQC-variable_list of (the_arity_of P')
            such that A22: fin = ll & CX |- P'!ll by A17,A18;
           consider ll' being  CQC-variable_list of (the_arity_of P')
            such that A23: fin' = ll' & CX |- P'!ll' by A17,A18,A21;
           len fin = the_arity_of P' by A22,QC_LANG1:def 8;
           hence thesis by A23,QC_LANG1:def 8;
         end;
        hence thesis by A20,MARGREL1:def 8;
      end;
      then rng f c= relations_on HCar by TARSKI:def 3;
      then reconsider f as Function of A,relations_on HCar by A5,FUNCT_2:4;
      reconsider f as interpretation of HCar by A11,VALUAT_1:def 10;
      take f;
      thus thesis by A6;
    end;
end;

definition
  func valH -> Element of Valuations_in HCar equals
  :Def7: id bound_QC-variables;
  coherence by Def5,VALUAT_1:def 1;
end;

begin :: Some Properties of the Henkin Model

 reserve JH for Henkin_interpretation of CX,
         rel for Element of relations_on HCar;

theorem Th14:
  valH*'ll = ll
  proof
A1: dom (valH*'ll) = dom ll &
    for i st i in dom (valH*'ll) holds valH.(ll.i) = ll.i
    proof
      thus dom (valH*'ll) = dom ll
      proof
        thus dom (valH*'ll) c= dom ll by RELAT_1:44;
        thus dom ll c= dom (valH*'ll)
        proof
          let a such that
A2:      a in dom ll;
          reconsider i = a as Nat by A2;
A3:      ll.i in rng ll by A2,FUNCT_1:12;
          rng ll c= bound_QC-variables by CQC_LANG:def 5; then
          ll.i in bound_QC-variables by A3;
          then ll.i in dom id bound_QC-variables by RELAT_1:71;
          hence thesis by A2,Def7,FUNCT_1:21;
        end;
      end;
      thus for i st i in dom (valH*'ll) holds valH.(ll.i) = ll.i
      proof
        let i such that
A4:     i in dom (valH*'ll);
        dom(valH*'ll) c= dom ll by RELAT_1:44; then
A5:     ll.i in rng ll by A4,FUNCT_1:12;
        rng ll c= bound_QC-variables by CQC_LANG:def 5;
        hence thesis by A5,Def7,FUNCT_1:35;
      end;
    end;
A6: for i st 1 <= i & i <= k holds valH.(ll.i) = ll.i
    proof
      let i such that
A7:  1 <= i & i <= k;
      1 <= i & i <= len (valH*'ll) by A7,VALUAT_1:def 8;
      then i in dom (valH*'ll) by FINSEQ_3:27;
      hence thesis by A1;
    end;
A8:  for i st i in Seg k holds (valH*'ll).i = ll.i
    proof
      let i such that
A9:  i in Seg k;
A10:  1 <= i & i <= k by A9,FINSEQ_1:3; then
      (valH*'ll).i = valH.(ll.i) by VALUAT_1:def 8;
      hence thesis by A6,A10;
    end;
    len (valH*'ll) = k & len ll = k by QC_LANG1:def 8,VALUAT_1:def 8;
    hence thesis by A8,FINSEQ_2:10;
 end;

theorem Th15:
  |- f^<*VERUM*>
  proof
    set PR = <*[f^<*VERUM*>,1]*>;
A1: rng PR = {[f^<*VERUM*>,1]} by FINSEQ_1:55;
    now let a such that A2: a in rng PR;
A3:   a = [f^<*VERUM*>,1] by A1,A2,TARSKI:def 1;
      f^<*VERUM*> in set_of_CQC-WFF-seq &
               1 in Proof_Step_Kinds by CALCUL_1:def 6,CQC_THE1:43;
      hence a in [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by A3,ZFMISC_1:106;
    end; then
    rng PR c= [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by TARSKI:def 3; then
    reconsider PR as FinSequence of [:set_of_CQC-WFF-seq,Proof_Step_Kinds:]
                                                           by FINSEQ_1:def 4;
    now let n such that A4: 1 <= n & n <= len PR;
      1 <= n & n <= 1 by A4,FINSEQ_1:56; then
      n = 1 by AXIOMS:21; then
      PR.n =  [f^<*VERUM*>,1] by FINSEQ_1:57; then
      (PR.n)`1 = f^<*VERUM*> & (PR.n)`2 = 1 by MCART_1:7;
      hence PR,n is_a_correct_step by CALCUL_1:def 7;
    end; then
A5: PR is a_proof by CALCUL_1:def 8;
    PR.1 = [f^<*VERUM*>,1] by FINSEQ_1:57; then
    PR.(len PR) = [f^<*VERUM*>,1] by FINSEQ_1:57; then
    (PR.(len PR))`1 = f^<*VERUM*> by MCART_1:7;
    hence thesis by A5,CALCUL_1:def 9;
  end;

theorem
  JH,valH |= VERUM iff CX |- VERUM
  proof
    reconsider f = {} as FinSequence of CQC-WFF by FINSEQ_1:29;
    rng f c= CX & |- f^<*VERUM*> by Th15,RELAT_1:60,XBOOLE_1:2;
    hence thesis by Def2,VALUAT_1:44;
  end;

theorem
  JH,valH |= P!ll iff CX |- P!ll
   proof
     thus JH,valH |= P!ll implies CX |- P!ll
     proof
       assume A1: JH,valH |= P!ll;
       set rel = JH.P;
       reconsider rel as Element of relations_on HCar;
       Valid(P!ll,JH).valH = TRUE by A1,VALUAT_1:def 12;
       then (valH*'ll) in rel by VALUAT_1:16;
       then ll in rel by Th14;
       then consider ll' being CQC-variable_list of
       (the_arity_of P) such that
A2:    ll' = ll & CX |- P!ll' by Def6;
       thus thesis by A2;
     end;
     thus CX |- P!ll implies JH,valH |= P!ll
     proof
       assume A3: CX |- P!ll;
       P is (QC-pred_symbol of the_arity_of P) by QC_LANG3:3;
       then the_arity_of P = k by SUBSTUT2:3;
       then ll in JH.P by A3,Def6;
       then (valH*'ll) in JH.P by Th14;
       then Valid(P!ll,JH).valH = TRUE by VALUAT_1:16;
       hence thesis by VALUAT_1:def 12;
     end;   
   end;
