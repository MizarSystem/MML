:: Real Numbers - Basic Theorems
::  by Library Committee
::
:: Received February 9, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies ARYTM_1, ARYTM, ARYTM_3, RELAT_1, ARYTM_2, BOOLE, COMPLEX1,
      FUNCOP_1, OPPCAT_1, ZF_LANG, ASYMPT_0, XCMPLX_0, SUPINF_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, ORDINAL1, FUNCT_4, ARYTM_2,
      ARYTM_1, ARYTM_0, XCMPLX_0, XREAL_0, NUMBERS, XXREAL_0;
 constructors FUNCT_4, ARYTM_1, ARYTM_0, XCMPLX_0, XXREAL_0, XREAL_0;
 registrations ARYTM_2, NUMBERS, XCMPLX_0, XXREAL_0, XREAL_0, ORDINAL1;
 requirements BOOLE, SUBSET, NUMERALS, ARITHM, REAL;
 definitions XXREAL_0;
 theorems TARSKI, ARYTM_2, ARYTM_1, ARYTM_0, XBOOLE_0, XCMPLX_0, XCMPLX_1,
      XREAL_0, ZFMISC_1, XXREAL_0, NUMBERS;

begin

 reserve a,b,c,d for real number;

reserve r,s for real number;

Lm1:
  (r in REAL+ & s in REAL+ &
   ex x',y' being Element of REAL+ st r = x' & s = y' & x' <=' y') or
  (r in [:{0},REAL+:] & s in [:{0},REAL+:] &
  ex x',y' being Element of REAL+ st r = [0,x'] & s = [0,y'] & y' <=' x') or
   s in REAL+ & r in [:{0},REAL+:]
  implies r <= s
proof assume
A1: (r in REAL+ & s in REAL+ &
   ex x',y' being Element of REAL+ st r = x' & s = y' & x' <=' y') or
  (r in [:{0},REAL+:] & s in [:{0},REAL+:] &
  ex x',y' being Element of REAL+ st r = [0,x'] & s = [0,y'] & y' <=' x') or
  ( s in REAL+ & r in [:{0},REAL+:]);
 per cases;
 case r in REAL+ & s in REAL+;
  hence ex x',y' being Element of REAL+ st r = x' & s = y' & x' <=' y' by A1,
ARYTM_0:5,XBOOLE_0:3;
 end;
 case r in [:{0},REAL+:] & s in [:{0},REAL+:];
  hence ex x',y' being Element of REAL+ st r = [0,x'] & s = [0,y'] & y' <=' x'
   by A1,ARYTM_0:5,XBOOLE_0:3;
 end;
 case not(r in REAL+ & s in REAL+) &
   not (r in [:{0},REAL+:] & s in [:{0},REAL+:]);
  hence thesis by A1;
 end;
end;

Lm2: for x1,x2 being Element of REAL st a = [*x1,x2*]
       holds x2 = 0 & a = x1
 proof let x1,x2 being Element of REAL;
  assume
A1:  a = [*x1,x2*];
A2: a in REAL by XREAL_0:def 1;
  hereby assume x2 <> 0;
    then a = (0,1) --> (x1,x2) by A1,ARYTM_0:def 7;
   hence contradiction by A2,ARYTM_0:10;
  end;
  hence a = x1 by A1,ARYTM_0:def 7;
 end;

Lm3:
 for a',b' being Element of REAL, a,b st a' = a & b' = b
  holds +(a',b') = a + b
proof let a',b' be Element of REAL, a,b such that
A1: a' = a & b' = b;
  consider x1,x2,y1,y2 being Element of REAL such that
A2: a = [* x1,x2 *] and
A3: b = [*y1,y2*] and
A4: a+b = [*+(x1,y1),+(x2,y2)*] by XCMPLX_0:def 4;
A5: a = x1 & b = y1 by A2,A3,Lm2;
      x2 = 0 & y2 = 0 by A2,A3,Lm2;
  then +(x2,y2) = 0 by ARYTM_0:13;
 hence +(a',b') = a + b by A1,A4,A5,ARYTM_0:def 7;
end;

Lm4: {} in {{}} by TARSKI:def 1;

Lm5: :: AXIOMS:24
 a <= b implies a + c <= b + c
proof
 assume
A1: a <= b;
  reconsider x1=a, y1=b, z1=c as Element of REAL by XREAL_0:def 1;
A2: +(y1,z1) = b + c & +(x1,z1) = a + c by Lm3;
 per cases by A1,XXREAL_0:def 5;
 suppose that
A3: a in REAL+ and
A4: b in REAL+ and
A5: c in REAL+;
  consider a'',b'' being Element of REAL+ such that
A6: a = a'' and
A7: b = b'' and
A8: a'' <=' b'' by A1,A3,A4,XXREAL_0:def 5;
  consider a',c' being Element of REAL+ such that
A9: a = a' and
A10: c = c' and
A11: +(x1,z1) = a' + c' by A3,A5,ARYTM_0:def 2;
  consider b',c'' being Element of REAL+ such that
A12: b = b' and
A13: c = c'' and
A14: +(y1,z1) = b' + c'' by A4,A5,ARYTM_0:def 2;
       a' + c' <=' b' + c'' by A6,A7,A8,A9,A10,A12,A13,ARYTM_1:7;
 hence a + c <= b + c by A2,A11,A14,Lm1;
 end;
 suppose that
A15: a in [:{0},REAL+:] and
A16: b in REAL+ and
A17: c in REAL+;
  consider a',c' being Element of REAL+ such that
        a = [0,a'] and
A18: c = c' and
A19: +(x1,z1) = c' - a' by A15,A17,ARYTM_0:def 2;
  consider b',c'' being Element of REAL+ such that
        b = b' and
A20: c = c'' and
A21: +(y1,z1) = b' + c'' by A16,A17,ARYTM_0:def 2;
      now per cases;
   suppose a' <=' c';
    then A22:   c' - a' = c' -' a' by ARYTM_1:def 2;
A23:  c' -' a' <=' c' by ARYTM_1:11;
        c' <=' b' + c'' by A18,A20,ARYTM_2:20;
    then c' -' a' <=' b' + c'' by A23,ARYTM_1:3;
   hence a + c <= b + c by A2,A19,A21,A22,Lm1;
   end;
   suppose not a' <=' c';
    then c' - a' = [0,a' -' c'] by ARYTM_1:def 2;
    then c' - a' in [:{0},REAL+:] by Lm4,ZFMISC_1:106;
    then not a + c in REAL+ & not b + c in [:{0},REAL+:]
                 by A2,A19,A21,ARYTM_0:5,XBOOLE_0:3;
   hence a + c <= b + c by XXREAL_0:def 5;
  end;
  end;
 hence thesis;
 end;
 suppose that
A24: a in [:{0},REAL+:] and
A25: b in [:{0},REAL+:] and
A26: c in REAL+;
  consider a'',b'' being Element of REAL+ such that
A27: a = [0,a''] and
A28: b = [0,b''] and
A29: b'' <=' a'' by A1,A24,A25,XXREAL_0:def 5;
  consider a',c' being Element of REAL+ such that
A30: a = [0,a'] and
A31: c = c' and
A32: +(x1,z1) = c' - a' by A24,A26,ARYTM_0:def 2;
  consider b',c'' being Element of REAL+ such that
A33: b = [0,b'] and
A34: c = c'' and
A35: +(y1,z1) = c'' - b' by A25,A26,ARYTM_0:def 2;
A36: a' = a'' by A27,A30,ZFMISC_1:33;
A37: b' = b'' by A28,A33,ZFMISC_1:33;
      now per cases;
   suppose
A38:  a' <=' c';
then A39:   c' - a' = c' -' a' by ARYTM_1:def 2;
        b' <=' c' by A29,A36,A37,A38,ARYTM_1:3;
then A40:   c' - b' = c' -' b' by ARYTM_1:def 2;
        c' -' a' <=' c'' -' b' by A29,A31,A34,A36,A37,ARYTM_1:16;
   hence a + c <= b + c by A2,A31,A32,A34,A35,A39,A40,Lm1;
   end;
   suppose not a' <=' c';
    then A41:  +(x1,z1) = [0,a' -' c'] by A32,ARYTM_1:def 2;
    then A42:   +(x1,z1) in [:{0},REAL+:] by Lm4,ZFMISC_1:106;
       now per cases;
    suppose b' <=' c';
     then c' - b' = c' -' b' by ARYTM_1:def 2;
     then not +(x1,z1) in REAL+ & not +(y1,z1) in [:{0},REAL+:]
                 by A31,A34,A35,A42,ARYTM_0:5,XBOOLE_0:3;
    hence a + c <= b + c by A2,XXREAL_0:def 5;
    end;
    suppose not b' <=' c';
     then A43:  +(y1,z1) = [0,b' -' c'] by A31,A34,A35,ARYTM_1:def 2;
    then A44:   +(y1,z1) in [:{0},REAL+:] by Lm4,ZFMISC_1:106;
         b' -' c' <=' a' -' c' by A29,A36,A37,ARYTM_1:17;
    hence a + c <= b + c by A2,A41,A42,A43,A44,Lm1;
   end;
   end;
   hence thesis;
  end;
  end;
 hence thesis;
 end;
 suppose that
A45: a in REAL+ and
A46: b in REAL+ and
A47: c in [:{0},REAL+:];
  consider a'',b'' being Element of REAL+ such that
A48: a = a'' and
A49: b = b'' and
A50: a'' <=' b'' by A1,A45,A46,XXREAL_0:def 5;
  consider a',c' being Element of REAL+ such that
A51: a = a' and
A52: c = [0,c'] and
A53: +(x1,z1) = a' - c' by A45,A47,ARYTM_0:def 2;
  consider b',c'' being Element of REAL+ such that
A54: b = b' and
A55: c = [0,c''] and
A56: +(y1,z1) = b' - c'' by A46,A47,ARYTM_0:def 2;
A57: c' = c'' by A52,A55,ZFMISC_1:33;
      now per cases;
   suppose
A58:  c' <=' a';
then A59:   a' - c' = a' -' c' by ARYTM_1:def 2;
        c' <=' b' by A48,A49,A50,A51,A54,A58,ARYTM_1:3;
then A60:   b' - c' = b' -' c' by ARYTM_1:def 2;
        a' -' c' <=' b' -' c'' by A48,A49,A50,A51,A54,A57,ARYTM_1:17;
   hence a + c <= b + c by A2,A53,A56,A57,A59,A60,Lm1;
   end;
   suppose not c' <=' a';
    then A61:  +(x1,z1) = [0,c' -' a'] by A53,ARYTM_1:def 2;
    then A62:   +(x1,z1) in [:{0},REAL+:] by Lm4,ZFMISC_1:106;
       now per cases;
    suppose c' <=' b';
     then b' - c' = b' -' c' by ARYTM_1:def 2;
     then not +(x1,z1) in REAL+ & not +(y1,z1) in [:{0},REAL+:]
                 by A56,A57,A62,ARYTM_0:5,XBOOLE_0:3;
    hence a + c <= b + c by A2,XXREAL_0:def 5;
    end;
    suppose not c' <=' b';
     then A63:  +(y1,z1) = [0,c' -' b'] by A56,A57,ARYTM_1:def 2;
    then A64:   +(y1,z1) in [:{0},REAL+:] by Lm4,ZFMISC_1:106;
     c' -' b' <=' c' -' a' by A48,A49,A50,A51,A54,ARYTM_1:16;
    hence a + c <= b + c by A2,A61,A62,A63,A64,Lm1;
   end;
   end;
   hence thesis;
  end;
  end;
 hence thesis;
 end;
 suppose that
A65: a in [:{0},REAL+:] and
A66: b in REAL+ and
A67: c in [:{0},REAL+:];
      not a in REAL+ & not c in REAL+ by A65,A67,ARYTM_0:5,XBOOLE_0:3;
  then consider a',c' being Element of REAL+ such that
        a = [0,a'] and
A68: c = [0,c'] and
A69: +(x1,z1) = [0,a' + c'] by ARYTM_0:def 2;
  consider b',c'' being Element of REAL+ such that
        b = b' and
A70: c = [0,c''] and
A71: +(y1,z1) = b' - c'' by A66,A67,ARYTM_0:def 2;
A72: c' = c'' by A68,A70,ZFMISC_1:33;
A73:  +(x1,z1) in [:{0},REAL+:] by A69,Lm4,ZFMISC_1:106;
      now per cases;
   suppose c' <=' b';
    then b' - c'' = b' -' c'' by A72,ARYTM_1:def 2;
    then not +(x1,z1) in REAL+ & not +(y1,z1) in [:{0},REAL+:]
                 by A71,A73,ARYTM_0:5,XBOOLE_0:3;
   hence a + c <= b + c by A2,XXREAL_0:def 5;
   end;
   suppose
        not c' <=' b';
    then A74:   +(y1,z1) = [0,c' -' b'] by A71,A72,ARYTM_1:def 2;
then A75:  +(y1,z1) in [:{0},REAL+:] by Lm4,ZFMISC_1:106;
A76:  c' -' b' <=' c' by ARYTM_1:11;
        c' <=' c' + a' by ARYTM_2:20;
    then c' -' b' <=' c' + a' by A76,ARYTM_1:3;
   hence a + c <= b + c by A2,A69,A73,A74,A75,Lm1;
  end;
  end;
 hence thesis;
 end;
 suppose that
A77: a in [:{0},REAL+:] and
A78: b in [:{0},REAL+:] and
A79: c in [:{0},REAL+:];
  consider a'',b'' being Element of REAL+ such that
A80: a = [0,a''] and
A81: b = [0,b''] and
A82: b'' <=' a'' by A1,A77,A78,XXREAL_0:def 5;
      not a in REAL+ & not c in REAL+ by A77,A79,ARYTM_0:5,XBOOLE_0:3;
  then consider a',c' being Element of REAL+ such that
A83: a = [0,a'] and
A84: c = [0,c'] and
A85: +(x1,z1) = [0,a' + c'] by ARYTM_0:def 2;
      not b in REAL+ & not c in REAL+ by A78,A79,ARYTM_0:5,XBOOLE_0:3;
  then consider b',c'' being Element of REAL+ such that
A86: b = [0,b'] and
A87: c = [0,c''] and
A88: +(y1,z1) = [0,b' + c''] by ARYTM_0:def 2;
A89: a' = a'' by A80,A83,ZFMISC_1:33;
A90: b' = b'' by A81,A86,ZFMISC_1:33;
A91: c' = c'' by A84,A87,ZFMISC_1:33;
then A92: b' + c' <=' a' + c'' by A82,A89,A90,ARYTM_1:7;
A93: +(x1,z1) in [:{0},REAL+:] by A85,Lm4,ZFMISC_1:106;
       +(y1,z1) in [:{0},REAL+:] by A88,Lm4,ZFMISC_1:106;
 hence a + c <= b + c by A2,A85,A88,A91,A92,A93,Lm1;
end;
end;

Lm6:
 a <= b & c <= d implies a+c <= b+d
proof
  assume A1: a<=b;
  assume c <= d;
then A2: b+c <= b+d by Lm5;
   a+c <= b+c by A1,Lm5;
  hence thesis by A2,XXREAL_0:2;
end;

Lm7: a <= b implies a-c <= b-c
proof
   a <= b implies a + -c <= b + -c by Lm5;
  hence thesis;
end;

Lm8: a < b & c <= d implies a+c < b+d
  proof
    assume A1: a<b & c <= d;
    for a,b,c,d holds a<b & c <= d implies a+c < b+d
    proof
      let a,b,c,d;
      assume A2: a<b;
      assume A3: c <= d;
      then A4: a+c <= b+d by A2,Lm6;
       a+c <> b+d
      proof
        assume A5: a+c = b+d;
         b<=a
        proof
            a+c <= d+a by A3,Lm5;
           then a+c-d<=a+d-d by Lm7;
           hence thesis by A5;
         end;
         hence contradiction by A2;
       end;
       hence thesis by A4,XXREAL_0:1;
     end;
    hence thesis by A1;
  end;

Lm9: 0 < a implies b < b+a
proof
  assume 0<a;
  then b+0<b+a by Lm8;
  hence thesis;
end;

canceled 2;

theorem Th3: :: REAL_1:76
 ex b st a < b
proof
   take a+1;
    a+0<a+1 by Lm8;
   hence thesis;
end;

theorem Th4: :: REAL_1:77
 ex b st b < a
proof
   take a-1;
    a+ -1<a+ -0 by Lm8;
   hence thesis;
end;

theorem :: CHAIN_1:2
  ex c st a < c & b < c
proof
 per cases;
 suppose
A1: a <= b;
 take z = b + 1;
   b < z by Lm9;
 hence thesis by A1,XXREAL_0:2;
 end;
 suppose
A2: b <= a;
 take z = a + 1;
   a < z by Lm9;
 hence thesis by A2,XXREAL_0:2;
 end;
end;

Lm10: :: REAL_1:53
 a+c <= b+c implies a <= b
proof
   assume a+c <= b+c;
   then a+c+(-c)<=b+c+(-c) by Lm5;
   hence thesis;
end;

theorem
  ex c st c < a & c < b
proof
 per cases;
 suppose
A1: a <= b;
 take z = a+-1;
   z < a+0 by Lm10;
 hence thesis by A1,XXREAL_0:2;
 end;
 suppose
A2: b <= a;
 take z = b+-1;
   z < b+0 by Lm10;
 hence thesis by A2,XXREAL_0:2;
 end;
end;

Lm11:
 for a',b' being Element of REAL, a,b st a' = a & b' = b
  holds *(a',b') = a * b
proof let a',b' be Element of REAL, a,b such that
A1: a' = a & b' = b;
  consider x1,x2,y1,y2 being Element of REAL such that
A2: a = [* x1,x2 *] and
A3: b = [*y1,y2*] and
A4: a*b = [* +(*(x1,y1),opp*(x2,y2)), +(*(x1,y2),*(x2,y1)) *]
      by XCMPLX_0:def 5;
A5: a = x1 & b = y1 by A2,A3,Lm2;
A6: x2 = 0 & y2 = 0 by A2,A3,Lm2;
  then *(x1,y2) = 0 & *(x2,y1) = 0 by ARYTM_0:14;
  then A7: +(*(x1,y2),*(x2,y1)) = 0 by ARYTM_0:13;
 thus *(a',b') = +(*(x1,y1),0) by A1,A5,ARYTM_0:13
      .= +(*(x1,y1),*(opp x2,y2)) by A6,ARYTM_0:14
      .= +(*(x1,y1),opp*(x2,y2)) by ARYTM_0:17
      .= a * b by A4,A7,ARYTM_0:def 7;
end;
 reconsider o = 0 as Element of REAL+ by ARYTM_2:21;

Lm12: a <= b & 0 <= c implies a*c <= b*c
proof assume that
A1: a <= b and
A2: 0 <= c;
  reconsider x1=a, y1=b, z1=c as Element of REAL by XREAL_0:def 1;
A3: *(y1,z1) = b * c & *(x1,z1) = a * c by Lm11;
    not o in [:{0},REAL+:] by ARYTM_0:5,XBOOLE_0:3;
then A4: c in REAL+ by A2,XXREAL_0:def 5;
assume
A5: not thesis;
 then A6: c <> 0;
 per cases by A1,XXREAL_0:def 5;
 suppose that
A7: a in REAL+ and
A8: b in REAL+;
  consider a'',b'' being Element of REAL+ such that
A9: a = a'' and
A10: b = b'' and
A11: a'' <=' b'' by A1,A7,A8,XXREAL_0:def 5;
  consider a',c' being Element of REAL+ such that
A12: a = a' and
A13: c = c' and
A14: *(x1,z1) = a' *' c' by A4,A7,ARYTM_0:def 3;
  consider b',c'' being Element of REAL+ such that
A15: b = b' and
A16: c = c'' and
A17: *(y1,z1) = b' *' c'' by A4,A8,ARYTM_0:def 3;
       a' *' c' <=' b' *' c' by A9,A10,A11,A12,A15,ARYTM_1:8;
 hence contradiction by A3,A5,A13,A14,A16,A17,Lm1;
 end;
 suppose that
A18: a in [:{0},REAL+:] and
A19: b in REAL+;
  consider a',c' being Element of REAL+ such that
        a = [0,a'] and
        c = c' and
A20: *(x1,z1) = [0,c' *' a'] by A4,A6,A18,ARYTM_0:def 3;
  consider b',c'' being Element of REAL+ such that
        b = b' and
        c = c'' and
A21: *(y1,z1) = b' *' c'' by A4,A19,ARYTM_0:def 3;
       *(x1,z1) in [:{0},REAL+:] by A20,Lm4,ZFMISC_1:106;
  then not *(x1,z1) in REAL+ & not *(y1,z1) in [:{0},REAL+:]
     by A21,ARYTM_0:5,XBOOLE_0:3;
 hence contradiction by A3,A5,XXREAL_0:def 5;
 end;
 suppose that
A22: a in [:{0},REAL+:] and
A23: b in [:{0},REAL+:];
  consider a'',b'' being Element of REAL+ such that
A24: a = [0,a''] and
A25: b = [0,b''] and
A26: b'' <=' a'' by A1,A22,A23,XXREAL_0:def 5;
  consider a',c' being Element of REAL+ such that
A27: a = [0,a'] and
A28: c = c' and
A29: *(x1,z1) = [0,c' *' a'] by A4,A6,A22,ARYTM_0:def 3;
  consider b',c'' being Element of REAL+ such that
A30: b = [0,b'] and
A31: c = c'' and
A32: *(y1,z1) = [0,c'' *' b'] by A4,A6,A23,ARYTM_0:def 3;
A33: a' = a'' by A24,A27,ZFMISC_1:33;
A34: b' = b'' by A25,A30,ZFMISC_1:33;
A35: *(x1,z1) in [:{0},REAL+:] & *(y1,z1) in [:{0},REAL+:]
  by A29,A32,Lm4,ZFMISC_1:106;
       b' *' c' <=' a' *' c' by A26,A33,A34,ARYTM_1:8;
 hence contradiction by A3,A5,A28,A29,A31,A32,A35,Lm1;
end;
end;

Lm13: 0 < c & a < b implies a*c < b*c
proof
  assume A1: 0<c;
  assume A2: a<b;
  then A3: a*c <= b*c by A1,Lm12;
       a*c <> b*c
    proof
      assume a*c = b*c;
      then a*(c*c")=b*c*c" by XCMPLX_1:4;
      then a*1=b*(c*c") by A1,XCMPLX_0:def 7;
      then a=b*1 by A1,XCMPLX_0:def 7;
     hence contradiction by A2;
   end;
 hence thesis by A3,XXREAL_0:1;
end;

theorem Th7: :: REAL_1:75, CHAIN_1:1
 a < b implies ex c st a < c & c < b
proof
  assume A1:a<b;
  take z=(a+b)/2;
  thus a<z
  proof
        1*a+a<a+b by A1,Lm10;
    then 2"*(2*a)<2"*(a+b) by Lm13;
    hence thesis;
  end;
      a+b<1*b+b by A1,Lm10;
  then 2"*(a+b)<2"*(2*b) by Lm13;
  hence thesis;
end;

begin :: addition

theorem :: AXIOMS:24, REAL_1:53
  a <= b iff a + c <= b + c by Lm5,Lm10;

theorem :: REAL_1:55
  a <= b & c <= d implies a+c <= b+d by Lm6;

theorem :: REAL_1:67
  a < b & c <= d implies a+c < b+d by Lm8;

Lm14: a<=b implies -b<=-a
  proof
   assume a<=b;
    then a-b<=b-b by Lm7;
    then a-b-a<=0-a by Lm7;
   hence thesis;
  end;

Lm15: -b<=-a implies a<=b
  proof
   assume -b<=-a;
    then -(-a)<=-(-b) by Lm14;
   hence thesis;
  end;

begin :: subtraction

theorem Th11: :: REAL_1:49, REAL_1:54
 a <= b iff a-c <= b-c
 proof thus a <= b implies a-c <= b-c by Lm7;
  assume a-c <= b-c;
  then a+ (-c) + c <= b+ (-c)+c by Lm5;
  hence thesis;
end;

theorem :: REAL_2:106`3, REAL_2:105'6
  a <= b iff c-b <= c-a
 proof
  hereby assume
A1: a <= b;
   assume c-a < c-b;
    then -(c-b)<-(c-a) by Lm15; then b-c < a-c;
   hence contradiction by A1,Lm7;
  end;
  assume c-a>=c-b; then -(c-a)<=-(c-b) by Lm14; then a-c <= b-c;
  hence a <= b by Th11;
 end;

Lm16: a+b <= c implies a <= c-b
proof
 assume a+b<=c;
  then a+b-b<=c-b by Lm7;
 hence a <= c-b;
end;

Lm17: a <= b-c implies a+c <= b
proof
 assume a <= b-c;
  then a+c <= b - c + c by Lm5;
 hence a+c <= b;
end;

Lm18: a <= b+c implies a-b <= c
proof
     assume a<=b+c;
        then a-b <= c+b-b by Lm7;
      hence a-b <= c;
end;

Lm19: a-b <= c implies a <= b+c
proof
  assume a-b <= c;
  then a + -b <= c;
  then a <= c - -b by Lm16;
  hence a <= b + c;
end;

theorem :: REAL_2:165
  a <= b-c implies c <= b-a
  proof
   assume a<=b-c; then a+c <= b by Lm17;
   hence c <= b-a by Lm16;
  end;

theorem :: REAL_2:165
  a-b <= c implies a-c <= b
  proof
   assume c>=a-b; then c+b>=a by Lm19;
   hence b>=a-c by Lm18;
  end;

theorem Th15: :: REAL_1:92`1
 a <= b & c <= d implies a-d <= b-c
proof
 assume A1: a <= b & c <= d;
  then -d <= -c by Lm14;
  then a+ -d <= b+ -c by A1,Lm6;
 hence thesis;
end;

theorem Th16: :: REAL_1:92`2
 a < b & c <= d implies a-d < b-c
proof
 assume A1: a < b & c <= d;
  then - d <= - c by Lm14;
  then a+ -d < b+ -c by A1,Lm8;
 hence thesis;
end;

theorem :: REAL_1:92`3
  a <= b & c < d implies a-d < b-c
proof
 assume a <= b & c < d;
  then c-b < d-a by Th16;
  then -(d-a) < -(c-b) by Lm15;
 hence thesis;
end;

:: tylko - binarny, 4 minusy
Lm20: a+b<=c+d implies a-c <= d-b
  proof
   assume a+b<=c+d; then a<=c+d-b by Lm16;
    then a<=c+(d-b);
   hence a-c <= d-b by Lm18;
  end;

theorem :: REAL_2:169`2, REAL_2:170`2
  a-b <= c-d implies a-c <= b-d
  proof
   assume a-b<=c-d; then a-b+d<=c by Lm17;
   then a+d-b<=c;
   then a+d<=c+b by Lm19;
   hence a-c <= b-d by Lm20;
  end;

theorem :: REAL_2:169`3, REAL_2:170`3
  a-b <= c-d implies d-b <= c-a
  proof
   assume a-b<=c-d; then a-b+d<=c by Lm17;
   then a+d-b<=c;
   then a+d<=c+b by Lm19;
   hence c-a>=d-b by Lm20;
  end;

theorem :: REAL_2:169`4, REAL_2:170`4
  a-b <= c-d implies d-c <= b-a
  proof
   assume a-b<=c-d; then a-b+d<=c by Lm17;
   then a+d-b<=c;
   then a+d<=c+b by Lm19;
   hence thesis by Lm20;
  end;

begin :: addition & subtraction

theorem :: REAL_1:84
  a+b <= c iff a <= c-b by Lm16,Lm17;

theorem :: REAL_1:86
  a <= b+c iff a-b <= c by Lm18,Lm19;

theorem :: REAL_2:167, REAL_2:170`1,REAL_2:168, REAL_2:169`1
  a+b<=c+d iff a-c <= d-b
 proof thus a+b<=c+d implies a-c <= d-b by Lm20;
  assume
A1:  a-c <= d-b;
   assume c+d<a+b; then d<b+a-c by Lm19;
    then d<b+(a-c);
   hence thesis by A1,Lm17;
 end;

theorem :: REAL_2:171`1
  a+b<=c-d implies a+d<=c-b
   proof
    assume A1:a+b<=c-d;
       per cases by A1;
        suppose a+b<=c-d; then a+b+d<=c by Lm17;
         then a+d+b<=c;hence a+d<=c-b by Lm16;
        end;
        suppose b+a<=c-d;
         then a+b+d<=c by Lm17;
         then a+d+b<=c;hence a+d<=c-b by Lm16;
    end;
   end;

theorem :: REAL_2:171`2
  a-b<=c+d implies a-d<=c+b
   proof
    assume A1:c+d>=a-b;
       per cases by A1;
        suppose c+d>=a-b; then c+d+b>=a by Lm19;
         then c+b+d>=a;hence c+b>=a-d by Lm18;
        end;
        suppose d+c>=a-b;
         then c+d+b>=a by Lm19;
         then c+b+d>=a;hence c+b>=a-d by Lm18;
    end;
   end;

begin :: opposite

theorem :: REAL_1:50
  a<=b iff -b<=-a by Lm14,Lm15;

Lm21: a < b implies 0 < b-a
 proof assume a < b; then a + 0 < b;
  hence thesis by Lm19;
 end;

theorem Th27: :: REAL_2:109'1
 a<=-b implies b<=-a
  proof
   assume a<=-b; then a+b<=-b+b by Lm5;
    then b--a<=0;hence b<=-a by Lm21;
  end;

Lm22: a <= b implies 0 <= b-a
 proof assume a <= b;
   then a - a <= b - a by Lm7;
  hence 0 <= b - a;
 end;

theorem Th28: :: REAL_2:110`1
 -b<=a implies -a<=b
  proof assume
A1:  -b<=a;
   assume b<-a; then b+a<-a+a by Lm10;
    then a--b<0;
   hence thesis by A1,Lm22;
  end;

begin :: with numerals, only 0 and addition

theorem Th29: :: METRIC_3:2, SPPOL_1:8
 0 <= a & 0 <= b & a+b = 0 implies a = 0
proof assume that
A1: a>=0 & b>=0 & a+b=0 and
A2: a<>0;
    0+b<a+b by A1,A2,Lm10;
   hence contradiction by A1;
end;

theorem :: SPPOL_1:9
  a <= 0 & b <= 0 & a+b = 0 implies a = 0
proof assume
A1: a<=0 & b<=0 & a+b=0;
then -a>=-0 & -b>=-0 by Lm14;
 hence thesis by A1;
end;

begin :: 2 inequalities

theorem :: REAL_1:69, REAL_2:174`1`2
  0 < a implies b < b+a by Lm9;

theorem :: REAL_2:173`1`1
  a < 0 implies a+b < b
  proof
   assume a<0; then b+a<0+b by Lm10;
   hence a+b<b;
  end;

Lm23: :: REAL_2:105'1`2, REAL_2:105'1`3*
 a < b implies a-b < 0
 proof assume
A1: a < b;
  assume a-b>=0; then a-b+b>=0+b by Lm5;
    hence thesis by A1;
 end;

Lm24: a < 0 iff 0 < -a
proof
  thus a < 0 implies 0 < -a
  proof
    assume a < 0;
    then a+ (-a) < 0 + (-a) by Lm10;
    hence thesis;
  end;
  assume 0 < -a;
  then 0 + a <-a + a by Lm10;
  hence thesis;
end;

theorem :: REAL_2:173`1`2
  0 <= a implies b <= a+b
  proof assume
A1: 0 <= a;
   assume A2:a+b<b;
    per cases by A2;
     suppose a+b<b; then a+b-b<0 by Lm23;
     hence thesis by A1;
     end;
     suppose b-a>b; then b+-a-b>0 by Lm21;
     hence thesis by A1,Lm24;
  end;
  end;

theorem :: REAL_2:174`1`1
  a <= 0 implies a+b <= b
  proof
   assume a<=0; then b+a<=0+b by Lm6;
      hence a+b<=b;
  end;

begin :: 3 inequalities

theorem :: JORDAN2C:5
  0 <= a & 0 <= b implies 0 <= a+b
proof assume a>=0 & b>=0;
  then a+b>=0+0 by Lm6;
 hence a+b>=0;
end;

theorem :: JGRAPH_6:3`1, JGRAPH_6:3`2, JORDAN2C:6, ANALOAF:2
  0 <= a & 0 < b implies 0 < a+b
proof assume a>=0 & b>0;
  then b+a>0+0 by Lm8;
 hence a+b>0;
end;

theorem Th37: :: WSIERP_1:7`1
 a <= 0 & c <= b implies c+a <= b
 proof assume a <= 0 & c <= b;
   then a+c <= 0+b by Lm6;
  hence thesis;
 end;

theorem Th38: :: WSIERP_1:8`1`1
 a <= 0 & c < b implies c+a < b
 proof assume a <= 0 & c < b;
   then a+c < 0+b by Lm8;
  hence thesis;
 end;

theorem Th39: :: WSIERP_1:8`1`2
 a < 0 & c <= b implies c+a < b
 proof assume a < 0 & c <= b;
   then a+c < 0+b by Lm8;
  hence thesis;
 end;

theorem
  0 <= a & b <= c implies b <= a+c
 proof
  assume 0 <= a & b <= c;
   then b+0 <= a+c by Lm6;
  hence thesis;
 end;

theorem
  0 < a & b <= c implies b < a+c
 proof
  assume 0 < a & b <= c;
   then b+0 < a+c by Lm8;
  hence thesis;
 end;

theorem Th42: :: HEINE:3
 0 <= a & b < c implies b < a+c
 proof
  assume 0 <= a & b < c;
   then b+0 < a+c by Lm8;
  hence thesis;
 end;

Lm25: c < 0 & a < b implies b*c < a*c
proof
 assume A1: c < 0;
 assume A2: a<b;
   -0<-c by A1,Lm15;
  then a*(-c)<b*(-c) by A2,Lm13;
  then -(a*c)<-(b*c);
 hence thesis by Lm14;
end;

Lm26: 0 < a implies 0 < a"
proof
   assume A1:0<a;
   assume A2:not 0<a";
       a"<>0 by A1,XCMPLX_1:203;
   then a*a"<0*a" by A1,A2,Lm25;
   hence contradiction by XCMPLX_0:def 7;
end;

Lm27: 0 <= c & b <= a implies b/c <= a/c
proof
  assume A1: 0<=c & b <= a;
 per cases by A1;
 suppose c = 0;
  then c" = 0 by XCMPLX_0:def 7;
  then b*c" = 0 & a*c" = 0;
  then b/c = 0 & a/c = 0 by XCMPLX_0:def 9;
  hence thesis;
 end;
 suppose A2: 0<c;
      assume a/c < b/c;
      then a/c*c < b/c*c by A2,Lm13;
      then a<b/c*c by A2,XCMPLX_1:88;
  hence contradiction by A1,A2,XCMPLX_1:88;
 end;
end;

Lm28: 0 < c & a < b implies a/c < b/c
proof
  assume 0<c;
  then A1: 0<c" by Lm26;
   a<b implies a/c < b/c
   proof
     assume a<b;
     then a*c"<b*c" by A1,Lm13;
     then a/c <b*c" by XCMPLX_0:def 9;
     hence thesis by XCMPLX_0:def 9;
   end;
  hence thesis;
end;

Lm29: 0 < a implies 0 < a/2
  proof
   assume
A1: 0<a;
       0/2=0;
   hence 0<a/2 by A1,Lm28;
  end;

Lm30: 0 < a implies a/2 < a
  proof
   assume 0<a;
   then 0+a/2<a/2+a/2 by Lm10;
   hence a/2<a;
  end;

begin :: a universal assumption

theorem :: REAL_2:181`1, COMPLSP1:83
  (for a being real number st a>0 holds c <= b+a) implies c <= b
  proof
   assume A1:for a st a>0 holds b+a>=c;
     assume b<c; then A2:0<c-b by Lm21;
     set d=c-b;   d/2<d by A2,Lm30;
     then A3: b+d/2<b+d by Lm10;
         d/2>0 by A2,Lm29;
     hence contradiction by A1,A3;
  end;

theorem :: REAL_2:182`1
  (for a being real number st a<0 holds b+a <= c) implies b <= c
  proof
    assume A1:for a st a<0 holds b+a<=c;
     assume c < b; then A2:0>c-b by Lm23;
     set d=c-b;A3:-d>0 by A2,Lm24;
     then (-d)/2<-d by Lm30;
     then c+-d/2<c+-d by Lm10;
     then c-d/2<c-d;
     then A4:c < b+d/2 by Lm16;
         (-d)/2>0 by A3,Lm29;
     then -d/2>0;
     then d/2<0 by Lm24;
     hence contradiction by A1,A4;
  end;

begin :: with numerals, only 0 and substraction

theorem :: PROB_1:2, REAL_2:173`2`2
  0 <= a implies b-a <= b
  proof
     0 <= a implies b - a <= b - 0 by Th15;
   hence thesis;
  end;

theorem :: SQUARE_1:29, REAL_2:174`2`1
  0 < a implies b-a < b
 proof assume 0 < a;
  then -a < -0 by Lm15;
  then b + -a < b + 0 by Lm10;
  hence b - a < b;
 end;

theorem :: REAL_2:174`2`2
  a <= 0 implies b <= b-a
  proof
   assume a<=0; then b+a<=0+b by Lm6;
      hence b<=b-a by Lm16;
  end;

theorem :: REAL_2:173`2`1
  a < 0 implies b < b-a
  proof
   assume a<0; then b+a<0+b by Lm10;
   hence b<b-a by Lm19;
  end;

theorem :: REAL_2:106`2, REAL_2:106`3*
  a <= b implies a-b <= 0
 proof assume
A1: a <= b;
  assume a-b>0; then a-b+b>0+b by Lm10;
    hence thesis by A1;
 end;

theorem Th50: :: SQUARE_1:12, REAL_2:106`1*
 a <= b implies 0 <= b-a
 proof assume a <= b;
   then a - a <= b - a by Lm7;
  hence 0 <= b - a;
 end;

theorem :: REAL_2:105'1`2, REAL_2:105'1`3*
  a < b implies a-b < 0 by Lm23;

theorem :: SQUARE_1:11, REAL_2:105'1`1*
  a < b implies 0 < b-a by Lm21;

theorem :: HEINE:4
  0 <= a & b < c implies b-a < c
 proof
  assume 0 <= a & b < c;
   then b+0 < a+c by Th42;
  hence thesis by Lm17;
 end;

theorem :: WSIERP_1:7`2
  a <= 0 & b <= c implies b <= c-a
 proof assume a <= 0 & b <= c;
   then b+a <= c by Th37;
  hence thesis by Lm16;
 end;

theorem :: WSIERP_1:8`2`1
  a <= 0 & b < c implies b < c-a
 proof assume a <= 0 & b < c;
   then b+a < c by Th38;
  hence thesis by Lm19;
 end;

theorem :: WSIERP_1:8`2`2
  a < 0 & b <= c implies b < c-a
 proof assume a < 0 & b <= c;
   then a+b < c by Th39;
  hence thesis by Lm19;
 end;

theorem :: ANALOAF:3
  a <> b implies 0 < a-b or 0 < b-a
 proof assume
A1: a <> b;
  per cases by A1,XXREAL_0:1;
  suppose a < b;
    then 0 + a < b;
   hence thesis by Lm19;
  end;
  suppose b < a;
    then 0 + b < a;
   hence thesis by Lm19;
  end;
 end;

begin :: with a universal premise

theorem :: REAL_2:181`2
  (for a being real number st a<0 holds c <= b-a) implies b>=c
  proof
    assume A1:for a st a<0 holds b-a>=c;
     assume b<c; then A2:0>b-c by Lm23;
     set d=b-c;A3:-d>0 by A2,Lm24;
     then (-d)/2<-d by Lm30;
     then b+-d/2<b+-d by Lm10;
     then A4: b-d/2<b-d;
         (-d)/2>0 by A3,Lm29;
     then -d/2>0;
     then d/2<0 by Lm24;
     hence contradiction by A1,A4;
  end;

theorem :: REAL_2:182`2
  (for a being real number st a>0 holds b-a <= c) implies b<=c
  proof
   assume A1:for a st a>0 holds b-a<=c;
     assume b>c; then A2:0<b-c by Lm21;
     set d=b-c;   d/2<d by A2,Lm30;
     then c+d/2<c+d by Lm10;
     then A3:c < b-d/2 by Lm19;
         d/2>0 by A2,Lm29;
     hence contradiction by A1,A3;
  end;

begin :: with numerals, only 0 and unary -

theorem :: REAL_1:66
  a < 0 iff 0 < -a by Lm24;

theorem :: REAL_2:109'2
  a <= -b implies a+b <= 0
  proof
   assume a<=-b; then a+b<=-b+b by Lm5;
   hence a+b<=0;
  end;

theorem :: REAL_2:111
  -a <= b implies 0 <= a+b
  proof
   assume b>=-a; then b+a>=-a+a by Lm5;
   hence a+b>=0;
  end;

theorem :: REAL_2:110'2
  a < -b implies a+b < 0
  proof
   assume a<-b; then a+b<-b+b by Lm10;
   hence a+b<0;
  end;

theorem :: REAL_2:112
  -b < a implies 0 < a+b
  proof
   assume a>-b; then a+b>-b+b by Lm10;
   hence a+b>0;
  end;

Lm31: a <= b & c <= 0 implies b*c <= a*c
proof
  assume A1: a<=b;
  assume c <= 0;
  then -0<=-c by Lm14;
  then a*(-c)<=b*(-c) by A1,Lm12;
  then -(a*c)<=-(b*c);
  hence thesis by Lm15;
end;

begin :: numeral 0 & multiplication

theorem :: REAL_1:93
  0 <= a*a
proof
  per cases;
  suppose 0 <= a;
  then 0*a <= a*a by Lm12;
  hence thesis;
  end;
  suppose not 0 <= a;
  then 0*a <= a*a by Lm31;
  hence thesis;
end;
end;

theorem  :: AXIOMS:25
  a <= b & 0 <= c implies a*c <= b*c by Lm12;

theorem :: REAL_1:52
  a <= b & c <= 0 implies b*c <= a*c by Lm31;

theorem Th68: :: REAL_2:197, SEQ_4:7, RFUNCT_1:2
 0 <= a & a <= b & 0 <= c & c <= d implies a*c <= b*d
  proof
   assume A1: 0<=a & a<=b & 0<=c & c <= d;
   then A2:a*c <= b*c by Lm12;
     b*c <= b*d by A1,Lm12;
   hence a*c <= b*d by A2,XXREAL_0:2;
  end;

theorem :: REAL_2:198
  a <= 0 & b <= a & c <= 0 & d <= c implies a*c <= b*d
  proof
   assume 0>=a & a>=b & 0>=c & c>=d;
   then -0<=-a & -a<=-b & -0<=-c & -c <= -d by Lm14;
   then (-a)*(-c)<=(-b)*(-d) & (-a)*(-c)<=(-d)*(-b) & (-c)*(-a)<=(-d)*(-b)
      & (-c)*(-a)<=(-b)*(-d) by Th68;
   hence thesis;
  end;

theorem :: REAL_1:70
  0 < c & a < b implies a*c < b*c by Lm13;

theorem :: REAL_1:71
  c < 0 & a < b implies b*c < a*c by Lm25;

theorem :: REAL_2:199`2
  a < 0 & b <= a & c < 0 & d < c implies a*c < b*d
  proof
    assume A1:0>a & a>=b & 0>c & c>d;
     then A2:a*c < a*d by Lm25;
       a*d<=b*d by A1,Lm31;
     hence a*c < b*d by A2,XXREAL_0:2;
  end;

begin :: with numerals, only 0, addition and multiplication

theorem Th73: :: SPPOL_1:11
 0 <= a & 0 <= b & 0 <= c & 0 <= d & a*c+b*d = 0
   implies a = 0 or c = 0
proof assume
A1: a>=0 & b>=0 & c>=0 & d>=0 & a*c+b*d=0;
then A2: a*c>=a*0 by Lm12;
       b*d>=0 * d by A1,Lm12;
  then a*c =0 & b*d=0 by A1,A2,Th29;
 hence thesis by XCMPLX_1:6;
end;

Lm32: c < 0 & a < b implies b/c < a/c
proof
  assume that
A1: c < 0 and
A2: a < b;
   -0<-c by A1,Lm15;
  then a/(-c)<b/(-c) by A2,Lm28;
  then -a/c < b/(-c) by XCMPLX_1:189;
  then -a/c < -b/c by XCMPLX_1:189;
 hence thesis by Lm14;
end;

Lm33: c <= 0 & b/c < a/c implies a < b
proof
  assume that
A1:  c <= 0 and
A2: b/c < a/c;
A3: now assume c = 0;
    then A4:   c" = 0 by XCMPLX_0:def 7;
      a/c = a*c" by XCMPLX_0:def 9
        .= b*c" by A4
        .= b/c by XCMPLX_0:def 9;
    hence contradiction by A2;
   end;
   then a/c*c < b/c*c by A1,A2,Lm25;
   then a<b/c*c by A3,XCMPLX_1:88;
  hence thesis by A3,XCMPLX_1:88;
end;

begin :: numeral 0 & division

theorem :: REAL_1:73`2
  0 <= c & b <= a implies b/c <= a/c by Lm27;

theorem :: REAL_1:74`2
  c <= 0 & a <= b implies b/c <= a/c by Lm33;

theorem :: REAL_1:73`1
  0 < c & a < b implies a/c < b/c by Lm28;

theorem :: REAL_1:74`1
  c < 0 & a < b implies b/c < a/c by Lm32;

theorem :: SEQ_2:10, REAL_2:200`1
  0 < c & 0 < a & a < b implies c/b < c/a
  proof
   assume that
A1: 0<c and
A2: 0<a and
A3: a<b;
A4: 0<b" by A2,A3,Lm26;
A5: 0<a" by A2,Lm26;
       a*b"<b*b" by A3,A4,Lm13;
   then a*b"<1 by A2,A3,XCMPLX_0:def 7;
   then a"*(a*b")<a"*1 by A5,Lm13;
   then a"*a*b"<a";
   then 1*b"<a" by A2,XCMPLX_0:def 7;
   then c*b"<c*a" by A1,Lm13;
   then c/b<c*a" by XCMPLX_0:def 9;
   hence thesis by XCMPLX_0:def 9;
  end;

Lm34: :: REAL_2:128`2
 a < 0 & 0 < b implies b/a < 0
  proof
   assume a<0 & b>0;
    then b/a<0/a by Lm32;hence b/a<0;
  end;

Lm35: b < 0 & a < b implies b" < a"
  proof
A1: a" = 1/a & b" = 1/b by XCMPLX_1:217;
   assume A2: 0>b & a<b;
     then a"<0 by A1,Lm34; then b*(a")<a*(a") by A2,Lm25;
     then A3:b*(a")<1 by A1,A2,XCMPLX_1:88;
         b"<0 by A1,A2,Lm34; then b"*(b*(a"))>1*(b") by A3,Lm25;
      then b"*b*(a")>1*(b");
      then 1*(a")>1*(b") by A2,XCMPLX_0:def 7;
     hence thesis;
  end;

begin :: with numerals, only 0, multiplication and division

theorem Th79: :: REAL_2:177`1
 0 < b & a*b <= c implies a <= c/b
 proof
  assume A1:b>0 & a*b<=c;
    then a*b/b<=c/b by Lm27;
    hence a<=c/b by A1,XCMPLX_1:90;
 end;

theorem Th80: :: REAL_2:177`2
 b < 0 & a*b <= c implies c/b <= a
 proof
  assume A1:b<0 & a*b<=c;
    then a*b/b>=c/b by Lm33;
     hence a>=c/b by A1,XCMPLX_1:90;
 end;

theorem Th81: :: REAL_2:177`3
 0 < b & c <= a*b implies c/b <= a
 proof
  assume A1:b>0 & a*b>=c;
    then a*b/b>=c/b by Lm27;
     hence a>=c/b by A1,XCMPLX_1:90;
 end;

theorem Th82: :: REAL_2:177`3
 b < 0 & c <= a*b implies a <= c/b
 proof
  assume A1:b<0 & a*b>=c;
    then a*b/b<=c/b by Lm33;
  hence a<=c/b by A1,XCMPLX_1:90;
 end;

theorem Th83: :: REAL_2:178`1
 0 < b & a*b < c implies a< c/b
 proof
  assume A1:b>0 & a*b<c;
    then a*b/b<c/b by Lm28;
     hence a<c/b by A1,XCMPLX_1:90;
 end;

theorem Th84: :: REAL_2:178`2
 b < 0 & a*b < c implies c/b < a
 proof
  assume A1:b<0 & a*b<c;
    then a*b/b>c/b by Lm32;
     hence a>c/b by A1,XCMPLX_1:90;
 end;

theorem Th85: :: REAL_2:178`3
 0 < b & c < a*b implies c/b < a
 proof
  assume A1:b>0 & a*b>c;
    then a*b/b>c/b by Lm28;
     hence a>c/b by A1,XCMPLX_1:90;
 end;

theorem Th86: :: REAL_2:178`2
 b < 0 & c < a*b implies a < c/b
 proof
  assume A1:b<0 & a*b>c;
    then a*b/b<c/b by Lm32;
  hence a<c/b by A1,XCMPLX_1:90;
 end;

Lm36: 0 < a & a <= b implies b" <= a"
  proof
   assume
A1: 0<a & a<=b;
    then b" > 0 by Lm26;
     then a*(b")<=b*(b") by A1,Lm12;
     then A2:a*(b")<=1 by A1,XCMPLX_0:def 7;
         a">=0 by A1,Lm26;
     then a"*(a*b")<=1*a" by A2,Lm12;
     then a"*a*b"<=1*a";
     then 1*b"<=1*a" by A1,XCMPLX_0:def 7;
     hence thesis;
  end;

Lm37: b < 0 & a <= b implies b" <= a"
  proof
A1:  1/a = a" & 1/b = b" by XCMPLX_1:217;
   assume A2: 0>b & a<=b;
    then a"<0 by A1,Lm34;
     then b*(a")<=a*(a") by A2,Lm31;
     then A3:b*(a")<=1 by A2,XCMPLX_0:def 7;
         b"<=0 by A1,A2,Lm34;
     then b"*(b*(a"))>=1*(b") by A3,Lm31;
     then b"*b*a">=1*b";
     then 1*a">=1*b" by A2,XCMPLX_0:def 7;
     hence thesis;
  end;

begin :: numeral 0 & inverse

theorem :: REAL_2:152`1
  0 < a & a <= b implies b" <= a" by Lm36;

theorem :: REAL_2:152`2
  b < 0 & a <= b implies b" <= a" by Lm37;

theorem :: REAL_2:151`2
  b < 0 & a < b implies b" < a" by Lm35;

Lm38: :: SEQ_2:6, REAL_2:127`1
 0 < a & 0 < b implies 0 < a/b
  proof
   assume a>0 & b>0;
    then a/b>0/b by Lm28;hence a/b>0;
  end;

Lm39: 0 < a & a < b implies b" < a"
  proof
A1: b" = 1/b & a" = 1/a by XCMPLX_1:217;
   assume A2:0<a & a<b;
    then b">0 by A1,Lm38; then a*(b")<b*(b") by A2,Lm13;
     then A3:a*(b")<1 by A1,A2,XCMPLX_1:88;
         a">0 by A1,A2,Lm38; then a"*(a*(b"))<1*(a") by A3,Lm13;
     then a"*a*b"<1*(a");
     then 1*b"<1*(a") by A2,XCMPLX_0:def 7;
     hence b"<a";
  end;

theorem :: REAL_2:151`1
  0 < a & a < b implies b" < a" by Lm39;

theorem :: REAL_2:155`1
  0 < b" & b" <= a" implies a <= b
  proof
   assume b">0 & a">=b";
   then a""<=b"" by Lm36;
   hence thesis;
  end;

theorem :: REAL_2:155`2
  a" < 0 & b" <= a" implies a <= b
  proof
   assume a"<0 & a">=b";
   then a""<=b"" by Lm37;
   hence thesis;
  end;

theorem :: REAL_2:154`1
  0 < b" & b" < a" implies a < b
  proof
   assume b">0 & a">b";
   then a""<b"" by Lm39;
   hence thesis;
  end;

theorem :: REAL_2:154`2
  a" < 0 & b" < a" implies a < b
  proof
   assume a"<0 & a">b";
   then a""<b"" by Lm35;
   hence thesis;
  end;

Lm40: :: SQUARE_1:21, REAL_2:122`2
 for a,b being positive (real number) holds 0 < a*b;

Lm41: :: SQUARE_1:21, REAL_2:122`2
 for a,b being negative (real number) holds 0 < a*b;

Lm42: :: SQUARE_1:21, REAL_2:122`2
 for a,b being non negative (real number) holds 0 <= a*b;

Lm43: :: SQUARE_1:21, REAL_2:122`2
 for a,b being non positive (real number) holds 0 <= a*b;

Lm44:
 for a being non negative non positive (real number)
   holds 0 = a*b;

Lm45: :: SQUARE_1:24
 for a being positive (real number), b being negative (real number)
   holds a*b < 0;

begin :: two occurrences of 0

theorem :: JGRAPH_3:4
  0 <= a & (b-a)*(b+a) <= 0 implies -a <= b & b <= a
 proof assume A1:a>=0 & (b-a)*(b+a)<=0;
   then A2:b-a>=0 & b+a<=0 or b-a<=0 & b+a>=0 by Lm40,Lm41;
      b+0<=b+a by A1,Lm6;
   then b<=a+0 & b+a>=0 by A1,A2,Lm18,Lm19;
   then b<=a & b>=0-a by Lm18;
  hence -a<=b & b<=a;
 end;

theorem :: JGRAPH_4:2
  0 <= a & (b-a)*(b+a) < 0 implies -a < b & b < a
 proof assume A1:a>=0 & (b-a)*(b+a)<0;
   then A2:b-a>0 & b+a<0 or b-a<0 & b+a>0 by Lm42,Lm43,Lm44;
   A3:b+0<=b+a by A1,Lm6;
   then b-a<=b+a-a by Lm7;
   then b<a+0 & b+a>0 by A2,A3,Lm16;
   then b<a & b>0-a by Lm17;
  hence -a<b & b<a;
 end;

theorem :: JGRAPH_5:1
  0 <= a & 0 <= (b-a)*(b+a) implies b <= -a or a <= b
proof assume a>=0 & (b-a)*(b+a)>=0;
   then b-a>=0 & b+a>=0 or b-a<=0 & b+a<=0 by Lm45;
   then b-a+a>=0+a or b+a<=0 by Lm6;
   then b>=0+a or b+a-a<=0-a by Lm7;
  hence -a>=b or b>=a;
end;

theorem :: SEQ_2:7
  0 <= a & 0 <= c & a < b & c < d implies a*c < b*d
  proof
   assume that
A1: 0<=a and
A2: 0<=c and
A3: a<b and
A4: c < d;
A5: a*d<b*d by A2,A3,A4,Lm13;
       a*c <= a*d by A1,A4,Lm12;
   hence thesis by A5,XXREAL_0:2;
  end;

theorem :: LIMFUNC1:1, SEQ_4:6
  0 <= a & 0 < c & a < b & c <= d implies a*c < b*d
proof assume A1: 0<=a & 0<c & a<b & c <=d;
then A2: a*c <=a*d by Lm12;
   a*d<b*d by A1,Lm13;
 hence thesis by A2,XXREAL_0:2;
end;

theorem Th100: :: REAL_2:199`1
 0 < a & 0 < c & a <= b & c < d implies a*c < b*d
  proof
    assume A1:0<a & 0<c & a<=b & c < d;
     then A2:a*c < a*d by Lm13;
       a*d<=b*d by A1,Lm12;
     hence a*c < b*d by A2,XXREAL_0:2;
  end;

theorem :: REAL_2:200`2
  0 < c & b < 0 & a < b implies c/b < c/a
  proof
   assume A1:c>0 & b<0 & a<b;
   then a">b" by Lm35;
   then a"*c>b"*c by A1,Lm13;
   then c/a>b"*c by XCMPLX_0:def 9;
   hence c/a>c/b by XCMPLX_0:def 9;
  end;

theorem :: REAL_2:202`1
  c < 0 & 0 < a & a < b implies c/a < c/b
  proof
   assume A1:c < 0 & a>0 & a<b;
   then a">b" by Lm39;
   then a"*c < b"*c by A1,Lm25;
   then c/a<b"*c by XCMPLX_0:def 9;
   hence c/a<c/b by XCMPLX_0:def 9;
  end;

theorem :: REAL_2:202`2
  c < 0 & b < 0 & a < b implies c/a < c/b
  proof
   assume A1:c < 0 & b<0 & a<b;
   then a">b" by Lm35;
   then a"*c < b"*c by A1,Lm25;
   then c/a<b"*c by XCMPLX_0:def 9;
   hence c/a<c/b by XCMPLX_0:def 9;
  end;

theorem :: REAL_2:187`1
  0 < b & 0 < d & a*d <= c*b implies a/b <= c/d
  proof
   assume A1: b>0 & d>0 & a*d<=c*b;
     then a*d/b<=c by Th81;
     then a/b*d<=c by XCMPLX_1:75;
     hence thesis by A1,Th79;
  end;

theorem :: REAL_2:187`2
  b < 0 & d < 0 & a*d <= c*b implies a/b <= c/d
  proof
   assume A1: b<0 & d<0 & a*d<=c*b;
     then a*d/b>=c by Th82;
     then a/b*d>=c by XCMPLX_1:75;
     hence thesis by A1,Th82;
  end;

theorem :: REAL_2:188`1
  0 < b & d < 0 & a*d <= c*b implies c/d <= a/b
  proof
   assume A1: b>0 & d<0 & a*d<=c*b;
     then a*d/b<=c by Th81;
     then a/b*d<=c by XCMPLX_1:75;
     hence thesis by A1,Th80;
  end;

theorem :: REAL_2:188`2
  b < 0 & 0 < d & a*d <= c*b implies c/d <= a/b
  proof
   assume A1: b<0 & d>0 & a*d<=c*b;
     then a*d/b>=c by Th82;
     then a/b*d>=c by XCMPLX_1:75;
     hence thesis by A1,Th81;
  end;

theorem :: REAL_2:185`1
  0 < b & 0 < d & a*d < c*b implies a/b < c/d
  proof
   assume A1: b>0 & d>0 & a*d<c*b;
     then a*d/b<c by Th85;
     then a/b*d<c by XCMPLX_1:75;
     hence thesis by A1,Th83;
  end;

theorem :: REAL_2:185`2
  b < 0 & d < 0 & a*d < c*b implies a/b < c/d
  proof
   assume A1:  b<0 & d<0 & a*d<c*b;
     then a*d/b>c by Th86;
     then a/b*d>c by XCMPLX_1:75;
     hence thesis by A1,Th86;
  end;

theorem :: REAL_2:186`1
  0 < b & d < 0 & a*d < c*b implies c/d < a/b
  proof
   assume A1: b>0 & d<0 & a*d<c*b;
     then a*d/b<c by Th85;
     then a/b*d<c by XCMPLX_1:75;
     hence thesis by A1,Th84;
  end;

theorem :: REAL_2:186`2
  b < 0 & 0 < d & a*d < c*b implies c/d < a/b
  proof
   assume A1: b<0 & d>0 & a*d<c*b;
     then a*d/b>c by Th86;
     then a/b*d>c by XCMPLX_1:75;
     hence thesis by A1,Th85;
  end;

theorem :: REAL_2:193`1
  b < 0 & d < 0 & a*b < c/d implies a*d < c/b
  proof
   assume
A1:   b<0 & d<0;
     assume a*b<c/d;
      then a*b*d>c by A1,Th80;
      then a*d*b>c;
      hence a*d<c/b by A1,Th86;
  end;

theorem :: REAL_2:193`2
  0 < b & 0 < d & a*b < c/d implies a*d < c/b
  proof
   assume
A1:   b>0 & d>0;
     assume a*b<c/d;
      then a*b*d<c by A1,Th81;
      then a*d*b<c;
      hence a*d<c/b by A1,Th83;
  end;

theorem :: REAL_2:193`3
  b < 0 & d < 0 & c/d < a*b implies c/b < a*d
  proof
   assume
A1: b<0 & d<0;
     assume a*b>c/d;
      then a*b*d<c by A1,Th82;
      then a*d*b<c;
      hence a*d>c/b by A1,Th84;
  end;

theorem :: REAL_2:193`4
  0 < b & 0 < d & c/d < a*b implies c/b < a*d
  proof
   assume
A1:   b>0 & d>0;
     assume a*b>c/d;
      then a*b*d>c by A1,Th79;
      then a*d*b>c;
      hence a*d>c/b by A1,Th85;
  end;

theorem :: REAL_2:194`1
  b < 0 & 0 < d & a*b < c/d implies c/b < a*d
  proof
   assume
A1:   b<0 & d>0;
     assume a*b<c/d;
      then a*b*d<c by A1,Th81;
      then a*d*b<c;
      hence a*d>c/b by A1,Th84;
  end;

theorem :: REAL_2:194`2
  0 < b & d < 0 & a*b < c/d implies c/b < a*d
  proof
   assume
A1:   b>0 & d<0;
      assume a*b<c/d;
      then a*b*d>c by A1,Th80;
      then a*d*b>c;
      hence a*d>c/b by A1,Th85;
  end;

theorem :: REAL_2:194`3
  b < 0 & 0 < d & c/d < a*b implies a*d < c/b
  proof
   assume
A1:   b<0 & d>0;
   assume a*b>c/d;
      then a*b*d>c by A1,Th79;
      then a*d*b>c;
      hence a*d<c/b by A1,Th86;
  end;

theorem :: REAL_2:194`4
  0 < b & d < 0 & c/d < a*b implies a*d < c/b
  proof
   assume
A1:   b>0 & d<0;
    assume a*b>c/d;
      then a*b*d<c by A1,Th82;
      then a*d*b<c;
      hence a*d<c/b by A1,Th83;
  end;

theorem :: SEQ_4:1, REAL_2:201`1
  0 < a & 0 <= c & a <= b implies c/b <= c/a
  proof
   assume that
A1: 0<a and
A2: 0<=c and
A3: a<=b;
A4: 0<=b" by A1,A3,Lm26;
   A5: 0<=a" by A1,Lm26;
       a*b"<=b*b" by A3,A4,Lm12;
   then a*b"<=1 by A1,A3,XCMPLX_0:def 7;
   then a"*(a*b")<=a"*1 by A5,Lm12;
   then a"*a*b"<=a";
   then 1*b"<=a" by A1,XCMPLX_0:def 7;
   then c*b"<=c*a" by A2,Lm12;
   then c/b<=c*a" by XCMPLX_0:def 9;
   hence thesis by XCMPLX_0:def 9;
  end;

theorem :: REAL_2:201`2
  0 <= c & b < 0 & a <= b implies c/b <= c/a
  proof
   assume A1: c>=0 & b<0 & a<=b;
   then a">=b" by Lm37;
   then a"*c>=b"*c by A1,Lm12;
   then c/a>=b"*c by XCMPLX_0:def 9;
   hence c/a>=c/b by XCMPLX_0:def 9;
  end;

theorem :: REAL_2:203`1
  c <= 0 & 0 < a & a <= b implies c/a <= c/b
  proof
   assume
A1: c <= 0;
   assume a>0 & a<=b;
   then a">=b" by Lm36;
   then a"*c <= b"*c by A1,Lm31;
   then c/a<=b"*c by XCMPLX_0:def 9;
   hence c/a<=c/b by XCMPLX_0:def 9;
  end;

theorem :: REAL_2:203`2
  c <= 0 & b < 0 & a <= b implies c/a <= c/b
  proof
   assume A1: c <= 0 & b<0 & a<=b;
   then a">=b" by Lm37;
   then a"*c <= b"*c by A1,Lm31;
   then c/a<=b"*c by XCMPLX_0:def 9;
   hence c/a<=c/b by XCMPLX_0:def 9;
  end;

theorem :: REAL_1:72, REAL_2:150`2
  0 < a iff 0 < a"
proof
 thus 0 < a implies 0 < a" by Lm26;
   a"" = 1/(a") by XCMPLX_1:217;
 hence thesis by Lm38;
end;

theorem :: REAL_2:149, REAL_2:150`1
  a < 0 iff a" < 0
proof a" = 1/a by XCMPLX_1:217;
 hence a < 0 implies a" < 0 by Lm34;
   a"" = 1/(a") by XCMPLX_1:217;
 hence thesis by Lm34;
end;

theorem Th126: :: REAL_2:153
 a < 0 & 0 < b implies a" < b"
  proof
A1:  a" = 1/a & b" = 1/b by XCMPLX_1:217;
   assume a<0 & b>0;
   then a"<0 & b">0 by A1,Lm34,Lm38;
   hence thesis;
  end;

theorem :: REAL_2:156
  a" < 0 & 0 < b" implies a < b
  proof
   assume a"<0 & b">0;
    then a""<b"" by Th126;
   hence thesis;
  end;

begin :: three occurrences of 0

theorem
  a <= 0 & 0 <= a implies 0 = a*b by Lm44;

theorem :: REAL_2:121`1, SQUARE_1:19
  0 <= a & 0 <= b implies 0 <= a*b by Lm42;

theorem :: REAL_2:121`2, SQUARE_1:20
  a <= 0 & b <= 0 implies 0 <= a*b by Lm43;

theorem :: SQUARE_1:21, REAL_2:122`2
  0 < a & 0 < b implies 0 < a*b by Lm40;

theorem :: SQUARE_1:22, REAL_2:122`1
  a < 0 & b < 0 implies 0 < a*b by Lm41;

theorem Th133: :: SQUARE_1:23, REAL_2:123
 0 <= a & b <= 0 implies a*b <= 0
 proof assume
A1: 0 <= a & b <= 0;
  then -0 <= -b by Lm14;
  then 0 <= a*(-b) by A1,Lm42;
  then -0 <= -(a*b);
  hence a*b <= 0 by Lm15;
 end;

theorem :: SQUARE_1:24
  0 < a & b < 0 implies a*b < 0 by Lm45;

theorem  :: REAL_2:132
  a*b<0 implies a>0 & b<0 or a<0 & b>0
proof assume
A1: a*b<0;
  then a <> 0 & b <> 0;
 hence thesis by A1,Lm42,Lm43;
end;

theorem  :: SQUARE_1:26
  a*b>0 implies a>0 & b>0 or a<0 & b<0
proof assume
A1: a*b>0;
  then a <> 0 & b <> 0;
 hence thesis by A1,Th133;
end;

theorem Th137:  :: REAL_2:125`1
 a <= 0 & b <= 0 implies 0 <= a/b
  proof
   assume
A1: a<=0 & b<=0;
   per cases by A1;
   suppose A2: b < 0;
    per cases by A1;
    suppose a<0; then a/b>0/b by A2,Lm32;
     hence a/b>=0;
    end;
    suppose a=0;
     hence a/b>=0;
    end;
   end;
   suppose b = 0;
    hence a/b>=0 by XCMPLX_1:49;
   end;
  end;

theorem Th138: :: SQUARE_1:27, REAL_2:125`2
  0 <= a & 0 <= b implies 0 <= a/b
 proof assume that
A1: 0 <= a and
A2: 0 <= b;
  per cases by A2;
  suppose 0 = b;
   then a/b = a*0" by XCMPLX_0:def 9 .= a*0;
   hence thesis;
  end;
  suppose 0 < b;
  then 0 < b" by Lm26;
  then 0 <= a*b" by A1,Lm42;
  hence 0 <= a/b by XCMPLX_0:def 9;
 end;
 end;

theorem Th139: :: REAL_2:126`1
 0 <= a & b <= 0 implies a/b <= 0
  proof
   assume A1:a>=0 & b <=0;
   per cases by A1;
     suppose a>0 & b<0; then a/b<0/b by Lm32;
      hence a/b<=0;
     end;
     suppose a=0;hence a/b<=0; end;
     suppose b=0;
      then b" = 0 by XCMPLX_0:def 7;
      then a*b" = 0;
     hence a/b<=0 by XCMPLX_0:def 9;
    end;
  end;

theorem Th140: :: REAL_2:126`2
 a <= 0 & 0 <= b implies a/b <= 0
  proof
   assume A1:a<=0 & b>=0;
   per cases by A1;
     suppose a<0 & b>0; then a/b<0/b by Lm28;
      hence a/b<=0;
     end;
     suppose a=0;hence a/b<=0; end;
     suppose b=0;
      then b" = 0 by XCMPLX_0:def 7;
      then a*b" = 0;
     hence a/b<=0 by XCMPLX_0:def 9;
     end;
  end;

theorem :: SEQ_2:6, REAL_2:127`1
  0 < a & 0 < b implies 0 < a/b by Lm38;

theorem :: REAL_2:127`2
  a < 0 & b < 0 implies 0 < a/b
  proof
   assume a<0 & b<0;
    then a/b>0/b by Lm32;hence a/b>0;
  end;

theorem :: REAL_2:128`1
  a < 0 & 0 < b implies a/b < 0
  proof
   assume a<0 & b>0;
   then a/b<0/b by Lm28;hence a/b<0;
  end;

theorem :: REAL_2:128`2
  a < 0 & 0 < b implies b/a < 0 by Lm34;

theorem
   a/b<0 implies b<0 & a>0 or b>0 & a<0
proof assume
A1: a/b<0;
   a/b = a*b" by XCMPLX_0:def 9;
  then b" <> 0 by A1;
  then a <> 0 & b <> 0 by A1,XCMPLX_0:def 7;
 hence thesis by A1,Th137,Th138;
end;

theorem
   a/b>0 implies b>0 & a>0 or b<0 & a<0
proof assume
A1: a/b>0;
   a/b = a*b" by XCMPLX_0:def 9;
  then b" <> 0 by A1;
  then a <> 0 & b <> 0 by A1,XCMPLX_0:def 7;
 hence thesis by A1,Th139,Th140;
end;

begin :: numeral 1

theorem :: SPPOL_1:5`2
  a <= b implies a < b+1
proof assume a<=b;
 then A1:a-1<=b-1 by Lm7;
  b-1<b-1+1 by Lm9;
 then a-1<b by A1,XXREAL_0:2;
 then a-1+1<b+1 by Lm10;
hence thesis;
end;

theorem :: INT_1:26
  a - 1 < a
  proof
     a+- 1 < a + 0 by Lm10;
   hence thesis;
  end;

theorem :: SPPOL_1:5`1
  a <= b implies a-1 < b
proof assume a<=b;
  then A1:a-1<=b-1 by Lm7;
   b-1<b-1+1 by Lm9;
 hence thesis by A1,XXREAL_0:2;
end;

theorem :: JGRAPH_4:3
  -1 < a implies 0 < 1+a
proof
 assume -1<a;
  then -1+1<a+1 by Lm8;
 hence 1+a>0;
end;

theorem :: JGRAPH_4:3
  a<1 implies 1-a>0 by Lm21;

begin :: numeral 1 and multiplication

theorem :: SPPOL_1:10
  0 <= a & a <= 1 & 0 <= b & b <= 1 & a*b = 1 implies a=1
proof assume
A1: 0<=a & a<=1 & 0<=b & b<=1 & a*b=1;
   now per cases by A1;
   case b=0;
   hence contradiction by A1;
   end;
   case A2:b>0;
    then a=b" by A1,XCMPLX_0:def 7;
    then a>=1" by A1,A2,Lm36;
    hence a=1 by A1,XXREAL_0:1;
  end;
  end;
 hence thesis;
end;

theorem :: REAL_2:146`1
  0 <= a & 1 <= b implies a <= a*b
  proof
   assume a>=0 & b>=1;
    then a*b>=a*1 by Lm12;
     hence a*b>=a;
  end;

theorem :: REAL_2:146`2
  a <= 0 & b <= 1 implies a <= a*b
  proof
   assume  a<=0 & b<=1;
    then a*b>=a*1 by Lm31;
     hence a*b>=a;
  end;

theorem :: REAL_2:147`1
  0 <= a & b <= 1 implies a*b <= a
  proof
   assume a>=0 & b<=1;
    then a*b<=a*1 by Lm12;
     hence a*b<=a;
  end;

theorem :: REAL_2:147`2
  a <= 0 & 1 <= b implies a*b <= a
  proof
   assume a<=0 & b>=1;
    then a*b<=a*1 by Lm31;
     hence a*b<=a;
  end;

theorem Th157: :: REAL_2:144`1
 0 < a & 1 < b implies a < a*b
  proof
   assume a>0 & b>1;
    then a*b>a*1 by Lm13;
     hence a*b>a;
  end;

theorem :: REAL_2:144`2
  a < 0 & b < 1 implies a < a*b
  proof
   assume a<0 & b<1;
    then a*b>a*1 by Lm25;
     hence a*b>a;
  end;

theorem :: REAL_2:145
  0 < a & b < 1 implies a*b < a
  proof
   assume a>0 & b<1;
    then a*b<a*1 by Lm13;
     hence a*b<a;
  end;

theorem :: REAL_2:145
  a < 0 & 1 < b implies a*b < a
  proof
   assume a<0 & b>1;
    then a*b<a*1 by Lm25;
     hence a*b<a;
  end;

theorem :: REAL_2:138`1
  1 <= a & 1 <= b implies 1 <= a*b
  proof
   assume
A1: a>=1 & b>=1;
    then a*b>=b*1 by Lm12;
   hence a*b>=1 by A1,XXREAL_0:2;
  end;

theorem :: REAL_2:140`1
  0 <= a & a <= 1 & 0 <= b & b <= 1 implies a*b <= 1
  proof
   assume
A1:   0<=a & a<=1 & 0<=b & b<=1;
    then a*b<=1*a by Lm12;
    hence a*b<=1 by A1,XXREAL_0:2;
  end;

theorem :: REAL_2:137`2
  1 < a & 1 <= b implies 1 < a*b
  proof
   assume
A1: a>1 & b>=1;
    then a*b>b*1 by Lm13;
   hence a*b>1 by A1,XXREAL_0:2;
  end;

theorem :: REAL_2:139`1
  0 <= a & a < 1 & 0 <= b & b <= 1 implies a*b < 1
 proof
  assume
A1:  0<=a & a<1 & 0<=b & b<=1;
    then a*b<=1*a by Lm12;
    hence a*b<1 by A1,XXREAL_0:2;
 end;

theorem :: REAL_2:138`2
  a <= -1 & b <= -1 implies 1 <= a*b
  proof
   assume
A1:   a<=-1 & b<=-1;
then A2:  a*b>=b*(-1) by Lm31;
         -b>=--1 by A1,Lm14;
   hence a*b>=1 by A2,XXREAL_0:2;
  end;

theorem :: REAL_2:137`4
  a < -1 & b <= -1 implies 1 < a*b
  proof
   assume
A1:   a<-1 & b<=-1;
then A2:  a*b>b*(-1) by Lm25;
         -b>=--1 by A1,Lm14;
   hence a*b>1 by A2,XXREAL_0:2;
  end;

theorem :: REAL_2:140`2
  a <= 0 & -1 <= a & b <= 0 & -1 <= b implies a*b <= 1
  proof
   assume
A1:   0>=a & a>=-1 & 0>=b & b>=-1;
    then A2: a*b<=(-1)*a by Lm31;
        -a<=--1 by A1,Lm14;
    hence a*b<=1 by A2,XXREAL_0:2;
  end;

theorem :: REAL_2:139`2
  a <= 0 & -1 < a & b <= 0 & -1 <= b implies a*b < 1
 proof
  assume
A1:  0>=a & a>-1 & 0>=b & b>=-1;
    then A2: a*b<=(-1)*a by Lm31;
        -a<--1 by A1,Lm15;
    hence a*b<1 by A2,XXREAL_0:2;
 end;

begin :: with a universal premise

theorem Th169: :: REAL_2:183
 (for a st 1 < a holds c <= b*a) implies c <= b
  proof
       assume A1:for a st a>1 holds b*a>=c;
       assume A2:not thesis;
       A3:b>=0
        proof
         assume b<0;
         then A4:b*2<0 by Lm45;
A5:         c <= b*2 by A1;
            c < 0 by A1,A4;
         then b/c>c/c by A2,Lm32;
         then A6:b/c>1 by A4,A5,XCMPLX_1:60;
         then A7:b*(b/c)>=c by A1;
          b*(b/c)<c*(b/c) by A2,A6,Lm13;
         then b*(b/c)<b by A4,A5,XCMPLX_1:88;
         hence contradiction by A2,A7,XXREAL_0:2;
        end;
        per cases by A3;
         suppose A8:b>0;
          then b/b<c/b by A2,Lm28;
          then 1<c/b by A8,XCMPLX_1:60;
          then consider d being real number such that
          A9:1<d & d<c/b by Th7;
              b*d<b*(c/b) by A8,A9,Lm13;
          then b*d<c by A8,XCMPLX_1:88;
          hence contradiction by A1,A9;
         end;
         suppose A10:b=0;
              b*2>=c by A1;
          hence contradiction by A2,A10;
      end;
  end;

Lm46:  1 < a implies a" < 1
 proof
   assume
A1: 1<a;
   then 0<a" by Lm26;
   then 1*a"<a*a" by A1,Lm13;
   hence a" < 1 by A1,XCMPLX_0:def 7;
 end;

theorem  :: REAL_2:184
  (for a st 0 < a & a < 1 holds b*a <= c) implies b <= c
  proof
     assume A1:for a st 0<a & a<1 holds b*a<=c;
         now let d;
A2:  d" = 1/d by XCMPLX_1:217;
         assume A3:d>1;
      then 0<d" & d"<1 by A2,Lm38,Lm46;
      then b*(d")<=c by A1;
      then b/d<=c by XCMPLX_0:def 9;
      hence b<=c*d by A3,Th83;
     end;
     hence thesis by Th169;
  end;

Lm47: a <= b & 0 <= a implies a/b <= 1
  proof assume
A1: a <= b;
   assume
A2:  0 <= a;
   per cases by A2;
   suppose a = 0;
    hence thesis;
   end;
   suppose
A3: a > 0;
     then a/b<=b/b by A1,Lm27;
     hence a/b<=1 by A1,A3,XCMPLX_1:60;
   end;
  end;

Lm48: b <= a & 0 <= a implies b/a <= 1
  proof
   assume
A1:  b <= a;
   assume A2:a>=0;
   per cases by A2;
   suppose a = 0;
     then a" = 0 by XCMPLX_0:def 7;
     then b*a" < 1;
    hence thesis by XCMPLX_0:def 9;
   end;
   suppose
A3:  a > 0;
    assume b/a>1; then b/a*a>1*a by A3,Lm13;
    hence thesis by A1,A3,XCMPLX_1:88;
   end;
  end;

theorem :: POLYNOM5:3
  (for a st 0 < a & a < 1 holds b <= a*c) implies b <= 0
  proof
   assume that
A1: for a st 0 < a & a < 1 holds b <= a*c and
A2: b > 0;
A3: b*2 > b by A2,Th157;
    then A4: b > b/2 by Th85;
   per cases;
    suppose c <= 0;
     then 1/2*c <= 0 by Th133;
    hence contradiction by A1,A2;
    end;
    suppose that
A5:   c <= b and
A6:  c > 0;
     set a = c/(2*b);
      2*b > 0 by A2,Lm40;
       then A7:  a > 0 by A6,Lm38;
A8:  c/b <= 1 by A5,A6,Lm47;
      c/b > 0 by A2,A6,Lm38;
       then c/b*2 > c/b by Th157;
       then c/b > c/b/2 by Th85;
       then c/b > c/(b*2) by XCMPLX_1:79;
       then a < 1 by A8,XXREAL_0:2;
       then A9: a*c >= b by A1,A7;
        per cases;
        suppose c >= b;
         then b = c by A5,XXREAL_0:1;
         then a*c = b/2 by A6,XCMPLX_1:93;
         hence contradiction by A3,A9,Th85;
        end;
        suppose A10: c < b;
          2*b > 0 by A2,Lm40;
         then 0 < c/(2*b) by A6,Lm38;
         then a*c < c/(2*b)*b by A6,A10,Th100;
         then A11:     a*c < c/2 by A2,XCMPLX_1:93;
          c/2 < b/2 by A10,Lm28;
         then a*c < b/2 by A11,XXREAL_0:2;
         hence contradiction by A4,A9,XXREAL_0:2;
       end;
   end;
     suppose that
A12:     b <= c and
A13:    c > 0;
     set a = b/(2*c);
        2*c > 0 by A13,Lm40;
       then A14:    a > 0 by A2,Lm38;
A15:    b/c <= 1 by A12,A13,Lm48;
        b/c > 0 by A2,A13,Lm38;
       then b/c*2 > b/c by Th157;
       then b/c > b/c/2 by Th85;
       then b/c > b/(c*2) by XCMPLX_1:79;
       then a < 1 by A15,XXREAL_0:2;
       then A16: a*c >= b by A1,A14;
        per cases;
        suppose c >= b;
          a*c = b/2 by A13,XCMPLX_1:93;
         hence contradiction by A3,A16,Th85;
        end;
        suppose c < b;
         hence contradiction by A12;
       end;
   end;
  end;

theorem :: SPPOL_1:12
  0 <= d & d <= 1 & 0 <= a & 0 <= b & d*a+(1-d)*b=0
   implies d=0 & b=0 or d=1 & a=0 or a=0 & b=0
proof assume
A1: 0<=d & d<=1 & a>=0 & b>=0 & d*a+(1-d)*b=0;
   then d-d<=1-d by Lm7;
   then (d=0 or a=0) & (1-d=0 or b=0) by A1,Th73;
  hence thesis;
end;

theorem :: JGRAPH_6:2`1
  0 <= d & d <= 1 & a <= b implies a <= (1-d)*a+d*b
proof
 assume 0<=d & d<=1 & a<=b;
  then d*a<=d*b by Lm12;
  then (1-d)*a+d*a<=(1-d)*a+d*b by Lm6;
 hence a<=(1-d)*a+d*b;
end;

theorem :: JGRAPH_6:2`2
  0 <= d & d <= 1 & a <= b implies (1-d)*a+d*b <= b
proof
 assume A1: 0<=d & d<=1 & a<=b;
  then 1-d>=0 by Th50;
       then (1-d)*a<=(1-d)*b by A1,Lm12;
  then (1-d)*a+d*b<=(1-d)*b+d*b by Lm6;
 hence (1-d)*a+d*b<=b;
end;

theorem :: JORDAN1A:1
  0 <= d & d <= 1 & a <= b & a <= c implies a <= (1-d)*b+d*c
   proof
    assume A1: 0<=d & d<=1 & a<=b & a<=c;
    then A2:d*a<=d*c by Lm12;
         1-d>=0 by A1,Th50;
    then A3: (1-d)*a<=(1-d)*b by A1,Lm12;
         (1-d)*a+d*a = a;
    hence thesis by A2,A3,Lm6;
   end;

theorem :: JORDAN1A:2
  0 <= d & d <= 1 & b <= a & c <= a implies (1-d)*b+d*c <= a
   proof
    assume A1: 0<=d & d<=1 & a>=b & a>=c;
    then A2: d*a>=d*c by Lm12;
         1-d>=0 by A1,Th50;
    then A3: (1-d)*a>=(1-d)*b by A1,Lm12;
         (1-d)*a+d*a = a;
    hence thesis by A2,A3,Lm6;
   end;

theorem :: JORDAN1:10
  0 <= d & d <= 1 & a < b & a < c implies a < (1-d)*b+d*c
proof
 assume
A1: 0 <= d & d <= 1 & a<b & a<c;
  per cases;
  suppose d=0;
   hence thesis by A1;
  end;
  suppose d=1;
   hence thesis by A1;
  end;
  suppose
A2: not(d=0 or d=1);
   then A3: d>0 & d<1 by A1,XXREAL_0:1;
A4:d*a<d*c by A1,A2,Lm13;
    1-d>0 by A3,Lm21;
   then A5: (1-d)*a<(1-d)*b by A1,Lm13;
     (1-d)*a+d*a=a;
   hence thesis by A4,A5,Lm8;
 end;
end;

theorem :: JORDAN1:11
  0 <= d & d <= 1 & b < a & c < a implies (1-d)*b+d*c < a
proof
 assume
A1: 0 <= d & d <= 1 & a>b & a>c;
 per cases;
  suppose d=0;
   hence thesis by A1;
  end;
  suppose d=1;
   hence thesis by A1;
  end;
  suppose
A2: not(d=0 or d=1);
   then A3: d>0 & d<1 by A1,XXREAL_0:1;
A4:d*a>d*c by A1,A2,Lm13;
    1-d>0 by A3,Lm21;
   then A5: (1-d)*a>(1-d)*b by A1,Lm13;
         (1-d)*a+d*a=a;
   hence thesis by A4,A5,Lm8;
 end;
end;

theorem :: BORSUK_5:48
  0 < d & d < 1 & a <= b & a < c implies a < (1-d)*b+d*c
  proof
   assume
A1:  0 < d & d < 1 & a <= b & a < c;
    then A2: d*a < d*c by Lm13;
        1-d > 0 by A1,Lm21;
    then A3: (1-d)*a <= (1-d)*b by A1,Lm12;
     (1-d)*a+d*a = a;
   hence thesis by A2,A3,Lm8;
  end;

theorem :: BORSUK_5:49
  0 < d & d < 1 & b < a & c <= a implies (1-d)*b+d*c < a
  proof
    assume
A1:  0 < d & d < 1 & a > b & a >= c;
then A2: d*a >= d*c by Lm12;
        1-d > 0 by A1,Lm21;
then A3: (1-d)*a > (1-d)*b by A1,Lm13;
        (1-d)*a+d*a = a;
    hence thesis by A2,A3,Lm8;
  end;

theorem :: SPRECT_3:3
  0 <= d & d <= 1 & a <= (1-d)*a+d*b & b < (1-d)*a+d*b
  implies d = 0
proof assume that
A1: d >= 0 and
A2: d <= 1 and
A3: a <= (1-d)*a + d*b and
A4: b < (1-d)*a + d*b;
 set s = (1-d)*a + d*b;
A5: 1*s = (1-d)*s + d*s;
 assume d <> 0;
  then A6: d*b < d*s by A1,A4,Lm13;
   1-d >= 0 by A2,Th50;
  then (1-d)*a <= (1-d)*s by A3,Lm12;
 hence contradiction by A5,A6,Lm8;
end;

theorem :: SPRECT_3:4
  0 <= d & d <= 1 & (1-d)*a+d*b <= a & (1-d)*a+d*b < b
  implies d = 0
proof assume that
A1: d >= 0 and
A2: d <= 1 and
A3: a >= (1-d)*a + d*b and
A4: b > (1-d)*a + d*b;
  set s = (1-d)*a + d*b;
A5: 1*s = (1-d)*s + d*s;
 assume d <> 0;
  then A6: d*b > d*s by A1,A4,Lm13;
   1-d >= 0 by A2,Th50;
  then (1-d)*a >= (1-d)*s by A3,Lm12;
 hence contradiction by A5,A6,Lm8;
end;

begin :: with numerals z 1, without -1

theorem :: REAL_2:117`2, REAL_2:143`2
  0 < a & a <= b implies 1 <= b/a
  proof
   assume
A1:   0<a & a<=b;
        then b/a>=a/a by Lm27;
     hence b/a>=1 by A1,XCMPLX_1:60;
  end;

theorem :: REAL_2:119`2, REAL_2:143`3
  a < 0 & b <= a implies 1 <= b/a
  proof
   assume
A1:   a < 0 & b<=a;
        then b/a>=a/a by Lm33;
     hence b/a>=1 by A1,XCMPLX_1:60;
  end;

theorem :: REAL_2:143`1
  0 <= a & a <= b implies a/b <= 1 by Lm47;

theorem :: REAL_2:143`3
  b <= a & a <= 0 implies a/b <= 1
  proof
   assume
A1: b <= a;
   assume
A2:   a <= 0;
   per cases by A2;
   suppose a = 0;
    hence thesis;
   end;
   suppose
A3:   a < 0;
     then a/b<=b/b by A1,Lm33;
     hence a/b<=1 by A1,A3,XCMPLX_1:60;
   end;
  end;

theorem :: REAL_2:117`1
  0 <= a & b <= a implies b/a <= 1 by Lm48;

theorem :: REAL_2:119`1
   a <= 0 & a <= b implies b/a <= 1
  proof
   assume A1:a<=0;
   per cases by A1;
   suppose a = 0;
     then a" = 0 by XCMPLX_0:def 7;
     then b*a" = 0;
    hence thesis by XCMPLX_0:def 9;
   end;
   suppose A2:a<0;
   assume
A3:  a <= b;
   assume b/a>1; then b/a*a<1*a by A2,Lm25;
    hence thesis by A2,A3,XCMPLX_1:88;
   end;
  end;

theorem :: REAL_2:118`2, REAL_2:142`2
   0 < a & a < b implies 1 < b/a
  proof
   assume A1:a>0;
   assume
A2: a < b;
   assume b/a<=1; then b/a*a<=1*a by A1,Lm12;
   hence thesis by A1,A2,XCMPLX_1:88;
  end;

theorem :: REAL_2:120`2, REAL_2:142`4
   a < 0 & b < a implies 1 < b/a
  proof
    assume
A1: a < 0 & b<a;
    then b/a>a/a by Lm32;
    hence b/a>1 by A1,XCMPLX_1:60;
  end;

theorem :: REAL_2:142`1
   0 <= a & a < b implies a/b < 1
  proof
   assume
A1:0 <= a & a < b;
   then a/b<b/b by Lm28;
   hence a/b<1 by A1,XCMPLX_1:60;
  end;

theorem :: REAL_2:142`3
   a <= 0 & b < a implies a/b < 1
  proof
   assume
A1:a <= 0 & b<a;
   then a/b<b/b by Lm32;
   hence a/b<1 by A1,XCMPLX_1:60;
  end;

theorem :: REAL_2:118`1
   0 < a & b < a implies b/a < 1
  proof
   assume A1:a > 0;
   assume
A2: b < a;
   assume b/a>=1; then b/a*a>=1*a by A1,Lm12;
   hence thesis by A1,A2,XCMPLX_1:88;
  end;

theorem :: REAL_2:120`1
   a < 0 & a < b implies b/a < 1
  proof
   assume A1:a<0;
   assume
A2: a < b;
   assume b/a>=1; then b/a*a<=1*a by A1,Lm31;
   hence thesis by A1,A2,XCMPLX_1:88;
  end;

begin :: with numerals z 1

theorem :: REAL_2:117`5
   0 <= b & -b <= a implies -1 <= a/b
  proof
   assume A1:b>=0;
   per cases by A1;
   suppose b = 0;
    then b" = 0 by XCMPLX_0:def 7;
    then a*b" = 0;
    hence thesis by XCMPLX_0:def 9;
   end;
   suppose A2:b>0;
   assume
A3:  -b <= a;
    assume a/b<-1; then a/b*b<(-1)*b by A2,Lm13;
     hence thesis by A2,A3,XCMPLX_1:88;
   end;
  end;

theorem :: REAL_2:117`6
   0 <= b & -a <= b implies -1 <= a/b
  proof
   assume A1:b >= 0;
   per cases by A1;
   suppose b = 0;
     then b" = 0 by XCMPLX_0:def 7;
     then a*b" = 0;
    hence thesis by XCMPLX_0:def 9;
   end;
   suppose
A2: b > 0;
   assume
A3:  -a <= b;
    assume a/b<-1; then a/b*b<(-1)*b by A2,Lm13;
     then a<-b by A2,XCMPLX_1:88;
    hence thesis by A3,Th28;
   end;
  end;

theorem :: REAL_2:119`5
   b <= 0 & a <= -b implies -1 <= a/b
  proof
   assume A1:b<=0;
   per cases by A1;
   suppose b=0;
    then b" =0 by XCMPLX_0:def 7;
    then a*b" = 0;
   hence thesis by XCMPLX_0:def 9;
   end;
   suppose
A2: b < 0;
   assume
A3:  a <= -b;
   assume a/b<-1; then a/b*b>(-1)*b by A2,Lm25;
   hence thesis by A2,A3,XCMPLX_1:88;
   end;
  end;

theorem :: REAL_2:119`6
   b <= 0 & b <= -a implies -1 <= a/b
  proof
   assume A1:b<=0;
   per cases by A1;
   suppose b = 0;
    then b" =0 by XCMPLX_0:def 7;
    then a*b" = 0;
   hence thesis by XCMPLX_0:def 9;
   end;
   suppose
A2: b < 0;
   assume
A3:  b <= -a;
   assume a/b<-1; then a/b*b>(-1)*b by A2,Lm25;
    then a>-b by A2,XCMPLX_1:88;
    hence thesis by A3,Th27;
   end;
  end;

theorem :: REAL_2:118`5
   0 < b & -b < a implies -1 < a/b
  proof
   assume A1:b>0;
   assume
A2:  -b < a;
   assume a/b<=-1; then a/b*b<=(-1)*b by A1,Lm12;
   hence thesis by A1,A2,XCMPLX_1:88;
  end;

theorem :: REAL_2:118`6
   0 < b & -a < b implies -1 < a/b
  proof
   assume A1:b>0;
   assume
A2:  -a < b;
   assume a/b<=-1; then a/b*b<=(-1)*b by A1,Lm12;
    then a<=-b by A1,XCMPLX_1:88;
    hence thesis by A2,Th27;
  end;

theorem :: REAL_2:120`5
   b < 0 & a < -b implies -1 < a/b
  proof
   assume A1:b<0;
   assume
A2:   a < -b;
    assume a/b<=-1; then a/b*b>=(-1)*b by A1,Lm31;
    hence thesis by A1,A2,XCMPLX_1:88;
  end;

theorem :: REAL_2:120`6
   b < 0 & b < -a implies -1 < a/b
  proof
   assume A1:b<0;
   assume
A2:  b < -a;
   assume a/b<=-1; then a/b*b>=(-1)*b by A1,Lm31;
    then a>=-b by A1,XCMPLX_1:88;
    hence thesis by A2,Th28;
  end;

theorem :: REAL_2:117`3
   0 < b & a <= -b implies a/b <= -1
  proof
   assume A1:b>0;
   assume
A2:  a <= -b;
    assume a/b>-1; then a/b*b>(-1)*b by A1,Lm13;
    hence thesis by A1,A2,XCMPLX_1:88;
  end;

theorem :: REAL_2:117`4
   0 < b & b <= -a implies a/b <= -1
  proof
   assume A1:b>0;
   assume
A2:  b <= -a;
    assume a/b>-1; then a/b*b>(-1)*b by A1,Lm13;
     then a>-b by A1,XCMPLX_1:88;
    hence thesis by A2,Th27;
  end;

theorem :: REAL_2:119`3
  b < 0 & -b <= a implies a/b <= -1
  proof
   assume A1:b<0;
   assume
A2:  -b <= a;
   assume a/b>-1; then a/b*b<(-1)*b by A1,Lm25;
   hence thesis by A1,A2,XCMPLX_1:88;
  end;

theorem :: REAL_2:119`4
  b < 0 & -a <= b implies a/b <= -1
  proof
   assume A1:b<0;
   assume
A2:  -a <= b;
   assume a/b>-1; then a/b*b<(-1)*b by A1,Lm25;
    then a<-b by A1,XCMPLX_1:88;
    hence thesis by A2,Th28;
  end;

theorem :: REAL_2:118`3
  0 < b & a < -b implies a/b < -1
  proof
   assume A1:b>0;
   assume
A2:  a < -b;
   assume a/b>=-1; then a/b*b>=(-1)*b by A1,Lm12;
   hence thesis by A1,A2,XCMPLX_1:88;
  end;

theorem :: REAL_2:118`4
  0 < b & b < -a implies a/b < -1
  proof
   assume A1:b>0;
   assume
A2:  b < -a;
   assume a/b>=-1; then a/b*b>=(-1)*b by A1,Lm12;
    then a>=-b by A1,XCMPLX_1:88;
    hence thesis by A2,Th28;
  end;

theorem :: REAL_2:120`3
  b < 0 & -b < a implies a/b < -1
  proof
   assume A1:b<0;
   assume
A2:  -b < a;
   assume a/b>=-1; then a/b*b<=(-1)*b by A1,Lm31;
   hence thesis by A1,A2,XCMPLX_1:88;
  end;

theorem :: REAL_2:120`4
  b < 0 & -a < b implies a/b < -1
  proof
   assume A1:b<0;
   assume
A2:  -a < b;
   assume a/b>=-1; then a/b*b<=(-1)*b by A1,Lm31;
    then a<=-b by A1,XCMPLX_1:88;
    hence thesis by A2,Th27;
  end;

begin :: with a universal premise

theorem Th211: :: REAL_2:183
 (for a being real number st 0 < a & a < 1 holds c <= b/a) implies c <= b
  proof
     assume A1:for a st a>0 & a<1 holds b/a>=c;
         now let d;
A2:  d" = 1/d by XCMPLX_1:217;
         assume d>1;
      then d"<1 & d">0 by A2,Lm38,Lm46;
      then b/(d")>=c by A1;
      then b*d/1>=c by A2,XCMPLX_1:78;
      hence b*d>=c;
     end;
      hence b>=c by Th169;
  end;

theorem :: REAL_2:184
  (for a being real number st 1 < a holds b/a <= c) implies b <= c
  proof
       (for a st a>1 holds b/a<=c) implies b<=c
    proof
     assume A1:for a st a>1 holds b/a<=c;
         now let d;
A2:    d" = 1/d by XCMPLX_1:217;
        assume A3:0<d & d<1;
      then d">1" by Lm39;
      then b/(d")<=c by A1;
      then b*d/1<=c by A2,XCMPLX_1:78;
      hence b<=c/d by A3,Th79;
     end;
     hence thesis by Th211;
    end;
   hence thesis;
  end;

theorem :: REAL_2:164
   1 <= a implies a" <= 1
proof
   1 <= a implies a" <= 1" by Lm36;
 hence thesis;
end;

theorem :: SQUARE_1:2
   1 < a implies a" < 1 by Lm46;

theorem :: REAL_2:158
  a <= -1 implies -1 <= a"
 proof
    a <= -1 implies (-1)" <= a" by Lm37;
  hence thesis;
 end;

theorem :: REAL_2:157
  a < -1 implies -1 < a"
 proof
    a < -1 implies (-1)" < a" by Lm35;
  hence thesis;
 end;

begin :: numeral 2

theorem :: SEQ_2:3`1
  0 < a implies 0 < a/2 by Lm29;

theorem :: SEQ_2:4
  0 < a implies a/2 < a by Lm30;

theorem :: BORSUK_6:2
  a <= 1/2 implies 2 * a - 1 <= 0
  proof
    assume a <= 1/2;
     then 2 * a <= 2 * (1/2) by Lm12;
     then 2 * a <= 1 + 0;
    hence thesis by Lm18;
  end;

theorem :: BORSUK_6:2
  a <= 1/2 implies 0 <= 1 - 2 * a
  proof
    assume a <= 1/2;
     then 2 * a <= 2 * (1/2) by Lm12;
     then 2 * a + 0 <= 1;
    hence thesis by Lm16;
  end;

theorem :: BORSUK_6:3
  a >= 1/2 implies 2 * a - 1 >= 0
  proof
    assume a >= 1/2;
     then 2 * a >= 2 * (1/2) by Lm12;
     then 2 * a >= 1 + 0;
    hence thesis by Lm16;
  end;

theorem :: BORSUK_6:3
  a >= 1/2 implies 0 >= 1 - 2 * a
  proof
    assume a >= 1/2;
     then 2 * a >= 2 * (1/2) by Lm12;
     then 2 * a + 0 >= 1;
    hence thesis by Lm18;
  end;

begin :: numeral 3

theorem :: JGRAPH_3:7`1
  0 < a implies a/3 < a
  proof
   assume 0<a;
   then A1: 0<a/3 by Lm38;
   then A2:0+a/3<a/3+a/3 by Lm10;
        a/3+a/3+0<a/3+a/3+a/3 by A1,Lm10;
   hence a/3<a by A2,XXREAL_0:2;
  end;

theorem :: SEQ_4:4
  0 < a implies 0 < a/3
  proof
   assume 0<a;
    then 0/3<a/3 by Lm28;
   hence thesis;
  end;

begin :: numeral 4

theorem :: JGRAPH_3:7`2
  0 < a implies a/4 < a
  proof
   assume 0<a;
   then A1: 0<a/4 by Lm38;
   then A2:0+a/4<a/4+a/4 by Lm10;
   then a/4< a/4+a/4+a/4 by A1,Lm10;
   then a/4+a/4<a/4+a/4+a/4+a/4 by Lm10;
   hence a/4<a by A2,XXREAL_0:2;
  end;

theorem :: SEQ_2:3`2
  0 < a implies 0 < a/4
  proof
   assume
A1: 0<a;
    0/4=0;
   hence 0<a/4 by A1,Lm28;
  end;

theorem :: REAL_2:64
   for a,b st b<>0 ex c st a=b/c
  proof let a,b;
   assume
A1:  b<>0;
    then consider c being complex number such that
A2:   a=b/c by XCMPLX_1:234;
   per cases;
   suppose c = 0;
    hence thesis by A2;
   end;
   suppose c <> 0;
     then b/c <> 0 by A1,XCMPLX_1:50;
     then c = b/a by A2,XCMPLX_1:54;
     then reconsider c as real number;
     take c;
    thus thesis by A2;
   end;
  end;

begin :: Addenda

:: from TOPREAL3, 2006.07.15, A.T.

theorem
 r<s implies r < (r+s)/2 & (r+s)/2 < s
 proof assume r<s;
  then A1: r/2 < s/2 by Lm28;
  then r/2 + r/2 < r/2 + s/2 by Lm10;
  hence r < (r+s)/2;
      r/2 + s/2 < s/2 + s/2 by A1,Lm10;
  hence thesis;
 end;

:: missing, 2007.01.16, A.T.

registration
 cluster -> ext-real Element of REAL;
 coherence
  proof let x be Element of REAL;
     x in REAL;
   hence thesis by NUMBERS:31,XXREAL_0:def 1;
  end;
end;

reserve p,q,r,s,t for ext-real number;

theorem Th229:
 r < t implies ex s st r < s & s < t
 proof assume
A1: r < t;
  then A2:  r <> +infty & t <> -infty by XXREAL_0:3,5;
  per cases by A2,XXREAL_0:14;
  suppose that
A3: r = -infty and
A4: t = +infty;
    consider s being Element of REAL;
   take s;
   thus thesis by A3,A4,XXREAL_0:9,12;
  end;
  suppose that
A5: r = -infty and
A6: t in REAL;
    reconsider t as real number by A6,XREAL_0:def 1;
    consider s being real number such that
A7:   s < t by Th4;
   take s;
     s in REAL by XREAL_0:def 1;
   hence r < s by A5,XXREAL_0:12;
   thus thesis by A7;
  end;
  suppose that
A8: r in REAL and
A9: t = +infty;
    reconsider r' = r as real number by A8,XREAL_0:def 1;
    consider s being real number such that
A10:   r' < s by Th3;
   take s;
   thus r < s by A10;
     s in REAL by XREAL_0:def 1;
   hence thesis by A9,XXREAL_0:9;
  end;
  suppose that
A11: r in REAL and
A12: t in REAL;
    reconsider r,t as real number by A11,A12,XREAL_0:def 1;
    consider s being real number such that
A13:   r < s & s < t by A1,Th7;
   take s;
   thus thesis by A13;
  end;
 end;

theorem
  r < s & (for q st r < q & q < s holds t <= q) implies t <= r
 proof assume that
A1: r < s and
A2: for q st r < q & q < s holds t <= q;
    for q st r < q holds t <= q
    proof let q such that
A3:   r < q;
     per cases;
     suppose q < s;
      hence thesis by A2,A3;
     end;
     suppose
A4:     s <= q;
       consider p such that
A5:     r < p and
A6:     p < s by A1,Th229;
        t <= p by A2,A5,A6;
       then t <= s by A6,XXREAL_0:2;
      hence thesis by A4,XXREAL_0:2;
     end;
    end;
  hence thesis by Th229;
 end;

theorem
  r < s & (for q st r < q & q < s holds q <= t) implies s <= t
 proof assume that
A1: r < s and
A2: for q st r < q & q < s holds q <= t;
    for q st t < q holds s <= q
    proof let q such that
A3:   t < q and
A4:   q < s;
     per cases;
     suppose r < q;
      hence thesis by A2,A3,A4;
     end;
     suppose
A5:     q <= r;
       consider p such that
A6:     r < p and
A7:     p < s by A1,Th229;
        p <= t by A2,A6,A7;
       then r <= t by A6,XXREAL_0:2;
      hence thesis by A3,A5,XXREAL_0:2;
     end;
    end;
  hence thesis by Th229;
 end;

