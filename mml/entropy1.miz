:: Definition and Some Properties of Information Entropy
::  by Bo Zhang and Yatsuka Nakamura
:: 
:: Received July 9, 2007
:: Copyright (c) 2007 Association of Mizar Users

environ

 vocabularies ARYTM, FINSEQ_1, BOOLE, ARYTM_1, FUNCT_1, NAT_1, MATRIXC1,
      MATRPROB, RELAT_1, RVSUM_1, ARYTM_3, FINSEQ_2, INCSP_1, MATRIXR1, RAT_1,
      FCONT_1, RLVECT_1, MATRIX_1, TREES_1, PARTFUN1, CAT_3, POWER, TAYLOR_1,
      LIMFUNC1, FDIFF_1, RCOMP_1, SEQ_1, SUPINF_2, PARTFUN3, ROUGHS_1,
      FINSEQ_4, ENTROPY1, FUNCOP_1;
 notations TARSKI, SUBSET_1, XBOOLE_0, XCMPLX_0, XXREAL_0, XXREAL_1, XREAL_0,
      REAL_1, NAT_1, NAT_D, ORDINAL1, NUMBERS, PARTFUN1, RELAT_1, FUNCT_1,
      SEQ_1, ZFMISC_1, FUNCOP_1, BINOP_1, RLVECT_1, BINOP_2, FINSOP_1, FUNCT_2,
      RVSUM_1, FVSUM_1, FINSEQ_1, FINSEQ_2, FINSEQOP, NEWTON, MATRIXR1,
      MATRIX_1, EUCLID, PARTFUN3, RFUNCT_2, POWER, LIMFUNC1, RCOMP_1, FCONT_1,
      MATRPROB, FDIFF_1, TAYLOR_1, FINSEQ_4;
 constructors FVSUM_1, SETWOP_2, NUMBERS, REAL_1, XREAL_0, XCMPLX_0, XBOOLE_0,
      NAT_D, FINSEQ_4, BINOP_2, MATRIX_1, SEQ_1, MATRIX_4, MATRIX_3, FINSOP_1,
      VECTSP_1, MATRIXR1, MEMBERED, BINOP_1, XXREAL_0, FINSEQ_3, PARTFUN1,
      FUNCOP_1, FINSEQ_2, RVSUM_1, NEWTON, SQUARE_1, MATRLIN, EUCLID_2, NAT_1,
      MATRPROB, PARTFUN3, PARTFUN2, FDIFF_1, RFUNCT_2, RCOMP_1, FCONT_1, POWER,
      LIMFUNC1, SIN_COS, TAYLOR_1, INTEGRA1, RCOMP_2, XXREAL_1;
 registrations MATRPROB, RELSET_1, NAT_1, RAT_1, FINSEQ_2, MEMBERED, FUNCT_1,
      FUNCT_2, BINOP_2, ORDINAL1, SEQ_1, XREAL_0, FCONT_3, RCOMP_1, XXREAL_0,
      NUMBERS, ARYTM_3, XBOOLE_0;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, XBOOLE_0, PARTFUN3, FUNCOP_1, FINSEQ_2, LIMFUNC1,
      XXREAL_1, RCOMP_1;
 theorems FUNCT_1, SEQ_1, NAT_1, REAL_2, MATRIX_2, XREAL_1, NAT_2, MATRIXR1,
      GOBOARD1, FINSEQ_4, XBOOLE_0, EUCLID_2, PROB_3, INTEGRA5, FINSEQ_1,
      MATRIX_1, GOBRD13, FINSEQ_2, PARTFUN3, FINSEQ_3, RVSUM_1, POWER, FDIFF_1,
      RELSET_1, TREES_1, XBOOLE_1, TAYLOR_1, LIMFUNC1, SQUARE_1, RELAT_1,
      ROLLE, FCONT_1, XCMPLX_1, INTEGRA1, XXREAL_0, REAL_1, ORDINAL1, MATRPROB,
      FUNCOP_1, PEPIN, NAT_D;
 schemes FINSEQ_2, SEQ_1, NAT_1, RECDEF_1, FINSEQ_1, MATRIX_1;

begin :: Preliminaries

 reserve D for non empty set,
         i,j,k,l for Element of NAT,
         n,m for Nat,
         x,y for set,
         a,b,c,b1,r,r1,r2 for Real,
         p,q for FinSequence of REAL,
         MR,MR1 for Matrix of REAL;

theorem th477:
  k <> 0 & i < l & l <= j & k divides l implies
    i div k < j div k
  proof assume
a0: k <> 0 & i < l & l <= j & k divides l; then
aa: l mod k = 0 by PEPIN:6;
a1: k > 0 by a0; then
    l = k * (l div k) + (l mod k) by NAT_D:2
     .= k * (l div k) by aa; then
    k / k * (l div k) = l / k by XCMPLX_1:75; then
a3: 1 * (l div k) = l / k by a0,XCMPLX_1:60;
    i = k * (i div k) + (i mod k) by a1,NAT_D:2; then
    k / k * (i div k) = (i - (i mod k)) / k by XCMPLX_1:75; then
a5: 1 * (i div k) = (i - (i mod k)) / k by a0,XCMPLX_1:60;
    i + (i mod k) >= i by NAT_1:11; then
    i - (i mod k) <= (i + (i mod k)) - (i mod k) by XREAL_1:11; then
    i - (i mod k) < l by a0,XXREAL_0:2; then
a7: (i - (i mod k)) / k < l / k by a1,REAL_1:73;
    l div k <= j div k by a0,NAT_2:26;
    hence thesis by a3,a5,a7,XXREAL_0:2;
  end;

theorem th300:
  r > 0 implies ((log_ number_e).r <= r - 1) &
    (r = 1 iff (log_ number_e).r = r - 1) &
    (r <> 1 iff (log_ number_e).r < r - 1)
  proof
    set Z = right_open_halfline(0);
    defpred P[set] means $1 in REAL;
    deffunc G(Real) = $1-1;
    consider f1 being PartFunc of REAL,REAL such that
aa: (for r holds r in dom f1 iff P[r]) &
    for r st r in dom f1 holds f1.r=G(r) from SEQ_1:sch 3;
    dom f1 is Subset of REAL by RELSET_1:12; then
ab: dom f1 = REAL by aa,FDIFF_1:1;
    defpred P[set] means $1 in Z;
    deffunc G(Real) = (log_ number_e).$1;
    reconsider f2 = log_ number_e as PartFunc of REAL, REAL;
ae: dom f2 = Z by TAYLOR_1:def 2;
    defpred P[set] means $1 in Z;
    deffunc F(Real) = ($1 - 1) - (log_ number_e).$1;
    set f = f1 - f2;
a2: dom f = Z /\ REAL by ae,ab,SEQ_1:def 4
         .= Z by XBOOLE_1:28;
a5: for r st r in Z holds f1.r = 1*r + (-1)
    proof let r such that r in Z;
      f1.r = r - 1 by aa,ab;
      hence thesis;
    end;
ai: f1 is_differentiable_on Z & for r st r in Z holds diff(f1,r) = 1
    proof
i0:   f1 is_differentiable_on Z & for r st r in Z holds (f1`|Z).r = 1
        by a5,ab,FDIFF_1:31;
      now let r such that
i1:     r in Z;
        thus diff(f1,r) = (f1`|Z).r by i0,i1,FDIFF_1:def 8
          .= 1 by i1,a5,ab,FDIFF_1:31;
      end;
      hence thesis by a5,ab,FDIFF_1:31;
    end;
ah: f is_differentiable_on Z & for r st r in Z holds diff(f,r)=1 - 1/r
    proof
      thus
j2:   f is_differentiable_on Z
        by a2,FDIFF_1:27,ai,TAYLOR_1:18,TAYLOR_1:def 3;
      hereby let r such that
j3:     r in Z;
j4:     ((f1-f2)`|Z).r = diff(f1,r) - diff(f2,r)
          by j3,FDIFF_1:27,ai,a2,TAYLOR_1:18,TAYLOR_1:def 3
          .= 1 - diff(f2,r) by ai,j3
          .= 1 - 1/r by j3,TAYLOR_1:18,TAYLOR_1:def 3;
        thus diff(f,r) = 1 - 1/r by j4,j2,j3,FDIFF_1:def 8;
      end;
    end;
    1 in {g where g is Real: 0 < g}; then
a7: 1 in Z by LIMFUNC1:152;
ag: number_e > 0 & number_e <> 1 by TAYLOR_1:11;
a8: for x be Real st x > 0 holds f.x = (x - 1) - (log_ number_e).x
    proof let x be Real such that
b1:   x > 0;
      x in {g where g is Real : 0<g} by b1; then
bb:   x in dom f by a2,LIMFUNC1:152; then
bc:   f.x = f1.x - f2.x by SEQ_1:def 4;
      x in dom f1 /\ dom f2 by bb,SEQ_1:def 4; then
      x in dom f1 & x in dom f2 by XBOOLE_0:def 3;
      hence thesis by aa,bc;
    end; then
a9: f.1 = (1 - 1) - (log_ number_e).1 .= - (log_ number_e).1
      .= - log(number_e,1) by a7,TAYLOR_1:def 2
      .= - 0 by ag,POWER:59 .= 0;
    set Z1 = right_open_halfline(1);
A0: Z1 c= Z by LIMFUNC1:8; then
A1: f is_differentiable_on Z1 by ah,FDIFF_1:34;
A2: for r st r in Z1 holds diff(f,r) > 0
    proof let r such that
k1:   r in Z1;
      r in {g where g is Real: 1 < g} by LIMFUNC1:152,k1; then
      ex g1 being Real st g1 = r & 1 < g1; then
k2:   1/r < 1 by SQUARE_1:2;
      diff(f,r) = 1 - 1/r by A0,k1,ah;
      hence thesis by k2,XREAL_1:52;
    end;
    set Z2 = ].0,1.[;
A3: Z2 c= Z by LIMFUNC1:11; then
A4: f is_differentiable_on Z2 by ah,FDIFF_1:34;
A5: for r st r in Z2 holds diff(f,r) < 0
    proof let r; assume
l1:   r in Z2; then
      ex g1 being Real st g1 = r & 0 < g1 & g1 < 1; then
l2:   1 < 1/r by XREAL_1:189;
      diff(f,r) = 1 - 1/r by A3,l1,ah;
      hence thesis by l2,XREAL_1:51;
    end;
A8: f is_continuous_on Z & f is_continuous_on Z1 & f is_continuous_on Z2
      by ah,A1,A4,FDIFF_1:33;
B1: for r st r in Z1 holds f.r > 0
    proof
      assume not for r st r in Z1 holds f.r > 0; then
      consider r1 such that
m1:   r1 in Z1 & f.r1 <= 0;
      r1 in {g where g is Real: 1 < g} by LIMFUNC1:152,m1; then
m2:   ex g1 being Real st g1 = r1 & 1 < g1;
      [.1,r1.] c= Z by LIMFUNC1:21; then
m3:   f is_continuous_on [.1,r1.] by A8,FCONT_1:17;
      ].1,r1.[ c= Z by LIMFUNC1:20;  then
m4:   f is_differentiable_on ].1,r1.[ by ah,FDIFF_1:34;
      per cases by m1;
      suppose f.r1 = 0; then
        consider r2 being Real such that
m6:     r2 in ].1,r1.[ & diff(f,r2)=0 by a9,m2,m3,m4,ROLLE:1;
        ex g1 being Real st g1 = r2 & 1 < g1 & g1 < r1 by m6; then
        r2 in {g where g is Real : 1<g}; then
        r2 in Z1 by LIMFUNC1:152;
        hence contradiction by m6,A2;
      end;
      suppose
n1:     f.r1 < 0;
        consider r3 being Real such that
n2:     r3 in ].1,r1.[ & diff(f,r3)=(f.r1-f.1)/(r1-1) by m2,m3,m4,ROLLE:3;
        ex g1 being Real st g1 = r3 & 1 < g1 & g1 < r1 by n2; then
        r3 in {g where g is Real : 1<g}; then
n4:     r3 in Z1 by LIMFUNC1:152;
        r1 -1 > 0 by m2,XREAL_1:52; then
        diff(f,r3) < 0 by n2,n1,a9,REAL_2:128;
        hence contradiction by n4,A2;
      end;
    end;
B2: for r st r in Z2 holds f.r > 0
    proof
      assume not for r st r in Z2 holds f.r > 0; then
      consider r1 such that
o1:   r1 in Z2 & f.r1 <= 0;
o2:   ex g1 being Real st g1 = r1 & 0<g1 & g1<1 by o1;
      [.r1,1.] c= Z by o2,LIMFUNC1:21; then
o3:   f is_continuous_on [.r1,1.] by A8,FCONT_1:17;
      ].r1,1.[ c= Z by o2,LIMFUNC1:20; then
o4:   f is_differentiable_on ].r1,1.[ by ah,FDIFF_1:34;
      per cases by o1;
      suppose f.r1 = 0; then
        consider r2 being Real such that
o6:     r2 in ].r1,1.[ & diff(f,r2)=0 by a9,o2,o3,o4,ROLLE:1;
        ex g1 being Real st g1 = r2 & r1 < g1 & g1 < 1 by o6; then
        r2 in {g where g is Real : 0<g & g<1 } by o2;
        hence contradiction by o6,A5;
      end;
      suppose
p1:     f.r1 < 0;
        consider r3 being Real such that
p2:     r3 in ].r1,1.[ & diff(f,r3)=(f.1-f.r1)/(1-r1) by o2,o3,o4,ROLLE:3;
        ex g1 being Real st g1 = r3 & r1 < g1 & g1 < 1 by p2; then
p4:     r3 in Z2 by o2;
p5:     f.1 - f.r1 > 0 by p1,a9,XREAL_1:52;
        1 -r1 > 0 by o2,XREAL_1:52; then
        diff(f,r3) > 0 by p2,p5,REAL_2:127;
        hence contradiction by p4,A5;
      end;
    end;
B3: for r st r > 0 & r < 1 holds f.r > 0
    proof let r such that
q1:   r > 0 & r < 1;
      r in {g where g is Real : 0<g & g<1 } by q1;
      hence thesis by B2;
    end;
B4: for r st r > 1 holds f.r > 0
    proof let r such that
r1:   r > 1;
      r in {g where g is Real : 1<g } by r1; then
      r in Z1 by LIMFUNC1:152;
      hence thesis by B1;
    end;
B5: for r st r > 0 holds f.r >= 0
    proof let r such that
s1:   r > 0;
      per cases by XXREAL_0:1;
      suppose r < 1;
        hence thesis by s1,B3;
      end;
      suppose r = 1;
        hence thesis by a9;
      end;
      suppose r > 1;
        hence thesis by B4;
      end;
    end;
    assume
B6: r > 0; then
    f.r >= 0 by B5; then
    (r - 1) - (log_ number_e).r >= 0 by B6,a8; then
    (r - 1) - 0 >= (log_ number_e).r by XREAL_1:13;
    hence (log_ number_e).r <= r - 1;
    thus
B7: r = 1 iff (log_ number_e).r = r - 1
    proof
      hereby assume r = 1; then
        (r - 1) - (log_ number_e).r = 0 by a9,a8;
        hence (log_ number_e).r = r - 1;
      end;
      assume (log_ number_e).r = r - 1; then
u1:   (r - 1) - (log_ number_e).r = 0;
      assume
u2:   r <> 1;
      per cases by u2,XXREAL_0:1;
      suppose r < 1; then
        f.r > 0 by B3,B6;
        hence contradiction by B6,a8,u1;
      end;
      suppose
u4:     r > 1; then
        f.r > 0 by B4;
        hence contradiction by u1,u4,a8;
      end;
    end;
    thus r <> 1 iff (log_ number_e).r < r - 1
    proof
      hereby assume r <> 1; then
v1:     r-1>0 or 1-r>0 by XREAL_1:57;
        per cases by v1,REAL_2:106;
        suppose r < 1; then
          f.r > 0 by B3,B6; then
          (r - 1) - (log_ number_e).r > 0 by B6,a8;
          hence (log_ number_e).r < r - 1 by REAL_2:106;
        end;
        suppose
v2:       r > 1; then
          f.r > 0 by B4; then
          (r - 1) - (log_ number_e).r > 0 by v2,a8;
          hence (log_ number_e).r < r - 1 by REAL_2:106;
        end;
      end;
      assume
v3:   (log_ number_e).r < r - 1;
      assume r = 1;
      hence contradiction by v3,B7;
    end;
  end;

theorem th300a:
  r > 0 implies (log(number_e,r) <= r - 1) &
    (r = 1 iff log(number_e,r) = r - 1) &
    (r <> 1 iff log(number_e,r) < r - 1)
  proof assume
a1: r>0; then
    r in {g where g is Real : 0<g}; then
    r in right_open_halfline(0) by LIMFUNC1:152; then
    log(number_e,r) = (log_ number_e).r by TAYLOR_1:def 2;
    hence thesis by a1,th300;
  end;

theorem th290b:
  a>1 & b>1 implies log(a,b) > 0
  proof assume
a1: a>1 & b>1; then
a2: a to_power log(a,b) > 1 by POWER:def 3;
    assume
a3: log(a,b) <=0;
    per cases by a3;
    suppose log(a,b) =0;
     hence contradiction by a2,POWER:29;
    end;
    suppose log(a,b) <0;
     hence contradiction by a1,a2,POWER:41;
    end;
  end;

theorem th290:
  a>0 & a<>1 & b>0 implies -log(a,b) = log(a,1/b)
  proof assume
a1: a>0 & a<>1 & b>0;
    thus -log(a,b) = 0 - log(a,b) .= log(a,1) - log(a,b) by a1,POWER:59
      .= log(a,1/b) by a1,POWER:62;
  end;

theorem th291:
  a>0 & a<>1 & b>=0 & c>=0 implies b*c*log(a,b*c) = b*c*log(a,b)+b*c*log(a,c)
  proof assume
a1: a>0 & a<>1 & b>=0 & c>=0;
    per cases by a1;
    suppose b>0 & c = 0;
      hence thesis;
    end;
    suppose b=0 & c = 0;
      hence thesis;
    end;
    suppose b=0 & c > 0;
      hence thesis;
    end;
    suppose b>0 & c > 0;
      hence b*c*log(a,b*c) = b*c*(log(a,b) + log(a,c)) by a1,POWER:61
        .= b*c*log(a,b)+b*c*log(a,c);
    end;
  end;

theorem th306:
  for q,q1,q2 being FinSequence of REAL st len q1 = len q & len q1 = len q2 &
    (for k st k in dom q1 holds q.k = q1.k + q2.k) holds
      Sum q = Sum q1 + Sum q2
  proof let q,q1,q2 be FinSequence of REAL such that
a0: len q1 = len q & len q1 = len q2 &
    for k st k in dom q1 holds q.k = q1.k + q2.k;
    for k st k in dom q1 holds q.k = q1/.k + q2/.k
    proof let k such that
a1:  k in dom q1;
a2:  k in dom q2 by a0,a1,FINSEQ_3:31;
     thus q.k = q1.k + q2.k by a1,a0
       .= q1/.k + q2.k by a1,FINSEQ_4:def 4
       .= q1/.k + q2/.k by a2,FINSEQ_4:def 4;
    end;
    hence thesis by a0,INTEGRA1:23;
  end;

theorem th306a:
  for q,q1,q2 being FinSequence of REAL st len q1 = len q & len q1 = len q2 &
    (for k st k in dom q1 holds q.k = q1.k - q2.k) holds
      Sum q = Sum q1 - Sum q2
  proof let q,q1,q2 be FinSequence of REAL such that
a0: len q1 = len q & len q1 = len q2 &
    for k st k in dom q1 holds q.k = q1.k - q2.k;
    for k st k in dom q1 holds q.k = q1/.k - q2/.k
    proof let k such that
a1:   k in dom q1;
a2:   k in dom q2 by a0,a1,FINSEQ_3:31;
     thus q.k = q1.k - q2.k by a1,a0
       .= q1/.k - q2.k by a1,FINSEQ_4:def 4
       .= q1/.k - q2/.k by a2,FINSEQ_4:def 4;
    end;
    hence thesis by a0,INTEGRA1:24;
  end;

theorem th419:
  len p >= 1 implies ex q st len q = len p & q.1 = p.1 &
    (for k st 0 <> k & k < len p holds q.(k+1) = q.k + p.(k+1)) &
    Sum p = q.(len p)
  proof assume
a1: len p >= 1;
    consider r be Real_Sequence such that
a2: r.1 = p.1 &
    (for n st 0 <> n & n < len p holds
      r.(n+1) = r.n + p.(n+1)) & Sum p = r.(len p) by a1,PROB_3:68;
    deffunc F(Nat) = r.$1;
    consider q being FinSequence such that
a3: len q = len p & for k st k in Seg len p holds q.k = F(k)
      from FINSEQ_1:sch 2;
az: 1 in Seg len p by a1,FINSEQ_1:3; 
    rng q c= REAL
    proof let x such that
c1:   x in rng q;
      consider j such that
c2:   j in dom q & q.j = x by FINSEQ_2:11,c1;
      j in Seg len p by a3,c2,FINSEQ_1:def 3; then
      q.j = r.j by a3;
      hence x in REAL by c2;
    end; then
    reconsider q as FinSequence of REAL by FINSEQ_1:def 4;
a6: now let k such that
b1:   0 <> k & k < len p;
      k >= 1 & k <= len p &
      k+1 >= 1 & k+1 <= len p by b1,NAT_1:13,NAT_1:14; then
b4:   k in Seg len p & k+1 in Seg len p by FINSEQ_1:3;
      hence q.(k+1) = r.(k+1) by a3 .= r.k + p.(k+1) by a2,b1
        .= q.k + p.(k+1) by b4,a3;
    end;
a7: len p in Seg len p by a1,FINSEQ_1:5;
    take q;
    thus thesis by a6,a2,a7,az,a3;
  end;

notation let p;
  synonym p is nonnegative for p is nonnegative-yielding;
end;

definition let p;
  redefine attr p is nonnegative means :Def109:
    for i st i in dom p holds p.i >= 0;
  compatibility
  proof
    hereby assume
A1:   p is nonnegative;
      let i such that
AA:   i in dom p;
      p.i in rng p by AA,FUNCT_1:12;
      hence p.i >= 0 by A1,PARTFUN3:def 4;
    end;
    assume
A2: for i st i in dom p holds p.i >= 0;
    let r be real number;
    assume r in rng p; then
    consider j such that
A3: j in dom p & r = p.j by FINSEQ_2:11;
    thus thesis by A3,A2;
  end;
end;

registration
  cluster nonnegative FinSequence of REAL;
  existence
  proof
a1: <*1*> is nonnegative
    proof
      set p=<*1*>;
      for i st i in dom p holds p.i >= 0
      proof let i such that
a2:     i in dom p;
        i in Seg 1 by a2,FINSEQ_1:def 8; then
        i >= 1 & i <= 1 by FINSEQ_1:3; then
        i = 1 by XXREAL_0:1;
        hence thesis by FINSEQ_1:57;
      end;
      hence thesis by Def109;
    end;
    take <*1*>;
    thus thesis by a1;
  end;
end;

theorem th293a:
  p is nonnegative & r>=0 implies r*p is nonnegative
  proof assume
a1: p is nonnegative & r>=0;
    now let k such that
b1:   k in dom (r*p);
      k in dom p by b1,SEQ_1:def 6; then
b2:   p.k >= 0 by a1,Def109;
      (r*p).k = r*(p.k) by RVSUM_1:66;
      hence (r*p).k >= 0 by b2,a1,XREAL_1:129;
    end;
    hence thesis by Def109;
  end;

definition let p,k;
  pred p has_onlyone_value_in k means :Def16:
    k in dom p & for i st i in dom p & i<>k holds p.i=0;
end;

theorem
  p has_onlyone_value_in k & i<>k implies p.i=0
  proof assume
a1: p has_onlyone_value_in k & i<>k;
    per cases;
    suppose not i in dom p;
      hence thesis by FUNCT_1:def 4;
    end;
    suppose i in dom p;
      hence thesis by Def16,a1;
    end;
  end;

theorem th232:
  len p = len q & p has_onlyone_value_in k implies
    mlt(p,q) has_onlyone_value_in k & mlt(p,q).k = p.k * q.k
  proof assume
a1: len p = len q & p has_onlyone_value_in k;
    len mlt(p,q) = len p by a1,MATRPROB:30; then
a3: dom mlt(p,q) = dom p by FINSEQ_3:31; then
a4: k in dom mlt(p,q) by Def16,a1;
    now let i such that
b1:   i in dom mlt(p,q) & i <> k;
      thus (mlt(p,q)).i = p.i * q.i by b1,RVSUM_1:86
        .= 0*q.i by Def16,b1,a3,a1
        .= 0;
    end;
    hence thesis by a4,Def16,RVSUM_1:86;
  end;

theorem th233:
  p has_onlyone_value_in k implies Sum p = p.k
  proof
    assume
A1: k in dom p & for i st i in dom p & i<>k holds p.i=0;
    reconsider a=p.k as Element of REAL;
A2: 1 <= k & k <= len p by A1,FINSEQ_3:27;
    reconsider p1=p|Seg k as FinSequence of REAL by FINSEQ_1:23;
    p1 c= p by TREES_1:def 1; then
    consider p2 being FinSequence such that
A4: p=p1^p2 by TREES_1:8;
    k in Seg k by A2,FINSEQ_1:5; then
    k in (dom p) /\ (Seg k) by A1,XBOOLE_0:def 3; then
A5: k in dom p1 by RELAT_1:90; then
    p1 <> {} by FINSEQ_1:26; then
    len p1<>0 by FINSEQ_1:25; then
    consider p3 being FinSequence of REAL,x being Element of REAL such that
A6: p1=p3^<*x*> by FINSEQ_2:22;
    reconsider p2 as FinSequence of REAL by A4,FINSEQ_1:50;
A7: k =len p1 by A2,FINSEQ_1:21
      .=len p3+ len <*x*> by A6,FINSEQ_1:35
      .= len p3 + 1 by FINSEQ_1:56; then
A8: x =p1.k by A6,FINSEQ_1:59 .=a by A4,A5,FINSEQ_1:def 7;
A9: for i st i in Seg len p2 holds p2.i=0
    proof let i;
      assume i in Seg len p2; then
A10:  i in dom p2 by FINSEQ_1:def 3;
A11:  1 <= k & k <=len p1 by A5,FINSEQ_3:27;
      0<>i by A10,FINSEQ_3:27; then
A12:  len p1<>len p1 + i;
      len p1 <= len p1 + i by NAT_1:12; then
A13:  k<>len p1 + i by A11,A12,REAL_1:def 5;
A14:  (len p1+ i) in dom p by A4,A10,FINSEQ_1:41;
      thus p2.i=p.(len p1+i) by A4,A10,FINSEQ_1:def 7
        .=0 by A1,A13,A14;
    end;
A15:p2=len p2 |->0
    proof
A16:  len (len p2 |->0)=len p2 by FINSEQ_2:69;
      now let j be Element of NAT; assume
A17:    j in Seg len p2;
        hence p2.j =0 by A9
          .= (len p2 |->0).j by A17,FINSEQ_1:4,FINSEQ_2:71;
      end;
      hence thesis by A16,FINSEQ_2:10;
    end;
A18:for i st i in Seg len p3 holds p3.i=0
    proof let i; assume
A19:  i in Seg len p3; then
      1<=i & i <= len p3 by FINSEQ_1:3;then
A20:  i <> k by A7,NAT_1:13;
A21:  i in dom p3 by A19,FINSEQ_1:def 3; then
A22:  i in dom p1 by A6,FINSEQ_2:18; then
A23:  i in dom p & i in dom p1 by A4,FINSEQ_2:18;
      thus p3.i=p1.i by A6,A21,FINSEQ_1:def 7
        .=p.i by A4,A22,FINSEQ_1:def 7
        .=0 by A1,A20,A23;
    end;
A24:p3=len p3 |->0
    proof
A25:  len (len p3 |->0)=len p3 by FINSEQ_2:69;
      now let j be Element of NAT; assume
A26:    j in Seg len p3;
        hence p3.j =0 by A18
          .= (len p3 |->0).j by A26,FINSEQ_1:4,FINSEQ_2:71;
      end;
      hence thesis by A25,FINSEQ_2:10;
    end;
    Sum p=Sum(p3^<*x*>) + Sum(len p2 |->0) by A4,A6,A15,RVSUM_1:105
      .=Sum(p3^<*x*>) + 0 by RVSUM_1:111
      .=Sum(len p3 |->0) + x by A24,RVSUM_1:104
      .=0 + a by A8,RVSUM_1:111
      .=p.k;
    hence thesis;
  end;

theorem th390:
  p is nonnegative implies
    for k st k in dom p & p.k = Sum p holds p has_onlyone_value_in k
  proof assume
a1: p is nonnegative;
    let k1 being Element of NAT such that
a3: k1 in dom p & p.k1 =Sum p;
a4: k1 >=1 & k1 <= len p by FINSEQ_3:27,a3;
    consider j1 being Nat such that
a5: k1 + j1 = len p by NAT_1:10,a4;
    reconsider j1 as Element of NAT by ORDINAL1:def 13;
    not ex i st i in dom p & i<>k1 & p.i <> 0
    proof assume ex i st i in dom p & i<>k1 & p.i <> 0; then
      consider k2 being Element of NAT such that
b1:   k2 in dom p & k2<>k1 & p.k2<>0;
b2:   k2 >=1 & k2 <= len p by FINSEQ_3:27,b1;
      consider j2 being Nat such that
b3:   k2 + j2 = len p by NAT_1:10,b2;
      reconsider j2 as Element of NAT by ORDINAL1:def 13;
b4:   p.k2 > 0 by b1,a1,Def109;
      per cases by b1,REAL_1:def 5;
      suppose k1 > k2; then
ca:     not k1 in Seg k2 by FINSEQ_1:3;
        consider p1,p2 being FinSequence of REAL such that
c1:     len p1 = k2 & len p2 = j2 & p = p1 ^ p2 by b3,FINSEQ_2:26;
        k2 in Seg k2 by FINSEQ_1:5,b2; then
c3:     k2 in dom p1 by c1,FINSEQ_1:def 3;
        p1.k2 > 0 & for k st k in dom p1 holds p1.k >= 0
        proof
          thus p1.k2 > 0 by FINSEQ_1:def 7,c3,c1,b4;
          let k such that
d1:       k in dom p1;
d2:       dom p1 c= dom p by c1,FINSEQ_1:39;
          p1.k = p.k by FINSEQ_1:def 7,d1,c1;
          hence thesis by d2,d1,a1,Def109;
        end; then
cz:     Sum p1 > 0 by c3,RVSUM_1:115;
        not k1 in dom p1 by c1,ca,FINSEQ_1:def 3; then
        consider kk be Element of NAT such that
c4:     kk in dom p2 & k1 = k2 + kk by a3,FINSEQ_1:38,c1;
c5:     p2.kk = p.k1 by c1,c4,FINSEQ_1:def 7;
        for k being Nat st k in dom p2 holds p2.k >= 0
        proof let k be Nat such that
e1:       k in dom p2;
          k >= 1 & k <= j2 by c1,FINSEQ_3:27,e1; then
          k2+k>=1+0 & k2+k <= len p by b3,XREAL_1:9; then
e3:       k2+k in dom p by FINSEQ_3:27;
          p2.k = p.(k2+k) by c1,e1,FINSEQ_1:def 7;
          hence thesis by e3,a1,Def109;
        end; then
cy:     Sum p2 >= p.k1 by c5,MATRPROB:5,c4;
        Sum p = Sum p1 + Sum p2 by c1,RVSUM_1:105; then
        Sum p > p.k1 + 0 by cz,cy,XREAL_1:10;
        hence thesis by a3;
      end;
      suppose k1 < k2; then
fa:     not k2 in Seg k1 by FINSEQ_1:3;
        consider p1,p2 being FinSequence of REAL such that
f1:     len p1 = k1 & len p2 = j1 & p = p1 ^ p2 by a5,FINSEQ_2:26;
        k1 in Seg k1 by FINSEQ_1:5,a4; then
f3:     k1 in dom p1 by f1,FINSEQ_1:def 3;
        p1.k1 = p.k1 & for k be Nat st k in dom p1 holds p1.k >= 0
        proof
          thus p1.k1 = p.k1 by FINSEQ_1:def 7,f3,f1;
          let k be Nat such that
g1:       k in dom p1;
g2:       dom p1 c= dom p by f1,FINSEQ_1:39;
          p1.k = p.k by FINSEQ_1:def 7,g1,f1;
          hence thesis by g1,g2,a1,Def109;
        end; then
fz:     Sum p1 >= p.k1 by f3,MATRPROB:5;
        not k2 in dom p1 by f1,fa,FINSEQ_1:def 3; then
        consider kk be Element of NAT such that
f4:     kk in dom p2 & k2 = k1 + kk by b1,FINSEQ_1:38,f1;
f5:     p2.kk = p.k2 by f1,f4,FINSEQ_1:def 7;
        for k st k in dom p2 holds p2.k >= 0
        proof let k such that
h1:       k in dom p2;
          k >= 1 & k <= j1 by f1,FINSEQ_3:27,h1; then
          k1+k>=1+0 & k1+k <= len p by a5,XREAL_1:9; then
h3:       k1+k in dom p by FINSEQ_3:27;
          p2.k =p.(k1+k) by f1,h1,FINSEQ_1:def 7;
          hence thesis by h3,a1,Def109;
        end; then
fy:     Sum p2 > 0 by f4,f5,b4,RVSUM_1:115;
        Sum p = Sum p1 + Sum p2 by f1,RVSUM_1:105; then
        Sum p > p.k1 + 0 by fz,fy,XREAL_1:10;
        hence thesis by a3;
      end;
    end;
    hence thesis by a3,Def16;
  end;

registration
  cluster ProbFinS -> non empty nonnegative FinSequence of REAL;
  coherence
  proof let p be FinSequence of REAL; assume
a1: p is ProbFinS;
    hence p is non empty by RVSUM_1:102,MATRPROB:def 5;
    for i st i in dom p holds p.i >= 0 by a1,MATRPROB:def 5;
    hence thesis by Def109;
  end;
end;

theorem th391a:
  for p being ProbFinS FinSequence of REAL
   for k st k in dom p & p.k = 1 holds p has_onlyone_value_in k
  proof let p be ProbFinS FinSequence of REAL;
    let k such that
a1: k in dom p & p.k = 1;
    p.k = Sum p by MATRPROB:def 5,a1;
    hence thesis by a1,th390;
  end;

theorem th270a:
  for i being non empty Nat holds
    (i |-> (1/i)) is ProbFinS FinSequence of REAL
  proof let i be non empty Nat;
    reconsider i as non empty Element of NAT by ORDINAL1:def 13;
A2: Sum(i |-> (1/i)) = i * (1/i) by RVSUM_1:110 .= 1 by XCMPLX_1:107;
    for k st k in dom (i |-> (1/i)) holds (i |-> (1/i)).k >= 0
    proof let k such that
A4:   k in dom (i |-> (1/i));
      k in Seg i by A4,FUNCOP_1:19;
      hence thesis by FUNCOP_1:13;
    end;
    hence thesis by A2,MATRPROB:def 5;
  end;

registration
  cluster with_sum=1 -> non empty-yielding Matrix of REAL;
  coherence
  proof let M be Matrix of REAL such that
a1: M is with_sum=1;
    assume
aa: M is empty-yielding;
    per cases by aa,GOBOARD1:def 5;
    suppose len M = 0; then
      SumAll M = 0 by MATRPROB:23;
      hence thesis by a1,MATRPROB:def 7;
    end;
    suppose width M = 0 & len M > 0; then
      reconsider M1 = M as Matrix of len M,0,REAL by MATRIX_1:20;
      SumAll M1 = 0 by MATRPROB:24;
      hence thesis by a1,MATRPROB:def 7;
    end;
  end;
  cluster Joint_Probability -> non empty-yielding Matrix of REAL;
  coherence
  proof let M be Matrix of REAL such that
b1: M is Joint_Probability;
    reconsider M1=M as with_sum=1 Matrix of REAL by b1,MATRPROB:def 8;
    M1 is non empty-yielding Matrix of REAL;
    hence thesis;
  end;
end;

theorem
  for M being Matrix of REAL st M = {} holds SumAll M = 0
  proof let M be Matrix of REAL;
    assume M = {}; then
    reconsider M1=M as Matrix of 0,width M,REAL by MATRIX_1:13;
    len M1 = 0 by MATRIX_1:23;
    hence thesis by MATRPROB:23;
  end;

theorem th350:
  for M being Matrix of D holds
    for i st i in dom M holds dom(M.i) = Seg width M
  proof let M be Matrix of D;
    let i; assume i in dom M;
    hence dom(M.i) = dom Line(M,i) by MATRIX_2:18
      .= Seg len Line(M,i) by FINSEQ_1:def 3
      .= Seg width M by MATRIX_1:def 8;
  end;

theorem th294:
   MR is m-nonnegative iff
    (for i st i in dom MR holds Line(MR,i) is nonnegative)
  proof
    hereby assume
a0: MR is m-nonnegative;
      let i such that
a1:   i in dom MR;
      now let k such that
b1:     k in dom Line(MR,i);
b2:     k in dom (MR.i) by a1,b1,MATRIX_2:18; then
b3:     k in Seg width MR by a1,th350;
b4:     [i,k] in Indices MR by b2,MATRPROB:13,a1;
        (Line(MR,i)).k = MR*(i,k) by MATRIX_1:def 8,b3;
        hence (Line(MR,i)).k >= 0 by a0,MATRPROB:def 6,b4;
      end;
      hence Line(MR,i) is nonnegative by Def109;
    end;
    assume
c1: for i st i in dom MR holds Line(MR,i) is nonnegative;
    now let i,j such that
c2:   [i,j] in Indices MR;
c3:   i in Seg len MR & j in Seg width MR by c2,MATRPROB:12; then
      j in Seg len Line(MR,i) by MATRIX_1:def 8; then
c4:   i in dom MR & j in dom Line(MR,i) by c3,FINSEQ_1:def 3; then
c5:   Line(MR,i) is nonnegative by c1;
      MR*(i,j) = Line(MR,i).j by c3,MATRIX_1:def 8;
      hence MR*(i,j) >= 0 by c5,c4,Def109;
    end;
    hence MR is m-nonnegative by MATRPROB:def 6;
  end;

begin :: Properties on Transformations between Vector and Matrix

theorem th339:
  for j st j in dom p holds Col(LineVec2Mx p,j) = <*p.j*>
  proof
    set M = LineVec2Mx p;
    let j such that
a1: j in dom p;
a2: len Col(M,j) = len M by MATRIX_1:def 9; then
aa: len Col(M,j) = 1 by MATRIXR1:def 10;
a3: dom <*p.j*> = Seg 1 by FINSEQ_1:def 8; then
a5: dom Col(M,j) = dom <*p.j*> by aa,FINSEQ_1:def 3;
    now let k such that
b1:   k in dom Col(M,j);
      k >= 1  & k <= 1 by b1,a5,a3,FINSEQ_1:3; then
b2:   k = 1 by XXREAL_0:1;
      k in dom M by a2,b1,FINSEQ_3:31;
      hence (Col(M,j)).k = M*(k,j) by MATRIX_1:def 9
         .= p.j by b2,a1,MATRIXR1:def 10
         .= (<*p.j*>).k by b2,FINSEQ_1:def 8;
    end;
    hence thesis by a5,FINSEQ_1:17;
  end;

theorem th330:
  for p being non empty FinSequence of REAL for q being FinSequence of REAL
    for M being Matrix of REAL holds
    M = (ColVec2Mx p) * (LineVec2Mx q) iff
      (len M = len p & width M = len q &
      for i,j st [i,j] in Indices M holds M*(i,j) = p.i * q.j)
  proof let p be non empty FinSequence of REAL;
        let q be FinSequence of REAL;
        let M be Matrix of REAL;
    set M1 = ColVec2Mx p;
    set M2 = LineVec2Mx q;
    len p <> 0 by FINSEQ_1:25; then
a2: len p > 0;
ab: len M1 = len p & width M1 = 1 &
    for j st j in dom p holds M1.j = <*p.j*> by a2,MATRIXR1:def 9;
ac: len M2 = 1 & width M2 = len q &
    for j st j in dom q holds M2*(1,j) =q.j by MATRIXR1:def 10;
    hereby assume
b1:   M = M1 * M2;
b4:   len M = len M1 & width M = width M2 &
      for i,j st [i,j] in Indices M holds
        M*(i,j) = Line(M1,i) "*" Col(M2,j) by b1,ab,ac,MATRPROB:39;
      thus len M = len p & width M = len q
        by b1,ab,ac,MATRPROB:39;
      thus for i,j st [i,j] in Indices M holds M*(i,j) = p.i * q.j
      proof let i,j such that
c1:     [i,j] in Indices M;
c2:     i in Seg len M & j in Seg width M by c1,MATRPROB:12; then
c4:     i in dom M1 by b4,FINSEQ_1:def 3;
c5:     i in dom p & j in dom q by c2,ab,ac,b4,FINSEQ_1:def 3;
c6:     Line(M1,i) = M1.i by c4,MATRIX_2:18 .= <*p.i*> by a2,MATRIXR1:def 9,c5;
        thus M*(i,j) = Line(M1,i) "*" Col(M2,j) by b1,ab,ac,MATRPROB:39,c1
          .= <*p.i*> "*" <*q.j*> by c5,th339,c6
          .= Sum mlt(<*p.i*>,<*q.j*>) by EUCLID_2:def 1
          .= Sum <*p.i * q.j*> by RVSUM_1:89
          .= p.i * q.j by RVSUM_1:103;
      end;
    end;
    assume
d1: len M = len p & width M = len q &
      for i,j st [i,j] in Indices M holds M*(i,j) = p.i * q.j;
    for i,j st [i,j] in Indices M holds
      M*(i,j) = Line(M1,i) "*" Col(M2,j)
    proof let i,j such that
e1:   [i,j] in Indices M;
e2:     i in Seg len M & j in Seg width M by e1,MATRPROB:12; then
e4:     i in dom M1 by d1,ab,FINSEQ_1:def 3;
e5:     i in dom p & j in dom q by e2,d1,FINSEQ_1:def 3;
e6:     <*p.i*> = M1.i by a2,MATRIXR1:def 9,e5 .= Line(M1,i) by e4,MATRIX_2:18;
      thus M*(i,j) = p.i * q.j by d1,e1
      .= Sum <*p.i * q.j*> by RVSUM_1:103
      .= Sum mlt(<*p.i*>,<*q.j*>) by RVSUM_1:89
      .= <*p.i*> "*" <*q.j*> by EUCLID_2:def 1
      .= Line(M1,i) "*" Col(M2,j) by e5,th339,e6;
    end;
    hence M = M1 * M2 by d1,ab,ac,MATRPROB:39;
  end;

theorem th331:
  for p being non empty FinSequence of REAL for q being FinSequence of REAL
    for M being Matrix of REAL holds
    M = (ColVec2Mx p) * (LineVec2Mx q) iff
      (len M = len p & width M = len q &
      for i st i in dom M holds Line(M,i) = p.i * q)
  proof let p be non empty FinSequence of REAL,
            q be FinSequence of REAL,
            M be Matrix of REAL;
    set M1 = ColVec2Mx p;
    set M2 = LineVec2Mx q;
    hereby assume
b2:   M = M1 * M2;
      thus len M = len p & width M = len q by b2,th330;
      thus for i st i in dom M holds Line(M,i) = p.i * q
      proof let i such that
c1:     i in dom M;
c2:     i in Seg len M by FINSEQ_1:def 3,c1;
c3:     dom Line(M,i) = Seg len Line(M,i) by FINSEQ_1:def 3
          .= Seg width M by MATRIX_1:def 8 .= Seg len q by b2,th330
          .= dom q by FINSEQ_1:def 3 .= dom(p.i * q) by SEQ_1:def 6;
        for j st j in dom Line(M,i) holds (Line(M,i)).j = (p.i * q).j
        proof let j such that
d1:       j in dom Line(M,i);
          j in Seg len Line(M,i) by FINSEQ_1:def 3,d1; then
d3:       j in Seg width M by MATRIX_1:def 8;then
d4:       [i,j] in Indices M by c2,MATRPROB:12;
          thus (Line(M,i)).j = M*(i,j) by d3,MATRIX_1:def 8
            .= p.i * q.j by d4,b2,th330 .= (p.i * q).j by RVSUM_1:66;
        end;
        hence thesis by c3,FINSEQ_1:17;
      end;
    end;
    assume
e1: len M = len p & width M = len q &
    for i st i in dom M holds Line(M,i) = p.i * q;
    for i,j st [i,j] in Indices M holds M*(i,j) = p.i * q.j
    proof let i,j such that
e2:   [i,j] in Indices M;
e3:   i in dom M & j in dom(M.i) by e2,MATRPROB:13;
      j in Seg width M by e2,MATRPROB:12;
      hence M*(i,j) = (Line(M,i)).j by MATRIX_1:def 8
        .= (p.i * q).j by e3,e1
        .= p.i * q.j by RVSUM_1:66;
    end;
    hence M = M1 * M2 by e1,th330;
  end;

theorem th332:
  for p,q being ProbFinS FinSequence of REAL holds
    (ColVec2Mx p) * (LineVec2Mx q) is Joint_Probability
  proof let p,q be ProbFinS FinSequence of REAL;
    set M = (ColVec2Mx p) * (LineVec2Mx q);
a1: len M = len p & width M = len q &
    for i st i in dom M holds Line(M,i) = p.i * q by th331;
a4: M is m-nonnegative
    proof
      now let i,j such that
b1:     [i,j] in Indices M;
        i in Seg len p & j in Seg len q by a1,MATRPROB:12,b1; then
        i in dom p & j in dom q by FINSEQ_1:def 3; then
b2:     p.i >= 0 & q.j >= 0 by MATRPROB:def 5;
        M*(i,j) = p.i * q.j by b1,th330;
        hence M*(i,j) >= 0 by b2,XREAL_1:129;
      end;
      hence thesis by MATRPROB:def 6;
    end;
    M is with_sum=1
    proof
c0:   LineSum M = p
      proof
        set M1=LineSum M;
c1:     len M1 = len M &
        for k st k in Seg len M holds M1.k = Sum(Line(M,k)) by MATRPROB:20;
c2:     dom M1 = dom p & dom M1 = dom M by c1,a1,FINSEQ_3:31;
        now let k such that
c3:       k in dom M1;
          k in Seg len M by FINSEQ_1:def 3,c1,c3;
          hence M1.k = Sum(Line(M,k)) by MATRPROB:20 
            .= Sum(p.k * q) by th331,c3,c2
            .= p.k * Sum q by RVSUM_1:117 .= p.k * 1 by MATRPROB:def 5
            .= p.k;
        end;
        hence thesis by c2,FINSEQ_1:17;
      end;
      SumAll M = Sum LineSum M by MATRPROB:def 3 
        .= 1 by MATRPROB:def 5,c0;
      hence thesis by MATRPROB:def 7;
    end;
    hence thesis by a4,MATRPROB:def 8;
  end;

definition let n;
           let MR be Matrix of n,REAL;
  attr MR is diagonal means :Def15:
    for i,j st [i,j] in Indices MR & MR*(i,j) <> 0 holds i=j;
end;

registration let n;
  cluster diagonal Matrix of n,REAL;
  existence
  proof
    reconsider n1=n as Element of NAT by ORDINAL1:def 13;
    deffunc F(set,set) = 0;
    consider M being Matrix of n1,REAL such that
aa: for i,j being Nat st [i,j] in Indices M holds M*(i,j) = F(i,j)
      from MATRIX_1:sch 1;
ab: for i,j st [i,j] in Indices M & M*(i,j) <> 0 holds i=j by aa;
    reconsider M1=M as Matrix of n,REAL;
    take M1;
    thus thesis by ab,Def15;
  end;
end;

theorem th231:
  for MR being Matrix of n,REAL holds
    MR is diagonal iff
      for i st i in dom MR holds Line(MR,i) has_onlyone_value_in i
  proof let MR be Matrix of n,REAL;
    len MR = n & width MR = n by MATRIX_1:25; then
aa: dom MR = Seg width MR by FINSEQ_1:def 3;
    hereby assume
a1:   MR is diagonal;
      now let i such that
b1:     i in dom MR;
b2:     len Line(MR,i) = width MR by MATRIX_1:def 8; then
b3:     i in Seg width MR & i in dom Line(MR,i) by FINSEQ_1:def 3,aa,b1;
        now let j such that
c1:       j in dom Line(MR,i) & j<>i;
c2:       j in dom (MR.i) & j in Seg width MR
            by FINSEQ_1:def 3,b2,b1,c1,MATRIX_2:18; then
c3:       [i,j] in Indices MR by b1,MATRPROB:13;
          thus Line(MR,i).j = MR*(i,j) by c2,MATRIX_1:def 8
            .= 0 by c3,a1,Def15,c1;
        end;
        hence Line(MR,i) has_onlyone_value_in i by b3,Def16;
      end;
      hence for i st i in dom MR holds Line(MR,i) has_onlyone_value_in i;
    end;
    assume
d1: for i st i in dom MR holds Line(MR,i) has_onlyone_value_in i;
    for i,j st [i,j] in Indices MR & MR*(i,j) <> 0 holds i=j
    proof let i,j such that
e1:   [i,j] in Indices MR & MR*(i,j) <> 0; assume
ee:   i<>j;
e2:   i in dom MR & j in dom(MR.i) by MATRPROB:13,e1; then
e3:   j in dom Line(MR,i) by MATRIX_2:18;
e4:   Line(MR,i) has_onlyone_value_in i by e2,d1;
      len Line(MR,i) = width MR by MATRIX_1:def 8; then
      dom Line(MR,i) = Seg width MR by FINSEQ_1:def 3; then
      MR*(i,j) = Line(MR,i).j by e3,MATRIX_1:def 8
        .= 0 by ee,e4,e3,Def16;
      hence thesis by e1;
    end;
    hence thesis by Def15;
  end;

definition let p;
  func Vec2DiagMx p -> diagonal Matrix of len p,REAL means :Def17:
    for j st j in dom p holds it*(j,j) = p.j;
  existence
  proof
    defpred P[Nat,Nat,Element of REAL] means
    ($1=$2 implies $3=p.$1) & ($1 <> $2 implies $3=0);
A1: for i,j being Nat st [i,j] in [:Seg len p, Seg len p:]
      for x1,x2 being Element of REAL st P[i,j,x1] & P[i,j,x2]
      holds x1 = x2;
A2: for i,j being Nat st [i,j] in [:Seg len p, Seg len p:]
     ex x being Element of REAL st P[i,j,x]
    proof
      let i,j be Nat;
      assume [i,j] in [:Seg len p, Seg len p:];
      (i=j implies P[i,j,p.i]) & (i<>j implies P[i,j,0]);
      hence thesis;
    end;
    consider M being Matrix of len p,REAL such that
a1: for i,j being Nat st [i,j] in Indices M holds P[i,j,M*(i,j)]
      from MATRIX_1:sch 2(A1,A2);
a2: width M = len p & len M = len p by MATRIX_1:25;
    for i,j st [i,j] in Indices M & M*(i,j) <> 0 holds i=j by a1; then
    reconsider M1 = M as diagonal Matrix of len p,REAL by Def15;
a3: Seg width M1 = dom p & dom M1 = dom p & Seg len M1 = dom p
      by a2,FINSEQ_1:def 3,FINSEQ_3:31;
bc: for j st j in dom p holds M1*(j,j) = p.j
    proof let j such that
c1:   j in dom p;
      [j,j] in Indices M1 by MATRPROB:12,c1,a3;
      hence M1*(j,j) = p.j by a1;
    end;
    take M1;
    thus thesis by bc;
  end;
  uniqueness
  proof let M1,M2 be diagonal Matrix of len p,REAL such that
d1: for j st j in dom p holds M1*(j,j) = p.j and
d2: for j st j in dom p holds M2*(j,j) = p.j;
d3: Indices M1 = Indices M2 by MATRIX_1:27;
    width M1 = len p & width M2 = len p by MATRIX_1:25; then
d4: Seg width M1 = dom p & Seg width M2 = dom p by FINSEQ_1:def 3;
    now let i,j being Nat such that
e1:   [i,j] in Indices M1;
      reconsider i1=i,j1=j as Element of NAT by ORDINAL1:def 13;
ee:   [i1,j1] in Indices M1 by e1; then
e2:   j1 in Seg width M1 by MATRPROB:12;
      per cases;
      suppose
f1:     i=j;
        hence M1*(i,j) = p.j by e2,d4,d1 .= M2*(i,j) by f1,e2,d4,d2;
      end;
      suppose
g1:     i<>j;
        hence M1*(i,j) = 0 by ee,Def15 .= M2*(i,j) by g1,ee,Def15,d3;
      end;
    end;
    hence thesis by MATRIX_1:28;
  end;
end;

theorem th234:
  MR=Vec2DiagMx p iff
    len MR = len p & width MR = len p & for i st i in dom MR holds
      Line(MR,i) has_onlyone_value_in i & Line(MR,i).i=p.i
  proof
    hereby assume
bb:   MR=Vec2DiagMx p; then
      reconsider M1=MR as diagonal Matrix of len p,REAL;
bd:   len M1 = len p & width M1 = len p by MATRIX_1:25; then
b3:   dom MR = dom p & Seg width MR = dom p by FINSEQ_1:def 3,FINSEQ_3:31;
      thus len MR = len p & width MR = len p by bd;
      thus for i st i in dom MR holds Line(MR,i) has_onlyone_value_in i &
        Line(MR,i).i=p.i
      proof let i such that
c1:     i in dom MR;
c2:     Line(M1,i) has_onlyone_value_in i by c1,th231;
        Line(MR,i).i = MR*(i,i) by c1,b3,MATRIX_1:def 8
          .= p.i by c1,b3,Def17,bb;
        hence thesis by c2;
      end;
    end; assume
a1: len MR = len p & width MR = len p &
    for i st i in dom MR holds Line(MR,i) has_onlyone_value_in i &
    Line(MR,i).i=p.i; then
    reconsider MM = MR as Matrix of len p,REAL by MATRIX_2:7;
    for i st i in dom MM holds Line(MM,i) has_onlyone_value_in i by a1; then
    reconsider MM as diagonal Matrix of len p,REAL by th231;
    for j st j in dom p holds MM*(j,j) = p.j
    proof let j such that
aa:   j in dom p;
ab:   dom MM = dom p & Seg width MM = dom p by a1,FINSEQ_1:def 3,FINSEQ_3:31;
      hence MM*(j,j) = Line(MM,j).j by aa,MATRIX_1:def 8 .= p.j by aa,ab,a1;
    end;
    hence thesis by Def17;
  end;

theorem th235:
  len p = len MR implies
    (MR1 = (Vec2DiagMx p) * MR iff
      len MR1 = len p & width MR1 = width MR &
      for i,j st [i,j] in Indices MR1 holds MR1*(i,j) = p.i * (MR*(i,j)))
  proof assume
a1: len p = len MR;
aa: dom p = dom MR by FINSEQ_3:31,a1;
    set MM = Vec2DiagMx p;
a2: len MM = len p & width MM = len p &
    for i st i in dom MM holds Line(MM,i) has_onlyone_value_in i &
      Line(MM,i).i=p.i by th234;
    hereby assume
b1:   MR1 = MM * MR;
      len MR1 = len MM & width MR1 = width MR &
      for i,j st [i,j] in Indices MR1 holds
        MR1*(i,j) = Line(MM,i) "*" Col(MR,j) by b1,a1,a2,MATRPROB:39; then
b6:   dom MR1 = dom p & dom MR1 = dom MM & Seg width MR1 = Seg width MR
        by a2,FINSEQ_3:31;
      thus len MR1 = len p & width MR1 = width MR by b1,a1,a2,MATRPROB:39;
      thus for i,j st [i,j] in Indices MR1 holds MR1*(i,j) = p.i *(MR*(i,j))
      proof let i,j such that
c1:     [i,j] in Indices MR1;
        set q = mlt(Line(MM,i),Col(MR,j));
        i in Seg len MR1 & j in Seg width MR1 by c1,MATRPROB:12; then
c3:     i in dom MR1 by FINSEQ_1:def 3; then
c4:     Line(MM,i) has_onlyone_value_in i & Line(MM,i).i=p.i by th234,b6;
        len Line(MM,i) = width MM & len Col(MR,j) = len MR
          by MATRIX_1:def 8,MATRIX_1:def 9; then
c7:     q has_onlyone_value_in i & q.i = (Line(MM,i).i) * (Col(MR,j).i)
          by a2,a1,c4,th232;
        thus MR1*(i,j) = Line(MM,i) "*" Col(MR,j) by c1,b1,a1,a2,MATRPROB:39
          .= Sum q by EUCLID_2:def 1 
          .= p.i * (Col(MR,j).i) by c7,th233,c4
          .= p.i*(MR*(i,j)) by aa,b6,c3,MATRIX_1:def 9;
      end;
    end;
    assume
d1: len MR1 = len p & width MR1 = width MR &
    for i,j st [i,j] in Indices MR1 holds MR1*(i,j) = p.i*(MR*(i,j)); then
dd: dom MR1 = dom MM & dom MR = dom MR1 by FINSEQ_3:31,a1,a2;
    for i,j st [i,j] in Indices MR1 holds
      MR1*(i,j) = Line(MM,i) "*" Col(MR,j)
    proof let i,j such that
e1:   [i,j] in Indices MR1;
      set q = mlt(Line(MM,i),Col(MR,j));
      i in Seg len MR1 & j in Seg width MR1 by e1,MATRPROB:12; then
e3:   i in dom MR1 by FINSEQ_1:def 3; then
e4:   Line(MM,i) has_onlyone_value_in i & Line(MM,i).i=p.i by th234,dd;
      len Line(MM,i) = width MM & len Col(MR,j) = len MR
        by MATRIX_1:def 8,MATRIX_1:def 9; then
e7:   q has_onlyone_value_in i & q.i = (Line(MM,i).i) * (Col(MR,j).i)
        by a2,a1,e4,th232;
      thus MR1*(i,j) = p.i*(MR*(i,j)) by e1,d1
        .= p.i * (Col(MR,j).i) by dd,e3,MATRIX_1:def 9
        .= Sum q by e7,th233,e4
        .= Line(MM,i) "*" Col(MR,j) by EUCLID_2:def 1;
    end;
    hence MR1 = MM * MR by d1,a1,a2,MATRPROB:39;
  end;

theorem th236:
  len p = len MR implies
    (MR1 = (Vec2DiagMx p) * MR iff
      len MR1 = len p & width MR1 = width MR &
      for i st i in dom MR1 holds Line(MR1,i) = p.i * Line(MR,i))
  proof
    assume
a0: len p = len MR;
    set MM = Vec2DiagMx p;
    hereby assume
bz:   MR1 = MM * MR; then
b2:   len MR1 = len p & width MR1 = width MR &
      for i,j st [i,j] in Indices MR1 holds MR1*(i,j) = p.i*(MR*(i,j))
        by a0,th235;
      thus len MR1 = len p & width MR1 = width MR by bz,a0,th235;
      thus for i st i in dom MR1 holds Line(MR1,i) = p.i * Line(MR,i)
      proof let i such that
c1:     i in dom MR1;
c2:     i in Seg len MR1 by FINSEQ_1:def 3,c1;
c3:     dom Line(MR1,i) = Seg len Line(MR1,i) by FINSEQ_1:def 3
          .= Seg width MR1 by MATRIX_1:def 8
          .= Seg len Line(MR,i) by b2,MATRIX_1:def 8
          .= dom Line(MR,i) by FINSEQ_1:def 3
          .= dom(p.i * Line(MR,i)) by SEQ_1:def 6;
        for j st j in dom Line(MR1,i) holds
          (Line(MR1,i)).j = (p.i * Line(MR,i)).j
        proof let j such that
d1:       j in dom Line(MR1,i);
          j in Seg len Line(MR1,i) by FINSEQ_1:def 3,d1; then
d3:       j in Seg width MR1 by MATRIX_1:def 8;then
d4:       [i,j] in Indices MR1 by c2,MATRPROB:12;
          thus (Line(MR1,i)).j = MR1*(i,j) by d3,MATRIX_1:def 8
            .= p.i*(MR*(i,j)) by d4,bz,a0,th235
            .= p.i*(Line(MR,i).j) by MATRIX_1:def 8,d3,b2
            .= (p.i * Line(MR,i)).j by RVSUM_1:66;
        end;
        hence thesis by c3,FINSEQ_1:17;
      end;
    end;
    assume
e1: len MR1 = len p & width MR1 = width MR &
    for i st i in dom MR1 holds Line(MR1,i) = p.i*Line(MR,i);
    for i,j st [i,j] in Indices MR1 holds MR1*(i,j) = p.i*(MR*(i,j))
    proof let i,j such that
e2:   [i,j] in Indices MR1;
e3:   i in dom MR1 & j in dom(MR1.i) by e2,MATRPROB:13;
e4:   j in Seg width MR1 by e2,MATRPROB:12;
      hence MR1*(i,j) = (Line(MR1,i)).j by MATRIX_1:def 8
        .= (p.i*Line(MR,i)).j by e3,e1
        .= p.i * Line(MR,i).j by RVSUM_1:66
        .= p.i*(MR*(i,j)) by MATRIX_1:def 8,e4,e1;
    end;
    hence MR1 = MM * MR by a0,e1,th235;
  end;

theorem th237:
  for p being ProbFinS FinSequence of REAL
  for M being non empty-yielding Conditional_Probability Matrix of REAL
    st len p = len M holds (Vec2DiagMx p) * M is Joint_Probability
  proof let p be ProbFinS FinSequence of REAL;
        let M be non empty-yielding Conditional_Probability Matrix of REAL;
    assume
a0: len p = len M;
    set M1 = (Vec2DiagMx p) * M;
a1: len M1 = len p & width M1 = width M &
    for i,j st [i,j] in Indices M1 holds M1*(i,j) = p.i*(M*(i,j))
      by a0,th235;
a4: M1 is m-nonnegative
    proof
      now let i,j such that
b1:     [i,j] in Indices M1;
        i in Seg len p & i in Seg len M & j in Seg width M
          by MATRPROB:12,b1,a0,a1; then
        i in dom p & [i,j] in Indices M by FINSEQ_1:def 3,MATRPROB:12; then
b2:     p.i >= 0 & M*(i,j) >= 0 by MATRPROB:def 5,MATRPROB:def 6;
        M1*(i,j) = p.i *(M*(i,j)) by b1,a0,th235;
        hence M1*(i,j) >= 0 by b2,XREAL_1:129;
      end;
      hence thesis by MATRPROB:def 6;
    end;
    M1 is with_sum=1
    proof
c0:   LineSum M1 = p
      proof
        set M2=LineSum M1;
c1:     len M2 = len M1 &
        for k st k in Seg len M1 holds M2.k = Sum(Line(M1,k)) by MATRPROB:20;
c2:     dom M2 = dom M1 & dom M2 = dom M & dom M2 = dom p
          by a0,c1,a1,FINSEQ_3:31;
        now let k such that
c3:       k in dom M2;
          k in Seg len M1 by FINSEQ_1:def 3,c1,c3;
          hence M2.k = Sum(Line(M1,k)) by MATRPROB:20
            .= Sum(p.k*Line(M,k)) by a0,th236,c3,c2
            .= p.k * Sum Line(M,k) by RVSUM_1:117
            .= p.k * Sum(M.k) by c2,c3,MATRIX_2:18
            .= p.k * 1 by c2,c3,MATRPROB:def 9
            .= p.k;
        end;
        hence thesis by c2,FINSEQ_1:17;
      end;
      SumAll M1 = Sum LineSum M1 by MATRPROB:def 3 
        .= 1 by MATRPROB:def 5,c0;
      hence thesis by MATRPROB:def 7;
    end;
    hence thesis by a4,MATRPROB:def 8;
  end;

theorem th409:
  for M being Matrix of D for p being FinSequence of D* st
    len p = len M & p.1 = M.1 &
    (for k st k >= 1 & k < len M holds p.(k+1) = (p.k) ^ M.(k+1)) holds
      for k st k in dom p holds len (p.k) = k * width M
  proof let M be Matrix of D;
        let p be FinSequence of D* such that
a1: len p = len M and
a2: p.1 = M.1 and
a3: for k st k >= 1 & k < len M holds p.(k+1) = (p.k) ^ M.(k+1);
    defpred P[Element of NAT] means
      $1 >= 1 & $1 <= len M implies len (p.$1) = $1 * width M;
aa: P[0];
ab: for n being Element of NAT st P[n] holds P[n + 1]
    proof let n be Element of NAT;
      assume
b1:   n >= 1 & n <= len M implies len (p.n) = n * width M;
      assume
b2:   n + 1 >= 1 & n + 1 <= len M;
      now per cases;
        suppose
b3:       n = 0; then
b4:       1 in dom M by FINSEQ_3:27,b2;
          thus thesis by b3,a2,GOBRD13:4,b4;
        end;
          suppose 
bz:         n <> 0; then
b6:         n >= 1 by NAT_1:14;
bb:         n + 1 in dom M by b2,FINSEQ_3:27;
            n < len M by b2,NAT_1:13; then
            p.(n + 1) = (p.n) ^ M.(n+1) by b6,a3; then
            len (p.(n + 1)) = len (p.n) + len (M.(n+1)) by FINSEQ_1:35
              .= (n * width M) + width M 
                by bb,GOBRD13:4,b1,bz,b2,NAT_1:13,NAT_1:14
              .= (n + 1) * width M;
            hence thesis;
          end;
        end;
        hence len (p.(n + 1)) = (n + 1) * width M;
      end;
b8:   for n being Element of NAT holds P[n] from NAT_1:sch 1(aa,ab);
      let k such that
c1:   k in dom p;
      k in Seg len p by c1,FINSEQ_1:def 3; then
      k >= 1 & k <= len p by FINSEQ_1:3;
      hence thesis by b8,a1;
    end;

theorem th409a:
  for M being Matrix of D for p being FinSequence of D* st
    len p = len M & p.1 = M.1 &
    (for k st k >= 1 & k < len M holds p.(k+1) = (p.k) ^ M.(k+1)) holds
      for i,j st i in dom p & j in dom p & i <= j holds dom (p.i) c= dom (p.j)
  proof let M be Matrix of D;
        let p be FinSequence of D* such that
a2: len p = len M & p.1 = M.1 &
    for k st k >= 1 & k < len M holds p.(k+1) = (p.k) ^ M.(k+1);
    let i,j such that
a3: i in dom p & j in dom p & i <= j;
    len (p.i) = i * width M & len (p.j) = j * width M by a2,a3,th409; then
    len (p.i) <= len (p.j) by a3,NAT_1:4; then
    Seg len (p.i) c= Seg len (p.j) by FINSEQ_1:7; then
    dom (p.i) c= Seg len (p.j) by FINSEQ_1:def 3;
    hence thesis by FINSEQ_1:def 3;
  end;

theorem th412:
  for M being Matrix of D for p being FinSequence of D* st
    len p = len M & p.1 = M.1 &
    (for k st k >= 1 & k < len M holds p.(k+1) = (p.k) ^ M.(k+1)) holds
      len (p.1) = width M & (for j st [1,j] in Indices M holds
      j in dom (p.1) & (p.1).j = M*(1,j))
  proof let M be Matrix of D;
        let p be FinSequence of D* such that
a1: len p = len M & p.1 = M.1 &
    for k st k >= 1 & k < len M holds p.(k+1) = (p.k) ^ M.(k+1);
    per cases;
    suppose
b1:   len M = 0; then
b2:   width M = 0 by MATRIX_1:def 4;
      p = {} by a1,b1,FINSEQ_1:25; then
      dom p = {} by FINSEQ_1:26; then
      p.1 = {} by FUNCT_1:def 4;
      hence len(p.1)=width M by b2,FINSEQ_1:25;
      let j such that
b3:   [1,j] in Indices M;
      thus thesis by b1,FINSEQ_1:4,b3,MATRPROB:12;
   end;
    suppose len M > 0; then
      1 <= len p by a1,NAT_1:14; then
      1 in Seg len p by FINSEQ_1:3; then
aa:   1 in dom p by FINSEQ_1:def 3;
      thus
ab:   len (p.1) = 1 * width M by aa,a1,th409 .= width M;
      let j such that
a3:   [1,j] in Indices M;
      j in Seg width M by a3,MATRPROB:12;
      hence thesis by a1,a3,MATRPROB:14,ab,FINSEQ_1:def 3;
    end;
  end;

theorem th413:
  for M being Matrix of D for p being FinSequence of D* st
     len p = len M & p.1 = M.1 &
     (for k st k >= 1 & k < len M holds p.(k+1) = (p.k) ^ M.(k+1)) holds
       for j st j >= 1 & j < len p holds
       (for l st l in dom(p.j) holds (p.j).l = (p.(j+1)).l)
  proof let M be Matrix of D;
        let p be FinSequence of D* such that
a2: len p = len M & p.1 = M.1 &
    for k st k >= 1 & k < len M holds p.(k+1) = (p.k) ^ M.(k+1);
    let j such that
b1: j >= 1 & j < len p;
    let l such that
b2: l in dom(p.j);
    p.(j+1) = (p.j) ^ M.(j+1) by a2,b1;
    hence (p.j).l = (p.(j+1)).l by b2,FINSEQ_1:def 7;
  end;

theorem th413a:
  for M being Matrix of D for p being FinSequence of D* st
     len p = len M & p.1 = M.1 &
     (for k st k >= 1 & k < len M holds p.(k+1) = (p.k) ^ M.(k+1)) holds
       for i,j st i in dom p & j in dom p & i <= j holds
       (for l st l in dom(p.i) holds (p.i).l = (p.j).l)
  proof let M be Matrix of D;
        let p be FinSequence of D* such that
a2: len p = len M & p.1 = M.1 &
    for k st k >= 1 & k < len M holds p.(k+1) = (p.k) ^ M.(k+1);
    defpred P[Element of NAT] means $1 in dom p implies
      for i st i in dom p & i<=$1 holds
        (for l st l in dom(p.i) holds (p.i).l = (p.$1).l);
aa: P[0]
    proof
      not 0 in Seg len p by FINSEQ_1:3;
      hence thesis by FINSEQ_1:def 3;
    end;
ab: for j st P[j] holds P[j+1]
    proof let j such that
b1:   j in dom p implies for i st i in dom p & i<=j holds
        for l st l in dom(p.i) holds(p.i).l=(p.j).l;
      assume j+1 in dom p; then
b3:   j+1 >= 1 & j+1 <= len p by FINSEQ_3:27; then
b5:   j+1 = 1 or j+1 > 1 by REAL_1:def 5;
      let i such that
c1:   i in dom p & i<=j+1;
      i in Seg len p by c1,FINSEQ_1:def 3; then
c3:   i >= 1 & i <= len p by FINSEQ_1:3;
      per cases by b5,NAT_1:13;
      suppose j+1 = 1;
        hence thesis by c1,c3,XXREAL_0:1;
      end;
      suppose
c5:     j >= 1;
cc:     j < len p by b3,NAT_1:13; then
bz:     j in Seg len p by c5,FINSEQ_1:3; then
bb:     j in dom p by FINSEQ_1:def 3;
        thus for l st l in dom(p.i) holds (p.i).l=(p.(j+1)).l
        proof let l such that
d1:       l in dom(p.i);
          per cases by c1,NAT_1:8;
          suppose
d2:         i <= j; then
d3:         dom(p.i) c= dom(p.j) by a2,c1,bb,th409a;
            thus (p.i).l=(p.j).l by d2,c1,d1,b1,bz,FINSEQ_1:def 3
              .= (p.(j+1)).l by a2,c5,cc,d3,d1,th413;
          end;
          suppose
            i = j+1;
            hence thesis;
          end;
        end;
      end;
    end;
    for j holds P[j] from NAT_1:sch 1(aa,ab);
    hence thesis;
  end;

theorem th414:
  for M being Matrix of D for p being FinSequence of D* st
     len p = len M & p.1 = M.1 &
     (for k st k >= 1 & k < len M holds p.(k+1) = (p.k) ^ M.(k+1)) holds
       for j st j >= 1 & j < len p holds
       (for l st l in Seg width M holds (j*width M+l) in dom (p.(j+1)) &
       (p.(j+1)).(j*width M+l)=(M.(j+1)).l)
  proof let M be Matrix of D;
        let p be FinSequence of D* such that
a2: len p = len M & p.1 = M.1 &
    for k st k >= 1 & k < len M holds p.(k+1) = (p.k) ^ M.(k+1);
aa: dom p = dom M by a2,FINSEQ_3:31;
    let j such that
b1: j >= 1 & j < len p;
    let l such that
b2: l in Seg width M;
bb: l >= 1 & l <= width M by FINSEQ_1:3,b2;
b4: p.(j+1) = (p.j) ^ M.(j+1) by a2,b1;
    j+1 >=1 & j+1 <= len M by a2,b1,NAT_1:13; then
    j+1 in Seg len M by FINSEQ_1:3; then
b5: j+1 in dom M by FINSEQ_1:def 3; then
b7: l in dom(M.(j+1)) by th350,b2;
    j in Seg len p by b1,FINSEQ_1:3; then
b8: j in dom p by FINSEQ_1:def 3;
    len(p.(j+1)) = (j+1) * width M by a2,b5,aa,th409
      .= j * width M + width M; then
b9: j*width M + l <= len(p.(j+1)) by XREAL_1:9,bb;
    j*width M + l >= 0+1 by bb,XREAL_1:9; then
    j*width M + l in Seg len(p.(j+1)) by FINSEQ_1:3,b9;
    hence j*width M + l in dom(p.(j+1)) by FINSEQ_1:def 3;
    len(p.j) = j * width M by a2,b8,th409;
    hence thesis by b4,b7,FINSEQ_1:def 7;
  end;

theorem th415:
  for M being Matrix of D for p being FinSequence of D* st
    len p = len M & p.1 = M.1 &
    (for k st k >= 1 & k < len M holds p.(k+1) = (p.k) ^ M.(k+1)) holds
      for i,j st [i,j] in Indices M holds (i-1)*(width M)+j in dom(p.i) &
        M*(i,j) = (p.i).((i-1)*(width M)+j)
  proof let M be Matrix of D;
        let p be FinSequence of D* such that
a2: len p = len M & p.1 = M.1 &
    for k st k >= 1 & k < len M holds p.(k+1) = (p.k) ^ M.(k+1);
    let i,j such that
b0: [i,j] in Indices M;
b1: i in Seg len M & j in Seg width M by b0,MATRPROB:12; then
    i in dom M by FINSEQ_1:def 3; then
b2: i >= 1 & i <= len M by FINSEQ_3:27;
    per cases by b2,REAL_1:def 5;
    suppose
d1:   i > 1;
      reconsider ii = i - 1 as Element of NAT by d1,NAT_1:20;
      ii + 1 > 1 by d1; then
d2:   ii >= 1 by NAT_1:13;
      i < len M + 1 by b2,NAT_1:13; then
      i - 1 < (len M + 1) - 1 by REAL_1:92; then
      (ii*width M+j) in dom(p.(ii+1)) &
        (p.(ii+1)).(ii*width M+j)=(M.(ii+1)).j by a2,d2,b1,th414;
      hence thesis by b0,MATRPROB:14;
    end;
    suppose i = 1;
      hence thesis by a2,b0,th412;
    end;
  end;

theorem th416:
  for M being Matrix of D for p being FinSequence of D* st
    len p = len M & p.1 = M.1 &
    (for k st k >= 1 & k < len M holds p.(k+1) = (p.k) ^ M.(k+1)) holds
      for i,j st [i,j] in Indices M holds (i-1)*(width M)+j in dom(p.(len M))
      & M*(i,j) = (p.(len M)).((i-1)*(width M)+j)
  proof let M be Matrix of D;
        let p be FinSequence of D* such that
a2: len p = len M & p.1 = M.1 &
    for k st k >= 1 & k < len M holds p.(k+1) = (p.k) ^ M.(k+1);
    let i,j such that
a3: [i,j] in Indices M;
a4: i in Seg len M & j in Seg width M by a3,MATRPROB:12; then
a5: i >=1 & i <= len M by FINSEQ_1:3;
a6: i in dom p by a2,a4,FINSEQ_1:def 3;
    len M >= 1 & len M <= len p by FINSEQ_1:4,a4,NAT_1:14,a2; then
    len M in Seg len p by FINSEQ_1:3; then
a7: len M in dom p by FINSEQ_1:def 3;
    i > 0 by a4,FINSEQ_1:3; then
    reconsider ii = i-1 as Element of NAT by NAT_1:20;
    set jj=ii*(width M)+j;
ac: (i-1)*(width M)+j in dom(p.i) &
    M*(i,j) = (p.i).((i-1)*(width M)+j) by a2,a3,th415;
    dom(p.i) c= dom(p.(len M)) by a2,a6,a7,a5,th409a;
    hence (i-1)*(width M)+j in dom(p.(len M)) &
    M*(i,j) = (p.(len M)).((i-1)*(width M)+j) by ac,a2,a6,a7,a5,th413a;
  end;

theorem th420:
  for M being Matrix of REAL for p being FinSequence of REAL* st
    len p = len M & p.1 = M.1 &
    (for k st k >= 1 & k < len M holds p.(k+1)=(p.k) ^ M.(k+1)) holds
      for k st k >= 1 & k < len M holds Sum(p.(k+1))=Sum(p.k)+Sum(M.(k+1))
  proof let M be Matrix of REAL;
        let p be FinSequence of REAL* such that
    len p = len M and
    p.1 = M.1 and
a3: for k st k >= 1 & k < len M holds p.(k+1) = (p.k) ^ M.(k+1);
    let k such that
b1: k >= 1 & k < len M;
    p.(k+1) = (p.k) ^ M.(k+1) by b1,a3;
    hence thesis by RVSUM_1:105;
  end;

theorem th421:
  for M being Matrix of REAL for p being FinSequence of REAL* st
    len p = len M & p.1 = M.1 &
    (for k st k >= 1 & k < len M holds p.(k+1)=(p.k) ^ M.(k+1)) holds
      SumAll M = Sum(p.(len M))
  proof let M be Matrix of REAL;
        let p be FinSequence of REAL* such that
a1: len p = len M and
a2: p.1 = M.1 and
a3: for k st k >= 1 & k < len M holds p.(k+1) = (p.k) ^ M.(k+1);
    per cases;
    suppose
b1:   len M = 0; then
      p = {} by a1,FINSEQ_1:25; then
      dom p = {} by FINSEQ_1:26;
      hence Sum(p.(len M)) = 0 by FUNCT_1:def 4,RVSUM_1:102
        .= SumAll M by MATRPROB:23,b1;
    end;
    suppose
az:   len M > 0;
    set q = LineSum M;
aa: len q = len M by MATRPROB:def 1; then
a5: len M >= 1 & len q >= 1 by az,NAT_1:14;
ac: len M in Seg len M by az,FINSEQ_1:5;
ad: len Sum p = len p by MATRPROB:def 1; then
af: len M in dom Sum p by ac,a1,FINSEQ_1:def 3;
    consider qq being FinSequence of REAL such that
a6: len qq = len q & qq.1 = q.1 &
    (for k st 0 <> k & k < len q holds qq.(k+1) = qq.k + q.(k+1)) &
      Sum q = qq.(len q) by a5,th419;
a7: qq = Sum p
    proof
b1:   len qq = len M by a6,MATRPROB:def 1 .= len Sum p by MATRPROB:def 1,a1;
b3:   dom Sum p = dom p by ad,FINSEQ_3:31;
      for j st j in Seg len qq holds qq.j = (Sum p).j
      proof
        defpred P[Nat] means $1 in Seg len qq implies qq.$1 = (Sum p).$1;
c1:     P[0] by FINSEQ_1:3;
c2:     for k st P[k] holds P[k+1]
        proof let k such that
d1:       P[k];
          assume
d2:       k+1 in Seg len qq; then
de:       k+1 in dom M & k+1 in dom Sum p by aa,a6,FINSEQ_1:def 3,ad,a1;
d3:       k+1 >= 1 & k+1 <= len qq by d2,FINSEQ_1:3; then
d5:       k+1 = 1 or k+1 > 1 by REAL_1:def 5;
          per cases by d5,NAT_1:13;
          suppose
e1:         k+1 = 1;
e2:         1 in Seg len M by a5,FINSEQ_1:3; then
e3:         1 in dom M & 1 in dom p by FINSEQ_1:def 3,a1;
            qq.(k+1) = Sum Line(M,1) by e2,MATRPROB:20,e1,a6
              .= Sum(M.1) by e3,MATRIX_2:18 
              .= (Sum p).1 by e3,b3,MATRPROB:def 1,a2;
            hence thesis by e1;
          end;
          suppose
e4:         k >= 1;
e5:         k < len qq by d3,NAT_1:13; then
            k in Seg len qq by e4,FINSEQ_1:3; then
e8:         k in dom Sum p by FINSEQ_1:def 3,b1;
e9:         k < len M by e5,MATRPROB:def 1,a6;
            qq.(k+1) = qq.k + q.(k+1) by e4,e5,a6
              .= Sum(p.k) + q.(k+1) by e8,MATRPROB:def 1,e5,e4,FINSEQ_1:3,d1
              .= Sum(p.k) + Sum Line(M,(k+1)) by d2,aa,a6,MATRPROB:20
              .= Sum(p.k) + Sum(M.(k+1)) by de,MATRIX_2:18
              .= Sum(p.(k+1)) by a1,a2,a3,e4,e9,th420
              .= (Sum p).(k+1) by de,MATRPROB:def 1;
            hence thesis;
          end;
        end;
        for j holds P[j] from NAT_1:sch 1(c1,c2);
        hence thesis;
      end;
      hence thesis by b1,FINSEQ_2:10;
    end;
    thus SumAll M = Sum q by MATRPROB:def 3 
      .= (Sum p).(len M) by MATRPROB:def 1,a7,a6
      .=Sum(p.(len M)) by af,MATRPROB:def 1;
    end;
  end;

definition let D be non empty set;
           let M be Matrix of D;
  func Mx2FinS(M) -> FinSequence of D means :Def210:
  it = {} if len M = 0 otherwise
  ex p being FinSequence of D* st it = p.(len M) &
    len p = len M & p.1 = M.1 &
    for k st k >= 1 & k < len M holds p.(k+1) = (p.k) ^ M.(k+1);
  existence
  proof
    thus len M = 0 implies ex z being FinSequence of D st z = {}
    proof assume len M = 0;
      {} is FinSequence of D by FINSEQ_1:29;
      hence thesis;
    end;
    thus len M <> 0 implies (ex z being FinSequence of D st
      ex p being FinSequence of D* st z = p.(len M) &
      len p = len M & p.1 = M.1 &
      for k st k >= 1 & k < len M holds p.(k+1) = (p.k) ^ M.(k+1))
    proof assume
A2:   len M <> 0;
      defpred P[Element of NAT,set,set] means
        ex p1,q1 being Element of D* st q1 = $2 & p1 = M.($1+1) & $3 = q1 ^ p1;
       reconsider M1 = M.1 as Element of D* by FINSEQ_1:def 11;
a2:   for n being Element of NAT st 1 <= n & n < len M
        for x being Element of D* ex y being Element of D* st P[n,x,y]
      proof
        let n be Element of NAT such that
A3:     1 <= n & n < len M;
        let x be Element of D*;
A4:     1 <= n+1 by NAT_1:11;
        n+1 <= len M by A3,NAT_1:13; then
        n+1 in dom M by A4,FINSEQ_3:27; then
        reconsider p1 = M.(n+1) as Element of D* by FINSEQ_2:13;
        set y = x ^ p1;
        reconsider y as Element of D* by FINSEQ_1:def 11;
        take y, p1, x;
        thus thesis;
      end;
      ex p being FinSequence of D* st len p = len M &
        (p.1 = M1 or len M = 0) & for n being Element of NAT st
        1 <= n & n < len M holds P[n,p.n,p.(n+1)]
          from RECDEF_1:sch 6(a2); then
      consider p being FinSequence of D* such that
b1:     len p = len M and
b2:     p.1 = M1 or len M = 0 and
b3:   for n being Element of NAT st 1 <= n & n < len M holds
        P[n,p.n,p.(n+1)];
bb:   for n being Element of NAT st 1 <= n & n < len M holds
        p.(n+1)= (p.n) ^ M.(n+1)
      proof let n be Element of NAT such that
b4:     1 <= n & n < len M;
        consider p1,q1 being Element of D* such that
b5:     q1 = p.n & p1 = M.(n+1) & p.(n+1) = q1 ^ p1 by b3,b4;
        thus thesis by b5;
      end;
      reconsider z = p.len M as FinSequence of D;
      take z;
      thus thesis by b1,b2,A2,bb;
    end;
  end;
  uniqueness
  proof let z1,z2 be FinSequence of D;
    thus len M = 0 & z1 ={} & z2={} implies z1=z2;
    assume
    len M <> 0; then
cb: len M >= 1 by NAT_1:14;
    given p1 being FinSequence of D* such that
c2:   z1 = p1.(len M) & len p1 = len M & p1.1 = M.1 &
      for k st k >= 1 & k < len M holds p1.(k+1) = (p1.k) ^ M.(k+1);
    given p2 being FinSequence of D* such that
c3:   z2 = p2.(len M) & len p2 = len M & p2.1 = M.1 &
      for k st k >= 1 & k < len M holds p2.(k+1) = (p2.k) ^ M.(k+1);
cc: for k st k >= 1 & k <= len M holds p1.k = p2.k
    proof
      defpred P[Element of NAT] means
      $1 >= 1 & $1 <= len M implies p1.$1 = p2.$1;
c4:   P[0];
c5:   for n being Element of NAT st P[n] holds P[n + 1]
      proof let n being Element of NAT;
        assume
c6:     n >= 1 & n <= len M implies p1.n = p2.n;
        assume
c7:     n + 1 >= 1 & n + 1 <= len M;
        now per cases;
          suppose n = 0;
            hence thesis by c2,c3;
          end;
          suppose n <> 0; then
c9:         n >= 1 by NAT_1:14;
ca:         n < len M by c7,NAT_1:13;
            hence p1.(n + 1) = (p2.n) ^ M.(n+1) by c6,c9,c2
              .= p2.(n + 1) by c9,ca,c3;
          end;
        end;
        hence p1.(n + 1) = p2.(n + 1);
      end;
      thus for n being Element of NAT holds P[n] from NAT_1:sch 1(c4,c5);
    end;
    thus z1 = z2 by c3,c2,cc,cb;
  end;
  consistency;
end;

theorem th410:
  for M being Matrix of D holds
    len Mx2FinS(M) = len M * width M
  proof let M be Matrix of D;
    per cases;
    suppose
aa:   len M = 0; then
      Mx2FinS(M) = {} by Def210;
      hence len Mx2FinS(M) = len M * width M by aa,FINSEQ_1:25;
    end;
    suppose
ad:   len M > 0; then
ac:   len M in Seg len M by FINSEQ_1:5;
      consider p being FinSequence of D* such that
a0:   Mx2FinS(M) = p.(len M) and
a1:   len p = len M & p.1 = M.1 &
      for k st k >= 1 & k < len M holds
        p.(k+1) = (p.k) ^ M.(k+1) by ad,Def210;
a3:   len M in dom p by ac,a1,FINSEQ_1:def 3;
      thus len Mx2FinS(M) = len M * width M by a0,a3,a1,th409;
    end;
  end;

theorem th417:
  for M being Matrix of D for i,j st [i,j] in Indices M holds
    (i-1) * (width M) + j in dom Mx2FinS M &
    M*(i,j) = (Mx2FinS(M)).((i-1) * (width M) + j)
  proof let M be Matrix of D;
        let i,j such that
a1: [i,j] in Indices M;
b0: len M <> 0 & width M <> 0 by FINSEQ_1:4,MATRPROB:12,a1;
    consider p being FinSequence of D* such that
b1: Mx2FinS(M) = p.(len M) and
b2: len p = len M & p.1 = M.1 &
    for k st k >= 1 & k < len M holds
    p.(k+1) = (p.k) ^ M.(k+1) by b0,Def210;
    thus thesis by b1,b2,th416,a1;
  end;

theorem th418:
  for M being Matrix of D for k,l st
    k in dom(Mx2FinS(M)) & l = k - 1 holds
    [((l div width M)+1),((l mod width M)+1)] in Indices M &
    (Mx2FinS(M)).k = M*(((l div width M)+1),((l mod width M)+1))
  proof let M be Matrix of D;
        let k,l such that
a1: k in dom(Mx2FinS(M)) & l = k - 1;
aa: Mx2FinS(M) <> {} by a1,FINSEQ_1:26;
a2: len Mx2FinS(M) = len M * width M by th410; then
a0: len M <> 0 & width M <> 0 by aa,FINSEQ_1:25; then
ai: len M > 0 & width M > 0;
    set ii = (l div width M)+1;
    set jj = (l mod width M)+1;
    k in Seg len Mx2FinS(M) by a1,FINSEQ_1:def 3; then
    k >= 1 & k <= len Mx2FinS(M) by FINSEQ_1:3; then
    k < len M * width M + 1 by a2,NAT_1:13; then
a4: k - 1 < len M * width M + 1 - 1 by XREAL_1:11;
    width M divides len M * width M by NAT_D:def 3; then
    l div width M < (len M * width M) div width M by a0,a4,a1,th477; then
    l div width M < len M by a0,NAT_D:18; then
a6: ii <= len M by NAT_1:13;
    l mod width M < width M by ai,NAT_D:1; then
a7: jj <= width M by NAT_1:13;
a8: ii >= 1 & jj >= 1 by NAT_1:11;
ab: ii in Seg len M by a8,a6,FINSEQ_1:3;
ac: jj in Seg width M by a8,a7,FINSEQ_1:3;
ad: l = (l div width M) * (width M) + (l mod width M) by ai,NAT_D:2;
    thus [ii,jj] in Indices M by ab,ac,MATRPROB:12; then
    M*(ii,jj) = (Mx2FinS(M)).((ii-1) * (width M) + jj) by th417
      .= (Mx2FinS(M)).k by a1,ad;
    hence thesis;
  end;

theorem th422:
  SumAll MR = Sum Mx2FinS MR
  proof
    per cases;
    suppose
aa:   len MR = 0;
      hence Sum Mx2FinS MR = 0 by Def210,RVSUM_1:102
        .= SumAll MR by MATRPROB:23,aa;
    end;
    suppose
ad:   len MR > 0;
      consider p being FinSequence of REAL* such that
a0:   Mx2FinS(MR) = p.(len MR) and
a1:   len p = len MR & p.1 = MR.1 &
      for k st k >= 1 & k < len MR holds
        p.(k+1) = (p.k) ^ MR.(k+1) by ad,Def210;
      thus Sum Mx2FinS(MR) = SumAll MR by a1,th421,a0;
    end;
  end;

theorem th423:
  MR is m-nonnegative iff Mx2FinS(MR) is nonnegative
  proof
    hereby assume
bb:   MR is m-nonnegative;
      for k st k in dom(Mx2FinS(MR)) holds (Mx2FinS(MR)).k >= 0
      proof
        per cases;
        suppose len MR=0; then
          Mx2FinS MR = {} by Def210;
          hence thesis by FINSEQ_1:26;
        end;
        suppose len MR>0 & width MR=0; then
          len Mx2FinS MR = len MR * 0 by th410 .= 0; then
          Mx2FinS MR = {} by FINSEQ_1:25;
          hence thesis by FINSEQ_1:26;
        end;
        suppose
          len MR>0 & width MR >0;
          let i such that
b1:       i in dom Mx2FinS(MR);
          i in Seg len Mx2FinS(MR) by b1,FINSEQ_1:def 3; then
          i >= 1 & i <= len Mx2FinS(MR) by FINSEQ_1:3; then
          reconsider l = i - 1 as Element of NAT by NAT_1:21;
          [((l div width MR)+1),((l mod width MR)+1)] in Indices MR &
            (Mx2FinS(MR)).i = MR*(((l div width MR)+1),((l mod width MR)+1))
             by b1,th418;
          hence thesis by bb,MATRPROB:def 6;
        end;
      end;
      hence Mx2FinS(MR) is nonnegative by Def109;
    end;
    assume
c0: Mx2FinS(MR) is nonnegative;
    now let i,j such that
c1:   [i,j] in Indices MR;
      (i-1) * (width MR) + j in dom Mx2FinS MR &
      MR*(i,j) = (Mx2FinS(MR)).((i-1) * (width MR) + j) by th417,c1;
      hence MR*(i,j) >= 0 by c0,Def109;
    end;
    hence MR is m-nonnegative by MATRPROB:def 6;
  end;

theorem th447:
  MR is Joint_Probability iff Mx2FinS(MR) is ProbFinS
  proof
    hereby assume
      MR is Joint_Probability; then
      reconsider MRR = MR as Joint_Probability Matrix of REAL;
a2:   MRR is m-nonnegative with_sum=1 Matrix of REAL;
      Mx2FinS(MR) is nonnegative by a2,th423; then
ab:   for i st i in dom Mx2FinS(MR) holds (Mx2FinS(MR)).i >= 0 by Def109;
ac:   Sum Mx2FinS(MR) = SumAll MR by th422
        .= 1 by a2,MATRPROB:def 7;
      thus Mx2FinS(MR) is ProbFinS by ab,ac,MATRPROB:def 5;
    end;
    assume Mx2FinS(MR) is ProbFinS; then
    reconsider pp=Mx2FinS(MR) as ProbFinS FinSequence of REAL;
    reconsider p=pp as non empty ProbFinS FinSequence of REAL;
     p is nonnegative; then
ae: MR is m-nonnegative by th423;
    SumAll MR = Sum p by th422 .= 1 by MATRPROB:def 5; then
    MR is with_sum=1 by MATRPROB:def 7;
    hence thesis by ae,MATRPROB:def 8;
  end;

theorem th450:
  for p,q being ProbFinS FinSequence of REAL holds
    Mx2FinS((ColVec2Mx p) * (LineVec2Mx q)) is ProbFinS
  proof let p,q be ProbFinS FinSequence of REAL;
    (ColVec2Mx p) * (LineVec2Mx q) is Joint_Probability Matrix of REAL
      by th332;
    hence thesis by th447;
  end;

theorem th450a:
  for p being ProbFinS FinSequence of REAL
  for M being non empty-yielding Conditional_Probability Matrix of REAL st
  len p = len M holds
    Mx2FinS((Vec2DiagMx p) * M) is ProbFinS
  proof let p be non empty ProbFinS FinSequence of REAL;
    let M be non empty-yielding Conditional_Probability Matrix of REAL;
    assume len p = len M; then
    (Vec2DiagMx p) * M is Joint_Probability Matrix of REAL by th237;
    hence thesis by th447;
  end;

begin :: Information Entropy

definition let a,p;
  assume a1: a > 0 & a <> 1 & p is nonnegative;
  func FinSeq_log(a,p) -> FinSequence of REAL means :Def299:
    len it = len p & for k st k in dom it holds
      (p.k > 0 implies it.k = log(a,p.k))  &
      (p.k = 0 implies it.k = 0);
  existence
  proof
    defpred P[Nat,set] means
      (p.$1 > 0 implies $2 = log(a,p.$1)) &
      (p.$1 = 0 implies $2 = 0);
b0: for k st k in Seg len p ex x being Element of REAL st P[k,x]
    proof let k such that
b1:   k in Seg len p;
b2:   k in dom p by b1,FINSEQ_1:def 3;
      per cases by b2,a1,Def109;
      suppose
b3:     p.k>0;
        take log(a,p.k);
        thus thesis by b3;
      end;
      suppose
b4:     p.k=0;
        take 0;
        thus thesis by b4;
      end;
    end;
    consider pp being FinSequence of REAL such that
b5: dom pp = Seg len p &
    for k st k in Seg len p holds P[k,pp.k] from FINSEQ_1:sch 5(b0);
    len pp = len p by b5,FINSEQ_1:def 3;
    hence thesis by b5;
  end;
  uniqueness
  proof let p1,p2 be FinSequence of REAL such that
B1: len p1 = len p & for k st k in dom p1 holds
    (p.k > 0 implies p1.k = log(a,p.k)) & (p.k = 0 implies p1.k = 0) and
B2: len p2 = len p & for k st k in dom p2 holds
    (p.k > 0 implies p2.k = log(a,p.k)) & (p.k = 0 implies p2.k = 0);
B3: dom p1 = Seg len p2 by FINSEQ_1:def 3,B2,B1 .= dom p2 by FINSEQ_1:def 3;
    for k st k in dom p1 holds p1.k = p2.k
    proof let k;
      assume
B4:   k in dom p1; then
BB:   k in dom p & k in dom p2 by B2,FINSEQ_3:31,B1;
      per cases by BB,a1,Def109;
      suppose
B6:     p.k>0;
        hence p1.k = log(a,p.k) by B4,B1 .= p2.k by BB,B6,B2;
      end;
      suppose
B7:     p.k=0;
        hence p1.k = 0 by B4,B1 .= p2.k by BB,B7,B2;
      end;
    end;
    hence thesis by B3,FINSEQ_1:17;
  end;
end;

definition let p;
  func Infor_FinSeq_of p -> FinSequence of REAL equals
    mlt(p,FinSeq_log(2,p));
  correctness;
end;

theorem th196:
  for p being nonnegative FinSequence of REAL
  for q holds
    q = Infor_FinSeq_of p iff
    len q = len p & for k st k in dom q holds q.k = p.k * log(2,p.k)
  proof let p be nonnegative FinSequence of REAL;
        let q;
    set pp = mlt(p,FinSeq_log(2,p));
aa: len p = len FinSeq_log(2,p) by Def299; then
a0: dom p = dom FinSeq_log(2,p) by FINSEQ_3:31;
ab: len pp = len p by MATRPROB:30,aa;
    hereby assume a2: q = Infor_FinSeq_of p;
      thus len q = len p & for k st k in dom q holds q.k = p.k * log(2,p.k)
      proof
        thus len q = len p by MATRPROB:30,aa,a2;
        let k such that
b3:     k in dom q;
b4:     dom q = dom pp & dom p = dom q by ab,a2,FINSEQ_3:31;
b5:     q.k = p.k * (FinSeq_log(2,p)).k by b3,a2,RVSUM_1:86;
bb:     k in dom FinSeq_log(2,p) by aa,FINSEQ_3:31,ab,a2,b3;
        per cases by Def109,b4,b3;
        suppose p.k=0;
          hence q.k = p.k * log(2,p.k) by b5;
        end;
        suppose p.k>0;
          hence q.k = p.k * log(2,p.k) by b5,bb,Def299;
        end;
      end;
    end;
    assume
c1: len q = len p & for k st k in dom q holds q.k = p.k * log(2,p.k); then
    len q = len pp by MATRPROB:30,aa; then
c3: dom q = dom pp & dom q = dom p by c1,FINSEQ_3:31;
    now let k such that
d1:   k in dom q;
d2:   pp.k = p.k * (FinSeq_log(2,p)).k by c3,d1,RVSUM_1:86;
      per cases by Def109,d1,c3;
      suppose
e1:     p.k=0;
        hence q.k = 0 * log(2,p.k) by d1,c1 .= pp.k by e1,d2;
      end;
      suppose p.k>0; then
        (FinSeq_log(2,p)).k = log(2,p.k) by d1,c3,a0,Def299;
        hence pp.k = p.k * log(2,p.k) by d1,c3,RVSUM_1:86
          .= q.k by c1,d1;
      end;
    end;
    hence thesis by c3,FINSEQ_1:17;
  end;

theorem th401:
  for p being nonnegative FinSequence of REAL for k st k in dom p holds
    (p.k = 0 implies (Infor_FinSeq_of p).k = 0) &
    (p.k > 0 implies (Infor_FinSeq_of p).k = p.k * log(2,p.k))
  proof
    let p be nonnegative FinSequence of REAL;
    let k such that
a1:   k in dom p;
a2:   dom p = Seg len p by FINSEQ_1:def 3
        .= Seg len (Infor_FinSeq_of p) by th196
        .= dom (Infor_FinSeq_of p) by FINSEQ_1:def 3;
    hereby assume p.k=0;
      hence (Infor_FinSeq_of p).k = 0*log(2,p.k) by a2,a1,th196 .=0;
    end;
    thus thesis by a2,a1,th196;
  end;

theorem
  for p being nonnegative FinSequence of REAL
  for q holds q = - Infor_FinSeq_of p iff
    (len q = len p & for k st k in dom q holds q.k = p.k * log(2,1/(p.k)))
  proof let p be nonnegative FinSequence of REAL, q;
    set p0 = Infor_FinSeq_of p;
    hereby assume
c0:   q = -p0;
      thus len q = len p & for k st k in dom q holds
        q.k = p.k * log(2,1/(p.k))
      proof
d1:     dom q = dom p0 by c0,SEQ_1:def 7;
d2:     Seg len q = dom q by FINSEQ_1:def 3 .= Seg len p0 by d1,FINSEQ_1:def 3
          .= Seg len p by th196;
        hence len q = len p by FINSEQ_1:8;
        thus for k st k in dom q holds q.k = p.k * log(2,1/(p.k))
        proof let k such that
c1:       k in dom q;
          k in Seg len q by c1,FINSEQ_1:def 3; then
ca:       k in dom p by d2,FINSEQ_1:def 3;
c3:       q.k = -p0.k by c0,RVSUM_1:35; then
c4:       q.k = -(p.k * log(2,p.k)) by c1,d1,th196
            .= p.k * (-log(2,p.k));
          per cases by ca,Def109;
          suppose d1: p.k=0; then
            p0.k=0 by ca,th401;
            hence q.k = p.k * log(2,1/(p.k)) by c3,d1;
          end;
          suppose p.k>0;
            hence q.k = p.k * log(2,1/(p.k)) by c4,th290;
          end;
        end;
      end;
    end;
    assume
a1: len q = len p & for k st k in dom q holds q.k = p.k * log(2,1/(p.k));
a2: dom q = Seg len q by FINSEQ_1:def 3 .= Seg len p0 by a1,th196 
      .= dom p0 by FINSEQ_1:def 3; then
a3: dom q = dom -p0 by SEQ_1:def 7;
   for k st k in dom q holds (-p0).k = q.k
    proof let k; assume
b1:   k in dom q; then
      k in Seg len p by FINSEQ_1:def 3,a1; then
b2:   k in dom p by FINSEQ_1:def 3;
      per cases by b2,Def109;
      suppose c1: p.k = 0;
        thus (-p0).k = -p0.k by RVSUM_1:35
          .= -0 by c1,b2,th401
          .= p.k * log(2,1/(p.k)) by c1
          .= q.k by b1,a1;
      end;
      suppose c2: p.k > 0;
        thus (-p0).k = -p0.k by RVSUM_1:35
          .= -(p.k * log(2,p.k)) by a2,b1,th196
          .= p.k * (-log(2,p.k)) .= p.k * log(2,1/(p.k)) by c2,th290
          .= q.k by b1,a1;
      end;
    end;
    hence thesis by a3,FINSEQ_1:17;
  end;

theorem th292:
  for p being nonnegative FinSequence of REAL st r1>=0 & r2>=0 holds
    for i st i in dom p & p.i = r1*r2 holds
      (Infor_FinSeq_of p).i = r1*r2*log(2,r1) + r1*r2*log(2,r2)
  proof let p be nonnegative FinSequence of REAL such that
a1: r1>=0 & r2>=0;
    let i such that
a2: i in dom p & p.i = r1*r2;
    len p = len Infor_FinSeq_of p by th196; then
a3: i in dom Infor_FinSeq_of p by a2,FINSEQ_3:31;
    thus (Infor_FinSeq_of p).i = r1*r2*log(2,r1*r2) by a2,a3,th196
      .= r1*r2*log(2,r1) + r1*r2*log(2,r2) by th291,a1;
  end;

theorem th293:
  for p being nonnegative FinSequence of REAL st r>=0 holds
    Infor_FinSeq_of (r*p) = (r*log(2,r)*p) + (r*mlt(p,FinSeq_log(2,p)))
  proof let p be nonnegative FinSequence of REAL such that
a1: r>=0;
    reconsider q=r*p as nonnegative FinSequence of REAL by a1,th293a;
    set qq = Infor_FinSeq_of q;
a2: dom q = dom p by SEQ_1:def 6;
    len q = len qq by th196; then
a3: dom q = dom qq by FINSEQ_3:31;
a4: dom (r*log(2,r)*p) = dom p by SEQ_1:def 6;
a5: len FinSeq_log(2,p) = len p by Def299; then
a6: dom FinSeq_log(2,p) = dom p by FINSEQ_3:31;
    len mlt(p,FinSeq_log(2,p)) = len p by a5,MATRPROB:30; then
a8: dom mlt(p,FinSeq_log(2,p)) = dom p by FINSEQ_3:31; then
    dom (r*mlt(p,FinSeq_log(2,p))) = dom p by SEQ_1:def 6; then
    len (r*log(2,r)*p) = len (r*mlt(p,FinSeq_log(2,p)))
      by a4,FINSEQ_3:31; then
    len ((r*log(2,r)*p) + (r*mlt(p,FinSeq_log(2,p)))) = len (r*log(2,r)*p)
      by INTEGRA5:2; then
ac: dom ((r*log(2,r)*p) + (r*mlt(p,FinSeq_log(2,p)))) = dom (r*log(2,r)*p)
      by FINSEQ_3:31;
    now let k such that
b1:   k in dom qq;
      p.k>=0 & q.k = r*p.k by a2,b1,a3,RVSUM_1:66,Def109; then
b3:   qq.k = r*p.k*log(2,r) + r*p.k*log(2,p.k)
      by a1,b1,a3,th292;
b4:   (r*log(2,r)*p).k = r*log(2,r)*p.k by RVSUM_1:66;
b5:   (r*mlt(p,FinSeq_log(2,p))).k = r*(mlt(p,FinSeq_log(2,p))).k
        by RVSUM_1:66
        .= r*(p.k*(FinSeq_log(2,p)).k) by RVSUM_1:86,a8,a2,b1,a3
        .= r*p.k*(FinSeq_log(2,p)).k;
      per cases by a2,b1,a3,Def109;
      suppose p.k>0; then
        qq.k = (r*log(2,r)*p).k + (r*mlt(p,FinSeq_log(2,p))).k
          by b5,a6,a2,b1,a3,Def299,b3,b4;
        hence qq.k = ((r*log(2,r)*p)+(r*mlt(p,FinSeq_log(2,p)))).k
          by a4,ac,a2,b1,a3,SEQ_1:def 3;
      end;
      suppose p.k=0;
        hence qq.k = ((r*log(2,r)*p)+(r*mlt(p,FinSeq_log(2,p)))).k
          by a4,ac,a2,b1,a3,SEQ_1:def 3,b5,b3,b4;
      end;
    end;
    hence thesis by a3,a2,a4,ac,FINSEQ_1:17;
  end;

theorem th405:
  for p being non empty ProbFinS FinSequence of REAL
    for k st k in dom p holds (Infor_FinSeq_of p).k <= 0
  proof let p be non empty ProbFinS FinSequence of REAL, k such that
a1: k in dom p;
    per cases by a1,MATRPROB:def 5,MATRPROB:53;
    suppose p.k = 0;
      hence thesis by th401,a1;
    end;
    suppose b2: p.k > 0 & p.k <= 1; then
c1:   (Infor_FinSeq_of p).k = p.k * log(2,p.k) by th401,a1;
      thus (Infor_FinSeq_of p).k <= 0
      proof
c2:     log(2,1) = 0 by POWER:59;
        per cases by b2,REAL_1:def 5;
        suppose p.k < 1; then
          log(2,p.k) < 0 by c2,POWER:65,b2;
          hence thesis by c1,b2,XREAL_1:134;
        end;
        suppose p.k = 1;
          hence thesis by c1,POWER:59;
        end;
      end;
    end;
  end;

definition let MR;
  assume A1: MR is m-nonnegative;
  func Infor_FinSeq_of MR -> Matrix of REAL means :Def198:
    len it = len MR & width it = width MR &
    for k st k in dom it holds it.k=mlt(Line(MR,k),FinSeq_log(2,Line(MR,k)));
  existence
  proof
    deffunc F(Nat) = mlt(Line(MR,$1),FinSeq_log(2,Line(MR,$1)));
    consider p be FinSequence such that
a1: len p = len MR and
a2: for k st k in Seg len MR holds p.k = F(k) from FINSEQ_1:sch 2;
a3: dom p = dom MR by a1,FINSEQ_3:31;
a5: dom MR = Seg len MR by FINSEQ_1:def 3;
a6: for x st x in rng p
      ex q being FinSequence st q=x & len q = width MR
    proof
      set n=width MR;
      let x such that
c1:     x in rng p;
        consider j such that
c2:     j in dom p & x = p.j by c1,FINSEQ_2:11;
        j in Seg len p by c2,FINSEQ_1:def 3; then
c4:     x = mlt(Line(MR,j),FinSeq_log(2,Line(MR,j))) by c2,a1,a2;
        j in dom MR by a1,FINSEQ_3:31,c2; then
        Line(MR,j) is nonnegative by A1,th294; then
        len Line(MR,j)=width MR &
        len FinSeq_log(2,Line(MR,j)) = len Line(MR,j)
          by MATRIX_1:def 8,Def299; then
        len mlt(Line(MR,j),FinSeq_log(2,Line(MR,j))) = width MR by MATRPROB:30;
        hence ex q being FinSequence st q=x & len q = n by c4;
    end;
    rng p c= REAL*
    proof let x such that
d1:   x in rng p;
      consider j such that
d2:   j in dom p & x = p.j by d1,FINSEQ_2:11;
      j in Seg len p by d2,FINSEQ_1:def 3; then
      p.j = mlt(Line(MR,j),FinSeq_log(2,Line(MR,j))) by a1,a2;
      hence x in REAL* by d2,FINSEQ_1:def 11;
    end; then
    reconsider p as Matrix of REAL by a6,MATRIX_1:def 1,FINSEQ_1:def 4;
 b2: width p = width MR
    proof
      per cases;
      suppose
f1:     len MR = 0; then
        width p = 0 by a1,MATRIX_1:def 4;
        hence thesis by f1,MATRIX_1:def 4;
      end;
      suppose
f2:     len MR > 0; then
        len p >= 1 by a1,NAT_1:14; then
        1 in Seg len p by FINSEQ_1:3; then
        1 in dom p by FINSEQ_1:def 3; then
f3:     p.1 in rng p by FUNCT_1:12; then
        ex q being FinSequence st q=p.1 & len q = width MR by a6;
        hence thesis by f2,a1,f3,MATRIX_1:def 4;
      end;
    end;
    take p;
    thus thesis by a1,a5,a3,a2,b2;
  end;
  uniqueness
  proof let M1,M2 be Matrix of REAL such that
e1: len M1 = len MR & width M1 = width MR & for k st k in dom M1 holds
      M1.k = mlt(Line(MR,k),FinSeq_log(2,Line(MR,k))) and
e2: len M2 = len MR & width M2 = width MR & for k st k in dom M2 holds
      M2.k = mlt(Line(MR,k),FinSeq_log(2,Line(MR,k)));
e3: dom M1 = dom M2 by e1,e2,FINSEQ_3:31;
    now let k such that
f1:   k in dom M1;
      thus M1.k = mlt(Line(MR,k),FinSeq_log(2,Line(MR,k))) by f1,e1
        .= M2.k by f1,e2,e3;
    end;
    hence M1=M2 by e3,FINSEQ_1:17;
  end;
end;

theorem th295:
  for M being m-nonnegative Matrix of REAL
    for k st k in dom M holds
      Line(Infor_FinSeq_of M,k) = Infor_FinSeq_of Line(M,k)
  proof let M be m-nonnegative Matrix of REAL;
        let k such that
a1: k in dom M;
    reconsider p = Line(M,k) as nonnegative FinSequence of REAL by a1,th294;
    set M1 = Infor_FinSeq_of M;
    len M1 = len M by Def198; then
a3: dom M1 = dom M by FINSEQ_3:31;
    hence Line(M1,k) = M1.k by a1,MATRIX_2:18
      .= Infor_FinSeq_of Line(M,k) by a1,a3,Def198;
  end;

theorem th296:
  for M being m-nonnegative Matrix of REAL
  for M1 being Matrix of REAL holds
    M1 = Infor_FinSeq_of M iff
      len M1 = len M & width M1 = width M &
      for i,j st [i,j] in Indices M1 holds M1*(i,j)=M*(i,j)*log(2,M*(i,j))
  proof let M be m-nonnegative Matrix of REAL;
        let M1 be Matrix of REAL;
    hereby assume
b1:   M1 = Infor_FinSeq_of M; then
b2:   len M1 = len M & width M1 = width M by Def198;
      now let i,j such that
d1:     [i,j] in Indices M1;
d2:     i in Seg len M1 & j in Seg width M1 by d1,MATRPROB:12; then
d4:     i in dom M by FINSEQ_1:def 3,b2; then
        reconsider p=Line(M,i) as nonnegative FinSequence of REAL by th294;
d7:     len p = width M by MATRIX_1:def 8; 
dc:     p.j = M*(i,j) by d2,b2,MATRIX_1:def 8;
        len p = len Infor_FinSeq_of p by th196; then
dd:     j in dom Infor_FinSeq_of p by d7,b2,d2,FINSEQ_1:def 3;
        thus M1*(i,j) = Line(M1,i).j by d2,MATRIX_1:def 8
          .= (Infor_FinSeq_of p).j by b1,d4,th295
          .= M*(i,j)*log(2,M*(i,j)) by dc,th196,dd;
      end;
      hence len M1 = len M & width M1 = width M &
      for i,j st [i,j] in Indices M1 holds M1*(i,j)=M*(i,j)*log(2,M*(i,j))
        by b1,Def198;
    end;
    assume
a1: len M1 = len M & width M1 = width M &
    for i,j st [i,j] in Indices M1 holds M1*(i,j)=M*(i,j)*log(2,M*(i,j)); then
a2: dom M1 = dom M & Seg width M1 = Seg width M by FINSEQ_3:31;
    for k st k in dom M1 holds M1.k = mlt(Line(M,k),FinSeq_log(2,Line(M,k)))
    proof let k such that
e1:   k in dom M1;
      reconsider p=Line(M,k) as nonnegative FinSequence of REAL by e1,a2,th294;
ed:   k in Seg len M by e1,a1,FINSEQ_1:def 3;
e3:   len p = width M & len FinSeq_log(2,p) = len p
        by Def299,MATRIX_1:def 8; then
e4:   len mlt(p,FinSeq_log(2,p)) = len p by MATRPROB:30;
ee:   len (M1.k) = width M1 by e1,GOBRD13:4; then
e5:   dom (M1.k) = dom mlt(p,FinSeq_log(2,p)) by a1,e3,e4,FINSEQ_3:31;
      now let j such that
f1:     j in dom (M1.k);
fg:     j in Seg width M by ee,a1,f1,FINSEQ_1:def 3; then
fh:     [k,j] in Indices M by ed,MATRPROB:12;
f2:     [k,j] in Indices M1 by MATRPROB:13,e1,f1;
fi:     j in dom FinSeq_log(2,p) by e3,fg,FINSEQ_1:def 3;
f3:     (M1.k).j = M1*(k,j) by f2,MATRPROB:14
          .= M*(k,j)*log(2,M*(k,j)) by f2,a1;
f4:      p.j=M*(k,j) by fg,MATRIX_1:def 8;
f5:      (mlt(p,FinSeq_log(2,p))).j = p.j * (FinSeq_log(2,p)).j
          by f1,e5,RVSUM_1:86
          .= M*(k,j)*(FinSeq_log(2,p)).j by fg,MATRIX_1:def 8;
         per cases by MATRPROB:def 6,fh;
         suppose M*(k,j)=0;
           hence (M1.k).j = (mlt(p,FinSeq_log(2,p))).j by f5,f3;
         end;
         suppose M*(k,j)>0;
           hence (mlt(p,FinSeq_log(2,p))).j
             = (M1.k).j by f5,fi,f4,Def299,f3;
         end;
      end;
      hence thesis by e5,FINSEQ_1:17;
    end;
    hence thesis by a1,Def198;
  end;

definition let p being FinSequence of REAL;
  func Entropy p -> Real equals
    -Sum Infor_FinSeq_of p;
  correctness;
end;

theorem
  for p being non empty ProbFinS FinSequence of REAL
    holds Entropy p >= 0
  proof let p be non empty ProbFinS FinSequence of REAL;
    set p1 = - Infor_FinSeq_of p;
a0: dom p = Seg len p by FINSEQ_1:def 3
      .= Seg len Infor_FinSeq_of p by th196
      .= dom Infor_FinSeq_of p by FINSEQ_1:def 3;
a1: for k st k in dom p1 holds p1.k >= 0
    proof let k such that
b1:   k in dom p1;
      k in dom p by a0,b1,SEQ_1:def 7; then
      (Infor_FinSeq_of p).k <= 0 by th405; then
      -((Infor_FinSeq_of p).k) >= -0 by XREAL_1:26;
      hence thesis by RVSUM_1:35;
    end;
    Entropy p = Sum p1 by RVSUM_1:118;
    hence thesis by a1,RVSUM_1:114;
  end;

theorem
  for p being non empty ProbFinS FinSequence of REAL
   st ex k st k in dom p & p.k = 1 holds
     Entropy p = 0
  proof let p be non empty ProbFinS FinSequence of REAL such that
a1: ex k st k in dom p & p.k = 1;
    consider k1 being Element of NAT such that
a2: k1 in dom p & p.k1 = 1 by a1;
a3: p has_onlyone_value_in k1 by th391a,a2;
    set q = Infor_FinSeq_of p;
    len q = len p by th196; then
a4: dom q = dom p by FINSEQ_3:31;
    for k st k in dom q holds q.k = 0
    proof let k such that
b1:   k in dom q;
      per cases;
      suppose
c1:     k=k1;
        thus q.k = 1*log(2,1) by a2,c1,th196,b1 .= 0 by POWER:59;
      end;
      suppose k<>k1; then
c3:     p.k = 0 by b1,a4,a3,Def16;
        thus q.k = p.k * log(2,p.k) by th196,b1
          .= 0 by c3;
      end;
    end; then
    for x st x in dom q holds q.x = 0; then
    q = dom q --> 0 by FUNCOP_1:17; then
    q = len q |-> 0 by FINSEQ_1:def 3; then
    Sum q = 0 by RVSUM_1:111;
    hence Entropy p = 0;
  end;

theorem th310a:
  for p,q being non empty ProbFinS FinSequence of REAL,
    pp,qq being FinSequence of REAL st len p = len q &
    len pp = len p & len qq = len q &
    (for k st k in dom p holds p.k > 0 & q.k > 0 &
      pp.k=-p.k*log(2,p.k) & qq.k = -p.k*log(2,q.k)) holds
        Sum pp <= Sum qq &
        ((for k st k in dom p holds p.k=q.k) iff Sum pp = Sum qq) &
        ((ex k st k in dom p & p.k<>q.k) iff Sum pp < Sum qq)
   proof let p,q be non empty ProbFinS FinSequence of REAL,
             pp,qq be FinSequence of REAL such that
a1: len p = len q & len pp = len p & len qq = len q and
a2: for k st k in dom p holds p.k > 0 & q.k > 0 &
    pp.k=-p.k*log(2,p.k) & qq.k=-p.k*log(2,q.k);
    set n = len p;
a0: dom p = Seg n by FINSEQ_1:def 3;
aa: number_e > 0 & number_e <> 1 by TAYLOR_1:11;
ab: len (pp - qq) = len p by a1,EUCLID_2:7; then
ac: dom (pp - qq) = dom pp & dom (pp - qq) = dom qq by a1,FINSEQ_3:31;
ae: dom pp = Seg len pp by FINSEQ_1:def 3 .= dom p by a1,FINSEQ_1:def 3;
    for k st k in dom pp holds (pp - qq).k = pp.k - qq.k
      by ac,SEQ_1:def 4; then
a4: Sum pp - Sum qq = Sum(pp-qq) by a1,ab,th306a;
    set p1 = pp-qq;
a5: len p1 = len p & for k st k in dom p holds
      p1.k = p.k*log(2,number_e)*log(number_e,(q.k)/(p.k))
    proof
      thus len p1 = len p by a1,EUCLID_2:7;
      thus for k st k in dom p holds
        p1.k = p.k*log(2,number_e)*log(number_e,(q.k)/(p.k))
      proof let k such that
c2:     k in dom p;
c4:     p.k > 0 & q.k > 0 &
        pp.k=-p.k*log(2,p.k) &
        qq.k=-p.k*log(2,q.k) by c2,a2; then
c5:     1/(p.k) > 0 & (q.k)/(p.k) > 0 by REAL_2:127;
c6:     -log(2,p.k)=log(2,1/(p.k)) by c4,th290;
c7:     log(2,(q.k)/(p.k))
          = log(2,number_e)*log(number_e,(q.k)/(p.k)) by c5,aa,POWER:64;
        thus p1.k = pp.k - qq.k by c2,ae,ac,SEQ_1:def 4
          .= p.k*(log(2,1/(p.k)) + log(2,(q.k))) by c4,c6
          .= p.k*log(2,(1/(p.k))*(q.k)) by c4,c5,POWER:61
          .= p.k*log(2,(1*(q.k))/(p.k)) by XCMPLX_1:75
          .= p.k*log(2,number_e)*log(number_e,(q.k)/(p.k)) by c7;
      end;
    end;
    deffunc F(Nat) = p.$1*log(2,number_e)*(((q.$1)/(p.$1))-1);
    consider p2 be FinSequence of REAL such that
a6: len p2 = n and
a7: for k st k in Seg n holds p2.k = F(k) from FINSEQ_2:sch 1;
af: dom p2 = Seg len p2 by FINSEQ_1:def 3 .= dom p by a6,FINSEQ_1:def 3;
ag: dom p1 = dom p2 by a6,a5,FINSEQ_3:31;
    number_e-0 > 2-1 by TAYLOR_1:11,XREAL_1:17; then
a8: log(2,number_e) > 0 by th290b;
a9: for k st k in Seg n holds p1.k <= p2.k
    proof let k such that
e1:   k in Seg n;
ee:   k in dom p by e1,FINSEQ_1:def 3;
e3:   p.k > 0 & q.k > 0 by ee,a2; then
      (q.k)/(p.k) > 0 by REAL_2:127; then
e4:   log(number_e,(q.k)/(p.k)) <= ((q.k)/(p.k))-1 by th300a;
      p.k * log(2,number_e) > 0 by e3,a8,REAL_2:122; then
      p.k * log(2,number_e)*log(number_e,(q.k)/(p.k))<=
        p.k * log(2,number_e)*(((q.k)/(p.k))-1) by e4,XREAL_1:66; then
      p1.k <= p.k * log(2,number_e) *(((q.k)/(p.k))-1) by ee,a5;
      hence thesis by e1,a7;
    end;
ah: p1 is Element of n-tuples_on REAL by a5,FINSEQ_2:110;
ai: p2 is Element of n-tuples_on REAL by a6,FINSEQ_2:110;
al: len(q-p)=len q by a1,EUCLID_2:7; then
an: dom(q-p)=dom p2 by a6,a1,FINSEQ_3:31; then
ao: dom p2 = dom(log(2,number_e)*(q-p)) by SEQ_1:def 6;
az: for k st k in dom p2 holds p2.k=(log(2,number_e)*(q-p)).k
    proof let k such that
f0:   k in dom p2;
ff:   k in Seg n by f0,af,FINSEQ_1:def 3;
f2:   p.k>0 by a2,f0,af;
      thus p2.k = log(2,number_e)*p.k*(((q.k)/(p.k))-1) by ff,a7
        .= log(2,number_e)*(p.k*((q.k)/(p.k))-p.k)
        .= log(2,number_e)*(((p.k)*(q.k))/(p.k)-p.k) by XCMPLX_1:75
        .= log(2,number_e)*(((p.k)/(p.k))*(q.k)-p.k) by XCMPLX_1:75
        .= log(2,number_e)*(1*(q.k)-p.k) by f2,XCMPLX_1:60
        .= log(2,number_e)*(q-p).k by f0,an,SEQ_1:def 4
        .= (log(2,number_e)*(q-p)).k by RVSUM_1:66;
    end;
ar: for k st k in dom q holds (q-p).k=q.k-p.k
    proof let k such that
h1:   k in dom q;
      k in dom(q-p) by h1,al,FINSEQ_3:31;
      hence thesis by SEQ_1:def 4;
    end;
at: Sum p2 = Sum(log(2,number_e)*(q-p)) by ao,FINSEQ_1:17,az
      .= log(2,number_e)* Sum(q-p) by RVSUM_1:117
      .= log(2,number_e)* (Sum q - Sum p) by th306a,a1,al,ar
      .= log(2,number_e)*(1-Sum p) by MATRPROB:def 5
      .= log(2,number_e)*(1-1) by MATRPROB:def 5 .=0; then
    Sum pp +- Sum qq <=0 by ai,ah,a9,RVSUM_1:112,a4; hence
    Sum pp <= Sum qq by REAL_2:105;
A2: (ex k st k in Seg n & p.k<>q.k) implies Sum pp < Sum qq
    proof assume ex k st k in Seg n & p.k<>q.k; then
      consider k1 being Nat such that
i1:   k1 in Seg n & p.k1 <> q.k1;
ii:   k1 in dom p by i1,FINSEQ_1:def 3; then
i2:   p.k1 > 0 & q.k1 > 0 by a2; then
i3:   q.k1/p.k1 >0 by REAL_2:127;
      q.k1/p.k1 <> 1 by i1,XCMPLX_1:58; then
i4:   log(number_e,(q.k1)/(p.k1)) < ((q.k1)/(p.k1)-1) by i3,th300a;
      p.k1 * log(2,number_e) > 0 by i2,a8,REAL_2:122; then
      p.k1 * log(2,number_e) * log(number_e,(q.k1)/(p.k1)) <
        p.k1 * log(2,number_e) * ((q.k1)/(p.k1)-1) by i4,XREAL_1:70;then
      p1.k1 < p.k1 * log(2,number_e) * ((q.k1)/(p.k1)-1) by ii,a5; then
      p1.k1 < p2.k1 by i1,a7; then
      Sum pp +- Sum qq < 0 by at,a4,i1,ai,ah,a9,RVSUM_1:113;
      hence Sum pp < Sum qq by REAL_2:106;
    end;
    thus
A3: (for k st k in dom p holds p.k=q.k) iff Sum pp = Sum qq
    proof
      hereby assume
g1:     for k st k in dom p holds p.k=q.k;
        for k st k in dom p holds p1.k = p2.k
        proof let k such that
g2:       k in dom p;
gg:       k in Seg n by FINSEQ_1:def 3,g2;
g3:       p.k > 0 by g2,a2;
          p.k = q.k by g2,g1; then
          q.k/p.k = 1 by g3,XCMPLX_1:60; then
          p.k * log(2,number_e) * log(number_e,(q.k)/(p.k)) =
            p.k * log(2,number_e) * ((q.k)/(p.k)-1) by th300a; then
          p1.k = p.k * log(2,number_e) * ((q.k)/(p.k)-1) by g2,a5;
          hence thesis by gg,a7;
        end; then
        Sum p1 = 0 by af,at,ag,FINSEQ_1:17;
        hence Sum pp = Sum qq by a4;
      end;
      assume
h1:   Sum pp = Sum qq;
      assume not for k st k in dom p holds p.k=q.k;
      hence contradiction by h1,a0,A2;
    end;
    thus thesis by a0,A2,A3;
  end;

theorem
  for p being non empty ProbFinS FinSequence of REAL st
    (for k st k in dom p holds p.k>0) holds
      (Entropy p <= log(2,len p)) &
      ((for k st k in dom p holds p.k=1/(len p)) iff Entropy p=log(2,len p)) &
      ((ex k st k in dom p & p.k<>1/(len p)) iff Entropy p<log(2,len p))
  proof let p be non empty ProbFinS FinSequence of REAL such that
a0: for k st k in dom p holds p.k > 0;
    set n = len p;
    reconsider n1=n as non empty Element of NAT by FINSEQ_1:25;
ay: dom p = Seg len p by FINSEQ_1:def 3;
    deffunc F(Nat) = 1/n1;
    consider p1 be FinSequence of REAL such that
a1: len p1 = n and
a2: for k st k in Seg n holds p1.k = F(k) from FINSEQ_2:sch 1;
a3: 1/n1 > 0 by REAL_2:127;
a4: dom p1 = Seg n by a1,FINSEQ_1:def 3;
    for k being Nat st k in dom p1 holds p1.k = 1/n1 by a2,a4; then
    p1 =(n1 |-> (1/n1)) by a1,MATRPROB:1; then
a7: p1 is non empty ProbFinS FinSequence of REAL by th270a;
    deffunc F(Nat) = -p.$1*log(2,p1.$1);
    consider p2 be FinSequence of REAL such that
a8: len p2 = n and
a9: for k st k in Seg n holds p2.k = F(k) from FINSEQ_2:sch 1;
    set p3 = - Infor_FinSeq_of p;
aa: dom p = Seg n by FINSEQ_1:def 3;
ab: dom Infor_FinSeq_of p
      = Seg len Infor_FinSeq_of p by FINSEQ_1:def 3
     .= Seg n by th196;
ac: len p3 = n & for k st k in Seg n holds p3.k=-p.k*log(2,p.k)
    proof
      dom p3 = Seg n by ab,SEQ_1:def 7;
      hence len p3 = n by FINSEQ_1:def 3;
      hereby let k such that
b2:     k in Seg n;
        thus p3.k = -((Infor_FinSeq_of p).k) by RVSUM_1:35
          .= -p.k*log(2,p.k) by b2,ab,th196;
      end;
    end;
az: for k st k in dom p holds p.k > 0 & p1.k > 0 &
      p3.k=-p.k*log(2,p.k) & p2.k = -p.k*log(2,p1.k)
      by ay,a0,a2,a3,a9,ac; then
ad: Sum p3 <= Sum p2 &
      ((for k st k in dom p holds p.k=p1.k) iff Sum p3 = Sum p2) &
      ((ex k st k in dom p & p.k<>p1.k) iff Sum p3 < Sum p2)
        by a1,a7,a8,ac,th310a;
ae: Sum p3 = Entropy p by RVSUM_1:118;
ag: dom p2 = dom p by a8,FINSEQ_3:31;
ah: p2 = (-log(2,1/n1))*p
    proof
c1:   dom p2 = dom((-log(2,1/n1))*p) by ag,SEQ_1:def 6;
      for k st k in dom p2 holds p2.k = ((-log(2,1/n1))*p).k
      proof let k such that
c2:     k in dom p2;
        thus p2.k = -p.k*log(2,p1.k) by c2,aa,ag,a9
          .= -p.k*log(2,1/n1) by c2,aa,ag,a2 .= (-log(2,1/n1))*p.k
          .= ((-log(2,1/n1))*p).k by RVSUM_1:66;
      end;
      hence thesis by c1,FINSEQ_1:17;
    end;
ai: Sum p2 = (-log(2,1/n1)) * Sum p by RVSUM_1:117,ah
        .= (-log(2,1/n1)) * 1 by MATRPROB:def 5
        .= log(2,1/(1/n1)) by a3,th290
        .= log(2,n) by XCMPLX_1:56;
    hence Entropy p <= log(2,len p) by az,a1,a7,a8,ac,th310a,ae;
    thus (for k st k in dom p holds p.k=1/(len p)) iff Entropy p=log(2,len p)
    proof
      hereby assume
dd:     for k st k in dom p holds p.k=1/(len p);
        now let k such that
d2:       k in dom p;
          thus p.k = 1/n by d2,dd .= p1.k by d2,a2,ay;
        end;
        hence Entropy p=log(2,n) by az,a1,a7,a8,ac,th310a,ai,ae;
      end;
      assume 
d3:   Entropy p=log(2,n); 
      hereby let k; assume
d4:     k in dom p;
        hence p.k = p1.k by d3,az,a1,a7,a8,ac,th310a,ai,ae
          .= 1/n by d4,a2,ay;
      end;
    end;
    thus (ex k st k in dom p & p.k<>1/(len p)) iff Entropy p<log(2,len p)
    proof
      hereby assume
        ex k st k in dom p & p.k<>1/(len p); then
        consider k1 being Nat such that
e2:     k1 in dom p & p.k1<>1/n;
        p1.k1 <> p.k1 by e2,a2,ay;
        hence Entropy p<log(2,n) by e2,az,a1,a7,a8,ac,th310a,ai,ae;
      end;
      assume Entropy p<log(2,n); then
      consider k1 being Nat such that
e3:   k1 in dom p & p.k1<>p1.k1 by ai,RVSUM_1:118,ad;
      p1.k1 = 1/n by ay,e3,a2;
      hence thesis by e3;
    end;
  end;

theorem th451:
  for M being m-nonnegative Matrix of REAL holds
    Mx2FinS(Infor_FinSeq_of M) = Infor_FinSeq_of Mx2FinS(M)
  proof let M be m-nonnegative Matrix of REAL;
    reconsider p=Mx2FinS(M) as nonnegative FinSequence of REAL by th423;
    set qq=Mx2FinS(Infor_FinSeq_of M);
    set pp=Infor_FinSeq_of p;
    set MM=Infor_FinSeq_of M;
a2: len p = len M * width M by th410;
a3: len pp = len p by th196;
a4: len MM = len M & width MM = width M by Def198; then
a5: len qq = len M * width M by th410; then
a7: dom qq = dom pp & dom qq = dom p by a2,a3,FINSEQ_3:31;
    per cases;
    suppose len M = 0; then
      qq = {} & pp = {} by a5,a2,a3,FINSEQ_1:25;
      hence thesis;
    end;
    suppose len M > 0 & width M = 0; then
      qq = {} & pp = {} by a5,a2,a3,FINSEQ_1:25;
      hence thesis;
    end;
    suppose len M > 0 & width M > 0;
      now let k such that
d1:     k in dom qq;
        k in Seg len qq by d1,FINSEQ_1:def 3; then
        k >= 1 & k <= len qq by FINSEQ_1:3; then
        reconsider l = k - 1 as Element of NAT by NAT_1:21;
        set ii=(l div width MM)+1;
        set jj=(l mod width MM)+1;
b4:     [ii,jj] in Indices MM & qq.k = MM*(ii,jj) by d1,th418;
b6:     p.k = M*(ii,jj) by a4,d1,a7,th418;
        thus pp.k = p.k *log(2,p.k) by d1,a7,th196
          .= qq.k by b6,b4,th296;
      end;
      hence thesis by a7,FINSEQ_1:17;
    end;
  end;

theorem th452:
  for p,q being ProbFinS FinSequence of REAL
    for M being Matrix of REAL st M=(ColVec2Mx p)*(LineVec2Mx q) holds
      SumAll Infor_FinSeq_of M = Sum Infor_FinSeq_of p+Sum Infor_FinSeq_of q
  proof let p,q be ProbFinS FinSequence of REAL;
        let M be Matrix of REAL such that
a1: M=(ColVec2Mx p)*(LineVec2Mx q);
    reconsider M as Joint_Probability Matrix of REAL
      by a1,th332;
    set M1 = Infor_FinSeq_of M;
    set L = LineSum M1;
aa: len M = len p & width M = len q by a1,th331;
ab: len M1 = len M & width M1 = width M by Def198;
ac: len L = len M1 by MATRPROB:def 1; then
ad: dom L = dom M1 & dom M1 = dom M & dom M = dom p by aa,ab,FINSEQ_3:31;
    len FinSeq_log(2,q) = len q & len FinSeq_log(2,p) = len p by Def299; then
af: len mlt(q,FinSeq_log(2,q)) = len q by MATRPROB:30;
a3: now let k such that
b1:   k in dom L;
      reconsider pp=Line(M,k) as nonnegative FinSequence of REAL
        by b1,ad,th294;
b3:   pp = p.k * q by a1,b1,ad,th331;
b4:   p.k>=0 by Def109,b1,ad;
      dom (p.k*log(2,p.k)*q) = dom q by SEQ_1:def 6; then
bb:   len (p.k*log(2,p.k)*q) = len q by FINSEQ_3:31;
      dom (p.k*mlt(q,FinSeq_log(2,q))) = dom mlt(q,FinSeq_log(2,q))
        by SEQ_1:def 6; then
b5:   len (p.k*log(2,p.k)*q) = len (p.k*mlt(q,FinSeq_log(2,q)))
        by bb,af,FINSEQ_3:31;
      thus L.k = Sum(M1.k) by MATRPROB:def 1,b1
        .= Sum(Line(M1,k)) by MATRIX_2:18,b1,ad
        .= Sum(Infor_FinSeq_of pp) by b1,ad,th295
        .= Sum((p.k*log(2,p.k)*q)+(p.k*mlt(q,FinSeq_log(2,q))))
          by b3,b4,th293
        .= Sum(p.k*log(2,p.k)*q)+Sum(p.k*mlt(q,FinSeq_log(2,q)))
          by INTEGRA5:2,b5
        .= p.k*log(2,p.k)*Sum q+Sum(p.k*mlt(q,FinSeq_log(2,q)))
          by RVSUM_1:117
        .= p.k*log(2,p.k)*1+Sum(p.k*mlt(q,FinSeq_log(2,q)))
          by MATRPROB:def 5        
        .= p.k*log(2,p.k)+p.k*Sum Infor_FinSeq_of q by RVSUM_1:117;
    end;
    set p1= Infor_FinSeq_of p;
    set p2= (Sum Infor_FinSeq_of q)*p;
a0: len p1 = len p by th196;
    dom p2 = dom p by SEQ_1:def 6; then
ai: len p1 = len L & len p1 = len p2 & dom p1 = dom L & dom p1 = dom p2
      by FINSEQ_3:31,aa,ab,ac,a0;
    now let k such that
c1:   k in dom p1;
      thus L.k = p.k*log(2,p.k)+p.k*Sum Infor_FinSeq_of q by ai,c1,a3
        .= p1.k + p.k*Sum Infor_FinSeq_of q by c1,th196
        .= p1.k + p2.k by RVSUM_1:66;
    end; then
    Sum L = Sum p1 + Sum p2 by ai,th306
      .= Sum p1 + (Sum Infor_FinSeq_of q)*Sum p by RVSUM_1:117
      .= Sum p1 + (Sum Infor_FinSeq_of q)*1 by MATRPROB:def 5
      .= Sum Infor_FinSeq_of p+Sum Infor_FinSeq_of q;
    hence thesis by MATRPROB:def 3;
  end;

definition let MR;
  func Entropy_of_Joint_Prob MR -> Real equals
    Entropy Mx2FinS MR;
  coherence;
end;

theorem
  for p,q being ProbFinS FinSequence of REAL holds
    Entropy_of_Joint_Prob ((ColVec2Mx p) * (LineVec2Mx q)) =
      Entropy p + Entropy q
  proof let p,q be ProbFinS FinSequence of REAL;
    set pp = Mx2FinS((ColVec2Mx p) * (LineVec2Mx q));
    set M = (ColVec2Mx p) * (LineVec2Mx q);
    reconsider M as Joint_Probability Matrix of REAL by th332;
    reconsider pp as ProbFinS FinSequence of REAL by th450;
    Entropy p + Entropy q
       = -(Sum Infor_FinSeq_of p + Sum Infor_FinSeq_of q)
      .= -(SumAll (Infor_FinSeq_of M)) by th452
      .= -(Sum (Mx2FinS(Infor_FinSeq_of M))) by th422
      .= -(Sum (Infor_FinSeq_of pp)) by th451;
    hence thesis;
  end;

definition let MR;
  func Entropy_of_Cond_Prob MR -> FinSequence of REAL means :Def10:
    len it = len MR &
    for k st k in dom it holds it.k = Entropy Line(MR,k);
  existence
  proof
    deffunc F(Nat) = Entropy Line(MR,$1);
    consider p be FinSequence of REAL such that
a1: len p = len MR and
a2: for k st k in Seg len MR holds p.k = F(k) from FINSEQ_2:sch 1;
    take p;
    dom p = Seg len MR by a1,FINSEQ_1:def 3;
    hence thesis by a1,a2;
  end;
  uniqueness
  proof let p1,p2 be FinSequence of REAL such that
b1: len p1 = len MR &
    for k st k in dom p1 holds p1.k = Entropy Line(MR,k) and
b2: len p2 = len MR &
    for k st k in dom p2 holds p2.k = Entropy Line(MR,k);
b3: dom p1 = dom p2 by b1,b2,FINSEQ_3:31;
    now let k such that
b4:   k in dom p1;
      thus p1.k = Entropy Line(MR,k) by b4,b1 .= p2.k by b2,b3,b4;
    end;
    hence thesis by b3,FINSEQ_1:17;
  end;
end;

theorem th444:
  for M being non empty-yielding Conditional_Probability Matrix of REAL
  for p being FinSequence of REAL holds
    p = Entropy_of_Cond_Prob M iff
      len p = len M &
      for k st k in dom p holds p.k = -Sum ((Infor_FinSeq_of M).k)
  proof let M be non empty-yielding Conditional_Probability Matrix of REAL;
        let p be FinSequence of REAL;
av: len Infor_FinSeq_of M = len M & width Infor_FinSeq_of M = width M
      by Def198; then
ab: dom Infor_FinSeq_of M = dom M & Seg width Infor_FinSeq_of M = Seg width M
      by FINSEQ_3:31;
  hereby assume
a1:   p = Entropy_of_Cond_Prob M; then
az:   len p = len M &
      for k st k in dom p holds p.k = Entropy Line(M,k) by Def10; then
a2:   dom p = dom M by FINSEQ_3:31;
      now let k such that
b1:     k in dom p;
b3:     k in dom (Infor_FinSeq_of M) by b1,az,av,FINSEQ_3:31;
        thus p.k = Entropy Line(M,k) by a1,b1,Def10
          .= -Sum Line(Infor_FinSeq_of M,k) by th295,b1,a2
          .= -Sum ((Infor_FinSeq_of M).k) by b3,MATRIX_2:18;
      end;
      hence len p = len M &
      for k st k in dom p holds p.k = -Sum ((Infor_FinSeq_of M).k) by a1,Def10;
    end; assume
c1: len p = len M &
    for k st k in dom p holds p.k = -Sum ((Infor_FinSeq_of M).k); then
cc: dom p = dom M by FINSEQ_3:31;
    now let k such that
c2:  k in dom p;
     thus p.k = -Sum ((Infor_FinSeq_of M).k) by c2,c1
       .= -Sum Line(Infor_FinSeq_of M,k) by c2,cc,ab,MATRIX_2:18
       .= Entropy Line(M,k) by th295,c2,cc;
    end;
    hence p = Entropy_of_Cond_Prob M by c1,Def10;
  end;

theorem th445:
  for M being non empty-yielding Conditional_Probability Matrix of REAL holds
    Entropy_of_Cond_Prob M = -LineSum Infor_FinSeq_of M
  proof let M be non empty-yielding Conditional_Probability Matrix of REAL;
    set p=Entropy_of_Cond_Prob M;
    set q=-LineSum Infor_FinSeq_of M;
a1: len p = len M &
    for k st k in dom p holds p.k = -Sum ((Infor_FinSeq_of M).k) by th444;
aa: dom q = dom LineSum Infor_FinSeq_of M by SEQ_1:def 7; then
    len q = len LineSum Infor_FinSeq_of M by FINSEQ_3:31; then
    len q = len Infor_FinSeq_of M by MATRPROB:def 1; then
    len q = len M by Def198; then
a5: dom p = dom q & dom p = dom M by a1,FINSEQ_3:31;
    now let k such that
b1:   k in dom p;
      thus p.k = -Sum ((Infor_FinSeq_of M).k) by th444,b1
        .= -(Sum Infor_FinSeq_of M).k by b1,a5,aa,MATRPROB:def 1
        .= q.k by RVSUM_1:35;
    end;
    hence thesis by a5,FINSEQ_1:17;
  end;

theorem th454:
  for p being ProbFinS FinSequence of REAL
  for M being non empty-yielding Conditional_Probability Matrix of REAL st
  len p = len M holds
    for M1 being Matrix of REAL st M1=(Vec2DiagMx p) * M holds
    SumAll Infor_FinSeq_of M1 =
      Sum Infor_FinSeq_of p + Sum mlt(p,LineSum(Infor_FinSeq_of M))
  proof let p be ProbFinS FinSequence of REAL;
    let M be non empty-yielding Conditional_Probability Matrix of REAL;
    assume
a0: len p = len M;
    let M1 be Matrix of REAL such that
a1: M1=(Vec2DiagMx p)*M;
    reconsider M1 as Joint_Probability Matrix of REAL by a0,a1,th237;
    set M2= Infor_FinSeq_of M1;
    set L = LineSum M2;
aa: len M1 = len p & width M1 = width M by a0,a1,th235;
ab: len M2 = len M1 & width M2 = width M1 by Def198;
ac: len L = len M2 by MATRPROB:def 1; then
ad: dom L = dom M2 & dom M2 = dom M1 & dom M1 = dom p & dom p = dom M
      by aa,ab,FINSEQ_3:31,a0;
a2: for k st k in dom L holds L.k =
      p.k*log(2,p.k)+p.k*Sum Infor_FinSeq_of Line(M,k)
    proof let k such that
b1:   k in dom L;
      reconsider pp=Line(M1,k) as nonnegative FinSequence of REAL
        by b1,ad,th294;
      reconsider q=Line(M,k) as non empty ProbFinS FinSequence of REAL
        by MATRPROB:60,b1,ad;
      len FinSeq_log(2,q) = len q by Def299; then
ba:   len mlt(q,FinSeq_log(2,q)) = len q by MATRPROB:30;
b3:   pp = p.k * q by a0,a1,b1,ad,th236;
b4:   p.k>=0 by Def109,b1,ad;
      dom (p.k*log(2,p.k)*q) = dom q by SEQ_1:def 6; then
bb:   len (p.k*log(2,p.k)*q) = len q by FINSEQ_3:31;
      dom (p.k*mlt(q,FinSeq_log(2,q))) = dom mlt(q,FinSeq_log(2,q))
        by SEQ_1:def 6; then
b5:   len (p.k*mlt(q,FinSeq_log(2,q))) = len mlt(q,FinSeq_log(2,q))
        by FINSEQ_3:31;
       L.k = Sum(M2.k) by MATRPROB:def 1,b1
        .= Sum(Line(M2,k)) by MATRIX_2:18,b1,ad
        .= Sum(Infor_FinSeq_of pp) by b1,ad,th295
        .= Sum((p.k*log(2,p.k)*q)+
          (p.k*mlt(q,FinSeq_log(2,q)))) by b3,b4,th293
        .= Sum(p.k*log(2,p.k)*q)+Sum(p.k*mlt(q,FinSeq_log(2,q)))
          by INTEGRA5:2,b5,bb,ba
        .= p.k*log(2,p.k)*Sum q+Sum(p.k*mlt(q,FinSeq_log(2,q)))
          by RVSUM_1:117
        .= p.k*log(2,p.k)*1+Sum(p.k*mlt(q,FinSeq_log(2,q)))
          by MATRPROB:def 5
        .= p.k*log(2,p.k)+p.k*Sum Infor_FinSeq_of q by RVSUM_1:117;
      hence thesis;
    end;
    set M3 = Infor_FinSeq_of M;
    set L2 = LineSum M3;
    len M3 = len M & width M3 = width M by Def198; then
ag: len L2 = len M & len L2 = len p & len L2 = len M3 by a0,MATRPROB:def 1;
    set p1 = Infor_FinSeq_of p;
    set p2 = mlt(p,L2);
a0: len p1 = len p by th196;
ai: len p2 = len p by ag,MATRPROB:30; then
aj: dom p1 = dom L & dom p1 = dom p2 & dom L2 = dom p1 & dom p1 = dom M3 &
    dom p1 = dom M by aa,ab,ac,ag,a0,FINSEQ_3:31;
    now let k such that
c1:   k in dom p1;
c2:   p2.k = p.k*L2.k by c1,aj,RVSUM_1:86
        .= p.k*Sum (M3.k) by MATRPROB:def 1,c1,aj
        .= p.k*Sum (Line(M3,k)) by c1,aj,MATRIX_2:18
        .= p.k*Sum (Infor_FinSeq_of Line(M,k)) by th295,c1,aj;
      thus L.k = p.k*log(2,p.k)+p.k*Sum Infor_FinSeq_of Line(M,k) by aj,c1,a2
        .= p1.k + p2.k by c2,c1,th196;
    end; then
    Sum L = Sum Infor_FinSeq_of p+Sum mlt(p,LineSum(Infor_FinSeq_of M)) 
      by ai,aa,ab,ac,a0,th306;
    hence thesis by MATRPROB:def 3;
  end;

theorem
  for p being ProbFinS FinSequence of REAL
  for M being non empty-yielding Conditional_Probability Matrix of REAL st
  len p = len M holds
    Entropy_of_Joint_Prob ((Vec2DiagMx p) * M) =
      Entropy p + Sum mlt(p,Entropy_of_Cond_Prob M)
  proof let p be ProbFinS FinSequence of REAL;
        let M be non empty-yielding Conditional_Probability Matrix of REAL;
    assume
a1: len p = len M;
    set pp = Mx2FinS((Vec2DiagMx p) * M);
    set M1 = (Vec2DiagMx p) * M;
    len LineSum Infor_FinSeq_of M = len Infor_FinSeq_of M by MATRPROB:def 1
      .= len p by a1,Def198; then
ab: p is Element of (len p)-tuples_on REAL &
    LineSum Infor_FinSeq_of M is Element of (len p)-tuples_on REAL
      by FINSEQ_2:110;
    reconsider M1 as Joint_Probability Matrix of REAL by a1,th237;
    reconsider pp as ProbFinS FinSequence of REAL by a1,th450a;
a3: Entropy_of_Joint_Prob M1 = -Sum Mx2FinS Infor_FinSeq_of M1 by th451
      .= -SumAll Infor_FinSeq_of M1 by th422
      .= -(Sum Infor_FinSeq_of p + Sum mlt(p,LineSum Infor_FinSeq_of M))
        by th454,a1
      .= -Sum Infor_FinSeq_of p - Sum mlt(p,LineSum Infor_FinSeq_of M);
    Entropy p + Sum mlt(p,Entropy_of_Cond_Prob M)
       =-Sum Infor_FinSeq_of p + Sum mlt(p,-LineSum Infor_FinSeq_of M)
        by th445
      .=-Sum Infor_FinSeq_of p + Sum mlt(p,(-1)*LineSum Infor_FinSeq_of M)
       by EUCLID_2:3
       .=-Sum Infor_FinSeq_of p + Sum ((-1)*mlt(p,LineSum Infor_FinSeq_of M))
        by RVSUM_1:94,ab
      .=-Sum Infor_FinSeq_of p + Sum -mlt(p,LineSum Infor_FinSeq_of M)
        by EUCLID_2:3
      .=-Sum Infor_FinSeq_of p + -Sum mlt(p,LineSum Infor_FinSeq_of M)
        by RVSUM_1:118;
    hence thesis by a3;
  end;
