:: Catalan Numbers
::  by Dorota Cz\c{e}stochowska and Adam Grabowski
::
:: Received May 31, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies ORDINAL2, ARYTM, ARYTM_3, ARYTM_1, QC_LANG1, NEWTON, REALSET1,
      CATALAN1, INT_1;
 notations XBOOLE_0, ORDINAL1, ORDINAL2, XCMPLX_0, XREAL_0, REAL_1, NAT_1,
      NEWTON, INT_1, BINARITH, REALSET1, BINOM;
 constructors FINSEQOP, INT_1, REAL_1, NAT_1, RVSUM_1, FINSEQ_4, SEQ_1,
      MEMBERED, XBOOLE_0, NEWTON, BINARITH, REALSET1, BINOM;
 registrations INT_1, XREAL_0, NAT_1, MEMBERED, NAT_2;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 theorems AXIOMS, REAL_1, NAT_1, XCMPLX_1, NAT_2, SQUARE_1, NEWTON, BINARITH,
      REAL_2, AMI_5, JORDAN3, SCMFSA_7, GOBOARD9, CQC_THE1, SPRECT_3, XREAL_0,
      ORDINAL2, INT_1;
 schemes NAT_1, NAT_2, BINARITH;

begin :: Preliminaries

theorem Th1:
  for n being natural number st n > 1 holds
    n -' 1 <= 2 * n -' 3
  proof
    let n be natural number;
    assume
A1: n > 1; then
A2: n -' 1 = n - 1 by SCMFSA_7:3;
    2 * 1 < 2 * n by A1,REAL_1:70; then
    2 + 1 <= 2 * n by NAT_1:38; then
A3: 2 * n -' 3 = 2 * n - 3 by SCMFSA_7:3;
    n is Nat by ORDINAL2:def 21; then
    n -' 1 > 1 -' 1 by A1,SPRECT_3:10; then
    n -' 1 + n > 1 -' 1 + n by REAL_1:53; then
    n -' 1 + n > 0 + n by GOBOARD9:1; then
    2 * n - 1 > n by A2; then
    2 * n - 1 - 1 > n - 1 by REAL_1:54; then
    2 * n - 2 - 1 >= n - 1 by SCMFSA_7:4;
    hence thesis by A1,A3,SCMFSA_7:3;
  end;

theorem Th2:
  for n being natural number st n >= 1 holds
    n -' 1 <= 2 * n -' 2
  proof
    let n be natural number;
A1: n is Nat by ORDINAL2:def 21;
    assume
A2: n >= 1; then
    1 + 0 <= n; then
    n -' 1 >= 0 by A1,SPRECT_3:8; then
A3: 1 * (n -' 1) <= 2 * (n -' 1) by AXIOMS:25;
    2 * 1 <= 2 * n by A2,AXIOMS:25; then
A4: 2 * n -' 2 = 2 * n - 2 by SCMFSA_7:3;
    n -' 1 = n - 1 by A2,SCMFSA_7:3;
    hence thesis by A3,A4;
  end;

theorem Th3:
  for n being natural number st n > 1 holds
    n < 2 * n -' 1
  proof
    let n be natural number;
    assume
A1: n > 1; then
    1 * 2 < 2 * n by REAL_1:70; then
A2: 1 < 2 * n by AXIOMS:22;
    n + n > n + 1 by A1,REAL_1:53; then
    n + n - 1 > n + 1 - 1 by REAL_1:54;
    hence thesis by A2,SCMFSA_7:3;
  end;

theorem Th4:
  for n being natural number st n > 1 holds
    n -' 2 + 1 = n -' 1
  proof
    let n be natural number;
    assume n > 1; then
A1: n -' 1 >= 1 by JORDAN3:12;
    n -' 2 + 1 = n -' 1 -' 1 + 1 by JORDAN3:8
              .= n -' 1 by A1,AMI_5:4;
    hence thesis;
  end;

Lm1:
  for a, b being natural number st a < b & 1 < b holds
    a -' 1 < b -' 1
  proof
    let a, b be natural number;
A1: a is Nat & b is Nat by ORDINAL2:def 21;
    assume a < b & 1 < b;
    hence thesis by A1,SPRECT_3:10;
  end;

theorem
  for n being natural number st n > 1 holds
    (4 * n * n - 2*n) / (n + 1) > 1
  proof
    let n be natural number;
    assume
A1: n > 1; then
    n <> 0; then
A2: n is non trivial by A1,NAT_2:30;
    defpred P[Nat] means (4 * $1 * $1 - 2*$1)/($1 + 1) > 1;
A3: P[2];
A4: for k be non trivial Nat st P[k] holds P[k + 1]
    proof
      let k be non trivial Nat such that
A5:   P[k];
      set k1 = k+1;
A6:   0 <= k + 1 by NAT_1:18;
A7:   k1 + 1 > 0 by NAT_1:18;
      (4 * k * k - 2*k) / (k + 1) = (4 * k * k - 2*k) * ( 1/ (k + 1))
        by XCMPLX_1:100; then
      (4 * k * k - 2*k) * (1 / (k + 1)) * (k + 1) > 1 * (k + 1)
        by A5,A6,REAL_1:70; then
      (4 * k * k - 2*k) > 1 * (k + 1) by XCMPLX_1:110; then
A8:   (4 * k * k - 2*k)-(k + 1) > 0 by SQUARE_1:11;
A9:   0 < 8 * k + 1 by NAT_1:37;
A10:   (4 * k * k - 3*k - 1) + (8 * k + 1) > 0 + 0
        by A8,A9,REAL_1:67;
      (4 * k1 * k1 - 2*k1 - (k1 + 1)) + (k1 + 1) > 0 + (k1 + 1)
        by A10,REAL_1:67; then
      (4 * k1 * k1 - 2 * k1) / (k1 + 1) > (k1 + 1) / (k1 + 1)
        by A7,REAL_1:73;
      hence P[k+1] by XCMPLX_1:60;
    end;
A11: n is Nat by ORDINAL2:def 21;
    for k be non trivial Nat holds P[k] from NAT_2:sch 2 (A3, A4);
    hence thesis by A2,A11;
  end;

theorem Th6:
  for n being natural number st n > 1 holds
    (2 * n -' 2)! * n * (n + 1) < (2 * n)!
  proof
    let n be natural number;
    assume
A1: n > 1; then
A2: 2 * 1 < 2 * n by REAL_1:70; then
A3: 1 < 2 * n by AXIOMS:22;
    2 - 1 < 2 * n - 1 by A2,REAL_1:54; then
A4: 1 < 2 * n -' 1 by A3,SCMFSA_7:3;
A5: 2 * n -' 2 + 1 = 2 * n -' 1 -' 1 + 1 by JORDAN3:8
                  .= 2 * n -' 1 by A4,AMI_5:4;
A6: 2 * n -' 1 + 1 = 2 * n by A3,AMI_5:4;
A7: (2 * n -' 2)! * (2 * n -' 1) * (2 * n) =
    (2 * n -' 1)! * (2 * n) by A5,NEWTON:21
    .= (2 * n)! by A6,NEWTON:21;
A8: n < 2 * n -' 1 by A1,Th3;
A9: n + 1 < n + n by A1,REAL_1:53;
A10: (2 * n -' 2)! > 0 by NEWTON:23;
    n > 0 by A1;then
A11: (2 * n -' 2)! * n > 0 * n by A10,REAL_1:70;
    n + 1 > n by REAL_1:69;then
A12: n + 1 > 0 by A1;
    (2 * n -' 2)! * n < (2 * n -' 2)! * (2 * n -' 1)
      by A8,A10,REAL_1:70;
    hence thesis by A7,A9,A11,A12,REAL_2:199;
  end;

theorem Th7:
  for n being natural number holds
    2 * (2 - (3 / (n + 1))) < 4
  proof
    let n be natural number;
    assume 2 * (2 - (3 / (n + 1))) >= 4; then
    2 * (2 - (3 / (n + 1))) / 2 >= 4 / 2 by REAL_1:73; then
    2 - (3 / (n + 1)) - 2 >= 2 - 2 by REAL_1:49; then
    --- (3 / (n + 1)) >= 0; then
    3 / (n + 1) <= 0 by REAL_1:66; then
    n + 1 <= 0 by REAL_2:127;
    hence thesis by NAT_1:18;
  end;

begin :: Catalan Numbers

definition let n be natural number;
  func Catalan (n) -> Real equals :Def1:
    ((2*n -' 2) choose (n -' 1)) / n;
  coherence by XREAL_0:def 1;
end;

theorem Th8:
  for n being natural number st n > 1 holds
    Catalan (n) = (2 * n -' 2)! / ((n -' 1)! * (n!))
  proof
    let n be natural number;
    assume
A1: n > 1; then
A2: n -' 1 <= 2 * n -' 2 by Th2;
A3: 2 * 1 <= 2 * n by A1,AXIOMS:25;
A4: n -' 1 + 1 = n by A1,AMI_5:4;
    (2*n -' 2) - (n -' 1) = (2*n -' 2) - (n - 1) by A1,SCMFSA_7:3
         .= (2*n - 2) - (n - 1) by A3,SCMFSA_7:3
         .= n -' 1 by A1,SCMFSA_7:3; then
A5: ((2*n -' 2) choose (n -' 1)) =
      ((2*n -' 2)!) / ((n -' 1)! * ((n -' 1)!)) by A2,NEWTON:def 3;
    Catalan (n) = ((2*n -' 2) choose (n -' 1)) / n by Def1
       .= ((2*n -' 2)!) / ((n -' 1)! * ((n -' 1)!) * n) by A5,XCMPLX_1:79
       .= ((2*n -' 2)!) / ((n -' 1)! * (((n -' 1)!) * n))
       .= ((2*n -' 2)!) / ((n -' 1)! * (n!)) by A4,NEWTON:21;
    hence thesis;
  end;

theorem Th9:
  for n being natural number st n > 1 holds
    Catalan n =
      4 * ((2*n -' 3) choose (n -' 1)) - ((2*n -' 1) choose (n -' 1))
  proof
    let n be natural number;
    assume
A1: n > 1; then
A2: n -' 1 <= 2 * n -' 3 by Th1;
A3: 1 + 1 <= n by A1,NAT_1:38; then
    2 * 2 <= 2 * n by AXIOMS:25; then
A4: 3 <= 2 * n by AXIOMS:22;
    (2*n -' 3) - (n -' 1) = (2*n -' 3) - (n - 1) by A1,SCMFSA_7:3
         .= (2*n - 3) - (n - 1) by A4,SCMFSA_7:3
         .= n - 2
         .= n -' 2 by A3,SCMFSA_7:3; then
    ((2*n -' 3) choose (n -' 1)) =
      ((2*n -' 3)!) / ((n -' 1)! * ((n -' 2)!)) by A2,NEWTON:def 3; then
A5: 4 * ((2*n -' 3) choose (n -' 1)) =
      (4 * ((2*n -' 3)!)) / ((n -' 1)! * ((n -' 2)!)) by XCMPLX_1:75;
A6: n > 0 by A1; then
A7: 1 * n < 2 * n by REAL_1:70; then
A8: 1 < 2 * n by A1,AXIOMS:22; then
A9: n -' 1 < 2 * n -' 1 by A7,Lm1;
    2 * n -' 1 - (n -' 1) = 2 * n -' 1 - (n - 1) by A1,SCMFSA_7:3
      .= 2 * n -' 1 - n + 1
      .= 2 * n - 1 - n + 1 by A8,SCMFSA_7:3
      .= n; then
A10: (2*n -' 1) choose (n -' 1) =
      ((2*n -' 1)!) / ((n -' 1)! * (n!)) by A9,NEWTON:def 3;
    n - 1 > 0 by A1,SQUARE_1:11; then
A11: n * (n - 1) <> 0 by A6,XCMPLX_1:6;
A12: n -' 1 = n - 1 by A1,SCMFSA_7:3;
A13: 2 * 1 <= 2 * n by A1,AXIOMS:25; then
A14: 2 * n -' 2 = 2 * n - 2 by SCMFSA_7:3;
A15: n -' 1 + 1 = n by A12;
A16: 1 < 2 * n by A1,A7,AXIOMS:22; then
A17: 2 * n -' 1 = 2 * n - 1 by SCMFSA_7:3;
A18: n -' 2 + 1 = n -' 1 by A1,Th4;
    2 * n -' 3 = 2 * n - 3 by A4,SCMFSA_7:3; then
A19: 2 * n -' 3 + 1 = 2 * n - 2
                  .= 2 * n -' 2 by A13,SCMFSA_7:3;
A20: 2 * n -' 2 + 1 = 2 * n -' 1 by A16,Th4;
A21: 4 * ((2*n -' 3) choose (n -' 1)) =
      ((n * (n - 1)) * (4 * ((2*n -' 3)!))) /
         (((n * (n - 1)) * ((n -' 1)! * ((n -' 2)!)))) by A5,A11,XCMPLX_1:92
      .= (((n - 1) * n) * (4 * ((2*n -' 3)!))) /
         (((n - 1) * (n * ((n -' 1)!)) * ((n -' 2)!)))
      .= (((n - 1) * n) * (4 * ((2*n -' 3)!))) /
         (((n - 1) * (n!) * ((n -' 2)!))) by A15,NEWTON:21
      .= (((n - 1) * n) * (4 * ((2*n -' 3)!))) /
         (((n!) * ((n -' 1) * ((n -' 2)!)))) by A12
      .= (2 * n * ((2 * n -' 2) * ((2*n -' 3)!))) /
         (((n!) * ((n -' 1)!))) by A14,A18,NEWTON:21
      .= (2 * n * ((2*n -' 2)!)) /
         (((n!) * ((n -' 1)!))) by A19,NEWTON:21;
A22: (2*n -' 1) choose (n -' 1) =
       ((2*n -' 2)! * (2*n -' 1)) / ((n -' 1)! * (n!)) by A10,A20,NEWTON:21;
    4 * ((2*n -' 3) choose (n -' 1)) - ((2*n -' 1) choose (n -' 1)) =
        ((2 * n * ((2*n -' 2)!)) - ((2*n -' 2)! * (2*n -' 1)))
        / (((n!) * ((n -' 1)!))) by A21,A22,XCMPLX_1:121
      .= Catalan n by A1,A17,Th8;
    hence thesis;
  end;

theorem Th10:
  Catalan 0 = 0
  proof
    Catalan 0 = ((2*0 -' 2) choose (0 -' 1)) / 0 by Def1;
    hence thesis by XCMPLX_1:49;
  end;

theorem Th11:
  Catalan 1 = 1
  proof
    Catalan 1 = ((2*1 -' 2) choose (1 -' 1)) / 1 by Def1
       .= (2*1 -' 2) choose 0 by GOBOARD9:1
       .= 1 by GOBOARD9:1,NEWTON:27;
    hence thesis;
  end;

theorem Th12:
  Catalan 2 = 1
  proof
A1: 2 -' 1 = 2 - 1 by SCMFSA_7:3 .= 1;
A2: 4 -' 2 = 4 - 2 by SCMFSA_7:3 .= 2;
    Catalan 2 = ((2*2 -' 2) choose (2 -' 1)) / 2 by Def1
       .= 2 / 2 by A1,A2,NEWTON:33
       .= 1;
    hence thesis;
  end;

theorem Th13:
  for n being natural number holds
    Catalan (n) is Integer
  proof
    let n be natural number;
    per cases by CQC_THE1:2;
    suppose n = 0;
      hence thesis by Th10;
    end;
    suppose n = 1;
      hence thesis by Th11;
    end;
    suppose n > 1; then
      Catalan n =
        4 * ((2*n -' 3) choose (n -' 1)) - ((2*n -' 1) choose (n -' 1))
          by Th9;
      hence thesis;
    end;
  end;

theorem Th14:
  for k being natural number st k >= 1 holds
    Catalan (k + 1) = (2*k)! / (k! * ((k+1)!))
  proof
    let k be natural number;
    assume
A1: k >= 1;
    per cases by A1,REAL_1:def 5;
    suppose k = 1;
      hence thesis by Th12,NEWTON:19,20;
    end;
    suppose k > 1; then
A2:   k > 1 + 0;
A3:   Catalan (k + 1) = ((2*(k+1) -' 2) choose ((k+1) -' 1)) / (k + 1)
        by Def1;
A4:   2*k + 2 -' 2 = 2*k by BINARITH:39;
A5:   k + 1 -' 1 = k by BINARITH:39;
      reconsider l = 2*k - k as Nat by ORDINAL2:def 21;
A6:   l = k;
      0 <= k by A2; then
      1*k <= 2*k by AXIOMS:25; then
A7:   (2*k) choose k = (2*k!)/((k!) * (k!)) by A6,NEWTON:def 3;
      Catalan (k+1)
        = (2*k!) / ((k!) * (k!) * (k+1)) by A3,A4,A5,A7,XCMPLX_1:79
       .= (2*k!) / ((k!) * ((k!) * (k+1)))
       .= (2*k!) / ((k!) * ((k+1)!)) by NEWTON:21;
      hence thesis;
    end;
  end;

theorem Th15:
  for n being natural number st n > 1 holds
    Catalan (n) < Catalan (n + 1)
  proof
    let n be natural number;
    assume
A1: n > 1; then
A2: n > 0; then
    reconsider k = n as non trivial Nat by A1,NAT_2:30,ORDINAL2:def 21;
A3: Catalan (n + 1) = (2*n)! / (n! * ((n+1)!)) by A1,Th14;
A4: n! > 0 by NEWTON:23;
A5: (n + 1)! > 0 by NEWTON:23;
A6: n! * ((n + 1)!) > 0 * (n!) by A4,A5,REAL_1:70;
    set a = (2 * n -' 2)!;
    set b = (2 * n)!;
    (2 * n -' 2)! * n * (n + 1) < (2 * n)! by A1,Th6; then
A7: (a * n * (n + 1)) / (n! * ((n + 1)!)) <
      b / (n! * ((n + 1)!)) by A6,REAL_1:73;
A8: n -' 1 + 1 = n by A1,AMI_5:4;
    (a * n * (n + 1)) / (n! * ((n + 1)!)) =
      (a * n * (n + 1)) / (n * ((n -' 1)!)* ((n + 1)!)) by A8,NEWTON:21
     .= (n * (a * (n + 1))) / (n * (((n -' 1)!)* ((n + 1)!)))
     .= (a * (n + 1)) /(((n -' 1)!) * ((n + 1)!)) by A2,XCMPLX_1:92
     .= (a * (n + 1)) / (((n -' 1)!) * ((n + 1) * (n!))) by NEWTON:21
     .= (a * (n + 1)) / (((n -' 1)!) * (n!) * (n + 1))
     .= a / ((n -' 1)! * (n!)) by XCMPLX_1:92;
    hence thesis by A1,A3,A7,Th8;
  end;

theorem Th16:
  for n being natural number holds
    Catalan (n) <= Catalan (n + 1)
  proof
    let n be natural number;
    per cases by CQC_THE1:2;
    suppose n = 0;
      hence thesis by Th10,Th11;
    end;
    suppose n = 1;
      hence thesis by Th11,Th12;
    end;
    suppose n > 1;
      hence thesis by Th15;
    end;
  end;

theorem Th17:
  for n being natural number holds
    Catalan (n) >= 0
  proof
    let n be natural number;
    defpred P[Nat] means Catalan $1 >= 0;
A1: P[0] by Th10;
A2: for n being Nat st P[n] holds P[n + 1]
    proof
      let n be Nat;
      assume
A3:   P[n];
      per cases;
      suppose n <= 1; then
        n = 0 or n = 1 by CQC_THE1:2;
        hence thesis by Th11,Th12;
      end;
      suppose n > 1; then
        Catalan (n+1) > Catalan n by Th15;
        hence thesis by A3;
      end;
    end;
A4: n is Nat by ORDINAL2:def 21;
    for n being Nat holds P[n] from NAT_1:sch 1(A1, A2);
    hence thesis by A4;
  end;

theorem Th18:
  for n being natural number holds
    Catalan (n) is Nat
  proof
    let n be natural number;
A1: Catalan (n) >= 0 by Th17;
    Catalan (n) is Integer by Th13;
    hence thesis by A1,INT_1:16;
  end;

theorem Th19:
  for n being natural number st n > 0 holds
    Catalan (n+1) = 2 * (2 - (3 / (n + 1))) * Catalan (n)
  proof
    let n be natural number;
    assume
A1: n > 0; then
A2: n >= 1 + 0 by NAT_1:38;
A3: n -' 1 + 1 = n by A2,AMI_5:4;
A4: 1 * 2 <= 2 * n by A2,AXIOMS:25; then
A5: 1 < 2 * n by AXIOMS:22; then
A6: 2 * n -' 1 = 2 * n - 1 by SCMFSA_7:3;
A7: 2 * n -' 2 + 1 = 2 * n -' 1 by A5,Th4;
A8: 2 * n -' 1 + 1 = 2 * n by A5,AMI_5:4;
A9: 2 * (n -' 1) = 2 * (n - 1) by A2,SCMFSA_7:3
                .= 2 * n - 2 * 1
                .= 2 * n -' 2 by A4,SCMFSA_7:3;
A10: n is Nat by ORDINAL2:def 21;
    per cases by A2,REAL_1:def 5;
    suppose n = 1;
      hence thesis by Th11,Th12;
    end;
    suppose n > 1; then
      1 + 1 <= n by NAT_1:38; then
A11:   n -' 1 >= 1 by A10,SPRECT_3:8;
A12:   Catalan n = Catalan (n -' 1 + 1) by A2,AMI_5:4
        .= (2 * (n -' 1))! / ((n -' 1)! * ((n -' 1 + 1)!)) by A11,Th14
        .= (2 * n -' 2)! / ((n -' 1)! * (n!)) by A2,A9,AMI_5:4;
A13:   2 * (2 - (3 / (n + 1))) = 2 * ((2 * (n + 1) / (n + 1) - (3 / (n + 1))))
          by XCMPLX_1:90
       .= 2 * ((2 * (n + 1) - 3) / (n + 1)) by XCMPLX_1:121
       .= (2 * (2 * n - 1)) / (n + 1) by XCMPLX_1:75
       .= ((2 * n -' 1) * 2 * n) / (n * (n + 1)) by A1,A6,XCMPLX_1:92
       .= ((2 * n -' 1) * (2 * n)) / (n * (n + 1));
      Catalan (n + 1) = (2*n)! / (n! * ((n+1)!)) by A2,Th14
        .= ((2*n -' 1)! * (2*n)) / (n! * ((n+1)!)) by A8,NEWTON:21
        .= ((2*n -' 2)! * (2 * n -' 1) * (2*n)) / (n! * ((n+1)!))
             by A7,NEWTON:21
        .= ((2*n -' 2)! * (2 * n -' 1) * (2*n)) /
             (n! * (n! * (n+1))) by NEWTON:21
        .= ((2*n -' 2)! * (2 * n -' 1) * (2*n)) /
             (n! * ((n -' 1)! * n * (n+1))) by A3,NEWTON:21
        .= ((2*n -' 2)! * ((2 * n -' 1) * (2*n))) /
             (n! * ((n -' 1)!) * (n * (n+1)))
        .= (Catalan n) *
           (((2 * n -' 1) * (2*n)) / (n * (n+1))) by A12,XCMPLX_1:77;
      hence thesis by A13;
    end;
  end;

registration let n be natural number;
  cluster Catalan n -> natural;
  coherence by Th18;
end;

theorem Th20:
  for n being natural number st n > 0 holds
    Catalan n > 0
  proof
    let n be natural number;
    assume
A1: n > 0;
    defpred P[Nat] means Catalan $1 > 0;
A2: P[1] by Th11;
A3: for n being non empty Nat st P[n] holds P[n+1] by Th16;
A4: for n being non empty Nat holds P[n] from BINARITH:sch 1(A2,A3);
    n is non empty Nat by A1,ORDINAL2:def 21;
    hence thesis by A4;
  end;

registration let n be non empty natural number;
  cluster Catalan n -> non empty;
  coherence
  proof
    n > 0 by NAT_1:19;
    hence thesis by Th20;
  end;
end;

theorem
  for n being natural number st n > 0 holds
    Catalan (n+1) < 4 * Catalan (n)
  proof
    let n be natural number;
    assume
A1: n > 0; then
A2: Catalan n > 0 by Th20;
A3: Catalan (n + 1) = 2 * (2 - (3 / (n + 1))) * Catalan (n) by A1,Th19;
    2 * (2 - (3 / (n + 1))) < 4 by Th7;
    hence thesis by A2,A3,REAL_1:70;
  end;

