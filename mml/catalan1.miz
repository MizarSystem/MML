:: Catalan Numbers
::  by Dorota Cz\c{e}stochowska and Adam Grabowski
::
:: Received May 31, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies ORDINAL2, ARYTM, ARYTM_3, ARYTM_1, QC_LANG1, NEWTON, REALSET1,
      CATALAN1, INT_1;
 notations XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XXREAL_0, REAL_1,
      NAT_1, NEWTON, INT_1, NAT_D, REALSET1;
 constructors XXREAL_0, REAL_1, NAT_1, NEWTON, REALSET1, NAT_D, VALUED_1;
 registrations XXREAL_0, XREAL_0, NAT_1, INT_1, MEMBERED, NAT_2, XBOOLE_0;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 theorems NAT_1, XCMPLX_1, NAT_2, NEWTON, INT_1, XREAL_1, XXREAL_0, NAT_D;
 schemes NAT_2, NAT_1;

begin :: Preliminaries

theorem Th1:
  for n being natural number st n > 1 holds n -' 1 <= 2 * n -' 3
proof
  let n be natural number;
  assume
A1: n > 1;
  then
A2: n -' 1 = n - 1 by XREAL_1:235;
  2 * 1 < 2 * n by A1,XREAL_1:70;
  then 2 + 1 <= 2 * n by NAT_1:13;
  then
A3: 2 * n -' 3 = 2 * n - 3 by XREAL_1:235;
  n -' 1 > 1 -' 1 by A1,NAT_D:57;
  then n -' 1 + n > 0 + n by XREAL_1:8;
  then 2 * n - 1 - 1 > n - 1 by A2,XREAL_1:11;
  then 2 * n - 2 - 1 >= n - 1 by INT_1:79;
  hence thesis by A1,A3,XREAL_1:235;
end;

theorem Th2:
  for n being natural number st n >= 1 holds n -' 1 <= 2 * n -' 2
proof
  let n be natural number;
  assume
A1: n >= 1;
A2: 1 * (n -' 1) <= 2 * (n -' 1) by XREAL_1:66;
  2 * 1 <= 2 * n by A1,XREAL_1:66;
  then
A3: 2 * n -' 2 = 2 * n - 2 by XREAL_1:235;
  n -' 1 = n - 1 by A1,XREAL_1:235;
  hence thesis by A2,A3;
end;

theorem Th3:
  for n being natural number st n > 1 holds n < 2 * n -' 1
proof
  let n be natural number;
  assume
A1: n > 1;
  then
A2: 1 * 2 < 2 * n by XREAL_1:70;
  n + n > n + 1 by A1,XREAL_1:8;
  then n + n - 1 > n + 1 - 1 by XREAL_1:11;
  hence thesis by A2,XREAL_1:235,XXREAL_0:2;
end;

theorem Th4:
  for n being natural number st n > 1 holds n -' 2 + 1 = n -' 1
proof
  let n be natural number;
  assume n > 1;
  then
A1: n -' 1 >= 1 by NAT_D:49;
  n -' 2 + 1 = n -' 1 -' 1 + 1 by NAT_D:45
    .= n -' 1 by A1,XREAL_1:237;
  hence thesis;
end;

theorem
  for n being natural number st n > 1 holds (4 * n * n - 2*n) / (n + 1) > 1
proof
  let n be natural number;
  assume
A1: n > 1;
A2: n is non trivial by A1,NAT_2:30;
  defpred P[Nat] means (4 * $1 * $1 - 2*$1)/($1 + 1) > 1;
A3: P[2];
A4: for k be non trivial Nat st P[k] holds P[k + 1]
  proof
    let k be non trivial Nat such that
A5: P[k];
    set k1 = k+1;
    (4 * k * k - 2*k) / (k + 1) = (4 * k * k - 2*k) * ( 1/ (k + 1))
    by XCMPLX_1:100;
    then (4 * k * k - 2*k) * (1 / (k + 1)) * (k + 1) > 1 * (k + 1)
    by A5,XREAL_1:70;
    then (4 * k * k - 2*k) > 1 * (k + 1) by XCMPLX_1:110;
    then (4 * k * k - 2*k)-(k + 1) > 0 by XREAL_1:52;
    then (4 * k * k - 3*k - 1) + (8 * k + 1) > 0 + 0;
    then (4 * k1 * k1 - 2*k1 - (k1 + 1)) + (k1 + 1) > 0 + (k1 + 1)
    by XREAL_1:10;
    then (4 * k1 * k1 - 2 * k1) / (k1 + 1) > (k1 + 1) / (k1 + 1)
     by XREAL_1:76;
    hence thesis by XCMPLX_1:60;
  end;
  for k be non trivial Nat holds P[k] from NAT_2:sch 2 (A3, A4);
  hence thesis by A2;
end;

theorem Th6:
  for n being natural number st n > 1 holds
  (2 * n -' 2)! * n * (n + 1) < (2 * n)!
proof
  let n be natural number;
  assume
A1: n > 1;
  then
A2: 2 * 1 < 2 * n by XREAL_1:70;
  then 2 - 1 < 2 * n - 1 by XREAL_1:11;
  then
A3: 1 < 2 * n -' 1 by A2,XREAL_1:235,XXREAL_0:2;
A4: 2 * n -' 2 + 1 = 2 * n -' 1 -' 1 + 1 by NAT_D:45
    .= 2 * n -' 1 by A3,XREAL_1:237;
A5: 2 * n -' 1 + 1 = 2 * n by A2,XREAL_1:237,XXREAL_0:2;
A6: (2 * n -' 2)! * (2 * n -' 1) * (2 * n) =
  (2 * n -' 1)! * (2 * n) by A4,NEWTON:21
    .= (2 * n)! by A5,NEWTON:21;
A7: n + 1 < n + n by A1,XREAL_1:8;
A8: (2 * n -' 2)! > 0 by NEWTON:23;
  then
A9: (2 * n -' 2)! * n > 0 * n by A1,XREAL_1:70;
  (2 * n -' 2)! * n < (2 * n -' 2)! * (2 * n -' 1) by A1,A8,Th3,XREAL_1:70;
  hence thesis by A6,A7,A9,XREAL_1:100;
end;

theorem Th7:
  for n being natural number holds 2 * (2 - (3 / (n + 1))) < 4
proof
  let n be natural number;
  assume 2 * (2 - (3 / (n + 1))) >= 4;
  then 2 * (2 - (3 / (n + 1))) / 2 >= 4 / 2 by XREAL_1:74;
  then 2 - (3 / (n + 1)) - 2 >= 2 - 2 by XREAL_1:11;
  then --- (3 / (n + 1)) >= 0;
  then 3 / (n + 1) <= 0;
  hence thesis by XREAL_1:141;
end;

begin :: Catalan Numbers

definition
  let n be natural number;
  func Catalan (n) -> Real equals

  ((2*n -' 2) choose (n -' 1)) / n;
  coherence;
end;

theorem Th8:
  for n being natural number st n > 1 holds
  Catalan (n) = (2 * n -' 2)! / ((n -' 1)! * (n!))
proof
  let n be natural number;
  assume
A1: n > 1;
  then
A2: n -' 1 <= 2 * n -' 2 by Th2;
A3: 2 * 1 <= 2 * n by A1,XREAL_1:66;
A4: n -' 1 + 1 = n by A1,XREAL_1:237;
  (2*n -' 2) - (n -' 1) = (2*n -' 2) - (n - 1) by A1,XREAL_1:235
    .= (2*n - 2) - (n - 1) by A3,XREAL_1:235
    .= n -' 1 by A1,XREAL_1:235;
  then ((2*n -' 2) choose (n -' 1)) =
  ((2*n -' 2)!) / ((n -' 1)! * ((n -' 1)!)) by A2,NEWTON:def 3;
  then Catalan (n) =
  ((2*n -' 2)!) / ((n -' 1)! * ((n -' 1)!) * n) by XCMPLX_1:79
    .= ((2*n -' 2)!) / ((n -' 1)! * (((n -' 1)!) * n))
    .= ((2*n -' 2)!) / ((n -' 1)! * (n!)) by A4,NEWTON:21;
  hence thesis;
end;

theorem Th9:
  for n being natural number st n > 1 holds Catalan n =
  4 * ((2*n -' 3) choose (n -' 1)) - ((2*n -' 1) choose (n -' 1))
proof
  let n be natural number;
  assume
A1: n > 1;
  then
A2: n -' 1 <= 2 * n -' 3 by Th1;
A3: 1 + 1 <= n by A1,NAT_1:13;
  then
A4: 2 * 2 <= 2 * n by XREAL_1:66;
  (2*n -' 3) - (n -' 1) = (2*n -' 3) - (n - 1) by A1,XREAL_1:235
    .= (2*n - 3) - (n - 1) by A4,XREAL_1:235,XXREAL_0:2
    .= n - 2
    .= n -' 2 by A3,XREAL_1:235;
  then ((2*n -' 3) choose (n -' 1)) =
  ((2*n -' 3)!) / ((n -' 1)! * ((n -' 2)!)) by A2,NEWTON:def 3;
  then
A5: 4 * ((2*n -' 3) choose (n -' 1)) =
  (4 * ((2*n -' 3)!)) / ((n -' 1)! * ((n -' 2)!)) by XCMPLX_1:75;
A6: 1 * n < 2 * n by A1,XREAL_1:70;
  then 1 < 2 * n by A1,XXREAL_0:2;
  then
A7: n -' 1 < 2 * n -' 1 by A6,NAT_D:57;
  2 * n -' 1 - (n -' 1) = 2 * n -' 1 - (n - 1) by A1,XREAL_1:235
    .= 2 * n -' 1 - n + 1
    .= 2 * n - 1 - n + 1 by A1,A6,XREAL_1:235,XXREAL_0:2
    .= n;
  then
A8: (2*n -' 1) choose (n -' 1) =
  ((2*n -' 1)!) / ((n -' 1)! * (n!)) by A7,NEWTON:def 3;
A9: n - 1 > 0 by A1,XREAL_1:52;
A10: n -' 1 = n - 1 by A1,XREAL_1:235;
A11: 2 * 1 <= 2 * n by A1,XREAL_1:66;
  then
A12: 2 * n -' 2 = 2 * n - 2 by XREAL_1:235;
A13: n -' 1 + 1 = n by A10;
A14: 1 < 2 * n by A1,A6,XXREAL_0:2;
A15: 2 * n -' 1 = 2 * n - 1 by A1,A6,XREAL_1:235,XXREAL_0:2;
A16: n -' 2 + 1 = n -' 1 by A1,Th4;
  2 * n -' 3 = 2 * n - 3 by A4,XREAL_1:235,XXREAL_0:2;
  then
A17: 2 * n -' 3 + 1 = 2 * n - 2
    .= 2 * n -' 2 by A11,XREAL_1:235;
A18: 2 * n -' 2 + 1 = 2 * n -' 1 by A14,Th4;
A19: 4 * ((2*n -' 3) choose (n -' 1)) =
  ((n * (n - 1)) * (4 * ((2*n -' 3)!))) /
  (((n * (n - 1)) * ((n -' 1)! * ((n -' 2)!)))) by A1,A5,A9,XCMPLX_1:6,92
    .= (((n - 1) * n) * (4 * ((2*n -' 3)!))) /
  (((n - 1) * (n * ((n -' 1)!)) * ((n -' 2)!)))
    .= (((n - 1) * n) * (4 * ((2*n -' 3)!))) /
  (((n - 1) * (n!) * ((n -' 2)!))) by A13,NEWTON:21
    .= (((n - 1) * n) * (4 * ((2*n -' 3)!))) /
  (((n!) * ((n -' 1) * ((n -' 2)!)))) by A10
    .= (2 * n * ((2 * n -' 2) * ((2*n -' 3)!))) /
  (((n!) * ((n -' 1)!))) by A12,A16,NEWTON:21
    .= (2 * n * ((2*n -' 2)!)) /
  (((n!) * ((n -' 1)!))) by A17,NEWTON:21;
  (2*n -' 1) choose (n -' 1) =
  ((2*n -' 2)! * (2*n -' 1)) / ((n -' 1)! * (n!)) by A8,A18,NEWTON:21;
  then 4 * ((2*n -' 3) choose (n -' 1)) - ((2*n -' 1) choose (n -' 1)) =
  ((2 * n * ((2*n -' 2)!)) - ((2*n -' 2)! * (2*n -' 1)))
  / (((n!) * ((n -' 1)!))) by A19,XCMPLX_1:121
    .= Catalan n by A1,A15,Th8;
  hence thesis;
end;

theorem
  Catalan 0 = 0;

theorem Th11:
  Catalan 1 = 1
proof
  Catalan 1 = (2*1 -' 2) choose 0 by XREAL_1:234
    .= 1 by NEWTON:27,XREAL_1:234;
  hence thesis;
end;

theorem Th12:
  Catalan 2 = 1
proof
A1: 2 -' 1 = 2 - 1 by XREAL_1:235
    .= 1;
  4 -' 2 = 4 - 2 by XREAL_1:235
    .= 2;
  then Catalan 2 = 2 / 2 by A1,NEWTON:33
    .= 1;
  hence thesis;
end;

theorem Th13:
  for n being natural number holds Catalan (n) is Integer
proof
  let n be natural number;
  per cases by NAT_1:26;
  suppose n = 0;
    hence thesis;
  end;
  suppose n = 1;
    hence thesis;
  end;
  suppose n > 1;
    then Catalan n =
    4 * ((2*n -' 3) choose (n -' 1)) - ((2*n -' 1) choose (n -' 1)) by Th9;
    hence thesis;
  end;
end;

theorem Th14:
  for k being natural number holds Catalan (k + 1) = (2*k)! / (k! * ((k+1)!))
proof
  let k be natural number;
A1: 2*k + 2 -' 2 = 2*k by NAT_D:34;
A2: k + 1 -' 1 = k by NAT_D:34;
  reconsider l = 2*k - k as Nat;
A3: l = k;
  1*k <= 2*k by XREAL_1:66;
  then (2*k) choose k = (2*k!)/((k!) * (k!)) by A3,NEWTON:def 3;
  then Catalan (k+1) = (2*k!) / ((k!) * (k!) * (k+1)) by A1,A2,XCMPLX_1:79
    .= (2*k!) / ((k!) * ((k!) * (k+1)))
    .= (2*k!) / ((k!) * ((k+1)!)) by NEWTON:21;
  hence thesis;
end;

theorem Th15:
  for n being natural number st n > 1 holds Catalan (n) < Catalan (n + 1)
proof
  let n be natural number;
  assume
A1: n > 1;
A2: Catalan (n + 1) = (2*n)! / (n! * ((n+1)!)) by Th14;
A3: n! > 0 by NEWTON:23;
  (n + 1)! > 0 by NEWTON:23;
  then
A4: n! * ((n + 1)!) > 0 * (n!) by A3,XREAL_1:70;
  set a = (2 * n -' 2)!;
  set b = (2 * n)!;
  (2 * n -' 2)! * n * (n + 1) < (2 * n)! by A1,Th6;
  then
A5: (a * n * (n + 1)) / (n! * ((n + 1)!)) <
  b / (n! * ((n + 1)!)) by A4,XREAL_1:76;
  n -' 1 + 1 = n by A1,XREAL_1:237;
  then (a * n * (n + 1)) / (n! * ((n + 1)!)) =
  (a * n * (n + 1)) / (n * ((n -' 1)!)* ((n + 1)!)) by NEWTON:21
    .= (n * (a * (n + 1))) / (n * (((n -' 1)!)* ((n + 1)!)))
    .= (a * (n + 1)) /(((n -' 1)!) * ((n + 1)!)) by A1,XCMPLX_1:92
    .= (a * (n + 1)) / (((n -' 1)!) * ((n + 1) * (n!))) by NEWTON:21
    .= (a * (n + 1)) / (((n -' 1)!) * (n!) * (n + 1))
    .= a / ((n -' 1)! * (n!)) by XCMPLX_1:92;
  hence thesis by A1,A2,A5,Th8;
end;

theorem Th16:
  for n being natural number holds Catalan (n) <= Catalan (n + 1)
proof
  let n be natural number;
  per cases by NAT_1:26;
  suppose n = 0;
    hence thesis;
  end;
  suppose n = 1;
    hence thesis by Th11,Th12;
  end;
  suppose n > 1;
    hence thesis by Th15;
  end;
end;

theorem
  for n being natural number holds Catalan (n) >= 0;

theorem Th18:
  for n being natural number holds Catalan (n) is Element of NAT
proof
  let n be natural number;
  Catalan (n) is Integer by Th13;
  hence thesis by INT_1:16;
end;

theorem Th19:
  for n being natural number st n > 0 holds
  Catalan (n+1) = 2 * (2 - (3 / (n + 1))) * Catalan (n)
proof
  let n be natural number;
  assume
A1: n > 0;
  then
A2: n >= 1 + 0 by NAT_1:13;
  then
A3: n -' 1 + 1 = n by XREAL_1:237;
A4: 1 * 2 <= 2 * n by A2,XREAL_1:66;
  then
A5: 1 < 2 * n by XXREAL_0:2;
A6: 2 * n -' 1 = 2 * n - 1 by A4,XREAL_1:235,XXREAL_0:2;
A7: 2 * n -' 2 + 1 = 2 * n -' 1 by A5,Th4;
A8: 2 * n -' 1 + 1 = 2 * n by A4,XREAL_1:237,XXREAL_0:2;
A9: 2 * (n -' 1) = 2 * (n - 1) by A2,XREAL_1:235
    .= 2 * n - 2 * 1
    .= 2 * n -' 2 by A2,XREAL_1:66,235;
A10: Catalan n = Catalan (n -' 1 + 1) by A2,XREAL_1:237
    .= (2 * (n -' 1))! / ((n -' 1)! * ((n -' 1 + 1)!)) by Th14
    .= (2 * n -' 2)! / ((n -' 1)! * (n!)) by A2,A9,XREAL_1:237;
A11: 2 * (2 - (3 / (n + 1))) = 2 * ((2 * (n + 1) / (n + 1) - (3 / (n + 1))))
  by XCMPLX_1:90
    .= 2 * ((2 * (n + 1) - 3) / (n + 1)) by XCMPLX_1:121
    .= (2 * (2 * n - 1)) / (n + 1) by XCMPLX_1:75
    .= ((2 * n -' 1) * 2 * n) / (n * (n + 1)) by A1,A6,XCMPLX_1:92
    .= ((2 * n -' 1) * (2 * n)) / (n * (n + 1));
  Catalan (n + 1) = (2*n)! / (n! * ((n+1)!)) by Th14
    .= ((2*n -' 1)! * (2*n)) / (n! * ((n+1)!)) by A8,NEWTON:21
    .= ((2*n -' 2)! * (2 * n -' 1) * (2*n)) / (n! * ((n+1)!)) by A7,NEWTON:21
    .= ((2*n -' 2)! * (2 * n -' 1) * (2*n)) / (n! * (n! * (n+1))) by NEWTON:21
    .= ((2*n -' 2)! * (2 * n -' 1) * (2*n)) /
  (n! * ((n -' 1)! * n * (n+1))) by A3,NEWTON:21
    .= ((2*n -' 2)! * ((2 * n -' 1) * (2*n))) /
  (n! * ((n -' 1)!) * (n * (n+1)))
    .= (Catalan n) *
  (((2 * n -' 1) * (2*n)) / (n * (n+1))) by A10,XCMPLX_1:77;
  hence thesis by A11;
end;

registration
  let n be natural number;
  cluster Catalan n -> natural;
  coherence by Th18;
end;

theorem Th20:
  for n being natural number st n > 0 holds Catalan n > 0
proof
  let n be natural number;
  assume
A1: n > 0;
  defpred P[Nat] means Catalan $1 > 0;
A2: P[1] by Th11;
A3: for n being non empty Nat st P[n] holds P[n+1] by Th16;
A4: for n being non empty Nat holds P[n] from NAT_1:sch 10(A2,A3);
  n is non empty Nat by A1;
  hence thesis by A4;
end;

registration
  let n be non empty natural number;
  cluster Catalan n -> non empty;
  coherence by Th20;
end;

theorem
  for n being natural number st n > 0 holds Catalan (n+1) < 4 * Catalan (n)
proof
  let n be natural number;
  assume
A1: n > 0;
  then
A2: Catalan n > 0;
  Catalan (n + 1) = 2 * (2 - (3 / (n + 1))) * Catalan (n) by A1,Th19;
  hence thesis by A2,Th7,XREAL_1:70;
end;

