:: Properties of the Intervals of Real Numbers
::  by J\'ozef Bia{\l}as
::
:: Received January 12, 1993
:: Copyright (c) 1993 Association of Mizar Users

environ

 vocabularies SUPINF_1, RLVECT_1, ARYTM_1, ORDINAL2, RCOMP_1, BOOLE, MEASURE5,
      ARYTM, SQUARE_1, MEMBERED, RELAT_2, XXREAL_2, TBSP_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, DOMAIN_1, NUMBERS,
      MEMBERED, XXREAL_0,
      XREAL_0, REAL_1, XXREAL_1, RCOMP_1, RCOMP_2, XXREAL_2, SUPINF_1,
      SUPINF_2;
 constructors DOMAIN_1, REAL_1, RCOMP_1, SUPINF_2, SUPINF_1, FINSET_1, RCOMP_2;
 registrations XBOOLE_0, NUMBERS, XXREAL_0, XREAL_0, MEMBERED, XXREAL_1,
      XXREAL_2, SUBSET_1, FINSET_1;
 requirements SUBSET, BOOLE, NUMERALS, ARITHM, REAL;
 definitions TARSKI, XXREAL_1, MEMBERED, XBOOLE_0, SUPINF_2;
 theorems TARSKI, SUPINF_1, SUPINF_2, MEASURE3, MEASURE4, XREAL_0, XBOOLE_0,
      XBOOLE_1, XREAL_1, XXREAL_0, NUMBERS, MEMBERED, XXREAL_1, XXREAL_2;
 schemes SUPINF_1;

begin  :: Some theorems about R_eal numbers

reserve x,y,a,b,a1,b1,a2,b2 for R_eal;

canceled 7;

theorem
  for a,b,c being R_eal holds (b <> -infty & b <> +infty &
  not (a = -infty & c = -infty) & not (a = +infty & c = +infty)) implies
  (c - b) + (b - a) = c - a
proof
  let a,b,c be R_eal;
  assume
A1: b <> -infty & b <> +infty &
  not (a = -infty & c = -infty) & not (a = +infty & c = +infty);
A2: (a in REAL or a in {-infty,+infty}) & (b in REAL or b in {-infty,+infty
  }) by XBOOLE_0:def 3,XXREAL_0:def 4;
  per cases by A1,A2,TARSKI:def 2;
  suppose
A3: a in REAL & b in REAL;
A4: c in REAL or c in {-infty,+infty} by XBOOLE_0:def 3,XXREAL_0:def 4;
    now per cases by A3,A4,TARSKI:def 2;
      suppose a in REAL & b in REAL & c in REAL;
        then consider x,y,z being Real such that
A5:     x = a & y = b & z = c;
        c - b = z - y & b - a = y - x by A5,SUPINF_2:5;
        hence (c -b) + (b - a) = (z - y) + (y - x) by SUPINF_2:1
          .= z - (0 + x)
          .= c - a by A5,SUPINF_2:5;
      end;
      suppose
A6:     a in REAL & b in REAL & c = -infty;
        then
A7:     (c -b) + (b - a) = -infty + (b - a) by SUPINF_2:7;
        consider x,y being Real such that
A8:     x = a & y = b by A6;
        b - a = y - x by A8,SUPINF_2:5;
        then (c -b) + (b - a) = -infty by A7,SUPINF_2:def 2;
        hence thesis by A6,SUPINF_2:7;
      end;
      suppose
A9:     a in REAL & b in REAL & c = +infty;
        then
A10:    (c -b) + (b - a) = +infty + (b - a) by SUPINF_2:6;
        consider x,y being Real such that
A11:    x = a & y = b by A9;
        b - a = y - x by A11,SUPINF_2:5;
        then (c -b) + (b - a) = +infty by A10,SUPINF_2:def 2;
        hence thesis by A9,SUPINF_2:6;
      end;
    end;
    hence thesis;
  end;
  suppose
A12: a = -infty & b in REAL;
A13: c in REAL or c in {-infty,+infty} by XBOOLE_0:def 3,XXREAL_0:def 4;
    now per cases by A1,A12,A13,TARSKI:def 2;
      suppose
A14:    a = -infty & b in REAL & c in REAL;
        then
A15:    (c -b) + (b - a) = (c - b) + (+infty) by SUPINF_2:7;
        consider x,y being Real such that
A16:    x = c & y = b by A14;
        c - b = x - y by A16,SUPINF_2:5;
        then (c -b) + (b - a) = +infty by A15,SUPINF_2:def 2;
        hence thesis by A14,SUPINF_2:7;
      end;
      suppose
A17:    a = -infty & b in REAL & c = +infty;
        then
A18:    (c -b) + (b - a) = (c - b) + (+infty) by SUPINF_2:7;
        c - b = +infty by A17,SUPINF_2:6;
        then (c -b) + (b - a) = +infty by A18,SUPINF_2:def 2;
        hence thesis by A17,SUPINF_2:7;
      end;
    end;
    hence thesis;
  end;
  suppose
A19: a = +infty & b in REAL;
A20: c in REAL or c in {-infty,+infty} by XBOOLE_0:def 3,XXREAL_0:def 4;
    now per cases by A1,A19,A20,TARSKI:def 2;
      suppose
A21:    a = +infty & b in REAL & c in REAL;
        then
A22:    (c -b) + (b - a) = (c - b) + (-infty) by SUPINF_2:6;
        consider x,y being Real such that
A23:    x = c & y = b by A21;
        c - b = x - y by A23,SUPINF_2:5;
        then (c -b) + (b - a) = -infty by A22,SUPINF_2:def 2;
        hence thesis by A21,SUPINF_2:6;
      end;
      suppose
A24:    a = +infty & b in REAL & c = -infty;
        then
A25:    (c -b) + (b - a) = (c - b) + (-infty) by SUPINF_2:6;
        c - b = -infty by A24,SUPINF_2:7;
        then (c -b) + (b - a) = -infty by A25,SUPINF_2:def 2;
        hence thesis by A24,SUPINF_2:6;
      end;
    end;
    hence thesis;
  end;
end;

:: PROPERTIES OF THE INTERVALS

scheme RSetEq {P[set]} : for X1,X2 being Subset of REAL st
  (for x being R_eal holds x in X1 iff P[x]) &
  (for x being R_eal holds x in X2 iff P[x]) holds X1 = X2
proof
  let A1,A2 be Subset of REAL such that
A1: for x being R_eal holds x in A1 iff P[x] and
A2: for x being R_eal holds x in A2 iff P[x];
  thus A1 c= A2
  proof
    let x be real number;
    assume
A3: x in A1;
    then x in REAL;
    then reconsider x as R_eal by NUMBERS:31;
    P[x] by A1,A3;
    hence thesis by A2;
  end;
  let x be real number;
  assume
A4: x in A2;
  then x in REAL;
  then reconsider x as R_eal by NUMBERS:31;
  P[x] by A2,A4;
  hence thesis by A1;
end;

definition
 canceled;
  let a,b be R_eal;
  redefine func ].a,b.[ -> Subset of REAL means
 for x being R_eal holds x in it iff a < x & x < b;
  coherence
  proof
    for x being set st x in ]. a,b .[ holds x in REAL;
    hence thesis;
  end;
  compatibility
  proof
    let X be Subset of REAL;
    thus X = ].a,b.[ implies for x being R_eal holds
    x in X iff a < x & x < b by XXREAL_1:4;
    assume
A1: for x being R_eal holds x in X iff a < x & x < b;
    thus X c= ].a,b.[
    proof
      let x be real number;
      assume
A2:   x in X;
      then x in REAL;
      then reconsider t=x as R_eal by NUMBERS:31;
      a < t & t < b & x in REAL by A1,A2;
      hence x in ].a,b.[;
    end;
    let x be real number;
    assume
A3: x in ].a,b.[;
    reconsider t=x as R_eal by XXREAL_0:def 1;
    a < t & t < b by A3,XXREAL_1:4;
    hence x in X by A1;
  end;
end;

definition
 canceled 2;
  let IT be Subset of REAL;
  attr IT is open_interval means
  :Def5:
  ex a,b being R_eal st a <= b & IT = ].a,b.[;
  attr IT is closed_interval means
:Def6:  ex a,b being real number st a <= b & IT = [.a,b.];
end;

registration
  cluster open_interval Subset of REAL;
  existence
  proof
    consider a,b being R_eal such that
A1: a = -infty & b = +infty;
    take ].a,b.[, a,b;
    thus thesis by A1;
  end;
  cluster closed_interval Subset of REAL;
  existence
  proof
    consider a,b being R_eal;
    take [.0,1.],0,1;
    thus thesis;
  end;
end;

definition
  let IT be Subset of REAL;
  attr IT is right_open_interval means
:Def7: ex a being real number, b being R_eal st a <= b & IT = [.a,b.[;
end;

notation
  let IT be Subset of REAL;
  synonym IT is left_closed_interval for IT is right_open_interval;
end;

definition
  let IT be Subset of REAL;
  attr IT is left_open_interval means
:Def8:  ex a being R_eal,b being real number st a <= b & IT = ].a,b.];
end;

notation
  let IT be Subset of REAL;
  synonym IT is right_closed_interval for IT is left_open_interval;
end;

registration
  cluster right_open_interval Subset of REAL;
  existence
  proof
    consider a,b being R_eal;
    take [.0,+infty.[,0,+infty;
    thus thesis;
  end;
  cluster left_open_interval Subset of REAL;
  existence
  proof
    consider a,b being R_eal such that
A2: a = -infty & b = +infty;
    take ].a,1.],a,1;
    thus thesis by A2;
  end;
end;

notation
  let IT be Subset of REAL;
  synonym IT is interval for IT is connected;
end;

definition
  let IT be Subset of REAL;
  redefine attr IT is interval means
  :Def9:
  IT is open_interval or IT is closed_interval or
  IT is right_open_interval or IT is left_open_interval;
 compatibility
  proof
   thus IT is interval implies
  IT is open_interval or IT is closed_interval or
  IT is right_open_interval or IT is left_open_interval
  proof assume
Z: IT is interval;
   per cases;
   suppose
S:   IT is left_end right_end;
    reconsider a = inf IT,b = sup IT as R_eal;
W:     a in IT & b in IT & IT = [.a,b.] by S,Z,XXREAL_2:79,def 5,def 6;
X:  a <= b by W,XXREAL_1:1;
    IT is closed_interval by W,Def6,X;
    hence thesis;
   end;
   suppose
S:  IT is non left_end right_end;
    then
X:  IT = ].inf IT, sup IT.] by Z,XXREAL_2:80;
    set a = inf IT, b = sup IT;
Y:  b in IT by S,XXREAL_2:def 6;
    then
W:    a <= b & IT = ].a,b.] by X,XXREAL_2:40;
    not a in IT by S,XXREAL_2:def 5;
    then
WW:  a < b by W,Y,XXREAL_0:1;
    reconsider a,b as R_eal;
    b in IT by WW,W;
    then b is real number;
    then IT is left_open_interval by W,Def8;
    hence thesis;
   end;
   suppose
S:   IT is left_end non right_end;
    then
X:  IT = [.inf IT, sup IT.[ by Z,XXREAL_2:81;
    set a = inf IT, b = sup IT;
Y:  a in IT by S,XXREAL_2:def 5;
    then
W:   a <= b & IT = [.a,b.[ by X,XXREAL_2:40;
    not b in IT by S,XXREAL_2:def 6;
    then
WW:  a < b by W,Y,XXREAL_0:1;
    reconsider a,b as R_eal;
    a in IT by WW,W;
    then a is real number;
    then IT is right_open_interval by W,Def7;
    hence thesis;
   end;
   suppose IT is non left_end non right_end;
    then consider a,b being ext-real number such that
W:    a <= b & IT = ].a,b.[ by Z,XXREAL_2:83;
    reconsider a,b as R_eal by XXREAL_0:def 1;
    a <= b by W;
    then IT is open_interval by W,Def5;
    hence thesis;
   end;
  end;
  assume
Z:  IT is open_interval or IT is closed_interval or
  IT is right_open_interval or IT is left_open_interval;
  per cases by Z;
  suppose IT is open_interval;
   then ex a,b being R_eal st a <= b & IT = ].a,b.[ by Def5;
  hence IT is connected;
  end;
  suppose IT is closed_interval;
    then consider a,b being real number such that
W:   a <= b & IT = [.a,b.] by Def6;
  thus IT is connected by W;
  end;
  suppose IT is left_open_interval;
   then consider a being R_eal,b being real number such that
W:  a <= b & IT = ].a,b.] by Def8;
  thus IT is connected by W;
  end;
  suppose IT is right_open_interval;
   then consider a being real number, b being R_eal such that
W:   a <= b & IT = [.a,b.[ by Def7;
  thus IT is connected by W;
  end;
  end;
end;

registration
  cluster interval Subset of REAL;
  existence
  proof
    consider I being open_interval Subset of REAL;
    take I;
    thus I is open_interval or I is closed_interval
    or I is right_open_interval or I is left_open_interval;
  end;
end;

definition
  mode Interval is interval Subset of REAL;
end;

reserve A,B for Interval;

registration
  cluster open_interval -> interval Subset of REAL;
  coherence by Def9;
  cluster closed_interval -> interval Subset of REAL;
  coherence by Def9;
  cluster right_open_interval -> interval Subset of REAL;
  coherence by Def9;
  cluster left_open_interval -> interval Subset of REAL;
  coherence by Def9;
end;

canceled 8;

theorem Th17:
  for a,b being R_eal st a < b ex x being R_eal st a < x & x < b & x in REAL
proof
  let a,b be R_eal;
  assume
A1: a < b;
A2: (a in REAL or a in {-infty,+infty}) & (b in REAL or b in {-infty,+infty})
  by XBOOLE_0:def 3,XXREAL_0:def 4;
A3: not a = +infty & not b = -infty by A1,XXREAL_0:4,6;
  per cases by A2,A3,TARSKI:def 2;
  suppose a in REAL & b in REAL;
    then consider x,y being Real such that
A4: x = a & y = b & x<=y by A1;
    consider z being real number such that
A5: x<z & z<y by A1,A4,XREAL_1:7;
    reconsider z as Real by XREAL_0:def 1;
    reconsider o = z as R_eal by XXREAL_0:def 1;
    take o;
    thus thesis by A4,A5;
  end;
  suppose
A6: a in REAL & b = +infty;
    then reconsider x = a as Real;
    consider z being real number such that
A7: x<z by XREAL_1:3;
    reconsider z as Real by XREAL_0:def 1;
    reconsider o = z as R_eal by XXREAL_0:def 1;
    take o;
    thus thesis by A6,A7,XXREAL_0:9;
  end;
  suppose
A8: a = -infty & b in REAL;
    then reconsider y = b as Real;
    consider z being real number such that
A9: z<y by XREAL_1:4;
    reconsider z as Real by XREAL_0:def 1;
    reconsider o = z as R_eal by XXREAL_0:def 1;
    take o;
    thus thesis by A8,A9,XXREAL_0:12;
  end;
  suppose
A10: a = -infty & b = +infty;
    take 0.;
    0. = 0;
    hence thesis by A10;
  end;
end;

theorem
  for a,b,c being R_eal st a < b & a < c
  ex x being R_eal st a < x & x < b & x < c & x in REAL
proof
  let a,b,c be R_eal;
  assume
A1: a < b & a < c;
X: min(b,c) = min{b,c} by XXREAL_2:14;
  reconsider m = min(b,c) as R_eal by XXREAL_0:def 1;
  a < min(b,c) by A1,XXREAL_0:def 9;
  then consider x being R_eal such that
A2: a < x & x < m & x in REAL by Th17;
  ex o being R_eal st o in {b,c} & o <= b
  proof
    take b;
    thus thesis by TARSKI:def 2;
  end;
  then
A3: min(b,c) <= b by X,XXREAL_2:62;
  ex o being R_eal st (o in {b,c} & o <= c)
  proof
    take c;
    thus thesis by TARSKI:def 2;
  end;
  then
A4: min(b,c) <= c by X,XXREAL_2:62;
  take x;
  thus thesis by A2,A3,A4,XXREAL_0:2;
end;

theorem
  for a,b,c being R_eal st a < c & b < c
  ex x being R_eal st a < x & b < x & x < c & x in REAL
proof
  let a,b,c be R_eal;
  assume
A1: a < c & b < c;
X: max(a,b) = max{a,b} by XXREAL_2:12;
  reconsider m = max(a,b) as R_eal by XXREAL_0:def 1;
  max(a,b) < c by A1,XXREAL_0:def 10;
  then consider x being R_eal such that
A2: m < x & x < c & x in REAL by Th17;
  take x;
  a in {a,b} & b in {a,b} by TARSKI:def 2;
  hence thesis by A2,X,XXREAL_2:61;
end;

definition
  let A be ext-real-membered set;
  func diameter A -> R_eal equals
:Def10: sup A - inf A if A <> {}
    otherwise 0.;
  coherence;
  consistency;
end;

canceled 34;

theorem
  for a,b being R_eal holds
  ((a < b implies diameter ].a,b.[ = b - a) &
  (b <= a implies diameter ].a,b.[ = 0.))
  proof
   let a,b being R_eal;
   hereby
    assume
Z:    a < b;
     then
A:    ].a,b.[ <> {} by XXREAL_1:33;
     inf ].a,b.[ = a & sup ].a,b.[ = b by Z,XXREAL_2:28,32;
    hence diameter ].a,b.[ = b - a by A,Def10;
   end;
   assume b <= a;
    then ].a,b.[ = {} by XXREAL_1:28;
   hence diameter ].a,b.[ = 0. by Def10;
  end;

theorem
  for a,b being R_eal holds
  ((a <= b implies diameter [.a,b.] = b - a) &
  (b < a implies diameter [.a,b.] = 0.))
  proof
   let a,b being R_eal;
   hereby
    assume
Z:    a <= b;
     then
A:    [.a,b.] <> {} by XXREAL_1:30;
     inf [.a,b.] = a & sup [.a,b.] = b by Z,XXREAL_2:25,29;
    hence diameter [.a,b.] = b - a by A,Def10;
   end;
   assume b < a;
    then [.a,b.] = {} by XXREAL_1:29;
   hence diameter [.a,b.] = 0. by Def10;
  end;

theorem
  for a,b being R_eal holds
  ((a < b implies diameter [.a,b.[ = b - a) &
   (b <= a implies diameter [.a,b.[ = 0.))
  proof
   let a,b being R_eal;
   hereby
    assume
Z:    a < b;
     then
A:    [.a,b.[ <> {} by XXREAL_1:31;
     inf [.a,b.[ = a & sup [.a,b.[ = b by Z,XXREAL_2:26,31;
    hence diameter [.a,b.[ = b - a by A,Def10;
   end;
   assume b <= a;
    then [.a,b.[ = {} by XXREAL_1:27;
   hence diameter [.a,b.[ = 0. by Def10;
  end;

theorem
  for a,b being R_eal holds
  ((a < b implies diameter ].a,b.] = b - a) &
   (b <= a implies diameter ].a,b.] = 0.))
  proof
   let a,b being R_eal;
   hereby
    assume
Z:    a < b;
     then
A:    ].a,b.] <> {} by XXREAL_1:32;
     inf ].a,b.] = a & sup ].a,b.] = b by Z,XXREAL_2:27,30;
    hence diameter ].a,b.] = b - a by A,Def10;
   end;
   assume b <= a;
    then ].a,b.] = {} by XXREAL_1:26;
   hence diameter ].a,b.] = 0. by Def10;
  end;

canceled;

theorem
  for a,b being R_eal holds (a = -infty & b = +infty &
  (A = ].a,b.[ or A = [.a,b.] or A = [.a,b.[ or A = ].a,b.]))
  implies diameter(A) = +infty
proof
  let a,b be R_eal;
  assume
A1: a = -infty & b = +infty &
  (A = ].a,b.[ or A = [.a,b.] or A = [.a,b.[ or A = ].a,b.]);
B: sup A = +infty & inf A = -infty by A1,XXREAL_2:25,26,27,28,29,30,31,32;
  then A is non empty by XXREAL_2:40;
  then diameter(A) = b - a by Def10,A1,B
    .= +infty by A1,SUPINF_2:6;
  hence thesis;
end;

registration
  cluster empty Interval;
  existence
  proof
    {} is open_interval Subset of REAL
    proof
      ex a,b being R_eal st a <= b & {} = ].a,b.[
      proof
        take 0.,0.;
        thus thesis;
      end;
      hence thesis by Def5;
    end;
    hence thesis;
  end;
end;

definition
  redefine func {} -> Interval;
  coherence
  proof
    {} is open_interval Subset of REAL
    proof
      ex a,b being R_eal st (a <= b & {} = ].a,b.[)
      proof
        take 0.,0.;
        thus thesis;
      end;
      hence thesis by Def5;
    end;
    hence thesis;
  end;
end;

theorem Th60:
  diameter {} = 0. by Def10;

LX3:
  diameter A >= 0
   proof per cases;
    suppose A is empty;
     hence thesis by Def10;
    end;
    suppose A is non empty;
      then inf A <= sup A by XXREAL_2:40;
      then sup A - inf A >= 0 by SUPINF_2:72;
     hence thesis by Def10;
    end;
   end;

LX4:
  A c= B implies diameter A <= diameter B
 proof assume
Z:  A c= B;
  per cases;
  suppose A = {};
   then diameter A = 0 by Th60;
  hence diameter A <= diameter B by LX3;
  end;
  suppose
S:  A <> {};
   then B <> {} by Z;
   then
B: diameter B = sup B - inf B by Def10;
A: diameter A = sup A - inf A by S,Def10;
C: sup A <= sup B by Z,XXREAL_2:59;
   inf B <= inf A by Z,XXREAL_2:60;
  hence diameter A <= diameter B by A,B,C,SUPINF_2:15;
  end;
 end;

theorem
  (A c= B & B =[.a,b.] & b <= a) implies (diameter(A) = 0. & diameter(B) = 0.)
proof
  assume that
A1: A c= B and
A2: B =[.a,b.] and
A3: b <= a;
  per cases by A3,XXREAL_0:1;
  suppose a = b;
    then
U:   B = {a} by A2,XXREAL_1:17;
    then inf B = a & sup B = a by XXREAL_2:11,13;
    then
X:  diameter B = a - a by Def10,U
        .= 0 by SUPINF_2:73;
    then
Y:   diameter A <= 0 by A1,LX4;
    0 <= diameter A by LX3;
   hence thesis by Y,X;
  end;
  suppose b < a;
    then
X:   B = {} by A2,XXREAL_1:29;
     then A = {} by A1;
   hence thesis by X,Th60;
  end;
end;

theorem
  A c= B implies diameter A <= diameter B by LX4;

theorem
  0. <= diameter A by LX3;

