:: Real Sequences and Basic Operations on Them
::  by Jaros{\l}aw Kotowicz
::
:: Received July 4, 1989
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies FUNCT_1, ARYTM, RELAT_1, PARTFUN1, BOOLE, ARYTM_1, ABSVALUE,
      ARYTM_3, SEQ_1, VALUED_0, ANPROJ_1, FUNCOP_1, NORMSP_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0,
      VALUED_0, REAL_1, RELAT_1, FUNCT_1, FUNCOP_1,
      COMPLEX1, NAT_1, RELSET_1, PARTFUN1,
      FUNCT_2, VALUED_1;
 constructors PARTFUN1, FUNCT_2, XXREAL_0, REAL_1, COMPLEX1, VALUED_1,
      FUNCOP_1, RELSET_1;
 registrations XBOOLE_0, ORDINAL1, RELSET_1, NUMBERS, XREAL_0, MEMBERED,
      VALUED_0, VALUED_1, FUNCT_2;
 requirements NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions XREAL_0, RELAT_1, VALUED_1;
 theorems FUNCT_1, TARSKI, ABSVALUE, FUNCT_2, PARTFUN1, RELSET_1, RELAT_1,
      XREAL_0, ZFMISC_1, XBOOLE_0, XCMPLX_0, XCMPLX_1, COMPLEX1,
      VALUED_1, FUNCOP_1;
 schemes CLASSES1, FUNCT_2, XBOOLE_0;

begin

reserve f for Function;
reserve n,k,n1 for Element of NAT;
reserve r,p for real number;
reserve x,y,z for set;

definition
  mode Real_Sequence is sequence of REAL;
end;

reserve seq,seq1,seq2,seq3,seq',seq1' for Real_Sequence;

canceled 2;

theorem Th3:
  f is Real_Sequence iff (dom f=NAT & for x st x in NAT holds f.x is real)
proof
  thus f is Real_Sequence implies
  (dom f=NAT & for x st x in NAT holds f.x is real)
              by FUNCT_2:def 1;
  assume that
A3: dom f= NAT and
A4: for x st x in NAT holds f.x is real;
  now
    let y;
    assume y in rng f;
    then consider x such that
A5: x in dom f and
A6: y=f.x by FUNCT_1:def 5;
    f.x is real by A3,A4,A5;
    hence y in REAL by A6,XREAL_0:def 1;
  end;
  then rng f c=REAL by TARSKI:def 3;
  hence thesis by A3,FUNCT_2:def 1,RELSET_1:11;
end;

theorem Th4:
  f is Real_Sequence iff (dom f=NAT & for n holds f.n is real)
proof
  thus f is Real_Sequence implies (dom f=NAT & for n holds f.n is real) by Th3;
  assume that
A1: dom f=NAT and
A2: for n holds f.n is real;
  for x holds x in NAT implies f.x is real by A2;
  hence thesis by A1,Th3;
end;

definition
  let f be real-valued Function, x be set;
  redefine func f.x -> Real;
  coherence by XREAL_0:def 1;
end;

registration
 cluster non-zero PartFunc of NAT,REAL;
 existence
  proof
    reconsider p = NAT --> 1 as PartFunc of NAT,REAL by FUNCOP_1:58;
   take p;
    rng p = {1} by FUNCOP_1:14;
   hence not 0 in rng p by TARSKI:def 1;
  end;
end;

theorem
 for f being non-zero PartFunc of NAT,REAL
  holds rng f c= REAL \ {0}
    proof let f be non-zero PartFunc of NAT,REAL;
       not {} in rng f by RELAT_1:def 9;
      hence thesis by ZFMISC_1:40;
    end;

theorem Th6:
  seq is non-zero iff for x st x in NAT holds seq.x<>0
proof
  thus seq is non-zero implies for x st x in NAT holds seq.x<>0
  proof
    assume
Z:   seq is non-zero;
    let x;
    assume x in NAT;
    then x in dom seq by Th4;
    then seq.x in rng seq by FUNCT_1:def 5;
    hence thesis by Z,RELAT_1:def 9;
  end;
  assume
A2: for x st x in NAT holds seq.x<>0;
    assume 0 in rng seq;
    then ex x st x in dom seq & seq.x=0 by FUNCT_1:def 5;
  hence contradiction by A2;
end;

theorem Th7:
  seq is non-zero iff for n holds seq.n<>0
proof
  thus seq is non-zero implies for n holds seq.n<>0 by Th6;
  assume for n holds seq.n<>0;
  then for x holds x in NAT implies seq.x<>0;
  hence thesis by Th6;
end;

canceled 2;

theorem
  for r ex seq st rng seq={r}
proof
  let r;
  consider f such that
A1: dom f=NAT and
A2: rng f={r} by FUNCT_1:15;
  for x st x in {r} holds x in REAL by XREAL_0:def 1;
  then rng f c= REAL by A2,TARSKI:def 3;
  then reconsider f as Real_Sequence by A1,FUNCT_2:def 1,RELSET_1:11;
  take f;
  thus thesis by A2;
end;

scheme ExRealSeq{F(set)->real number}: ex seq st for n holds seq.n=F(n)
proof
  defpred P[set,set] means ex n st n=$1 & $2=F(n);
A1: now
    let x;
    assume x in NAT;
    then consider n such that
A2: n=x;
    reconsider r2=F(n) as set;
    take y=r2;
    thus P[x,y] by A2;
  end;
  consider f such that
A4: dom f=NAT and
A5: for x st x in NAT holds P[x,f.x] from CLASSES1:sch 1(A1);
  now
    let x;
    assume x in NAT;
    then ex n st n=x & f.x=F(n) by A5;
    hence f.x is real;
  end;
  then reconsider f as Real_Sequence by A4,Th3;
  take seq=f;
  let n;
  ex k st k=n & seq.n=F(k) by A5;
  hence thesis;
end;

::
::  BASIC OPERATIONS ON SEQUENCES
::

scheme PartFuncExD'{D,C()->non empty set, P[set,set]}:
  ex f being PartFunc of D(),C() st (for d be Element of D() holds
  d in dom f iff (ex c be Element of C() st P[d,c])) &
  (for d be Element of D() st d in dom f holds P[d,f.d])
proof
  consider x being Element of C();
  defpred X[Element of D(),Element of C()] means
  ((ex c be Element of C() st P[$1,c]) implies P[$1,$2]) &
  ((for c be Element of C() holds not P[$1,c]) implies $2=x);
A1: for d be Element of D() ex z be Element of C() st X[d,z]
  proof
    let d be Element of D();
    (for c be Element of C() holds not P[d,c]) implies
    ex z st ((ex c be Element of C() st P[d,c]) implies P[d,z]) &
    ((for c be Element of C() holds not P[d,c]) implies z=x);
    hence thesis;
  end;
  consider g being Function of D(),C() such that
A2: for d be Element of D() holds X[d,g.d] from FUNCT_2:sch 3(A1);
A3: dom g = D() & rng g c= C() by FUNCT_2:def 1;
  defpred X[set] means ex c be Element of C() st P[$1,c];
  consider X be set such that
A4: for x holds x in X iff x in D() & X[x] from XBOOLE_0:sch 1;
  for x holds x in X implies x in D() by A4;
  then reconsider X as Subset of D() by TARSKI:def 3;
  reconsider f=g|X as PartFunc of D(),C();
  take f;
  thus for d be Element of D() holds
  d in dom f iff (ex c be Element of C() st P[d,c])
  proof
    let d be Element of D();
    dom f c= X by RELAT_1:87;
    hence d in dom f implies ex c be Element of C() st P[d,c] by A4;
    assume ex c be Element of C() st P[d,c];
    then d in X & d in D() by A4;
    then d in dom g /\ X by A3,XBOOLE_0:def 4;
    hence thesis by RELAT_1:90;
  end;
  let d be Element of D();
  assume
A5: d in dom f;
  dom f c= X by RELAT_1:87;
  then ex c be Element of C() st P[d,c] by A4,A5;
  then P[d,g.d] by A2;
  hence thesis by A5,FUNCT_1:70;
end;

scheme LambdaPFD'{D,C()->non empty set, F(set)->Element of C(), P[set]}:
  ex f being PartFunc of D(),C() st
  (for d be Element of D() holds d in dom f iff P[d]) &
  (for d be Element of D() st d in dom f holds f.d = F(d))
proof
  defpred X[Element of D(),set] means P[$1] & $2 = F($1);
  consider f being PartFunc of D(),C() such that
A1: for d be Element of D() holds
  d in dom f iff ex c be Element of C() st X[d,c] and
A2: for d be Element of D() st d in dom f holds X[d,f.d] from PartFuncExD';
  take f;
  thus for d be Element of D() holds d in dom f iff P[d]
  proof
    let d be Element of D();
    thus d in dom f implies P[d]
    proof
      assume d in dom f;
      then ex c be Element of C() st P[d] & c = F(d) by A1;
      hence thesis;
    end;
    assume P[d];
    then ex c be Element of C() st P[d] & c = F(d);
    hence thesis by A1;
  end;
  thus thesis by A2;
end;

scheme UnPartFuncD'{C,D,X() -> set, F(set)->set}:
  for f,g being PartFunc of C(),D() st
  (dom f=X() & for c be Element of C() st c in dom f holds f.c = F(c)) &
  (dom g=X() & for c be Element of C() st c in dom g holds g.c = F(c))
  holds f = g
proof
  let f,g be PartFunc of C(),D();
  assume that
A1: (dom f=X() & for c be Element of C() st c in dom f holds f.c = F(c)) and
A2: dom g=X() & for c be Element of C() st c in dom g holds g.c = F(c);
  now
    let c be Element of C();
    assume
A3: c in dom f;
    hence f.c = F(c) by A1
      .= g.c by A1,A2,A3;
  end;
  hence thesis by A1,A2,PARTFUN1:34;
end;

theorem Th11:
  seq = seq1 + seq2 iff for n holds seq.n =seq1.n + seq2.n
proof
  thus seq = seq1 + seq2 implies for n holds seq.n =seq1.n + seq2.n
  proof
    assume
A1: seq = seq1 + seq2;
    let n;
    dom seq = NAT by FUNCT_2:def 1;
    hence thesis by A1,VALUED_1:def 1;
  end;
  assume
A2: for n holds seq.n =seq1.n + seq2.n;
A3: dom seq = NAT /\ NAT by FUNCT_2:def 1
    .= NAT /\ dom seq2 by FUNCT_2:def 1
    .= dom seq1 /\ dom seq2 by FUNCT_2:def 1;
  for n being set st n in dom seq holds seq.n = seq1.n + seq2.n by A2;
  hence thesis by A3,VALUED_1:def 1;
end;

theorem Th12:
  seq = seq1 (#) seq2 iff for n holds seq.n =seq1.n * seq2.n
proof
  thus seq = seq1 (#) seq2 implies for n holds seq.n =seq1.n * seq2.n
  proof
    assume
A1: seq = seq1 (#) seq2;
    let n;
    dom seq = NAT by FUNCT_2:def 1;
    hence thesis by A1,VALUED_1:def 4;
  end;
  assume
A2: for n holds seq.n =seq1.n * seq2.n;
A3: dom seq = NAT /\ NAT by FUNCT_2:def 1
    .= NAT /\ dom seq2 by FUNCT_2:def 1
    .= dom seq1 /\ dom seq2 by FUNCT_2:def 1;
  for n being set st n in dom seq holds seq.n = seq1.n * seq2.n by A2;
  hence thesis by A3,VALUED_1:def 4;
end;

theorem Th13:
  seq1 = r(#)seq2 iff for n holds seq1.n=r*seq2.n
proof
  thus seq1 = r(#)seq2 implies for n holds seq1.n=r*seq2.n by VALUED_1:6;
  assume
A1: for n holds seq1.n=r*seq2.n;
A2: dom seq1 = NAT by FUNCT_2:def 1
    .= dom seq2 by FUNCT_2:def 1;
  for n being set st n in dom seq1 holds seq1.n = r * seq2.n by A1;
  hence thesis by A2,VALUED_1:def 5;
end;

theorem
  seq1 = -seq2 iff for n holds seq1.n= -seq2.n
proof
  thus seq1 = -seq2 implies for n holds seq1.n=-seq2.n by VALUED_1:8;
  assume
A1: for n holds seq1.n= -seq2.n;
A2: dom seq1 = NAT by FUNCT_2:def 1
    .= dom seq2 by FUNCT_2:def 1;
  for n being set st n in dom seq1 holds seq1.n = - seq2.n by A1;
  hence thesis by A2,VALUED_1:9;
end;

theorem
  seq1 - seq2 = seq1 +- seq2;

theorem Th16:
  seq1 = abs seq iff for n holds seq1.n= abs(seq.n)
proof
  thus seq1 = abs seq implies for n holds seq1.n=abs(seq.n) by VALUED_1:18;
  assume
A1: for n holds seq1.n= abs(seq.n);
A2: dom seq1 = NAT by FUNCT_2:def 1
    .= dom seq by FUNCT_2:def 1;
  for n being set st n in dom seq1 holds seq1.n = abs(seq.n) by A1;
  hence thesis by A2,VALUED_1:def 11;
end;

canceled 3;

theorem Th20:
  (seq1 + seq2) + seq3 = seq1 + (seq2 + seq3)
proof
  now
    let n;
    thus ((seq1+seq2)+seq3).n=(seq1+seq2).n+ seq3.n by Th11
      .=seq1.n+seq2.n+seq3.n by Th11
      .=seq1.n+(seq2.n+seq3.n)
      .=seq1.n+(seq2+seq3).n by Th11
      .=(seq1+(seq2+seq3)).n by Th11;
  end;
  hence thesis by FUNCT_2:113;
end;

canceled;

theorem Th22:
  (seq1 (#) seq2) (#) seq3 = seq1 (#) (seq2 (#) seq3)
proof
  now
    let n;
    thus ((seq1(#)seq2)(#)seq3).n=(seq1(#)seq2).n*seq3.n by Th12
      .=seq1.n*seq2.n*seq3.n by Th12
      .=seq1.n*(seq2.n*seq3.n)
      .=seq1.n*(seq2(#)seq3).n by Th12
      .=(seq1(#)(seq2(#)seq3)).n by Th12;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th23:
  (seq1 + seq2) (#) seq3 = seq1 (#) seq3 + seq2 (#) seq3
proof
  now
    let n;
    thus ((seq1+seq2)(#)seq3).n=(seq1+seq2).n*seq3.n by Th12
      .=(seq1.n+seq2.n)*seq3.n by Th11
      .=seq1.n*seq3.n+seq2.n*seq3.n
      .=(seq1(#)seq3).n+seq2.n*seq3.n by Th12
      .=(seq1(#)seq3).n+(seq2(#)seq3).n by Th12
      .=((seq1(#)seq3)+(seq2(#)seq3)).n by Th11;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem
  seq3 (#) (seq1 + seq2) = seq3 (#) seq1 + seq3 (#) seq2 by Th23;

theorem
  -seq = (-1) (#) seq;

theorem Th26:
  r(#)(seq1(#)seq2)=r(#)seq1(#)seq2
proof
  now
    let n;
    thus (r(#)(seq1(#)seq2)).n=r*(seq1(#)seq2).n by Th13
      .=r*(seq1.n*seq2.n) by Th12
      .=(r*seq1.n)*seq2.n
      .=(r(#)seq1).n*seq2.n by Th13
      .=(r(#)seq1 (#) seq2).n by Th12;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th27:
  r (#) (seq1 (#) seq2) = seq1 (#) (r (#) seq2)
proof
  now
    let n;
    thus (r(#)(seq1(#)seq2)).n=r*(seq1(#)seq2).n by Th13
      .=r*(seq1.n*seq2.n) by Th12
      .=seq1.n*(r*seq2.n)
      .=seq1.n*(r(#)seq2).n by Th13
      .=(seq1(#)(r(#)seq2)).n by Th12;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th28:
  (seq1 - seq2) (#) seq3 = seq1 (#) seq3 - seq2 (#) seq3
proof
  thus (seq1-seq2)(#)seq3=seq1(#)seq3+(-seq2)(#)seq3 by Th23
    .=seq1(#)seq3-seq2(#)seq3 by Th26;
end;

theorem
  seq3(#)seq1-seq3(#)seq2=seq3(#)(seq1-seq2) by Th28;

theorem Th30:
  r(#)(seq1+seq2)=r(#)seq1+r(#)seq2
proof
  now
    let n;
    thus (r(#)(seq1 + seq2)).n=r*(seq1+seq2).n by Th13
      .=r*(seq1.n+seq2.n) by Th11
      .=r*seq1.n+r*seq2.n
      .=(r(#)seq1).n+r*seq2.n by Th13
      .=(r(#)seq1).n+(r(#)seq2).n by Th13
      .=((r(#)seq1)+(r(#)seq2)).n by Th11;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th31:
  (r*p)(#)seq=r(#)(p(#)seq)
proof
  now
    let n;
    thus ((r*p)(#)seq).n=(r*p)*seq.n by Th13
      .=r*(p*seq.n)
      .=r*(p(#)seq).n by Th13
      .=(r(#)(p(#)seq)).n by Th13;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th32:
  r(#)(seq1-seq2)=r(#)seq1-r(#)seq2
proof
  thus r(#)(seq1-seq2)=r(#)seq1+r(#)(-seq2) by Th30
    .=r(#)seq1+((-1)*r)(#)seq2 by Th31
    .=r(#)seq1-(r(#)seq2) by Th31;
end;

theorem
  r(#)(seq1/"seq)=(r(#)seq1)/"seq
proof
  thus r(#)(seq1/"seq)= r(#)(seq1(#)(seq"))
    .= (r(#)seq1)/"seq by Th26;
end;

theorem
  seq1-(seq2+seq3)=seq1-seq2-seq3
proof
  thus seq1-(seq2+seq3)=seq1+(-seq2+(-1)(#)seq3) by Th30
    .=seq1-seq2-seq3 by Th20;
end;

theorem
  1(#)seq=seq
proof
  now
    let n;
    thus (1(#)seq).n=1*seq.n by Th13
      .=seq.n;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem
  for F being real-valued Function holds --F = F;

theorem
  seq1 - (-seq2) = seq1 + seq2;

theorem
  seq1-(seq2-seq3)=seq1-seq2+seq3
proof
  thus seq1-(seq2-seq3)=seq1+(-seq2-(-seq3)) by Th32
    .=seq1-seq2+seq3 by Th20;
end;

theorem
  seq1+(seq2-seq3)=seq1+seq2-seq3
proof
  thus seq1+(seq2-seq3)=seq1+seq2+-seq3 by Th20
    .=seq1+seq2-seq3;
end;

theorem
  (-seq1)(#)seq2=-(seq1(#)seq2) & seq1(#)(-seq2)=-(seq1(#)seq2) by Th26;

theorem Th41:
  seq is non-zero implies seq" is non-zero
proof
  assume that
A1: seq is non-zero and
A2: not seq" is non-zero;
  consider n1 such that
A3: (seq").n1=0 by A2,Th7;
  (seq.n1)"=(seq").n1 by VALUED_1:10;
  hence contradiction by A1,A3,Th7,XCMPLX_1:203;
end;

theorem
  seq"" = seq;

theorem Th43:
  seq is non-zero & seq1 is non-zero iff seq(#)seq1 is non-zero
proof
  thus seq is non-zero & seq1 is non-zero implies seq(#)seq1 is
  non-zero
  proof
    assume that
A1: seq is non-zero and
A2: seq1 is non-zero;
    now
      let n;
A3:   seq.n<>0 by A1,Th7;
A4:   seq1.n<>0 by A2,Th7;
      (seq(#)seq1).n=(seq.n)*(seq1.n) by Th12;
      hence (seq(#)seq1).n<>0 by A3,A4,XCMPLX_1:6;
    end;
    hence thesis by Th7;
  end;
  assume
A5: seq(#)seq1 is non-zero;
  now
    let n;
    (seq(#)seq1).n=(seq.n)*(seq1.n) by Th12;
    hence seq.n<>0 by A5,Th7;
  end;
  hence seq is non-zero by Th7;
  now
    let n;
    (seq(#)seq1).n=(seq.n)*(seq1.n) by Th12;
    hence seq1.n<>0 by A5,Th7;
  end;
  hence thesis by Th7;
end;

theorem Th44:
  seq"(#)seq1"=(seq(#)seq1)"
proof
  now
    let n;
    thus ((seq")(#)(seq1")).n=((seq").n)*((seq1").n) by Th12
      .=((seq").n)*(seq1.n)" by VALUED_1:10
      .=(seq.n)"*(seq1.n)" by VALUED_1:10
      .=((seq.n)*(seq1.n))" by XCMPLX_1:205
      .=((seq(#)seq1).n)" by Th12
      .=((seq(#)seq1)").n by VALUED_1:10;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem
  seq is non-zero implies (seq1/"seq)(#)seq=seq1
proof
  assume
A1: seq is non-zero;
  now
    let n;
A2: seq.n<>0 by A1,Th7;
    thus ((seq1/"seq)(#)seq).n=((seq1(#)seq").n)*seq.n by Th12
      .=(seq1.n)*(seq".n)*seq.n by Th12
      .=(seq1.n)*(seq.n)"*seq.n by VALUED_1:10
      .=(seq1.n)*((seq.n)"*seq.n)
      .=(seq1.n)*1 by A2,XCMPLX_0:def 7
      .=seq1.n;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem
  (seq'/"seq)(#)(seq1'/"seq1)=(seq'(#)seq1')/"(seq(#)seq1)
proof
  now
    let n;
    thus ((seq'/"seq)(#)(seq1'/"seq1)).n=((seq'(#)seq").n)*(seq1'/"seq1).n by
Th12
      .=(seq'.n)*(seq".n)*(seq1'(#)seq1").n by Th12
      .=(seq'.n)*(seq".n)*((seq1'.n)*seq1".n) by Th12
      .=(seq'.n)*((seq1'.n)*((seq".n)*seq1".n))
      .=(seq'.n)*((seq1'.n)*((seq"(#)seq1").n)) by Th12
      .=(seq'.n)*(seq1'.n)*((seq"(#)seq1").n)
      .=(seq'.n)*(seq1'.n)*((seq(#)seq1)".n) by Th44
      .=((seq'(#)seq1').n)*(seq(#)seq1)".n by Th12
      .=((seq'(#)seq1')/"(seq(#)seq1)).n by Th12;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem
  seq is non-zero & seq1 is non-zero implies seq/"seq1 is non-zero
proof
  assume that
A1: seq is non-zero and
A2: seq1 is non-zero;
  seq1" is non-zero by A2,Th41;
  hence thesis by A1,Th43;
end;

theorem Th48:
  (seq/"seq1)"=seq1/"seq
proof
  now
    let n;
    thus (seq/"seq1)".n=(seq"(#)seq1"").n by Th44
      .=(seq1/"seq).n;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem
  seq2(#)(seq1/"seq)=(seq2(#)seq1)/"seq
proof
  thus seq2(#)(seq1/"seq) = seq2(#)(seq1(#)(seq"))
    .=(seq2(#)seq1)/"seq by Th22;
end;

theorem
  seq2/"(seq/"seq1)=(seq2(#)seq1)/"seq
proof
  thus seq2/"(seq/"seq1) = seq2(#)(seq1/"seq) by Th48
    .= seq2(#)(seq1(#)(seq"))
    .= (seq2(#)seq1)/"seq by Th22;
end;

theorem Th51:
  seq1 is non-zero implies seq2/"seq=(seq2(#)seq1)/"(seq(#)seq1)
proof
  assume that
A1: seq1 is non-zero;
  now
    let n;
A2: seq1.n<>0 by A1,Th7;
    thus (seq2/"seq).n=(seq2.n)*1*seq".n by Th12
      .=(seq2.n)*((seq1.n)*(seq1.n)")*seq".n by A2,XCMPLX_0:def 7
      .=(seq2.n)*(seq1.n)*((seq1.n)"*seq".n)
      .=((seq2(#)seq1).n)*((seq1.n)"*seq".n) by Th12
      .=((seq2(#)seq1).n)*((seq1".n)*seq".n) by VALUED_1:10
      .=((seq2(#)seq1).n)*(seq"(#)seq1").n by Th12
      .=((seq2(#)seq1).n)*(seq(#)seq1)".n by Th44
      .=((seq2(#)seq1)/"(seq(#)seq1)).n by Th12;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th52:
  r<>0 & seq is non-zero implies r(#)seq is non-zero
proof
  assume that
A1: r<>0 and
A2: seq is non-zero and
A3: not r(#)seq is non-zero;
  consider n1 such that
A4: (r(#)seq).n1=0 by A3,Th7;
A5: r*seq.n1=0 by A4,Th13;
  seq.n1 <> 0 by A2,Th7;
  hence contradiction by A1,A5,XCMPLX_1:6;
end;

theorem
  seq is non-zero implies -seq is non-zero by Th52;

theorem Th54:
  (r(#)seq)"=r"(#)seq"
proof
  now
    let n;
    thus (r(#)seq)".n=((r(#)seq).n)" by VALUED_1:10
      .=(r*(seq.n))" by Th13
      .=r"*(seq.n)" by XCMPLX_1:205
      .=r"*seq".n by VALUED_1:10
      .=(r"(#)seq").n by Th13;
  end;
  hence thesis by FUNCT_2:113;
end;

Lm1: (-1)"=-1;

theorem
  (-seq)" = (-1)(#)seq" by Lm1,Th54;

theorem
  -seq1/"seq=(-seq1)/"seq & seq1/"(-seq)=-seq1/"seq
proof
  thus -seq1/"seq=(-1)(#)(seq1(#)(seq"))
    .=(-seq1)/"seq by Th26;
  thus seq1/"(-seq)=seq1(#)((-1)(#)seq") by Lm1,Th54
    .=-(seq1/"seq) by Th27;
end;

theorem
  seq1/"seq + seq1'/"seq = (seq1 + seq1') /" seq &
  seq1/"seq - seq1'/"seq = (seq1 - seq1') /" seq
proof
  thus seq1/"seq + seq1'/"seq = (seq1 + seq1') (#) (seq") by Th23
    .= (seq1 + seq1') /" seq;
  thus seq1/"seq - seq1'/"seq = (seq1 - seq1') (#) (seq") by Th28
    .= (seq1 - seq1') /" seq;
end;

theorem
  seq is non-zero & seq' is non-zero implies
  (seq1/"seq + seq1'/"seq'=(seq1(#)seq'+seq1'(#)seq)/"(seq(#)seq')) &
  (seq1/"seq - seq1'/"seq'=(seq1(#)seq'-seq1'(#)seq)/"(seq(#)seq'))
proof
  assume that
A1: seq is non-zero and
A2: seq' is non-zero;
  thus seq1/"seq + seq1'/"seq'=(seq1(#)seq')/"(seq(#)seq')+seq1'/"seq'
  by A2,Th51
    .=(seq1(#)seq')/"(seq(#)seq')+(seq1'(#)seq)/"(seq(#)seq') by A1,Th51
    .=(seq1(#)seq'+seq1'(#)seq)(#)((seq(#)seq')") by Th23
    .=(seq1(#)seq'+seq1'(#)seq)/"(seq(#)seq');
  thus seq1/"seq - seq1'/"seq'=(seq1(#)seq')/"(seq(#)seq')-seq1'/"seq'
  by A2,Th51
    .=(seq1(#)seq')/"(seq(#)seq')-(seq1'(#)seq)/"(seq(#)seq') by A1,Th51
    .=(seq1(#)seq'-seq1'(#)seq)(#)((seq(#)seq')") by Th28
    .=(seq1(#)seq'-seq1'(#)seq)/"(seq(#)seq');
end;

theorem
  (seq1'/"seq)/"(seq'/"seq1)=(seq1'(#)seq1)/"(seq(#)seq')
proof
  thus (seq1'/"seq)/"(seq'/"seq1)=(seq1'/"seq)(#)(seq'"(#)seq1"") by Th44
    .=seq1'(#)seq"(#)seq1(#)seq'" by Th22
    .=seq1'(#)(seq1(#)seq")(#)seq'" by Th22
    .=seq1'(#)((seq1(#)seq")(#)seq'") by Th22
    .=seq1'(#)(seq1(#)(seq"(#)seq'")) by Th22
    .=seq1'(#)seq1(#)(seq"(#)seq'") by Th22
    .=(seq1'(#)seq1)/"(seq(#)seq') by Th44;
end;

theorem Th60:
  abs(seq(#)seq')=abs(seq)(#)abs(seq')
proof
  now
    let n;
    thus (abs(seq(#)seq')).n=abs(((seq(#)seq').n)) by Th16
      .=abs((seq.n)*(seq'.n)) by Th12
      .=abs((seq.n))*abs((seq'.n)) by COMPLEX1:151
      .=((abs(seq)).n)*abs((seq'.n)) by Th16
      .=((abs(seq)).n)*(abs(seq')).n by Th16
      .=(abs(seq)(#)abs(seq')).n by Th12;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem
  seq is non-zero implies abs(seq) is non-zero
proof
  assume
A1: seq is non-zero;
  now
    let n;
    seq.n<>0 by A1,Th7;
    then abs(seq.n)<>0 by COMPLEX1:133;
    hence (abs(seq)).n<>0 by Th16;
  end;
  hence thesis by Th7;
end;

theorem Th62:
  abs(seq)"=abs(seq")
proof
  now
    let n;
    thus (abs(seq")).n=abs(seq".n) by Th16
      .=abs((seq.n)") by VALUED_1:10
      .=abs(1/(seq.n)) by XCMPLX_1:217
      .=1/abs(seq.n) by ABSVALUE:15
      .=(abs(seq.n))" by XCMPLX_1:217
      .=(abs(seq).n)" by Th16
      .=(abs(seq))".n by VALUED_1:10;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem
  abs(seq'/"seq)=abs(seq')/"abs(seq)
proof
  thus abs(seq'/"seq)=abs(seq')(#)abs(seq") by Th60
    .=abs(seq')/"abs(seq) by Th62;
end;

theorem
  abs(r(#)seq)=abs(r)(#)abs(seq)
proof
  now
    let n;
    thus abs(r(#)seq).n=abs((r(#)seq).n) by Th16
      .=abs(r*(seq.n)) by Th13
      .=abs(r)*abs(seq.n) by COMPLEX1:151
      .=abs(r)*(abs(seq)).n by Th16
      .=(abs(r)(#)abs(seq)).n by Th13;
  end;
  hence thesis by FUNCT_2:113;
end;

