:: The Limit of a Real Function at Infinity. Halflines.
:: Real Sequence Divergent to Infinity
::  by Jaros{\l}aw Kotowicz
::
:: Received August 20, 1990
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies NUMBERS, XREAL_0, ORDINAL1, SUBSET_1, SEQ_1, PARTFUN1, CARD_1,
      ARYTM_3, XXREAL_0, ARYTM_1, RELAT_1, TARSKI, XBOOLE_0, VALUED_1, PROB_1,
      XXREAL_1, VALUED_0, XXREAL_2, FUNCT_1, SEQ_2, ORDINAL2, COMPLEX1,
      FUNCOP_1, REAL_1, NAT_1, SQUARE_1, FUNCT_3, SEQM_3, FUNCT_2, ORDINAL4,
      LIMFUNC1;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0,
      COMPLEX1, REAL_1, NAT_1, FUNCT_2, FUNCOP_1, VALUED_0, VALUED_1, SEQ_1,
      SEQ_2, SEQM_3, PROB_1, RELSET_1, SQUARE_1, PARTFUN1, FUNCT_3, RFUNCT_1,
      XXREAL_0, XXREAL_1, RECDEF_1;
 constructors PARTFUN1, FUNCOP_1, REAL_1, SQUARE_1, NAT_1, COMPLEX1, VALUED_1,
      SEQ_2, SEQM_3, PROB_1, RCOMP_1, RFUNCT_1, RFUNCT_2, SEQ_1, RECDEF_1,
      RELSET_1, BINOP_2, RVSUM_1;
 registrations ORDINAL1, RELSET_1, NUMBERS, XXREAL_0, XREAL_0, MEMBERED,
      XXREAL_1, VALUED_0, VALUED_1, FUNCT_2, FUNCOP_1, SEQ_2, SEQ_4;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions SQUARE_1, TARSKI, SEQ_2, XBOOLE_0, PROB_1, MEMBERED, VALUED_1;
 theorems TARSKI, NAT_1, ABSVALUE, SEQ_1, SEQ_2, SEQM_3, SEQ_4, SQUARE_1,
      RFUNCT_1, RFUNCT_2, FUNCT_1, FUNCT_2, XREAL_0, XBOOLE_0, XBOOLE_1,
      FINSEQ_2, XCMPLX_0, XCMPLX_1, XREAL_1, FUNCOP_1, COMPLEX1, XXREAL_0,
      ORDINAL1, XXREAL_1, VALUED_1, VALUED_0;
 schemes SEQ_1, FUNCT_2;

begin

reserve r,r1,g for real number,
  n,m,k for Element of NAT,
  seq,seq1, seq2 for Real_Sequence,
  f,f1,f2 for PartFunc of REAL,REAL,
  x for set;

Lm1: 0<g & r<=r1 implies r-g<r1 & r<r1+g
proof
  assume
A1: 0<g & r<=r1;
  then r-g<r1-0 by XREAL_1:17;
  hence r-g<r1;
  r+0<r1+g by A1,XREAL_1:10;
  hence thesis;
end;

Lm2: rng seq c=dom(f1+f2) implies dom(f1+f2)=dom f1/\dom f2 & rng seq c=dom f1
& rng seq c=dom f2
proof
  assume
A1: rng seq c=dom(f1+f2);
  thus dom(f1+f2)=dom f1/\dom f2 by VALUED_1:def 1;
  then dom(f1+f2)c=dom f1 & dom(f1+f2)c=dom f2 by XBOOLE_1:17;
  hence thesis by A1,XBOOLE_1:1;
end;

Lm3: rng seq c=dom(f1(#)f2) implies dom(f1(#)f2)=dom f1/\dom f2 & rng seq c=
dom f1 & rng seq c=dom f2
proof
  assume
A1: rng seq c=dom(f1(#)f2);
  thus dom(f1(#)f2)=dom f1/\dom f2 by VALUED_1:def 4;
  then dom(f1(#)f2)c=dom f1 & dom(f1(#)f2)c=dom f2 by XBOOLE_1:17;
  hence thesis by A1,XBOOLE_1:1;
end;

definition
  let n,m;
  redefine func max(n,m) -> Element of NAT;
  coherence by FINSEQ_2:1;
end;

notation
  let r be real number;
  synonym left_open_halfline r for halfline r;
end;

definition
  let r be real number;
  func left_closed_halfline r -> Subset of REAL equals
  ].-infty,r.];
  coherence
  proof
    for x st x in ].-infty,r.] holds x in REAL by XREAL_0:def 1;
    hence thesis by TARSKI:def 3;
  end;
  func right_closed_halfline r -> Subset of REAL equals
  [.r,+infty.[;
  coherence
  proof
    for x st x in [.r,+infty.[ holds x in REAL by XREAL_0:def 1;
    hence thesis by TARSKI:def 3;
  end;
  func right_open_halfline r -> Subset of REAL equals
  ].r,+infty.[;
  coherence
  proof
    for x st x in ].r,+infty.[ holds x in REAL by XREAL_0:def 1;
    hence thesis by TARSKI:def 3;
  end;
end;

canceled 25;

theorem
  (seq is non-decreasing implies seq is bounded_below) & (seq is
  non-increasing implies seq is bounded_above)
proof
  thus seq is non-decreasing implies seq is bounded_below
  proof
    assume
A1: seq is non-decreasing;
    take seq.0-1;
    let n;
    seq.0-1<seq.0-0 & seq.0<=seq.n by A1,SEQM_3:21,XREAL_1:17;
    hence thesis by XXREAL_0:2;
  end;
  assume
A2: seq is non-increasing;
  take seq.0+1;
  let n;
  seq.0+0<seq.0+1 & seq.n<=seq.0 by A2,SEQM_3:22,XREAL_1:10;
  hence thesis by XXREAL_0:2;
end;

theorem Th27:
  seq is non-zero & seq is convergent & lim seq=0 & seq is
  non-decreasing implies for n holds seq.n<0
proof
  assume that
A1: seq is non-zero and
A2: seq is convergent & lim seq=0 and
A3: seq is non-decreasing and
A4: ex n st not seq.n<0;
  consider n such that
A5: not seq.n<0 by A4;
  now
    per cases by A5;
    suppose
A6:   0<seq.n;
      then consider n1 be Element of NAT such that
A7:   for m st n1<=m holds abs(seq.m-0)<seq.n by A2,SEQ_2:def 7;
      abs(seq.(n1+n)-0)<seq.n by A7,NAT_1:12;
      then n<=n1+n & seq.(n1+n)<seq.n by A6,ABSVALUE:def 1,NAT_1:12;
      hence contradiction by A3,SEQM_3:12;
    end;
    suppose
      seq.n=0;
      hence contradiction by A1,SEQ_1:7;
    end;
  end;
  hence contradiction;
end;

theorem Th28:
  seq is non-zero & seq is convergent & lim seq=0 & seq is
  non-increasing implies for n holds 0<seq.n
proof
  assume that
A1: seq is non-zero and
A2: seq is convergent & lim seq=0 and
A3: seq is non-increasing and
A4: ex n st not 0<seq.n;
  consider n such that
A5: not 0<seq.n by A4;
  now
    per cases by A5;
    suppose
A6:   seq.n<0;
      then -0<-seq.n by XREAL_1:26;
      then consider n1 be Element of NAT such that
A7:   for m st n1<=m holds abs(seq.m-0)<-seq.n by A2,SEQ_2:def 7;
A8:   abs(seq.(n1+n)-0)<-seq.n by A7,NAT_1:12;
A9:   n<=n1+n by NAT_1:12;
      then seq.(n1+n)<0 by A3,A6,SEQM_3:14;
      then -seq.(n1+n)<-seq.n by A8,ABSVALUE:def 1;
      then seq.n<seq.(n1+n) by XREAL_1:26;
      hence contradiction by A3,A9,SEQM_3:14;
    end;
    suppose
      seq.n=0;
      hence contradiction by A1,SEQ_1:7;
    end;
  end;
  hence contradiction;
end;

theorem Th29:
  seq is convergent & 0<lim seq implies ex n st for m st n<=m holds 0<seq.m
proof
  assume that
A1: seq is convergent and
A2: 0<lim seq and
A3: for n ex m st n<=m & not 0<seq.m;
  consider n such that
A4: for m st n<=m holds abs(seq.m-lim seq)<lim seq by A1,A2,SEQ_2:def 7;
  consider m such that
A5: n<=m and
A6: not 0<seq.m by A3;
A7: abs(seq.m-lim seq)<lim seq by A4,A5;
  now
    per cases by A6;
    suppose
A8:   seq.m<0;
      then -(seq.m-lim seq)<lim seq by A2,A7,ABSVALUE:def 1;
      then lim seq-seq.m<lim seq;
      then lim seq<lim seq+seq.m by XREAL_1:21;
      then lim seq - lim seq<seq.m by XREAL_1:21;
      hence contradiction by A8;
    end;
    suppose
      seq.m=0;
      then abs(-lim seq)<lim seq by A7;
      then abs(lim seq)<lim seq by COMPLEX1:138;
      hence contradiction by A2,ABSVALUE:def 1;
    end;
  end;
  hence contradiction;
end;

theorem Th30:
  seq is convergent & 0<lim seq implies ex n st for m st n<=m
  holds (lim seq)/2<seq.m
proof
  assume that
A1: seq is convergent and
A2: 0<lim seq;
  reconsider s1 = NAT -->(lim seq)/2 as Real_Sequence;
A3: seq-s1 is convergent by A1,SEQ_2:25;
  s1.0=(lim seq)/2 by FUNCOP_1:13;
  then lim(seq-s1)=(lim seq)/2+(lim seq)/2-(lim seq)/2 by A1,SEQ_4:59
    .=(lim seq)/2;
  then consider n such that
A4: for m st n<=m holds 0<(seq-s1).m by A2,A3,Th29,XREAL_1:217;
  take n;
  let m;
  assume n<=m;
  then 0<(seq-s1).m by A4;
  then 0<seq.m-s1.m by RFUNCT_2:6;
  then 0<seq.m-(lim seq)/2 by FUNCOP_1:13;
  then 0+(lim seq)/2<seq.m by XREAL_1:22;
  hence thesis;
end;

reserve r,r1,r2,g,g1,g2 for Real;

definition
  let seq;
  attr seq is divergent_to+infty means
  :Def4:
  for r ex n st for m st n<=m holds r<seq.m;
  attr seq is divergent_to-infty means
  :Def5:
  for r ex n st for m st n<=m holds seq.m<r;
end;

canceled 2;

theorem
  seq is divergent_to+infty or seq is divergent_to-infty implies ex n st
  for m st n<=m holds seq^\m is non-zero
proof
  assume
A1: seq is divergent_to+infty or seq is divergent_to-infty;
  now
    per cases by A1;
    suppose
      seq is divergent_to+infty;
      then consider n such that
A2:   for m st n<=m holds 0<seq.m by Def4;
      take n;
      let m such that
A3:   n<=m;
      now
        let k;
        0<seq.(k+m) by A2,A3,NAT_1:12;
        hence 0<>(seq^\m).k by NAT_1:def 3;
      end;
      hence seq^\m is non-zero by SEQ_1:7;
    end;
    suppose
      seq is divergent_to-infty;
      then consider n such that
A4:   for m st n<=m holds seq.m<0 by Def5;
      take n;
      let m such that
A5:   n<=m;
      now
        let k;
        seq.(k+m)<0 by A4,A5,NAT_1:12;
        hence (seq^\m).k<>0 by NAT_1:def 3;
      end;
      hence seq^\m is non-zero by SEQ_1:7;
    end;
  end;
  hence thesis;
end;

theorem Th34:
  (seq^\k is divergent_to+infty implies seq is divergent_to+infty)
  & (seq^\k is divergent_to-infty implies seq is divergent_to-infty)
proof
  thus seq^\k is divergent_to+infty implies seq is divergent_to+infty
  proof
    assume
A1: seq^\k is divergent_to+infty;
    let r be Real;
    consider n1 be Element of NAT such that
A2: for m st n1<=m holds r<(seq^\k).m by A1,Def4;
    take n=n1+k;
    let m;
    assume n<=m;
    then consider n2 be Nat such that
A3: m=n+n2 by NAT_1:10;
    reconsider n2 as Element of NAT by ORDINAL1:def 13;
A4: r<(seq^\k).(n1+n2) by A2,NAT_1:12;
    n1+n2+k=m by A3;
    hence thesis by A4,NAT_1:def 3;
  end;
  assume
A5: seq^\k is divergent_to-infty;
  let r be Real;
  consider n1 be Element of NAT such that
A6: for m st n1<=m holds (seq^\k).m<r by A5,Def5;
  take n=n1+k;
  let m;
  assume n<=m;
  then consider n2 be Nat such that
A7: m=n+n2 by NAT_1:10;
  reconsider n2 as Element of NAT by ORDINAL1:def 13;
A8: (seq^\k).(n1+n2)< r by A6,NAT_1:12;
  n1+n2+k=m by A7;
  hence thesis by A8,NAT_1:def 3;
end;

theorem Th35:
  seq1 is divergent_to+infty & seq2 is divergent_to+infty implies
  seq1+seq2 is divergent_to+infty
proof
  assume that
A1: seq1 is divergent_to+infty and
A2: seq2 is divergent_to+infty;
  let r be Real;
  consider n1 be Element of NAT such that
A3: for m st n1<=m holds r/2<seq1.m by A1,Def4;
  consider n2 be Element of NAT such that
A4: for m st n2<=m holds r/2<seq2.m by A2,Def4;
  take n=max(n1,n2);
  let m such that
A5: n<=m;
  n2<=n by XXREAL_0:25;
  then n2<=m by A5,XXREAL_0:2;
  then
A6: r/2<seq2.m by A4;
  n1<=n by XXREAL_0:25;
  then n1<=m by A5,XXREAL_0:2;
  then r/2<seq1.m by A3;
  then r/2+r/2<seq1.m+seq2.m by A6,XREAL_1:10;
  hence thesis by SEQ_1:11;
end;

theorem Th36:
  seq1 is divergent_to+infty & seq2 is bounded_below implies seq1+
  seq2 is divergent_to+infty
proof
  assume that
A1: seq1 is divergent_to+infty and
A2: seq2 is bounded_below;
  let r be Real;
  consider M be real number such that
A3: for n holds M<seq2.n by A2,SEQ_2:def 4;
  consider n such that
A4: for m st n<=m holds r-M<seq1.m by A1,Def4;
  take n;
  let m;
  assume n<=m;
  then r-M<seq1.m by A4;
  then r-M+M<seq1.m+seq2.m by A3,XREAL_1:10;
  hence thesis by SEQ_1:11;
end;

theorem Th37:
  seq1 is divergent_to+infty & seq2 is divergent_to+infty implies
  seq1(#)seq2 is divergent_to+infty
proof
  assume that
A1: seq1 is divergent_to+infty and
A2: seq2 is divergent_to+infty;
  let r be Real;
  consider n1 be Element of NAT such that
A3: for m st n1<=m holds sqrt abs(r)<seq1.m by A1,Def4;
  consider n2 be Element of NAT such that
A4: for m st n2<=m holds sqrt abs(r)<seq2.m by A2,Def4;
  take n=max(n1,n2);
  let m such that
A5: n<=m;
  n2<=n by XXREAL_0:25;
  then n2<= m by A5,XXREAL_0:2;
  then
A6: sqrt abs(r)<seq2.m by A4;
  n1<=n by XXREAL_0:25;
  then n1<= m by A5,XXREAL_0:2;
  then
A7: sqrt abs(r)<seq1.m by A3;
A8: abs(r)>=0 by COMPLEX1:132;
  then sqrt abs(r)>=0 by SQUARE_1:def 4;
  then (sqrt abs(r))^2<seq1.m*seq2.m by A7,A6,XREAL_1:98;
  then
A9: abs(r)<seq1.m*seq2.m by A8,SQUARE_1:def 4;
  r<=abs(r) by ABSVALUE:11;
  then r<seq1.m*seq2.m by A9,XXREAL_0:2;
  hence thesis by SEQ_1:12;
end;

theorem Th38:
  seq1 is divergent_to-infty & seq2 is divergent_to-infty implies
  seq1+seq2 is divergent_to-infty
proof
  assume that
A1: seq1 is divergent_to-infty and
A2: seq2 is divergent_to-infty;
  let r be Real;
  consider n1 be Element of NAT such that
A3: for m st n1<=m holds seq1.m<r/2 by A1,Def5;
  consider n2 be Element of NAT such that
A4: for m st n2<=m holds seq2.m<r/2 by A2,Def5;
  take n=max(n1,n2);
  let m such that
A5: n<=m;
  n2<=n by XXREAL_0:25;
  then n2<=m by A5,XXREAL_0:2;
  then
A6: seq2.m<r/2 by A4;
  n1<=n by XXREAL_0:25;
  then n1<=m by A5,XXREAL_0:2;
  then seq1.m<r/2 by A3;
  then seq1.m+seq2.m<r/2+r/2 by A6,XREAL_1:10;
  hence thesis by SEQ_1:11;
end;

theorem Th39:
  seq1 is divergent_to-infty & seq2 is bounded_above implies seq1+
  seq2 is divergent_to-infty
proof
  assume that
A1: seq1 is divergent_to-infty and
A2: seq2 is bounded_above;
  let r be Real;
  consider M be real number such that
A3: for n holds seq2.n<M by A2,SEQ_2:def 3;
  consider n such that
A4: for m st n<=m holds seq1.m<r-M by A1,Def5;
  take n;
  let m;
  assume n<=m;
  then seq1.m<r-M by A4;
  then seq1.m+seq2.m<r-M+M by A3,XREAL_1:10;
  hence thesis by SEQ_1:11;
end;

theorem Th40:
  (seq is divergent_to+infty & r>0 implies r(#)seq is
  divergent_to+infty) & (seq is divergent_to+infty & r<0 implies r(#)seq is
  divergent_to-infty) & ( r=0 implies rng (r(#)seq)={0} & r(#) seq is constant)
proof
  thus seq is divergent_to+infty & r>0 implies r(#)seq is divergent_to+infty
  proof
    assume that
A1: seq is divergent_to+infty and
A2: r>0;
    let g be Real;
    consider n such that
A3: for m st n<=m holds g/r<seq.m by A1,Def4;
    take n;
    let m;
    assume n<=m;
    then g/r<seq.m by A3;
    then g/r*r<r*seq.m by A2,XREAL_1:70;
    then g<r*seq.m by A2,XCMPLX_1:88;
    hence thesis by SEQ_1:13;
  end;
  thus seq is divergent_to+infty & r<0 implies r(#)seq is divergent_to-infty
  proof
    assume that
A4: seq is divergent_to+infty and
A5: r<0;
    let g be Real;
    consider n such that
A6: for m st n<=m holds g/r<seq.m by A4,Def4;
    take n;
    let m;
    assume n<=m;
    then g/r<seq.m by A6;
    then r*seq.m<g/r*r by A5,XREAL_1:71;
    then r*seq.m<g by A5,XCMPLX_1:88;
    hence thesis by SEQ_1:13;
  end;
  assume
A7: r=0;
  thus rng(r(#)seq)={0}
  proof
    let x be real number;
    thus x in rng(r(#)seq) implies x in {0}
    proof
      assume x in rng(r(#)seq);
      then consider n such that
A8:   x=(r(#)seq).n by FUNCT_2:190;
      x=r*seq.n by A8,SEQ_1:13
        .=0 by A7;
      hence thesis by TARSKI:def 1;
    end;
    assume x in {0};
    then
A9: x=0 by TARSKI:def 1;
    (r(#)seq).0=r*seq.0 by SEQ_1:13
      .=0 by A7;
    hence thesis by A9,VALUED_0:28;
  end;
  hence thesis by FUNCT_2:188;
end;

theorem Th41:
  (seq is divergent_to-infty & r>0 implies r(#)seq is
  divergent_to-infty) & (seq is divergent_to-infty & r<0 implies r(#)seq is
  divergent_to+infty) & ( r=0 implies rng (r(#)seq)={0} & r(#) seq is constant)
proof
  thus seq is divergent_to-infty & r>0 implies r(#)seq is divergent_to-infty
  proof
    assume that
A1: seq is divergent_to-infty and
A2: r>0;
    let g be Real;
    consider n such that
A3: for m st n<=m holds seq.m<g/r by A1,Def5;
    take n;
    let m;
    assume n<=m;
    then seq.m<g/r by A3;
    then r*seq.m<g/r*r by A2,XREAL_1:70;
    then r*seq.m<g by A2,XCMPLX_1:88;
    hence thesis by SEQ_1:13;
  end;
  thus seq is divergent_to-infty & r<0 implies r(#)seq is divergent_to+infty
  proof
    assume that
A4: seq is divergent_to-infty and
A5: r<0;
    let g be Real;
    consider n such that
A6: for m st n<=m holds seq.m<g/r by A4,Def5;
    take n;
    let m;
    assume n<=m;
    then seq.m<g/r by A6;
    then g/r*r<r*seq.m by A5,XREAL_1:71;
    then g<r*seq.m by A5,XCMPLX_1:88;
    hence thesis by SEQ_1:13;
  end;
  assume
A7: r=0;
  thus rng(r(#)seq)={0}
  proof
    let x be real number;
    thus x in rng(r(#)seq) implies x in {0}
    proof
      assume x in rng(r(#)seq);
      then consider n such that
A8:   x=(r(#)seq).n by FUNCT_2:190;
      x=r*seq.n by A8,SEQ_1:13
        .=0 by A7;
      hence thesis by TARSKI:def 1;
    end;
    assume x in {0};
    then
A9: x=0 by TARSKI:def 1;
    (r(#)seq).0=r*seq.0 by SEQ_1:13
      .=0 by A7;
    hence thesis by A9,VALUED_0:28;
  end;
  hence thesis by FUNCT_2:188;
end;

theorem
  (seq is divergent_to+infty implies -seq is divergent_to-infty) & (seq
  is divergent_to-infty implies -seq is divergent_to+infty)
proof
A1: (-1)(#)seq=-seq;
  hence seq is divergent_to+infty implies -seq is divergent_to-infty by Th40;
  assume seq is divergent_to-infty;
  hence thesis by A1,Th41;
end;

theorem
  seq is bounded_below & seq1 is divergent_to-infty implies seq-seq1 is
  divergent_to+infty
proof
  assume that
A1: seq is bounded_below and
A2: seq1 is divergent_to-infty;
  (-1)(#)seq1 is divergent_to+infty by A2,Th41;
  hence thesis by A1,Th36;
end;

theorem
  seq is bounded_above & seq1 is divergent_to+infty implies seq-seq1 is
  divergent_to-infty
proof
  assume that
A1: seq is bounded_above and
A2: seq1 is divergent_to+infty;
  (-1)(#)seq1 is divergent_to-infty by A2,Th40;
  hence thesis by A1,Th39;
end;

theorem
  seq is divergent_to+infty & seq1 is convergent implies seq+seq1 is
  divergent_to+infty
proof
  assume that
A1: seq is divergent_to+infty and
A2: seq1 is convergent;
  seq1 is bounded by A2,SEQ_2:27;
  hence thesis by A1,Th36;
end;

theorem
  seq is divergent_to-infty & seq1 is convergent implies seq+seq1 is
  divergent_to-infty
proof
  assume that
A1: seq is divergent_to-infty and
A2: seq1 is convergent;
  seq1 is bounded by A2,SEQ_2:27;
  hence thesis by A1,Th39;
end;

theorem Th47:
  (for n holds seq.n=n) implies seq is divergent_to+infty
proof
  assume
A1: for n holds seq.n=n;
  let r be Real;
  consider n such that
A2: r<n by SEQ_4:10;
  take n;
  let m;
  assume n<=m;
  then r<m by A2,XXREAL_0:2;
  hence thesis by A1;
end;

set s1 = incl NAT;

Lm4: for n holds s1.n=n by FUNCT_1:35;

theorem Th48:
  (for n holds seq.n=-n) implies seq is divergent_to-infty
proof
  assume
A1: for n holds seq.n=-n;
A2: now
    let n;
    thus (-s1).n=-s1.n by SEQ_1:14
      .=-n by FUNCT_1:35
      .=seq.n by A1;
  end;
  s1 is divergent_to+infty by Lm4,Th47;
  then (-1)(#)s1 is divergent_to-infty by Th40;
  hence thesis by A2,FUNCT_2:113;
end;

theorem Th49:
  seq1 is divergent_to+infty & (ex r st r>0 & for n holds seq2.n>=
  r) implies seq1(#)seq2 is divergent_to+infty
proof
  assume that
A1: seq1 is divergent_to+infty and
A2: ex r st r>0 & for n holds seq2.n>=r;
  consider M be Real such that
A3: M>0 and
A4: for n holds seq2.n>=M by A2;
  let r be Real;
A5: 0<=abs(r) by COMPLEX1:132;
  consider n such that
A6: for m st n<=m holds abs(r)/M<seq1.m by A1,Def4;
  take n;
  let m;
  assume n<=m;
  then abs(r)/M<seq1.m by A6;
  then abs(r)/M*M<seq1.m*seq2.m by A3,A4,A5,XREAL_1:99;
  then
A7: abs(r)<seq1.m*seq2.m by A3,XCMPLX_1:88;
  r<=abs(r) by ABSVALUE:11;
  then r<seq1.m*seq2.m by A7,XXREAL_0:2;
  hence thesis by SEQ_1:12;
end;

theorem
  seq1 is divergent_to-infty & (ex r st 0<r & for n holds seq2.n>=r)
  implies seq1(#)seq2 is divergent_to-infty
proof
  assume that
A1: seq1 is divergent_to-infty and
A2: ex r st 0<r & for n holds seq2.n>=r;
  (-1)(#)seq1 is divergent_to+infty by A1,Th41;
  then
A3: (-1)(#)seq1(#)seq2 is divergent_to+infty by A2,Th49;
  (-1)(#)((-1)(#)seq1(#)seq2)=(-1)(#)((-1)(#)(seq1(#)seq2)) by SEQ_1:26
    .=(-1)*(-1)(#)(seq1(#)seq2) by SEQ_1:31
    .=seq1(#)seq2 by SEQ_1:35;
  hence thesis by A3,Th40;
end;

theorem Th51:
  seq1 is divergent_to-infty & seq2 is divergent_to-infty implies
  seq1(#)seq2 is divergent_to+infty
proof
  assume seq1 is divergent_to-infty & seq2 is divergent_to-infty;
  then
A1: (-1)(#)seq1 is divergent_to+infty & (-1)(#)seq2 is divergent_to+infty by
  Th41;
  (-1)(#)seq1(#)((-1)(#)seq2)=(-1)(#)(seq1(#)((-1)(#)seq2)) by SEQ_1:26
    .=(-1)(#)((-1)(#)(seq1(#)seq2)) by SEQ_1:27
    .=(-1)*(-1)(#)(seq1(#)seq2) by SEQ_1:31
    .=seq1(#)seq2 by SEQ_1:35;
  hence thesis by A1,Th37;
end;

theorem Th52:
  (seq is divergent_to+infty or seq is divergent_to-infty) implies
  abs(seq) is divergent_to+infty
proof
  assume
A1: seq is divergent_to+infty or seq is divergent_to-infty;
  let r be Real;
  now
    per cases by A1;
    suppose
      seq is divergent_to+infty;
      then consider n such that
A2:   for m st n<=m holds abs(r)<seq.m by Def4;
      take n;
      let m;
      assume n<=m;
      then r<=abs(r) & abs(r)<seq.m by A2,ABSVALUE:11;
      then
A3:   r<seq.m by XXREAL_0:2;
      seq.m<=abs(seq.m) by ABSVALUE:11;
      then seq.m<=abs(seq).m by SEQ_1:16;
      hence r<abs(seq).m by A3,XXREAL_0:2;
    end;
    suppose
      seq is divergent_to-infty;
      then consider n such that
A4:   for m st n<=m holds seq.m<-r by Def5;
      take n;
      let m;
      -abs(seq.m)<=seq.m by ABSVALUE:11;
      then
A5:   -abs(seq).m<=seq.m by SEQ_1:16;
      assume n<=m;
      then seq.m<-r by A4;
      then -abs(seq).m<-r by A5,XXREAL_0:2;
      hence r<abs(seq).m by XREAL_1:26;
    end;
  end;
  hence thesis;
end;

theorem Th53:
  seq is divergent_to+infty & seq1 is subsequence of seq implies
  seq1 is divergent_to+infty
proof
  assume that
A1: seq is divergent_to+infty and
A2: seq1 is subsequence of seq;
  consider Ns be increasing sequence of NAT such that
A3: seq1=seq*Ns by A2,VALUED_0:def 17;
  let r;
  consider n such that
A4: for m st n<=m holds r<seq.m by A1,Def4;
  take n;
  let m;
  assume
A5: n<=m;
  m<=Ns.m by SEQM_3:33;
  then n<=Ns.m by A5,XXREAL_0:2;
  then r<seq.(Ns.m) by A4;
  hence thesis by A3,FUNCT_2:21;
end;

theorem Th54:
  seq is divergent_to-infty & seq1 is subsequence of seq implies
  seq1 is divergent_to-infty
proof
  assume that
A1: seq is divergent_to-infty and
A2: seq1 is subsequence of seq;
  consider Ns be increasing sequence of NAT such that
A3: seq1=seq*Ns by A2,VALUED_0:def 17;
  let r;
  consider n such that
A4: for m st n<=m holds seq.m<r by A1,Def5;
  take n;
  let m;
  assume
A5: n<=m;
  m<=Ns.m by SEQM_3:33;
  then n<=Ns.m by A5,XXREAL_0:2;
  then seq.(Ns.m)<r by A4;
  hence thesis by A3,FUNCT_2:21;
end;

theorem
  seq1 is divergent_to+infty & seq2 is convergent & 0<lim seq2 implies
  seq1(#)seq2 is divergent_to+infty
proof
  assume that
A1: seq1 is divergent_to+infty and
A2: seq2 is convergent and
A3: 0<lim seq2;
  consider n1 be Element of NAT such that
A4: for m st n1<=m holds (lim seq2)/2<seq2.m by A2,A3,Th30;
  now
    thus 0<(lim seq2)/2 by A3,XREAL_1:217;
    let n;
    (lim seq2)/2<seq2.(n+n1) by A4,NAT_1:12;
    hence (lim seq2)/2<=(seq2^\n1).n by NAT_1:def 3;
  end;
  then (seq1^\n1)(#)(seq2^\n1) is divergent_to+infty by A1,Th49,Th53;
  then (seq1(#)seq2)^\n1 is divergent_to+infty by SEQM_3:42;
  hence thesis by Th34;
end;

theorem Th56:
  seq is non-decreasing & not seq is bounded_above implies seq is
  divergent_to+infty
proof
  assume that
A1: seq is non-decreasing and
A2: not seq is bounded_above;
  let r;
  consider n such that
A3: r+1<=seq.n by A2,SEQ_2:def 3;
  take n;
  let m;
  assume n<=m;
  then seq.n<=seq.m by A1,SEQM_3:12;
  then r+1<=seq.m by A3,XXREAL_0:2;
  hence thesis by Lm1;
end;

theorem Th57:
  seq is non-increasing & not seq is bounded_below implies seq is
  divergent_to-infty
proof
  assume that
A1: seq is non-increasing and
A2: not seq is bounded_below;
  let r;
  consider n such that
A3: seq.n<=r-1 by A2,SEQ_2:def 4;
  take n;
  let m;
  assume n<=m;
  then seq.m<=seq.n by A1,SEQM_3:14;
  then seq.m<=r-1 by A3,XXREAL_0:2;
  hence thesis by Lm1;
end;

theorem
  seq is increasing & not seq is bounded_above implies seq is
  divergent_to+infty by Th56;

theorem
  seq is decreasing & not seq is bounded_below implies seq is
  divergent_to-infty by Th57;

theorem
  seq is monotone implies seq is convergent or seq is divergent_to+infty
  or seq is divergent_to-infty
proof
  assume
A1: seq is monotone;
  now
    per cases by A1,SEQM_3:def 7;
    suppose
A2:   seq is non-decreasing;
      now
        per cases;
        suppose
          seq is bounded_above;
          hence thesis by A2;
        end;
        suppose
          not seq is bounded_above;
          hence thesis by A2,Th56;
        end;
      end;
      hence thesis;
    end;
    suppose
A3:   seq is non-increasing;
      now
        per cases;
        suppose
          seq is bounded_below;
          hence thesis by A3;
        end;
        suppose
          not seq is bounded_below;
          hence thesis by A3,Th57;
        end;
      end;
      hence thesis;
    end;
  end;
  hence thesis;
end;

theorem Th61:
  (seq is divergent_to+infty or seq is divergent_to-infty) implies
  seq" is convergent & lim(seq")=0
proof
  assume
A1: seq is divergent_to+infty or seq is divergent_to-infty;
  now
    per cases by A1;
    suppose
A2:   seq is divergent_to+infty;
A3:   now
        let r be real number such that
A4:     0<r;
        r" is Real by XREAL_0:def 1;
        then consider n such that
A5:     for m st n<=m holds r"<seq.m by A2,Def4;
        take n;
        let m;
        assume n<=m;
        then
A6:     r"<seq.m by A5;
        then 1/seq.m<1/r" by A4,XREAL_1:78;
        then
A7:     1/seq.m<r by XCMPLX_1:218;
A8:     1/seq.m=(seq.m)" & (seq.m)"=(seq").m by VALUED_1:10,XCMPLX_1:217;
        0<r" by A4;
        hence abs((seq").m-0)<r by A6,A7,A8,ABSVALUE:def 1;
      end;
      hence seq" is convergent by SEQ_2:def 6;
      hence thesis by A3,SEQ_2:def 7;
    end;
    suppose
A9:   seq is divergent_to-infty;
A10:  now
        let r be real number such that
A11:    0<r;
A12:    -(r")<-0 by A11,XREAL_1:26;
        -(r") is Real by XREAL_0:def 1;
        then consider n such that
A13:    for m st n<=m holds seq.m<-(r") by A9,Def5;
        take n;
        let m;
        assume
A14:    n<=m;
        then seq.m<-(r") by A13;
        then 1/(-(r"))<1/seq.m by A12,XREAL_1:101;
        then ((-1)*(r"))"<1/seq.m by XCMPLX_1:217;
        then
A15:    (-1)"*(r"")<1/seq.m by XCMPLX_1:205;
        seq.m<-0 by A11,A13,A14;
        then 1/seq.m<0/seq.m by XREAL_1:77;
        then abs(1/seq.m)=-(1/seq.m) by ABSVALUE:def 1;
        then -(1*r)<-abs(1/seq.m) by A15;
        then abs(1/seq.m)<r by XREAL_1:26;
        then abs((seq.m)")<r by XCMPLX_1:217;
        hence abs((seq").m-0)<r by VALUED_1:10;
      end;
      hence seq" is convergent by SEQ_2:def 6;
      hence thesis by A10,SEQ_2:def 7;
    end;
  end;
  hence thesis;
end;

theorem Th62:
  seq is convergent & lim seq=0 & (ex k st for n st k<=n holds 0<
  seq.n) implies seq" is divergent_to+infty
proof
  assume
A1: seq is convergent & lim seq=0;
  given k such that
A2: for n st k<=n holds 0<seq.n;
  let r;
  set l=abs(r)+1;
  0<=abs(r) by COMPLEX1:132;
  then consider o be Element of NAT such that
A3: for n st o<=n holds abs(seq.n-0)<l" by A1,SEQ_2:def 7;
  take m=max(k,o);
  let n;
  assume
A4: m<=n;
  k<=m by XXREAL_0:25;
  then k<=n by A4,XXREAL_0:2;
  then
A5: 0<seq.n by A2;
  o<=m by XXREAL_0:25;
  then o<=n by A4,XXREAL_0:2;
  then abs(seq.n-0)<l" by A3;
  then seq.n<l" by A5,ABSVALUE:def 1;
  then 1/(l")<1/seq.n by A5,XREAL_1:78;
  then
A6: l<1/seq.n by XCMPLX_1:218;
  r<=abs(r) by ABSVALUE:11;
  then r<l by Lm1;
  then r<1/seq.n by A6,XXREAL_0:2;
  then r<(seq.n)" by XCMPLX_1:217;
  hence thesis by VALUED_1:10;
end;

theorem Th63:
  seq is convergent & lim seq=0 & (ex k st for n st k<=n holds seq
  .n<0) implies seq" is divergent_to-infty
proof
  assume
A1: seq is convergent & lim seq=0;
  given k such that
A2: for n st k<=n holds seq.n<0;
  let r;
  set l=abs(r)+1;
  0<=abs(r) by COMPLEX1:132;
  then consider o be Element of NAT such that
A3: for n st o<=n holds abs(seq.n-0)<l" by A1,SEQ_2:def 7;
  take m=max(k,o);
  let n;
  assume
A4: m<=n;
  k<=m by XXREAL_0:25;
  then k<= n by A4,XXREAL_0:2;
  then
A5: seq.n<0 by A2;
  then
A6: 0<-(seq.n) by XREAL_1:60;
  o<=m by XXREAL_0:25;
  then o<=n by A4,XXREAL_0:2;
  then abs(seq.n-0)<l" by A3;
  then -(seq.n)<l" by A5,ABSVALUE:def 1;
  then 1/(l")<1/(-(seq.n)) by A6,XREAL_1:78;
  then l<1/(-(seq.n)) by XCMPLX_1:218;
  then l<(-(seq.n))" by XCMPLX_1:217;
  then l<-((seq.n)") by XCMPLX_1:224;
  then
A7: --((seq.n)")<-l by XREAL_1:26;
  -abs(r)<=r by ABSVALUE:11;
  then -abs(r)-1<r by Lm1;
  then (seq.n)"<r by A7,XXREAL_0:2;
  hence thesis by VALUED_1:10;
end;

theorem Th64:
  seq is non-zero & seq is convergent & lim seq=0 & seq is
  non-decreasing implies seq" is divergent_to-infty
proof
  assume that
A1: seq is non-zero and
A2: seq is convergent & lim seq=0 and
A3: seq is non-decreasing;
  for n holds 0<=n implies seq.n<0 by A1,A2,A3,Th27;
  hence thesis by A2,Th63;
end;

theorem Th65:
  seq is non-zero & seq is convergent & lim seq=0 & seq is
  non-increasing implies seq" is divergent_to+infty
proof
  assume that
A1: seq is non-zero and
A2: seq is convergent & lim seq=0 and
A3: seq is non-increasing;
  for n holds 0<=n implies 0<seq.n by A1,A2,A3,Th28;
  hence thesis by A2,Th62;
end;

theorem
  seq is non-zero & seq is convergent & lim seq=0 & seq is increasing
  implies seq" is divergent_to-infty by Th64;

theorem
  seq is non-zero & seq is convergent & lim seq=0 & seq is decreasing
  implies seq" is divergent_to+infty by Th65;

theorem
  seq1 is bounded & (seq2 is divergent_to+infty or seq2 is
  divergent_to-infty) implies seq1/"seq2 is convergent & lim(seq1/"seq2)=0
proof
  assume that
A1: seq1 is bounded and
A2: seq2 is divergent_to+infty or seq2 is divergent_to-infty;
  seq2" is convergent & lim(seq2")=0 by A2,Th61;
  hence thesis by A1,SEQ_2:39,40;
end;

theorem Th69:
  seq is divergent_to+infty & (for n holds seq.n<=seq1.n) implies
  seq1 is divergent_to+infty
proof
  assume that
A1: seq is divergent_to+infty and
A2: for n holds seq.n<= seq1.n;
  let r;
  consider n such that
A3: for m st n<=m holds r<seq.m by A1,Def4;
  take n;
  let m;
  assume n<=m;
  then
A4: r<seq.m by A3;
  seq.m<=seq1.m by A2;
  hence thesis by A4,XXREAL_0:2;
end;

theorem Th70:
  seq is divergent_to-infty & (for n holds seq1.n<=seq.n) implies
  seq1 is divergent_to-infty
proof
  assume that
A1: seq is divergent_to-infty and
A2: for n holds seq1.n<= seq.n;
  let r;
  consider n such that
A3: for m st n<=m holds seq.m<r by A1,Def5;
  take n;
  let m;
  assume n<=m;
  then
A4: seq.m<r by A3;
  seq1.m<=seq.m by A2;
  hence thesis by A4,XXREAL_0:2;
end;

definition
  let f;
  attr f is convergent_in+infty means
  :Def6:
  (for r ex g st r<g & g in dom f)
& ex g st for seq st seq is divergent_to+infty & rng seq c= dom f holds f/*seq
  is convergent & lim(f/*seq)=g;
  attr f is divergent_in+infty_to+infty means
  :Def7:
  (for r ex g st r<g & g in
  dom f) & for seq st seq is divergent_to+infty & rng seq c= dom f holds f/*seq
  is divergent_to+infty;
  attr f is divergent_in+infty_to-infty means
  :Def8:
  (for r ex g st r<g & g in
  dom f) & for seq st seq is divergent_to+infty & rng seq c= dom f holds f/*seq
  is divergent_to-infty;
  attr f is convergent_in-infty means
  :Def9:
  (for r ex g st g<r & g in dom f)
& ex g st for seq st seq is divergent_to-infty & rng seq c= dom f holds f/*seq
  is convergent & lim(f/*seq)=g;
  attr f is divergent_in-infty_to+infty means
  :Def10:
  (for r ex g st g<r & g
  in dom f) & for seq st seq is divergent_to-infty & rng seq c= dom f holds f/*
  seq is divergent_to+infty;
  attr f is divergent_in-infty_to-infty means
  :Def11:
  (for r ex g st g<r & g
  in dom f) & for seq st seq is divergent_to-infty & rng seq c= dom f holds f/*
  seq is divergent_to-infty;
end;

canceled 6;

theorem
  f is convergent_in+infty iff (for r ex g st r<g & g in dom f) & ex g
  st for g1 st 0<g1 ex r st for r1 st r<r1 & r1 in dom f holds abs(f.r1-g)<g1
proof
  thus f is convergent_in+infty implies (for r ex g st r<g & g in dom f) & ex
  g st for g1 st 0<g1 ex r st for r1 st r<r1 & r1 in dom f holds abs(f.r1-g)<g1
  proof
    assume
A1: f is convergent_in+infty;
    then consider g2 such that
A2: for seq st seq is divergent_to+infty & rng seq c=dom f holds f/*
    seq is convergent & lim(f/*seq)=g2 by Def6;
    assume not (for r ex g st r<g & g in dom f) or for g ex g1 st 0<g1 & for
    r ex r1 st r<r1 & r1 in dom f & abs(f.r1-g)>=g1;
    then consider g such that
A3: 0<g and
A4: for r ex r1 st r<r1 & r1 in dom f & abs(f.r1-g2)>=g by A1,Def6;
    defpred X[Element of NAT,real number] means $1<$2 & $2 in dom f & abs(f.$2
    -g2)>=g;
A5: for n ex r st X[n,r] by A4;
    consider s be Real_Sequence such that
A6: for n holds X[n,s.n] from FUNCT_2:sch 3(A5);
    now
      let x be set;
      assume x in rng s;
      then ex n st s.n=x by FUNCT_2:190;
      hence x in dom f by A6;
    end;
    then
A7: rng s c=dom f by TARSKI:def 3;
    now
      let n;
      n<s.n by A6;
      hence s1.n<=s.n by FUNCT_1:35;
    end;
    then s is divergent_to+infty by Lm4,Th47,Th69;
    then f/*s is convergent & lim(f/*s)=g2 by A2,A7;
    then consider n such that
A8: for m st n<=m holds abs((f/*s).m-g2)<g by A3,SEQ_2:def 7;
    abs((f/*s).n-g2)<g by A8;
    then abs(f.(s.n)-g2)<g by A7,FUNCT_2:185;
    hence contradiction by A6;
  end;
  assume
A9: for r ex g st r<g & g in dom f;
  given g such that
A10: for g1 st 0<g1 ex r st for r1 st r<r1 & r1 in dom f holds abs(f.r1- g)<g1;
  now
    let s be Real_Sequence such that
A11: s is divergent_to+infty and
A12: rng s c=dom f;
A13: now
      let g1 be real number;
      assume
A14:  0<g1;
      g1 is Real by XREAL_0:def 1;
      then consider r such that
A15:  for r1 st r<r1 & r1 in dom f holds abs(f.r1-g)<g1 by A10,
      A14;
      consider n such that
A16:  for m st n<=m holds r<s.m by A11,Def4;
      take n;
      let m;
A17:  s.m in rng s by VALUED_0:28;
      assume n<=m;
      then abs(f.(s.m)-g)<g1 by A12,A15,A16,A17;
      hence abs((f/*s).m-g)<g1 by A12,FUNCT_2:185;
    end;
    hence f/*s is convergent by SEQ_2:def 6;
    hence lim(f/*s)=g by A13,SEQ_2:def 7;
  end;
  hence thesis by A9,Def6;
end;

theorem
  f is convergent_in-infty iff (for r ex g st g<r & g in dom f) & ex g
  st for g1 st 0<g1 ex r st for r1 st r1<r & r1 in dom f holds abs(f.r1-g)<g1
proof
  thus f is convergent_in-infty implies (for r ex g st g<r & g in dom f) & ex
  g st for g1 st 0<g1 ex r st for r1 st r1<r & r1 in dom f holds abs(f.r1-g)<g1
  proof
    assume
A1: f is convergent_in-infty;
    then consider g2 such that
A2: for seq st seq is divergent_to-infty & rng seq c=dom f holds f/*
    seq is convergent & lim(f/*seq)=g2 by Def9;
    assume not (for r ex g st g<r & g in dom f) or for g ex g1 st 0<g1 & for
    r ex r1 st r1<r & r1 in dom f & abs(f.r1-g)>=g1;
    then consider g such that
A3: 0<g and
A4: for r ex r1 st r1<r & r1 in dom f & abs(f.r1-g2)>=g by A1,Def9;
    defpred X[Element of NAT,real number] means $2<-$1 & $2 in dom f & abs(f.
    $2-g2)>=g;
A5: for n ex r st X[n,r] by A4;
    consider s be Real_Sequence such that
A6: for n holds X[n,s.n] from FUNCT_2:sch 3(A5);
    now
      let x be set;
      assume x in rng s;
      then ex n st s.n=x by FUNCT_2:190;
      hence x in dom f by A6;
    end;
    then
A7: rng s c=dom f by TARSKI:def 3;
    deffunc U(Element of NAT) = -$1;
    consider s1 be Real_Sequence such that
A8: for n holds s1.n=U(n) from SEQ_1:sch 1;
    now
      let n;
      s.n<-n by A6;
      hence s.n<=s1.n by A8;
    end;
    then s is divergent_to-infty by A8,Th48,Th70;
    then f/*s is convergent & lim(f/*s)=g2 by A2,A7;
    then consider n such that
A9: for m st n<=m holds abs((f/*s).m-g2)<g by A3,SEQ_2:def 7;
    abs((f/*s).n-g2)<g by A9;
    then abs(f.(s.n)-g2)<g by A7,FUNCT_2:185;
    hence contradiction by A6;
  end;
  assume
A10: for r ex g st g<r & g in dom f;
  given g such that
A11: for g1 st 0<g1 ex r st for r1 st r1<r & r1 in dom f holds abs(f.r1- g)<g1;
  now
    let s be Real_Sequence such that
A12: s is divergent_to-infty and
A13: rng s c=dom f;
A14: now
      let g1 be real number;
      assume
A15:  0<g1;
      g1 is Real by XREAL_0:def 1;
      then consider r such that
A16:  for r1 st r1<r & r1 in dom f holds abs(f.r1-g)<g1 by A11,
      A15;
      consider n such that
A17:  for m st n<=m holds s.m<r by A12,Def5;
      take n;
      let m;
A18:  s.m in rng s by VALUED_0:28;
      assume n<=m;
      then abs(f.(s.m)-g)<g1 by A13,A16,A17,A18;
      hence abs((f/*s).m-g)<g1 by A13,FUNCT_2:185;
    end;
    hence f/*s is convergent by SEQ_2:def 6;
    hence lim(f/*s)=g by A14,SEQ_2:def 7;
  end;
  hence thesis by A10,Def9;
end;

theorem
  f is divergent_in+infty_to+infty iff (for r ex g st r<g & g in dom f)
  & for g ex r st for r1 st r<r1 & r1 in dom f holds g<f.r1
proof
  thus f is divergent_in+infty_to+infty implies (for r ex g st r<g & g in dom
  f) & for g ex r st for r1 st r<r1 & r1 in dom f holds g<f.r1
  proof
    assume
A1: f is divergent_in+infty_to+infty;
    assume not (for r ex g st r<g & g in dom f) or ex g st for r ex r1 st r<
    r1 & r1 in dom f & g>=f.r1;
    then consider g such that
A2: for r ex r1 st r<r1 & r1 in dom f & g>=f.r1 by A1,Def7;
    defpred X[Element of NAT,real number] means $1<$2 & $2 in dom f & g >= f.
    $2;
A3: for n ex r st X[n,r] by A2;
    consider s be Real_Sequence such that
A4: for n holds X[n,s.n] from FUNCT_2:sch 3(A3);
    now
      let x be set;
      assume x in rng s;
      then ex n st s.n=x by FUNCT_2:190;
      hence x in dom f by A4;
    end;
    then
A5: rng s c=dom f by TARSKI:def 3;
    now
      let n;
      n<s.n by A4;
      hence s1.n<=s.n by FUNCT_1:35;
    end;
    then s is divergent_to+infty by Lm4,Th47,Th69;
    then f/*s is divergent_to+infty by A1,A5,Def7;
    then consider n such that
A6: for m st n<=m holds g<(f/*s).m by Def4;
    g<(f/*s).n by A6;
    then g<f.(s.n) by A5,FUNCT_2:185;
    hence contradiction by A4;
  end;
  assume that
A7: for r ex g st r<g & g in dom f and
A8: for g ex r st for r1 st r<r1 & r1 in dom f holds g<f.r1;
  now
    let s be Real_Sequence such that
A9: s is divergent_to+infty and
A10: rng s c=dom f;
    now
      let g;
      consider r such that
A11:  for r1 st r<r1 & r1 in dom f holds g<f.r1 by A8;
      consider n such that
A12:  for m st n<=m holds r<s.m by A9,Def4;
      take n;
      let m;
A13:  s.m in rng s by VALUED_0:28;
      assume n<=m;
      then g<f.(s.m) by A10,A11,A12,A13;
      hence g<(f/*s).m by A10,FUNCT_2:185;
    end;
    hence f/*s is divergent_to+infty by Def4;
  end;
  hence thesis by A7,Def7;
end;

theorem
  f is divergent_in+infty_to-infty iff (for r ex g st r<g & g in dom f)
  & for g ex r st for r1 st r<r1 & r1 in dom f holds f.r1<g
proof
  thus f is divergent_in+infty_to-infty implies (for r ex g st r<g & g in dom
  f) & for g ex r st for r1 st r<r1 & r1 in dom f holds f.r1<g
  proof
    assume
A1: f is divergent_in+infty_to-infty;
    assume not (for r ex g st r<g & g in dom f) or ex g st for r ex r1 st r<
    r1 & r1 in dom f & f.r1>=g;
    then consider g such that
A2: for r ex r1 st r<r1 & r1 in dom f & f.r1>=g by A1,Def8;
    defpred X[Element of NAT,real number] means $1<$2 & $2 in dom f & g <= f.
    $2;
A3: for n ex r st X[n,r] by A2;
    consider s be Real_Sequence such that
A4: for n holds X[n,s.n] from FUNCT_2:sch 3(A3);
    now
      let x be set;
      assume x in rng s;
      then ex n st s.n=x by FUNCT_2:190;
      hence x in dom f by A4;
    end;
    then
A5: rng s c=dom f by TARSKI:def 3;
    now
      let n;
      n<s.n by A4;
      hence s1.n<=s.n by FUNCT_1:35;
    end;
    then s is divergent_to+infty by Lm4,Th47,Th69;
    then f/*s is divergent_to-infty by A1,A5,Def8;
    then consider n such that
A6: for m st n<=m holds (f/*s).m<g by Def5;
    (f/*s).n<g by A6;
    then f.(s.n)<g by A5,FUNCT_2:185;
    hence contradiction by A4;
  end;
  assume that
A7: for r ex g st r<g & g in dom f and
A8: for g ex r st for r1 st r<r1 & r1 in dom f holds f.r1<g;
  now
    let s be Real_Sequence such that
A9: s is divergent_to+infty and
A10: rng s c=dom f;
    now
      let g;
      consider r such that
A11:  for r1 st r<r1 & r1 in dom f holds f.r1<g by A8;
      consider n such that
A12:  for m st n<=m holds r<s.m by A9,Def4;
      take n;
      let m;
A13:  s.m in rng s by VALUED_0:28;
      assume n<=m;
      then f.(s.m)<g by A10,A11,A12,A13;
      hence (f/*s).m<g by A10,FUNCT_2:185;
    end;
    hence f/*s is divergent_to-infty by Def5;
  end;
  hence thesis by A7,Def8;
end;

theorem
  f is divergent_in-infty_to+infty iff (for r ex g st g<r & g in dom f)
  & for g ex r st for r1 st r1<r & r1 in dom f holds g<f.r1
proof
  thus f is divergent_in-infty_to+infty implies (for r ex g st g<r & g in dom
  f) & for g ex r st for r1 st r1<r & r1 in dom f holds g<f.r1
  proof
    deffunc U(Element of NAT) = -$1;
    assume
A1: f is divergent_in-infty_to+infty;
    assume not (for r ex g st g<r & g in dom f) or ex g st for r ex r1 st r1<
    r & r1 in dom f & g>=f.r1;
    then consider g such that
A2: for r ex r1 st r1<r & r1 in dom f & g>=f.r1 by A1,Def10;
    defpred X[Element of NAT,real number] means $2<-$1 & $2 in dom f & g >= f.
    $2;
A3: for n ex r st X[n,r] by A2;
    consider s be Real_Sequence such that
A4: for n holds X[n,s.n] from FUNCT_2:sch 3(A3);
    now
      let x be set;
      assume x in rng s;
      then ex n st s.n=x by FUNCT_2:190;
      hence x in dom f by A4;
    end;
    then
A5: rng s c=dom f by TARSKI:def 3;
    consider s1 be Real_Sequence such that
A6: for n holds s1.n=U(n) from SEQ_1:sch 1;
    now
      let n;
      s.n<-n by A4;
      hence s.n<=s1.n by A6;
    end;
    then s is divergent_to-infty by A6,Th48,Th70;
    then f/*s is divergent_to+infty by A1,A5,Def10;
    then consider n such that
A7: for m st n<=m holds g<(f/*s).m by Def4;
    g<(f/*s).n by A7;
    then g<f.(s.n) by A5,FUNCT_2:185;
    hence contradiction by A4;
  end;
  assume that
A8: for r ex g st g<r & g in dom f and
A9: for g ex r st for r1 st r1<r & r1 in dom f holds g<f.r1;
  now
    let s be Real_Sequence such that
A10: s is divergent_to-infty and
A11: rng s c=dom f;
    now
      let g;
      consider r such that
A12:  for r1 st r1<r & r1 in dom f holds g<f.r1 by A9;
      consider n such that
A13:  for m st n<=m holds s.m<r by A10,Def5;
      take n;
      let m;
A14:  s.m in rng s by VALUED_0:28;
      assume n<=m;
      then g<f.(s.m) by A11,A12,A13,A14;
      hence g<(f/*s).m by A11,FUNCT_2:185;
    end;
    hence f/*s is divergent_to+infty by Def4;
  end;
  hence thesis by A8,Def10;
end;

theorem
  f is divergent_in-infty_to-infty iff (for r ex g st g<r & g in dom f)
  & for g ex r st for r1 st r1<r & r1 in dom f holds f.r1<g
proof
  thus f is divergent_in-infty_to-infty implies (for r ex g st g<r & g in dom
  f) & for g ex r st for r1 st r1<r & r1 in dom f holds f.r1<g
  proof
    deffunc U(Element of NAT) = -$1;
    assume
A1: f is divergent_in-infty_to-infty;
    assume not (for r ex g st g<r & g in dom f) or ex g st for r ex r1 st r1<
    r & r1 in dom f & f.r1>=g;
    then consider g such that
A2: for r ex r1 st r1<r & r1 in dom f & f.r1>=g by A1,Def11;
    defpred X[Element of NAT,real number] means $2<-$1 & $2 in dom f & g <= f.
    $2;
A3: for n ex r st X[n,r] by A2;
    consider s be Real_Sequence such that
A4: for n holds X[n,s.n] from FUNCT_2:sch 3(A3);
    now
      let x be set;
      assume x in rng s;
      then ex n st s.n=x by FUNCT_2:190;
      hence x in dom f by A4;
    end;
    then
A5: rng s c=dom f by TARSKI:def 3;
    consider s1 be Real_Sequence such that
A6: for n holds s1.n=U(n) from SEQ_1:sch 1;
    now
      let n;
      s.n<-n by A4;
      hence s.n<=s1.n by A6;
    end;
    then s is divergent_to-infty by A6,Th48,Th70;
    then f/*s is divergent_to-infty by A1,A5,Def11;
    then consider n such that
A7: for m st n<=m holds (f/*s).m<g by Def5;
    (f/*s).n<g by A7;
    then f.(s.n)<g by A5,FUNCT_2:185;
    hence contradiction by A4;
  end;
  assume that
A8: for r ex g st g<r & g in dom f and
A9: for g ex r st for r1 st r1<r & r1 in dom f holds f.r1<g;
  now
    let s be Real_Sequence such that
A10: s is divergent_to-infty and
A11: rng s c=dom f;
    now
      let g;
      consider r such that
A12:  for r1 st r1<r & r1 in dom f holds f.r1<g by A9;
      consider n such that
A13:  for m st n<=m holds s.m<r by A10,Def5;
      take n;
      let m;
A14:  s.m in rng s by VALUED_0:28;
      assume n<=m;
      then f.(s.m)<g by A11,A12,A13,A14;
      hence (f/*s).m<g by A11,FUNCT_2:185;
    end;
    hence f/*s is divergent_to-infty by Def5;
  end;
  hence thesis by A8,Def11;
end;

theorem
  f1 is divergent_in+infty_to+infty & f2 is divergent_in+infty_to+infty
  & (for r ex g st r<g & g in dom f1 /\ dom f2) implies f1+f2 is
  divergent_in+infty_to+infty & f1(#)f2 is divergent_in+infty_to+infty
proof
  assume that
A1: f1 is divergent_in+infty_to+infty and
A2: f2 is divergent_in+infty_to+infty and
A3: for r ex g st r<g & g in dom f1/\dom f2;
A4: now
    let seq;
    assume that
A5: seq is divergent_to+infty and
A6: rng seq c=dom(f1+f2);
A7: dom(f1+f2)=dom f1/\dom f2 by A6,Lm2;
    rng seq c=dom f2 by A6,Lm2;
    then
A8: f2/*seq is divergent_to+infty by A2,A5,Def7;
    rng seq c=dom f1 by A6,Lm2;
    then f1/*seq is divergent_to+infty by A1,A5,Def7;
    then f1/*seq+f2/*seq is divergent_to+infty by A8,Th35;
    hence (f1+f2)/*seq is divergent_to+infty by A6,A7,RFUNCT_2:23;
  end;
A9: now
    let seq;
    assume that
A10: seq is divergent_to+infty and
A11: rng seq c=dom(f1(#) f2);
A12: dom(f1(#)f2)=dom f1/\ dom f2 by A11,Lm3;
    rng seq c=dom f2 by A11,Lm3;
    then
A13: f2/*seq is divergent_to+infty by A2,A10,Def7;
    rng seq c=dom f1 by A11,Lm3;
    then f1/*seq is divergent_to+infty by A1,A10,Def7;
    then (f1/*seq)(#)(f2/*seq) is divergent_to+infty by A13,Th37;
    hence (f1(#)f2)/*seq is divergent_to+infty by A11,A12,RFUNCT_2:23;
  end;
  now
    let r;
    consider g such that
A14: r<g & g in dom f1/\dom f2 by A3;
    take g;
    thus r<g & g in dom(f1+f2) by A14,VALUED_1:def 1;
  end;
  hence f1+f2 is divergent_in+infty_to+infty by A4,Def7;
  now
    let r;
    consider g such that
A15: r<g & g in dom f1/\dom f2 by A3;
    take g;
    thus r<g & g in dom(f1(#)f2) by A15,VALUED_1:def 4;
  end;
  hence thesis by A9,Def7;
end;

theorem
  f1 is divergent_in+infty_to-infty & f2 is divergent_in+infty_to-infty
  & (for r ex g st r<g & g in dom f1 /\ dom f2) implies f1+f2 is
  divergent_in+infty_to-infty & f1(#)f2 is divergent_in+infty_to+infty
proof
  assume that
A1: f1 is divergent_in+infty_to-infty and
A2: f2 is divergent_in+infty_to-infty and
A3: for r ex g st r<g & g in dom f1/\dom f2;
A4: now
    let seq;
    assume that
A5: seq is divergent_to+infty and
A6: rng seq c=dom(f1+f2);
A7: dom(f1+f2)=dom f1/\dom f2 by A6,Lm2;
    rng seq c=dom f2 by A6,Lm2;
    then
A8: f2/*seq is divergent_to-infty by A2,A5,Def8;
    rng seq c=dom f1 by A6,Lm2;
    then f1/*seq is divergent_to-infty by A1,A5,Def8;
    then f1/*seq+f2/*seq is divergent_to-infty by A8,Th38;
    hence (f1+f2)/*seq is divergent_to-infty by A6,A7,RFUNCT_2:23;
  end;
A9: now
    let seq;
    assume that
A10: seq is divergent_to+infty and
A11: rng seq c=dom(f1(#) f2);
A12: dom(f1(#)f2)=dom f1/\ dom f2 by A11,Lm3;
    rng seq c=dom f2 by A11,Lm3;
    then
A13: f2/*seq is divergent_to-infty by A2,A10,Def8;
    rng seq c=dom f1 by A11,Lm3;
    then f1/*seq is divergent_to-infty by A1,A10,Def8;
    then (f1/*seq)(#)(f2/*seq) is divergent_to+infty by A13,Th51;
    hence (f1(#)f2)/*seq is divergent_to+infty by A11,A12,RFUNCT_2:23;
  end;
  now
    let r;
    consider g such that
A14: r<g & g in dom f1/\dom f2 by A3;
    take g;
    thus r<g & g in dom(f1+f2) by A14,VALUED_1:def 1;
  end;
  hence f1+f2 is divergent_in+infty_to-infty by A4,Def8;
  now
    let r;
    consider g such that
A15: r<g & g in dom f1/\dom f2 by A3;
    take g;
    thus r<g & g in dom(f1(#)f2) by A15,VALUED_1:def 4;
  end;
  hence thesis by A9,Def7;
end;

theorem
  f1 is divergent_in-infty_to+infty & f2 is divergent_in-infty_to+infty
  & (for r ex g st g<r & g in dom f1 /\ dom f2) implies f1+f2 is
  divergent_in-infty_to+infty & f1(#)f2 is divergent_in-infty_to+infty
proof
  assume that
A1: f1 is divergent_in-infty_to+infty and
A2: f2 is divergent_in-infty_to+infty and
A3: for r ex g st g<r & g in dom f1/\dom f2;
A4: now
    let seq;
    assume that
A5: seq is divergent_to-infty and
A6: rng seq c=dom(f1+f2);
A7: dom(f1+f2)=dom f1/\dom f2 by A6,Lm2;
    rng seq c=dom f2 by A6,Lm2;
    then
A8: f2/*seq is divergent_to+infty by A2,A5,Def10;
    rng seq c=dom f1 by A6,Lm2;
    then f1/*seq is divergent_to+infty by A1,A5,Def10;
    then f1/*seq+f2/*seq is divergent_to+infty by A8,Th35;
    hence (f1+f2)/*seq is divergent_to+infty by A6,A7,RFUNCT_2:23;
  end;
A9: now
    let seq;
    assume that
A10: seq is divergent_to-infty and
A11: rng seq c=dom(f1(#) f2);
A12: dom(f1(#)f2)=dom f1/\ dom f2 by A11,Lm3;
    rng seq c=dom f2 by A11,Lm3;
    then
A13: f2/*seq is divergent_to+infty by A2,A10,Def10;
    rng seq c=dom f1 by A11,Lm3;
    then f1/*seq is divergent_to+infty by A1,A10,Def10;
    then (f1/*seq)(#)(f2/*seq) is divergent_to+infty by A13,Th37;
    hence (f1(#)f2)/*seq is divergent_to+infty by A11,A12,RFUNCT_2:23;
  end;
  now
    let r;
    consider g such that
A14: g<r & g in dom f1/\dom f2 by A3;
    take g;
    thus g<r & g in dom(f1+f2) by A14,VALUED_1:def 1;
  end;
  hence f1+f2 is divergent_in-infty_to+infty by A4,Def10;
  now
    let r;
    consider g such that
A15: g<r & g in dom f1/\dom f2 by A3;
    take g;
    thus g<r & g in dom(f1(#)f2) by A15,VALUED_1:def 4;
  end;
  hence thesis by A9,Def10;
end;

theorem
  f1 is divergent_in-infty_to-infty & f2 is divergent_in-infty_to-infty
  & (for r ex g st g<r & g in dom f1 /\ dom f2) implies f1+f2 is
  divergent_in-infty_to-infty & f1(#)f2 is divergent_in-infty_to+infty
proof
  assume that
A1: f1 is divergent_in-infty_to-infty and
A2: f2 is divergent_in-infty_to-infty and
A3: for r ex g st g<r & g in dom f1/\dom f2;
A4: now
    let seq;
    assume that
A5: seq is divergent_to-infty and
A6: rng seq c=dom(f1+f2);
A7: dom(f1+f2)=dom f1/\dom f2 by A6,Lm2;
    rng seq c=dom f2 by A6,Lm2;
    then
A8: f2/*seq is divergent_to-infty by A2,A5,Def11;
    rng seq c=dom f1 by A6,Lm2;
    then f1/*seq is divergent_to-infty by A1,A5,Def11;
    then f1/*seq+f2/*seq is divergent_to-infty by A8,Th38;
    hence (f1+f2)/*seq is divergent_to-infty by A6,A7,RFUNCT_2:23;
  end;
A9: now
    let seq;
    assume that
A10: seq is divergent_to-infty and
A11: rng seq c=dom(f1(#) f2);
A12: dom(f1(#)f2)=dom f1/\ dom f2 by A11,Lm3;
    rng seq c=dom f2 by A11,Lm3;
    then
A13: f2/*seq is divergent_to-infty by A2,A10,Def11;
    rng seq c=dom f1 by A11,Lm3;
    then f1/*seq is divergent_to-infty by A1,A10,Def11;
    then (f1/*seq)(#)(f2/*seq) is divergent_to+infty by A13,Th51;
    hence (f1(#)f2)/*seq is divergent_to+infty by A11,A12,RFUNCT_2:23;
  end;
  now
    let r;
    consider g such that
A14: g<r & g in dom f1/\dom f2 by A3;
    take g;
    thus g<r & g in dom(f1+f2) by A14,VALUED_1:def 1;
  end;
  hence f1+f2 is divergent_in-infty_to-infty by A4,Def11;
  now
    let r;
    consider g such that
A15: g<r & g in dom f1/\dom f2 by A3;
    take g;
    thus g<r & g in dom(f1(#)f2) by A15,VALUED_1:def 4;
  end;
  hence thesis by A9,Def10;
end;

theorem
  f1 is divergent_in+infty_to+infty & (for r ex g st r<g & g in dom(f1+
  f2)) & (ex r st f2|right_open_halfline r is bounded_below) implies f1+f2 is
  divergent_in+infty_to+infty
proof
  assume that
A1: f1 is divergent_in+infty_to+infty and
A2: for r ex g st r<g & g in dom(f1+f2);
  given r1 such that
A3: f2|right_open_halfline r1 is bounded_below;
  now
    let seq;
    assume that
A4: seq is divergent_to+infty and
A5: rng seq c=dom(f1+f2);
    consider k such that
A6: for n st k<=n holds r1<seq.n by A4,Def4;
A7: rng(seq^\k)c=rng seq by VALUED_0:21;
    dom(f1+f2)=dom f1/\dom f2 by A5,Lm2;
    then rng(seq^\k)c=dom f1/\dom f2 by A5,A7,XBOOLE_1:1;
    then
A8: f1/*(seq^\k)+f2/*(seq^\k)=(f1+f2)/*(seq^\k) by RFUNCT_2:23
      .=((f1+f2)/*seq)^\k by A5,VALUED_0:27;
    consider r2 be real number such that
A9: for g being set st g in right_open_halfline(r1)/\dom f2 holds r2
    <=f2.g by A3,RFUNCT_1:88;
A10: rng seq c=dom f2 by A5,Lm2;
    then
A11: rng(seq^\k)c=dom f2 by A7,XBOOLE_1:1;
    now
      let n;
      r1<seq.(n+k) by A6,NAT_1:12;
      then (seq^\k).n < +infty & r1<(seq^\k).n by NAT_1:def 3,XXREAL_0:9;
      then (seq^\k).n in rng(seq^\k) & (seq^\k).n in right_open_halfline(r1)
      by VALUED_0:28,XXREAL_1:4;
      then (seq^\k).n in right_open_halfline(r1)/\dom f2 by A11,XBOOLE_0:
      def 4;
      then r2<=f2.((seq^\k).n) by A9;
      then
A12:  r2<=(f2/*(seq^\k)).n by A10,A7,FUNCT_2:185,XBOOLE_1:1;
      -abs(r2)<=r2 by ABSVALUE:11;
      then -abs(r2)-1<r2-0 by XREAL_1:17;
      hence -abs(r2)-1<(f2/*(seq^\k)).n by A12,XXREAL_0:2;
    end;
    then
A13: f2/*(seq^\k) is bounded_below by SEQ_2:def 4;
    rng seq c=dom f1 by A5,Lm2;
    then
A14: rng(seq^\k)c=dom f1 by A7,XBOOLE_1:1;
    seq^\k is divergent_to+infty by A4,Th53;
    then f1/*(seq^\k) is divergent_to+infty by A1,A14,Def7;
    then f1/*(seq^\k)+f2/*(seq^\k) is divergent_to+infty by A13,Th36;
    hence (f1+f2)/*seq is divergent_to+infty by A8,Th34;
  end;
  hence thesis by A2,Def7;
end;

theorem
  f1 is divergent_in+infty_to+infty & (for r ex g st r<g & g in dom(f1
  (#)f2 ) ) & (ex r,r1 st 0<r & for g st g in dom f2 /\ right_open_halfline(r1)
  holds r<=f2.g) implies f1(#)f2 is divergent_in+infty_to+infty
proof
  assume that
A1: f1 is divergent_in+infty_to+infty and
A2: for r ex g st r<g & g in dom(f1(#)f2);
  given r2,r1 such that
A3: 0<r2 and
A4: for g st g in dom f2/\right_open_halfline(r1) holds r2<=f2.g;
  now
    let seq;
    assume that
A5: seq is divergent_to+infty and
A6: rng seq c=dom(f1(#) f2);
    consider k such that
A7: for n st k<=n holds r1<seq.n by A5,Def4;
A8: rng(seq^\k)c=rng seq by VALUED_0:21;
A9: rng seq c=dom f2 by A6,Lm3;
    then
A10: rng(seq^\k)c=dom f2 by A8,XBOOLE_1:1;
A11: now
      thus 0<r2 by A3;
      let n;
      r1<seq.(n+k) by A7,NAT_1:12;
      then r1<(seq^\k).n by NAT_1:def 3;
      then (seq^\k).n in {g2: r1<g2};
      then (seq^\k).n in rng(seq^\k) & (seq^\k).n in right_open_halfline(r1)
      by VALUED_0:28,XXREAL_1:230;
      then (seq^\k).n in dom f2/\right_open_halfline(r1) by A10,XBOOLE_0:
      def 4;
      then r2<=f2.((seq^\k).n) by A4;
      hence r2<=(f2/*(seq^\k)).n by A9,A8,FUNCT_2:185,XBOOLE_1:1;
    end;
    dom(f1(#)f2)=dom f1/\dom f2 by A6,Lm3;
    then rng(seq^\k)c=dom f1/\dom f2 by A6,A8,XBOOLE_1:1;
    then
A12: (f1/*(seq^\k))(#)(f2/*(seq^\k))=(f1(#)f2)/*(seq^\k) by RFUNCT_2:
    23
      .=((f1(#)f2)/*seq)^\k by A6,VALUED_0:27;
    rng seq c=dom f1 by A6,Lm3;
    then
A13: rng(seq^\k)c=dom f1 by A8,XBOOLE_1:1;
    seq^\k is divergent_to+infty by A5,Th53;
    then f1/*(seq^\k) is divergent_to+infty by A1,A13,Def7;
    then (f1/*(seq^\k))(#)(f2/*(seq^\k)) is divergent_to+infty by
    A11,Th49;
    hence (f1(#)f2)/*seq is divergent_to+infty by A12,Th34;
  end;
  hence thesis by A2,Def7;
end;

theorem
  f1 is divergent_in-infty_to+infty & (for r ex g st g<r & g in dom(f1+
  f2)) & (ex r st f2|left_open_halfline r is bounded_below) implies f1+f2 is
  divergent_in-infty_to+infty
proof
  assume that
A1: f1 is divergent_in-infty_to+infty and
A2: for r ex g st g<r & g in dom(f1+f2);
  given r1 such that
A3: f2|left_open_halfline r1 is bounded_below;
  now
    let seq;
    assume that
A4: seq is divergent_to-infty and
A5: rng seq c=dom(f1+f2);
    consider k such that
A6: for n st k<=n holds seq.n<r1 by A4,Def5;
A7: rng(seq^\k)c=rng seq by VALUED_0:21;
    dom(f1+f2)=dom f1/\dom f2 by A5,Lm2;
    then rng(seq^\k)c=dom f1/\dom f2 by A5,A7,XBOOLE_1:1;
    then
A8: f1/*(seq^\k)+f2/*(seq^\k)=(f1+f2)/*(seq^\k) by RFUNCT_2:23
      .=((f1+f2)/*seq)^\k by A5,VALUED_0:27;
    consider r2 be real number such that
A9: for g being set st g in left_open_halfline(r1)/\dom f2 holds r2<=
    f2.g by A3,RFUNCT_1:88;
A10: rng seq c=dom f2 by A5,Lm2;
    then
A11: rng(seq^\k)c=dom f2 by A7,XBOOLE_1:1;
    now
      let n;
      seq.(n+k)<r1 by A6,NAT_1:12;
      then (seq^\k).n<r1 by NAT_1:def 3;
      then (seq^\k).n in {g2: g2<r1};
      then
      (seq^\k).n in rng(seq^\k) & (seq^\k).n in left_open_halfline(r1) by
      VALUED_0:28,XXREAL_1:229;
      then (seq^\k).n in left_open_halfline(r1)/\dom f2 by A11,XBOOLE_0:
      def 4;
      then r2<=f2.((seq^\k).n) by A9;
      then
A12:  r2<=(f2/*(seq^\k)).n by A10,A7,FUNCT_2:185,XBOOLE_1:1;
      -abs(r2)<=r2 by ABSVALUE:11;
      then -abs(r2)-1<r2-0 by XREAL_1:17;
      hence -abs(r2)-1<(f2/*(seq^\k)).n by A12,XXREAL_0:2;
    end;
    then
A13: f2/*(seq^\k) is bounded_below by SEQ_2:def 4;
    rng seq c=dom f1 by A5,Lm2;
    then
A14: rng(seq^\k)c=dom f1 by A7,XBOOLE_1:1;
    seq^\k is divergent_to-infty by A4,Th54;
    then f1/*(seq^\k) is divergent_to+infty by A1,A14,Def10;
    then f1/*(seq^\k)+f2/*(seq^\k) is divergent_to+infty by A13,Th36;
    hence (f1+f2)/*seq is divergent_to+infty by A8,Th34;
  end;
  hence thesis by A2,Def10;
end;

theorem
  f1 is divergent_in-infty_to+infty & (for r ex g st g<r & g in dom(f1
  (#)f2 ) ) & (ex r,r1 st 0<r & for g st g in dom f2 /\ left_open_halfline(r1)
  holds r<=f2.g) implies f1(#)f2 is divergent_in-infty_to+infty
proof
  assume that
A1: f1 is divergent_in-infty_to+infty and
A2: for r ex g st g<r & g in dom(f1(#)f2);
  given r2,r1 such that
A3: 0<r2 and
A4: for g st g in dom f2/\left_open_halfline(r1) holds r2<=f2.g;
  now
    let seq;
    assume that
A5: seq is divergent_to-infty and
A6: rng seq c=dom(f1(#) f2);
    consider k such that
A7: for n st k<=n holds seq.n<r1 by A5,Def5;
A8: rng(seq^\k)c=rng seq by VALUED_0:21;
A9: rng seq c=dom f2 by A6,Lm3;
    then
A10: rng(seq^\k)c=dom f2 by A8,XBOOLE_1:1;
A11: now
      thus 0<r2 by A3;
      let n;
      seq.(n+k)<r1 by A7,NAT_1:12;
      then (seq^\k).n<r1 by NAT_1:def 3;
      then (seq^\k).n in {g2: g2<r1};
      then
      (seq^\k).n in rng(seq^\k) & (seq^\k).n in left_open_halfline(r1) by
      VALUED_0:28,XXREAL_1:229;
      then (seq^\k).n in dom f2/\left_open_halfline(r1) by A10,XBOOLE_0:
      def 4;
      then r2<=f2.((seq^\k).n) by A4;
      hence r2<=(f2/*(seq^\k)).n by A9,A8,FUNCT_2:185,XBOOLE_1:1;
    end;
    dom(f1(#)f2)=dom f1/\dom f2 by A6,Lm3;
    then rng(seq^\k)c=dom f1/\dom f2 by A6,A8,XBOOLE_1:1;
    then
A12: (f1/*(seq^\k))(#)(f2/*(seq^\k))=(f1(#)f2)/*(seq^\k) by RFUNCT_2:
    23
      .=((f1(#)f2)/*seq)^\k by A6,VALUED_0:27;
    rng seq c=dom f1 by A6,Lm3;
    then
A13: rng(seq^\k)c=dom f1 by A8,XBOOLE_1:1;
    seq^\k is divergent_to-infty by A5,Th54;
    then f1/*(seq^\k) is divergent_to+infty by A1,A13,Def10;
    then (f1/*(seq^\k))(#)(f2/*(seq^\k)) is divergent_to+infty by
    A11,Th49;
    hence (f1(#)f2)/*seq is divergent_to+infty by A12,Th34;
  end;
  hence thesis by A2,Def10;
end;

theorem
  (f is divergent_in+infty_to+infty & r>0 implies r(#)f is
divergent_in+infty_to+infty) & (f is divergent_in+infty_to+infty & r<0 implies
r(#)f is divergent_in+infty_to-infty) & (f is divergent_in+infty_to-infty & r>0
  implies r(#)f is divergent_in+infty_to-infty) & (f is
divergent_in+infty_to-infty & r<0 implies r(#)f is divergent_in+infty_to+infty)
proof
  thus f is divergent_in+infty_to+infty & r>0 implies r(#)f is
  divergent_in+infty_to+infty
  proof
    assume that
A1: f is divergent_in+infty_to+infty and
A2: r>0;
A3: now
      let seq;
      assume that
A4:   seq is divergent_to+infty and
A5:   rng seq c=dom(r(#)f);
A6:   rng seq c=dom f by A5,VALUED_1:def 5;
      then f/*seq is divergent_to+infty by A1,A4,Def7;
      then r(#)(f/*seq) is divergent_to+infty by A2,Th40;
      hence (r(#)f)/*seq is divergent_to+infty by A6,RFUNCT_2:24;
    end;
    now
      let r1;
      consider g such that
A7:   r1<g & g in dom f by A1,Def7;
      take g;
      thus r1<g & g in dom(r(#)f) by A7,VALUED_1:def 5;
    end;
    hence thesis by A3,Def7;
  end;
  thus f is divergent_in+infty_to+infty & r<0 implies r(#)f is
  divergent_in+infty_to-infty
  proof
    assume that
A8: f is divergent_in+infty_to+infty and
A9: r<0;
A10: now
      let seq;
      assume that
A11:  seq is divergent_to+infty and
A12:  rng seq c=dom(r(#)f);
A13:  rng seq c=dom f by A12,VALUED_1:def 5;
      then f/*seq is divergent_to+infty by A8,A11,Def7;
      then r(#)(f/*seq) is divergent_to-infty by A9,Th40;
      hence (r(#)f)/*seq is divergent_to-infty by A13,RFUNCT_2:24;
    end;
    now
      let r1;
      consider g such that
A14:  r1<g & g in dom f by A8,Def7;
      take g;
      thus r1<g & g in dom(r(#)f) by A14,VALUED_1:def 5;
    end;
    hence thesis by A10,Def8;
  end;
  thus f is divergent_in+infty_to-infty & r>0 implies r(#)f is
  divergent_in+infty_to-infty
  proof
    assume that
A15: f is divergent_in+infty_to-infty and
A16: r>0;
A17: now
      let seq;
      assume that
A18:  seq is divergent_to+infty and
A19:  rng seq c=dom(r(#)f);
A20:  rng seq c=dom f by A19,VALUED_1:def 5;
      then f/*seq is divergent_to-infty by A15,A18,Def8;
      then r(#)(f/*seq) is divergent_to-infty by A16,Th41;
      hence (r(#)f)/*seq is divergent_to-infty by A20,RFUNCT_2:24;
    end;
    now
      let r1;
      consider g such that
A21:  r1<g & g in dom f by A15,Def8;
      take g;
      thus r1<g & g in dom(r(#)f) by A21,VALUED_1:def 5;
    end;
    hence thesis by A17,Def8;
  end;
  assume that
A22: f is divergent_in+infty_to-infty and
A23: r<0;
A24: now
    let seq;
    assume that
A25: seq is divergent_to+infty and
A26: rng seq c=dom(r(#)f);
A27: rng seq c=dom f by A26,VALUED_1:def 5;
    then f/*seq is divergent_to-infty by A22,A25,Def8;
    then r(#)(f/*seq) is divergent_to+infty by A23,Th41;
    hence (r(#)f)/*seq is divergent_to+infty by A27,RFUNCT_2:24;
  end;
  now
    let r1;
    consider g such that
A28: r1<g & g in dom f by A22,Def8;
    take g;
    thus r1<g & g in dom(r(#)f) by A28,VALUED_1:def 5;
  end;
  hence thesis by A24,Def7;
end;

theorem
  (f is divergent_in-infty_to+infty & r>0 implies r(#)f is
divergent_in-infty_to+infty) & (f is divergent_in-infty_to+infty & r<0 implies
r(#)f is divergent_in-infty_to-infty) & (f is divergent_in-infty_to-infty & r>0
  implies r(#)f is divergent_in-infty_to-infty) & (f is
divergent_in-infty_to-infty & r<0 implies r(#)f is divergent_in-infty_to+infty)
proof
  thus f is divergent_in-infty_to+infty & r>0 implies r(#)f is
  divergent_in-infty_to+infty
  proof
    assume that
A1: f is divergent_in-infty_to+infty and
A2: r>0;
A3: now
      let seq;
      assume that
A4:   seq is divergent_to-infty and
A5:   rng seq c=dom(r(#)f);
A6:   rng seq c=dom f by A5,VALUED_1:def 5;
      then f/*seq is divergent_to+infty by A1,A4,Def10;
      then r(#)(f/*seq) is divergent_to+infty by A2,Th40;
      hence (r(#)f)/*seq is divergent_to+infty by A6,RFUNCT_2:24;
    end;
    now
      let r1;
      consider g such that
A7:   g<r1 & g in dom f by A1,Def10;
      take g;
      thus g<r1 & g in dom(r(#)f) by A7,VALUED_1:def 5;
    end;
    hence thesis by A3,Def10;
  end;
  thus f is divergent_in-infty_to+infty & r<0 implies r(#)f is
  divergent_in-infty_to-infty
  proof
    assume that
A8: f is divergent_in-infty_to+infty and
A9: r<0;
A10: now
      let seq;
      assume that
A11:  seq is divergent_to-infty and
A12:  rng seq c=dom(r(#)f);
A13:  rng seq c=dom f by A12,VALUED_1:def 5;
      then f/*seq is divergent_to+infty by A8,A11,Def10;
      then r(#)(f/*seq) is divergent_to-infty by A9,Th40;
      hence (r(#)f)/*seq is divergent_to-infty by A13,RFUNCT_2:24;
    end;
    now
      let r1;
      consider g such that
A14:  g<r1 & g in dom f by A8,Def10;
      take g;
      thus g<r1 & g in dom(r(#)f) by A14,VALUED_1:def 5;
    end;
    hence thesis by A10,Def11;
  end;
  thus f is divergent_in-infty_to-infty & r>0 implies r(#)f is
  divergent_in-infty_to-infty
  proof
    assume that
A15: f is divergent_in-infty_to-infty and
A16: r>0;
A17: now
      let seq;
      assume that
A18:  seq is divergent_to-infty and
A19:  rng seq c=dom(r(#)f);
A20:  rng seq c=dom f by A19,VALUED_1:def 5;
      then f/*seq is divergent_to-infty by A15,A18,Def11;
      then r(#)(f/*seq) is divergent_to-infty by A16,Th41;
      hence (r(#)f)/*seq is divergent_to-infty by A20,RFUNCT_2:24;
    end;
    now
      let r1;
      consider g such that
A21:  g<r1 & g in dom f by A15,Def11;
      take g;
      thus g<r1 & g in dom(r(#)f) by A21,VALUED_1:def 5;
    end;
    hence thesis by A17,Def11;
  end;
  assume that
A22: f is divergent_in-infty_to-infty and
A23: r<0;
A24: now
    let seq;
    assume that
A25: seq is divergent_to-infty and
A26: rng seq c=dom(r(#)f);
A27: rng seq c=dom f by A26,VALUED_1:def 5;
    then f/*seq is divergent_to-infty by A22,A25,Def11;
    then r(#)(f/*seq) is divergent_to+infty by A23,Th41;
    hence (r(#)f)/*seq is divergent_to+infty by A27,RFUNCT_2:24;
  end;
  now
    let r1;
    consider g such that
A28: g<r1 & g in dom f by A22,Def11;
    take g;
    thus g<r1 & g in dom(r(#)f) by A28,VALUED_1:def 5;
  end;
  hence thesis by A24,Def10;
end;

theorem
  (f is divergent_in+infty_to+infty or f is divergent_in+infty_to-infty)
  implies abs(f) is divergent_in+infty_to+infty
proof
  assume
A1: f is divergent_in+infty_to+infty or f is divergent_in+infty_to-infty;
  now
    per cases by A1;
    suppose
A2:   f is divergent_in+infty_to+infty;
A3:   now
        let seq;
        assume that
A4:     seq is divergent_to+infty and
A5:     rng seq c=dom abs(f);
A6:     rng seq c=dom f by A5,VALUED_1:def 11;
        then f/*seq is divergent_to+infty by A2,A4,Def7;
        then abs(f/*seq) is divergent_to+infty by Th52;
        hence (abs f)/*seq is divergent_to+infty by A6,RFUNCT_2:25;
      end;
      now
        let r;
        consider g such that
A7:     r<g & g in dom f by A2,Def7;
        take g;
        thus r<g & g in dom abs(f) by A7,VALUED_1:def 11;
      end;
      hence thesis by A3,Def7;
    end;
    suppose
A8:   f is divergent_in+infty_to-infty;
A9:   now
        let seq;
        assume that
A10:    seq is divergent_to+infty and
A11:    rng seq c=dom abs( f );
A12:    rng seq c=dom f by A11,VALUED_1:def 11;
        then f/*seq is divergent_to-infty by A8,A10,Def8;
        then abs(f/*seq) is divergent_to+infty by Th52;
        hence (abs f)/*seq is divergent_to+infty by A12,RFUNCT_2:25;
      end;
      now
        let r;
        consider g such that
A13:    r<g & g in dom f by A8,Def8;
        take g;
        thus r<g & g in dom abs(f) by A13,VALUED_1:def 11;
      end;
      hence thesis by A9,Def7;
    end;
  end;
  hence thesis;
end;

theorem
  (f is divergent_in-infty_to+infty or f is divergent_in-infty_to-infty)
  implies abs(f) is divergent_in-infty_to+infty
proof
  assume
A1: f is divergent_in-infty_to+infty or f is divergent_in-infty_to-infty;
  now
    per cases by A1;
    suppose
A2:   f is divergent_in-infty_to+infty;
A3:   now
        let seq;
        assume that
A4:     seq is divergent_to-infty and
A5:     rng seq c=dom abs(f);
A6:     rng seq c=dom f by A5,VALUED_1:def 11;
        then f/*seq is divergent_to+infty by A2,A4,Def10;
        then abs(f/*seq) is divergent_to+infty by Th52;
        hence (abs f)/*seq is divergent_to+infty by A6,RFUNCT_2:25;
      end;
      now
        let r;
        consider g such that
A7:     g<r & g in dom f by A2,Def10;
        take g;
        thus g<r & g in dom abs(f) by A7,VALUED_1:def 11;
      end;
      hence thesis by A3,Def10;
    end;
    suppose
A8:   f is divergent_in-infty_to-infty;
A9:   now
        let seq;
        assume that
A10:    seq is divergent_to-infty and
A11:    rng seq c=dom abs( f );
A12:    rng seq c=dom f by A11,VALUED_1:def 11;
        then f/*seq is divergent_to-infty by A8,A10,Def11;
        then abs(f/*seq) is divergent_to+infty by Th52;
        hence (abs f)/*seq is divergent_to+infty by A12,RFUNCT_2:25;
      end;
      now
        let r;
        consider g such that
A13:    g<r & g in dom f by A8,Def11;
        take g;
        thus g<r & g in dom abs(f) by A13,VALUED_1:def 11;
      end;
      hence thesis by A9,Def10;
    end;
  end;
  hence thesis;
end;

theorem Th95:
  (ex r st f|right_open_halfline r is non-decreasing & not f|
  right_open_halfline r is bounded_above) & (for r ex g st r<g & g in dom f)
  implies f is divergent_in+infty_to+infty
proof
  given r1 such that
A1: f|right_open_halfline r1 is non-decreasing and
A2: not f|right_open_halfline r1 is bounded_above;
A3: now
    let seq such that
A4: seq is divergent_to+infty and
A5: rng seq c=dom f;
    now
      let r;
      consider g1 being set such that
A6:   g1 in right_open_halfline(r1)/\dom f and
A7:   r<f.g1 by A2,RFUNCT_1:87;
      reconsider g1 as Real by A6;
      consider n such that
A8:   for m st n<=m holds abs(g1)+abs(r1)<seq.m by A4,Def4;
      take n;
      let m;
      assume n<=m;
      then
A9:   abs(g1)+abs(r1)<seq.m by A8;
      r1<=abs(r1) & 0<=abs(g1) by ABSVALUE:11,COMPLEX1:132;
      then 0+r1<=abs(g1)+abs(r1) by XREAL_1:9;
      then r1<seq.m by A9,XXREAL_0:2;
      then seq.m in {g2: r1<g2};
      then seq.m in rng seq & seq.m in right_open_halfline(r1) by
      VALUED_0
      :28 ,XXREAL_1:230;
      then
A10:  seq.m in right_open_halfline(r1)/\dom f by A5,XBOOLE_0:def 4;
      g1<=abs(g1) & 0<=abs(r1) by ABSVALUE:11,COMPLEX1:132;
      then g1+0<=abs(g1)+abs(r1) by XREAL_1:9;
      then g1<seq.m by A9,XXREAL_0:2;
      then f.g1<=f.(seq.m) by A1,A6,A10,RFUNCT_2:45;
      then r<f.(seq.m) by A7,XXREAL_0:2;
      hence r<(f/*seq).m by A5,FUNCT_2:185;
    end;
    hence f/*seq is divergent_to+infty by Def4;
  end;
  assume for r ex g st r<g & g in dom f;
  hence thesis by A3,Def7;
end;

theorem
  (ex r st f|right_open_halfline r is increasing & not f|
  right_open_halfline r is bounded_above) & (for r ex g st r<g & g in dom f)
  implies f is divergent_in+infty_to+infty by Th95;

theorem Th97:
  (ex r st f|right_open_halfline r is non-increasing & not f|
  right_open_halfline r is bounded_below) & (for r ex g st r<g & g in dom f)
  implies f is divergent_in+infty_to-infty
proof
  given r1 such that
A1: f|right_open_halfline r1 is non-increasing and
A2: not f|right_open_halfline r1 is bounded_below;
A3: now
    let seq such that
A4: seq is divergent_to+infty and
A5: rng seq c=dom f;
    now
      let r;
      consider g1 being set such that
A6:   g1 in right_open_halfline(r1)/\dom f and
A7:   f.g1<r by A2,RFUNCT_1:88;
      reconsider g1 as Real by A6;
      consider n such that
A8:   for m st n<=m holds abs(g1)+abs(r1)<seq.m by A4,Def4;
      take n;
      let m;
      assume n<=m;
      then
A9:   abs(g1)+abs(r1)<seq.m by A8;
      r1<=abs(r1) & 0<=abs(g1) by ABSVALUE:11,COMPLEX1:132;
      then 0+r1<=abs(g1)+abs(r1) by XREAL_1:9;
      then r1<seq.m by A9,XXREAL_0:2;
      then seq.m in {g2: r1<g2};
      then seq.m in rng seq & seq.m in right_open_halfline(r1) by
      VALUED_0
      :28 ,XXREAL_1:230;
      then
A10:  seq.m in right_open_halfline(r1)/\dom f by A5,XBOOLE_0:def 4;
      g1<=abs(g1) & 0<=abs(r1) by ABSVALUE:11,COMPLEX1:132;
      then g1+0<=abs(g1)+abs(r1) by XREAL_1:9;
      then g1<seq.m by A9,XXREAL_0:2;
      then f.(seq.m)<=f.g1 by A1,A6,A10,RFUNCT_2:46;
      then f.(seq.m)<r by A7,XXREAL_0:2;
      hence (f/*seq).m<r by A5,FUNCT_2:185;
    end;
    hence f/*seq is divergent_to-infty by Def5;
  end;
  assume for r ex g st r<g & g in dom f;
  hence thesis by A3,Def8;
end;

theorem
  (ex r st f|right_open_halfline r is decreasing & not f|
  right_open_halfline r is bounded_below) & (for r ex g st r<g & g in dom f)
  implies f is divergent_in+infty_to-infty by Th97;

theorem Th99:
  (ex r st f|left_open_halfline r is non-increasing & not f|
  left_open_halfline r is bounded_above) & (for r ex g st g<r & g in dom f)
  implies f is divergent_in-infty_to+infty
proof
  given r1 such that
A1: f|left_open_halfline r1 is non-increasing and
A2: not f|left_open_halfline r1 is bounded_above;
A3: now
    let seq such that
A4: seq is divergent_to-infty and
A5: rng seq c=dom f;
    now
      let r;
      consider g1 being set such that
A6:   g1 in left_open_halfline(r1)/\dom f and
A7:   r<f.g1 by A2,RFUNCT_1:87;
      reconsider g1 as Real by A6;
      consider n such that
A8:   for m st n<=m holds seq.m<-abs(r1)-abs(g1) by A4,Def5;
      take n;
      let m;
      assume n<=m;
      then
A9:   seq.m<-abs(r1)-abs(g1) by A8;
      -abs(r1)<=r1 & 0<=abs(g1) by ABSVALUE:11,COMPLEX1:132;
      then -abs(r1)-abs(g1)<=r1-0 by XREAL_1:15;
      then seq.m<r1 by A9,XXREAL_0:2;
      then seq.m in {g2: g2<r1};
      then seq.m in rng seq & seq.m in left_open_halfline(r1) by
      VALUED_0:
      28 ,XXREAL_1:229;
      then
A10:  seq.m in left_open_halfline(r1)/\dom f by A5,XBOOLE_0:def 4;
      -abs(g1)<=g1 & 0<=abs(r1) by ABSVALUE:11,COMPLEX1:132;
      then -abs(g1)-abs(r1)<=g1-0 by XREAL_1:15;
      then seq.m<g1 by A9,XXREAL_0:2;
      then f.g1<= f.(seq.m) by A1,A6,A10,RFUNCT_2:46;
      then r<f.(seq.m) by A7,XXREAL_0:2;
      hence r<(f/*seq).m by A5,FUNCT_2:185;
    end;
    hence f/*seq is divergent_to+infty by Def4;
  end;
  assume for r ex g st g<r & g in dom f;
  hence thesis by A3,Def10;
end;

theorem
  (ex r st f|left_open_halfline r is decreasing & not f|
  left_open_halfline r is bounded_above) & (for r ex g st g<r & g in dom f)
  implies f is divergent_in-infty_to+infty by Th99;

theorem Th101:
  (ex r st f|left_open_halfline r is non-decreasing & not f|
  left_open_halfline r is bounded_below) & (for r ex g st g<r & g in dom f)
  implies f is divergent_in-infty_to-infty
proof
  given r1 such that
A1: f|left_open_halfline r1 is non-decreasing and
A2: not f|left_open_halfline r1 is bounded_below;
A3: now
    let seq such that
A4: seq is divergent_to-infty and
A5: rng seq c=dom f;
    now
      let r;
      consider g1 being set such that
A6:   g1 in left_open_halfline(r1)/\dom f and
A7:   f.g1<r by A2,RFUNCT_1:88;
      reconsider g1 as Real by A6;
      consider n such that
A8:   for m st n<=m holds seq.m<-abs(r1)-abs(g1) by A4,Def5;
      take n;
      let m;
      assume n<=m;
      then
A9:   seq.m<-abs(r1)-abs(g1) by A8;
      -abs(r1)<=r1 & 0<=abs(g1) by ABSVALUE:11,COMPLEX1:132;
      then -abs(r1)-abs(g1)<=r1-0 by XREAL_1:15;
      then seq.m<r1 by A9,XXREAL_0:2;
      then seq.m in {g2: g2<r1};
      then seq.m in rng seq & seq.m in left_open_halfline(r1) by
      VALUED_0:
      28 ,XXREAL_1:229;
      then
A10:  seq.m in left_open_halfline(r1)/\dom f by A5,XBOOLE_0:def 4;
      -abs(g1)<=g1 & 0<=abs(r1) by ABSVALUE:11,COMPLEX1:132;
      then -abs(g1)-abs(r1)<=g1-0 by XREAL_1:15;
      then seq.m<g1 by A9,XXREAL_0:2;
      then f.(seq.m)<= f.g1 by A1,A6,A10,RFUNCT_2:45;
      then f.(seq.m)<r by A7,XXREAL_0:2;
      hence (f/*seq).m<r by A5,FUNCT_2:185;
    end;
    hence f/*seq is divergent_to-infty by Def5;
  end;
  assume for r ex g st g<r & g in dom f;
  hence thesis by A3,Def11;
end;

theorem
  (ex r st f|left_open_halfline r is increasing & not f|
  left_open_halfline r is bounded_below) & (for r ex g st g<r & g in dom f)
  implies f is divergent_in-infty_to-infty by Th101;

theorem Th103:
  f1 is divergent_in+infty_to+infty & (for r ex g st r<g & g in
  dom f) & (ex r st dom f /\ right_open_halfline(r) c= dom f1 /\
right_open_halfline(r) & for g st g in dom f /\ right_open_halfline(r) holds f1
  .g<=f.g) implies f is divergent_in+infty_to+infty
proof
  assume that
A1: f1 is divergent_in+infty_to+infty and
A2: for r ex g st r<g & g in dom f;
  given r1 such that
A3: dom f/\right_open_halfline(r1)c= dom f1/\right_open_halfline(r1) and
A4: for g st g in dom f/\right_open_halfline(r1) holds f1.g<=f.g;
  now
    let seq;
    assume that
A5: seq is divergent_to+infty and
A6: rng seq c=dom f;
    consider k such that
A7: for n st k<=n holds r1<seq.n by A5,Def4;
    now
      let x be set;
      assume x in rng(seq^\k);
      then consider n such that
A8:   (seq^\k).n=x by FUNCT_2:190;
      r1<seq.(n+k) by A7,NAT_1:12;
      then r1<(seq^\k).n by NAT_1:def 3;
      then x in {g2: r1<g2} by A8;
      hence x in right_open_halfline(r1) by XXREAL_1:230;
    end;
    then
A9: rng(seq^\k)c=right_open_halfline(r1) by TARSKI:def 3;
A10: rng(seq^\k)c=rng seq by VALUED_0:21;
    then rng(seq^\k)c=dom f by A6,XBOOLE_1:1;
    then
A11: rng(seq^\k)c=dom f/\right_open_halfline(r1) by A9,XBOOLE_1:19;
    then
A12: rng(seq^\k)c=dom f1/\right_open_halfline(r1) by A3,XBOOLE_1:1;
A13: dom f1/\right_open_halfline(r1)c=dom f1 by XBOOLE_1:17;
A14: now
      let n;
      (seq^\k).n in rng(seq^\k) by VALUED_0:28;
      then f1.((seq^\k).n)<=f.((seq^\k).n) by A4,A11;
      then (f1/*(seq^\k)).n<=f.((seq^\k).n) by A12,A13,FUNCT_2:185 ,
      XBOOLE_1:1;
      hence (f1/*(seq^\k)).n<=(f/*(seq^\k)).n by A6,A10,FUNCT_2:185 ,
      XBOOLE_1:1;
    end;
A15: seq^\k is divergent_to+infty by A5,Th53;
    rng(seq^\k)c=dom f1 by A12,A13,XBOOLE_1:1;
    then f1/*(seq^\k) is divergent_to+infty by A1,A15,Def7;
    then
A16: f/*(seq^\k) is divergent_to+infty by A14,Th69;
    f/*(seq^\k)=(f/*seq)^\k by A6,VALUED_0:27;
    hence f/*seq is divergent_to+infty by A16,Th34;
  end;
  hence thesis by A2,Def7;
end;

theorem Th104:
  f1 is divergent_in+infty_to-infty & (for r ex g st r<g & g in
  dom f) & (ex r st dom f/\right_open_halfline(r) c= dom f1 /\
right_open_halfline(r) & for g st g in dom f /\ right_open_halfline(r) holds f.
  g<=f1.g) implies f is divergent_in+infty_to-infty
proof
  assume that
A1: f1 is divergent_in+infty_to-infty and
A2: for r ex g st r<g & g in dom f;
  given r1 such that
A3: dom f/\right_open_halfline(r1)c= dom f1/\right_open_halfline(r1) and
A4: for g st g in dom f/\right_open_halfline(r1) holds f.g<=f1.g;
  now
    let seq;
    assume that
A5: seq is divergent_to+infty and
A6: rng seq c=dom f;
    consider k such that
A7: for n st k<=n holds r1<seq.n by A5,Def4;
    now
      let x be set;
      assume x in rng(seq^\k);
      then consider n such that
A8:   (seq^\k).n=x by FUNCT_2:190;
      r1<seq.(n+k) by A7,NAT_1:12;
      then r1<(seq^\k).n by NAT_1:def 3;
      then x in {g2: r1<g2} by A8;
      hence x in right_open_halfline(r1) by XXREAL_1:230;
    end;
    then
A9: rng(seq^\k)c=right_open_halfline(r1) by TARSKI:def 3;
A10: rng(seq^\k)c=rng seq by VALUED_0:21;
    then rng(seq^\k)c=dom f by A6,XBOOLE_1:1;
    then
A11: rng(seq^\k)c=dom f/\right_open_halfline(r1) by A9,XBOOLE_1:19;
    then
A12: rng(seq^\k)c=dom f1/\right_open_halfline(r1) by A3,XBOOLE_1:1;
A13: dom f1/\right_open_halfline(r1)c=dom f1 by XBOOLE_1:17;
A14: now
      let n;
      (seq^\k).n in rng(seq^\k) by VALUED_0:28;
      then f.((seq^\k).n)<=f1.((seq^\k).n) by A4,A11;
      then (f/*(seq^\k)).n<=f1.((seq^\k).n) by A6,A10,
      FUNCT_2:185,XBOOLE_1:
      1;
      hence (f/*(seq^\k)).n<=(f1/*(seq^\k)).n by A12,A13,FUNCT_2:185 ,
      XBOOLE_1:1;
    end;
A15: seq^\k is divergent_to+infty by A5,Th53;
    rng(seq^\k)c=dom f1 by A12,A13,XBOOLE_1:1;
    then f1/*(seq^\k) is divergent_to-infty by A1,A15,Def8;
    then
A16: f/*(seq^\k) is divergent_to-infty by A14,Th70;
    f/*(seq^\k)=(f/*seq)^\k by A6,VALUED_0:27;
    hence f/*seq is divergent_to-infty by A16,Th34;
  end;
  hence thesis by A2,Def8;
end;

theorem Th105:
  f1 is divergent_in-infty_to+infty & (for r ex g st g<r & g in
dom f) & (ex r st dom f/\left_open_halfline(r) c= dom f1 /\ left_open_halfline(
r) & for g st g in dom f /\ left_open_halfline(r) holds f1.g<=f.g) implies f is
  divergent_in-infty_to+infty
proof
  assume that
A1: f1 is divergent_in-infty_to+infty and
A2: for r ex g st g<r & g in dom f;
  given r1 such that
A3: dom f/\left_open_halfline(r1)c= dom f1/\left_open_halfline(r1) and
A4: for g st g in dom f/\left_open_halfline(r1) holds f1.g<=f.g;
  now
    let seq;
    assume that
A5: seq is divergent_to-infty and
A6: rng seq c=dom f;
    consider k such that
A7: for n st k<=n holds seq.n<r1 by A5,Def5;
    now
      let x be set;
      assume x in rng(seq^\k);
      then consider n such that
A8:   (seq^\k).n=x by FUNCT_2:190;
      seq.(n+k)<r1 by A7,NAT_1:12;
      then (seq^\k).n<r1 by NAT_1:def 3;
      then x in {g2: g2<r1} by A8;
      hence x in left_open_halfline(r1) by XXREAL_1:229;
    end;
    then
A9: rng(seq^\k)c=left_open_halfline(r1) by TARSKI:def 3;
A10: rng(seq^\k)c=rng seq by VALUED_0:21;
    then rng(seq^\k)c=dom f by A6,XBOOLE_1:1;
    then
A11: rng(seq^\k)c=dom f/\left_open_halfline(r1) by A9,XBOOLE_1:19;
    then
A12: rng(seq^\k)c=dom f1/\left_open_halfline(r1) by A3,XBOOLE_1:1;
A13: dom f1/\left_open_halfline(r1)c=dom f1 by XBOOLE_1:17;
A14: now
      let n;
      (seq^\k).n in rng(seq^\k) by VALUED_0:28;
      then f1.((seq^\k).n)<=f.((seq^\k).n) by A4,A11;
      then (f1/*(seq^\k)).n<=f.((seq^\k).n) by A12,A13,FUNCT_2:185 ,
      XBOOLE_1:1;
      hence (f1/*(seq^\k)).n<=(f/*(seq^\k)).n by A6,A10,FUNCT_2:185 ,
      XBOOLE_1:1;
    end;
A15: seq^\k is divergent_to-infty by A5,Th54;
    rng(seq^\k)c=dom f1 by A12,A13,XBOOLE_1:1;
    then f1/*(seq^\k) is divergent_to+infty by A1,A15,Def10;
    then
A16: f/*(seq^\k) is divergent_to+infty by A14,Th69;
    f/*(seq^\k)=(f/*seq)^\k by A6,VALUED_0:27;
    hence f/*seq is divergent_to+infty by A16,Th34;
  end;
  hence thesis by A2,Def10;
end;

theorem Th106:
  f1 is divergent_in-infty_to-infty & (for r ex g st g<r & g in
dom f) & (ex r st dom f/\left_open_halfline(r) c= dom f1 /\ left_open_halfline(
r) & for g st g in dom f /\ left_open_halfline(r) holds f.g<=f1.g) implies f is
  divergent_in-infty_to-infty
proof
  assume that
A1: f1 is divergent_in-infty_to-infty and
A2: for r ex g st g<r & g in dom f;
  given r1 such that
A3: dom f/\left_open_halfline(r1)c= dom f1/\left_open_halfline(r1) and
A4: for g st g in dom f/\left_open_halfline(r1) holds f.g<=f1.g;
  now
    let seq;
    assume that
A5: seq is divergent_to-infty and
A6: rng seq c=dom f;
    consider k such that
A7: for n st k<=n holds seq.n<r1 by A5,Def5;
    now
      let x be set;
      assume x in rng(seq^\k);
      then consider n such that
A8:   (seq^\k).n=x by FUNCT_2:190;
      seq.(n+k)<r1 by A7,NAT_1:12;
      then (seq^\k).n<r1 by NAT_1:def 3;
      then x in {g2: g2<r1} by A8;
      hence x in left_open_halfline(r1) by XXREAL_1:229;
    end;
    then
A9: rng(seq^\k)c=left_open_halfline(r1) by TARSKI:def 3;
A10: rng(seq^\k)c=rng seq by VALUED_0:21;
    then rng(seq^\k)c=dom f by A6,XBOOLE_1:1;
    then
A11: rng(seq^\k)c=dom f/\left_open_halfline(r1) by A9,XBOOLE_1:19;
    then
A12: rng(seq^\k)c=dom f1/\left_open_halfline(r1) by A3,XBOOLE_1:1;
A13: dom f1/\left_open_halfline(r1)c=dom f1 by XBOOLE_1:17;
A14: now
      let n;
      (seq^\k).n in rng(seq^\k) by VALUED_0:28;
      then f.((seq^\k).n)<=f1.((seq^\k).n) by A4,A11;
      then (f/*(seq^\k)).n<=f1.((seq^\k).n) by A6,A10,
      FUNCT_2:185,XBOOLE_1:
      1;
      hence (f/*(seq^\k)).n<=(f1/*(seq^\k)).n by A12,A13,FUNCT_2:185 ,
      XBOOLE_1:1;
    end;
A15: seq^\k is divergent_to-infty by A5,Th54;
    rng(seq^\k)c=dom f1 by A12,A13,XBOOLE_1:1;
    then f1/*(seq^\k) is divergent_to-infty by A1,A15,Def11;
    then
A16: f/*(seq^\k) is divergent_to-infty by A14,Th70;
    f/*(seq^\k)=(f/*seq)^\k by A6,VALUED_0:27;
    hence f/*seq is divergent_to-infty by A16,Th34;
  end;
  hence thesis by A2,Def11;
end;

theorem
  f1 is divergent_in+infty_to+infty & (ex r st right_open_halfline(r) c=
dom f /\ dom f1 & for g st g in right_open_halfline(r) holds f1.g<=f.g) implies
  f is divergent_in+infty_to+infty
proof
  assume
A1: f1 is divergent_in+infty_to+infty;
  given r1 such that
A2: right_open_halfline(r1)c=dom f/\dom f1 and
A3: for g st g in right_open_halfline(r1) holds f1.g<=f.g;
A4: now
    let r;
    consider g being real number such that
A5: abs(r)+abs(r1)<g by XREAL_1:3;
    reconsider g as Real by XREAL_0:def 1;
    take g;
    0<=abs(r1) & r<=abs(r) by ABSVALUE:11,COMPLEX1:132;
    then 0+r<=abs(r)+abs(r1) by XREAL_1:9;
    hence r<g by A5,XXREAL_0:2;
    0<=abs(r) & r1<=abs(r1) by ABSVALUE:11,COMPLEX1:132;
    then 0+r1<=abs(r)+abs(r1) by XREAL_1:9;
    then r1<g by A5,XXREAL_0:2;
    then g in {g2: r1<g2};
    then g in right_open_halfline(r1) by XXREAL_1:230;
    hence g in dom f by A2,XBOOLE_0:def 4;
  end;
  now
    dom f/\dom f1 c=dom f by XBOOLE_1:17;
    then
A6: dom f/\right_open_halfline(r1)=right_open_halfline(r1) by A2,
    XBOOLE_1:1,28;
    dom f/\dom f1 c=dom f1 by XBOOLE_1:17;
    hence dom f/\right_open_halfline(r1) c=dom f1/\right_open_halfline(r1) by
    A2,A6,XBOOLE_1:1,28;
    let g;
    assume g in dom f/\right_open_halfline(r1);
    hence f1.g<=f.g by A3,A6;
  end;
  hence thesis by A1,A4,Th103;
end;

theorem
  f1 is divergent_in+infty_to-infty & (ex r st right_open_halfline(r) c=
dom f /\ dom f1 & for g st g in right_open_halfline(r) holds f.g<=f1.g) implies
  f is divergent_in+infty_to-infty
proof
  assume
A1: f1 is divergent_in+infty_to-infty;
  given r1 such that
A2: right_open_halfline(r1)c=dom f/\dom f1 and
A3: for g st g in right_open_halfline(r1) holds f.g<=f1.g;
A4: now
    let r;
    consider g being real number such that
A5: abs(r)+abs(r1)<g by XREAL_1:3;
    reconsider g as Real by XREAL_0:def 1;
    take g;
    0<=abs(r1) & r<=abs(r) by ABSVALUE:11,COMPLEX1:132;
    then 0+r<=abs(r)+abs(r1) by XREAL_1:9;
    hence r<g by A5,XXREAL_0:2;
    0<=abs(r) & r1<=abs(r1) by ABSVALUE:11,COMPLEX1:132;
    then 0+r1<=abs(r)+abs(r1) by XREAL_1:9;
    then r1<g by A5,XXREAL_0:2;
    then g in {g2: r1<g2};
    then g in right_open_halfline(r1) by XXREAL_1:230;
    hence g in dom f by A2,XBOOLE_0:def 4;
  end;
  now
    dom f/\dom f1 c=dom f by XBOOLE_1:17;
    then
A6: dom f/\right_open_halfline(r1)=right_open_halfline(r1) by A2,
    XBOOLE_1:1,28;
    dom f/\dom f1 c=dom f1 by XBOOLE_1:17;
    hence dom f/\right_open_halfline(r1) c=dom f1/\right_open_halfline(r1) by
    A2,A6,XBOOLE_1:1,28;
    let g;
    assume g in dom f/\right_open_halfline(r1);
    hence f.g<=f1.g by A3,A6;
  end;
  hence thesis by A1,A4,Th104;
end;

theorem
  f1 is divergent_in-infty_to+infty & (ex r st left_open_halfline(r) c=
dom f /\ dom f1 & for g st g in left_open_halfline(r) holds f1.g<=f.g) implies
  f is divergent_in-infty_to+infty
proof
  assume
A1: f1 is divergent_in-infty_to+infty;
  given r1 such that
A2: left_open_halfline(r1)c=dom f/\dom f1 and
A3: for g st g in left_open_halfline(r1) holds f1.g<=f.g;
A4: now
    let r;
    consider g being real number such that
A5: g<-abs(r)-abs(r1) by XREAL_1:4;
    reconsider g as Real by XREAL_0:def 1;
    take g;
    0<=abs(r1) & -abs(r)<=r by ABSVALUE:11,COMPLEX1:132;
    then -abs(r)-abs(r1)<=r-0 by XREAL_1:15;
    hence g<r by A5,XXREAL_0:2;
    0<=abs(r) & -abs(r1)<=r1 by ABSVALUE:11,COMPLEX1:132;
    then -abs(r1)-abs(r)<=r1-0 by XREAL_1:15;
    then g<r1 by A5,XXREAL_0:2;
    then g in {g2: g2<r1};
    then g in left_open_halfline(r1) by XXREAL_1:229;
    hence g in dom f by A2,XBOOLE_0:def 4;
  end;
  now
    dom f/\dom f1 c=dom f by XBOOLE_1:17;
    then
A6: dom f/\left_open_halfline(r1)=left_open_halfline(r1) by A2,
    XBOOLE_1:1,28;
    dom f/\dom f1 c=dom f1 by XBOOLE_1:17;
    hence
    dom f/\left_open_halfline(r1) c=dom f1/\left_open_halfline(r1) by A2,
    A6,XBOOLE_1:1,28;
    let g;
    assume g in dom f/\left_open_halfline(r1);
    hence f1.g<=f.g by A3,A6;
  end;
  hence thesis by A1,A4,Th105;
end;

theorem
  f1 is divergent_in-infty_to-infty & (ex r st left_open_halfline(r) c=
dom f /\ dom f1 & for g st g in left_open_halfline(r) holds f.g<=f1.g) implies
  f is divergent_in-infty_to-infty
proof
  assume
A1: f1 is divergent_in-infty_to-infty;
  given r1 such that
A2: left_open_halfline(r1)c=dom f/\dom f1 and
A3: for g st g in left_open_halfline(r1) holds f.g<=f1.g;
A4: now
    let r;
    consider g being real number such that
A5: g<-abs(r)-abs(r1) by XREAL_1:4;
    reconsider g as Real by XREAL_0:def 1;
    take g;
    0<=abs(r1) & -abs(r)<=r by ABSVALUE:11,COMPLEX1:132;
    then -abs(r)-abs(r1)<=r-0 by XREAL_1:15;
    hence g<r by A5,XXREAL_0:2;
    0<=abs(r) & -abs(r1)<=r1 by ABSVALUE:11,COMPLEX1:132;
    then -abs(r1)-abs(r)<=r1-0 by XREAL_1:15;
    then g<r1 by A5,XXREAL_0:2;
    then g in {g2: g2<r1};
    then g in left_open_halfline(r1) by XXREAL_1:229;
    hence g in dom f by A2,XBOOLE_0:def 4;
  end;
  now
    dom f/\dom f1 c=dom f by XBOOLE_1:17;
    then
A6: dom f/\left_open_halfline(r1)=left_open_halfline(r1) by A2,
    XBOOLE_1:1,28;
    dom f/\dom f1 c=dom f1 by XBOOLE_1:17;
    hence
    dom f/\left_open_halfline(r1) c=dom f1/\left_open_halfline(r1) by A2,
    A6,XBOOLE_1:1,28;
    let g;
    assume g in dom f/\left_open_halfline(r1);
    hence f.g<=f1.g by A3,A6;
  end;
  hence thesis by A1,A4,Th106;
end;

definition
  let f;
  assume
A1: f is convergent_in+infty;
  func lim_in+infty f ->Real means
  :Def12:
  for seq st seq is
divergent_to+infty & rng seq c= dom f holds f/*seq is convergent & lim(f/*seq)=
  it;
  existence by A1,Def6;
  uniqueness
  proof
    defpred X[Element of NAT,real number] means $1<$2 & $2 in dom f;
    let g1,g2 such that
A2: for seq st seq is divergent_to+infty & rng seq c= dom f holds f/*
    seq is convergent & lim(f/*seq)=g1 and
A3: for seq st seq is divergent_to+infty & rng seq c= dom f holds f/*
    seq is convergent & lim(f/*seq)=g2;
A4: for n ex r st X[n,r] by A1,Def6;
    consider s2 be Real_Sequence such that
A5: for n holds X[n,s2.n] from FUNCT_2:sch 3(A4);
A6: rng s2 c= dom f
    proof
      let x be real number;
      assume x in rng s2;
      then ex n st x=s2.n by FUNCT_2:190;
      hence thesis by A5;
    end;
    now
      let n;
      n<s2.n by A5;
      hence s1.n<=s2.n by FUNCT_1:35;
    end;
    then
A7: s2 is divergent_to+infty by Lm4,Th47,Th69;
    then lim(f/*s2)=g1 by A2,A6;
    hence thesis by A3,A7,A6;
  end;
end;

definition
  let f;
  assume
A1: f is convergent_in-infty;
  func lim_in-infty f ->Real means
  :Def13:
  for seq st seq is
divergent_to-infty & rng seq c= dom f holds f/*seq is convergent & lim(f/*seq)=
  it;
  existence by A1,Def9;
  uniqueness
  proof
    deffunc U(Element of NAT) = -$1;
    defpred X[Element of NAT,real number] means $2<-$1 & $2 in dom f;
    let g1,g2 such that
A2: for seq st seq is divergent_to-infty & rng seq c= dom f holds f/*
    seq is convergent & lim(f/*seq)=g1 and
A3: for seq st seq is divergent_to-infty & rng seq c= dom f holds f/*
    seq is convergent & lim(f/*seq)=g2;
    consider s2 be Real_Sequence such that
A4: for n holds s2.n=U(n) from SEQ_1:sch 1;
A5: for n ex r st X[n,r] by A1,Def9;
    consider s1 be Real_Sequence such that
A6: for n holds X[n,s1.n] from FUNCT_2:sch 3(A5);
A7: rng s1 c= dom f
    proof
      let x be real number;
      assume x in rng s1;
      then ex n st x=s1.n by FUNCT_2:190;
      hence thesis by A6;
    end;
    now
      let n;
      s1.n<-n by A6;
      hence s1.n<=s2.n by A4;
    end;
    then
A8: s1 is divergent_to-infty by A4,Th48,Th70;
    then lim(f/*s1)=g1 by A2,A7;
    hence thesis by A3,A8,A7;
  end;
end;

canceled 2;

theorem
  f is convergent_in-infty implies (lim_in-infty f=g iff for g1 st 0<g1
  ex r st for r1 st r1<r & r1 in dom f holds abs(f.r1-g)<g1)
proof
  assume
A1: f is convergent_in-infty;
  thus lim_in-infty f=g implies for g1 st 0<g1 ex r st for r1 st r1<r & r1 in
  dom f holds abs(f.r1-g)<g1
  proof
    deffunc U(Element of NAT) = -$1;
    assume
A2: lim_in-infty f=g;
    consider s1 be Real_Sequence such that
A3: for n holds s1.n=U(n) from SEQ_1:sch 1;
    given g1 such that
A4: 0<g1 and
A5: for r ex r1 st r1<r & r1 in dom f & abs(f.r1-g)>=g1;
    defpred X[Element of NAT,real number] means $2<-$1 & $2 in dom f & abs(f.
    $2-g)>=g1;
A6: for n ex r st X[n,r] by A5;
    consider s be Real_Sequence such that
A7: for n holds X[n,s.n] from FUNCT_2:sch 3(A6);
    now
      let x be set;
      assume x in rng s;
      then ex n st s.n=x by FUNCT_2:190;
      hence x in dom f by A7;
    end;
    then
A8: rng s c=dom f by TARSKI:def 3;
    now
      let n;
      s.n<-n by A7;
      hence s.n<=s1.n by A3;
    end;
    then s is divergent_to-infty by A3,Th48,Th70;
    then f/*s is convergent & lim(f/*s)=g by A1,A2,A8,Def13;
    then consider n such that
A9: for m st n<=m holds abs((f/*s).m-g)<g1 by A4,SEQ_2:def 7;
    abs((f/*s).n-g)<g1 by A9;
    then abs(f.(s.n)-g)<g1 by A8,FUNCT_2:185;
    hence contradiction by A7;
  end;
  assume
A10: for g1 st 0<g1 ex r st for r1 st r1<r & r1 in dom f holds abs(f.r1- g)<g1;
  now
    let s be Real_Sequence such that
A11: s is divergent_to-infty and
A12: rng s c=dom f;
A13: now
      let g1 be real number;
      assume
A14:  0<g1;
      g1 is Real by XREAL_0:def 1;
      then consider r such that
A15:  for r1 st r1<r & r1 in dom f holds abs(f.r1-g)<g1 by A10,
      A14;
      consider n such that
A16:  for m st n<=m holds s.m<r by A11,Def5;
      take n;
      let m;
A17:  s.m in rng s by VALUED_0:28;
      assume n<=m;
      then abs(f.(s.m)-g)<g1 by A12,A15,A16,A17;
      hence abs((f/*s).m-g)<g1 by A12,FUNCT_2:185;
    end;
    hence f/*s is convergent by SEQ_2:def 6;
    hence lim(f/*s)=g by A13,SEQ_2:def 7;
  end;
  hence thesis by A1,Def13;
end;

theorem
  f is convergent_in+infty implies (lim_in+infty f=g iff for g1 st 0<g1
  ex r st for r1 st r<r1 & r1 in dom f holds abs(f.r1-g)<g1)
proof
  assume
A1: f is convergent_in+infty;
  thus lim_in+infty f=g implies for g1 st 0<g1 ex r st for r1 st r<r1 & r1 in
  dom f holds abs(f.r1-g)<g1
  proof
    assume
A2: lim_in+infty f=g;
    given g1 such that
A3: 0<g1 and
A4: for r ex r1 st r<r1 & r1 in dom f & abs(f.r1-g)>=g1;
    defpred X[Element of NAT,real number] means $1<$2 & $2 in dom f & abs(f.$2
    -g)>=g1;
A5: for n ex r st X[n,r] by A4;
    consider s be Real_Sequence such that
A6: for n holds X[n,s.n] from FUNCT_2:sch 3(A5);
    now
      let x be set;
      assume x in rng s;
      then ex n st s.n=x by FUNCT_2:190;
      hence x in dom f by A6;
    end;
    then
A7: rng s c=dom f by TARSKI:def 3;
    now
      let n;
      n<s.n by A6;
      hence s1.n<=s.n by FUNCT_1:35;
    end;
    then s is divergent_to+infty by Lm4,Th47,Th69;
    then f/*s is convergent & lim(f/*s)=g by A1,A2,A7,Def12;
    then consider n such that
A8: for m st n<=m holds abs((f/*s).m-g)<g1 by A3,SEQ_2:def 7;
    abs((f/*s).n-g)<g1 by A8;
    then abs(f.(s.n)-g)<g1 by A7,FUNCT_2:185;
    hence contradiction by A6;
  end;
  assume
A9: for g1 st 0<g1 ex r st for r1 st r<r1 & r1 in dom f holds abs(f.r1- g)<g1;
  now
    let s be Real_Sequence such that
A10: s is divergent_to+infty and
A11: rng s c=dom f;
A12: now
      let g1 be real number;
      assume
A13:  0<g1;
      g1 is Real by XREAL_0:def 1;
      then consider r such that
A14:  for r1 st r<r1 & r1 in dom f holds abs(f.r1-g)<g1 by A9,
      A13;
      consider n such that
A15:  for m st n<=m holds r<s.m by A10,Def4;
      take n;
      let m;
A16:  s.m in rng s by VALUED_0:28;
      assume n<=m;
      then abs(f.(s.m)-g)<g1 by A11,A14,A15,A16;
      hence abs((f/*s).m-g)<g1 by A11,FUNCT_2:185;
    end;
    hence f/*s is convergent by SEQ_2:def 6;
    hence lim(f/*s)=g by A12,SEQ_2:def 7;
  end;
  hence thesis by A1,Def12;
end;

theorem Th115:
  f is convergent_in+infty implies r(#)f is convergent_in+infty &
  lim_in+infty(r(#)f)=r*(lim_in+infty f)
proof
  assume
A1: f is convergent_in+infty;
A2: now
    let seq;
    assume that
A3: seq is divergent_to+infty and
A4: rng seq c=dom(r(#)f);
A5: rng seq c=dom f by A4,VALUED_1:def 5;
    then
A6: lim(f/*seq)=lim_in+infty f by A1,A3,Def12;
    lim_in+infty f=lim_in+infty f;
    then
A7: f/*seq is convergent by A1,A3,A5,Def12;
    then r(#)(f/*seq) is convergent by SEQ_2:21;
    hence (r(#)f)/*seq is convergent by A5,RFUNCT_2:24;
    thus lim((r(#)f)/*seq)=lim(r(#)(f/*seq)) by A5,RFUNCT_2:24
      .=r*(lim_in+infty f) by A7,A6,SEQ_2:22;
  end;
  for r1 ex g st r1<g & g in dom(r(#)f)
  proof
    let r1;
    consider g such that
A8: r1<g & g in dom f by A1,Def6;
    take g;
    thus thesis by A8,VALUED_1:def 5;
  end;
  hence r(#)f is convergent_in+infty by A2,Def6;
  hence thesis by A2,Def12;
end;

theorem Th116:
  f is convergent_in+infty implies -f is convergent_in+infty &
  lim_in+infty(-f)=-(lim_in+infty f)
proof
  assume
A1: f is convergent_in+infty;
  (-1)(#)f=-f;
  hence -f is convergent_in+infty by A1,Th115;
  thus lim_in+infty (-f)=(-1)*(lim_in+infty f) by A1,Th115
    .=-(lim_in+infty f);
end;

theorem Th117:
  f1 is convergent_in+infty & f2 is convergent_in+infty & (for r
  ex g st r<g & g in dom(f1+f2)) implies f1+f2 is convergent_in+infty &
  lim_in+infty(f1+f2) = lim_in+infty f1 + lim_in+infty f2
proof
  assume that
A1: f1 is convergent_in+infty and
A2: f2 is convergent_in+infty and
A3: for r ex g st r<g & g in dom(f1+f2);
A4: now
    let seq;
    assume that
A5: seq is divergent_to+infty and
A6: rng seq c=dom(f1+f2);
A7: dom(f1+f2)=dom f1/\dom f2 by A6,Lm2;
A8: rng seq c=dom f2 by A6,Lm2;
    then
A9: lim(f2/*seq)=lim_in+infty f2 by A2,A5,Def12;
    lim_in+infty f2=lim_in+infty f2;
    then
A10: f2/*seq is convergent by A2,A5,A8,Def12;
A11: rng seq c=dom f1 by A6,Lm2;
    then
A12: lim(f1/*seq)=lim_in+infty f1 by A1,A5,Def12;
    lim_in+infty f1=lim_in+infty f1;
    then
A13: f1/*seq is convergent by A1,A5,A11,Def12;
    then f1/*seq+f2/*seq is convergent by A10,SEQ_2:19;
    hence (f1+f2)/*seq is convergent by A6,A7,RFUNCT_2:23;
    thus lim((f1+f2)/*seq)=lim(f1/*seq+f2/*seq) by A6,A7,RFUNCT_2:23
      .=lim_in+infty f1+lim_in+infty f2 by A13,A12,A10,A9,SEQ_2:20;
  end;
  hence f1+f2 is convergent_in+infty by A3,Def6;
  hence thesis by A4,Def12;
end;

theorem
  f1 is convergent_in+infty & f2 is convergent_in+infty & (for r ex g st
r<g & g in dom(f1-f2)) implies f1-f2 is convergent_in+infty & lim_in+infty(f1-
  f2)=(lim_in+infty f1)-(lim_in+infty f2)
proof
  assume that
A1: f1 is convergent_in+infty and
A2: f2 is convergent_in+infty and
A3: for r ex g st r<g & g in dom(f1-f2);
A4: -f2 is convergent_in+infty by A2,Th116;
  hence f1-f2 is convergent_in+infty by A1,A3,Th117;
  lim_in+infty(-f2)=-(lim_in+infty f2) by A2,Th116;
  hence lim_in+infty(f1-f2)=lim_in+infty f1+-lim_in+infty f2 by A1,A3,A4,
  Th117
    .=lim_in+infty f1-lim_in+infty f2;
end;

theorem
  f is convergent_in+infty & f"{0}={} & (lim_in+infty f)<>0 implies f^
  is convergent_in+infty & lim_in+infty(f^)=(lim_in+infty f)"
proof
  assume that
A1: f is convergent_in+infty and
A2: f"{0}={} and
A3: (lim_in+infty f)<>0;
A4: dom f= dom f\f"{0} by A2
    .=dom(f^) by RFUNCT_1:def 8;
A5: now
    let seq;
    assume that
A6: seq is divergent_to+infty and
A7: rng seq c=dom(f^);
A8: f/*seq is convergent & lim(f/*seq)=lim_in+infty f by A1,A3,A4,A6,A7,
    Def12;
    then (f/*seq)" is convergent by A3,A7,RFUNCT_2:26,SEQ_2:35;
    hence (f^)/*seq is convergent by A7,RFUNCT_2:27;
    thus lim((f^)/*seq)=lim((f/*seq)") by A7,RFUNCT_2:27
      .=(lim_in+infty f)" by A3,A7,A8,RFUNCT_2:26,SEQ_2:36;
  end;
  for r ex g st r<g & g in dom(f^) by A1,A4,Def6;
  hence f^ is convergent_in+infty by A5,Def6;
  hence thesis by A5,Def12;
end;

theorem
  f is convergent_in+infty implies abs(f) is convergent_in+infty &
  lim_in+infty abs(f)=abs(lim_in+infty f)
proof
  assume
A1: f is convergent_in+infty;
A2: now
    let seq;
    assume that
A3: seq is divergent_to+infty and
A4: rng seq c=dom abs(f);
A5: rng seq c=dom f by A4,VALUED_1:def 11;
    then
A6: lim(f/*seq)=lim_in+infty f by A1,A3,Def12;
    lim_in+infty f=lim_in+infty f;
    then
A7: f/*seq is convergent by A1,A3,A5,Def12;
    then abs(f/*seq) is convergent by SEQ_4:26;
    hence (abs f)/*seq is convergent by A5,RFUNCT_2:25;
    thus lim((abs f)/*seq)=lim abs(f/*seq) by A5,RFUNCT_2:25
      .=abs(lim_in+infty f) by A7,A6,SEQ_4:27;
  end;
  now
    let r;
    consider g such that
A8: r<g & g in dom f by A1,Def6;
    take g;
    thus r<g & g in dom abs(f) by A8,VALUED_1:def 11;
  end;
  hence abs(f) is convergent_in+infty by A2,Def6;
  hence thesis by A2,Def12;
end;

theorem Th121:
  f is convergent_in+infty & lim_in+infty f<>0 & (for r ex g st r
<g & g in dom f & f.g<>0) implies f^ is convergent_in+infty & lim_in+infty(f^)=
  (lim_in+infty f)"
proof
  assume that
A1: f is convergent_in+infty and
A2: lim_in+infty f<>0 and
A3: for r ex g st r<g & g in dom f & f.g<>0;
A4: now
    let seq such that
A5: seq is divergent_to+infty and
A6: rng seq c=dom(f^);
    dom(f^)=dom f\f"{0} & dom f\f"{0}c=dom f by RFUNCT_1:def 8,XBOOLE_1:
    36;
    then rng seq c=dom f by A6,XBOOLE_1:1;
    then
A7: f/*seq is convergent & lim(f/*seq)=lim_in+infty f by A1,A2,A5,
    Def12;
    then (f/*seq)" is convergent by A2,A6,RFUNCT_2:26,SEQ_2:35;
    hence (f^)/*seq is convergent by A6,RFUNCT_2:27;
    thus lim((f^)/*seq)=lim((f/*seq)") by A6,RFUNCT_2:27
      .=(lim_in+infty f)" by A2,A6,A7,RFUNCT_2:26,SEQ_2:36;
  end;
  now
    let r;
    consider g such that
A8: r<g and
A9: g in dom f and
A10: f.g<>0 by A3;
    take g;
    not f.g in {0} by A10,TARSKI:def 1;
    then not g in f"{0} by FUNCT_1:def 13;
    then g in dom f\f"{0} by A9,XBOOLE_0:def 5;
    hence r<g & g in dom(f^) by A8,RFUNCT_1:def 8;
  end;
  hence f^ is convergent_in+infty by A4,Def6;
  hence thesis by A4,Def12;
end;

theorem Th122:
  f1 is convergent_in+infty & f2 is convergent_in+infty & (for r
  ex g st r<g & g in dom(f1(#)f2)) implies f1(#) f2 is convergent_in+infty &
  lim_in+infty(f1(#)f2)=(lim_in+infty f1)*(lim_in+infty f2)
proof
  assume that
A1: f1 is convergent_in+infty and
A2: f2 is convergent_in+infty and
A3: for r ex g st r<g & g in dom(f1(#)f2);
A4: now
    let seq;
    assume that
A5: seq is divergent_to+infty and
A6: rng seq c=dom(f1(#)f2);
A7: dom(f1(#)f2)=dom f1/\dom f2 by A6,Lm3;
A8: rng seq c=dom f2 by A6,Lm3;
    then
A9: lim(f2/*seq)=lim_in+infty f2 by A2,A5,Def12;
    lim_in+infty f2=lim_in+infty f2;
    then
A10: f2/*seq is convergent by A2,A5,A8,Def12;
A11: rng seq c=dom f1 by A6,Lm3;
    then
A12: lim(f1/*seq)=lim_in+infty f1 by A1,A5,Def12;
    lim_in+infty f1=lim_in+infty f1;
    then
A13: f1/*seq is convergent by A1,A5,A11,Def12;
    then (f1/*seq)(#)(f2/*seq) is convergent by A10,SEQ_2:28;
    hence (f1(#)f2)/*seq is convergent by A6,A7,RFUNCT_2:23;
    thus lim((f1(#)f2)/*seq)=lim((f1/*seq)(#)(f2/*seq)) by A6,A7,RFUNCT_2:23
      .=(lim_in+infty f1)*(lim_in+infty f2) by A13,A12,A10,A9,SEQ_2:29;
  end;
  hence f1(#) f2 is convergent_in+infty by A3,Def6;
  hence thesis by A4,Def12;
end;

theorem
  f1 is convergent_in+infty & f2 is convergent_in+infty & lim_in+infty
  f2<> 0 & (for r ex g st r<g & g in dom(f1/f2)) implies f1/f2 is
  convergent_in+infty & lim_in+infty(f1/f2)=(lim_in+infty f1)/(lim_in+infty f2)
proof
  assume that
A1: f1 is convergent_in+infty and
A2: f2 is convergent_in+infty & lim_in+infty f2<>0 and
A3: for r ex g st r<g & g in dom(f1/f2);
  dom(f1/f2)=dom f1/\(dom f2\f2"{0}) by RFUNCT_1:def 4;
  then
A4: dom(f1/f2)=dom f1/\dom(f2^) by RFUNCT_1:def 8;
A5: dom f1/\dom(f2^)c=dom(f2^) by XBOOLE_1:17;
A6: now
    let r;
    consider g such that
A7: r<g and
A8: g in dom(f1/f2) by A3;
    take g;
    g in dom(f2^) by A4,A5,A8;
    then
A9: g in dom f2\f2"{0} by RFUNCT_1:def 8;
    then g in dom f2 & not g in f2"{0} by XBOOLE_0:def 5;
    then not f2.g in {0} by FUNCT_1:def 13;
    hence r<g & g in dom f2 & f2.g<>0 by A7,A9,TARSKI:def 1 ,XBOOLE_0:def
    5;
  end;
  then
A10: f2^ is convergent_in+infty by A2,Th121;
A11: lim_in+infty(f2^)=(lim_in+infty f2)" by A2,A6,Th121;
A12: now
    let r;
    consider g such that
A13: r<g & g in dom(f1/f2) by A3;
    take g;
    thus r<g & g in dom(f1(#)(f2^)) by A4,A13,VALUED_1:def 4;
  end;
  then f1(#)(f2^) is convergent_in+infty by A1,A10,Th122;
  hence f1/f2 is convergent_in+infty by RFUNCT_1:47;
  thus lim_in+infty(f1/f2)=lim_in+infty(f1(#)(f2^)) by RFUNCT_1:47
    .=(lim_in+infty f1)*((lim_in+infty f2)") by A1,A12,A10,A11,Th122
    .=(lim_in+infty f1)/(lim_in+infty f2) by XCMPLX_0:def 9;
end;

theorem Th124:
  f is convergent_in-infty implies r(#)f is convergent_in-infty &
  lim_in-infty(r(#)f)=r*(lim_in-infty f)
proof
  assume
A1: f is convergent_in-infty;
A2: now
    let seq;
    assume that
A3: seq is divergent_to-infty and
A4: rng seq c=dom(r(#)f);
A5: rng seq c=dom f by A4,VALUED_1:def 5;
    then
A6: lim(f/*seq)=lim_in-infty f by A1,A3,Def13;
    lim_in-infty f=lim_in-infty f;
    then
A7: f/*seq is convergent by A1,A3,A5,Def13;
    then r(#)(f/*seq) is convergent by SEQ_2:21;
    hence (r(#)f)/*seq is convergent by A5,RFUNCT_2:24;
    thus lim((r(#)f)/*seq)=lim(r(#)(f/*seq)) by A5,RFUNCT_2:24
      .=r*(lim_in-infty f) by A7,A6,SEQ_2:22;
  end;
  for r1 ex g st g<r1 & g in dom(r(#)f)
  proof
    let r1;
    consider g such that
A8: g<r1 & g in dom f by A1,Def9;
    take g;
    thus thesis by A8,VALUED_1:def 5;
  end;
  hence r(#)f is convergent_in-infty by A2,Def9;
  hence thesis by A2,Def13;
end;

theorem Th125:
  f is convergent_in-infty implies -f is convergent_in-infty &
  lim_in-infty(-f)=-(lim_in-infty f)
proof
  assume
A1: f is convergent_in-infty;
  (-1)(#)f=-f;
  hence -f is convergent_in-infty by A1,Th124;
  thus lim_in-infty (-f)=(-1)*(lim_in-infty f) by A1,Th124
    .=-(lim_in-infty f);
end;

theorem Th126:
  f1 is convergent_in-infty & f2 is convergent_in-infty & (for r
  ex g st g<r & g in dom(f1+f2)) implies f1+f2 is convergent_in-infty &
  lim_in-infty(f1+f2) = lim_in-infty f1 + lim_in-infty f2
proof
  assume that
A1: f1 is convergent_in-infty and
A2: f2 is convergent_in-infty and
A3: for r ex g st g<r & g in dom(f1+f2);
A4: now
    let seq;
    assume that
A5: seq is divergent_to-infty and
A6: rng seq c=dom(f1+f2);
A7: rng seq c=dom f1/\dom f2 by A6,VALUED_1:def 1;
    dom f1/\dom f2 c=dom f2 by XBOOLE_1:17;
    then
A8: rng seq c=dom f2 by A7,XBOOLE_1:1;
    then
A9: lim(f2/*seq)=lim_in-infty f2 by A2,A5,Def13;
    lim_in-infty f2=lim_in-infty f2;
    then
A10: f2/*seq is convergent by A2,A5,A8,Def13;
    dom f1/\dom f2 c=dom f1 by XBOOLE_1:17;
    then
A11: rng seq c=dom f1 by A7,XBOOLE_1:1;
    then
A12: lim(f1/*seq)=lim_in-infty f1 by A1,A5,Def13;
    lim_in-infty f1=lim_in-infty f1;
    then
A13: f1/*seq is convergent by A1,A5,A11,Def13;
    then f1/*seq+f2/*seq is convergent by A10,SEQ_2:19;
    hence (f1+f2)/*seq is convergent by A7,RFUNCT_2:23;
    thus lim((f1+f2)/*seq)=lim(f1/*seq+f2/*seq) by A7,RFUNCT_2:23
      .=lim_in-infty f1+lim_in-infty f2 by A13,A12,A10,A9,SEQ_2:20;
  end;
  hence f1+f2 is convergent_in-infty by A3,Def9;
  hence thesis by A4,Def13;
end;

theorem
  f1 is convergent_in-infty & f2 is convergent_in-infty & (for r ex g st
g<r & g in dom(f1-f2)) implies f1-f2 is convergent_in-infty & lim_in-infty(f1-
  f2)=(lim_in-infty f1)-(lim_in-infty f2)
proof
  assume that
A1: f1 is convergent_in-infty and
A2: f2 is convergent_in-infty and
A3: for r ex g st g<r & g in dom(f1-f2);
A4: -f2 is convergent_in-infty by A2,Th125;
  hence f1-f2 is convergent_in-infty by A1,A3,Th126;
  lim_in-infty(-f2)=-(lim_in-infty f2) by A2,Th125;
  hence lim_in-infty(f1-f2)=lim_in-infty f1+-lim_in-infty f2 by A1,A3,A4,
  Th126
    .=lim_in-infty f1-lim_in-infty f2;
end;

theorem
  f is convergent_in-infty & f"{0}={} & (lim_in-infty f)<>0 implies f^
  is convergent_in-infty & lim_in-infty(f^)=(lim_in-infty f)"
proof
  assume that
A1: f is convergent_in-infty and
A2: f"{0}={} and
A3: (lim_in-infty f)<>0;
A4: dom f= dom f\f"{0} by A2
    .=dom(f^) by RFUNCT_1:def 8;
A5: now
    let seq;
    assume that
A6: seq is divergent_to-infty and
A7: rng seq c=dom(f^);
A8: f/*seq is convergent & lim(f/*seq)=lim_in-infty f by A1,A3,A4,A6,A7,
    Def13;
    then (f/*seq)" is convergent by A3,A7,RFUNCT_2:26,SEQ_2:35;
    hence (f^)/*seq is convergent by A7,RFUNCT_2:27;
    thus lim((f^)/*seq)=lim((f/*seq)") by A7,RFUNCT_2:27
      .=(lim_in-infty f)" by A3,A7,A8,RFUNCT_2:26,SEQ_2:36;
  end;
  for r ex g st g<r & g in dom(f^) by A1,A4,Def9;
  hence f^ is convergent_in-infty by A5,Def9;
  hence thesis by A5,Def13;
end;

theorem
  f is convergent_in-infty implies abs(f) is convergent_in-infty &
  lim_in-infty abs(f)=abs(lim_in-infty f)
proof
  assume
A1: f is convergent_in-infty;
A2: now
    let seq;
    assume that
A3: seq is divergent_to-infty and
A4: rng seq c=dom abs(f);
A5: rng seq c=dom f by A4,VALUED_1:def 11;
    then
A6: lim(f/*seq)=lim_in-infty f by A1,A3,Def13;
    lim_in-infty f=lim_in-infty f;
    then
A7: f/*seq is convergent by A1,A3,A5,Def13;
    then abs(f/*seq) is convergent by SEQ_4:26;
    hence (abs f)/*seq is convergent by A5,RFUNCT_2:25;
    thus lim((abs f)/*seq)=lim abs(f/*seq) by A5,RFUNCT_2:25
      .=abs(lim_in-infty f) by A7,A6,SEQ_4:27;
  end;
  now
    let r;
    consider g such that
A8: g<r & g in dom f by A1,Def9;
    take g;
    thus g<r & g in dom abs(f) by A8,VALUED_1:def 11;
  end;
  hence abs(f) is convergent_in-infty by A2,Def9;
  hence thesis by A2,Def13;
end;

theorem Th130:
  f is convergent_in-infty & lim_in-infty f<>0 & (for r ex g st g
<r & g in dom f & f.g<>0) implies f^ is convergent_in-infty & lim_in-infty(f^)=
  (lim_in-infty f)"
proof
  assume that
A1: f is convergent_in-infty and
A2: lim_in-infty f<>0 and
A3: for r ex g st g<r & g in dom f & f.g<>0;
A4: now
    let seq such that
A5: seq is divergent_to-infty and
A6: rng seq c=dom(f^);
    dom(f^)=dom f\f"{0} & dom f\f"{0}c=dom f by RFUNCT_1:def 8,XBOOLE_1:
    36;
    then rng seq c=dom f by A6,XBOOLE_1:1;
    then
A7: f/*seq is convergent & lim(f/*seq)=lim_in-infty f by A1,A2,A5,
    Def13;
    then (f/*seq)" is convergent by A2,A6,RFUNCT_2:26,SEQ_2:35;
    hence (f^)/*seq is convergent by A6,RFUNCT_2:27;
    thus lim((f^)/*seq)=lim((f/*seq)") by A6,RFUNCT_2:27
      .=(lim_in-infty f)" by A2,A6,A7,RFUNCT_2:26,SEQ_2:36;
  end;
  now
    let r;
    consider g such that
A8: g<r and
A9: g in dom f and
A10: f.g<>0 by A3;
    take g;
    not f.g in {0} by A10,TARSKI:def 1;
    then not g in f"{0} by FUNCT_1:def 13;
    then g in dom f\f"{0} by A9,XBOOLE_0:def 5;
    hence g<r & g in dom(f^) by A8,RFUNCT_1:def 8;
  end;
  hence f^ is convergent_in-infty by A4,Def9;
  hence thesis by A4,Def13;
end;

theorem Th131:
  f1 is convergent_in-infty & f2 is convergent_in-infty & (for r
  ex g st g<r & g in dom(f1(#)f2)) implies f1(#) f2 is convergent_in-infty &
  lim_in-infty(f1(#)f2)=(lim_in-infty f1)*(lim_in-infty f2)
proof
  assume that
A1: f1 is convergent_in-infty and
A2: f2 is convergent_in-infty and
A3: for r ex g st g<r & g in dom(f1(#)f2);
A4: now
    let seq;
    assume that
A5: seq is divergent_to-infty and
A6: rng seq c=dom(f1(#)f2);
A7: dom(f1(#)f2)=dom f1/\dom f2 by VALUED_1:def 4;
    dom f1/\dom f2 c=dom f2 by XBOOLE_1:17;
    then
A8: rng seq c=dom f2 by A6,A7,XBOOLE_1:1;
    then
A9: lim(f2/*seq)=lim_in-infty f2 by A2,A5,Def13;
    lim_in-infty f2=lim_in-infty f2;
    then
A10: f2/*seq is convergent by A2,A5,A8,Def13;
    dom f1/\dom f2 c=dom f1 by XBOOLE_1:17;
    then
A11: rng seq c=dom f1 by A6,A7,XBOOLE_1:1;
    then
A12: lim(f1/*seq)=lim_in-infty f1 by A1,A5,Def13;
    lim_in-infty f1=lim_in-infty f1;
    then
A13: f1/*seq is convergent by A1,A5,A11,Def13;
    then (f1/*seq)(#)(f2/*seq) is convergent by A10,SEQ_2:28;
    hence (f1(#)f2)/*seq is convergent by A6,A7,RFUNCT_2:23;
    thus lim((f1(#)f2)/*seq)=lim((f1/*seq)(#)(f2/*seq)) by A6,A7,RFUNCT_2:23
      .=(lim_in-infty f1)*(lim_in-infty f2) by A13,A12,A10,A9,SEQ_2:29;
  end;
  hence f1(#) f2 is convergent_in-infty by A3,Def9;
  hence thesis by A4,Def13;
end;

theorem
  f1 is convergent_in-infty & f2 is convergent_in-infty & lim_in-infty
  f2<> 0 & (for r ex g st g<r & g in dom(f1/f2)) implies f1/f2 is
  convergent_in-infty & lim_in-infty(f1/f2)=(lim_in-infty f1)/(lim_in-infty f2)
proof
  assume that
A1: f1 is convergent_in-infty and
A2: f2 is convergent_in-infty & lim_in-infty f2<>0 and
A3: for r ex g st g<r & g in dom(f1/f2);
  dom(f1/f2)=dom f1/\(dom f2\f2"{0}) by RFUNCT_1:def 4;
  then
A4: dom(f1/f2)=dom f1/\dom(f2^) by RFUNCT_1:def 8;
A5: dom f1/\dom(f2^) c=dom(f2^) by XBOOLE_1:17;
A6: now
    let r;
    consider g such that
A7: g<r and
A8: g in dom(f1/f2) by A3;
    take g;
    g in dom(f2^) by A4,A5,A8;
    then
A9: g in dom f2\f2"{0} by RFUNCT_1:def 8;
    then g in dom f2 & not g in f2"{0} by XBOOLE_0:def 5;
    then not f2.g in {0} by FUNCT_1:def 13;
    hence g<r & g in dom f2 & f2.g<>0 by A7,A9,TARSKI:def 1 ,XBOOLE_0:def
    5;
  end;
  then
A10: f2^ is convergent_in-infty by A2,Th130;
A11: lim_in-infty(f2^)=(lim_in-infty f2)" by A2,A6,Th130;
A12: now
    let r;
    consider g such that
A13: g<r & g in dom(f1/f2) by A3;
    take g;
    thus g<r & g in dom(f1(#)(f2^)) by A4,A13,VALUED_1:def 4;
  end;
  then f1(#)(f2^) is convergent_in-infty by A1,A10,Th131;
  hence f1/f2 is convergent_in-infty by RFUNCT_1:47;
  thus lim_in-infty(f1/f2)=lim_in-infty(f1(#)(f2^)) by RFUNCT_1:47
    .=(lim_in-infty f1)*((lim_in-infty f2)") by A1,A12,A10,A11,Th131
    .=(lim_in-infty f1)/(lim_in-infty f2) by XCMPLX_0:def 9;
end;

theorem
  f1 is convergent_in+infty & lim_in+infty f1=0 & (for r ex g st r<g & g
in dom(f1(#)f2)) & (ex r st f2|right_open_halfline r is bounded) implies f1(#)
  f2 is convergent_in+infty & lim_in+infty(f1(#)f2)=0
proof
  assume that
A1: f1 is convergent_in+infty & lim_in+infty f1=0 and
A2: for r ex g st r<g & g in dom(f1(#)f2);
  given r such that
A3: f2|right_open_halfline r is bounded;
  consider g be real number such that
A4: for r1 being set st r1 in right_open_halfline(r)/\dom f2 holds abs(
  f2.r1)<=g by A3,RFUNCT_1:90;
A5: now
    let s be Real_Sequence;
    assume that
A6: s is divergent_to+infty and
A7: rng s c=dom(f1(#)f2);
    consider k such that
A8: for n st k<=n holds r<s.n by A6,Def4;
A9: rng(s^\k)c=rng s by VALUED_0:21;
A10: rng s c=dom f2 by A7,Lm3;
    then
A11: rng(s^\k)c=dom f2 by A9,XBOOLE_1:1;
    now
      set t=abs(g)+1;
      0<=abs(g) by COMPLEX1:132;
      hence 0<t;
      let n;
      r<s.(n+k) by A8,NAT_1:12;
      then r<(s^\k).n by NAT_1:def 3;
      then (s^\k).n in {g1: r<g1};
      then (s^\k).n in rng(s^\k) & (s^\k).n in right_open_halfline(r) by
      VALUED_0:28,XXREAL_1:230;
      then (s^\k).n in right_open_halfline(r)/\dom f2 by A11,XBOOLE_0:def
      4;
      then abs(f2.((s^\k).n))<=g by A4;
      then
A12:  abs((f2/*(s^\k)).n)<= g by A10,A9,FUNCT_2:185,XBOOLE_1:1;
      g<=abs(g) by ABSVALUE:11;
      then g<t by Lm1;
      hence abs((f2/*(s^\k)).n)<t by A12,XXREAL_0:2;
    end;
    then
A13: f2/*(s^\k) is bounded by SEQ_2:15;
    dom(f1(#)f2)=dom f1/\dom f2 by A7,Lm3;
    then rng(s^\k)c=dom f1/\dom f2 by A7,A9,XBOOLE_1:1;
    then
A14: (f1/*(s^\k))(#)(f2/*(s^\k))=(f1(#)f2)/*(s^\k) by RFUNCT_2:23
      .=((f1(#)f2)/*s)^\k by A7,VALUED_0:27;
    rng s c=dom f1 by A7,Lm3;
    then
A15: rng(s^\k)c=dom f1 by A9,XBOOLE_1:1;
    s^\k is divergent_to+infty by A6,Th53;
    then
A16: f1/*(s^\k) is convergent & lim(f1/*(s^\k))=0 by A1,A15,Def12;
    then
A17: (f1/*(s^\k))(#)(f2/*(s^\k)) is convergent by A13,SEQ_2:39;
    hence (f1(#)f2)/*s is convergent by A14,SEQ_4:35;
    lim((f1/*(s^\k))(#)(f2/*(s^\k)))=0 by A16,A13,SEQ_2:40;
    hence lim((f1(#)f2)/*s)=0 by A17,A14,SEQ_4:36;
  end;
  hence f1(#) f2 is convergent_in+infty by A2,Def6;
  hence thesis by A5,Def12;
end;

theorem
  f1 is convergent_in-infty & lim_in-infty f1=0 & (for r ex g st g<r & g
in dom(f1(#)f2)) & (ex r st f2|left_open_halfline r is bounded) implies f1(#)f2
  is convergent_in-infty & lim_in-infty(f1(#)f2)=0
proof
  assume that
A1: f1 is convergent_in-infty & lim_in-infty f1=0 and
A2: for r ex g st g<r & g in dom(f1(#)f2);
  given r such that
A3: f2|left_open_halfline r is bounded;
  consider g be real number such that
A4: for r1 being set st r1 in left_open_halfline(r)/\dom f2 holds abs(f2
  .r1)<=g by A3,RFUNCT_1:90;
A5: now
    let s be Real_Sequence;
    assume that
A6: s is divergent_to-infty and
A7: rng s c=dom(f1(#)f2);
    consider k such that
A8: for n st k<=n holds s.n<r by A6,Def5;
A9: rng(s^\k)c=rng s by VALUED_0:21;
A10: rng s c=dom f2 by A7,Lm3;
    then
A11: rng(s^\k)c=dom f2 by A9,XBOOLE_1:1;
    now
      set t=abs(g)+1;
      0<=abs(g) by COMPLEX1:132;
      hence 0<t;
      let n;
      s.(n+k)<r by A8,NAT_1:12;
      then (s^\k).n<r by NAT_1:def 3;
      then (s^\k).n in {g1: g1<r};
      then (s^\k).n in rng(s^\k) & (s^\k).n in left_open_halfline(r) by
      VALUED_0:28,XXREAL_1:229;
      then (s^\k).n in left_open_halfline(r)/\dom f2 by A11,
      XBOOLE_0:def 4;
      then abs(f2.((s^\k).n))<=g by A4;
      then
A12:  abs((f2/*(s^\k)).n)<= g by A10,A9,FUNCT_2:185,XBOOLE_1:1;
      g<=abs(g) by ABSVALUE:11;
      then g<t by Lm1;
      hence abs((f2/*(s^\k)).n)<t by A12,XXREAL_0:2;
    end;
    then
A13: f2/*(s^\k) is bounded by SEQ_2:15;
    dom(f1(#)f2)=dom f1/\dom f2 by A7,Lm3;
    then rng(s^\k)c=dom f1/\dom f2 by A7,A9,XBOOLE_1:1;
    then
A14: (f1/*(s^\k))(#)(f2/*(s^\k))=(f1(#)f2)/*(s^\k) by RFUNCT_2:23
      .=((f1(#)f2)/*s)^\k by A7,VALUED_0:27;
    rng s c=dom f1 by A7,Lm3;
    then
A15: rng(s^\k)c=dom f1 by A9,XBOOLE_1:1;
    s^\k is divergent_to-infty by A6,Th54;
    then
A16: f1/*(s^\k) is convergent & lim(f1/*(s^\k))=0 by A1,A15,Def13;
    then
A17: (f1/*(s^\k))(#)(f2/*(s^\k)) is convergent by A13,SEQ_2:39;
    hence (f1(#)f2)/*s is convergent by A14,SEQ_4:35;
    lim((f1/*(s^\k))(#)(f2/*(s^\k)))=0 by A16,A13,SEQ_2:40;
    hence lim((f1(#)f2)/*s)=0 by A17,A14,SEQ_4:36;
  end;
  hence f1(#) f2 is convergent_in-infty by A2,Def9;
  hence thesis by A5,Def13;
end;

theorem Th135:
  f1 is convergent_in+infty & f2 is convergent_in+infty &
lim_in+infty f1=lim_in+infty f2 & (for r ex g st r<g & g in dom f) & (ex r st (
  (dom f1 /\ right_open_halfline(r) c= dom f2 /\ right_open_halfline(r) & dom f
  /\ right_open_halfline(r) c= dom f1 /\ right_open_halfline(r)) or (dom f2 /\
  right_open_halfline(r) c= dom f1 /\ right_open_halfline(r) & dom f /\
  right_open_halfline(r) c= dom f2 /\ right_open_halfline(r))) & for g st g in
  dom f /\ right_open_halfline(r) holds f1.g<=f.g & f.g<=f2.g) implies f is
  convergent_in+infty & lim_in+infty f=lim_in+infty f1
proof
  assume that
A1: f1 is convergent_in+infty and
A2: f2 is convergent_in+infty and
A3: lim_in+infty f1=lim_in+infty f2 and
A4: for r ex g st r<g & g in dom f;
  given r1 such that
A5: dom f1/\right_open_halfline(r1)c=dom f2/\right_open_halfline(r1) &
  dom f/\right_open_halfline(r1)c=dom f1/\right_open_halfline(r1) or dom f2/\
  right_open_halfline(r1)c=dom f1/\right_open_halfline(r1) & dom f/\
  right_open_halfline(r1)c=dom f2/\right_open_halfline(r1) and
A6: for g st g in dom f/\right_open_halfline(r1) holds f1.g<=f.g & f.g<= f2.g;
  now
    per cases by A5;
    suppose
A7:   dom f1/\right_open_halfline(r1)c=dom f2/\right_open_halfline(r1)
      & dom f/\right_open_halfline(r1)c=dom f1/\right_open_halfline(r1);
A8:   now
        let seq;
        assume that
A9:     seq is divergent_to+infty and
A10:    rng seq c=dom f;
        consider k such that
A11:    for n st k<=n holds r1<seq.n by A9,Def4;
A12:    seq^\k is divergent_to+infty by A9,Th53;
        now
          let x be set;
          assume x in rng(seq^\k);
          then consider n such that
A13:      x=(seq^\k).n by FUNCT_2:190;
          r1<seq.(n+k) by A11,NAT_1:12;
          then r1<(seq^\k).n by NAT_1:def 3;
          then x in {g: r1<g} by A13;
          hence x in right_open_halfline(r1) by XXREAL_1:230;
        end;
        then
A14:    rng(seq^\k)c=right_open_halfline(r1) by TARSKI:def 3;
A15:    rng(seq^\k)c=rng seq by VALUED_0:21;
        then rng(seq^\k)c=dom f by A10,XBOOLE_1:1;
        then
A16:    rng(seq^\k)c=dom f/\right_open_halfline(r1) by A14,
        XBOOLE_1:
        19;
        then
A17:    rng(seq^\k)c=dom f1/\right_open_halfline(r1) by A7,
        XBOOLE_1:
        1;
        then
A18:    rng(seq^\k)c=dom f2/\right_open_halfline(r1) by A7,
        XBOOLE_1:1;
A19:    dom f2/\right_open_halfline(r1) c=dom f2 by XBOOLE_1:17;
        then rng(seq^\k)c=dom f2 by A18,XBOOLE_1:1;
        then
A20:    f2/*(seq^\k) is convergent & lim(f2/*(seq^\k))=lim_in+infty f1 by
        A2,A3,A12,Def12;
A21:    dom f1/\right_open_halfline(r1)c=dom f1 by XBOOLE_1:17;
        then rng(seq^\k)c=dom f1 by A17,XBOOLE_1:1;
        then
A22:    f1/*(seq^\k) is convergent & lim(f1/*(seq^\k))=lim_in+infty f1 by
        A1,A3,A12,Def12;
A23:    now
          let n;
A24:      (seq^\k).n in rng(seq^\k) by VALUED_0:28;
          then f.((seq^\k).n)<=f2.((seq^\k).n) by A6,A16;
          then
A25:      (f/*(seq^\k)).n<=f2.((seq^\k).n) by A10,A15,FUNCT_2:185,
          XBOOLE_1:1;
          f1.((seq^\k).n)<=f.((seq^\k).n) by A6,A16,A24;
          then f1.((seq^\k).n)<=(f/*(seq^\k)).n by A10,A15,FUNCT_2:185,
          XBOOLE_1:1;
          hence (f1/*(seq^\k)).n<=(f/*(seq^\k)).n & (f/*(seq^\k)).n<=(f2/*(seq
          ^\k)).n by A17,A21,A18,A19,A25,FUNCT_2:185,XBOOLE_1:1;
        end;
A26:    f/*(seq^\k)=(f/*seq)^\k by A10,VALUED_0:27;
        then
A27:    (f/*seq)^\k is convergent by A22,A20,A23,SEQ_2:33;
        hence f/*seq is convergent by SEQ_4:35;
        lim((f/*seq)^\k)=lim_in+infty f1 by A22,A20,A23,A26,SEQ_2:34;
        hence lim(f/*seq)=lim_in+infty f1 by A27,SEQ_4:33,35;
      end;
      hence f is convergent_in+infty by A4,Def6;
      hence thesis by A8,Def12;
    end;
    suppose
A28:  dom f2/\right_open_halfline(r1)c=dom f1/\ right_open_halfline(
      r1) & dom f/\right_open_halfline(r1)c=dom f2/\right_open_halfline(r1);
A29:  now
        let seq;
        assume that
A30:    seq is divergent_to+infty and
A31:    rng seq c=dom f;
        consider k such that
A32:    for n st k<=n holds r1<seq.n by A30,Def4;
A33:    seq^\k is divergent_to+infty by A30,Th53;
        now
          let x be set;
          assume x in rng(seq^\k);
          then consider n such that
A34:      x=(seq^\k).n by FUNCT_2:190;
          r1<seq.(n+k) by A32,NAT_1:12;
          then r1<(seq^\k).n by NAT_1:def 3;
          then x in {g: r1<g} by A34;
          hence x in right_open_halfline(r1) by XXREAL_1:230;
        end;
        then
A35:    rng(seq^\k)c=right_open_halfline(r1) by TARSKI:def 3;
A36:    rng(seq^\k) c=rng seq by VALUED_0:21;
        then rng(seq^\k)c=dom f by A31,XBOOLE_1:1;
        then
A37:    rng(seq^\k)c=dom f/\right_open_halfline(r1) by A35,
        XBOOLE_1:
        19;
        then
A38:    rng(seq^\k)c=dom f2/\right_open_halfline(r1) by A28,
        XBOOLE_1
        :1;
        then
A39:    rng(seq^\k)c=dom f1/\right_open_halfline(r1) by A28,
        XBOOLE_1:
        1;
A40:    dom f1/\right_open_halfline(r1) c=dom f1 by XBOOLE_1:17;
        then rng(seq^\k)c=dom f1 by A39,XBOOLE_1:1;
        then
A41:    f1/*(seq^\k) is convergent & lim(f1/*(seq^\k))=lim_in+infty f1 by
        A1,A3,A33,Def12;
A42:    dom f2/\right_open_halfline(r1)c=dom f2 by XBOOLE_1:17;
        then rng(seq^\k)c=dom f2 by A38,XBOOLE_1:1;
        then
A43:    f2/*(seq^\k) is convergent & lim(f2/*(seq^\k))=lim_in+infty f1 by
        A2,A3,A33,Def12;
A44:    now
          let n;
A45:      (seq^\k).n in rng(seq^\k) by VALUED_0:28;
          then f.((seq^\k).n)<=f2.((seq^\k).n) by A6,A37;
          then
A46:      (f/*(seq^\k)).n<=f2.((seq^\k).n) by A31,A36,FUNCT_2:185,
          XBOOLE_1:1;
          f1.((seq^\k).n)<=f.((seq^\k).n) by A6,A37,A45;
          then f1.((seq^\k).n)<=(f/*(seq^\k)).n by A31,A36,FUNCT_2:185,
          XBOOLE_1:1;
          hence (f1/*(seq^\k)).n<=(f/*(seq^\k)).n & (f/*(seq^\k)).n<=(f2/*(seq
          ^\k)).n by A38,A42,A39,A40,A46,FUNCT_2:185,XBOOLE_1:1;
        end;
A47:    f/*(seq^\k)=(f/*seq)^\k by A31,VALUED_0:27;
        then
A48:    (f/*seq)^\k is convergent by A43,A41,A44,SEQ_2:33;
        hence f/*seq is convergent by SEQ_4:35;
        lim((f/*seq)^\k)=lim_in+infty f1 by A43,A41,A44,A47,SEQ_2:34;
        hence lim(f/*seq)=lim_in+infty f1 by A48,SEQ_4:33,35;
      end;
      hence f is convergent_in+infty by A4,Def6;
      hence thesis by A29,Def12;
    end;
  end;
  hence thesis;
end;

theorem
  f1 is convergent_in+infty & f2 is convergent_in+infty & lim_in+infty
f1=lim_in+infty f2 & (ex r st right_open_halfline(r) c= dom f1 /\ dom f2 /\ dom
f & for g st g in right_open_halfline(r) holds f1.g<=f.g & f.g<=f2.g) implies f
  is convergent_in+infty & lim_in+infty f=lim_in+infty f1
proof
  assume
A1: f1 is convergent_in+infty & f2 is convergent_in+infty & lim_in+infty
  f1= lim_in+infty f2;
  given r1 such that
A2: right_open_halfline(r1)c=dom f1/\dom f2/\dom f and
A3: for g st g in right_open_halfline(r1) holds f1.g<=f.g & f.g<=f2.g;
  dom f1/\dom f2/\dom f c=dom f by XBOOLE_1:17;
  then
A4: right_open_halfline(r1)c=dom f by A2,XBOOLE_1:1;
A5: now
    let r;
    consider g being real number such that
A6: abs(r)+abs(r1)<g by XREAL_1:3;
    reconsider g as Real by XREAL_0:def 1;
    take g;
    r<=abs(r) & 0<=abs(r1) by ABSVALUE:11,COMPLEX1:132;
    then r+0<=abs(r)+abs(r1) by XREAL_1:9;
    hence r<g by A6,XXREAL_0:2;
    r1<=abs(r1) & 0<=abs(r) by ABSVALUE:11,COMPLEX1:132;
    then 0+r1<=abs(r)+abs(r1) by XREAL_1:9;
    then r1<g by A6,XXREAL_0:2;
    then g in {g1: r1<g1};
    then g in right_open_halfline(r1) by XXREAL_1:230;
    hence g in dom f by A4;
  end;
A7: dom f1/\dom f2/\dom f c=dom f1/\dom f2 by XBOOLE_1:17;
  now
    dom f1/\dom f2 c=dom f1 by XBOOLE_1:17;
    then dom f1/\dom f2/\dom f c=dom f1 by A7,XBOOLE_1:1;
    then
A8: dom f1/\right_open_halfline(r1)=right_open_halfline(r1) by A2,
    XBOOLE_1:1,28;
    dom f1/\dom f2 c=dom f2 by XBOOLE_1:17;
    then dom f1/\dom f2/\ dom f c=dom f2 by A7,XBOOLE_1:1;
    hence dom f1/\right_open_halfline(r1)c=dom f2/\right_open_halfline(r1) by
    A2,A8,XBOOLE_1:1,28;
    thus dom f/\right_open_halfline(r1)c=dom f1/\ right_open_halfline(r1) by
    A8,XBOOLE_1:17;
    let g;
    assume g in dom f/\right_open_halfline(r1);
    then g in right_open_halfline(r1) by XBOOLE_0:def 4;
    hence f1.g<=f.g & f.g<=f2.g by A3;
  end;
  hence thesis by A1,A5,Th135;
end;

theorem Th137:
  f1 is convergent_in-infty & f2 is convergent_in-infty &
lim_in-infty f1=lim_in-infty f2 & (for r ex g st g<r & g in dom f) & (ex r st (
(dom f1 /\ left_open_halfline(r) c= dom f2 /\ left_open_halfline(r) & dom f /\
  left_open_halfline(r) c= dom f1 /\ left_open_halfline(r)) or (dom f2 /\
  left_open_halfline(r) c= dom f1 /\ left_open_halfline(r) & dom f /\
left_open_halfline(r) c= dom f2 /\ left_open_halfline(r))) & for g st g in dom
  f /\ left_open_halfline(r) holds f1.g<=f.g & f.g<=f2.g) implies f is
  convergent_in-infty & lim_in-infty f=lim_in-infty f1
proof
  assume that
A1: f1 is convergent_in-infty and
A2: f2 is convergent_in-infty and
A3: lim_in-infty f1=lim_in-infty f2 and
A4: for r ex g st g<r & g in dom f;
  given r1 such that
A5: dom f1/\left_open_halfline(r1)c=dom f2/\left_open_halfline(r1) & dom
  f/\left_open_halfline(r1)c=dom f1/\left_open_halfline(r1) or dom f2/\
  left_open_halfline(r1)c=dom f1/\left_open_halfline(r1) & dom f/\
  left_open_halfline(r1)c=dom f2/\left_open_halfline(r1) and
A6: for g st g in dom f/\left_open_halfline(r1) holds f1.g<=f.g & f.g<= f2.g;
  now
    per cases by A5;
    suppose
A7:   dom f1/\left_open_halfline(r1)c=dom f2/\left_open_halfline(r1) &
      dom f/\left_open_halfline(r1)c=dom f1/\left_open_halfline(r1);
A8:   now
        let seq;
        assume that
A9:     seq is divergent_to-infty and
A10:    rng seq c=dom f;
        consider k such that
A11:    for n st k<=n holds seq.n<r1 by A9,Def5;
A12:    seq^\k is divergent_to-infty by A9,Th54;
        now
          let x be set;
          assume x in rng(seq^\k);
          then consider n such that
A13:      x=(seq^\k).n by FUNCT_2:190;
          seq.(n+k)<r1 by A11,NAT_1:12;
          then (seq^\k).n<r1 by NAT_1:def 3;
          then x in {g: g<r1} by A13;
          hence x in left_open_halfline(r1) by XXREAL_1:229;
        end;
        then
A14:    rng(seq^\k)c=left_open_halfline(r1) by TARSKI:def 3;
A15:    rng(seq^\k)c=rng seq by VALUED_0:21;
        then rng(seq^\k)c=dom f by A10,XBOOLE_1:1;
        then
A16:    rng(seq^\k)c=dom f/\left_open_halfline(r1) by A14, XBOOLE_1:
        19;
        then
A17:    rng(seq^\k)c=dom f1/\left_open_halfline(r1) by A7,
        XBOOLE_1:1;
        then
A18:    rng(seq^\k)c=dom f2/\left_open_halfline(r1) by A7,
        XBOOLE_1:1;
A19:    dom f2/\left_open_halfline(r1)c=dom f2 by XBOOLE_1:17;
        then rng(seq^\k)c=dom f2 by A18,XBOOLE_1:1;
        then
A20:    f2/*(seq^\k) is convergent & lim(f2/*(seq^\k))=lim_in-infty f1 by
        A2,A3,A12,Def13;
A21:    dom f1/\left_open_halfline(r1)c=dom f1 by XBOOLE_1:17;
        then rng(seq^\k)c=dom f1 by A17,XBOOLE_1:1;
        then
A22:    f1/*(seq^\k) is convergent & lim(f1/*(seq^\k))=lim_in-infty f1 by
        A1,A3,A12,Def13;
A23:    now
          let n;
A24:      (seq^\k).n in rng(seq^\k) by VALUED_0:28;
          then f.((seq^\k).n)<=f2.((seq^\k).n) by A6,A16;
          then
A25:      (f/*(seq^\k)).n<=f2.((seq^\k).n) by A10,A15,FUNCT_2:185,
          XBOOLE_1:1;
          f1.((seq^\k).n)<=f.((seq^\k).n) by A6,A16,A24;
          then f1.((seq^\k).n)<=(f/*(seq^\k)).n by A10,A15,FUNCT_2:185,
          XBOOLE_1:1;
          hence (f1/*(seq^\k)).n<=(f/*(seq^\k)).n & (f/*(seq^\k)).n<=(f2/*(seq
          ^\k)).n by A17,A21,A18,A19,A25,FUNCT_2:185,XBOOLE_1:1;
        end;
A26:    f/*(seq^\k)=(f/*seq)^\k by A10,VALUED_0:27;
        then
A27:    (f/*seq)^\k is convergent by A22,A20,A23,SEQ_2:33;
        hence f/*seq is convergent by SEQ_4:35;
        lim((f/*seq)^\k)=lim_in-infty f1 by A22,A20,A23,A26,SEQ_2:34;
        hence lim(f/*seq)=lim_in-infty f1 by A27,SEQ_4:33,35;
      end;
      hence f is convergent_in-infty by A4,Def9;
      hence thesis by A8,Def13;
    end;
    suppose
A28:  dom f2/\left_open_halfline(r1)c=dom f1/\left_open_halfline(r1)
      & dom f/\left_open_halfline(r1)c=dom f2/\left_open_halfline(r1);
A29:  now
        let seq;
        assume that
A30:    seq is divergent_to-infty and
A31:    rng seq c=dom f;
        consider k such that
A32:    for n st k<=n holds seq.n<r1 by A30,Def5;
A33:    seq^\k is divergent_to-infty by A30,Th54;
        now
          let x be set;
          assume x in rng(seq^\k);
          then consider n such that
A34:      x=(seq^\k).n by FUNCT_2:190;
          seq.(n+k)<r1 by A32,NAT_1:12;
          then (seq^\k).n<r1 by NAT_1:def 3;
          then x in {g: g<r1} by A34;
          hence x in left_open_halfline(r1) by XXREAL_1:229;
        end;
        then
A35:    rng(seq^\k)c=left_open_halfline(r1) by TARSKI:def 3;
A36:    rng(seq^\k)c=rng seq by VALUED_0:21;
        then rng(seq^\k)c=dom f by A31,XBOOLE_1:1;
        then
A37:    rng(seq^\k)c=dom f/\left_open_halfline(r1) by A35, XBOOLE_1:
        19;
        then
A38:    rng(seq^\k)c=dom f2/\left_open_halfline(r1) by A28,
        XBOOLE_1:
        1;
        then
A39:    rng(seq^\k)c=dom f1/\left_open_halfline(r1) by A28,
        XBOOLE_1:1;
A40:    dom f1/\left_open_halfline(r1)c=dom f1 by XBOOLE_1:17;
        then rng(seq^\k)c=dom f1 by A39,XBOOLE_1:1;
        then
A41:    f1/*(seq^\k) is convergent & lim(f1/*(seq^\k))=lim_in-infty f1 by
        A1,A3,A33,Def13;
A42:    dom f2/\left_open_halfline(r1)c=dom f2 by XBOOLE_1:17;
        then rng(seq^\k)c=dom f2 by A38,XBOOLE_1:1;
        then
A43:    f2/*(seq^\k) is convergent & lim(f2/*(seq^\k))=lim_in-infty f1 by
        A2,A3,A33,Def13;
A44:    now
          let n;
A45:      (seq^\k).n in rng(seq^\k) by VALUED_0:28;
          then f.((seq^\k).n)<=f2.((seq^\k).n) by A6,A37;
          then
A46:      (f/*(seq^\k)).n<=f2.((seq^\k).n) by A31,A36,FUNCT_2:185,
          XBOOLE_1:1;
          f1.((seq^\k).n)<=f.((seq^\k).n) by A6,A37,A45;
          then f1.((seq^\k).n)<=(f/*(seq^\k)).n by A31,A36,FUNCT_2:185,
          XBOOLE_1:1;
          hence (f1/*(seq^\k)).n<=(f/*(seq^\k)).n & (f/*(seq^\k)).n<=(f2/*(seq
          ^\k)).n by A38,A42,A39,A40,A46,FUNCT_2:185,XBOOLE_1:1;
        end;
A47:    f/*(seq^\k)=(f/*seq)^\k by A31,VALUED_0:27;
        then
A48:    (f/*seq)^\k is convergent by A43,A41,A44,SEQ_2:33;
        hence f/*seq is convergent by SEQ_4:35;
        lim((f/*seq)^\k)=lim_in-infty f1 by A43,A41,A44,A47,SEQ_2:34;
        hence lim(f/*seq)=lim_in-infty f1 by A48,SEQ_4:33,35;
      end;
      hence f is convergent_in-infty by A4,Def9;
      hence thesis by A29,Def13;
    end;
  end;
  hence thesis;
end;

theorem
  f1 is convergent_in-infty & f2 is convergent_in-infty & lim_in-infty
f1=lim_in-infty f2 & (ex r st left_open_halfline(r) c= dom f1 /\ dom f2 /\ dom
f & for g st g in left_open_halfline(r) holds f1.g<=f.g & f.g<=f2.g) implies f
  is convergent_in-infty & lim_in-infty f=lim_in-infty f1
proof
  assume
A1: f1 is convergent_in-infty & f2 is convergent_in-infty & lim_in-infty
  f1= lim_in-infty f2;
  given r1 such that
A2: left_open_halfline(r1)c=dom f1/\dom f2/\dom f and
A3: for g st g in left_open_halfline(r1) holds f1.g<=f.g & f.g<=f2.g;
  dom f1/\dom f2/\dom f c=dom f by XBOOLE_1:17;
  then
A4: left_open_halfline(r1) c=dom f by A2,XBOOLE_1:1;
A5: now
    let r;
    consider g being real number such that
A6: g<-abs(r)-abs(r1) by XREAL_1:4;
    reconsider g as Real by XREAL_0:def 1;
    take g;
    -abs(r)<=r & 0<=abs(r1) by ABSVALUE:11,COMPLEX1:132;
    then -abs(r)-abs(r1)<=r-0 by XREAL_1:15;
    hence g<r by A6,XXREAL_0:2;
    -abs(r1)<=r1 & 0<=abs(r) by ABSVALUE:11,COMPLEX1:132;
    then -abs(r1)-abs(r)<=r1-0 by XREAL_1:15;
    then g<r1 by A6,XXREAL_0:2;
    then g in {g1: g1<r1};
    then g in left_open_halfline(r1) by XXREAL_1:229;
    hence g in dom f by A4;
  end;
A7: dom f1/\dom f2/\dom f c=dom f1/\dom f2 by XBOOLE_1:17;
  now
    dom f1/\dom f2 c=dom f1 by XBOOLE_1:17;
    then dom f1/\dom f2/\dom f c=dom f1 by A7,XBOOLE_1:1;
    then
A8: dom f1/\left_open_halfline(r1)=left_open_halfline(r1) by A2,
    XBOOLE_1:1,28;
    dom f1/\dom f2 c=dom f2 by XBOOLE_1:17;
    then dom f1/\dom f2/\ dom f c=dom f2 by A7,XBOOLE_1:1;
    hence
    dom f1/\left_open_halfline(r1) c=dom f2/\left_open_halfline(r1) by A2
    ,A8,XBOOLE_1:1,28;
    thus dom f/\left_open_halfline(r1)c=dom f1/\left_open_halfline(r1) by A8,
    XBOOLE_1:17;
    let g;
    assume g in dom f/\left_open_halfline(r1);
    then g in left_open_halfline(r1) by XBOOLE_0:def 4;
    hence f1.g<=f.g & f.g<=f2.g by A3;
  end;
  hence thesis by A1,A5,Th137;
end;

theorem
  f1 is convergent_in+infty & f2 is convergent_in+infty & (ex r st ((dom
  f1 /\ right_open_halfline(r) c= dom f2 /\ right_open_halfline(r) & for g st g
  in dom f1 /\ right_open_halfline(r) holds f1.g<=f2.g) or (dom f2 /\
right_open_halfline(r) c= dom f1 /\ right_open_halfline(r) & for g st g in dom
  f2 /\ right_open_halfline(r) holds f1.g<=f2.g))) implies lim_in+infty f1<=
  lim_in+infty f2
proof
  assume that
A1: f1 is convergent_in+infty and
A2: f2 is convergent_in+infty;
  given r such that
A3: (dom f1/\right_open_halfline(r)c=dom f2/\right_open_halfline(r) &
  for g st g in dom f1/\right_open_halfline(r) holds f1.g<=f2.g) or (dom f2/\
right_open_halfline(r)c=dom f1/\right_open_halfline(r) & for g st g in dom f2/\
  right_open_halfline(r) holds f1.g<=f2.g);
  now
    per cases by A3;
    suppose
A4:   dom f1/\right_open_halfline(r)c=dom f2/\right_open_halfline(r) &
      for g st g in dom f1/\right_open_halfline(r) holds f1.g<=f2.g;
      defpred X[Element of NAT,real number] means $1<$2 & $2 in dom f1/\
      right_open_halfline r;
A5:   now
        let n;
        0<=abs(r) by COMPLEX1:132;
        then
A6:     n+0<=n+abs(r) by XREAL_1:9;
        consider g such that
A7:     n+abs(r)<g and
A8:     g in dom f1 by A1,Def6;
        take g;
        0<=n & r<=abs(r) by ABSVALUE:11,NAT_1:2;
        then 0+r<=n+abs(r) by XREAL_1:9;
        then r<g by A7,XXREAL_0:2;
        then g in {g2: r<g2};
        then g in right_open_halfline(r) by XXREAL_1:230;
        hence X[n,g] by A7,A8,A6,XBOOLE_0:def 4,XXREAL_0:2;
      end;
      consider s2 be Real_Sequence such that
A9:   for n holds X[n,s2.n] from FUNCT_2:sch 3(A5);
      now
        let n;
        n<s2.n by A9;
        hence s1.n<=s2.n by FUNCT_1:35;
      end;
      then
A10:  s2 is divergent_to+infty by Lm4,Th47,Th69;
A11:  rng s2 c=dom f2
      proof
        let x be real number;
        assume x in rng s2;
        then ex n st x=s2.n by FUNCT_2:190;
        then x in dom f1/\right_open_halfline(r) by A9;
        hence thesis by A4,XBOOLE_0:def 4;
      end;
      then
A12:  lim(f2/*s2)=lim_in+infty f2 by A2,A10,Def12;
A13:  rng s2 c=dom f1
      proof
        let x be real number;
        assume x in rng s2;
        then ex n st x=s2.n by FUNCT_2:190;
        then x in dom f1/\right_open_halfline(r) by A9;
        hence thesis by XBOOLE_0:def 4;
      end;
A14:  now
        let n;
        f1.(s2.n)<=f2.(s2.n) by A4,A9;
        then (f1/*s2).n<=f2.(s2.n) by A13,FUNCT_2:185;
        hence (f1/*s2).n<=(f2/*s2).n by A11,FUNCT_2:185;
      end;
      lim_in+infty f2=lim_in+infty f2;
      then
A15:  f2/*s2 is convergent by A2,A10,A11,Def12;
      lim_in+infty f1=lim_in+infty f1;
      then
A16:  f1/*s2 is convergent by A1,A10,A13,Def12;
      lim(f1/*s2)=lim_in+infty f1 by A1,A10,A13,Def12;
      hence thesis by A16,A15,A12,A14,SEQ_2:32;
    end;
    suppose
A17:  dom f2/\right_open_halfline(r)c=dom f1/\right_open_halfline(r)
      & for g st g in dom f2/\right_open_halfline(r) holds f1.g<=f2.g;
      defpred X[Element of NAT,real number] means $1<$2 & $2 in dom f2/\
      right_open_halfline r;
A18:  now
        let n;
        0<=abs(r) by COMPLEX1:132;
        then
A19:    n+0<=n+abs(r) by XREAL_1:9;
        consider g such that
A20:    n+abs(r)<g and
A21:    g in dom f2 by A2,Def6;
        take g;
        0<=n & r<=abs(r) by ABSVALUE:11,NAT_1:2;
        then 0+r<=n+abs(r) by XREAL_1:9;
        then r<g by A20,XXREAL_0:2;
        then g in {g2: r<g2};
        then g in right_open_halfline(r) by XXREAL_1:230;
        hence X[n,g] by A20,A21,A19,XBOOLE_0:def 4,XXREAL_0:2;
      end;
      consider s2 be Real_Sequence such that
A22:  for n holds X[n,s2.n] from FUNCT_2:sch 3(A18);
      now
        let n;
        n<s2.n by A22;
        hence s1.n<=s2.n by FUNCT_1:35;
      end;
      then
A23:  s2 is divergent_to+infty by Lm4,Th47,Th69;
A24:  rng s2 c=dom f1
      proof
        let x be real number;
        assume x in rng s2;
        then ex n st x=s2.n by FUNCT_2:190;
        then x in dom f2/\right_open_halfline(r) by A22;
        hence thesis by A17,XBOOLE_0:def 4;
      end;
      then
A25:  lim(f1/*s2)=lim_in+infty f1 by A1,A23,Def12;
A26:  rng s2 c=dom f2
      proof
        let x be real number;
        assume x in rng s2;
        then ex n st x=s2.n by FUNCT_2:190;
        then x in dom f2/\right_open_halfline(r) by A22;
        hence thesis by XBOOLE_0:def 4;
      end;
A27:  now
        let n;
        f1.(s2.n)<=f2.(s2.n) by A17,A22;
        then (f1/*s2).n<=f2.(s2.n) by A24,FUNCT_2:185;
        hence (f1/*s2).n<=(f2/*s2).n by A26,FUNCT_2:185;
      end;
      lim_in+infty f1=lim_in+infty f1;
      then
A28:  f1/*s2 is convergent by A1,A23,A24,Def12;
      lim_in+infty f2=lim_in+infty f2;
      then
A29:  f2/*s2 is convergent by A2,A23,A26,Def12;
      lim(f2/*s2)=lim_in+infty f2 by A2,A23,A26,Def12;
      hence thesis by A29,A28,A25,A27,SEQ_2:32;
    end;
  end;
  hence thesis;
end;

theorem
  f1 is convergent_in-infty & f2 is convergent_in-infty & (ex r st ((dom
f1 /\ left_open_halfline(r) c= dom f2 /\ left_open_halfline(r) & for g st g in
  dom f1 /\ left_open_halfline(r) holds f1.g<=f2.g) or (dom f2 /\
left_open_halfline(r) c= dom f1 /\ left_open_halfline(r) & for g st g in dom f2
  /\ left_open_halfline(r) holds f1.g<=f2.g))) implies lim_in-infty f1<=
  lim_in-infty f2
proof
  assume that
A1: f1 is convergent_in-infty and
A2: f2 is convergent_in-infty;
  given r such that
A3: (dom f1/\left_open_halfline(r)c=dom f2/\left_open_halfline(r) & for
  g st g in dom f1/\left_open_halfline(r) holds f1.g<=f2.g) or (dom f2/\
  left_open_halfline(r)c=dom f1/\left_open_halfline(r) & for g st g in dom f2/\
  left_open_halfline(r) holds f1.g<=f2.g);
  now
    per cases by A3;
    suppose
A4:   dom f1/\left_open_halfline(r)c=dom f2/\left_open_halfline(r) &
      for g st g in dom f1/\left_open_halfline(r) holds f1.g<=f2.g;
      deffunc U(Element of NAT) = -$1;
      defpred X[Element of NAT,real number] means $2<-$1 & $2 in dom f1/\
      left_open_halfline r;
      consider s1 be Real_Sequence such that
A5:   for n holds s1.n=U(n) from SEQ_1:sch 1;
A6:   now
        let n;
        0<=abs(r) by COMPLEX1:132;
        then
A7:     -n-abs(r)<=-n-0 by XREAL_1:15;
        consider g such that
A8:     g<-n-abs(r) and
A9:     g in dom f1 by A1,Def9;
        take g;
        0<=n & -abs(r)<=r by ABSVALUE:11,NAT_1:2;
        then -abs(r)-n<=r-0 by XREAL_1:15;
        then g<r by A8,XXREAL_0:2;
        then g in {g2: g2<r};
        then g in left_open_halfline(r) by XXREAL_1:229;
        hence X[n,g] by A8,A9,A7,XBOOLE_0:def 4,XXREAL_0:2;
      end;
      consider s2 be Real_Sequence such that
A10:  for n holds X[n,s2.n] from FUNCT_2:sch 3(A6);
      now
        let n;
        s2.n<-n by A10;
        hence s2.n<=s1.n by A5;
      end;
      then
A11:  s2 is divergent_to-infty by A5,Th48,Th70;
A12:  rng s2 c=dom f2
      proof
        let x be real number;
        assume x in rng s2;
        then ex n st x=s2.n by FUNCT_2:190;
        then x in dom f1/\left_open_halfline(r) by A10;
        hence thesis by A4,XBOOLE_0:def 4;
      end;
      then
A13:  lim(f2/*s2)=lim_in-infty f2 by A2,A11,Def13;
A14:  rng s2 c=dom f1
      proof
        let x be real number;
        assume x in rng s2;
        then ex n st x=s2.n by FUNCT_2:190;
        then x in dom f1/\left_open_halfline(r) by A10;
        hence thesis by XBOOLE_0:def 4;
      end;
A15:  now
        let n;
        f1.(s2.n)<=f2.(s2.n) by A4,A10;
        then (f1/*s2).n<=f2.(s2.n) by A14,FUNCT_2:185;
        hence (f1/*s2).n<=(f2/*s2).n by A12,FUNCT_2:185;
      end;
      lim_in-infty f2=lim_in-infty f2;
      then
A16:  f2/*s2 is convergent by A2,A11,A12,Def13;
      lim_in-infty f1=lim_in-infty f1;
      then
A17:  f1/*s2 is convergent by A1,A11,A14,Def13;
      lim(f1/*s2)=lim_in-infty f1 by A1,A11,A14,Def13;
      hence thesis by A17,A16,A13,A15,SEQ_2:32;
    end;
    suppose
A18:  dom f2/\left_open_halfline(r)c=dom f1/\left_open_halfline(r) &
      for g st g in dom f2/\left_open_halfline(r) holds f1.g<=f2.g;
      deffunc U(Element of NAT) = -$1;
      defpred X[Element of NAT,real number] means $2<-$1 & $2 in dom f2/\
      left_open_halfline r;
      consider s1 be Real_Sequence such that
A19:  for n holds s1.n=U(n) from SEQ_1:sch 1;
A20:  now
        let n;
        0<=abs(r) by COMPLEX1:132;
        then
A21:    -n-abs(r)<=-n-0 by XREAL_1:15;
        consider g such that
A22:    g<-n-abs(r) and
A23:    g in dom f2 by A2,Def9;
        take g;
        0<=n & -abs(r)<=r by ABSVALUE:11,NAT_1:2;
        then -abs(r)-n<=r-0 by XREAL_1:15;
        then g<r by A22,XXREAL_0:2;
        then g in {g2: g2<r};
        then g in left_open_halfline(r) by XXREAL_1:229;
        hence X[n,g] by A22,A23,A21,XBOOLE_0:def 4,XXREAL_0:2;
      end;
      consider s2 be Real_Sequence such that
A24:  for n holds X[n,s2.n] from FUNCT_2:sch 3(A20);
      now
        let n;
        s2.n<-n by A24;
        hence s2.n<=s1.n by A19;
      end;
      then
A25:  s2 is divergent_to-infty by A19,Th48,Th70;
A26:  rng s2 c=dom f1
      proof
        let x be real number;
        assume x in rng s2;
        then ex n st x=s2.n by FUNCT_2:190;
        then x in dom f2/\left_open_halfline(r) by A24;
        hence thesis by A18,XBOOLE_0:def 4;
      end;
      then
A27:  lim(f1/*s2)=lim_in-infty f1 by A1,A25,Def13;
A28:  rng s2 c=dom f2
      proof
        let x be real number;
        assume x in rng s2;
        then ex n st x=s2.n by FUNCT_2:190;
        then x in dom f2/\left_open_halfline(r) by A24;
        hence thesis by XBOOLE_0:def 4;
      end;
A29:  now
        let n;
        f1.(s2.n)<=f2.(s2.n) by A18,A24;
        then (f1/*s2).n<=f2.(s2.n) by A26,FUNCT_2:185;
        hence (f1/*s2).n<=(f2/*s2).n by A28,FUNCT_2:185;
      end;
      lim_in-infty f1=lim_in-infty f1;
      then
A30:  f1/*s2 is convergent by A1,A25,A26,Def13;
      lim_in-infty f2=lim_in-infty f2;
      then
A31:  f2/*s2 is convergent by A2,A25,A28,Def13;
      lim(f2/*s2)=lim_in-infty f2 by A2,A25,A28,Def13;
      hence thesis by A31,A30,A27,A29,SEQ_2:32;
    end;
  end;
  hence thesis;
end;

theorem
  (f is divergent_in+infty_to+infty or f is divergent_in+infty_to-infty)
& (for r ex g st r<g & g in dom f & f.g<>0) implies f^ is convergent_in+infty &
  lim_in+infty(f^)=0
proof
  assume that
A1: f is divergent_in+infty_to+infty or f is divergent_in+infty_to-infty and
A2: for r ex g st r<g & g in dom f & f.g<>0;
A3: dom(f^)=dom f\f"{0} by RFUNCT_1:def 8;
A4: now
    let r;
    consider g such that
A5: r<g & g in dom f and
A6: f.g<>0 by A2;
    take g;
    not f.g in {0} by A6,TARSKI:def 1;
    then not g in f"{0} by FUNCT_1:def 13;
    hence r<g & g in dom(f^) by A3,A5,XBOOLE_0:def 5;
  end;
  now
    per cases by A1;
    suppose
A7:   f is divergent_in+infty_to+infty;
A8:   now
        let seq such that
A9:     seq is divergent_to+infty and
A10:    rng seq c=dom(f^);
        dom(f^) c=dom f by A3,XBOOLE_1:36;
        then rng seq c=dom f by A10,XBOOLE_1:1;
        then f/*seq is divergent_to+infty by A7,A9,Def7;
        then (f/*seq)" is convergent & lim((f/*seq)")=0 by Th61;
        hence (f^)/*seq is convergent & lim((f^)/*seq)=0 by A10,RFUNCT_2:27;
      end;
      hence f^ is convergent_in+infty by A4,Def6;
      hence thesis by A8,Def12;
    end;
    suppose
A11:  f is divergent_in+infty_to-infty;
A12:  now
        let seq such that
A13:    seq is divergent_to+infty and
A14:    rng seq c=dom(f^);
        dom(f^) c=dom f by A3,XBOOLE_1:36;
        then rng seq c=dom f by A14,XBOOLE_1:1;
        then f/*seq is divergent_to-infty by A11,A13,Def8;
        then (f/*seq)" is convergent & lim((f/*seq)")=0 by Th61;
        hence (f^)/*seq is convergent & lim((f^)/*seq)=0 by A14,RFUNCT_2:27;
      end;
      hence f^ is convergent_in+infty by A4,Def6;
      hence thesis by A12,Def12;
    end;
  end;
  hence thesis;
end;

theorem
  (f is divergent_in-infty_to+infty or f is divergent_in-infty_to-infty)
& (for r ex g st g<r & g in dom f & f.g<>0) implies f^ is convergent_in-infty &
  lim_in-infty(f^)=0
proof
  assume that
A1: f is divergent_in-infty_to+infty or f is divergent_in-infty_to-infty and
A2: for r ex g st g<r & g in dom f & f.g<>0;
A3: dom(f^)=dom f\f"{0} by RFUNCT_1:def 8;
A4: now
    let r;
    consider g such that
A5: g<r & g in dom f and
A6: f.g<>0 by A2;
    take g;
    not f.g in {0} by A6,TARSKI:def 1;
    then not g in f"{0} by FUNCT_1:def 13;
    hence g<r & g in dom(f^) by A3,A5,XBOOLE_0:def 5;
  end;
  now
    per cases by A1;
    suppose
A7:   f is divergent_in-infty_to+infty;
A8:   now
        let seq such that
A9:     seq is divergent_to-infty and
A10:    rng seq c=dom(f^);
        dom(f^) c=dom f by A3,XBOOLE_1:36;
        then rng seq c=dom f by A10,XBOOLE_1:1;
        then f/*seq is divergent_to+infty by A7,A9,Def10;
        then (f/*seq)" is convergent & lim((f/*seq)")=0 by Th61;
        hence (f^)/*seq is convergent & lim((f^)/*seq)=0 by A10,RFUNCT_2:27;
      end;
      hence f^ is convergent_in-infty by A4,Def9;
      hence thesis by A8,Def13;
    end;
    suppose
A11:  f is divergent_in-infty_to-infty;
A12:  now
        let seq such that
A13:    seq is divergent_to-infty and
A14:    rng seq c=dom(f^);
        dom(f^) c=dom f by A3,XBOOLE_1:36;
        then rng seq c=dom f by A14,XBOOLE_1:1;
        then f/*seq is divergent_to-infty by A11,A13,Def11;
        then (f/*seq)" is convergent & lim((f/*seq)")=0 by Th61;
        hence (f^)/*seq is convergent & lim((f^)/*seq)=0 by A14,RFUNCT_2:27;
      end;
      hence f^ is convergent_in-infty by A4,Def9;
      hence thesis by A12,Def13;
    end;
  end;
  hence thesis;
end;

theorem
  f is convergent_in+infty & lim_in+infty f=0 & (for r ex g st r<g & g
  in dom f & f.g<>0) & (ex r st for g st g in dom f /\ right_open_halfline(r)
  holds 0<=f.g) implies f^ is divergent_in+infty_to+infty
proof
  assume that
A1: f is convergent_in+infty & lim_in+infty f=0 and
A2: for r ex g st r<g & g in dom f & f.g<>0;
  given r such that
A3: for g st g in dom f/\ right_open_halfline(r) holds 0<=f.g;
  thus for r1 ex g1 st r1<g1 & g1 in dom(f^)
  proof
    let r1;
    consider g1 such that
A4: r1<g1 and
A5: g1 in dom f and
A6: f.g1<>0 by A2;
    take g1;
    thus r1<g1 by A4;
    not f.g1 in {0} by A6,TARSKI:def 1;
    then not g1 in f"{0} by FUNCT_1:def 13;
    then g1 in dom f\f"{0} by A5,XBOOLE_0:def 5;
    hence thesis by RFUNCT_1:def 8;
  end;
  let s be Real_Sequence;
  assume that
A7: s is divergent_to+infty and
A8: rng s c=dom(f^);
  consider k such that
A9: for n st k<=n holds r<s.n by A7,Def4;
A10: rng(s^\k)c=rng s by VALUED_0:21;
  dom(f^)=dom f\f"{0} by RFUNCT_1:def 8;
  then
A11: dom(f^)c=dom f by XBOOLE_1:36;
  then
A12: rng s c=dom f by A8,XBOOLE_1:1;
  then
A13: rng(s^\k)c=dom f by A10,XBOOLE_1:1;
A14: f/*(s^\k) is non-zero by A8,A10,RFUNCT_2:26,XBOOLE_1:1;
  now
    let n;
    r<s.(n+k) by A9,NAT_1:12;
    then r<(s^\k).n by NAT_1:def 3;
    then (s^\k).n in {g2: r<g2};
    then (s^\k).n in rng(s^\k) & (s^\k).n in right_open_halfline(r) by
    VALUED_0:28,XXREAL_1:230;
    then (s^\k).n in dom f/\right_open_halfline(r) by A13,XBOOLE_0:def 4;
    then
A15: 0<= f.((s^\k).n) by A3;
    (f/*(s^\k)).n<>0 by A14,SEQ_1:7;
    hence 0<(f/*(s^\k)).n by A12,A10,A15,FUNCT_2:185,XBOOLE_1:1;
  end;
  then
A16: for n holds 0<=n implies 0<(f/*(s^\k)).n;
  s^\k is divergent_to+infty by A7,Th53;
  then f/*(s^\k) is convergent & lim(f/*(s^\k))=0 by A1,A13,Def12;
  then
A17: (f/*(s^\k))" is divergent_to+infty by A16,Th62;
  (f/*(s^\k))"=((f/*s)^\k)" by A8,A11,VALUED_0:27,XBOOLE_1:1
    .=((f/*s)")^\k by SEQM_3:41
    .=((f^)/*s)^\k by A8,RFUNCT_2:27;
  hence thesis by A17,Th34;
end;

theorem
  f is convergent_in+infty & lim_in+infty f=0 & (for r ex g st r<g & g
  in dom f & f.g<>0) & (ex r st for g st g in dom f /\ right_open_halfline(r)
  holds f.g<=0) implies f^ is divergent_in+infty_to-infty
proof
  assume that
A1: f is convergent_in+infty & lim_in+infty f=0 and
A2: for r ex g st r<g & g in dom f & f.g<>0;
  given r such that
A3: for g st g in dom f/\right_open_halfline(r) holds f.g<=0;
  thus for r1 ex g1 st r1<g1 & g1 in dom(f^)
  proof
    let r1;
    consider g1 such that
A4: r1<g1 and
A5: g1 in dom f and
A6: f.g1<>0 by A2;
    take g1;
    thus r1<g1 by A4;
    not f.g1 in {0} by A6,TARSKI:def 1;
    then not g1 in f"{0} by FUNCT_1:def 13;
    then g1 in dom f\f"{0} by A5,XBOOLE_0:def 5;
    hence thesis by RFUNCT_1:def 8;
  end;
  let s be Real_Sequence;
  assume that
A7: s is divergent_to+infty and
A8: rng s c=dom(f^);
  consider k such that
A9: for n st k<=n holds r<s.n by A7,Def4;
A10: rng(s^\k)c=rng s by VALUED_0:21;
  dom(f^)=dom f\f"{0} by RFUNCT_1:def 8;
  then
A11: dom(f^)c=dom f by XBOOLE_1:36;
  then
A12: rng s c=dom f by A8,XBOOLE_1:1;
  then
A13: rng(s^\k)c=dom f by A10,XBOOLE_1:1;
A14: f/*(s^\k) is non-zero by A8,A10,RFUNCT_2:26,XBOOLE_1:1;
  now
    let n;
    r<s.(n+k) by A9,NAT_1:12;
    then r<(s^\k).n by NAT_1:def 3;
    then (s^\k).n in {g2: r<g2};
    then (s^\k).n in rng(s^\k) & (s^\k).n in right_open_halfline(r) by
    VALUED_0:28,XXREAL_1:230;
    then (s^\k).n in dom f/\right_open_halfline(r) by A13,XBOOLE_0:def 4;
    then
A15: f.((s^\k).n)<=0 by A3;
    (f/*(s^\k)).n<>0 by A14,SEQ_1:7;
    hence (f/*(s^\k)).n<0 by A12,A10,A15,FUNCT_2:185,XBOOLE_1:1;
  end;
  then
A16: for n holds 0<=n implies (f/*(s^\k)).n<0;
  s^\k is divergent_to+infty by A7,Th53;
  then f/*(s^\k) is convergent & lim(f/*(s^\k))=0 by A1,A13,Def12;
  then
A17: (f/*(s^\k))" is divergent_to-infty by A16,Th63;
  (f/*(s^\k))"=((f/*s)^\k)" by A8,A11,VALUED_0:27,XBOOLE_1:1
    .=((f/*s)")^\k by SEQM_3:41
    .=((f^)/*s)^\k by A8,RFUNCT_2:27;
  hence thesis by A17,Th34;
end;

theorem
  f is convergent_in-infty & lim_in-infty f=0 & (for r ex g st g<r & g
  in dom f & f.g<>0) & (ex r st for g st g in dom f /\ left_open_halfline(r)
  holds 0<=f.g) implies f^ is divergent_in-infty_to+infty
proof
  assume that
A1: f is convergent_in-infty & lim_in-infty f=0 and
A2: for r ex g st g<r & g in dom f & f.g<>0;
  given r such that
A3: for g st g in dom f/\left_open_halfline(r) holds 0<=f.g;
  thus for r1 ex g1 st g1<r1 & g1 in dom(f^)
  proof
    let r1;
    consider g1 such that
A4: g1<r1 and
A5: g1 in dom f and
A6: f.g1<>0 by A2;
    take g1;
    thus g1<r1 by A4;
    not f.g1 in {0} by A6,TARSKI:def 1;
    then not g1 in f"{0} by FUNCT_1:def 13;
    then g1 in dom f\f"{0} by A5,XBOOLE_0:def 5;
    hence thesis by RFUNCT_1:def 8;
  end;
  let s be Real_Sequence;
  assume that
A7: s is divergent_to-infty and
A8: rng s c=dom (f^);
  consider k such that
A9: for n st k<=n holds s.n<r by A7,Def5;
A10: rng(s^\k)c=rng s by VALUED_0:21;
  dom(f^)=dom f\f"{0} by RFUNCT_1:def 8;
  then
A11: dom(f^)c=dom f by XBOOLE_1:36;
  then
A12: rng s c=dom f by A8,XBOOLE_1:1;
  then
A13: rng(s^\k)c=dom f by A10,XBOOLE_1:1;
A14: f/*(s^\k) is non-zero by A8,A10,RFUNCT_2:26,XBOOLE_1:1;
  now
    let n;
    s.(n+k)<r by A9,NAT_1:12;
    then (s^\k).n<r by NAT_1:def 3;
    then (s^\k).n in {g2: g2<r};
    then (s^\k).n in rng(s^\k) & (s^\k).n in left_open_halfline(r) by
    VALUED_0:28,XXREAL_1:229;
    then (s^\k).n in dom f/\left_open_halfline(r) by A13,XBOOLE_0:def 4;
    then
A15: 0<=f.((s^\k).n) by A3;
    0<>(f/*(s^\k)).n by A14,SEQ_1:7;
    hence 0<(f/*(s^\k)).n by A12,A10,A15,FUNCT_2:185,XBOOLE_1:1;
  end;
  then
A16: for n holds 0<=n implies 0<(f/*(s^\k)).n;
  s^\k is divergent_to-infty by A7,Th54;
  then f/*(s^\k) is convergent & lim(f/*(s^\k))=0 by A1,A13,Def13;
  then
A17: (f/*(s^\k))" is divergent_to+infty by A16,Th62;
  (f/*(s^\k))"=((f/*s)^\k)" by A8,A11,VALUED_0:27,XBOOLE_1:1
    .=((f/*s)")^\k by SEQM_3:41
    .=((f^)/*s)^\k by A8,RFUNCT_2:27;
  hence thesis by A17,Th34;
end;

theorem
  f is convergent_in-infty & lim_in-infty f=0 & (for r ex g st g<r & g
  in dom f & f.g<>0) & (ex r st for g st g in dom f /\ left_open_halfline(r)
  holds f.g<=0) implies f^ is divergent_in-infty_to-infty
proof
  assume that
A1: f is convergent_in-infty & lim_in-infty f=0 and
A2: for r ex g st g<r & g in dom f & f.g<>0;
  given r such that
A3: for g st g in dom f/\left_open_halfline(r) holds f.g<=0;
  thus for r1 ex g1 st g1<r1 & g1 in dom(f^)
  proof
    let r1;
    consider g1 such that
A4: g1<r1 and
A5: g1 in dom f and
A6: f.g1<>0 by A2;
    take g1;
    thus g1<r1 by A4;
    not f.g1 in {0} by A6,TARSKI:def 1;
    then not g1 in f"{0} by FUNCT_1:def 13;
    then g1 in dom f\f"{0} by A5,XBOOLE_0:def 5;
    hence thesis by RFUNCT_1:def 8;
  end;
  let s be Real_Sequence;
  assume that
A7: s is divergent_to-infty and
A8: rng s c=dom (f^);
  consider k such that
A9: for n st k<=n holds s.n<r by A7,Def5;
A10: rng(s^\k)c=rng s by VALUED_0:21;
  dom(f^)=dom f\f"{0} by RFUNCT_1:def 8;
  then
A11: dom(f^)c=dom f by XBOOLE_1:36;
  then
A12: rng s c=dom f by A8,XBOOLE_1:1;
  then
A13: rng(s^\k)c=dom f by A10,XBOOLE_1:1;
A14: f/*(s^\k) is non-zero by A8,A10,RFUNCT_2:26,XBOOLE_1:1;
  now
    let n;
    s.(n+k)<r by A9,NAT_1:12;
    then (s^\k).n<r by NAT_1:def 3;
    then (s^\k).n in {g2: g2<r};
    then (s^\k).n in rng(s^\k) & (s^\k).n in left_open_halfline(r) by
    VALUED_0:28,XXREAL_1:229;
    then (s^\k).n in dom f/\left_open_halfline(r) by A13,XBOOLE_0:def 4;
    then
A15: f.((s^\k).n)<=0 by A3;
    (f/*(s^\k)).n<>0 by A14,SEQ_1:7;
    hence (f/*(s^\k)).n<0 by A12,A10,A15,FUNCT_2:185,XBOOLE_1:1;
  end;
  then
A16: for n holds 0<=n implies (f/*(s^\k)).n<0;
  s^\k is divergent_to-infty by A7,Th54;
  then f/*(s^\k) is convergent & lim(f/*(s^\k))=0 by A1,A13,Def13;
  then
A17: (f/*(s^\k))" is divergent_to-infty by A16,Th63;
  (f/*(s^\k))"=((f/*s)^\k)" by A8,A11,VALUED_0:27,XBOOLE_1:1
    .=((f/*s)")^\k by SEQM_3:41
    .=((f^)/*s)^\k by A8,RFUNCT_2:27;
  hence thesis by A17,Th34;
end;

theorem
  f is convergent_in+infty & lim_in+infty f=0 & (ex r st for g st g in
  dom f /\ right_open_halfline(r) holds 0<f.g) implies f^ is
  divergent_in+infty_to+infty
proof
  assume that
A1: f is convergent_in+infty and
A2: lim_in+infty f=0;
  given r such that
A3: for g st g in dom f/\right_open_halfline(r) holds 0<f.g;
  thus for r1 ex g1 st r1<g1 & g1 in dom(f^)
  proof
    let r1;
    consider g1 such that
A4: r1<g1 and
    g1 in dom f by A1,Def6;
    now
      per cases;
      suppose
A5:     g1<=r;
        consider g2 such that
A6:     r<g2 and
A7:     g2 in dom f by A1,Def6;
        take g2;
        g1<g2 by A5,A6,XXREAL_0:2;
        hence r1<g2 by A4,XXREAL_0:2;
        g2 in {r2: r<r2} by A6;
        then g2 in right_open_halfline(r) by XXREAL_1:230;
        then g2 in dom f/\right_open_halfline(r) by A7,XBOOLE_0:def 4;
        then 0<>f.g2 by A3;
        then not f.g2 in {0} by TARSKI:def 1;
        then not g2 in f"{0} by FUNCT_1:def 13;
        then g2 in dom f\f"{0} by A7,XBOOLE_0:def 5;
        hence g2 in dom(f^) by RFUNCT_1:def 8;
      end;
      suppose
A8:     r<=g1;
        consider g2 such that
A9:     g1<g2 and
A10:    g2 in dom f by A1,Def6;
        take g2;
        thus r1<g2 by A4,A9,XXREAL_0:2;
        r<g2 by A8,A9,XXREAL_0:2;
        then g2 in {r2: r<r2};
        then g2 in right_open_halfline(r) by XXREAL_1:230;
        then g2 in dom f/\right_open_halfline(r) by A10,XBOOLE_0:def 4;
        then 0<>f.g2 by A3;
        then not f.g2 in {0} by TARSKI:def 1;
        then not g2 in f"{0} by FUNCT_1:def 13;
        then g2 in dom f\f"{0} by A10,XBOOLE_0:def 5;
        hence g2 in dom(f^) by RFUNCT_1:def 8;
      end;
    end;
    hence thesis;
  end;
  let s be Real_Sequence;
  assume that
A11: s is divergent_to+infty and
A12: rng s c=dom (f^);
  consider k such that
A13: for n st k<=n holds r<s.n by A11,Def4;
A14: rng(s^\k)c=rng s by VALUED_0:21;
  dom(f^)=dom f\f"{0} by RFUNCT_1:def 8;
  then
A15: dom(f^)c=dom f by XBOOLE_1:36;
  then
A16: rng s c=dom f by A12,XBOOLE_1:1;
  then
A17: rng(s^\k)c=dom f by A14,XBOOLE_1:1;
  now
    let n;
    r<s.(n+k) by A13,NAT_1:12;
    then r<(s^\k).n by NAT_1:def 3;
    then (s^\k).n in {g2: r<g2};
    then (s^\k).n in rng(s^\k) & (s^\k).n in right_open_halfline(r) by
    VALUED_0:28,XXREAL_1:230;
    then (s^\k).n in dom f/\right_open_halfline(r) by A17,XBOOLE_0:def 4;
    then 0<f.((s^\k).n) by A3;
    hence 0<(f/*(s^\k)).n by A16,A14,FUNCT_2:185,XBOOLE_1:1;
  end;
  then
A18: for n holds 0<=n implies 0<(f/*(s^\k)).n;
  s^\k is divergent_to+infty by A11,Th53;
  then f/*(s^\k) is convergent & lim(f/*(s^\k))=0 by A1,A2,A17,Def12;
  then
A19: (f/*(s^\k))" is divergent_to+infty by A18,Th62;
  (f/*(s^\k))"=((f/*s)^\k)" by A12,A15,VALUED_0:27,XBOOLE_1:1
    .=((f/*s)")^\k by SEQM_3:41
    .=((f^)/*s)^\k by A12,RFUNCT_2:27;
  hence thesis by A19,Th34;
end;

theorem
  f is convergent_in+infty & lim_in+infty f=0 & (ex r st for g st g in
  dom f /\ right_open_halfline(r) holds f.g<0) implies f^ is
  divergent_in+infty_to-infty
proof
  assume that
A1: f is convergent_in+infty and
A2: lim_in+infty f=0;
  given r such that
A3: for g st g in dom f/\right_open_halfline(r) holds f.g<0;
  thus for r1 ex g1 st r1<g1 & g1 in dom(f^)
  proof
    let r1;
    consider g1 such that
A4: r1<g1 and
    g1 in dom f by A1,Def6;
    now
      per cases;
      suppose
A5:     g1<=r;
        consider g2 such that
A6:     r<g2 and
A7:     g2 in dom f by A1,Def6;
        take g2;
        g1<g2 by A5,A6,XXREAL_0:2;
        hence r1<g2 by A4,XXREAL_0:2;
        g2 in {r2: r<r2} by A6;
        then g2 in right_open_halfline(r) by XXREAL_1:230;
        then g2 in dom f/\right_open_halfline(r) by A7,XBOOLE_0:def 4;
        then 0<>f.g2 by A3;
        then not f.g2 in {0} by TARSKI:def 1;
        then not g2 in f"{0} by FUNCT_1:def 13;
        then g2 in dom f\f"{0} by A7,XBOOLE_0:def 5;
        hence g2 in dom(f^) by RFUNCT_1:def 8;
      end;
      suppose
A8:     r<=g1;
        consider g2 such that
A9:     g1<g2 and
A10:    g2 in dom f by A1,Def6;
        take g2;
        thus r1<g2 by A4,A9,XXREAL_0:2;
        r<g2 by A8,A9,XXREAL_0:2;
        then g2 in {r2: r<r2};
        then g2 in right_open_halfline(r) by XXREAL_1:230;
        then g2 in dom f/\right_open_halfline(r) by A10,XBOOLE_0:def 4;
        then 0<>f.g2 by A3;
        then not f.g2 in {0} by TARSKI:def 1;
        then not g2 in f"{0} by FUNCT_1:def 13;
        then g2 in dom f\f"{0} by A10,XBOOLE_0:def 5;
        hence g2 in dom(f^) by RFUNCT_1:def 8;
      end;
    end;
    hence thesis;
  end;
  let s be Real_Sequence;
  assume that
A11: s is divergent_to+infty and
A12: rng s c=dom (f^);
  consider k such that
A13: for n st k<=n holds r<s.n by A11,Def4;
A14: rng(s^\k)c=rng s by VALUED_0:21;
  dom(f^)=dom f\f"{0} by RFUNCT_1:def 8;
  then
A15: dom(f^)c=dom f by XBOOLE_1:36;
  then
A16: rng s c=dom f by A12,XBOOLE_1:1;
  then
A17: rng(s^\k)c=dom f by A14,XBOOLE_1:1;
  now
    let n;
    r<s.(n+k) by A13,NAT_1:12;
    then r<(s^\k).n by NAT_1:def 3;
    then (s^\k).n in {g2: r<g2};
    then (s^\k).n in rng(s^\k) & (s^\k).n in right_open_halfline(r) by
    VALUED_0:28,XXREAL_1:230;
    then (s^\k).n in dom f/\right_open_halfline(r) by A17,XBOOLE_0:def 4;
    then f.((s^\k).n)<0 by A3;
    hence (f/*(s^\k)).n<0 by A16,A14,FUNCT_2:185,XBOOLE_1:1;
  end;
  then
A18: for n holds 0<=n implies (f/*(s^\k)).n<0;
  s^\k is divergent_to+infty by A11,Th53;
  then f/*(s^\k) is convergent & lim(f/*(s^\k))=0 by A1,A2,A17,Def12;
  then
A19: (f/*(s^\k))" is divergent_to-infty by A18,Th63;
  (f/*(s^\k))"=((f/*s)^\k)" by A12,A15,VALUED_0:27,XBOOLE_1:1
    .=((f/*s)")^\k by SEQM_3:41
    .=((f^)/*s)^\k by A12,RFUNCT_2:27;
  hence thesis by A19,Th34;
end;

theorem
  f is convergent_in-infty & lim_in-infty f=0 & (ex r st for g st g in
  dom f /\ left_open_halfline(r) holds 0<f.g) implies f^ is
  divergent_in-infty_to+infty
proof
  assume that
A1: f is convergent_in-infty and
A2: lim_in-infty f=0;
  given r such that
A3: for g st g in dom f/\left_open_halfline(r) holds 0<f.g;
  thus for r1 ex g1 st g1<r1 & g1 in dom(f^)
  proof
    let r1;
    consider g1 such that
A4: g1<r1 and
    g1 in dom f by A1,Def9;
    now
      per cases;
      suppose
A5:     g1<=r;
        consider g2 such that
A6:     g2<g1 and
A7:     g2 in dom f by A1,Def9;
        take g2;
        thus g2<r1 by A4,A6,XXREAL_0:2;
        g2<r by A5,A6,XXREAL_0:2;
        then g2 in {r2: r2<r};
        then g2 in left_open_halfline(r) by XXREAL_1:229;
        then g2 in dom f/\left_open_halfline(r) by A7,XBOOLE_0:def 4;
        then 0<>f.g2 by A3;
        then not f.g2 in {0} by TARSKI:def 1;
        then not g2 in f"{0} by FUNCT_1:def 13;
        then g2 in dom f\f"{0} by A7,XBOOLE_0:def 5;
        hence g2 in dom(f^) by RFUNCT_1:def 8;
      end;
      suppose
A8:     r<=g1;
        consider g2 such that
A9:     g2<r and
A10:    g2 in dom f by A1,Def9;
        take g2;
        g2<g1 by A8,A9,XXREAL_0:2;
        hence g2<r1 by A4,XXREAL_0:2;
        g2 in {r2: r2<r} by A9;
        then g2 in left_open_halfline(r) by XXREAL_1:229;
        then g2 in dom f/\left_open_halfline(r) by A10,XBOOLE_0:def 4;
        then 0<>f.g2 by A3;
        then not f.g2 in {0} by TARSKI:def 1;
        then not g2 in f"{0} by FUNCT_1:def 13;
        then g2 in dom f\f"{0} by A10,XBOOLE_0:def 5;
        hence g2 in dom(f^) by RFUNCT_1:def 8;
      end;
    end;
    hence thesis;
  end;
  let s be Real_Sequence;
  assume that
A11: s is divergent_to-infty and
A12: rng s c=dom (f^);
  consider k such that
A13: for n st k<=n holds s.n<r by A11,Def5;
A14: rng(s^\k)c=rng s by VALUED_0:21;
  dom(f^)=dom f\f"{0} by RFUNCT_1:def 8;
  then
A15: dom(f^)c=dom f by XBOOLE_1:36;
  then
A16: rng s c=dom f by A12,XBOOLE_1:1;
  then
A17: rng(s^\k)c=dom f by A14,XBOOLE_1:1;
  now
    let n;
    s.(n+k)<r by A13,NAT_1:12;
    then (s^\k).n<r by NAT_1:def 3;
    then (s^\k).n in {g2: g2<r};
    then (s^\k).n in rng(s^\k) & (s^\k).n in left_open_halfline(r) by
    VALUED_0:28,XXREAL_1:229;
    then (s^\k).n in dom f/\left_open_halfline(r) by A17,XBOOLE_0:def 4;
    then 0<f.((s^\k).n) by A3;
    hence 0<(f/*(s^\k)).n by A16,A14,FUNCT_2:185,XBOOLE_1:1;
  end;
  then
A18: for n holds 0<=n implies 0<(f/*(s^\k)).n;
  s^\k is divergent_to-infty by A11,Th54;
  then f/*(s^\k) is convergent & lim(f/*(s^\k))=0 by A1,A2,A17,Def13;
  then
A19: (f/*(s^\k))" is divergent_to+infty by A18,Th62;
  (f/*(s^\k))"=((f/*s)^\k)" by A12,A15,VALUED_0:27,XBOOLE_1:1
    .=((f/*s)")^\k by SEQM_3:41
    .=((f^)/*s)^\k by A12,RFUNCT_2:27;
  hence thesis by A19,Th34;
end;

theorem
  f is convergent_in-infty & lim_in-infty f=0 & (ex r st for g st g in
  dom f /\ left_open_halfline(r) holds f.g<0) implies f^ is
  divergent_in-infty_to-infty
proof
  assume that
A1: f is convergent_in-infty and
A2: lim_in-infty f=0;
  given r such that
A3: for g st g in dom f/\left_open_halfline(r) holds f.g<0;
  thus for r1 ex g1 st g1<r1 & g1 in dom(f^)
  proof
    let r1;
    consider g1 such that
A4: g1<r1 and
    g1 in dom f by A1,Def9;
    now
      per cases;
      suppose
A5:     g1<=r;
        consider g2 such that
A6:     g2<g1 and
A7:     g2 in dom f by A1,Def9;
        take g2;
        thus g2<r1 by A4,A6,XXREAL_0:2;
        g2<r by A5,A6,XXREAL_0:2;
        then g2 in {r2: r2<r};
        then g2 in left_open_halfline(r) by XXREAL_1:229;
        then g2 in dom f/\left_open_halfline(r) by A7,XBOOLE_0:def 4;
        then 0<>f.g2 by A3;
        then not f.g2 in {0} by TARSKI:def 1;
        then not g2 in f"{0} by FUNCT_1:def 13;
        then g2 in dom f\f"{0} by A7,XBOOLE_0:def 5;
        hence g2 in dom(f^) by RFUNCT_1:def 8;
      end;
      suppose
A8:     r<=g1;
        consider g2 such that
A9:     g2<r and
A10:    g2 in dom f by A1,Def9;
        take g2;
        g2<g1 by A8,A9,XXREAL_0:2;
        hence g2<r1 by A4,XXREAL_0:2;
        g2 in {r2: r2<r} by A9;
        then g2 in left_open_halfline(r) by XXREAL_1:229;
        then g2 in dom f/\left_open_halfline(r) by A10,XBOOLE_0:def 4;
        then 0<>f.g2 by A3;
        then not f.g2 in {0} by TARSKI:def 1;
        then not g2 in f"{0} by FUNCT_1:def 13;
        then g2 in dom f\f"{0} by A10,XBOOLE_0:def 5;
        hence g2 in dom(f^) by RFUNCT_1:def 8;
      end;
    end;
    hence thesis;
  end;
  let s be Real_Sequence;
  assume that
A11: s is divergent_to-infty and
A12: rng s c=dom (f^);
  consider k such that
A13: for n st k<=n holds s.n<r by A11,Def5;
A14: rng(s^\k)c=rng s by VALUED_0:21;
  dom(f^)=dom f\f"{0} by RFUNCT_1:def 8;
  then
A15: dom(f^)c=dom f by XBOOLE_1:36;
  then
A16: rng s c=dom f by A12,XBOOLE_1:1;
  then
A17: rng(s^\k)c=dom f by A14,XBOOLE_1:1;
  now
    let n;
    s.(n+k)<r by A13,NAT_1:12;
    then (s^\k).n<r by NAT_1:def 3;
    then (s^\k).n in {g2: g2<r};
    then (s^\k).n in rng(s^\k) & (s^\k).n in left_open_halfline(r) by
    VALUED_0:28,XXREAL_1:229;
    then (s^\k).n in dom f/\left_open_halfline(r) by A17,XBOOLE_0:def 4;
    then f.((s^\k).n)<0 by A3;
    hence (f/*(s^\k)).n<0 by A16,A14,FUNCT_2:185,XBOOLE_1:1;
  end;
  then
A18: for n holds 0<=n implies (f/*(s^\k)).n<0;
  s^\k is divergent_to-infty by A11,Th54;
  then f/*(s^\k) is convergent & lim(f/*(s^\k))=0 by A1,A2,A17,Def13;
  then
A19: (f/*(s^\k))" is divergent_to-infty by A18,Th63;
  (f/*(s^\k))"=((f/*s)^\k)" by A12,A15,VALUED_0:27,XBOOLE_1:1
    .=((f/*s)")^\k by SEQM_3:41
    .=((f^)/*s)^\k by A12,RFUNCT_2:27;
  hence thesis by A19,Th34;
end;
