:: Conjugate Sequences, Bounded Complex Sequences and Convergent
:: Complex Sequences
::  by Adam Naumowicz
::
:: Received December 20, 1996
:: Copyright (c) 1996 Association of Mizar Users
 
environ

 vocabularies NUMBERS, SUBSET_1, REAL_1, COMSEQ_1, COMPLEX1, RELAT_1, ARYTM_1,
      ARYTM_3, CARD_1, XXREAL_0, FUNCT_1, XREAL_0, ORDINAL1, XBOOLE_0,
      PARTFUN1, VALUED_0, VALUED_1, XXREAL_2, FUNCOP_1, SEQ_2, ORDINAL2, SEQ_1,
      XCMPLX_0;
 notations XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0, COMPLEX1,
      REAL_1, FUNCT_1, RELSET_1, PARTFUN1, FUNCT_2, FUNCOP_1, VALUED_1, SEQ_1,
      SEQ_2, NAT_1, COMSEQ_1, XXREAL_0;
 constructors PARTFUN1, FUNCOP_1, REAL_1, SQUARE_1, NAT_1, COMPLEX1, SEQ_2,
      VALUED_1, SEQ_1, RELSET_1;
 registrations XBOOLE_0, ORDINAL1, RELSET_1, FUNCT_2, NUMBERS, XXREAL_0,
      XREAL_0, MEMBERED, VALUED_1, VALUED_0;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions VALUED_1, SEQ_2, PARTFUN1;
 theorems COMSEQ_1, SEQ_2, FINSEQ_2, COMPLEX1, NAT_1, SUBSET_1, FUNCT_2,
      XREAL_0, XCMPLX_0, XCMPLX_1, FUNCOP_1, XREAL_1, XXREAL_0, VALUED_1;
 schemes NAT_1, SEQ_1;

begin :: Preliminaries

reserve n,n1,n2,m for Element of NAT;
reserve r,g1,g2,g,g9 for Element of COMPLEX;
reserve R,R2 for Real;
reserve s,s9,s1 for Complex_Sequence;

theorem Th1:
  g<>0c & r<>0c implies |.g"-r".|=|.g-r.|/(|.g.|*|.r.|)
proof
  assume
A1: g<>0c & r<>0c;
  thus |.g"-r".|=|.1r/g-r".| by COMPLEX1:def 7,XCMPLX_1:217
    .=|.1r/g-1r/r.| by COMPLEX1:def 7,XCMPLX_1:217
    .=|.1r/g+-1r/r.|
    .=|.1r/g+-1r*r".| by XCMPLX_0:def 9
    .=|.1r/g+(-1r)*r".|
    .=|.1r/g+(-1r)/r.| by XCMPLX_0:def 9
    .=|.(1r*r+(-1r)*g)/(r*g).| by A1,XCMPLX_1:117
    .=|.r-g.|/|.g*r.| by COMPLEX1:153,def 7
    .=|.-(g-r).|/(|.g.|*|.r.|) by COMPLEX1:151
    .=|.g-r.|/(|.g.|*|.r.|) by COMPLEX1:138;
end;

theorem Th2:
  for n ex r being Real st 0<r & for m st m<=n holds |.s.m.|<r
proof
  let n;
  defpred P[Element of NAT] means ex r being real number st 0<r & for m st m<=
  $1 holds |.s.m.|<r;
A1: for n st P[n] holds P[n+1]
  proof
    let n;
    given R1 be real number such that
A2: 0<R1 and
A3: for m st m <= n holds |.s.m.| < R1;
A4: now
      assume
A5:   R1 <= |.s.(n+1).|;
      take R= |.s.(n+1).| +1;
      0+0<R by COMPLEX1:132,XREAL_1:10;
      hence 0<R;
      let m such that
A6:   m<=n+1;
A7:   now
        assume m<=n;
        then |.s.m.| <R1 by A3;
        then |.s.m.| < |.s.(n+1).| by A5,XXREAL_0:2;
        then |.s.m.| + 0 < R by XREAL_1:10;
        hence |.s.m.| < R;
      end;
      now
        assume m=n+1;
        then |.s.m.| +0 <R by XREAL_1:10;
        hence |.s.m.| < R;
      end;
      hence |.s.m.| <R by A6,A7,NAT_1:8;
    end;
    now
      assume
A8:   |.s.(n+1).| <= R1;
      take R=R1+1;
      thus R>0 by A2;
      let m such that
A9:   m <= n+1;
A10:  now
        assume m <= n;
        then
A11:    |.s.m.| < R1 by A3;
        R1+0<R by XREAL_1:10;
        hence |.s.m.| < R by A11,XXREAL_0:2;
      end;
      now
A12:    R1+0 <R by XREAL_1:10;
        assume m=n+1;
        hence |.s.m.| < R by A8,A12,XXREAL_0:2;
      end;
      hence |.s.m.| < R by A9,A10,NAT_1:8;
    end;
    hence thesis by A4;
  end;
A13: P[0]
  proof
    take R=|.s.0.|+1;
    0+0 < |.s.0.| +1 by COMPLEX1:132,XREAL_1:10;
    hence 0<R;
    let m;
    assume m <= 0;
    then m=0 by NAT_1:2;
    then |.s.m.| +0 < R by XREAL_1:10;
    hence thesis;
  end;
  for n holds P[n] from NAT_1:sch 1(A13,A1);
  then consider R being real number such that
A14: R>0 & for m st m<=n holds |.s.m.|<R;
  R is Real by XREAL_0:def 1;
  hence thesis by A14;
end;

begin :: Conjugate Sequences

definition
  let C be non empty set;
  let f be PartFunc of C,COMPLEX;
  deffunc F(set) = (f/.$1)*';
  func f*' ->PartFunc of C,COMPLEX means
  :Def1:
  dom it = dom f & for c being
  Element of C st c in dom it holds it.c = (f/.c)*';
  existence
  proof
    defpred P[set] means $1 in dom f;
    consider F being PartFunc of C,COMPLEX such that
A1: for c being Element of C holds c in dom F iff P[c] and
A2: for c being Element of C st c in dom F holds F.c = F(c) from SEQ_1
    :sch 3;
    take F;
    thus dom f = dom F by A1,SUBSET_1:8;
    thus thesis by A2;
  end;
  uniqueness
  proof
    thus for h,g being PartFunc of C,COMPLEX st (dom h=dom f & for c be
Element of C st c in dom h holds h.c = F(c)) & (dom g=dom f & for c be Element
    of C st c in dom g holds g.c = F(c)) holds h = g from SEQ_1:sch 4;
  end;
end;

definition
  let C be non empty set;
  let f be Function of C,COMPLEX;
  redefine func f*' means
  :Def2:
  dom it = C & for n being Element of C holds it.n=(f.n)*';
  compatibility
  proof
    let IT be PartFunc of C,COMPLEX;
    thus IT = f*' implies dom IT = C & for c being Element of C holds IT.c = (
    f.c)*'
    proof
      assume
A1:   IT = f*';
      hence
A2:   dom IT = dom f by Def1
        .= C by FUNCT_2:def 1;
      let c be Element of C;
      f.c = f/.c;
      hence thesis by A1,A2,Def1;
    end;
    assume dom IT = C & for c being Element of C holds IT.c = (f.c)*';
    then
    dom IT = dom f & for c being Element of C st c in dom IT holds IT.c =
    (f/.c) *' by FUNCT_2:def 1;
    hence IT = f*' by Def1;
  end;
end;

registration
  let C be non empty set;
  let seq be Function of C,COMPLEX;
  cluster seq*' -> total;
  coherence
  proof
    thus dom(seq*') = dom seq by Def1
      .= C by FUNCT_2:def 1;
  end;
end;

theorem
  s is non-zero implies s*' is non-zero
proof
  assume
A1: s is non-zero;
  now
    let n;
    s.n <> 0c by A1,COMSEQ_1:4;
    then (s.n)*' <>0c by COMPLEX1:114;
    hence s*'.n <>0c by Def2;
  end;
  hence thesis by COMSEQ_1:4;
end;

theorem
  (r(#)s)*' = (r*')(#)(s*')
proof
  now
    let n;
    thus (r(#)s)*'.n = ((r(#)s).n)*' by Def2
      .= (r*s.n)*' by VALUED_1:6
      .= (r*')*(s.n)*' by COMPLEX1:121
      .= (r*')*(s*'.n) by Def2
      .= ((r*')(#)(s*')).n by VALUED_1:6;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem
  (s (#) s9)*' = (s*') (#) (s9*')
proof
  now
    let n;
    thus (s (#) s9)*'.n = ((s (#) s9).n)*' by Def2
      .= (s.n * s9.n)*' by VALUED_1:5
      .= (s.n)*' * (s9.n)*' by COMPLEX1:121
      .= (s*'.n) * (s9.n)*' by Def2
      .= (s*'.n) * (s9*'.n) by Def2
      .= (s*' (#) s9*').n by VALUED_1:5;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem
  (s*')" = (s")*'
proof
  now
    let n;
    thus (s*')".n = (s*'.n)" by VALUED_1:10
      .= ((s.n)*')" by Def2
      .= ((s.n)")*' by COMPLEX1:122
      .= (s".n)*' by VALUED_1:10
      .= (s")*'.n by Def2;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem
  (s9/"s)*' = (s9*') /" (s*')
proof
  now
    let n;
    thus (s9/"s)*'.n = ((s9 (#) s").n)*' by Def2
      .= ((s9.n) * (s".n))*' by VALUED_1:5
      .= ((s9.n) * (s.n)")*' by VALUED_1:10
      .= (s9.n)*' * ((s.n)")*' by COMPLEX1:121
      .= (s9.n)*' * ((s.n)*')" by COMPLEX1:122
      .= (s9*').n * ((s.n)*')" by Def2
      .= (s9*').n * ((s*').n)" by Def2
      .= (s9*').n * ((s*')").n by VALUED_1:10
      .= ((s9*') /" (s*')).n by VALUED_1:5;
  end;
  hence thesis by FUNCT_2:113;
end;

begin :: BOUNDED COMPLEX SEQUENCES

definition
  let s;
  redefine attr s is bounded means
  :Def3:
  ex r being Real st for n holds |.s.n .|<r;
  compatibility
  proof
    thus s is bounded implies ex r being Real st for n holds |.s.n.|<r
    proof
      given r being real number such that
A1:   for y being set st y in dom s holds abs(s.y) < r;
      reconsider r as Element of REAL by XREAL_0:def 1;
      take r;
      let n;
      n in NAT;
      then n in dom s by FUNCT_2:def 1;
      hence thesis by A1;
    end;
    given r being Real such that
A2: for n holds |.s.n.|<r;
    take r;
    let y being set;
    assume y in dom s;
    hence thesis by A2;
  end;
end;

reconsider sc = NAT --> 0c as Complex_Sequence;

Lm1: for n holds sc.n = 0c by FUNCOP_1:13;

registration
  cluster bounded Complex_Sequence;
  existence
  proof
    take sc, 1;
    let n;
    thus thesis by COMPLEX1:130,FUNCOP_1:13;
  end;
end;

theorem Th8:
  s is bounded iff ex r being Real st (0<r & for n holds |.s.n.|<r)
proof
  thus s is bounded implies ex r being Real st (0<r & for n holds |.s.n.|<r)
  proof
    assume s is bounded;
    then consider r being Real such that
A1: for n holds |.s.n.|<r by Def3;
    take r;
    now
      let n;
      0 <= |.s.n.| by COMPLEX1:132;
      hence 0< r by A1;
    end;
    hence thesis by A1;
  end;
  thus thesis by Def3;
end;

begin
 
::   Convergent Complex Sequences
::  The Limit of Complex Sequences

definition
  let s;
  attr s is convergent means
  :Def4:
  ex g st for p be Real st 0<p ex n st for m st n<=m holds |.s.m-g.|<p;
end;

definition
  let s;
  assume
A1: s is convergent;
  func lim s -> Element of COMPLEX means
  :Def5:
  for p be Real st 0<p ex n st for m st n<=m holds |.s.m-it.|<p;
  existence by A1,Def4;
  uniqueness
  proof
    let g1,g2;
    assume that
A2: for p be Real st 0<p ex n1 st for m st n1<=m holds |.s.m-g1.|<p and
A3: for p be Real st 0<p ex n2 st for m st n2<=m holds |.s.m-g2.|<p and
A4: g1<>g2;
    reconsider p = |.g1 - g2.|/2 as Real;
A5: |.g1-g2.| > 0 by A4,COMPLEX1:148;
    then consider n1 such that
A6: for m st n1<=m holds |.s.m-g1.|<p by A2,XREAL_1:141;
    consider n2 such that
A7: for m st n2<=m holds |.s.m-g2.|<p by A3,A5,XREAL_1:141;
    reconsider n = max(n1,n2) as Element of NAT by FINSEQ_2:1;
    for m st n <= m holds 2*p<2*p
    proof
      let m;
      assume
A8:   n <= m;
      n2<=n by XXREAL_0:25;
      then n + n2 <= n + m by A8,XREAL_1:9;
      then n2<=m by XREAL_1:8;
      then
A9:   |.s.m - g2.|< p by A7;
      |.g1 - g2.| = |.g1 - s.m - (g2 - s.m).|;
      then |.g1 - g2.| <= |.g1-s.m.| + |.g2-s.m.| by COMPLEX1:143;
      then
A10:  |.g1 - g2.| <= |.s.m - g1.| + |.g2-s.m.| by COMPLEX1:146;
      n1<=n by XXREAL_0:25;
      then n + n1 <= n + m by A8,XREAL_1:9;
      then n1<=m by XREAL_1:8;
      then |.s.m - g1.|< p by A6;
      then |.s.m - g1.| + |.s.m - g2.| <p + p by A9,XREAL_1:10;
      hence thesis by A10,COMPLEX1:146;
    end;
    hence contradiction;
  end;
end;

theorem Th9:
  (ex g st for n being Element of NAT holds s.n = g) implies s is convergent
proof
  given g such that
A1: for n being Element of NAT holds s.n = g;
  take g;
  now
    let p be Real such that
A2: 0<p;
    take k = 0;
    let n such that
    k<=n;
    s.n = g by A1;
    hence |.s.n - g.| < p by A2,COMPLEX1:130;
  end;
  hence thesis;
end;

theorem Th10:
  for g st for n being Element of NAT holds s.n = g holds lim s = g
proof
  let g;
  assume
A1: for n being Element of NAT holds s.n = g;
A2: now
    let p be Real such that
A3: 0<p;
    take k = 0;
    let n such that
    k<=n;
    s.n = g by A1;
    hence |.s.n - g.| < p by A3,COMPLEX1:130;
  end;
  s is convergent by A1,Th9;
  hence thesis by A2,Def5;
end;

registration
  cluster convergent Complex_Sequence;
  existence by Lm1,Th9;
end;

registration
  let s be convergent Complex_Sequence;
  cluster |.s.| -> convergent Real_Sequence;
  coherence
  proof
    |.s.| is convergent
    proof
      consider g such that
A1:   for p be Real st 0<p ex n st for m st n<=m holds |.s.m-g.|<p by
      Def4;
      take |.g.|;
      let p be real number;
      assume
A2:   0<p;
      p is Real by XREAL_0:def 1;
      then consider n such that
A3:   for m st n<=m holds |.s.m-g.|<p by A1,A2;
      take n;
      let m;
      assume n<=m;
      then |.s.m-g.|<p by A3;
      then abs(|.s.m.|- |.g.|) + |.s.m-g.| < p+|.s.m-g.| by COMPLEX1:150,
      XREAL_1:10;
      then
      abs(|.s.m.|- |.g.|) + |.s.m-g.|- |.s.m-g.| < p+|.s.m-g.|- |.s.m-g.|
      by XREAL_1:11;
      hence thesis by VALUED_1:18;
    end;
    hence thesis;
  end;
end;

theorem Th11:
  s is convergent implies lim |.s.| = |.lim s.|
proof
  set g = lim s;
  assume
A1: s is convergent;
  then reconsider s1 = s as convergent Complex_Sequence;
  reconsider w = |.s1.| as Real_Sequence;
A2: w is convergent;
  reconsider w = |.lim s.| as Real;
  now
    let p be real number;
    assume
A3: 0<p;
    p is Real by XREAL_0:def 1;
    then consider n such that
A4: for m st n<=m holds |.s.m-g.|<p by A1,A3,Def5;
    take n;
    let m;
    assume n<=m;
    then |.s.m-g.|<p by A4;
    then abs(|.s.m.|- |.g.|) + |.s.m-g.| < p+|.s.m-g.| by COMPLEX1:150,
    XREAL_1:10;
    then abs(|.s.m.|- |.g.|) + |.s.m-g.|- |.s.m-g.| < p+|.s.m-g.|- |.s.m-g.|
    by XREAL_1:11;
    hence abs(|.s.|.m - w) < p by VALUED_1:18;
  end;
  hence thesis by A2,SEQ_2:def 7;
end;

registration
  let s be convergent Complex_Sequence;
  cluster s*' -> convergent;
  coherence
  proof
    consider g such that
A1: for p being Real st 0<p ex n st for m st n<=m holds |.s.m-g.|<p by
    Def4;
    take r = g*';
    let p be Real;
    assume 0<p;
    then consider n such that
A2: for m st n<=m holds |.s.m-g.|<p by A1;
    take n;
    let m such that
A3: n<=m;
    |.(s*').m - r.| = |.(s.m)*' - g*'.| by Def2
      .= |.(s.m - g)*'.| by COMPLEX1:120
      .= |.s.m-g.| by COMPLEX1:139;
    hence thesis by A2,A3;
  end;
end;

theorem Th12:
  s is convergent implies lim(s*') = (lim s)*'
proof
  set g = lim s;
  assume
A1: s is convergent;
  then reconsider s1 = s as convergent Complex_Sequence;
A2: now
    let p be Real;
    assume 0<p;
    then consider n such that
A3: for m st n<=m holds |.s.m-g.|<p by A1,Def5;
    take n;
    let m such that
A4: n<=m;
    |.(s*').m - g*'.| = |.(s.m)*' - g*'.| by Def2
      .= |.(s.m - g)*'.| by COMPLEX1:120
      .= |.s.m - g.| by COMPLEX1:139;
    hence |.(s*').m - (lim s)*'.| < p by A3,A4;
  end;
  s1*' is convergent;
  hence thesis by A2,Def5;
end;

begin :: Main Theorems

theorem Th13:
  s is convergent & s9 is convergent implies (s + s9) is convergent
proof
  assume that
A1: s is convergent and
A2: s9 is convergent;
  consider g such that
A3: for p be Real st 0<p ex n st for m st n<=m holds |.s.m-g.|<p by A1,
  Def4;
  consider g9 such that
A4: for p be Real st 0<p ex n st for m st n<=m holds |.s9.m-g9.|<p by A2
  ,Def4;
  take g1 = g + g9;
  let p be Real;
  assume
A5: p>0;
  then consider n1 such that
A6: for m st n1<=m holds |.s.m - g.|<p/2 by A3,XREAL_1:141;
  consider n2 such that
A7: for m st n2<=m holds |.s9.m - g9.|<p/2 by A4,A5,XREAL_1:141;
  reconsider n = max(n1,n2) as Element of NAT by FINSEQ_2:1;
  take n;
  let m such that
A8: n<=m;
  n2<=n by XXREAL_0:25;
  then n + n2 <= n + m by A8,XREAL_1:9;
  then n2<=m by XREAL_1:8;
  then
A9: |.s9.m - g9.|<p/2 by A7;
A10: |.(s + s9).m - g1.| = |.s.m + s9.m - (g + g9).| by VALUED_1:1
    .= |.(s.m - g) + (s9.m - g9).|;
  n1<=n by XXREAL_0:25;
  then n + n1 <= n + m by A8,XREAL_1:9;
  then n1<=m by XREAL_1:8;
  then |.s.m - g.|<p/2 by A6;
  then |.s.m - g.| + |.s9.m - g9.| < p/2 + p/2 by A9,XREAL_1:10;
  then |.s.m - g.| + |.s9.m - g9.| + |.(s + s9).m - g1.| < p + (|.s.m - g.| +
  |.s9.m - g9.|) by A10,COMPLEX1:142,XREAL_1:10;
  hence thesis by XREAL_1:8;
end;

theorem Th14:
  s is convergent & s9 is convergent implies lim (s + s9)=(lim s)+ (lim s9)
proof
  assume that
A1: s is convergent and
A2: s9 is convergent;
A3: for p be Real st 0<p ex n st for m st n<=m holds |.(s + s9).m-((lim s)+(
  lim s9)).|<p
  proof
    let p be Real;
    assume 0<p;
    then
A4: 0<p/2 by XREAL_1:141;
    then consider n1 such that
A5: for m st n1<=m holds |.s.m-lim s.|<p/2 by A1,Def5;
    consider n2 such that
A6: for m st n2<=m holds |.s9.m-lim s9.|<p/2 by A2,A4,Def5;
    reconsider n = max(n1,n2) as Element of NAT by FINSEQ_2:1;
    take n;
    let m such that
A7: n<=m;
    n2<=n by XXREAL_0:25;
    then n + n2 <= n + m by A7,XREAL_1:9;
    then n2<=m by XREAL_1:8;
    then
A8: |.s9.m-lim s9.|<p/2 by A6;
A9: |.(s + s9).m-((lim s)+(lim s9)).| = |.s.m + s9.m - ((lim s)+(lim s9))
    .| by VALUED_1:1
      .=|.(s.m -lim s) + (s9.m - lim s9).|;
    n1<=n by XXREAL_0:25;
    then n + n1 <= n + m by A7,XREAL_1:9;
    then n1<=m by XREAL_1:8;
    then |.s.m-lim s.|<p/2 by A5;
    then |.s.m-lim s.| + |.s9.m-lim s9.| < p/2 + p/2 by A8,XREAL_1:10;
    then |.s.m-lim s.| + |.s9.m -lim s9.| + |.(s + s9).m-((lim s)+(lim s9)).|
    < p + (|.s.m-lim s.| + |.s9.m -lim s9.|) by A9,COMPLEX1:142,XREAL_1:10;
    hence thesis by XREAL_1:8;
  end;
  s + s9 is convergent by A1,A2,Th13;
  hence thesis by A3,Def5;
end;

theorem
  s is convergent & s9 is convergent implies lim |.(s + s9).| = |.(lim s
  )+(lim s9).|
proof
  assume
A1: s is convergent & s9 is convergent;
  then s + s9 is convergent by Th13;
  hence lim |.(s + s9).| = |.lim (s + s9).| by Th11
    .= |.(lim s)+(lim s9).| by A1,Th14;
end;

theorem
  s is convergent & s9 is convergent implies lim (s + s9)*' = (lim s)*'
  + (lim s9)*'
proof
  assume
A1: s is convergent & s9 is convergent;
  then s + s9 is convergent by Th13;
  hence lim (s + s9)*' = (lim (s + s9))*' by Th12
    .= ((lim s) + (lim s9))*' by A1,Th14
    .= (lim s)*' + (lim s9)*' by COMPLEX1:118;
end;

registration
  let s be convergent Complex_Sequence, c be complex number;
  cluster c(#)s -> convergent Complex_Sequence;
  coherence
  proof
A1: now
      let c be Element of COMPLEX;
      per cases;
      suppose
A2:     c =0c;
        now
          let n;
          thus (c(#)s).n = 0c*s.n by A2,VALUED_1:6
            .= 0c;
        end;
        hence c(#)s is convergent by Th9;
      end;
      suppose
A3:     c <> 0c;
        thus c(#)s is convergent
        proof
          consider g such that
A4:       for p be Real st 0<p ex n st for m st n<=m holds |.s.m-g.|<
          p by Def4;
          take g9= c*g;
          let p be Real such that
A5:       0<p;
A6:       |.c.|>0 by A3,COMPLEX1:133;
          p/|.c.| = p*(|.c.|)" by XCMPLX_0:def 9;
          then consider n such that
A7:       for m st n<=m holds |.s.m-g.|<p/|.c.| by A6,A4,A5,
          XREAL_1:131;
          take n;
          let m;
          assume n<=m;
          then |.s.m-g.|<p/|.c.| by A7;
          then |.s.m-g.|*|.c.|<p / |.c.|*|.c.| by A6,XREAL_1:70;
          then |.s.m-g.|*|.c.|<p * (|.c.|)"*|.c.| by XCMPLX_0:def 9;
          then |.s.m-g.|*|.c.|<p * ((|.c.|)"*|.c.|);
          then
A8:       |.s.m-g.|*|.c.|<p * 1 by A6,XCMPLX_0:def 7;
          |.(c(#)s).m-g9.| = |.c*s.m-c*g.| by VALUED_1:6
            .= |.c*(s.m-g).|
            .= |.s.m-g.|*|.c.| by COMPLEX1:151;
          hence thesis by A8;
        end;
      end;
    end;
    c in COMPLEX by XCMPLX_0:def 2;
    hence thesis by A1;
  end;
end;

theorem
  s is convergent implies r(#)s is convergent;

theorem Th18:
  for r being complex number holds s is convergent implies lim(r
  (#)s)=r*(lim s)
proof
  let r be complex number;
  reconsider r as Element of COMPLEX by XCMPLX_0:def 2;
  assume
A1: s is convergent;
A2: now
    assume
A3: r<>0c;
    thus for p be Real st p>0 holds ex n st for m st n<=m holds |.(r(#)s)
    .m-r*(lim s).| <p
    proof
      let p be Real such that
A4:   p>0;
A5:   |.r.|>0 by A3,COMPLEX1:133;
      p/|.r.| = p*(|.r.|)" by XCMPLX_0:def 9;
      then p / |.r.| > 0 by A5,A4,XREAL_1:131;
      then consider n such that
A6:   for m st n<=m holds |.s.m-(lim s).|< p/|.r.| by A1,Def5;
      take n;
      let m;
      assume n<=m;
      then |.s.m-(lim s).|<p/|.r.| by A6;
      then |.s.m-(lim s).|*|.r.|<p / |.r.|*|.r.| by A5,XREAL_1:70;
      then |.s.m-(lim s).|*|.r.|< p * (|.r.|)"*|.r.| by XCMPLX_0:def 9;
      then |.s.m-(lim s).|*|.r.|< p * ((|.r.|)"*|.r.|);
      then
A7:   |.s.m-(lim s).|*|.r.|< p * 1 by A5,XCMPLX_0:def 7;
      |.(r(#)s).m - r*(lim s).|=|.r*s.m - r*(lim s).| by VALUED_1:6
        .= |. r *(s.m-(lim s)).|
        .= |.s.m-(lim s).|*|.r.| by COMPLEX1:151;
      hence thesis by A7;
    end;
    hence lim (r(#)s) = r * (lim s) by A1,Def5;
  end;
  now
    assume
A8: r=0c;
    now
      let n;
      thus (r(#)s).n = 0c*s.n by A8,VALUED_1:6
        .= 0c;
    end;
    hence lim (r(#)s) = r * (lim s) by A8,Th10;
  end;
  hence thesis by A2;
end;

theorem
  s is convergent implies lim |.(r(#)s).| = |.r.|*|.(lim s).|
proof
  assume
A1: s is convergent;
  hence lim |.(r(#)s).| = |.lim (r(#)s).| by Th11
    .= |.r*(lim s).| by A1,Th18
    .= |.r.|*|.(lim s).| by COMPLEX1:151;
end;

theorem
  s is convergent implies lim (r(#)s)*' = (r*')*(lim s)*'
proof
  assume
A1: s is convergent;
  hence lim (r(#)s)*' = (lim (r(#)s))*' by Th12
    .= (r*(lim s))*' by A1,Th18
    .= (r*')*(lim s)*' by COMPLEX1:121;
end;

theorem
  s is convergent implies - s is convergent;

theorem Th22:
  s is convergent implies lim(-s)=-(lim s)
proof
  assume s is convergent;
  then lim(-s) = (-1)*(lim s) by Th18
    .= - 1r*(lim s) by COMPLEX1:def 7;
  hence thesis by COMPLEX1:def 7;
end;

theorem
  s is convergent implies lim |.-s.| = |.lim s.|
proof
  assume
A1: s is convergent;
  hence lim |.-s.| = |.lim (-s).| by Th11
    .= |.-(lim s).| by A1,Th22
    .= |.lim s.| by COMPLEX1:138;
end;

theorem
  s is convergent implies lim (-s)*' = -(lim s)*'
proof
  assume
A1: s is convergent;
  hence lim (-s)*' = (lim (-s))*' by Th12
    .= (-(lim s))*' by A1,Th22
    .= -(lim s)*' by COMPLEX1:119;
end;

theorem Th25:
  s is convergent & s9 is convergent implies s - s9 is convergent
proof
  assume that
A1: s is convergent and
A2: s9 is convergent;
  - s9 is convergent by A2;
  hence thesis by A1,Th13;
end;

theorem Th26:
  s is convergent & s9 is convergent implies lim(s - s9)=(lim s)-( lim s9)
proof
  assume that
A1: s is convergent and
A2: s9 is convergent;
  lim(s - s9) = (lim s) + lim(-s9) by A1,A2,Th14
    .= (lim s) + -(lim s9) by A2,Th22;
  hence thesis;
end;

theorem
  s is convergent & s9 is convergent implies lim |.s - s9.| = |.(lim s)
  - (lim s9).|
proof
  assume
A1: s is convergent & s9 is convergent;
  then s - s9 is convergent by Th25;
  hence lim |.s - s9.| = |.lim (s - s9).| by Th11
    .= |.(lim s) - (lim s9).| by A1,Th26;
end;

theorem
  s is convergent & s9 is convergent implies lim (s - s9)*' = (lim s)*'
  - (lim s9)*'
proof
  assume
A1: s is convergent & s9 is convergent;
  then s - s9 is convergent by Th25;
  hence lim (s - s9)*' = (lim (s - s9))*' by Th12
    .= ((lim s) - (lim s9))*' by A1,Th26
    .= (lim s)*' - (lim s9)*' by COMPLEX1:120;
end;

registration
  cluster convergent -> bounded Complex_Sequence;
  coherence
  proof
    let s;
    assume s is convergent;
    then consider g such that
A1: for p be Real st 0<p ex n st for m st n<=m holds |.s.m-g.|<p by
    Def4;
    consider n1 such that
A2: for m st n1<=m holds |.s.m-g.|<1 by A1;
    now
      take R=|.g.| +1;
      0+0 < R by COMPLEX1:132,XREAL_1:10;
      hence 0<R;
      let m;
A3:   |.s.m.|- |.g.|+|.g.|=|.s.m.|;
      assume n1<=m;
      then
A4:   |.s.m-g.|<1 by A2;
      |.s.m.| - |.g.| <= |.s.m-g.| by COMPLEX1:145;
      then |.s.m.| - |.g.| < 1 by A4,XXREAL_0:2;
      hence |.s.m.|<R by A3,XREAL_1:8;
    end;
    then consider R1 be real number such that
A5: 0<R1 and
A6: for m st n1<=m holds |.s.m.|< R1;
    consider R2 such that
A7: 0<R2 and
A8: for m st m<=n1 holds |.s.m.|< R2 by Th2;
    take R=R1+R2;
    let m;
A9: R2+0 < R by A5,XREAL_1:10;
A10: now
      assume m<=n1;
      then |.s.m.|< R2 by A8;
      hence thesis by A9,XXREAL_0:2;
    end;
A11: R1+0 < R by A7,XREAL_1:10;
    now
      assume n1<=m;
      then |.s.m.|< R1 by A6;
      hence thesis by A11,XXREAL_0:2;
    end;
    hence thesis by A10;
  end;
end;

registration
  cluster non bounded -> non convergent Complex_Sequence;
  coherence;
end;

theorem Th29:
  s is convergent Complex_Sequence & s9 is convergent
  Complex_Sequence implies s (#) s9 is convergent
proof
  assume that
A1: s is convergent Complex_Sequence and
A2: s9 is convergent Complex_Sequence;
  consider g1 such that
A3: for p being Real st 0<p ex n st for m st n<=m holds |.s.m-g1.|<p by
  A1,Def4;
  consider g2 such that
A4: for p being Real st 0<p ex n st for m st n<=m holds |.s9.m-g2.|<p by
  A2,Def4;
  take g=g1*g2;
  let p be Real;
  consider R such that
A5: 0<R and
A6: for n holds |.s.n.|<R by A1,Th8;
A7: 0+0<|.g2.|+R by A5,COMPLEX1:132,XREAL_1:10;
  assume
A8: 0<p;
  then consider n1 such that
A9: for m st n1<=m holds |.s.m-g1.|<p/(|.g2.|+R) by A3,A7,XREAL_1:
  141;
  consider n2 such that
A10: for m st n2<=m holds |.s9.m-g2.|<p/(|.g2.|+R) by A4,A7,A8,XREAL_1:
  141;
  take n=n1+n2;
  let m such that
A11: n<=m;
  n1<=n1+n2 by NAT_1:12;
  then n1<=m by A11,XXREAL_0:2;
  then
A12: |.s.m-g1.|<=p/(|.g2.|+R) by A9;
  0<=|.g2.| & |.(s.m-g1)*g2.|=|.g2.|*|.s.m-g1.| by COMPLEX1:132,151;
  then
A13: |.(s.m-g1)*g2.|<=|.g2.|*(p/(|.g2.|+R)) by A12,XREAL_1:66;
  |.((s(#)s9).m)-g.|=|.(s.m*s9.m) -s.m*g2+s.m*g2-g1*g2.| by VALUED_1:5
    .=|.s.m*(s9.m-g2)+(s.m-g1)*g2.|;
  then
A14: |.((s(#)s9).m)-g.|<= |.s.m*(s9.m-g2).|+|.(s.m-g1)*g2.| by
  COMPLEX1
  :142;
  n2<=n by NAT_1:12;
  then n2<=m by A11,XXREAL_0:2;
  then
A15: |.s9.m-g2.|<p/(|.g2.|+R) by A10;
A16: 0<=|.s.m.| & 0<=|.s9.m-g2.| by COMPLEX1:132;
  |.s.m.|<R by A6;
  then |.s.m.|*|.s9.m-g2.|<R*(p/(|.g2.|+R)) by A16,A15,XREAL_1:98;
  then
A17: |.s.m*(s9.m-g2).|<R*(p/(|.g2.|+R)) by COMPLEX1:151;
  R*(p/(|.g2.|+R))+|.g2.|*(p/(|.g2.|+R)) =(p/(|.g2.|+R))*(|.g2.|+R)
    .=p by A7,XCMPLX_1:88;
  then |.s.m*(s9.m-g2).|+|.(s.m-g1)*g2.|<p by A17,A13,XREAL_1:10;
  hence thesis by A14,XXREAL_0:2;
end;

theorem Th30:
  s is convergent Complex_Sequence & s9 is convergent
  Complex_Sequence implies lim(s(#)s9)=(lim s)*(lim s9)
proof
  assume that
A1: s is convergent Complex_Sequence and
A2: s9 is convergent Complex_Sequence;
  consider R such that
A3: 0<R and
A4: for n holds |.s.n.|<R by A1,Th8;
A5: 0+0<|.(lim s9).|+R by A3,COMPLEX1:132,XREAL_1:10;
A6: 0<=|.(lim s9).| by COMPLEX1:132;
A7: now
    let p be Real;
    assume 0<p;
    then
A8: 0<p/(|.(lim s9).|+R) by A5,XREAL_1:141;
    then consider n1 such that
A9: for m st n1<=m holds |.s.m-(lim s).|<p/(|.(lim s9).|+R) by A1,
    Def5;
    consider n2 such that
A10: for m st n2<=m holds |.s9.m-(lim s9).|<p/(|.(lim s9).|+R) by A2,
    A8,Def5;
    take n=n1+n2;
    let m such that
A11: n<=m;
    n1<=n1+n2 by NAT_1:12;
    then n1<=m by A11,XXREAL_0:2;
    then
A12: |.s.m-(lim s).|<=p/(|.(lim s9).|+R) by A9;
    |.(s.m-(lim s))*(lim s9).| =|.(lim s9).|*|.s.m-(lim s).| by COMPLEX1:
    151;
    then
A13: |.(s.m-(lim s))*(lim s9).|<=|.(lim s9).|* (p/(|.(lim s9).|+R)) by A6,
    A12,XREAL_1:66;
A14: 0<=|.s.m.| & 0<=|.s9.m-(lim s9).| by COMPLEX1:132;
    n2<=n by NAT_1:12;
    then n2<=m by A11,XXREAL_0:2;
    then
A15: |.s9.m-(lim s9).|<p/(|.(lim s9).|+R) by A10;
    |.((s(#)s9).m)-(lim s)*(lim s9).| =|.(s.m*s9.m-s.m*(lim s9)+s.m*(lim
    s9))- (lim s)*(lim s9).| by VALUED_1:5
      .=|.s.m*(s9.m-(lim s9))+ (s.m-(lim s))*(lim s9).|;
    then
A16: |.((s(#)s9).m)-(lim s)*(lim s9).|<= |.s.m*(s9.m-(lim s9)).|+|.(s.m-(
    lim s))*(lim s9).| by COMPLEX1:142;
    |.s.m.|<R by A4;
    then |.s.m.|*|.s9.m-(lim s9).|<R*(p/(|.(lim s9).|+R)) by A14,A15,
    XREAL_1:98;
    then
A17: |.s.m*(s9.m-(lim s9)).|<R*(p/(|.(lim s9).|+R)) by COMPLEX1:151;
    R*(p/(|.(lim s9).|+R))+|.(lim s9).|*(p/(|.(lim s9).|+R)) =(p/(|.(lim
    s9).|+R))*(|.(lim s9).|+R)
      .=p by A5,XCMPLX_1:88;
    then |.s.m*(s9.m-(lim s9)).|+|.(s.m-(lim s))* (lim s9).|<p by A17,A13,
    XREAL_1:10;
    hence |.((s(#)s9).m)-(lim s)*(lim s9).|<p by A16,XXREAL_0:2;
  end;
  s(#)s9 is convergent by A1,A2,Th29;
  hence thesis by A7,Def5;
end;

theorem
  s is convergent & s9 is convergent implies lim |.s(#)s9.| = |.lim s.|*
  |.lim s9.|
proof
  assume
A1: s is convergent & s9 is convergent;
  then s(#)s9 is convergent by Th29;
  hence lim |.s(#)s9.| = |.lim (s(#)s9).| by Th11
    .= |.(lim s)*(lim s9).| by A1,Th30
    .= |.lim s.|*|.lim s9.| by COMPLEX1:151;
end;

theorem
  s is convergent & s9 is convergent implies lim (s(#)s9)*' = (lim s)*'
  * (lim s9)*'
proof
  assume
A1: s is convergent & s9 is convergent;
  then s(#)s9 is convergent by Th29;
  hence lim (s(#)s9)*' = (lim (s(#)s9))*' by Th12
    .= ((lim s) * (lim s9))*' by A1,Th30
    .= (lim s)*' * (lim s9)*' by COMPLEX1:121;
end;

theorem Th33:
  s is convergent implies ((lim s)<>0c implies ex n st for m st n
  <=m holds |.(lim s).|/2<|.s.m.|)
proof
  assume that
A1: s is convergent and
A2: (lim s)<>0c;
  0<|.(lim s).| by A2,COMPLEX1:133;
  then 0<|.(lim s).|/2 by XREAL_1:217;
  then consider n1 such that
A3: for m st n1<=m holds |.s.m-(lim s).|<|.(lim s).|/2 by A1,Def5;
  take n=n1;
  let m;
  assume n<=m;
  then
A4: |.s.m-(lim s).|<|.(lim s).|/2 by A3;
A5: |.(lim s)-s.m.|=|.-(s.m-(lim s)).| .=|.s.m-(lim s).| by COMPLEX1:138;
A6: |.(lim s).|/2+(|.s.m.|- |.(lim s).|/2) =|.s.m.| & |.(lim s).|- |.s.m.|+
  (|.s. m.|- |.(lim s).|/2) =|.(lim s).|/2;
  |.(lim s).|- |.s.m.|<=|.(lim s)-s.m.| by COMPLEX1:145;
  then |.(lim s).|- |.s.m.|<|.(lim s).|/2 by A4,A5,XXREAL_0:2;
  hence thesis by A6,XREAL_1:8;
end;

theorem Th34:
  s is convergent & (lim s)<>0c & s is non-zero implies s" is convergent
proof
  assume that
A1: s is convergent and
A2: (lim s)<>0c and
A3: s is non-zero;
  consider n1 such that
A4: for m st n1<=m holds |.(lim s).|/2<|.s.m.| by A1,A2,Th33;
  take(lim s)";
  let p be Real;
  assume
A5: 0<p;
A6: 0<|.(lim s).| by A2,COMPLEX1:133;
  then 0*0<|.(lim s).|*|.(lim s).| by XREAL_1:98;
  then 0<(|.(lim s).|*|.(lim s).|)/2 by XREAL_1:217;
  then 0*0<p*((|.(lim s).|*|.(lim s).|)/2) by A5,XREAL_1:98;
  then consider n2 such that
A7: for m st n2<=m holds |.s.m-(lim s).|<p*((|.(lim s).|*|.(lim s).|)/2
  ) by A1,Def5;
  take n=n1+n2;
  let m such that
A8: n<=m;
  n1<=n1+n2 by NAT_1:12;
  then n1<=m by A8,XXREAL_0:2;
  then
A9: |.(lim s).|/2<|.s.m.| by A4;
A10: 0<|.(lim s).|/2 by A6,XREAL_1:217;
  then 0*0<p*(|.(lim s).|/2) by A5,XREAL_1:98;
  then
A11: (p*(|.(lim s).|/2))/|.s.m.|< (p*(|.(lim s).|/2))/(|.(lim s).|/2) by A9
  ,A10,XREAL_1:78;
A12: 0<>|.(lim s).|/2 by A2,COMPLEX1:133;
A13: (p*(|.(lim s).|/2))/(|.(lim s).|/2 ) =(p*(|.(lim s).|/2))*(|.(lim s).|/
  2 )" by XCMPLX_0:def 9
    .=p*((|.(lim s).|/2)*(|.(lim s).|/2 )")
    .=p*1 by A12,XCMPLX_0:def 7
    .=p;
A14: 0<>|.(lim s).| by A2,COMPLEX1:133;
A15: (p*((|.(lim s).|*|.(lim s).|)/2))/(|.s.m.|*|.(lim s).|) =(p*(2"*(|.(lim
  s).|*|.(lim s).|)))* (|.s.m.|*|.(lim s).|)" by XCMPLX_0:def 9
    .=p*2"*((|.(lim s).|*|.(lim s).|)*(|.(lim s).|*|.s.m.|)")
    .=p*2"*((|.(lim s).|*|.(lim s).|)* (|.(lim s).|"*|.s.m.|")) by XCMPLX_1:
  205
    .=p*2"*(|.(lim s).|*(|.(lim s).|*|.(lim s).|")*|.s.m.|")
    .=p*2"*(|.(lim s).|*1*|.s.m.|") by A14,XCMPLX_0:def 7
    .=p*(|.(lim s).|/2)*|.s.m.|"
    .=(p*(|.(lim s).|/2))/|.s.m.| by XCMPLX_0:def 9;
A16: s.m<>0c by A3,COMSEQ_1:3;
  then s.m*(lim s)<>0c by A2,XCMPLX_1:6;
  then 0<|.s.m*(lim s).| by COMPLEX1:133;
  then
A17: 0<|.s.m.|*|.(lim s).| by COMPLEX1:151;
  n2<=n by NAT_1:12;
  then n2<=m by A8,XXREAL_0:2;
  then |.s.m-(lim s).|<p*((|.(lim s).|*|.(lim s).|)/2) by A7;
  then
A18: |.s.m-(lim s).|/(|.s.m.|*|.(lim s).|)< (p*((|.(lim s).|*|.(lim s).|)/2)
  )/(|.s.m.|*|.(lim s).|) by A17,XREAL_1:76;
  |.(s").m-(lim s)".|=|.(s.m)"-(lim s)".| by VALUED_1:10
    .=|.s.m-(lim s).|/(|.s.m.|*|.(lim s).|) by A2,A16,Th1;
  hence thesis by A18,A15,A11,A13,XXREAL_0:2;
end;

theorem Th35:
  s is convergent & (lim s)<>0c & s is non-zero implies lim s"=( lim s)"
proof
  assume that
A1: s is convergent and
A2: (lim s)<>0c and
A3: s is non-zero;
  consider n1 such that
A4: for m st n1<=m holds |.(lim s).|/2<|.s.m.| by A1,A2,Th33;
A5: 0<|.(lim s).| by A2,COMPLEX1:133;
  then 0*0<|.(lim s).|*|.(lim s).| by XREAL_1:98;
  then
A6: 0<(|.(lim s).|*|.(lim s).|)/2 by XREAL_1:217;
A7: 0<>|.(lim s).| by A2,COMPLEX1:133;
A8: now
    let p be Real;
A9: 0<>|.(lim s).|/2 by A2,COMPLEX1:133;
A10: (p*(|.(lim s).|/2))/(|.(lim s).|/2 ) =(p*(|.(lim s).|/2))*(|.(lim s)
    .|/2 )" by XCMPLX_0:def 9
      .=p*((|.(lim s).|/2)*(|.(lim s).|/2 )")
      .=p*1 by A9,XCMPLX_0:def 7
      .=p;
    assume
A11: 0<p;
    then 0*0<p*((|.(lim s).|*|.(lim s).|)/2) by A6,XREAL_1:98;
    then consider n2 such that
A12: for m st n2<=m holds |.s.m-(lim s).|<p*((|.(lim s).|*|.(lim s).|)
    /2) by A1,Def5;
    take n=n1+n2;
    let m such that
A13: n<=m;
    n1<=n1+n2 by NAT_1:12;
    then n1<=m by A13,XXREAL_0:2;
    then
A14: |.(lim s).|/2<|.s.m.| by A4;
A15: 0<|.(lim s).|/2 by A5,XREAL_1:217;
    then 0*0<p*(|.(lim s).|/2) by A11,XREAL_1:98;
    then
A16: (p*(|.(lim s).|/2))/|.s.m.|< (p*(|.(lim s).|/2))/(|.(lim s).|/2) by
    A14,A15,XREAL_1:78;
A17: (p*((|.(lim s).|*|.(lim s).|)/2))/(|.s.m.|*|.(lim s).|) =(p*(2"*(|.(
    lim s).|*|.(lim s).|)))* (|.s.m.|*|.(lim s).|)" by XCMPLX_0:def 9
      .=p*2"*((|.(lim s).|*|.(lim s).|)*(|.(lim s).|*|.s.m.|)")
      .=p*2"*((|.(lim s).|*|.(lim s).|)* (|.(lim s).|"*|.s.m.|")) by
    XCMPLX_1:205
      .=p*2"*(|.(lim s).|*(|.(lim s).|*|.(lim s).|")*|.s.m.|")
      .=p*2"*(|.(lim s).|*1*|.s.m.|") by A7,XCMPLX_0:def 7
      .=p*(|.(lim s).|/2)*|.s.m.|"
      .=(p*(|.(lim s).|/2))/|.s.m.| by XCMPLX_0:def 9;
A18: s.m<>0c by A3,COMSEQ_1:3;
    then s.m*(lim s)<>0c by A2,XCMPLX_1:6;
    then 0<|.s.m*(lim s).| by COMPLEX1:133;
    then
A19: 0<|.s.m.|*|.(lim s).| by COMPLEX1:151;
    n2<=n by NAT_1:12;
    then n2<=m by A13,XXREAL_0:2;
    then |.s.m-(lim s).|<p*((|.(lim s).|*|.(lim s).|)/2) by A12;
    then
A20: |.s.m-(lim s).|/(|.s.m.|*|.(lim s).|)< (p*((|.(lim s).|*|.(lim s).|)/
    2))/(|.s.m.|*|.(lim s).|) by A19,XREAL_1:76;
    |.(s").m-(lim s)".|=|.(s.m)"-(lim s)".| by VALUED_1:10
      .=|.s.m-(lim s).|/(|.s.m.|*|.(lim s).|) by A2,A18,Th1;
    hence |.(s").m-(lim s)".|<p by A20,A17,A16,A10,XXREAL_0:2;
  end;
  s" is convergent by A1,A2,A3,Th34;
  hence thesis by A8,Def5;
end;

theorem
  s is convergent & (lim s)<>0c & s is non-zero implies lim |.s".| = (|.
  lim s.|)"
proof
  assume
A1: s is convergent & (lim s)<>0c & s is non-zero;
  then s" is convergent by Th34;
  hence lim |.s".| = |.lim s".| by Th11
    .= |.(lim s)".| by A1,Th35
    .= (|.lim s.|)" by COMPLEX1:152;
end;

theorem
  s is convergent & (lim s)<>0c & s is non-zero implies lim (s")*' = ((
  lim s)*')"
proof
  assume
A1: s is convergent & (lim s)<>0c & s is non-zero;
  then s" is convergent by Th34;
  hence lim (s")*' = (lim s")*' by Th12
    .= ((lim s)")*' by A1,Th35
    .= ((lim s)*')" by COMPLEX1:122;
end;

theorem Th38:
  s9 is convergent & s is convergent & (lim s)<>0c & s is non-zero
  implies s9/"s is convergent
proof
  assume that
A1: s9 is convergent and
A2: s is convergent & (lim s)<>0c & s is non-zero;
  s" is convergent by A2,Th34;
  hence thesis by A1,Th29;
end;

theorem Th39:
  s9 is convergent & s is convergent & (lim s)<>0c & s is non-zero
  implies lim(s9/"s)=(lim s9)/(lim s)
proof
  assume that
A1: s9 is convergent and
A2: s is convergent & (lim s)<>0c & s is non-zero;
  s" is convergent by A2,Th34;
  then lim (s9(#)(s"))=(lim s9)*(lim s") by A1,Th30
    .=(lim s9)*(lim s)" by A2,Th35
    .=(lim s9)/(lim s) by XCMPLX_0:def 9;
  hence thesis;
end;

theorem
  s9 is convergent & s is convergent & (lim s)<>0c & s is non-zero
  implies lim |.(s9/"s).|=|.(lim s9).|/|.(lim s).|
proof
  assume
A1: s9 is convergent & s is convergent & (lim s)<>0c & s is non-zero;
  then s9/"s is convergent by Th38;
  hence lim |.s9/"s.| = |.lim (s9/"s).| by Th11
    .= |.(lim s9)/(lim s).| by A1,Th39
    .= |.(lim s9).|/|.(lim s).| by COMPLEX1:153;
end;

theorem
  s9 is convergent & s is convergent & (lim s)<>0c & s is non-zero
  implies lim (s9/"s)*' = ((lim s9)*')/((lim s)*')
proof
  assume
A1: s9 is convergent & s is convergent & (lim s)<>0c & s is non-zero;
  then s9/"s is convergent by Th38;
  hence lim (s9/"s)*' = (lim (s9/"s))*' by Th12
    .= ((lim s9)/(lim s))*' by A1,Th39
    .= ((lim s9)*')/((lim s)*') by COMPLEX1:123;
end;

theorem Th42:
  s is convergent & s1 is bounded & (lim s)=0c implies s(#)s1 is convergent
proof
  assume that
A1: s is convergent and
A2: s1 is bounded and
A3: (lim s) = 0c;
  take g=0c;
  consider R such that
A4: 0<R and
A5: for m holds |.s1.m.|<R by A2,Th8;
  let p be Real such that
A6: 0<p;
A7: 0<p/R by A6,A4,XREAL_1:141;
  then consider n1 such that
A8: for m st n1<=m holds |.s.m-0c.|<p/R by A1,A3,Def5;
  take n=n1;
  let m such that
A9: n<=m;
A10: |.((s(#)s1).m)-g.|=|.s.m*s1.m.| by VALUED_1:5
    .=|.s.m.|*|.s1.m.| by COMPLEX1:151;
  |.s.m.|=|.s.m-0c.|;
  then
A11: |.s.m.|<p/R by A8,A9;
  now
    (p/R)*R=p*R"*R by XCMPLX_0:def 9
      .=p*(R"*R)
      .=p*1 by A4,XCMPLX_0:def 7
      .=p;
    then
A12: (p/R)*|.s1.m.|<p by A5,A7,XREAL_1:70;
A13: 0<=|.s1.m.| by COMPLEX1:132;
    assume |.s1.m.|<>0;
    then |.((s(#)s1).m)-g.|<(p/R)*|.s1.m.| by A11,A10,A13,XREAL_1:70;
    hence thesis by A12,XXREAL_0:2;
  end;
  hence thesis by A6,A10;
end;

theorem Th43:
  s is convergent & s1 is bounded & (lim s)=0c implies lim(s(#)s1) =0c
proof
  assume that
A1: s is convergent and
A2: s1 is bounded and
A3: lim s=0c;
A4: now
    consider R such that
A5: 0<R and
A6: for m holds |.s1.m.|<R by A2,Th8;
    let p be Real such that
A7: 0<p;
A8: 0<p/R by A7,A5,XREAL_1:141;
    then consider n1 such that
A9: for m st n1<=m holds |.s.m-0c.|<p/R by A1,A3,Def5;
    take n=n1;
    let m;
    (p/R)*R=p*R"*R by XCMPLX_0:def 9
      .=p*(R"*R)
      .=p*1 by A5,XCMPLX_0:def 7;
    then
A10: (p/R)*|.s1.m.|<p by A6,A8,XREAL_1:70;
    assume n<=m;
    then
A11: |.s.m-0c.|<p/R by A9;
A12: |.((s(#)s1).m)-0c.|=|.s.m*s1.m.| by VALUED_1:5
      .=|.s.m.|*|.s1.m.| by COMPLEX1:151;
A13: 0<=|.s1.m.| by COMPLEX1:132;
    now
      assume |.s1.m.|<>0;
      then |.((s(#)s1).m)-0c.|<(p/R)*|.s1.m.| by A11,A12,A13,XREAL_1:70;
      hence |.((s(#)s1).m)-0c.|<p by A10,XXREAL_0:2;
    end;
    hence |.((s(#)s1).m)-0c.|<p by A7,A12;
  end;
  s(#)s1 is convergent by A1,A2,A3,Th42;
  hence thesis by A4,Def5;
end;

theorem
  s is convergent & s1 is bounded & (lim s)=0c implies lim |.s(#)s1.| = 0
proof
  assume
A1: s is convergent & s1 is bounded & (lim s)=0c;
  then s(#)s1 is convergent by Th42;
  hence lim |.s(#)s1.| = |.(lim (s(#)s1)).| by Th11
    .= 0 by A1,Th43,COMPLEX1:130;
end;

theorem
  s is convergent & s1 is bounded & (lim s)=0c implies lim (s(#)s1)*' = 0c
proof
  assume
A1: s is convergent & s1 is bounded & (lim s)=0c;
  then s(#)s1 is convergent by Th42;
  hence lim (s(#)s1)*' = (lim (s(#)s1))*' by Th12
    .= 0c by A1,Th43,COMPLEX1:113;
end;
