:: On the Permanent of a Matrix
::  by Ewa Romanowicz and Adam Grabowski
::
:: Received January 4, 2006
:: Copyright (c) 2006 Association of Mizar Users

environ

 vocabularies VECTSP_1, RLVECT_1, MATRIX_1, FINSEQ_2, ARYTM_1, FINSEQ_1,
      TREES_1, FUNCT_1, RELAT_1, BOOLE, GROUP_1, BINOP_1, SETWISEO, MATRIX_2,
      FINSUB_1, FINSET_1, FUNCOP_1, MATRIX_3, MATRIX_9, FINSEQ_5, FUNCT_2,
      SGRAPH1, CARD_3, FINSEQ_4, NAT_1, CALCUL_2;
 notations TARSKI, XBOOLE_0, SUBSET_1, NUMBERS, XCMPLX_0, NAT_1, NAT_D,
      ENUMSET1, FINSET_1, RELAT_1, FUNCT_1, PARTFUN1, FUNCT_2, FINSEQ_1,
      FINSEQ_2, BINOP_1, FINSOP_1, BINARITH, STRUCT_0, GROUP_1, VECTSP_1,
      RLVECT_1, SETWISEO, GROUP_4, SETWOP_2, FINSUB_1, MATRIX_1, FINSEQ_4,
      FINSEQ_5, MATRIX_2, MATRIX_3, XXREAL_0;
 constructors RELAT_2, PARTFUN1, DOMAIN_1, BINOP_1, SETWISEO, XXREAL_0, NAT_1,
      FINSEQ_4, FINSOP_1, SETWOP_2, BINARITH, FINSEQ_5, ALGSTR_1, GROUP_4,
      MATRIX_7, NAT_D;
 registrations FINSUB_1, FINSET_1, RELSET_1, STRUCT_0, FVSUM_1, MATRIX_2,
      FUNCT_2, PARTFUN1, XBOOLE_0, ORDINAL1, FINSEQ_5, FINSEQ_1, CHAIN_1,
      INT_1, FINSEQ_6, VECTSP_1, ARYTM_3, NAT_1;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 definitions BINOP_1, RLVECT_1, FINSEQ_2, FINSEQ_1, RVSUM_1, SETWOP_2;
 theorems FINSEQ_1, FINSEQ_2, TARSKI, FUNCT_2, NAT_1, BINOP_1, FUNCT_1,
      SETWISEO, VECTSP_1, ZFMISC_1, FVSUM_1, SETWOP_2, MATRIX_1, MATRIX_2,
      FUNCOP_1, FINSUB_1, FINSEQ_3, RLVECT_1, RELAT_1, FINSOP_1, RELSET_1,
      XBOOLE_0, XBOOLE_1, GROUP_1, MATRIX_3, FINSEQ_5, ENUMSET1, FINSEQ_6,
      GOBOARD7, FINSET_1, FINSEQ_4, GROUP_4, XREAL_1, GROUP_7, MATRIX_7,
      BINARITH, XXREAL_0, ORDINAL1, NAT_D;
 schemes FUNCT_2, FINSET_1, NAT_1;

begin :: Preliminaries

 reserve x for set,
         i,j,k,n for Nat,

         K for Field;

Lm1:
  idseq n in Permutations n by MATRIX_2:def 11;

theorem Th1:
  for a, A being set st a in A holds
    {a} in Fin A
  proof
    let a, A be set;
    assume
A1: a in A;
    {a} c= A by A1,ZFMISC_1:37;
    hence thesis by FINSUB_1:def 5;
  end;

registration let n be Nat;
  cluster non empty Element of Fin Permutations n;
  existence
  proof
    idseq n in Permutations n by Lm1; then
    {idseq n} in Fin Permutations n by Th1;
    hence thesis;
  end;
end;

scheme NonEmptyFiniteX
  { n() -> Element of NAT,
  A() -> non empty Element of Fin Permutations n(), P[set] } :
    P[A()]
  provided
A1: for x being Element of Permutations n() st x in A() holds P[{x}] and
A2: for x being Element of Permutations n(),
        B being non empty Element of Fin Permutations n() st
    x in A() & B c= A() & not x in B & P[B] holds P[B \/ {x}]
  proof
    defpred Q[set] means $1 is empty or P[$1];
A3: A() is finite;
A4: Q[{}];
A5: for x,B being set st x in A() & B c= A() & Q[B] holds Q[B \/ {x}]
    proof
      let x,B be set;
      assume
A6:   x in A() & B c= A() & Q[B];
A7:   A() c= Permutations n() by FINSUB_1:def 5; then
      reconsider x as Element of Permutations n() by A6;
A8:   B c= Permutations n() by A7,A6,XBOOLE_1:1;
      Permutations n() is finite by MATRIX_2:30; then
A9:   B is finite by A8,FINSET_1:13;
      per cases;
      suppose
A10:     x in B;
        then reconsider B as non empty Element of Fin Permutations n()
          by A8,A9,FINSUB_1:def 5;
        {x} c= B by A10,ZFMISC_1:37;
        hence thesis by A6,XBOOLE_1:12;
      end;
      suppose
A11:     B is non empty & not x in B;
        then reconsider B as non empty Element of Fin Permutations n()
          by A8,A9,FINSUB_1:def 5;
        P[B \/ {x}] by A2,A6,A11;
        hence thesis;
      end;
      suppose B is empty; then
        P[B \/ {x}] by A1,A6;
        hence thesis;
      end;
    end;
    Q[A()] from FINSET_1:sch 2(A3,A4,A5);
    hence thesis;
  end;

Lm2:<*1,2*> <> <*2,1*> by GROUP_7:2;

Lm3: <*2,1*> in Permutations 2 by TARSKI:def 2,MATRIX_7:3;

Lm4:
  for n being Element of NAT,a,b being Element of
  the carrier of Group_of_Perm(n),
pa,pb being Element of Permutations(n) st
a=pa & b=pb holds a*b=pb*pa by MATRIX_7:9;

registration let n;
  cluster one-to-one FinSequence-like Function of Seg n, Seg n;
  existence
  proof
A1: n in NAT by ORDINAL1:def 13;
    take f = id Seg n;
    dom f = Seg n by RELAT_1:71;
    hence thesis by A1,FINSEQ_1:def 2;
  end;
end;

Lm5:
  for f being FinSequence-like Function st
    f is one-to-one holds
      Rev f is one-to-one
  proof
    let f be FinSequence-like Function;
    assume f is one-to-one; then
    reconsider g = f as one-to-one FinSequence-like Function;
    Rev g is one-to-one;
    hence thesis;
  end;

registration let n;
  cluster id Seg n -> FinSequence-like;
  coherence
  proof
A1: n in NAT by ORDINAL1:def 13;
    dom id Seg n = Seg n by RELAT_1:71;
    hence thesis by A1,FINSEQ_1:def 2;
  end;
end;

theorem Th2:
  (Rev idseq 2).1 = 2 & (Rev idseq 2).2 = 1
  proof
    reconsider f = idseq 2 as one-to-one FinSequence-like Function
      of Seg 2, Seg 2;
    f.1 = (Rev f).len f & f.(len f) = (Rev f).1 by FINSEQ_5:65;then
A1: (idseq 2).1 = (Rev idseq 2).2 & f.2 = (Rev f).1 by FINSEQ_2:55;
A2: dom Rev f = dom f & rng Rev f = rng f by FINSEQ_5:60; then
A3: dom Rev f = Seg 2 by RELAT_1:71; then
    1 in dom f by A2; then
A4: (idseq 2).1 = 1 by A3,A2,FUNCT_1:35;
    2 in dom f by A2,A3;
    hence thesis by A1,A4,A3,A2,FUNCT_1:35;
  end;

theorem Th3:
  for f being one-to-one Function st
    dom f = Seg 2 & rng f = Seg 2 holds
      f = id Seg 2 or f = Rev id Seg 2
  proof
    let f be one-to-one Function;
A1: dom idseq 2 = Seg 2 by RELAT_1:71;
    assume
A2: dom f = Seg 2 & rng f = Seg 2;
A3: 1 in dom f by A2; then
    f.1 in rng f by FUNCT_1:12; then
A4: f.1 = 1 or f.1 = 2 by TARSKI:def 2,FINSEQ_1:4, A2;
A5: 2 in dom f by A2;then
    f.2 in rng f by FUNCT_1:12; then
    f.2 = 2 or f.2 = 1 by A2,TARSKI:def 2, FINSEQ_1:4; then
A6: f.1 = 1 & f.2 = 2 or f.1 = 2 & f.2 = 1 by A4,FUNCT_1:def 8,A3,A5;
    per cases by A6;
    suppose A7: f.1 = 1 & f.2 = 2;
    for x being set st x in Seg 2 holds f.x = x
    proof
      let x be set;
      assume x in Seg 2; then
      x = 1 or x = 2 by TARSKI:def 2,FINSEQ_1:4;
      hence thesis by A7;
    end;
    hence thesis by FUNCT_1:34,A2;
  end;
  suppose A8: f.1 = 2 & f.2 = 1;
A9: dom f = dom Rev id Seg 2 by A2,A1,FINSEQ_5:60;
    for x st x in Seg 2 holds f.x = (Rev id Seg 2).x
    proof
      let x be set;
      assume x in Seg 2;then
      x = 1 or x = 2 by TARSKI:def 2,FINSEQ_1:4;
      hence thesis by A8,Th2;
    end;
    hence thesis by A9,FUNCT_1:9, A2;
  end;
  end;

begin :: Permutations

theorem Th4:
  Rev idseq n in Permutations n
  proof
    reconsider f = idseq n as one-to-one FinSequence-like Function
      of Seg n, Seg n;
A1: rng idseq n = Seg n by FUNCT_2:def 3;
A2: dom f = dom Rev f & rng f = rng Rev f by FINSEQ_5:60;
A3: dom Rev f = Seg n by RELAT_1:71, A2;
    rng Rev f c= Seg n by FINSEQ_5:60,A1; then
    reconsider g = Rev f as FinSequence-like Function of Seg n, Seg n
      by A3,FUNCT_2:4;
A4: g is onto by FUNCT_2:def 3,A2,A1;
    g is bijective by A4,FUNCT_2:def 4;
    hence thesis by MATRIX_2:def 11;
  end;

theorem Th5:
  for f being FinSequence st
    n <> 0 & f in Permutations n holds
    Rev f in Permutations n
    proof
      let f be FinSequence;
      assume A1: n <> 0 & f in Permutations n;then
A2:   f is Permutation of Seg n by MATRIX_2:def 11;
A3:   Seg n <> {} by A1,FINSEQ_1:5;
A4:   Rev f is one-to-one by Lm5,A2;
A5:   rng f = Seg n by FUNCT_2:def 3,A2;
A6:   dom f = dom Rev f & rng f = rng Rev f by FINSEQ_5:60;
      dom Rev f = Seg n & rng Rev f = Seg n by A2,FUNCT_2:def 1,A3,A6,A5; then
      reconsider g = Rev f as FinSequence-like Function of Seg n,Seg n
        by FUNCT_2:4;
      g is onto by FUNCT_2:def 3,A5,A6; then
      g is bijective by A4,FUNCT_2:def 4;
      hence thesis by MATRIX_2:def 11;
    end;

theorem Th6:
  Permutations 2 = { idseq 2, Rev idseq 2 }
  proof
     now let p be set;
     assume p in Permutations 2; then
     reconsider q = p as Permutation of Seg 2 by MATRIX_2:def 11;
A1:  q is Function of Seg 2, Seg 2 & rng q = Seg 2 by FUNCT_2:def 3;
A2:  dom q = Seg 2 by FUNCT_2:67;
     reconsider q as FinSequence by A2,FINSEQ_1:def 2;
     q = idseq 2 or q = Rev idseq 2 by Th3,A1,A2;
     hence p in {idseq 2, Rev idseq 2} by TARSKI:def 2;
   end;then
A3: Permutations 2 c= {idseq 2, Rev idseq 2 } by TARSKI:def 3;
    now let x;
     assume x in {idseq 2, Rev idseq 2 }; then
     x = idseq 2 or x = Rev idseq 2 by TARSKI:def 2;
     hence x in Permutations 2 by Th4,Lm1;
    end;
    then {idseq 2, Rev idseq 2 } c= Permutations 2 by TARSKI:def 3;
    hence thesis by A3,XBOOLE_0:def 10;
   end;

begin :: The Permanent of a Matrix

definition let n,K;let M be Matrix of n,K;
  func PPath_product M -> Function of Permutations n, the carrier of K means
:Def1: for p being Element of Permutations(n) holds
    it.p = (the mult of K) $$ Path_matrix(p,M);
existence
proof
   deffunc V(Element of Permutations n) =
        (the mult of K) $$ Path_matrix($1,M);
   consider f being Function of Permutations(n),the carrier of K such that
A1: for p being Element of Permutations(n) holds
      f.p = V(p) from FUNCT_2:sch 4;
   take f;
   thus thesis by A1;
 end;
uniqueness
proof
   let f1,f2 be Function of Permutations(n), the carrier of K;
   assume that
A2: for p being Element of Permutations(n) holds
     f1.p = (the mult of K) $$ Path_matrix(p,M) and
A3: for p being Element of Permutations(n) holds
     f2.p = (the mult of K) $$ Path_matrix(p,M);
   now let p be Element of Permutations(n);
     f1.p = (the mult of K) $$ Path_matrix(p,M) by A2;
     hence f1.p=f2.p by A3;
   end;
   hence thesis by FUNCT_2:113;
 end;
end;

definition let n, K; let M be Matrix of n,K;
  func Per M -> Element of K equals
    (the add of K) $$ (FinOmega Permutations n, PPath_product M);
  coherence;
end;

 reserve a,b,c,d for Element of K;

theorem
  Per <*<*a*>*> = a
  proof
   set M = <*<*a*>*>;
A1:(PPath_product M).(idseq 1)=a
   proof
     reconsider p = idseq 1 as Element of Permutations 1
       by MATRIX_2:def 11;
A2:  len Path_matrix(p,M) = 1 by MATRIX_3:def 7; then
A3:  dom Path_matrix(p,M) = Seg 1 by FINSEQ_1:def 3; then
A4:  1 in dom Path_matrix(p,M); then
     1 = p.1 by A3,FUNCT_1:35; then
     Path_matrix(p,M).1 = M*(1,1) by A4,MATRIX_3:def 7; then
     Path_matrix(p,M).1 = a by MATRIX_2:5; then
A5:  Path_matrix(p,M) = <*a*> by A2,FINSEQ_1:57;
A6:  (PPath_product M).p = (the mult of K) $$ Path_matrix(p,M) by Def1;
     <*a*> = 1|->a by FINSEQ_2:73;
     hence thesis by A5,A6,FINSOP_1:17;
   end;
A7: FinOmega Permutations 1 = {idseq 1} by MATRIX_2:21,def 17;
    idseq 1 in Permutations 1 by MATRIX_2:21,TARSKI:def 1;
    hence thesis by A1,A7,SETWISEO:26;
  end;

theorem
  for K being Field, n being Element of NAT st n >= 1 holds
    Per (0.(K,n,n)) = 0.K
proof let K be Field, n be Element of NAT;
    assume A1: n>=1;then n >= 1+0; then
A2: n > 0 by NAT_1:13;
    set B = FinOmega Permutations n;
    set f = PPath_product(0.(K,n,n));
    set F = the add of K;
    set Y = the carrier of K;
    set X = Permutations n;
A3: Permutations(n) is finite by MATRIX_2:30;
A4: FinOmega Permutations n <> {} by A3,MATRIX_2:def 17;
    dom (Permutations(n) --> 0.K) = Permutations(n) by FUNCOP_1:19;then
A5:dom (PPath_product(0.(K,n,n))) = dom (Permutations(n) --> 0.K)
      by FUNCT_2:def 1;
    for x being set st x in dom PPath_product(0.(K,n,n)) holds
      (PPath_product(0.(K,n,n))).x = (Permutations(n) --> 0.K).x
    proof let x be set;
      assume x in dom PPath_product(0.(K,n,n));
      for p being Element of Permutations n holds
        (Permutations(n) --> 0.K).p =
          (the mult of K) $$ Path_matrix(p,(0.(K,n,n)))
      proof let p be Element of Permutations(n);
A6:     len (n |-> 0.K) = n by FINSEQ_2:69;
        for i,j st i in dom (n |-> 0.K) & j=p.i holds
          (n |-> 0.K).i = (0.(K,n,n))*(i,j)
        proof let i,j;
          assume A7: i in dom (n |-> 0.K) & j=p.i;then
A8:       i in Seg n by FUNCOP_1:19;then
A9:       (n |-> 0.K).i=0.K by FUNCOP_1:13;
          j in Seg n by A7,A8,MATRIX_7:14;then
          i in dom (0.(K,n,n)) & j in Seg width (0.(K,n,n))
            by A8,MATRIX_7:13,A2,MATRIX_1:24;then
          [i,j] in [:dom (0.(K,n,n)),Seg width (0.(K,n,n)):]
            by ZFMISC_1:def 2;then
          [i,j] in Indices (0.(K,n,n)) by MATRIX_1:def 5;
          hence (n |-> 0.K).i=(0.(K,n,n))*(i,j) by A9,MATRIX_3:3;
        end;then
A10:     Path_matrix(p,(0.(K,n,n))) = n |-> 0.K by A6,MATRIX_3:def 7;
        defpred P[Element of NAT] means
         (the mult of K) $$ (($1+1) |-> 0.K)=0.K;
        1 |-> 0.K = <* 0.K *> by FINSEQ_2:73;then
A11:     P[0] by FINSOP_1:12;
A12:     for k being Element of NAT st P[k] holds P[k+1]
        proof let k be Element of NAT;
          assume A13: P[k];
A14:       (the mult of K) has_a_unity by FVSUM_1:11;
          (k+1+1) |-> 0.K = ((k+1) |-> 0.K) ^ <* 0.K *> by FINSEQ_2:74;then
          (the mult of K) $$ ( (k+1+1) |-> 0.K)
             = (the mult of K).(0.K,0.K) by  A14,FINSOP_1:5,A13
            .= (0.K)*(0.K) by GROUP_1:def 1
            .= 0.K by VECTSP_1:36;
          hence P[k+1];
        end;
A15:     for k being Element of NAT holds P[k] from NAT_1:sch 1(A11,A12);
        n-'1+1=n by A1,BINARITH:53;then
        (the mult of K) $$ Path_matrix(p,(0.(K,n,n))) = 0.K by A10,A15;
        hence (Permutations(n) --> 0.K).p
          = (the mult of K) $$ Path_matrix(p,(0.(K,n,n)))
            by FUNCOP_1:13;
      end;
      hence (PPath_product(0.(K,n,n))).x = (Permutations(n) --> 0.K).x
        by Def1;
    end;then
A16:PPath_product(0.(K,n,n)) = Permutations(n) --> 0.K by A5,FUNCT_1:9;
    reconsider G0= Fin X --> 0.K as Function of Fin X, Y;
A17:G0.B=0.K by FUNCOP_1:13;
A18:for e being Element of Y st e is_a_unity_wrt F holds G0.({}) = e
    proof let e be Element of Y;
      assume e is_a_unity_wrt F;then
A19:   F.(0.K,e) = 0.K by BINOP_1:11;
      0.K is_a_unity_wrt F by FVSUM_1:8;then
A20:   F.(0.K,e) = e by BINOP_1:11;
      {} in Fin X by FINSUB_1:18;
      hence G0.({}) = e by A19,A20,FUNCOP_1:13;
    end;
A21:for x being Element of X holds G0.({x}) = f.x
    proof let x be Element of X;
A22:   G0.({.x.}) = 0.K by FUNCOP_1:13;
      f.x= 0.K by  A16,FUNCOP_1:13;
      hence G0.({x}) = f.x by A22;
    end;
    for B' being Element of Fin X st B' c= B & B' <> {}
      for x being Element of X st x in B\B' holds G0.(B' \/ {x})
                    = F.(G0.B',f.x)
    proof let B' be Element of Fin X;
      assume B' c= B & B' <> {};
      thus for x being Element of X st x in B\B' holds G0.(B' \/ {x})
                    = F.(G0.B',f.x)
      proof let x be Element of X;
        assume x in B \ B';
A23:     G0.(B' \/ {.x.})=0.K by FUNCOP_1:13;
A24:     G0.B' = 0.K by FUNCOP_1:13;
A25:     f.x = 0.K by  A16,FUNCOP_1:13;
        0.K is_a_unity_wrt F by FVSUM_1:8;
        hence G0.(B' \/ {x}) = F.(G0.B',f.x) by A23,A25,A24,BINOP_1:11;
      end;
    end;
    hence thesis by SETWISEO:def 3,A4,A17,A18,A21;
end;

theorem Th9:
  for p being Element of Permutations 2 st p = idseq 2 holds
    Path_matrix (p, (a,b)][(c,d)) = <* a,d *>
  proof
    let p be Element of Permutations 2 such that A1: p = idseq 2;
A2:  len Path_matrix (p, (a,b)][(c,d)) = 2 by MATRIX_3:def 7;then
A3:  dom Path_matrix (p, (a,b)][(c,d)) = Seg 2 by FINSEQ_1:def 3; then
A4:  1 in dom Path_matrix (p, (a,b)][(c,d));then
A5:  1 = p.1 by A3,FUNCT_1:35, A1;
A6:  2 in dom Path_matrix (p, (a,b)][(c,d)) by A3;then
A7:  2 = p.2 by A3,FUNCT_1:35, A1;
A8:  Path_matrix (p, (a,b)][(c,d)).1=(a,b)][(c,d)*(1,1)
       by A4, MATRIX_3:def 7, A5
     .= a by MATRIX_2:6;
     Path_matrix (p, (a,b)][(c,d)).2=(a,b)][(c,d)*(2,2)
       by A6, MATRIX_3:def 7, A7
     .= d by MATRIX_2:6;
    hence thesis by A8, A2, FINSEQ_1:61;
  end;

theorem Th10:
  for p being Element of Permutations 2 st p = Rev idseq 2 holds
    Path_matrix (p, (a,b)][(c,d)) = <* b,c *>
   proof
     let p be Element of Permutations 2 such that A1: p = Rev idseq 2;
A2:  len Path_matrix (p, (a,b)][(c,d)) = 2 by MATRIX_3:def 7;then
A3:  dom Path_matrix (p, (a,b)][(c,d)) = Seg 2 by FINSEQ_1:def 3; then
A4:  2 in dom Path_matrix (p, (a,b)][(c,d));
A5:  1 in dom Path_matrix (p, (a,b)][(c,d)) by A3;
A6:  Path_matrix (p, (a,b)][(c,d)).2=(a,b)][(c,d)*(2,1)
       by A4, MATRIX_3:def 7, A1,Th2
     .=c by MATRIX_2:6;
     Path_matrix (p, (a,b)][(c,d)).1=(a,b)][(c,d)*(1,2)
       by A5, MATRIX_3:def 7, A1,Th2
     .= b by MATRIX_2:6;
     hence thesis by A6, A2, FINSEQ_1:61;
   end;

theorem Th11:
  (the mult of K) $$ <* a,b *> = a * b
  proof
    (the mult of K) $$ <* a,b *> = Product <*a,b*> by GROUP_4:def 3
                                .= a * b by GROUP_4:13;
    hence thesis;
  end;

begin

registration
  cluster odd Permutation of Seg 2;
  existence
  proof
    rng idseq 2 c= Seg 2 by RELSET_1:12; then
    reconsider f = idseq 2 as FinSequence of Seg 2 by FINSEQ_1:def 4;
    reconsider g = <*2,1*> as Permutation of Seg 2 by Lm3,MATRIX_2:def 11;
    not ex l be FinSequence of Group_of_Perm 2 st
     (len l) mod 2 = 0 & g = Product l &
       for i st i in dom l
         ex q being Element of Permutations 2 st
           l.i = q & q is_transposition by MATRIX_7:11,Lm2; then
    g is odd by MATRIX_2:def 15;
    hence thesis;
  end;
end;

registration let n be Nat;
  cluster even Permutation of Seg n;
  existence
  proof
    reconsider f = id Seg n as Permutation of Seg n;
    f is even by MATRIX_2:29;
    hence thesis;
  end;
end;

theorem Th12:
  <*2,1*> is odd Permutation of Seg 2
  proof
    rng idseq 2 c= Seg 2 by RELSET_1:12; then
    reconsider f = idseq 2 as FinSequence of Seg 2 by FINSEQ_1:def 4;
    reconsider g = <*2,1*> as Permutation of Seg 2 by Lm3,MATRIX_2:def 11;
    not ex l be FinSequence of Group_of_Perm 2 st
     (len l) mod 2 = 0 & g = Product l &
       for i st i in dom l
         ex q being Element of Permutations 2 st
           l.i = q & q is_transposition by Lm2,MATRIX_7:11;
    hence thesis by MATRIX_2:def 15;
  end;

theorem
  Det (a,b)][(c,d) = a * d - b * c
  proof
    set M = (a,b)][(c,d);
    reconsider id2 = idseq 2 as Permutation of Seg 2;
    reconsider Id2 = idseq 2 as Element of Permutations 2 by Lm1;
    reconsider rid2 = Rev idseq 2 as Element of Permutations 2 by Th4;
    set F = the add of K;
    set f = Path_product M;
    len Permutations 2 = 2 by MATRIX_2:20; then
A1: Id2 is even by MATRIX_2:29;
A2: f.id2 = -((the mult of K) $$ Path_matrix (Id2,M),Id2)
           by MATRIX_3:def 8
         .= (the mult of K) $$ Path_matrix (Id2,M)
           by MATRIX_2:def 16,A1
         .= (the mult of K) $$ <*a,d*> by Th9
         .= a*d by Th11;
    reconsider f1 = f.Id2, f2 = f.rid2 as Element of K;
    1 in Seg 2; then
A3: id2.1 = 1 by FUNCT_1:35;
A4: rid2 = <*2,1*> by FINSEQ_5:64,FINSEQ_2:61;
A5: id2 <> rid2 by A3,Th2;
    len Permutations 2 = 2 by MATRIX_2:20; then
A6: rid2 is odd by A4,Th12;
A7: f.rid2 = -((the mult of K) $$ Path_matrix (rid2,M),rid2)
       by MATRIX_3:def 8
       .= -(the mult of K) $$ Path_matrix (rid2,M) by A6,MATRIX_2:def 16
       .= -(the mult of K) $$ <*b,c*> by Th10
       .= -b*c by Th11;
    Permutations 2 is finite by MATRIX_2:30; then
A8: FinOmega Permutations 2 = Permutations 2 by MATRIX_2:def 17;
    Det M = F $$ ({.Id2,rid2.},f) by A8,Th6,MATRIX_3:def 9
      .= a*d - b*c by  A5,SETWOP_2:3,A2,A7;
    hence thesis;
  end;

theorem
  Per (a,b)][(c,d) = a * d + b * c
  proof
    set M = (a,b)][(c,d);
    reconsider Id2 = idseq 2 as Element of Permutations 2 by Lm1;
    reconsider id2 = Id2 as Permutation of Seg 2;
    reconsider rid2 = Rev idseq 2 as Element of Permutations 2 by Th4;
    set F = the add of K;
    set f = PPath_product M;
A1: f.id2 = (the mult of K) $$ Path_matrix (Id2,M) by Def1
         .= (the mult of K) $$ <*a,d*> by Th9
         .= a*d by Th11;
A2: f.rid2 = (the mult of K) $$ Path_matrix (rid2,M) by Def1
       .= (the mult of K) $$ <*b,c*> by Th10
       .= b*c by Th11;
    Permutations 2 is finite by MATRIX_2:30; then
A3: FinOmega Permutations 2 = Permutations 2 by MATRIX_2:def 17;
    reconsider f1 = f.Id2, f2 = f.rid2 as Element of K;
    1 in Seg 2; then
A4: id2 <> rid2 by FUNCT_1:35,Th2;
    Per M = a*d + b*c by  A3,A4,Th6,SETWOP_2:3,A1,A2;
    hence thesis;
  end;

theorem Th15:
  Rev idseq 3 = <*3,2,1*>
  proof
    reconsider h = <*2,3*> as FinSequence of NAT;
    set g = Rev h;
A1: <*1*>^h = <*1,2,3*> by FINSEQ_1:60;
    Rev h = <*3,2*> by FINSEQ_5:64;
    hence thesis by A1,FINSEQ_2:62,FINSEQ_6:28;
  end;

 reserve D for non empty set;

theorem
  for x,y,z being Element of D
  for f being FinSequence of D st f = <*x,y,z*> holds
  Rev f = <*z,y,x*>
  proof
    let x,y,z be Element of D;
    let f be FinSequence of D;
    reconsider h = <*y,z*> as FinSequence of D;
    set g = Rev h;
    assume f = <*x,y,z*>; then
A1: f = <*x*>^h by FINSEQ_1:60;
    Rev h = <*z,y*> by FINSEQ_5:64;
    hence thesis by A1,FINSEQ_6:28;
  end;

theorem Th17:
  for f, g being FinSequence st f ^ g in Permutations n holds
    f ^ Rev g in Permutations n
  proof
    let f, g be FinSequence;
    assume A1: f ^ g in Permutations n;
A2: f ^ g is Permutation of Seg n by MATRIX_2:def 11,A1;
A3: rng (f^g) = Seg n by A2,FUNCT_2:def 3;
    dom (f ^ g) = Seg n by FUNCT_2:67,A2; then
A4: Seg n = Seg (len f + len g) by FINSEQ_1:def 7;
A5: len g = len Rev g by FINSEQ_5:def 3;
A6: dom g = dom Rev g & rng g = rng Rev g by FINSEQ_5:60;
A7: rng (f^g) = rng f \/ rng g by FINSEQ_1:44
    .= rng (f^Rev g) by A6,FINSEQ_1:44;
A8: rng f misses rng g & f is one-to-one & g is one-to-one
      by FINSEQ_3:98,A2;then
A9: Rev g is one-to-one by Lm5;
    set h = f ^ Rev g;
A10: dom h = Seg n by A4,A5,FINSEQ_1:def 7;
    rng h = Seg n by A7,A3; then
    reconsider h as FinSequence-like Function of
      Seg n, Seg n by A10,FUNCT_2:4;
A11: h is onto by FUNCT_2:def 3,A7,A3;
    h is one-to-one by A9,A8,A6,FINSEQ_3:98;then
    h is bijective by FUNCT_2:def 4,A11;
    hence thesis by MATRIX_2:def 11;
  end;

theorem Th18:
  for f, g being FinSequence st f ^ g in Permutations n holds
    g ^ f in Permutations n
  proof
    let f, g be FinSequence;
    assume A1: f ^ g in Permutations n;
A2: f ^ g is Permutation of Seg n by MATRIX_2:def 11,A1;
A3: f ^ g is Function of Seg n,Seg n & rng (f ^ g) = Seg n
      by FUNCT_2:def 3,A2;then
A4: dom (f ^ g) = Seg n by FUNCT_2:67;
A5: len(f^g) = len f + len g by FINSEQ_1:35
            .= len (g^f) by FINSEQ_1:35;
    Seg len(f^g) = dom (f^g) by FINSEQ_1:def 3; then
A6: dom (f^g) = dom (g^f) by A5,FINSEQ_1:def 3;
A7: rng (f^g) = rng f \/ rng g by FINSEQ_1:44
    .= rng (g^f) by FINSEQ_1:44;
A8: rng f misses rng g & f is one-to-one & g is one-to-one
      by FINSEQ_3:98,A2;
    reconsider h = g ^ f as FinSequence-like Function of Seg n, Seg n
      by FUNCT_2:4,A7,A3,A6,A4;
A9: h is onto by FUNCT_2:def 3,A3,A7;
    h is one-to-one by A8,FINSEQ_3:98;then
    h is bijective by FUNCT_2:def 4,A9;
    hence thesis by MATRIX_2:def 11;
  end;

theorem Th19:
  Permutations 3 =
    {<*1,2,3*>,<*3,2,1*>,<*1,3,2*>,<*2,3,1*>,<*2,1,3*>,<*3,1,2*>}
    proof
     now let p be set;
     assume p in Permutations 3; then
     reconsider q=p as Permutation of Seg 3 by MATRIX_2:def 11;
A1:  q is Function of Seg 3,Seg 3 & rng q = Seg 3 by FUNCT_2:def 3;
A2:  dom q = Seg 3 by FUNCT_2:67;
A3:  1 in Seg 3 & 2 in Seg 3 & 3 in Seg 3;
     q.1 in Seg 3 by A1,FUNCT_2:6,A3;then
A4:  q.1 = 1 or q.1 = 2 or q.1 = 3 by ENUMSET1:def 1,FINSEQ_3:1;
     q.2 in Seg 3 by A1,FUNCT_2:6,A3;then
A5:  q.2 = 1 or q.2 = 2 or q.2 = 3 by ENUMSET1:def 1,FINSEQ_3:1;
     q.3 in Seg 3 by A1,FUNCT_2:6,A3;then
A6:  q.3 = 1 or q.3 = 2 or q.3 = 3 by ENUMSET1:def 1,FINSEQ_3:1;
     q.1=1 & q.2=2 & q.3=3 or
     q.1=3 & q.2=2 & q.3=1 or
     q.1=1 & q.2=3 & q.3=2 or
     q.1=2 & q.2=3 & q.3=1 or
     q.1=2 & q.2=1 & q.3=3 or
     q.1=3 & q.2=1 & q.3=2 implies q=<*1,2,3*> or q=<*3,2,1*> or q=<*1,3,2*> or
     q=<*2,3,1*> or q=<*2,1,3*> or q=<*3,1,2*>
     proof
       reconsider q as FinSequence by A2,FINSEQ_1:def 2;
       len q = 3 by A2,FINSEQ_1:def 3;
       hence thesis by FINSEQ_1:62;
     end; then
     p in {<*1,2,3*>,<*3,2,1*>} or q in {<*1,3,2*>,<*2,3,1*>} or
       q in {<*2,1,3*>,<*3,1,2*>}
         by A6,TARSKI:def 2,A4,A5,FUNCT_1:def 8,A2,A3;then
     p in {<*1,2,3*>,<*3,2,1*>} or q in {<*1,3,2*>,<*2,3,1*>}\/
       {<*2,1,3*>,<*3,1,2*>} by XBOOLE_0:def 2;then
     p in {<*1,2,3*>,<*3,2,1*>} or
       q in {<*1,3,2*>,<*2,3,1*>,<*2,1,3*>,<*3,1,2*>} by ENUMSET1:45;then
     p in {<*1,2,3*>,<*3,2,1*>} \/ {<*1,3,2*>,<*2,3,1*>,<*2,1,3*>,<*3,1,2*>}
       by XBOOLE_0:def 2;
     hence p in {<*1,2,3*>,<*3,2,1*>,<*1,3,2*>,<*2,3,1*>,<*2,1,3*>,<*3,1,2*>}
       by ENUMSET1:52;
    end;then
A7: Permutations 3 c=
      {<*1,2,3*>,<*3,2,1*>,<*1,3,2*>,<*2,3,1*>,<*2,1,3*>,<*3,1,2*>}
        by TARSKI:def 3;
     now let x;
       assume x in {<*1,2,3*>,<*3,2,1*>,<*1,3,2*>,<*2,3,1*>,
         <*2,1,3*>,<*3,1,2*>};then
       x in {<*1,2,3*>,<*3,2,1*>}\/{<*1,3,2*>,<*2,3,1*>,<*2,1,3*>,<*3,1,2*>}
         by ENUMSET1:52; then
       x in {<*1,2,3*>,<*3,2,1*>} or
         x in {<*1,3,2*>,<*2,3,1*>,<*2,1,3*>,<*3,1,2*>}
           by XBOOLE_0:def 2;then
       x in {<*1,2,3*>,<*3,2,1*>} or
         x in {<*1,3,2*>,<*2,3,1*>} \/ {<*2,1,3*>,<*3,1,2*>}
           by ENUMSET1:45;then
       x in {<*1,2,3*>,<*3,2,1*>} or x in {<*1,3,2*>,<*2,3,1*>} or
         x in {<*2,1,3*>,<*3,1,2*>} by XBOOLE_0:def 2;then
A8:    x=<*1,2,3*> or x=<*3,2,1*> or x=<*1,3,2*> or x=<*2,3,1*> or
         x=<*2,1,3*> or x=<*3,1,2*> by TARSKI:def 2;
A9:    idseq 3 in Permutations 3 by Lm1;
A10:    Rev idseq 3 in Permutations 3 by Th4;
A11:   <*1,3,2*> in Permutations 3
      proof
        reconsider f = <*1,2,3*> as one-to-one FinSequence-like Function
          of Seg 3,Seg 3 by FINSEQ_2:62;
        reconsider h = <*2,3*> as FinSequence of NAT;
A12:     f = <*1*> ^ h by FINSEQ_1:60;
        Rev h = <*3,2*> by FINSEQ_5:64;then
        <*1*>^Rev h = <*1,3,2*> by FINSEQ_1:60;
        hence thesis by A12,A9,FINSEQ_2:62,Th17;
       end;
A13:    <*2,3,1*> in Permutations 3
       proof
        reconsider f = <*1,2,3*> as one-to-one FinSequence-like Function
          of Seg 3,Seg 3 by FINSEQ_2:62;
        reconsider h = <*2,3*> as FinSequence of NAT;
        f = <*1*> ^ h by FINSEQ_1:60;
        hence thesis by Th18,A9,FINSEQ_2:62;
       end;
A14:    <*2,1,3*> in Permutations 3
       proof
        reconsider f = <*1,2,3*> as one-to-one FinSequence-like Function
          of Seg 3,Seg 3 by FINSEQ_2:62;
        reconsider h = <*1,2*> as FinSequence of NAT;
A15:     <*3*> ^ h in Permutations 3 by Th18,A9,FINSEQ_2:62;
A16:     Rev h = <*2,1*> by FINSEQ_5:64;
        <*3*> ^ Rev h in Permutations 3 by Th17,A15;
        hence thesis by Th18,A16;
       end;
      <*3,1,2*> in Permutations 3
      proof
        reconsider f = <*1,2,3*> as one-to-one FinSequence-like Function
          of Seg 3,Seg 3 by FINSEQ_2:62;
        reconsider h = <*1,2*> as FinSequence of NAT;
        <*3*> ^ h = <*3,1,2*> by FINSEQ_1:60;
        hence thesis by Th18,A9,FINSEQ_2:62;
       end;
       hence x in Permutations 3 by A8,A9,A10,A11,A13,A14,Th15,FINSEQ_2:62;
     end; then
     {<*1,2,3*>,<*3,2,1*>,<*1,3,2*>,<*2,3,1*>,<*2,1,3*>,<*3,1,2*>}
     c= Permutations 3 by TARSKI:def 3;
    hence thesis by XBOOLE_0:def 10, A7;
  end;

theorem Th20:
  for a,b,c,d,e,f,g,h,i being Element of K
  for M being Matrix of 3,K st M = <*<*a,b,c*>,<*d,e,f*>,<*g,h,i*>*>
  for p being Element of Permutations 3 st p = <*1,2,3*> holds
  Path_matrix (p,M) = <* a,e,i *>
  proof
    let a,b,c,d,e,f,g,h,i be Element of K;
    let M be Matrix of 3, K;
    assume A1: M = <*<*a,b,c*>,<*d,e,f*>,<*g,h,i*>*>;
    let p be Element of Permutations 3;
    assume A2: p = <*1,2,3*>;
A3: len M = 3 by MATRIX_1:def 3;then
A4: width M = 3 by MATRIX_1:20;
A5: len Path_matrix (p,M) = 3 by MATRIX_3:def 7;then
A6: dom Path_matrix (p,M) = Seg 3 by FINSEQ_1:def 3;
A7: 1 in dom Path_matrix (p,M) by A6;
A8: 1 = p.1 by FINSEQ_1:62,A2;
A9: 2 in dom Path_matrix (p,M) by A6;
A10: 2 = p.2 by FINSEQ_1:62,A2;
A11: 3 in dom Path_matrix (p,M) by A6;
A12: 3 = p.3 by FINSEQ_1:62,A2;
A13: M.1 = <*a,b,c*> by A1,FINSEQ_1:62;
    [1,1] in Indices M by GOBOARD7:10,A3,A4; then
    consider r being FinSequence of the carrier of K such that
A14: r = M.1 & M*(1,1) = r.1 by MATRIX_1:def 6;
A15: r.1 = a by FINSEQ_1:62,A14,A13;
A16: Path_matrix (p,M).1 = a by  A7,A8,MATRIX_3:def 7,A15,A14;
A17: M.2 = <*d,e,f*> by A1,FINSEQ_1:62;
    [2,2] in Indices M by GOBOARD7:10,A3,A4; then
    consider y being FinSequence of the carrier of K such that
A18: y = M.2 & M*(2,2) = y.2 by MATRIX_1:def 6;
A19: y.2 = e by FINSEQ_1:62,A18,A17;
A20: Path_matrix (p,M).2 = e by  A9,A10,MATRIX_3:def 7,A19,A18;
A21: M.3 = <*g,h,i*> by A1,FINSEQ_1:62;
    [3,3] in Indices M by GOBOARD7:10,A3,A4; then
    consider z being FinSequence of the carrier of K such that
A22: z = M.3 & M*(3,3) = z.3 by MATRIX_1:def 6;
A23: z.3 = i by FINSEQ_1:62,A21,A22;
    Path_matrix (p,M).3 = i by  A11,A12,MATRIX_3:def 7,A23,A22;
    hence thesis by A16,A20,FINSEQ_1:62,A5;
  end;

theorem Th21:
  for a,b,c,d,e,f,g,h,i being Element of K
  for M being Matrix of 3,K st M = <*<*a,b,c*>,<*d,e,f*>,<*g,h,i*>*>
  for p being Element of Permutations 3 st p = <*3,2,1*> holds
  Path_matrix (p,M) = <* c,e,g *>
  proof
    let a,b,c,d,e,f,g,h,i be Element of K;
    let M be Matrix of 3, K;
    assume A1: M = <*<*a,b,c*>,<*d,e,f*>,<*g,h,i*>*>;
    let p be Element of Permutations 3;
    assume
A2: p = <*3,2,1*>;
A3: len M = 3 by MATRIX_1:def 3;then
A4: width M = 3 by MATRIX_1:20;
A5: len Path_matrix (p,M) = 3 by MATRIX_3:def 7;then
A6: dom Path_matrix (p,M)= Seg 3 by FINSEQ_1:def 3;
A7: 1 in dom Path_matrix (p,M) by A6;
A8: 3 = p.1 by FINSEQ_1:62,A2;
A9: 2 in dom Path_matrix (p,M) by A6;
A10: 2 = p.2 by FINSEQ_1:62,A2;
A11: 3 in dom Path_matrix (p,M) by A6;
A12: 1 = p.3 by FINSEQ_1:62,A2;
A13: M.1 = <*a,b,c*> by A1,FINSEQ_1:62;
    [1,3] in Indices M by GOBOARD7:10,A3,A4; then
    consider r being FinSequence of the carrier of K such that
A14: r = M.1 & M*(1,3) = r.3 by MATRIX_1:def 6;
A15: r.3 = c by FINSEQ_1:62,A13,A14;
A16: Path_matrix (p,M).1 = c by  A7,A8,MATRIX_3:def 7,A15,A14;
A17: M.2 = <*d,e,f*> by A1,FINSEQ_1:62;
    [2,2] in Indices M by GOBOARD7:10,A3,A4; then
    consider y being FinSequence of the carrier of K such that
A18: y = M.2 & M*(2,2) = y.2 by MATRIX_1:def 6;
A19: y.2 = e by FINSEQ_1:62,A17,A18;
A20: Path_matrix (p,M).2 = e by  A9,A10,MATRIX_3:def 7,A19,A18;
A21: M.3 = <*g,h,i*> by A1,FINSEQ_1:62;
    [3,1] in Indices M by GOBOARD7:10,A3,A4; then
    consider z being FinSequence of the carrier of K such that
A22: z = M.3 & M*(3,1) = z.1 by MATRIX_1:def 6;
A23: z.1 = g by FINSEQ_1:62,A21,A22;
    Path_matrix (p,M).3 = g by  A11,A12,MATRIX_3:def 7,A23,A22;
    hence thesis by A16,A20,FINSEQ_1:62,A5;
  end;

theorem Th22:
  for a,b,c,d,e,f,g,h,i being Element of K
  for M being Matrix of 3,K st M = <*<*a,b,c*>,<*d,e,f*>,<*g,h,i*>*>
  for p being Element of Permutations 3 st p = <*1,3,2*> holds
  Path_matrix (p,M) = <* a,f,h *>
  proof
    let a,b,c,d,e,f,g,h,i be Element of K;
    let M be Matrix of 3, K;
    assume A1: M = <*<*a,b,c*>,<*d,e,f*>,<*g,h,i*>*>;
    let p be Element of Permutations 3;
    assume A2: p = <*1,3,2*>;
A3: len M = 3 by MATRIX_1:def 3;then
A4: width M = 3 by MATRIX_1:20;
A5: len Path_matrix (p,M) = 3 by MATRIX_3:def 7;then
A6: dom Path_matrix (p,M)= Seg 3 by FINSEQ_1:def 3;
A7: 1 in dom Path_matrix (p,M) by A6;
A8: 1 = p.1 by FINSEQ_1:62,A2;
A9: 2 in dom Path_matrix (p,M) by A6;
A10: 3 = p.2 by FINSEQ_1:62,A2;
A11: 3 in dom Path_matrix (p,M) by A6;
A12: 2 = p.3 by FINSEQ_1:62,A2;
A13: M.1 = <*a,b,c*> by A1,FINSEQ_1:62;
    [1,1] in Indices M by A3,A4,GOBOARD7:10; then
    consider r being FinSequence of the carrier of K such that
A14: r = M.1 & M*(1,1) = r.1 by MATRIX_1:def 6;
A15: r.1 = a by FINSEQ_1:62,A13,A14;
A16: Path_matrix (p,M).1 = a by  A7,A8,MATRIX_3:def 7,A15,A14;
A17: M.2 = <*d,e,f*> by A1,FINSEQ_1:62;
    [2,3] in Indices M by GOBOARD7:10,A3,A4; then
    consider y being FinSequence of the carrier of K such that
A18: y = M.2 & M*(2,3) = y.3 by MATRIX_1:def 6;
A19: y.3 = f by FINSEQ_1:62,A17,A18;
A20: Path_matrix (p,M).2 = f by  A9,A10,MATRIX_3:def 7,A19,A18;
A21: M.3 = <*g,h,i*> by A1,FINSEQ_1:62;
    [3,2] in Indices M by GOBOARD7:10,A3,A4; then
    consider z being FinSequence of the carrier of K such that
A22: z = M.3 & M*(3,2) = z.2 by MATRIX_1:def 6;
A23: z.2 = h by FINSEQ_1:62,A21,A22;
    Path_matrix (p,M).3 = h by  A11,A12,MATRIX_3:def 7,A23,A22;
    hence thesis by A16,A20,FINSEQ_1:62,A5;
  end;

theorem Th23:
  for a,b,c,d,e,f,g,h,i being Element of K
  for M being Matrix of 3,K st M = <*<*a,b,c*>,<*d,e,f*>,<*g,h,i*>*>
  for p being Element of Permutations 3 st p = <*2,3,1*> holds
  Path_matrix (p,M) = <* b,f,g *>
    proof
    let a,b,c,d,e,f,g,h,i be Element of K;
    let M be Matrix of 3, K;
    assume A1: M = <*<*a,b,c*>,<*d,e,f*>,<*g,h,i*>*>;
    let p be Element of Permutations 3;
    assume A2: p = <*2,3,1*>;
A3: len M = 3 by MATRIX_1:def 3;then
A4: width M = 3 by MATRIX_1:20;
A5: len Path_matrix (p,M) = 3 by MATRIX_3:def 7;then
A6: dom Path_matrix (p,M)= Seg 3 by FINSEQ_1:def 3;
A7: 1 in dom Path_matrix (p,M) by A6;
A8: 2 = p.1 by FINSEQ_1:62,A2;
A9: 2 in dom Path_matrix (p,M) by A6;
A10: 3 = p.2 by FINSEQ_1:62,A2;
A11: 3 in dom Path_matrix (p,M) by A6;
A12: 1 = p.3 by FINSEQ_1:62,A2;
A13: M.1 = <*a,b,c*> by A1,FINSEQ_1:62;
    [1,2] in Indices M by A3,A4,GOBOARD7:10; then
    consider r being FinSequence of the carrier of K such that
A14: r = M.1 & M*(1,2) = r.2 by MATRIX_1:def 6;
A15: r.2 = b by FINSEQ_1:62,A13,A14;
A16: Path_matrix (p,M).1 = b by  A7,A8,MATRIX_3:def 7,A15,A14;
A17: M.2 = <*d,e,f*> by A1,FINSEQ_1:62;
    [2,3] in Indices M by GOBOARD7:10,A3,A4; then
    consider y being FinSequence of the carrier of K such that
A18: y = M.2 & M*(2,3) = y.3 by MATRIX_1:def 6;
A19: y.3 = f by FINSEQ_1:62,A17,A18;
A20: Path_matrix (p,M).2 = f by  A9,A10,MATRIX_3:def 7,A19,A18;
A21: M.3 = <*g,h,i*> by A1,FINSEQ_1:62;
    [3,1] in Indices M by GOBOARD7:10,A3,A4; then
    consider z being FinSequence of the carrier of K such that
A22: z = M.3 & M*(3,1) = z.1 by MATRIX_1:def 6;
A23: z.1 = g by FINSEQ_1:62,A21,A22;
    Path_matrix (p,M).3 = g by  A11,A12,MATRIX_3:def 7,A23,A22;
    hence thesis by A16,A20,FINSEQ_1:62,A5;
  end;

theorem Th24:
  for a,b,c,d,e,f,g,h,i being Element of K
  for M being Matrix of 3,K st M = <*<*a,b,c*>,<*d,e,f*>,<*g,h,i*>*>
  for p being Element of Permutations 3 st p = <*2,1,3*> holds
  Path_matrix (p,M) = <* b,d,i *>
    proof
    let a,b,c,d,e,f,g,h,i be Element of K;
    let M be Matrix of 3, K;
    assume A1: M = <*<*a,b,c*>,<*d,e,f*>,<*g,h,i*>*>;
    let p be Element of Permutations 3;
    assume A2: p = <*2,1,3*>;
A3: len M = 3 by MATRIX_1:def 3;then
A4: width M = 3 by MATRIX_1:20;
A5: len Path_matrix (p,M) = 3 by MATRIX_3:def 7;then
A6: dom Path_matrix (p,M)= Seg 3 by FINSEQ_1:def 3;
A7: 1 in dom Path_matrix (p,M) by A6;
A8: 2 = p.1 by FINSEQ_1:62,A2;
A9: 2 in dom Path_matrix (p,M) by A6;
A10: 1 = p.2 by FINSEQ_1:62,A2;
A11: 3 in dom Path_matrix (p,M) by A6;
A12: 3 = p.3 by FINSEQ_1:62,A2;
A13: M.1 = <*a,b,c*> by A1,FINSEQ_1:62;
    [1,2] in Indices M by GOBOARD7:10,A3,A4; then
    consider r being FinSequence of the carrier of K such that
A14: r = M.1 & M*(1,2) = r.2 by MATRIX_1:def 6;
A15: r.2 = b by FINSEQ_1:62,A13,A14;
A16: Path_matrix (p,M).1 = b by  A7,A8,MATRIX_3:def 7,A15,A14;
A17: M.2 = <*d,e,f*> by A1,FINSEQ_1:62;
    [2,1] in Indices M by GOBOARD7:10,A3,A4; then
    consider y being FinSequence of the carrier of K such that
A18: y = M.2 & M*(2,1) = y.1 by MATRIX_1:def 6;
A19: y.1 = d by FINSEQ_1:62,A17,A18;
A20: Path_matrix (p,M).2 = d by  A9,A10,MATRIX_3:def 7,A19,A18;
A21: M.3 = <*g,h,i*> by A1,FINSEQ_1:62;
    [3,3] in Indices M by GOBOARD7:10,A3,A4; then
    consider z being FinSequence of the carrier of K such that
A22: z = M.3 & M*(3,3) = z.3 by MATRIX_1:def 6;
A23: z.3 = i by FINSEQ_1:62,A21,A22;
    Path_matrix (p,M).3 = i by  A11,A12,MATRIX_3:def 7,A23,A22;
    hence thesis by A16,A20,FINSEQ_1:62,A5;
  end;

theorem Th25:
  for a,b,c,d,e,f,g,h,i being Element of K
  for M being Matrix of 3,K st M = <*<*a,b,c*>,<*d,e,f*>,<*g,h,i*>*>
  for p being Element of Permutations 3 st p = <*3,1,2*> holds
  Path_matrix (p,M) = <* c,d,h *>
    proof
    let a,b,c,d,e,f,g,h,i be Element of K;
    let M be Matrix of 3, K;
    assume A1: M = <*<*a,b,c*>,<*d,e,f*>,<*g,h,i*>*>;
    let p be Element of Permutations 3;
    assume A2: p = <*3,1,2*>;
A3: len M = 3 by MATRIX_1:def 3;then
A4: width M = 3 by MATRIX_1:20;
A5: len Path_matrix (p,M) = 3 by MATRIX_3:def 7;then
A6: dom Path_matrix (p,M) = Seg 3 by FINSEQ_1:def 3;
A7: 1 in dom Path_matrix (p,M) by A6;
A8: 3 = p.1 by FINSEQ_1:62,A2;
A9: 2 in dom Path_matrix (p,M) by A6;
A10: 1 = p.2 by FINSEQ_1:62,A2;
A11: 3 in dom Path_matrix (p,M) by A6;
A12: 2 = p.3 by FINSEQ_1:62,A2;
A13: M.1 = <*a,b,c*> by A1,FINSEQ_1:62;
    [1,3] in Indices M by GOBOARD7:10,A3,A4; then
    consider r being FinSequence of the carrier of K such that
A14: r = M.1 & M*(1,3) = r.3 by MATRIX_1:def 6;
A15: r.3 = c by FINSEQ_1:62,A13,A14;
A16: Path_matrix (p,M).1 = c by  A7,A8,MATRIX_3:def 7,A15,A14;
A17: M.2 = <*d,e,f*> by A1,FINSEQ_1:62;
    [2,1] in Indices M by GOBOARD7:10,A3,A4; then
    consider y being FinSequence of the carrier of K such that
A18: y = M.2 & M*(2,1) = y.1 by MATRIX_1:def 6;
A19: y.1 = d by FINSEQ_1:62,A17,A18;
A20: Path_matrix (p,M).2 = d by  A9,A10,MATRIX_3:def 7,A19,A18;
A21: M.3 = <*g,h,i*> by A1,FINSEQ_1:62;
    [3,2] in Indices M by GOBOARD7:10,A3,A4; then
    consider z being FinSequence of the carrier of K such that
A22: z = M.3 & M*(3,2) = z.2 by MATRIX_1:def 6;
A23: z.2 = h by FINSEQ_1:62,A21,A22;
    Path_matrix (p,M).3 = h by  A11,A12,MATRIX_3:def 7,A23,A22;
    hence thesis by A16,A20,FINSEQ_1:62,A5;
  end;

theorem Th26:
  (the mult of K) $$ <* a,b,c *> = a * b * c
  proof
    (the mult of K) $$ <* a,b,c *>
       = Product <* a,b,c *> by GROUP_4:def 3
      .= a * b * c by FVSUM_1:104;
    hence thesis;
  end;

theorem Th27:
  <*1,3,2*> in Permutations 3 & <*2,3,1*> in Permutations 3 &
  <*2,1,3*> in Permutations 3 & <*3,1,2*> in Permutations 3 &
  <*1,2,3*> in Permutations 3 & <*3,2,1*> in Permutations 3
  proof
A1:  idseq 3 in Permutations 3 by Lm1;
     reconsider f = <*1,2,3*> as one-to-one FinSequence-like Function
       of Seg 3,Seg 3 by FINSEQ_2:62;
A2:  <*1,3,2*> in Permutations 3
     proof
        set h = <*2,3*>;
A3:     f = <*1*> ^ h by FINSEQ_1:60;
A4:     f in Permutations 3 by Lm1,FINSEQ_2:62;
        Rev h = <*3,2*> by FINSEQ_5:64;then
        <*1*>^Rev h = <*1,3,2*> by FINSEQ_1:60;
        hence thesis by A3,A4,Th17;
       end;
A5:    <*2,3,1*> in Permutations 3
       proof
        f = <*1*> ^ <*2,3*> by FINSEQ_1:60;
        hence thesis by Th18,A1,FINSEQ_2:62;
       end;
       set h = <*1,2*>;
A6:    <*2,1,3*> in Permutations 3
       proof
A7:     <*3*> ^ h in Permutations 3 by A1,Th18,FINSEQ_2:62;
A8:     Rev h = <*2,1*> by FINSEQ_5:64;
        <*3*> ^ Rev h in Permutations 3 by Th17,A7;
        hence thesis by Th18,A8;
       end;
A9:  <*3,1,2*> in Permutations 3
     proof
       <*3*> ^ h = <*3,1,2*> by FINSEQ_1:60;
       hence thesis by Th18,A1,FINSEQ_2:62;
     end;
     <*3,2,1*> in Permutations 3 by Th15,Th5,A1;
     hence thesis by A2,A5,A6,A9,FINSEQ_2:62,Lm1;
   end;

Lm6:
  <*1,2,3*> <> <*3,2,1*> & <*1,2,3*> <> <*1,3,2*> & <*1,2,3*> <> <*2,3,1*> &
  <*1,2,3*> <> <*2,1,3*> & <*1,2,3*> <> <*3,1,2*> &
  <*3,2,1*> <> <*1,3,2*> & <*3,2,1*> <> <*2,3,1*> & <*3,2,1*> <> <*2,1,3*> &
  <*3,2,1*> <> <*3,1,2*> &
  <*1,3,2*> <> <*2,3,1*> & <*1,3,2*> <> <*2,1,3*> & <*1,3,2*> <> <*3,1,2*> &
  <*2,3,1*> <> <*2,1,3*> & <*2,3,1*> <> <*3,1,2*> & <*2,1,3*> <> <*3,1,2*>
    by GROUP_7:3;

theorem Th28:
  <*2,3,1*>" = <*3,1,2*>
proof
    set f = <*2,3,1*>, g = <*3,1,2*>;
A1: dom f = {1,2,3} by FINSEQ_3:1,30;
A2: f is one-to-one by FINSEQ_3:104;
    rng f = {2,3,1} by FINSEQ_2:148 .= {1,2,3} by ENUMSET1:100; then
A3: rng f = dom g by FINSEQ_3:1,30; then
A4: dom (g * f) = {1,2,3} by A1,RELAT_1:46; then
A5: dom (g * f) = dom id {1,2,3} by RELAT_1:71;
    for x being set st x in dom (g * f) holds
      (g * f).x = (id {1,2,3}).x
    proof
      let x be set;
      assume
A6:   x in dom (g * f); then
A7:   (g * f).x = g.(f.x) by FUNCT_1:22;
A8:   x in {1,2,3} by A6,A4;
      per cases by A8,ENUMSET1:def 1;
      suppose
A9:     x = 1; then
        g.(f.x) = g.2 by FINSEQ_1:62
               .= 1 by FINSEQ_1:62
               .= (id {1,2,3}).x by A9,A8,FUNCT_1:35;
        hence thesis by A7;
      end;
      suppose
A10:     x = 2; then
        g.(f.x) = g.3 by FINSEQ_1:62
               .= 2 by FINSEQ_1:62
               .= (id {1,2,3}).x by A10,A8,FUNCT_1:35;
        hence thesis by A7;
      end;
      suppose
A11:     x = 3; then
        g.(f.x) = g.1 by FINSEQ_1:62
               .= 3 by FINSEQ_1:62
               .= (id {1,2,3}).x by A11,A8,FUNCT_1:35;
        hence thesis by A7;
      end;
    end; then
    g * f = id {1,2,3} by A5,FUNCT_1:9;
    hence thesis by A1,A2,A3,FUNCT_1:63;
end;

theorem
  for a being Element of Group_of_Perm 3 st
    a = <*2,3,1*> holds a" = <*3,1,2*>
  proof
    let a be Element of Group_of_Perm 3;
    assume
A1: a = <*2,3,1*>;
    reconsider a1 = a as Element of Permutations 3 by MATRIX_2:def 13;
    a1" = <*3,1,2*> by A1,Th28;
    hence thesis by MATRIX_7:27;
  end;

begin :: Transpositions

theorem Th30:
  for p being Permutation of Seg 3 st p = <* 1,3,2 *> holds
    p is_transposition
  proof
    let p be Permutation of Seg 3;
    assume
A1: p = <*1,3,2*>; then
A2: dom p = {1,2,3} by  FINSEQ_3:30,1;
    ex i,j st i in dom p & j in dom p & i <> j & p.i = j & p.j = i &
      for k st k <> i & k <> j & k in dom p holds p.k = k
    proof
      take i = 2, j = 3;
      thus i in dom p & j in dom p by A2,ENUMSET1:def 1;
      for k st k <> i & k <> j & k in dom p holds p.k = k
      proof
        let k; assume k <> i & k <> j & k in dom p; then
        k = 1 by A2,ENUMSET1:def 1;
        hence thesis by A1,FINSEQ_1:62;
      end;
      hence thesis by A1,FINSEQ_1:62;
    end;
    hence thesis by MATRIX_2:def 14;
  end;

theorem Th31:
  for p being Permutation of Seg 3 st p = <* 2,1,3 *> holds
    p is_transposition
  proof
    let p be Permutation of Seg 3;
    assume
A1: p = <*2,1,3*>; then
A2: dom p = {1,2,3} by  FINSEQ_3:30,1;
    ex i,j st i in dom p & j in dom p & i <> j & p.i = j & p.j = i &
      for k st k <> i & k <> j & k in dom p holds p.k = k
    proof
      take i = 1, j = 2;
      thus i in dom p & j in dom p by A2,ENUMSET1:def 1;
      for k st k <> i & k <> j & k in dom p holds p.k = k
      proof
        let k; assume k <> i & k <> j & k in dom p; then
        k = 3 by A2,ENUMSET1:def 1;
        hence thesis by A1,FINSEQ_1:62;
      end;
      hence thesis by A1,FINSEQ_1:62;
    end;
    hence thesis by MATRIX_2:def 14;
  end;

theorem
  for p being Permutation of Seg 3 st p = <* 3,2,1 *> holds
    p is_transposition
  proof
    let p be Permutation of Seg 3;
    assume
A1: p = <*3,2,1*>; then
A2: dom p = {1,2,3} by  FINSEQ_3:30,1;
    ex i,j st i in dom p & j in dom p & i <> j & p.i = j & p.j = i &
      for k st k <> i & k <> j & k in dom p holds p.k = k
    proof
      take i = 1, j = 3;
      thus i in dom p & j in dom p by A2,ENUMSET1:def 1;
      for k st k <> i & k <> j & k in dom p holds p.k = k
      proof
        let k; assume k <> i & k <> j & k in dom p; then
        k = 2 by A2,ENUMSET1:def 1;
        hence thesis by A1,FINSEQ_1:62;
      end;
      hence thesis by A1,FINSEQ_1:62;
    end;
    hence thesis by MATRIX_2:def 14;
  end;

theorem Th33:
  for p being Permutation of Seg n st p = id Seg n holds
    not p is_transposition
  proof
    let p be Permutation of Seg n;
    assume
A1: p = id Seg n; then
A2: dom p = Seg n by FUNCT_1:34;
    not ex i,j st i in dom p & j in dom p & i <> j & p.i = j & p.j = i &
      for k st k <> i & k <> j & k in dom p holds p.k = k
        by A2,A1,FUNCT_1:34;
    hence thesis by MATRIX_2:def 14;
  end;

theorem Th34:
  for p being Permutation of Seg 3 st p = <* 3,1,2 *> holds
    not p is_transposition
  proof
    let p be Permutation of Seg 3;
    assume
A1: p = <*3,1,2*>; then
A2: dom p = {1,2,3} by  FINSEQ_3:30,1;
    not ex i,j st i in dom p & j in dom p & i <> j & p.i = j & p.j = i &
      for k st k <> i & k <> j & k in dom p holds p.k = k
    proof
      given i,j such that
A3:   i in dom p & j in dom p & i <> j & p.i = j & p.j = i &
      for k st k <> i & k <> j & k in dom p holds p.k = k;
      ex k being Element of NAT st k <> i & k <> j & k in dom p
      proof
A4:     (i = 1 or i = 2 or i = 3) & (j = 1 or j = 2 or j = 3)
          by A3,A2,ENUMSET1:def 1;
        per cases by A4,A3;
        suppose
A5:       i = 1 & j = 2;
          take 3;
          thus thesis by A5,A2,ENUMSET1:def 1;
        end;
        suppose
A6:       i = 2 & j = 3;
          take 1;
          thus thesis by A6,A2,ENUMSET1:def 1;
        end;
        suppose
A7:       i = 1 & j = 3;
          take 2;
          thus thesis by A7,A2,ENUMSET1:def 1;
        end;
        suppose
A8:       i = 2 & j = 1;
          take 3;
          thus thesis by A8,A2,ENUMSET1:def 1;
        end;
        suppose
A9:       i = 3 & j = 1;
          take 2;
          thus thesis by A9,A2,ENUMSET1:def 1;
        end;
        suppose
A10:       i = 3 & j = 2;
          take 1;
          thus thesis by A10,A2,ENUMSET1:def 1;
        end;
      end; then
      consider k such that
A11:   k <> i & k <> j & k in dom p;
A12:   p.k = k by A3,A11;
      per cases by A2,A11,ENUMSET1:def 1;
      suppose k = 1;
        hence thesis by A12,A1,FINSEQ_1:62;
      end;
      suppose k = 2;
        hence thesis by A12,A1,FINSEQ_1:62;
      end;
      suppose k = 3;
        hence thesis by A12,A1,FINSEQ_1:62;
      end;
    end;
    hence thesis by MATRIX_2:def 14;
  end;

theorem Th35:
  for p being Permutation of Seg 3 st p = <* 2,3,1 *> holds
    not p is_transposition
  proof
    let p be Permutation of Seg 3;
    assume
A1: p = <*2,3,1*>; then
A2: dom p = {1,2,3} by  FINSEQ_3:30,1;
    not ex i,j st i in dom p & j in dom p & i <> j & p.i = j & p.j = i &
      for k st k <> i & k <> j & k in dom p holds p.k = k
    proof
      given i,j such that
A3:   i in dom p & j in dom p & i <> j & p.i = j & p.j = i &
      for k st k <> i & k <> j & k in dom p holds p.k = k;
      ex k being Element of NAT st k <> i & k <> j & k in dom p
      proof
A4:     (i = 1 or i = 2 or i = 3) & (j = 1 or j = 2 or j = 3)
          by ENUMSET1:def 1,A3,A2;
        per cases by A4,A3;
        suppose
A5:       i = 1 & j = 2;
          take 3;
          thus thesis by A5,A2,ENUMSET1:def 1;
        end;
        suppose
A6:       i = 2 & j = 3;
          take 1;
          thus thesis by A6,A2,ENUMSET1:def 1;
        end;
        suppose
A7:       i = 1 & j = 3;
          take 2;
          thus thesis by A7,A2,ENUMSET1:def 1;
        end;
        suppose
A8:       i = 2 & j = 1;
          take 3;
          thus thesis by A8,A2,ENUMSET1:def 1;
        end;
        suppose
A9:       i = 3 & j = 1;
          take 2;
          thus thesis by A9,A2,ENUMSET1:def 1;
        end;
        suppose
A10:       i = 3 & j = 2;
          take 1;
          thus thesis by A10,A2,ENUMSET1:def 1;
        end;
      end; then
      consider k such that
A11:   k <> i & k <> j & k in dom p;
A12:   p.k = k by A3,A11;
      per cases by A2,A11,ENUMSET1:def 1;
      suppose k = 1;
        hence thesis by A12,A1,FINSEQ_1:62;
      end;
      suppose k = 2;
        hence thesis by A12,A1,FINSEQ_1:62;
      end;
      suppose k = 3;
        hence thesis by A12,A1,FINSEQ_1:62;
      end;
    end;
    hence thesis by MATRIX_2:def 14;
  end;

begin :: Corrollaries

theorem Th36:
  for f being Permutation of Seg n holds
    f is FinSequence of Seg n
  proof
    let f be Permutation of Seg n;
A1: rng f c= Seg n by RELSET_1:12;
A2: n in NAT by ORDINAL1:def 13;
    per cases;
    suppose n = 0; then
      dom f = Seg n by FUNCT_2:def 1,FINSEQ_1:4; then
      f is FinSequence by A2,FINSEQ_1:def 2;
      hence thesis by A1,FINSEQ_1:def 4;
    end;
    suppose n <> 0; then
      Seg n <> {} by FINSEQ_1:5; then
      dom f = Seg n by FUNCT_2:def 1; then
      f is FinSequence by A2,FINSEQ_1:def 2;
      hence thesis by A1,FINSEQ_1:def 4;
    end;
  end;

theorem Th37:
  <*2,1,3*> * <*1,3,2*> = <*2,3,1*> & <*1,3,2*> * <*2,1,3*> = <*3,1,2*> &
  <*2,1,3*> * <*3,2,1*> = <*3,1,2*> & <*3,2,1*> * <*2,1,3*> = <*2,3,1*> &
  <*3,2,1*> * <*3,2,1*> = <*1,2,3*> & <*2,1,3*> * <*2,1,3*> = <*1,2,3*> &
  <*1,3,2*> * <*1,3,2*> = <*1,2,3*> & <*1,3,2*> * <*2,3,1*> = <*3,2,1*> &
  <*2,3,1*> * <*2,3,1*> = <*3,1,2*> & <*2,3,1*> * <*3,1,2*> = <*1,2,3*> &
  <*3,1,2*> * <*2,3,1*> = <*1,2,3*> & <*3,1,2*> * <*3,1,2*> = <*2,3,1*> &
  <*1,3,2*> * <*3,2,1*> = <*2,3,1*> & <*3,2,1*> * <*1,3,2*> = <*3,1,2*>
  proof
    set f = <*1,3,2*>, g = <*2,1,3*>, h = <*3,2,1*>;
    set F = <*2,3,1*>, G = <*3,1,2*>, H = <*1,2,3*>;
    dom f = {1,2,3} by  FINSEQ_3:30,1; then
A1: 1 in dom f & 2 in dom f & 3 in dom f by ENUMSET1:def 1;
    dom F = {1,2,3} by  FINSEQ_3:30,1; then
A2: 1 in dom F & 2 in dom F & 3 in dom F by ENUMSET1:def 1;
    dom g = {1,2,3} by  FINSEQ_3:30,1; then
A3: 1 in dom g & 2 in dom g & 3 in dom g by ENUMSET1:def 1;
    dom G = {1,2,3} by  FINSEQ_3:30,1; then
A4: 1 in dom G & 2 in dom G & 3 in dom G by ENUMSET1:def 1;
    dom h = {1,2,3} by  FINSEQ_3:30,1; then
A5: 1 in dom h & 2 in dom h & 3 in dom h by ENUMSET1:def 1;
A6: g in Permutations 3 & f in Permutations 3 & h in Permutations 3 &
      G in Permutations 3 & F in Permutations 3 by Th27;
A7: g is Permutation of Seg 3 & h is Permutation of Seg 3 &
      F is Permutation of Seg 3 & G is Permutation of Seg 3 &
        f is Permutation of Seg 3 by MATRIX_2:def 11,Th27;
A8: f is Permutation of Seg 3 by A6,MATRIX_2:def 11;
    reconsider f,g,h,F,G as FinSequence of Seg 3 by A7,Th36;
A9: 3 = len h & 3 = len g & 3 = len f & 3 = len F & 3 = len G
      by FINSEQ_1:62; then
    reconsider gf = g * f as FinSequence of Seg 3 by A8,FINSEQ_2:50;
A10: f is Permutation of Seg 3 & g is Permutation of Seg 3 &
      h is Permutation of Seg 3 & G is Permutation of Seg 3
        & F is Permutation of Seg 3
        by A6,MATRIX_2:def 11;
A11: len gf = 3 by A10,A9,FINSEQ_2:47;
    reconsider fg = f * g as FinSequence of Seg 3 by A7,A9,FINSEQ_2:50;
A12: len fg = 3 by A10,A9,FINSEQ_2:47;
    reconsider fF = f * F as FinSequence of Seg 3 by A7,A9,FINSEQ_2:50;
A13: len fF = 3 by A10,A9,FINSEQ_2:47;
    reconsider fh = f * h as FinSequence of Seg 3 by A7,A9,FINSEQ_2:50;
A14: len fh = 3 by A10,A9,FINSEQ_2:47;
    reconsider hf = h * f as FinSequence of Seg 3 by A7,A9,FINSEQ_2:50;
A15: len hf = 3 by A10,A9,FINSEQ_2:47;
    reconsider FF = F * F as FinSequence of Seg 3 by A7,A9,FINSEQ_2:50;
A16: len FF = 3 by A10,A9,FINSEQ_2:47;
    reconsider FG = F * G as FinSequence of Seg 3 by A7,A9,FINSEQ_2:50;
A17: len FG = 3 by A10,A9,FINSEQ_2:47;
    reconsider GF = G * F as FinSequence of Seg 3 by A7,A9,FINSEQ_2:50;
A18: len GF = 3 by A10,A9,FINSEQ_2:47;
    reconsider GG = G * G as FinSequence of Seg 3 by A7,A9,FINSEQ_2:50;
A19: len GG = 3 by A10,A9,FINSEQ_2:47;
    reconsider gG = g * G as FinSequence of Seg 3 by A10,A9,FINSEQ_2:50;
    reconsider gh = g * h as FinSequence of Seg 3 by A10,A9,FINSEQ_2:50;
A20: len gh = 3 by A10,A9,FINSEQ_2:47;
    reconsider hg = h * g as FinSequence of Seg 3 by A10,A9,FINSEQ_2:50;
A21: len hg = 3 by A10,A9,FINSEQ_2:47;
    reconsider hh = h * h as FinSequence of Seg 3 by A10,A9,FINSEQ_2:50;
A22: len hh = 3 by A10,A9,FINSEQ_2:47;
    reconsider gg = g * g as FinSequence of Seg 3 by A10,A9,FINSEQ_2:50;
A23: len gg = 3 by A10,A9,FINSEQ_2:47;
    reconsider ff = f * f as FinSequence of Seg 3 by A10,A9,FINSEQ_2:50;
A24: len ff = 3 by A10,A9,FINSEQ_2:47;
    thus <*2,1,3*> * <*1,3,2*> = <*2,3,1*>
    proof
A25:   gf.1 = g.(f.1) by A1,FUNCT_1:23
          .= g.1 by FINSEQ_1:62 .= 2 by FINSEQ_1:62;
A26:   gf.2 = g.(f.2) by A1,FUNCT_1:23
          .= g.3 by FINSEQ_1:62 .= 3 by FINSEQ_1:62;
      gf.3 = g.(f.3) by A1,FUNCT_1:23
          .= g.2 by FINSEQ_1:62 .= 1 by FINSEQ_1:62;
      hence thesis by A11,A25,A26,FINSEQ_1:62;
    end;
    thus <*1,3,2*> * <*2,1,3*> = <*3,1,2*>
    proof
A27:   fg.1 = f.(g.1) by A3,FUNCT_1:23
          .= f.2 by FINSEQ_1:62 .= 3 by FINSEQ_1:62;
A28:   fg.2 = f.(g.2) by A3,FUNCT_1:23
          .= f.1 by FINSEQ_1:62 .= 1 by FINSEQ_1:62;
      fg.3 = f.(g.3) by A3,FUNCT_1:23
          .= f.3 by FINSEQ_1:62 .= 2 by FINSEQ_1:62;
      hence thesis by A12,A27,A28,FINSEQ_1:62;
    end;
    thus <*2,1,3*> * <*3,2,1*> = <*3,1,2*>
    proof
A29:   gh.1 = g.(h.1) by A5,FUNCT_1:23
          .= g.3 by FINSEQ_1:62 .= 3 by FINSEQ_1:62;
A30:   gh.2 = g.(h.2) by A5,FUNCT_1:23
          .= g.2 by FINSEQ_1:62 .= 1 by FINSEQ_1:62;
      gh.3 = g.(h.3) by A5,FUNCT_1:23
          .= g.1 by FINSEQ_1:62 .= 2 by FINSEQ_1:62;
      hence thesis by A20,A29,A30,FINSEQ_1:62;
    end;
    thus <*3,2,1*> * <*2,1,3*> = <*2,3,1*>
    proof
A31:   hg.1 = h.(g.1) by A3,FUNCT_1:23
          .= h.2 by FINSEQ_1:62 .= 2 by FINSEQ_1:62;
A32:   hg.2 = h.(g.2) by A3,FUNCT_1:23
          .= h.1 by FINSEQ_1:62 .= 3 by FINSEQ_1:62;
      hg.3 = h.(g.3) by A3,FUNCT_1:23
          .= h.3 by FINSEQ_1:62 .= 1 by FINSEQ_1:62;
      hence thesis by A21,A31,A32,FINSEQ_1:62;
    end;
    thus <*3,2,1*> * <*3,2,1*> = <*1,2,3*>
    proof
A33:   hh.1 = h.(h.1) by A5,FUNCT_1:23
          .= h.3 by FINSEQ_1:62 .= 1 by FINSEQ_1:62;
A34:   hh.2 = h.(h.2) by A5,FUNCT_1:23
          .= h.2 by FINSEQ_1:62 .= 2 by FINSEQ_1:62;
      hh.3 = h.(h.3) by A5,FUNCT_1:23
          .= h.1 by FINSEQ_1:62 .= 3 by FINSEQ_1:62;
      hence thesis by A22,A33,A34,FINSEQ_1:62;
    end;
    thus <*2,1,3*> * <*2,1,3*> = <*1,2,3*>
    proof
A35:   gg.1 = g.(g.1) by A3,FUNCT_1:23
          .= g.2 by FINSEQ_1:62 .= 1 by FINSEQ_1:62;
A36:   gg.2 = g.(g.2) by A3,FUNCT_1:23
          .= g.1 by FINSEQ_1:62 .= 2 by FINSEQ_1:62;
      gg.3 = g.(g.3) by A3,FUNCT_1:23
          .= g.3 by FINSEQ_1:62 .= 3 by FINSEQ_1:62;
      hence thesis by A23,A35,A36,FINSEQ_1:62;
    end;
    thus <*1,3,2*> * <*1,3,2*> = <*1,2,3*>
    proof
A37:   ff.1 = f.(f.1) by A1,FUNCT_1:23
          .= f.1 by FINSEQ_1:62 .= 1 by FINSEQ_1:62;
A38:   ff.2 = f.(f.2) by A1,FUNCT_1:23
          .= f.3 by FINSEQ_1:62 .= 2 by FINSEQ_1:62;
      ff.3 = f.(f.3) by A1,FUNCT_1:23
          .= f.2 by FINSEQ_1:62 .= 3 by FINSEQ_1:62;
      hence thesis by A24,A37,A38,FINSEQ_1:62;
    end;
    thus <*1,3,2*> * <*2,3,1*> = <*3,2,1*>
    proof
A39:   fF.1 = f.(F.1) by A2,FUNCT_1:23
          .= f.2 by FINSEQ_1:62 .= 3 by FINSEQ_1:62;
A40:   fF.2 = f.(F.2) by A2,FUNCT_1:23
          .= f.3 by FINSEQ_1:62 .= 2 by FINSEQ_1:62;
      fF.3 = f.(F.3) by A2,FUNCT_1:23
          .= f.1 by FINSEQ_1:62 .= 1 by FINSEQ_1:62;
      hence thesis by A13,A39,A40,FINSEQ_1:62;
    end;
    thus <*2,3,1*> * <*2,3,1*> = <*3,1,2*>
    proof
A41:   FF.1 = F.(F.1) by A2,FUNCT_1:23
          .= F.2 by FINSEQ_1:62 .= 3 by FINSEQ_1:62;
A42:   FF.2 = F.(F.2) by A2,FUNCT_1:23
          .= F.3 by FINSEQ_1:62 .= 1 by FINSEQ_1:62;
      FF.3 = F.(F.3) by A2,FUNCT_1:23
          .= F.1 by FINSEQ_1:62 .= 2 by FINSEQ_1:62;
      hence thesis by A16,A41,A42,FINSEQ_1:62;
    end;
    thus <*2,3,1*> * <*3,1,2*> = <*1,2,3*>
    proof
A43:   FG.1 = F.(G.1) by A4,FUNCT_1:23
          .= F.3 by FINSEQ_1:62 .= 1 by FINSEQ_1:62;
A44:   FG.2 = F.(G.2) by A4,FUNCT_1:23
          .= F.1 by FINSEQ_1:62 .= 2 by FINSEQ_1:62;
      FG.3 = F.(G.3) by A4,FUNCT_1:23
          .= F.2 by FINSEQ_1:62 .= 3 by FINSEQ_1:62;
      hence thesis by A17,A43,A44,FINSEQ_1:62;
    end;
    thus <*3,1,2*> * <*2,3,1*> = <*1,2,3*>
    proof
A45:   GF.1 = G.(F.1) by A2,FUNCT_1:23
          .= G.2 by FINSEQ_1:62 .= 1 by FINSEQ_1:62;
A46:   GF.2 = G.(F.2) by A2,FUNCT_1:23
          .= G.3 by FINSEQ_1:62 .= 2 by FINSEQ_1:62;
      GF.3 = G.(F.3) by A2,FUNCT_1:23
          .= G.1 by FINSEQ_1:62 .= 3 by FINSEQ_1:62;
      hence thesis by A18,A45,A46,FINSEQ_1:62;
    end;
    thus <*3,1,2*> * <*3,1,2*> = <*2,3,1*>
    proof
A47:   GG.1 = G.(G.1) by A4,FUNCT_1:23
          .= G.3 by FINSEQ_1:62 .= 2 by FINSEQ_1:62;
A48:   GG.2 = G.(G.2) by A4,FUNCT_1:23
          .= G.1 by FINSEQ_1:62 .= 3 by FINSEQ_1:62;
      GG.3 = G.(G.3) by A4,FUNCT_1:23
          .= G.2 by FINSEQ_1:62 .= 1 by FINSEQ_1:62;
      hence thesis by A19,A47,A48,FINSEQ_1:62;
    end;
    thus <*1,3,2*> * <*3,2,1*> = <*2,3,1*>
    proof
A49:   fh.1 = f.(h.1) by A5,FUNCT_1:23
          .= f.3 by FINSEQ_1:62 .= 2 by FINSEQ_1:62;
A50:   fh.2 = f.(h.2) by A5,FUNCT_1:23
          .= f.2 by FINSEQ_1:62 .= 3 by FINSEQ_1:62;
      fh.3 = f.(h.3) by A5,FUNCT_1:23
          .= f.1 by FINSEQ_1:62 .= 1 by FINSEQ_1:62;
      hence thesis by A14,A49,A50,FINSEQ_1:62;
    end;
    thus <*3,2,1*> * <*1,3,2*> = <*3,1,2*>
    proof
A51:   hf.1 = h.(f.1) by A1,FUNCT_1:23
          .= h.1 by FINSEQ_1:62 .= 3 by FINSEQ_1:62;
A52:   hf.2 = h.(f.2) by A1,FUNCT_1:23
          .= h.3 by FINSEQ_1:62 .= 1 by FINSEQ_1:62;
      hf.3 = h.(f.3) by A1,FUNCT_1:23
          .= h.2 by FINSEQ_1:62 .= 2 by FINSEQ_1:62;
      hence thesis by A15,A51,A52,FINSEQ_1:62;
    end;
  end;

theorem Th38:
  for p being Permutation of Seg 3 st p is_transposition holds
    p = <*2,1,3*> or p = <*1,3,2*> or p = <*3,2,1*>
proof let p be Permutation of Seg 3;
    assume A1: p is_transposition;
A2: id Seg 3 = <*1,2,3*> by FINSEQ_2:62;
A3: p in Permutations 3 by MATRIX_2:def 11;
    p=<*1,2,3*> or p=<*2,1,3*> or p=<*2,3,1*>
      or p=<*3,1,2*> or p=<*3,2,1*> or p=<*1,3,2*>
        by A3,ENUMSET1:def 4,Th19;
    hence thesis by A1,A2,Th33,Th34,Th35;
end;

theorem Th39:
  for f, g being Element of Permutations n holds
    f * g in Permutations n
  proof
    let f, g be Element of Permutations n;
    reconsider F = f, G = g as Permutation of Seg n by MATRIX_2:def 11;
    F * G is Permutation of Seg n;
    hence thesis by MATRIX_2:def 11;
  end;

Lm7: <*1,2,3*> is even Permutation of Seg 3
  by MATRIX_2:29,FINSEQ_2:62;

Lm8: <*2,3,1*> is even Permutation of Seg 3
  proof
    reconsider f = <*2,3,1*> as Permutation of Seg 3
      by MATRIX_2:def 11,Th27;
    ex l be FinSequence of Group_of_Perm 3 st
    (len l) mod 2 = 0 & f = Product l &
    for i st i in dom l ex q being Element of Permutations 3 st
    l.i = q & q is_transposition
    proof
A1:   <*2,1,3*> in Permutations 3 & <*1,3,2*> in Permutations 3
        by Th27; then
      reconsider l1 = <*2,1,3*>, l2 = <*1,3,2*>
        as Element of Group_of_Perm 3 by MATRIX_2:def 13;
      reconsider l = <* l2, l1 *> as FinSequence of Group_of_Perm 3;
      take l;
A2:   len l = 2 * 1 by FINSEQ_1:61;
      hence (len l) mod 2 = 0 by NAT_D:13;
      reconsider L2 = l2, L1 = l1 as Element of Permutations 3 by A1;
      Product l = l2 * l1 by GROUP_4:13
               .= (the mult of Group_of_Perm 3).(l2,l1) by GROUP_1:def 1
               .= L1 * L2 by MATRIX_2:def 13
               .= <*2,3,1*> by Th37;
      hence f = Product l;
A3:   dom l = {1,2} by  A2,FINSEQ_1:def 3,4;
      for i st i in dom l ex q being Element of Permutations 3 st
      l.i = q & q is_transposition
      proof
        let i; assume i in dom l; then
A4:     i in {1,2} by A3;
        per cases by A4,TARSKI:def 2;
        suppose i = 2; then
A5:       l.i = <*2,1,3*> by FINSEQ_1:61; then
          reconsider q = l.i as Element of Permutations 3 by Th27;
          len Permutations 3 = 3 by MATRIX_2:20; then
          q is_transposition by A5,Th31;
          hence thesis;
        end;
        suppose i = 1; then
A6:       l.i = <*1,3,2*> by FINSEQ_1:61; then
          reconsider q = l.i as Element of Permutations 3 by Th27;
          len Permutations 3 = 3 by MATRIX_2:20; then
          q is_transposition by A6,Th30;
          hence thesis;
        end;
      end;
      hence thesis;
    end;
    hence thesis by MATRIX_2:def 15;
  end;

Lm9: <*3,1,2*> is even Permutation of Seg 3
  proof
    reconsider f = <*3,1,2*> as Permutation of Seg 3
      by MATRIX_2:def 11,Th27;
    ex l be FinSequence of Group_of_Perm 3 st
    (len l) mod 2 = 0 & f = Product l &
    for i st i in dom l ex q being Element of Permutations 3 st
    l.i = q & q is_transposition
    proof
A1:   <*2,1,3*> in Permutations 3 & <*1,3,2*> in Permutations 3
        by Th27; then
      <*2,1,3*> in the carrier of Group_of_Perm 3 &
        <*1,3,2*> in the carrier of Group_of_Perm 3 by MATRIX_2:def 13; then
      reconsider l1 = <*2,1,3*>, l2 = <*1,3,2*>
        as Element of Group_of_Perm 3;
      reconsider l = <* l1, l2 *> as FinSequence of Group_of_Perm 3;
      take l;
A2:   len l = 2 * 1 by FINSEQ_1:61;
      hence (len l) mod 2 = 0 by NAT_D:13;
      reconsider L2 = l2, L1 = l1 as Element of Permutations 3 by A1;
      Product l = l1 * l2 by GROUP_4:13
               .= (the mult of Group_of_Perm 3).(l1,l2) by GROUP_1:def 1
               .= L2 * L1 by MATRIX_2:def 13
               .= <*3,1,2*> by Th37;
      hence f = Product l;
A3:   dom l = {1,2} by  A2,FINSEQ_1:def 3,4;
      for i st i in dom l ex q being Element of Permutations 3 st
      l.i = q & q is_transposition
      proof
        let i; assume i in dom l; then
A4:     i in {1,2} by A3;
        per cases by A4,TARSKI:def 2;
        suppose i = 2; then
A5:       l.i = <*1,3,2*> by FINSEQ_1:61; then
          reconsider q = l.i as Element of Permutations 3 by Th27;
          len Permutations 3 = 3 by MATRIX_2:20; then
          q is_transposition by A5,Th30;
          hence thesis;
        end;
        suppose i = 1; then
A6:       l.i = <*2,1,3*> by FINSEQ_1:61; then
          reconsider q = l.i as Element of Permutations 3 by Th27;
          len Permutations 3 = 3 by MATRIX_2:20; then
          q is_transposition by A6,Th31;
          hence thesis;
        end;
      end;
      hence thesis;
    end;
    hence thesis by MATRIX_2:def 15;
  end;

theorem
  for l being FinSequence of Group_of_Perm n st
    (len l) mod 2 = 0 &
       (for i being Element of NAT st i in dom l
       ex q being Element of Permutations n st
       l.i = q & q is_transposition) holds
    Product l is even Permutation of Seg n
  proof
    let l be FinSequence of Group_of_Perm n;
    assume
A1: (len l) mod 2 = 0 &
       for i being Element of NAT st i in dom l
       ex q being Element of Permutations n st
       l.i = q & q is_transposition;
    Product l in the carrier of Group_of_Perm n; then
    Product l in Permutations n by MATRIX_2:def 13; then
    Product l is Permutation of Seg n by MATRIX_2:def 11; then
    reconsider Pf = Product l as Permutation of Seg n;
    ex l be FinSequence of the carrier of Group_of_Perm n st
      (len l) mod 2 = 0 & Pf = Product l &
    for i st i in dom l ex q being Element of Permutations n
    st l.i=q & q is_transposition
    proof
      consider l be FinSequence of the carrier of Group_of_Perm n such that
A2:   (len l) mod 2 = 0 & Pf = Product l and
A3:   for i be Element of NAT st i in dom l
      ex q being Element of Permutations n st l.i=q & q is_transposition by A1;
      take l;
      thus (len l) mod 2 = 0 & Pf = Product l by A2;
      let i;
      i in NAT by ORDINAL1:def 13;
      hence thesis by A3;
    end; then
    Pf is even by MATRIX_2:def 15;
    hence thesis;
  end;

Lm10: for p being Permutation of Seg 3 holds p * <*1,2,3*> = p
    proof
      let p be Permutation of Seg 3;
A1:   p is Element of Permutations 3 by MATRIX_2:def 11;
      idseq 3 = <*1,2,3*> by FINSEQ_2:62;
      hence thesis by A1,MATRIX_2:24;
    end;
Lm11: for p being Permutation of Seg 3 holds <*1,2,3*> * p = p
    proof
      let p be Permutation of Seg 3;
A1:   p is Element of Permutations 3 by MATRIX_2:def 11;
      idseq 3 = <*1,2,3*> by FINSEQ_2:62;
      hence thesis by A1,MATRIX_2:24;
    end;

theorem Th41:
  for l being FinSequence of Group_of_Perm 3 st
    (len l) mod 2 = 0 &
       (for i being Element of NAT st i in dom l
       ex q being Element of Permutations 3 st
       l.i = q & q is_transposition) holds
    Product l = <*1,2,3*> or Product l = <*2,3,1*> or Product l = <*3,1,2*>
  proof
    let l be FinSequence of Group_of_Perm 3;
    assume
A1: (len l) mod 2 = 0 & (for i being Element of NAT st i in dom l
    ex q being Element of Permutations 3 st l.i = q & q is_transposition);
    then consider t being Nat such that
A2: len l = 2 * t + 0 & 0 < 2 by NAT_D:def 2;
A3: len l = 2 * t by A2;
    defpred P[Nat] means
    for f being FinSequence of Group_of_Perm 3 st
    len f = 2 * $1 & (for i be Element of NAT st i in dom f holds
     ex q being Element of Permutations 3 st f.i=q & q is_transposition)
     holds
    Product f = <*1,2,3*> or Product f = <*2,3,1*> or Product f = <*3,1,2*>;
A4: P[0]
    proof
      let f be FinSequence of Group_of_Perm 3;
      assume len f = 2*0 &
      (for i be Element of NAT st i in dom f holds
      ex q being Element of Permutations 3 st
        f.i = q & q is_transposition); then
A5:   len f = 0;
      set G = Group_of_Perm 3;
A6:   f = <*>the carrier of G by A5,FINSEQ_1:32;
      1_G = <*1,2,3*> by  MATRIX_2:28,FINSEQ_2:62;
      hence thesis by A6,GROUP_4:11;
    end;
A7: for k being Nat st P[k] holds P[k+1]
    proof
      let k be Nat;
      assume P[k]; then
A8:   for f being FinSequence of Group_of_Perm 3 st
      len f = 2*k & (for i be Element of NAT st i in dom f holds
      (ex q being Element of Permutations 3 st f.i = q & q is_transposition))
      holds
      Product f = <*1,2,3*> or Product f = <*2,3,1*> or Product f = <*3,1,2*>;
      let f be FinSequence of Group_of_Perm 3;
      assume
A9:   len f = 2 * (k+1) & (for i be Element of NAT st i in dom f holds
      ex q being Element of Permutations 3 st f.i = q & q is_transposition);
      reconsider k as Element of NAT by ORDINAL1:def 13;
A10:   rng f c= the carrier of Group_of_Perm 3 by RELSET_1:12;
      rng (f|Seg (2*k)) c= rng f by RELAT_1:99; then
      rng (f|Seg (2*k)) c= the carrier of Group_of_Perm 3
        by A10,XBOOLE_1:1; then
      reconsider g = f|Seg (2*k) as FinSequence of Group_of_Perm 3
        by FINSEQ_1:def 4;
      2*k <= 2*k + 2 by NAT_1:11; then
A11:   Seg (2*k) c= Seg (2*(k+1)) by FINSEQ_1:7;
      set l = 2*k;
A12:   dom f = Seg (2*(k+1)) by A9,FINSEQ_1:def 3; then
      dom g = Seg (2*k) by A11,RELAT_1:91; then
      Seg len g = Seg (2*k) by FINSEQ_1:def 3; then
A13:   len g = 2*k by FINSEQ_1:8;
A14:   dom g c= dom f by RELAT_1:89;
      for i be Element of NAT st i in dom g holds
      ex q being Element of Permutations 3 st g.i = q & q is_transposition
      proof
        let i be Element of NAT; assume
A15:     i in dom g; then
        consider q being Element of Permutations 3 such that
A16:     f.i = q & q is_transposition by A14,A9;
        take q;
        g.i = q by A16,A15,FUNCT_1:70;
        hence thesis by A16;
      end; then
A17:   Product g = <*1,2,3*> or Product g = <*2,3,1*> or
        Product g = <*3,1,2*> by A8,A13;
A18:   len f = l + 1 + 1 by A9; then
A19:   f = f | Seg (l+1) ^ <*f.(l+1+1)*> by FINSEQ_3:61;
      set h = f | Seg (l+1);
      len h = l + 1 by A18,FINSEQ_3:59; then
A20:   h = h | Seg l ^ <*h.(l+1)*> by FINSEQ_3:61;
A21:   1 <= l+1 by NAT_1:11;
      l+1 <= l+2 by XREAL_1:8; then
A22:   l+1 in dom f by A12,A21;
A23:   l+1 <= l+2 by XREAL_1:8;
      1 <= l+2 & l+2 <= 2*(k+1) by A21,A23,XXREAL_0:2; then
A24:   l+2 in dom f by A12;
A25:   l <= l+1 by NAT_1:11;
      l+1 in Seg (l+1) by FINSEQ_1:6; then
A26:   h.(l+1) = f.(l+1) by FUNCT_1:72;
A27:   (f | Seg (l+1)) | Seg l = (f | (l+1)) | l
         .= f | l by A25,FINSEQ_5:80
         .= f | Seg l;
A28:   h.(l+1) = f/.(l+1) by A26,A22,FINSEQ_4:def 4;
      f.(l+1+1) = f/.(l+2) by A24,FINSEQ_4:def 4; then
      f = g ^ (<*f/.(l+1)*> ^ <*f/.(l+2)*>) by  A19,A20,A27,
 A28,FINSEQ_1:45; then
A29:   Product f = Product g * Product (<*f/.(l+1)*> ^ <*f/.(l+2)*>)
        by GROUP_4:8
        .= Product g * (Product(<*f/.(l+1)*>) * f/.(l+2)) by GROUP_4:9
        .= Product g * (f/.(l+1) * f/.(l+2)) by GROUP_4:12;
      consider q2 being Element of Permutations 3 such that
A30:   f.(l+1) = q2 & q2 is_transposition by A9,A22;
A31:   q2 = f/.(l+1) by A22,A30,FINSEQ_4:def 4;
A32:   len Permutations 3 = 3 by MATRIX_2:20;
      consider q1 being Element of Permutations 3 such that
A33:   f.(l+2) = q1 & q1 is_transposition by A24,A9;
A34:   q1 = f/.(l+2) by A24,A33,FINSEQ_4:def 4;
A35:   q1 = <*2,1,3*> or q1 = <*1,3,2*> or q1 = <*3,2,1*> by A32,A33,Th38;
A36:   q2 = <*2,1,3*> or q2 = <*1,3,2*> or q2 = <*3,2,1*> by A30,A32,Th38;
A37:   q1 * q2 in Permutations 3 by Th39; then
A38:   q1 * q2 is Permutation of Seg 3 by MATRIX_2:def 11;
      Product g in the carrier of Group_of_Perm 3; then
      Product g in Permutations 3 by MATRIX_2:def 13; then
A39:   Product g is Permutation of Seg 3 by MATRIX_2:def 11; then
      reconsider Pg = Product g as Element of Permutations 3
        by MATRIX_2:def 11;
A40:   Pg = <*1,2,3*> or Pg = <*2,3,1*> or Pg = <*3,1,2*> by A17;
A41:   q1 = <*2,1,3*> or q1 = <*1,3,2*> or q1 = <*3,2,1*> by A35;
      q2 = <*2,1,3*> or q2 = <*1,3,2*> or q2 = <*3,2,1*> by A36; then
A42:   q1 * q2 = <*2,3,1*> or q1 * q2 = <*1,2,3*> or q1 * q2 = <*3,1,2*>
        by A41,Th37;
A43:   f/.(l+1) * f/.(l+2) = q1 * q2 by A34,A31,Lm4;
      reconsider q12 = q1 * q2 as Element of Permutations 3 by A37;
      reconsider f12 = f/.(l+1) * f/.(l+2) as Element of Group_of_Perm 3;
      Product f = Product g * f12 by A29; then
A44:   Product f = q12 * Pg by Lm4,A43;
      Product f = <*1,2,3*> or Product f = <*2,3,1*> or
        Product f = <*3,1,2*>
      proof
        per cases by A40,A42;
        suppose Pg = <*1,2,3*> & q1 * q2 = <*2,3,1*>;
          hence thesis by A44,Lm10,A38;
        end;
        suppose Pg = <*2,3,1*> & q1 * q2 = <*2,3,1*>;
          hence thesis by Th37,A44;
        end;
        suppose Pg = <*2,3,1*> & q1 * q2 = <*3,1,2*>;
          hence thesis by Th37,A44;
        end;
        suppose q1 * q2 = <*1,2,3*>;
          hence thesis by A40,A44,A39,Lm11;
        end;
        suppose Pg = <*1,2,3*> & q1 * q2 = <*3,1,2*>;
          hence thesis by A44,Lm10,A38;
        end;
        suppose Pg = <*3,1,2*> & q1 * q2 = <*2,3,1*>;
          hence thesis by Th37,A44;
        end;
        suppose Pg = <*3,1,2*> & q1 * q2 = <*3,1,2*>;
          hence thesis by Th37,A44;
        end;
      end;
      hence thesis;
    end;
    for s being Nat holds P[s] from NAT_1:sch 2(A4,A7);
    hence thesis by A1,A3;
  end;

Lm12:
  for l being FinSequence of Group_of_Perm 3 st
    (len l) mod 2 = 0 &
       (for i being Nat st i in dom l
       ex q being Element of Permutations 3 st
       l.i = q & q is_transposition) holds
    Product l = <*1,2,3*> or Product l = <*2,3,1*> or Product l = <*3,1,2*>
proof
  let l being FinSequence of Group_of_Perm 3 such that
A1: (len l) mod 2 = 0 and
A2: for i st i in dom l
    ex q being Element of Permutations 3 st
    l.i = q & q is_transposition;
    for i be Element of NAT st i in dom l ex q being Element of Permutations 3
    st l.i = q & q is_transposition by A2;
    hence thesis by A1,Th41;
end;

registration
  cluster odd Permutation of Seg 3;
  existence
  proof
    reconsider f = <*3,2,1*> as Permutation of Seg 3 by MATRIX_2:def 11,Th27;
    not ex l be FinSequence of Group_of_Perm 3 st
    (len l) mod 2 = 0 & f = Product l &
    for i st i in dom l ex q being Element of Permutations 3 st
    l.i = q & q is_transposition by Lm12,Lm6; then
    f is odd by MATRIX_2:def 15;
    hence thesis;
  end;
end;

theorem Th42:
  <*3,2,1*> is odd Permutation of Seg 3
  proof
    <*3,2,1*> in Permutations 3 by Th27; then
    reconsider f = <*3,2,1*> as Permutation of Seg 3 by MATRIX_2:def 11;
    not ex l be FinSequence of Group_of_Perm 3 st
    (len l) mod 2 = 0 & f = Product l &
    for i st i in dom l ex q being Element of Permutations 3 st
    l.i = q & q is_transposition by Lm12,Lm6;
    hence thesis by MATRIX_2:def 15;
  end;

theorem Th43:
  <*2,1,3*> is odd Permutation of Seg 3
  proof
    <*2,1,3*> in Permutations 3 by Th27; then
    reconsider f = <*2,1,3*> as Permutation of Seg 3 by MATRIX_2:def 11;
    not ex l be FinSequence of Group_of_Perm 3 st
    (len l) mod 2 = 0 & f = Product l &
    for i st i in dom l ex q being Element of Permutations 3 st
    l.i = q & q is_transposition by Lm12,Lm6;
    hence thesis by MATRIX_2:def 15;
  end;

theorem Th44:
  <*1,3,2*> is odd Permutation of Seg 3
  proof
    reconsider f = <*1,3,2*> as Permutation of Seg 3
      by MATRIX_2:def 11,Th27;
    not ex l be FinSequence of Group_of_Perm 3 st
    (len l) mod 2 = 0 & f = Product l &
    for i st i in dom l ex q being Element of Permutations 3 st
    l.i = q & q is_transposition by Lm12,Lm6;
    hence thesis by MATRIX_2:def 15;
  end;

theorem
  for p being odd Permutation of Seg 3 holds
    p = <*3,2,1*> or p = <*1,3,2*> or p = <*2,1,3*>
  proof
    let p be odd Permutation of Seg 3;
A1: Permutations 3 =
      {<*1,2,3*>,<*3,2,1*>,<*1,3,2*>,<*2,3,1*>,<*2,1,3*>,<*3,1,2*>}
        by Th19;
    p in Permutations 3 by MATRIX_2:def 11;
    hence thesis by Lm7,Lm8,Lm9,A1,ENUMSET1:def 4;
  end;

begin :: Determinant and Permanent

theorem
  for a,b,c,d,e,f,g,h,i being Element of K
  for M being Matrix of 3,K st M = <*<*a,b,c*>,<*d,e,f*>,<*g,h,i*>*> holds
  Det M = a*e*i - c*e*g - a*f*h + b*f*g - b*d*i + c*d*h
  proof
    let a,b,c,d,e,f,g,h,i be Element of K;
    let M be Matrix of 3,K;
    assume A1: M = <*<*a,b,c*>,<*d,e,f*>,<*g,h,i*>*>;
    reconsider id3 = idseq 3 as Permutation of Seg 3;
    reconsider Id3 = idseq 3 as Element of Permutations 3 by Lm1;
    reconsider rid3 = Rev idseq 3 as Element of Permutations 3 by Th4;
A2: id3 = <*1,2,3*> & rid3 = <*3,2,1*> by FINSEQ_2:62,Th15;
    reconsider a3 = <*1,3,2*>, a4 = <*2,3,1*>, a5 = <*2,1,3*>,
               a6 = <*3,1,2*> as Element of Permutations 3 by Th27;
    set F = the add of K;
    set r = Path_product M;
A3: id Seg 3 is even by MATRIX_2:29;
A4: id3 is even by A3;
A5: 3 = len Permutations 3 by MATRIX_2:20; then
A6: Id3 is even by A4;
A7: r.id3 = -((the mult of K) $$ Path_matrix (Id3,M),Id3)
            by MATRIX_3:def 8
         .= (the mult of K) $$ Path_matrix (Id3,M) by MATRIX_2:def 16,A6
         .= (the mult of K) $$ <* a,e,i *> by Th20,A1,A2
         .= a*e*i by Th26;
A8: rid3 is odd by Th42,A5,A2;
A9: r.rid3 = -((the mult of K) $$ Path_matrix (rid3,M),rid3)
          by MATRIX_3:def 8
       .= -(the mult of K) $$ Path_matrix (rid3,M) by A8,MATRIX_2:def 16
       .= -(the mult of K) $$ <* c,e,g *> by Th21,A1,A2
       .= -c*e*g by Th26;
A10: a3 is odd by Th44,A5;
A11: r.a3 = -((the mult of K) $$ Path_matrix (a3,M),a3) by MATRIX_3:def 8
       .= -(the mult of K) $$ Path_matrix (a3,M) by A10,MATRIX_2:def 16
       .= -(the mult of K) $$ <* a,f,h *> by Th22,A1
       .= -a*f*h by Th26;
A12: a4 is even by Lm8,A5;
A13: r.a4 = -((the mult of K) $$ Path_matrix (a4,M),a4) by MATRIX_3:def 8
       .= (the mult of K) $$ Path_matrix (a4,M) by A12,MATRIX_2:def 16
       .= (the mult of K) $$ <* b,f,g *> by Th23,A1
       .= b*f*g by Th26;
A14: a5 is odd by Th43,A5;
A15: r.a5 = -((the mult of K) $$ Path_matrix (a5,M),a5) by MATRIX_3:def 8
       .= -(the mult of K) $$ Path_matrix (a5,M) by A14,MATRIX_2:def 16
       .= -(the mult of K) $$ <* b,d,i *> by Th24,A1
       .= -b*d*i by Th26;
A16: a6 is even by Lm9,A5;
A17: r.a6 = -((the mult of K) $$ Path_matrix (a6,M),a6) by MATRIX_3:def 8
       .= (the mult of K) $$ Path_matrix (a6,M) by A16,MATRIX_2:def 16
       .= (the mult of K) $$ <* c,d,h *> by Th25,A1
       .= c*d*h by Th26;
    Permutations 3 is finite by MATRIX_2:30;then
A18: FinOmega Permutations 3 = Permutations 3 by MATRIX_2:def 17;
    reconsider r1 = r.id3, r2 = r.rid3, r3 = r.a3, r4 = r.a4, r5 = r.a5,
      r6 = r.a6 as Element of K by A6,FUNCT_2:7;
A19: {Id3,rid3,a3,a4,a5,a6} = Permutations 3 by A2,Th19;
    reconsider X = {Id3,rid3,a3,a4,a5,a6}
      as Element of Fin Permutations 3 by A18,A19;
A20: FinOmega Permutations 3 = X by A19,MATRIX_2:def 17;
A21: X = {Id3,rid3,a3} \/ {a4,a5,a6} by ENUMSET1:53;
    reconsider B1 = {.Id3,rid3,a3.}, B2 = {.a4,a5,a6.}
      as Element of Fin Permutations 3;
A22: B1 <> {} & B2 <> {} & B1 misses B2
    proof
      B1 \ B2 = B1
      proof
        for x be set st x in B1 \ B2 holds
          x in B1 by XBOOLE_0:def 4; then
A23:     B1 \ B2 c= B1 by TARSKI:def 3;
        now
          let x;
          assume A24: x in B1;
          x=Id3 or x=rid3 or x=a3 by ENUMSET1:def 1,A24;then
          x in B1 & not x in B2 by ENUMSET1:def 1,Lm6,A2;
          hence x in B1 \ B2 by XBOOLE_0:def 4;
        end;then
        B1 c= B1 \ B2 by TARSKI:def 3;
        hence thesis by A23,XBOOLE_0:def 10;
      end;
      hence thesis by XBOOLE_1:83;
    end;
A25: F $$(B1,r) = r1 + r2 + r3 by A2,Lm6,SETWOP_2:5;
A26: F $$(B2,r) = r4 + r5 + r6 by Lm6,SETWOP_2:5;
    Det M = F $$ (FinOmega Permutations 3, r) by MATRIX_3:def 9
      .= F.(F $$ (B1,r),F $$ (B2,r)) by SETWOP_2:6,A21,A22,A20
      .= r1 + r2 + r3 + (r4 + (r5 + r6)) by  A25,A26,RLVECT_1:def 6
      .= r1 + r2 + r3 + r4 + (r5 + r6) by RLVECT_1:def 6
      .= a*e*i - c*e*g - a*f*h + b*f*g - b*d*i + c*d*h
        by A7,A9,A11,A13,A15,A17,RLVECT_1:def 6;
    hence thesis;
  end;

theorem
  for a,b,c,d,e,f,g,h,i being Element of K
  for M being Matrix of 3,K st M = <*<*a,b,c*>,<*d,e,f*>,<*g,h,i*>*> holds
    Per M = a*e*i + c*e*g + a*f*h + b*f*g + b*d*i + c*d*h
  proof
    let a,b,c,d,e,f,g,h,i be Element of K;
    let M be Matrix of 3,K;
    assume A1: M = <*<*a,b,c*>,<*d,e,f*>,<*g,h,i*>*>;
    reconsider id3 = idseq 3 as Permutation of Seg 3;
    reconsider Id3 = idseq 3 as Element of Permutations 3 by Lm1;
    reconsider rid3 = Rev idseq 3 as Element of Permutations 3 by Th4;
A2: id3 = <*1,2,3*> & rid3 = <*3,2,1*> by FINSEQ_2:62,Th15;
    reconsider a3 = <*1,3,2*>, a4 = <*2,3,1*>, a5 = <*2,1,3*>,
               a6 = <*3,1,2*> as Element of Permutations 3 by Th27;
    set F = the add of K;
    set r = PPath_product M;
A3: r.id3 = (the mult of K) $$ Path_matrix (Id3,M) by Def1
         .= (the mult of K) $$ <* a,e,i *> by Th20,A1,A2
         .= a*e*i by Th26;
A4: r.rid3 = (the mult of K) $$ Path_matrix (rid3,M) by Def1
       .= (the mult of K) $$ <* c,e,g *> by Th21,A1,A2
       .= c*e*g by Th26;
A5: r.a3 = (the mult of K) $$ Path_matrix (a3,M) by Def1
       .= (the mult of K) $$ <* a,f,h *> by Th22,A1
       .= a*f*h by Th26;
A6: r.a4 = (the mult of K) $$ Path_matrix (a4,M) by Def1
       .= (the mult of K) $$ <* b,f,g *> by Th23,A1
       .= b*f*g by Th26;
A7: r.a5 = (the mult of K) $$ Path_matrix (a5,M) by Def1
       .= (the mult of K) $$ <* b,d,i *> by Th24,A1
       .= b*d*i by Th26;
A8: r.a6 = (the mult of K) $$ Path_matrix (a6,M) by Def1
       .= (the mult of K) $$ <* c,d,h *> by Th25,A1
       .= c*d*h by Th26;
    Permutations 3 is finite by MATRIX_2:30;then
A9: FinOmega Permutations 3 = Permutations 3 by MATRIX_2:def 17;
    id3 in Permutations 3 by Lm1;then
    reconsider r1 = r.id3, r2 = r.rid3, r3 = r.a3, r4 = r.a4, r5 = r.a5,
      r6 = r.a6 as Element of K by FUNCT_2:7;
    reconsider X = {Id3,rid3,a3,a4,a5,a6} as Element of Fin Permutations 3
      by A9,A2,Th19;
A10: FinOmega Permutations 3 = X by A2,Th19,MATRIX_2:def 17;
A11: X = {Id3,rid3,a3} \/ {a4,a5,a6} by ENUMSET1:53;
    reconsider B1 = {.Id3,rid3,a3.}, B2 = {.a4,a5,a6.}
      as Element of Fin Permutations 3;
A12: B1 <> {} & B2 <> {} & B1 misses B2
    proof
      B1 \ B2 = B1
      proof
        for x be set st x in B1 \ B2 holds
          x in B1 by XBOOLE_0:def 4; then
A13:     B1 \ B2 c= B1 by TARSKI:def 3;
        now
          let x;
          assume A14: x in B1;
          x=Id3 or x=rid3 or x=a3 by ENUMSET1:def 1,A14;then
          x in B1 & not x in B2 by ENUMSET1:def 1,Lm6,A2;
          hence x in B1 \ B2 by XBOOLE_0:def 4;
        end;then
        B1 c= B1 \ B2 by TARSKI:def 3;
        hence thesis by A13,XBOOLE_0:def 10;
      end;
      hence thesis by XBOOLE_1:83;
    end;
A15: F $$(B1,r) = r1 + r2 + r3 by A2,Lm6,SETWOP_2:5;
A16: F $$(B2,r) = r4 + r5 + r6 by Lm6,SETWOP_2:5;
    Per M = F.(F $$ (B1,r),F $$ (B2,r)) by SETWOP_2:6,A11,A12,A10
      .= r1 + r2 + r3 + (r4 + (r5 + r6)) by  A15,A16,RLVECT_1:def 6
      .= r1 + r2 + r3 + r4 + (r5 + r6) by RLVECT_1:def 6
      .= a*e*i + c*e*g + a*f*h + b*f*g + b*d*i + c*d*h
        by A3,A4,A5,A6,A7,A8,RLVECT_1:def 6;
    hence thesis;
  end;

theorem Th48:
  for i,n being Element of NAT
  for p being Element of Permutations n st i in Seg n holds
   ex k being Element of NAT st k in Seg n & i = p.k
  proof
    let i,n be Element of NAT;
    let p be Element of Permutations n;
    assume
A1: i in Seg n;
A2: p is Permutation of Seg n by MATRIX_2:def 11; then
A3: dom p = Seg n by FUNCT_2:67; then
    reconsider s = p as FinSequence by FINSEQ_1:def 2;
    i in rng s by A1,FUNCT_2:def 3,A2; then
    consider k being Element of NAT such that
A4: k in dom s & i = s.k by FINSEQ_2:11;
    thus thesis by A4,A3;
  end;

theorem Th49:
  for M being Matrix of n, K st
  (ex i being Element of NAT st i in Seg n &
   for k being Element of NAT st k in Seg n holds Col(M,i).k = 0.K) holds
   for p being Element of Permutations n holds
  ex l being Element of NAT st l in Seg n & Path_matrix (p,M).l = 0.K
  proof
    let M be Matrix of n, K;
    assume
A1: ex i being Element of NAT st i in Seg n &
    for k being Element of NAT st k in Seg n holds Col(M,i).k = 0.K;
    consider i being Element of NAT such that
A2: i in Seg n &
    for k being Element of NAT st k in Seg n holds Col(M,i).k = 0.K by A1;
    let p be Element of Permutations n;
    len Path_matrix (p, M) = n by MATRIX_3:def 7;then
A3: dom Path_matrix (p, M) = Seg n by FINSEQ_1:def 3;
    n in NAT by ORDINAL1:def 13;
    then consider k being Element of NAT such that
A4: k in Seg n and
A5: i = p.k by Th48,A2;
A6: len M = n by MATRIX_1:def 3;
    1 <= k & k <= len M by A4,FINSEQ_1:3,A6; then
A7: k in dom M by FINSEQ_3:27;
    Path_matrix (p, M).k = M*(k,i) by A5,MATRIX_3:def 7,A4,A3; then
A8: Path_matrix (p, M).k = Col(M,i).k by A7,MATRIX_1:def 9;
    take k;
    thus thesis by A8,A2,A4;
  end;

theorem Th50:
  for p being Element of Permutations n
  for M being Matrix of n, K st
    (ex i being Element of NAT st i in Seg n &
    for k being Element of NAT st k in Seg n holds Col(M,i).k = 0.K)
  holds (Path_product M).p = 0.K
  proof
    let p be Element of Permutations n;
    let M be Matrix of n, K;
    assume
A1: ex i being Element of NAT st i in Seg n &
      for k being Element of NAT st k in Seg n holds Col(M,i).k = 0.K;
    consider l being Element of NAT such that
A2: l in Seg n &
    Path_matrix (p,M).l = 0.K by A1,Th49;
    set k = l;
A3: Path_matrix(p,M).k = 0.K by A2;
    len Path_matrix (p, M) = n by MATRIX_3:def 7;then
    k in dom (Path_matrix(p,M)) by A2,FINSEQ_1:def 3; then
A4: Product Path_matrix(p,M) = 0.K by A3,FVSUM_1:107;
A5: (Path_product M).p = -((the mult of K) $$ Path_matrix(p,M),p)
      by MATRIX_3:def 8
                      .= - (Product (Path_matrix(p,M)),p) by GROUP_4:def 3;
    per cases;
    suppose p is even; then
      -(Product (Path_matrix(p,M)),p) = 0.K by  MATRIX_2:def 16,A4;
      hence thesis by A5;
    end;
    suppose p is odd;
      then -(Product (Path_matrix(p,M)),p) = -Product (Path_matrix(p,M))
        by MATRIX_2:def 16 .= 0.K by A4,RLVECT_1:25;
      hence thesis by A5;
    end;
  end;

theorem Th51:
  for M being Matrix of n, K st
    (ex i being Element of NAT st i in Seg n &
    for k being Element of NAT st k in Seg n holds Col(M,i).k = 0.K) holds
      (the add of K) $$ (FinOmega Permutations n, Path_product M) = 0.K
  proof
    let M be Matrix of n, K;
    reconsider n1=n as Element of NAT by ORDINAL1:def 13;
    reconsider M1=M as Matrix of n1, K;
    given i being Element of NAT such that
A1: i in Seg n &
    for k being Element of NAT st k in Seg n holds Col(M,i).k = 0.K;
    set F = the add of K;
    set f = Path_product M1;
    set P1 = FinOmega Permutations n;
    consider c being Element of Permutations n;
    Permutations n is finite by MATRIX_2:30; then
    reconsider P1 as non empty Element of Fin Permutations n1
      by MATRIX_2:def 17;
    defpred P[non empty Element of Fin Permutations n1] means
      F $$ ($1, f) = 0.K;
A2: for x being Element of Permutations n1 st x in P1 holds P[{.x.}]
    proof
      let x be Element of Permutations n1;
      assume x in P1;
      F $$ ({.x.}, f) = f.x by SETWISEO:26
                   .= 0.K by Th50,A1;
      hence P[{.x.}];
    end;
A3: for x being Element of Permutations n1,
        B being non empty Element of Fin Permutations n1
      st x in P1 & B c= P1 & not x in B & P[B] holds P[B \/ {.x.}]
    proof
      let x be Element of Permutations n1,
          B be non empty Element of Fin Permutations n1;
      assume
A4:   x in P1 & B c= P1 & not x in B & P[B];
      F $$ (B \/ {.x.}, f) = F.(F $$(B,f), f.x) by A4,SETWOP_2:4
                       .= F $$(B,f) + 0.K by Th50,A1
                       .= 0.K by A4,RLVECT_1:10;
      hence P[B \/ {.x.}];
    end;
    P[P1] from NonEmptyFiniteX(A2,A3);
    hence thesis;
  end;

theorem Th52:
  for p being Element of Permutations n
  for M being Matrix of n, K st
  (ex i being Element of NAT st i in Seg n &
  (for k being Element of NAT st k in Seg n holds Col(M,i).k = 0.K)) holds
    (PPath_product M).p = 0.K
   proof
     let p be Element of Permutations n;
     let M be Matrix of n, K;
     assume ex i being Element of NAT st i in Seg n &
     (for k being Element of NAT st k in Seg n holds Col(M,i).k = 0.K); then
     consider l being Element of NAT such that
A1:  l in Seg n & Path_matrix (p,M).l = 0.K by Th49;
     len Path_matrix (p, M) = n by MATRIX_3:def 7;then
     l in dom Path_matrix(p,M) by A1,FINSEQ_1:def 3; then
A2:  Product Path_matrix(p,M) = 0.K by A1,FVSUM_1:107;
     (PPath_product M).p = (the mult of K) $$ Path_matrix(p,M) by Def1
      .= 0.K by A2,GROUP_4:def 3;
     hence thesis;
   end;

Lm13:
  for M being Matrix of n, K st
    (ex i being Element of NAT st i in Seg n &
      for k being Element of NAT st k in Seg n holds Col(M,i).k = 0.K) holds
      (the add of K) $$ (FinOmega Permutations n, PPath_product M) = 0.K
  proof
    let M be Matrix of n, K;
    reconsider n1=n as Element of NAT by ORDINAL1:def 13;
    reconsider M1=M as Matrix of n1, K;
    assume
A1: ex i being Element of NAT st i in Seg n &
    for k being Element of NAT st k in Seg n holds Col(M,i).k = 0.K;
    set F = the add of K;
    set f = PPath_product M1;
    set P1 = FinOmega Permutations n1;
    consider c being Element of Permutations n;
    Permutations n is finite by MATRIX_2:30; then
    reconsider P1 as non empty Element of Fin Permutations n1
      by MATRIX_2:def 17;
    defpred P[non empty Element of Fin Permutations n1] means
      F $$ ($1, f) = 0.K;
A2: for x being Element of Permutations n1 st x in P1 holds P[{.x.}]
    proof
      let x be Element of Permutations n1;
      assume x in P1;
      F $$ ({.x.}, f) = f.x by SETWISEO:26
                   .= 0.K by Th52,A1;
      hence P[{.x.}];
    end;
A3: for x being Element of Permutations n1,
        B being non empty Element of Fin Permutations n1
      st x in P1 & B c= P1 & not x in B & P[B] holds P[B \/ {.x.}]
    proof
      let x be Element of Permutations n1,
          B be non empty Element of Fin Permutations n1;
      assume
A4:   x in P1 & B c= P1 & not x in B & P[B];
      F $$ (B \/ {.x.}, f) = F.(F $$(B,f), f.x) by A4,SETWOP_2:4
                       .= F $$(B,f) + 0.K by Th52,A1
                       .= 0.K by A4,RLVECT_1:10;
      hence P[B \/ {.x.}];
    end;
    P[P1] from NonEmptyFiniteX(A2,A3);
    hence thesis;
  end;

theorem
  for M being Matrix of n, K st
  (ex i being Element of NAT st i in Seg n &
    for k being Element of NAT st k in Seg n holds Col(M,i).k = 0.K) holds
      Det M = 0.K
  proof
    let M be Matrix of n,K;
    assume
A1: ex i being Element of NAT st i in Seg n &
    for k being Element of NAT st k in Seg n holds Col(M,i).k = 0.K;
    set F = the add of K;
    set f = Path_product M;
    Permutations n is finite by MATRIX_2:30; then
A2: FinOmega Permutations n = Permutations n by MATRIX_2:def 17;
    reconsider P1 = Permutations n as Element of Fin Permutations n by A2;
    Det M = F $$ (FinOmega Permutations n,f) by MATRIX_3:def 9
         .= 0.K by A1,Th51;
    hence thesis;
  end;

theorem
  for M being Matrix of n, K st
  (ex i being Element of NAT st i in Seg n &
    for k being Element of NAT st k in Seg n holds Col(M,i).k = 0.K) holds
      Per M = 0.K by Lm13;

begin :: Auxiliary Lemmas for Further Work

theorem
  for M,N being Matrix of n, K st i in Seg n
  for p being Element of Permutations n holds
  ex k being Element of NAT st k in Seg n & i = p.k &
    Col(N,i)/.k = Path_matrix (p, N)/.k
    proof
    let M,N be Matrix of n, K;
    assume A1: i in Seg n;
    let p be Element of Permutations n;
    n in NAT by ORDINAL1:def 13;
    then consider k being Element of NAT such that
A2: k in Seg n and
A3: i = p.k by Th48,A1;
    len N = n by MATRIX_1:def 3; then
A4: k in dom N by A2,FINSEQ_1:def 3;
    take k;
    len Path_matrix (p, N) = n by MATRIX_3:def 7; then
A5: k in dom Path_matrix (p, N) by A2,FINSEQ_1:def 3; then
    Path_matrix (p, N).k = N*(k,i) by A3,MATRIX_3:def 7; then
A6: Path_matrix (p, N)/.k = N*(k,i) by A5,FINSEQ_4:def 4;
    len Col(N,i) = len N by MATRIX_1:def 9; then
A7: k in dom Col(N,i) by A4,FINSEQ_3:31;
    Col(N,i).k = N*(k,i) by A4,MATRIX_1:def 9;
    hence thesis by A2,A3,A6,A7,FINSEQ_4:def 4;
  end;

theorem
  for a being Element of K
  for M,N being Matrix of n, K st
  (ex i being Element of NAT st i in Seg n & (for k being Element of NAT st
    k in Seg n holds Col(M,i).k = a * Col(N,i)/.k) &
  (for l being Element of NAT st l <> i & l in Seg n
  holds Col(M,l) = Col(N,l)))
  for p being Element of Permutations n holds
  ex l being Element of NAT st l in Seg n &
    Path_matrix (p,M)/.l = a * (Path_matrix (p,N)/.l)
  proof
    let a be Element of K;
    let M,N be Matrix of n, K;
    assume
A1: (ex i being Element of NAT st i in Seg n & (for k being Element of NAT st
    k in Seg n holds Col(M,i).k = a * Col(N,i)/.k) &
      (for l being Element of NAT st l <> i & l in Seg n
      holds Col(M,l) = Col(N,l)));
    consider i being Element of NAT such that
A2: i in Seg n and
A3: (for k being Element of NAT st
    k in Seg n holds Col(M,i).k = a * Col(N,i)/.k) &
      (for l being Element of NAT st l <> i & l in Seg n
      holds Col(M,l) = Col(N,l))
        by A1;
    let p be Element of Permutations n;
    len Path_matrix (p, M) = n & len Path_matrix (p, N) = n
    by MATRIX_3:def 7;then
A4: dom Path_matrix (p, M) = Seg n & dom Path_matrix (p, N) = Seg n
      by FINSEQ_1:def 3;
    n in NAT by ORDINAL1:def 13;
    then consider k being Element of NAT such that
A5: k in Seg n and
A6: i = p.k by Th48,A2;
A7: len M = n & len N = n by MATRIX_1:def 3;
    1 <= k & k <= len M & 1 <= k & k <= len N by A5,FINSEQ_1:3,A7; then
A8: k in dom M & k in dom N by FINSEQ_3:27; then
A9: Col(M,i).k = M*(k,i) & Col(N,i).k = N*(k,i) by MATRIX_1:def 9;
A10: Col(N,i)/.k = Path_matrix (p, N)/.k
    proof
      len Path_matrix (p, N) = n by MATRIX_3:def 7; then
A11:   k in dom Path_matrix (p, N) by A5,FINSEQ_1:def 3; then
      Path_matrix (p, N).k = N*(k,i) by A6,MATRIX_3:def 7; then
A12:   Path_matrix (p, N)/.k = N*(k,i) by A11,FINSEQ_4:def 4;
      len Col(N,i) = len N by MATRIX_1:def 9; then
      dom Col(N,i) = dom N by FINSEQ_3:31; then
      N*(k,i) = Col(N,i)/.k by A8,A9,FINSEQ_4:def 4;
      hence thesis by A12;
    end;
    k in dom Path_matrix (p, M) & k in dom Path_matrix (p, N) by A5,A4; then
    Path_matrix (p, M).k = M*(k,i) & Path_matrix (p, N).k = N*(k,i)
      by A6,MATRIX_3:def 7; then
A13: Path_matrix (p, M).k = Col(M,i).k by A8,MATRIX_1:def 9
      .= a * (Path_matrix (p, N)/.k) by A10,A5,A3;
    take k;
    thus thesis by A5,A4,FINSEQ_4:def 4,A13;
  end;
