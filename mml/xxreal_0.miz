:: Introduction to Arithmetic of Extended Real Numbers
::  by Library Committee
::
:: Received January 4, 2006
:: Copyright (c) 2006 Association of Mizar Users
 
environ

 vocabularies NUMBERS, ORDINAL1, CARD_1, SUBSET_1, XBOOLE_0, ARYTM_2, ARYTM_3,
      ZFMISC_1, XCMPLX_0, XXREAL_0;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, ORDINAL1, ARYTM_2, NUMBERS;
 constructors ARYTM_2, NUMBERS;
 registrations NUMBERS, ORDINAL1, XBOOLE_0;
 requirements BOOLE, SUBSET;
 definitions TARSKI, NUMBERS, XBOOLE_0;
 theorems XBOOLE_0, TARSKI, ORDINAL1, ARYTM_0, ZFMISC_1, ARYTM_1, ENUMSET1;

begin

reserve x for set;

definition
  let x;
  attr x is ext-real means
  :Def1:
  x in ExtREAL;
end;

registration
  cluster ext-real number;
  existence
  proof
    take 0;
    thus 0 in ExtREAL by XBOOLE_0:def 3;
  end;
  cluster -> ext-real Element of ExtREAL;
  coherence by Def1;
end;

definition
  func +infty equals
  REAL;
  coherence;
  func -infty equals
  [0,REAL];
  coherence;
end;

definition
  redefine func ExtREAL equals
  REAL \/ {+infty,-infty};
  compatibility;
end;

registration
  cluster +infty -> ext-real;
  coherence
  proof
    +infty in {REAL,[0,REAL]} by TARSKI:def 2;
    then +infty in ExtREAL by XBOOLE_0:def 3;
    hence thesis;
  end;
  cluster -infty -> ext-real;
  coherence
  proof
    -infty in {REAL,[0,REAL]} by TARSKI:def 2;
    then -infty in ExtREAL by XBOOLE_0:def 3;
    hence thesis;
  end;
end;

definition
  let x,y be ext-real number;
  pred x <= y means
  :Def5:
  ex x9,y9 being Element of REAL+ st x = x9 & y = y9
& x9 <=' y9 if x in REAL+ & y in REAL+, ex x9,y9 being Element of REAL+ st x =
  [0,x9] & y = [0,y9] & y9 <=' x9 if x in [:{0},REAL+:] & y in [:{0},REAL+:]
  otherwise y in REAL+ & x in [:{0},REAL+:] or x = -infty or y = +infty;
  consistency by ARYTM_0:5,XBOOLE_0:3;
  reflexivity
  proof
    let x be ext-real number such that
A1: not((x in REAL+ & x in REAL+ implies ex x9,y9 being Element of
REAL+ st x = x9 & x = y9 & x9 <=' y9) & (x in [:{0},REAL+:] & x in [:{0},REAL+
:] implies ex x9,y9 being Element of REAL+ st x = [0,x9] & x = [0,y9] & y9 <='
x9) & (not(x in REAL+ & x in REAL+) & not(x in [:{0},REAL+:] & x in [:{0},REAL+
    :]) implies x in REAL+ & x in [:{0},REAL+:] or x = -infty or x = +infty));
    x in ExtREAL by Def1;
    then
A2: x in REAL+ \/ [:{0},REAL+:] \ {[0,0]} or x in {+infty,-infty} by
    XBOOLE_0:def 3;
    per cases by A1;
    suppose that
A3:   x in REAL+ and
A4:   not ex x9,y9 being Element of REAL+ st x = x9 & x = y9 & x9 <=' y9;
      reconsider x9 = x as Element of REAL+ by A3;
      not x9 <=' x9 by A4;
      hence thesis;
    end;
    suppose that
A5:   x in [:{0},REAL+:] and
A6:   not ex x9,y9 being Element of REAL+ st x = [0,x9] & x = [0,y9] &
      y9 <=' x9;
      consider z,x9 being set such that
A7:   z in {0} and
A8:   x9 in REAL+ and
A9:   x = [z,x9] by A5,ZFMISC_1:103;
      reconsider x9 as Element of REAL+ by A8;
      x = [0,x9] by A7,A9,TARSKI:def 1;
      then not x9 <=' x9 by A6;
      hence thesis;
    end;
    suppose
      not(not x in REAL+ & not x in [:{0},REAL+:] implies x in REAL+
      & x in [:{0},REAL+:] or x = -infty or x = +infty);
      hence thesis by A2,TARSKI:def 2,XBOOLE_0:def 3;
    end;
  end;
  connectedness
  proof
    let x,y be ext-real number such that
A10: not((x in REAL+ & y in REAL+ implies ex x9,y9 being Element of
REAL+ st x = x9 & y = y9 & x9 <=' y9) & (x in [:{0},REAL+:] & y in [:{0},REAL+
:] implies ex x9,y9 being Element of REAL+ st x = [0,x9] & y = [0,y9] & y9 <='
x9) & (not(x in REAL+ & y in REAL+) & not(x in [:{0},REAL+:] & y in [:{0},REAL+
    :]) implies y in REAL+ & x in [:{0},REAL+:] or x = -infty or y = +infty));
    x in ExtREAL by Def1;
    then
A11: x in REAL+ \/ [:{0},REAL+:] \ {[0,0]} or x in {+infty,-infty} by
    XBOOLE_0:def 3;
    y in ExtREAL by Def1;
    then
A12: y in REAL+ \/ [:{0},REAL+:] \ {[0,0]} or y in {+infty,-infty} by
    XBOOLE_0:def 3;
    per cases by A10;
    suppose that
A13:  x in REAL+ & y in REAL+ and
A14:  not ex x9,y9 being Element of REAL+ st x = x9 & y = y9 & x9 <=' y9;
      hereby
        assume y in REAL+ & x in REAL+;
        then reconsider x9 = x, y9 = y as Element of REAL+;
        take y9,x9;
        thus y = y9 & x = x9;
        thus y9 <=' x9 by A14;
      end;
      thus thesis by A13,ARYTM_0:5,XBOOLE_0:3;
    end;
    suppose that
A15:  x in [:{0},REAL+:] & y in [:{0},REAL+:] and
A16:  not ex x9,y9 being Element of REAL+ st x = [0,x9] & y = [0,y9]
      & y9 <=' x9;
      now
        assume y in [:{0},REAL+:];
        then consider z,y9 being set such that
A17:    z in {0} and
A18:    y9 in REAL+ and
A19:    y = [z,y9] by ZFMISC_1:103;
A20:    z = 0 by A17,TARSKI:def 1;
        assume x in [:{0},REAL+:];
        then consider z,x9 being set such that
A21:    z in {0} and
A22:    x9 in REAL+ and
A23:    x = [z,x9] by ZFMISC_1:103;
        reconsider x9,y9 as Element of REAL+ by A18,A22;
        take y9,x9;
        thus y = [0,y9] & x = [0,x9] by A17,A19,A21,A23,TARSKI:def 1;
        z = 0 by A21,TARSKI:def 1;
        hence x9 <=' y9 by A16,A19,A20,A23;
      end;
      hence thesis by A15,ARYTM_0:5,XBOOLE_0:3;
    end;
    suppose
      not(not(x in REAL+ & y in REAL+) & not(x in [:{0},REAL+:] & y
in [:{0},REAL+:]) implies y in REAL+ & x in [:{0},REAL+:] or x = -infty or y =
      +infty);
      hence thesis by A11,A12,TARSKI:def 2,XBOOLE_0:def 3;
    end;
  end;
end;

reserve a,b,c,d for ext-real number;

notation
  let a,b;
  synonym b >= a for a <= b;
  antonym b < a for a <= b;
  antonym a > b for a <= b;
end;

Lm1: +infty <> [0,0]
proof
  assume +infty = [0,0];
  then +infty = {{0},{0}} by ENUMSET1:69
    .= {{0}} by ENUMSET1:69;
  hence contradiction by TARSKI:def 1;
end;

Lm2: not +infty in REAL+ by ARYTM_0:1,ORDINAL1:7;

Lm3: not -infty in REAL+
proof
A1: {0,REAL} in -infty & REAL in {0,REAL} by TARSKI:def 2;
  assume -infty in REAL+;
  hence contradiction by A1,ARYTM_0:1,ORDINAL1:3;
end;

Lm4: not +infty in [:{0},REAL+:]
proof
  assume +infty in [:{0},REAL+:];
  then +infty in REAL+ \/ [:{0},REAL+:] by XBOOLE_0:def 3;
  then +infty in REAL by Lm1,ZFMISC_1:64;
  hence contradiction;
end;

Lm5: not -infty in [:{0},REAL+:]
proof
  assume -infty in [:{0},REAL+:];
  then REAL in REAL+ by ZFMISC_1:106;
  hence contradiction by ARYTM_0:1,ORDINAL1:7;
end;

Lm6: -infty < +infty
proof
  -infty <> +infty by TARSKI:def 2;
  hence thesis by Def5,Lm3,Lm5;
end;

theorem Th1:
  a <= b & b <= a implies a = b
proof
  assume that
A1: a <= b and
A2: b <= a;
  per cases;
  suppose
    a in REAL+ & b in REAL+;
    then
    (ex a9,b9 being Element of REAL+ st a = a9 & b = b9 & a9 <=' b9 )& ex
b99, a99 being Element of REAL+ st b = b99 & a = a99 & b99 <=' a99 by A1,A2,
    Def5;
    hence thesis by ARYTM_1:4;
  end;
  suppose
A3: a in REAL+ & b in [:{0},REAL+:];
    then ( not b in REAL+)& not a in [:{0},REAL+:] by
    ARYTM_0:5,XBOOLE_0:3;
    hence thesis by A1,A3,Def5,Lm3,Lm4;
  end;
  suppose
A4: b in REAL+ & a in [:{0},REAL+:];
    then ( not a in REAL+)& not b in [:{0},REAL+:] by
    ARYTM_0:5,XBOOLE_0:3;
    hence thesis by A2,A4,Def5,Lm3,Lm4;
  end;
  suppose that
A5: a in [:{0},REAL+:] & b in [:{0},REAL+:];
    consider a9,b9 being Element of REAL+ such that
A6: a = [0,a9] & b = [0,b9] and
A7: b9 <=' a9 by A1,A5,Def5;
    consider b99,a99 being Element of REAL+ such that
A8: b = [0,b99] & a = [0,a99] and
A9: a99 <=' b99 by A2,A5,Def5;
    a9 = a99 & b9 = b99 by A6,A8,ZFMISC_1:33;
    hence thesis by A7,A8,A9,ARYTM_1:4;
  end;
  suppose
    (a = -infty or a = +infty) & (b = -infty or b = +infty);
    hence thesis by A1,A2,Lm6;
  end;
  suppose that
A10: ( not(a in REAL+ & b in REAL+))& not(a in [:{0},REAL+:] & b in [:
    {0},REAL+:]) and
A11: not(b in REAL+ & a in [:{0},REAL+:]) and
A12: not(a in REAL+ & b in [:{0},REAL+:]);
    a = -infty or b = +infty by A1,A10,A11,Def5;
    hence thesis by A2,A10,A12,Def5,Lm6;
  end;
end;

Lm7: -infty >= a implies a = -infty
proof
  a >= -infty by Def5,Lm3,Lm5;
  hence thesis by Th1;
end;

Lm8: +infty <= a implies a = +infty
proof
  a <= +infty by Def5,Lm2,Lm4;
  hence thesis by Th1;
end;

theorem Th2:
  a <= b & b <= c implies a <= c
proof
  assume that
A1: a <= b and
A2: b <= c;
  per cases;
  suppose that
A3: a in REAL+ and
A4: b in REAL+ and
A5: c in REAL+;
    consider b99,c9 being Element of REAL+ such that
A6: b = b99 and
A7: c = c9 and
A8: b99 <=' c9 by A2,A4,A5,Def5;
    consider a9,b9 being Element of REAL+ such that
A9: a = a9 and
A10: b = b9 & a9 <=' b9 by A1,A3,A4,Def5;
    a9 <=' c9 by A10,A6,A8,ARYTM_1:3;
    hence thesis by A5,A9,A7,Def5;
  end;
  suppose
A11: a in REAL+ & b in [:{0},REAL+:];
    then
    ( not(a in REAL+ & b in REAL+))& not(a in [:{0},REAL+:] & b in [:{0},
    REAL+:]) by ARYTM_0:5,XBOOLE_0:3;
    hence thesis by A1,A11,Def5,Lm3,Lm4;
  end;
  suppose
A12: b in REAL+ & c in [:{0},REAL+:];
    then
    ( not(c in REAL+ & b in REAL+))& not(c in [:{0},REAL+:] & b in [:{0},
    REAL+:]) by ARYTM_0:5,XBOOLE_0:3;
    hence thesis by A2,A12,Def5,Lm3,Lm4;
  end;
  suppose that
A13: a in [:{0},REAL+:] & c in REAL+;
    ( not(a in REAL+ & c in REAL+))& not(a in [:{0},REAL+:] & c in [:{0},
    REAL+:]) by A13,ARYTM_0:5,XBOOLE_0:3;
    hence thesis by A13,Def5;
  end;
  suppose that
A14: a in [:{0},REAL+:] and
A15: b in [:{0},REAL+:] and
A16: c in [:{0},REAL+:];
    consider b99,c9 being Element of REAL+ such that
A17: b = [0,b99] and
A18: c = [0,c9] and
A19: c9 <=' b99 by A2,A15,A16,Def5;
    consider a9,b9 being Element of REAL+ such that
A20: a = [0,a9] and
A21: b = [0,b9] and
A22: b9 <=' a9 by A1,A14,A15,Def5;
    b9 = b99 by A21,A17,ZFMISC_1:33;
    then c9 <=' a9 by A22,A19,ARYTM_1:3;
    hence thesis by A14,A16,A20,A18,Def5;
  end;
  suppose that
A23: not(a in REAL+ & b in REAL+ & c in REAL+) and
A24: not(a in REAL+ & b in [:{0},REAL+:]) and
A25: not(b in REAL+ & c in [:{0},REAL+:]) and
A26: not(a in [:{0},REAL+:] & c in REAL+) and
A27: not(a in [:{0},REAL+:] & b in [:{0},REAL+:] & c in [:{0},REAL+:]);
A28: b = +infty implies c = +infty by A2,Lm8;
A29: b = -infty implies a = -infty by A1,Lm7;
    a = -infty or b = +infty or b = -infty or c = +infty by A1,A2,A23,A25
    ,A26,A27,Def5;
    hence thesis by A1,A2,A23,A24,A25,A27,A28,A29,Def5;
  end;
end;

theorem
  a <= +infty by Def5,Lm2,Lm4;

theorem
  +infty <= a implies a = +infty by Lm8;

theorem
  a >= -infty by Def5,Lm3,Lm5;

theorem
  -infty >= a implies a = -infty by Lm7;

theorem
  -infty < +infty by Lm6;

theorem
  not +infty in REAL;

Lm9: a in REAL or a = +infty or a = -infty
proof
  a in ExtREAL by Def1;
  then a in REAL or a in { +infty, -infty } by XBOOLE_0:def 3;
  hence thesis by TARSKI:def 2;
end;

theorem Th9:
  a in REAL implies +infty > a
proof
  assume a in REAL;
  then
A1: a <> +infty;
  +infty >= a by Def5,Lm2,Lm4;
  hence thesis by A1,Th1;
end;

theorem Th10:
  a in REAL & b >= a implies b in REAL or b = +infty
proof
  assume that
A1: a in REAL and
A2: b >= a;
  assume
A3: not b in REAL;
  b = -infty implies a = -infty by A2,Lm7;
  hence thesis by A1,A3,Lm9;
end;

theorem Th11:
  not -infty in REAL
proof
A1: {0,REAL} in {{0,REAL},{0}} & REAL in {0,REAL} by TARSKI:def 2;
  assume -infty in REAL;
  hence contradiction by A1,ORDINAL1:3;
end;

theorem Th12:
  a in REAL implies -infty < a
proof
  -infty <= a by Def5,Lm3,Lm5;
  hence thesis by Th1,Th11;
end;

theorem Th13:
  a in REAL & b <= a implies b in REAL or b = -infty
proof
  assume that
A1: a in REAL and
A2: b <= a;
  assume
A3: not b in REAL;
  b = +infty implies a = +infty by A2,Lm8;
  hence thesis by A1,A3,Lm9;
end;

theorem
  a in REAL or a = +infty or a = -infty by Lm9;

begin :: positive & negative

registration
  cluster natural -> ext-real number;
  coherence
  proof
    let a be number;
    assume a is natural;
    then a in NAT by ORDINAL1:def 13;
    hence a in ExtREAL by XBOOLE_0:def 3;
  end;
end;

notation
  let a be number;
  synonym a is zero for a is empty;
end;

definition
  let a be ext-real number;
  attr a is positive means
  a > 0;
  attr a is negative means
  a < 0;
  redefine attr a is zero means
  a = 0;
  compatibility;
end;

registration
  cluster positive -> non negative non zero (ext-real number);
  coherence
  proof
    let r be ext-real number;
    assume r > 0;
    hence r >= 0 & r <> 0;
  end;
  cluster non negative non zero -> positive (ext-real number);
  coherence
  proof
    let r be ext-real number;
    assume r >= 0 & r <> 0;
    hence r > 0 by Th1;
  end;
  cluster negative -> non positive non zero (ext-real number);
  coherence
  proof
    let r be ext-real number;
    assume r < 0;
    hence r <= 0 & r <> {};
  end;
  cluster non positive non zero -> negative (ext-real number);
  coherence;
  cluster zero -> non negative non positive (ext-real number);
  coherence;
  cluster non negative non positive -> zero (ext-real number);
  coherence;
end;

registration
  cluster +infty -> positive;
  coherence
  proof
    thus 0 < +infty by Th9;
  end;
  cluster -infty -> negative;
  coherence
  proof
    thus 0 > -infty by Th12;
  end;
end;

registration
  cluster positive (ext-real number);
  existence
  proof
    take +infty;
    thus thesis;
  end;
  cluster negative (ext-real number);
  existence
  proof
    take -infty;
    thus thesis;
  end;
  cluster zero (ext-real number);
  existence
  proof
    reconsider z = 0 as ext-real number;
    take z;
    thus z = 0;
  end;
end;

begin :: min & max

definition
  let a,b;
  func min(a,b) equals
  :Def9:
  a if a <= b otherwise b;
  correctness;
  commutativity by Th1;
  idempotence;
  func max(a,b) equals
  :Def10:
  a if b <= a otherwise b;
  correctness;
  commutativity by Th1;
  idempotence;
end;

theorem
  min(a,b) = a or min(a,b) = b by Def9;

theorem
  max(a,b) = a or max(a,b) = b by Def10;

registration
  let a,b;
  cluster min(a,b) -> ext-real;
  coherence by Def9;
  cluster max(a,b) -> ext-real;
  coherence by Def10;
end;

theorem Th17:
  min(a,b) <= a
proof
  a <= b or not a <= b;
  hence thesis by Def9;
end;

theorem Th18:
  a <= b & c <= d implies min(a,c) <= min(b,d)
proof
  assume that
A1: a <= b and
A2: c <= d;
  min(a,c) <= c by Th17;
  then
A3: min(a,c) <= d by A2,Th2;
  min(a,c) <= a by Th17;
  then min(a,c) <= b by A1,Th2;
  hence thesis by A3,Def9;
end;

theorem
  a < b & c < d implies min(a,c) < min(b,d)
proof
  assume that
A1: a < b and
A2: c < d;
  min(a,c) <= c by Th17;
  then
A3: min(a,c) < d by A2,Th2;
  min(a,c) <= a by Th17;
  then min(a,c) < b by A1,Th2;
  hence thesis by A3,Def9;
end;

theorem
  a <= b & a <= c implies a <= min(b,c) by Def9;

theorem
  a < b & a < c implies a < min(b,c) by Def9;

theorem
  a <= min(b,c) implies a <= b
proof
  min(b,c) <= b by Th17;
  hence thesis by Th2;
end;

theorem
  a < min(b,c) implies a < b
proof
  min(b,c) <= b by Th17;
  hence thesis by Th2;
end;

theorem
  c <= a & c <= b & (for d st d <= a & d <= b holds d <= c) implies c =
  min(a,b)
proof
  assume that
A1: c <= a & c <= b and
A2: for d st d <= a & d <= b holds d <= c;
  min(a,b) <= a & min(a,b) <= b by Th17;
  then
A3: min(a,b) <= c by A2;
  c <= min(a,b) by A1,Def9;
  hence thesis by A3,Th1;
end;

theorem Th25:
  a <= max(a,b)
proof
  a <= b or not a <= b;
  hence thesis by Def10;
end;

theorem Th26:
  a <= b & c <= d implies max(a,c) <= max(b,d)
proof
  assume that
A1: a <= b and
A2: c <= d;
  d <= max(b,d) by Th25;
  then
A3: c <= max(b,d) by A2,Th2;
  b <= max(b,d) by Th25;
  then a <= max(b,d) by A1,Th2;
  hence thesis by A3,Def10;
end;

theorem
  a < b & c < d implies max(a,c) < max(b,d)
proof
  assume that
A1: a < b and
A2: c < d;
  d <= max(b,d) by Th25;
  then
A3: c < max(b,d) by A2,Th2;
  b <= max(b,d) by Th25;
  then a < max(b,d) by A1,Th2;
  hence thesis by A3,Def10;
end;

theorem
  b <= a & c <= a implies max(b,c) <= a by Def10;

theorem
  b < a & c < a implies max(b,c) < a by Def10;

theorem
  max(b,c) <= a implies b <= a
proof
  b <= max(b,c) by Th25;
  hence thesis by Th2;
end;

theorem
  max(b,c) < a implies b < a
proof
  b <= max(b,c) by Th25;
  hence thesis by Th2;
end;

theorem
  a <= c & b <= c & (for d st a <= d & b <= d holds c <= d) implies c =
  max(a,b)
proof
  assume that
A1: a <= c & b <= c and
A2: for d st a <= d & b <= d holds c <= d;
  a <= max(a,b) & b <= max(a,b) by Th25;
  then
A3: c <= max(a,b) by A2;
  max(a,b) <= c by A1,Def10;
  hence thesis by A3,Th1;
end;

theorem
  min(min(a,b),c) = min(a,min(b,c))
proof
  per cases by Th2;
  suppose
    a <= b & a <= c;
    then min(a,b) = a & min(a,c) = a by Def9;
    hence thesis by Def9;
  end;
  suppose
    b <= a & b <= c;
    then min(a,b) = b & min(b,c) = b by Def9;
    hence thesis;
  end;
  suppose
    c <= b & c <= a;
    then min(b,c) = c & min(a,c) = c by Def9;
    hence thesis by Def9;
  end;
end;

theorem
  max(max(a,b),c) = max(a,max(b,c))
proof
  per cases by Th2;
  suppose
A1: a <= b & a <= c;
A2: max(b,c) = b or max(b,c) = c by Def10;
    max(a,b) = b by A1,Def10;
    hence thesis by A1,A2,Def10;
  end;
  suppose
A3: b <= a & b <= c;
    then max(a,b) = a by Def10;
    hence thesis by A3,Def10;
  end;
  suppose
A4: c <= b & c <= a;
A5: max(a,b) = b or max(a,b) = a by Def10;
    max(b,c) = b by A4,Def10;
    hence thesis by A4,A5,Def10;
  end;
end;

theorem
  min(max(a,b),b) = b
proof
  b <= max(a,b) by Th25;
  hence thesis by Def9;
end;

theorem
  max(min(a,b),b) = b
proof
  min(a,b) <= b by Th17;
  hence thesis by Def10;
end;

theorem Th37:
  a <= c implies max(a,min(b,c)) = min(max(a,b),c)
proof
  assume
A1: a <= c;
  per cases;
  suppose
A2: a <= b;
    then a <= min(b,c) by A1,Def9;
    hence max(a,min(b,c)) = min(b,c) by Def10
      .= min(max(a,b),c) by A2,Def10;
  end;
  suppose
A3: b <= a;
    then b <= c by A1,Th2;
    hence max(a,min(b,c)) = max(a,b) by Def9
      .= a by A3,Def10
      .= min(a,c) by A1,Def9
      .= min(max(a,b),c) by A3,Def10;
  end;
end;

theorem
  min(a,max(b,c)) = max(min(a,b),min(a,c))
proof
  per cases;
  suppose
A1: b <= c;
    then
A2: min(a,b) <= min(a,c) by Th18;
    thus min(a,max(b,c)) = min(a,c) by A1,Def10
      .= max(min(a,b),min(a,c)) by A2,Def10;
  end;
  suppose
A3: c <= b;
    then
A4: min(a,c) <= min(a,b) by Th18;
    thus min(a,max(b,c)) = min(a,b) by A3,Def10
      .= max(min(a,b),min(a,c)) by A4,Def10;
  end;
end;

theorem
  max(a,min(b,c)) = min(max(a,b),max(a,c))
proof
  per cases;
  suppose
A1: b <= c;
    then
A2: max(a,b) <= max(a,c) by Th26;
    thus max(a,min(b,c)) = max(a,b) by A1,Def9
      .= min(max(a,b),max(a,c)) by A2,Def9;
  end;
  suppose
A3: c <= b;
    then
A4: max(a,c) <= max(a,b) by Th26;
    thus max(a,min(b,c)) = max(a,c) by A3,Def9
      .= min(max(a,b),max(a,c)) by A4,Def9;
  end;
end;

theorem
  max(max(min(a,b),min(b,c)),min(c,a)) = min(min(max(a,b),max(b,c)),max( c,a))
proof
  per cases;
  suppose
A1: a <= c;
    then
A2: max(a,b) <= max(b,c) by Th26;
    min(a,b) <= min(b,c) by A1,Th18;
    hence max(max(min(a,b),min(b,c)),min(c,a)) = max(min(b,c),min(c,a)) by
    Def10
      .= max(min(b,c),a) by A1,Def9
      .= min(max(a,b),c) by A1,Th37
      .= min(max(a,b),max(c,a)) by A1,Def10
      .= min(min(max(a,b),max(b,c)),max(c,a)) by A2,Def9;
  end;
  suppose
A3: c <= a;
    then
A4: max(a,b) >= max(b,c) by Th26;
    min(a,b) >= min(b,c) by A3,Th18;
    hence max(max(min(a,b),min(b,c)),min(c,a)) = max(min(a,b),min(c,a)) by
    Def10
      .= max(min(a,b),c) by A3,Def9
      .= min(max(c,b),a) by A3,Th37
      .= min(max(c,b),max(c,a)) by A3,Def10
      .= min(min(max(a,b),max(b,c)),max(c,a)) by A4,Def9;
  end;
end;

theorem
  max(a,+infty) = +infty
proof
  a <= +infty by Def5,Lm2,Lm4;
  hence thesis by Def10;
end;

theorem
  min(a,+infty) = a
proof
  a <= +infty by Def5,Lm2,Lm4;
  hence thesis by Def9;
end;

theorem
  max(a,-infty) = a
proof
  a >= -infty by Def5,Lm3,Lm5;
  hence thesis by Def10;
end;

theorem
  min(a,-infty) = -infty
proof
  a >= -infty by Def5,Lm3,Lm5;
  hence thesis by Def9;
end;

begin :: Addenda

theorem
  a in REAL & c in REAL & a <= b & b <= c implies b in REAL
proof
  assume that
A1: a in REAL and
A2: c in REAL and
A3: a <= b and
A4: b <= c;
  b in REAL or b = +infty by A1,A3,Th10;
  hence thesis by A2,A4,Th13;
end;

theorem
  a in REAL & a <= b & b < c implies b in REAL
proof
  assume that
A1: a in REAL & a <= b and
A2: b < c;
  b in REAL or b = +infty by A1,Th10;
  hence thesis by A2,Lm8;
end;

theorem
  c in REAL & a < b & b <= c implies b in REAL
proof
  assume that
A1: c in REAL and
A2: a < b and
A3: b <= c;
  b in REAL or b = -infty by A1,A3,Th13;
  hence thesis by A2,Lm7;
end;

theorem
  a < b & b < c implies b in REAL
proof
  assume
A1: a < b & b < c;
  b in REAL or b = +infty or b = -infty by Lm9;
  hence thesis by A1,Lm7,Lm8;
end;
 
:: from AMI_2, 2008.02.14, A.T.

definition
  let x,y be ext-real number, a,b be set;
  func IFGT(x,y,a,b) equals
  :Def11:
  a if x > y otherwise b;
  correctness;
end;

registration
  let x,y be ext-real number, a,b be natural number;
  cluster IFGT(x,y,a,b) -> natural;
  coherence by Def11;
end;
 
:: from TOPREAL7, 2008.07.05, A.T.

theorem
  max(a,b) <= a implies max(a,b) = a
proof
  assume max(a,b) <= a;
  then max(a,b) < a or max(a,b) = a by Th1;
  hence thesis by Th25;
end;

theorem
  a <= min(a,b) implies min(a,b) = a
proof
  assume min(a,b) >= a;
  then min(a,b) > a or min(a,b) = a by Th1;
  hence thesis by Th17;
end;
