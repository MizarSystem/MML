:: Convergent Sequences in Complex Unitary Space
::  by Noboru Endou
::
:: Received February 10, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies RLVECT_1, FUNCT_1, ARYTM, ARYTM_1, RELAT_1, ABSVALUE, ORDINAL2,
      SEQ_1, SEQ_2, BHSP_3, COMPLEX1, BOOLE, ARYTM_3, CSSPACE, CFUNCT_1,
      PRE_TOPC, NORMSP_1, METRIC_1, SEQM_3, LATTICES, FUNCT_3, FRECHET2;
 notations TARSKI, ORDINAL1, NUMBERS, XREAL_0, REAL_1, NAT_1, RELAT_1, FUNCT_1,
      SEQ_1, SEQM_3,
      STRUCT_0, PRE_TOPC, RLVECT_1, NORMSP_1, BHSP_3, SEQ_2,
      COMPLEX1, CFUNCT_1, CLVECT_1, CSSPACE, FUNCT_3;
 constructors REAL_1, NAT_1, ABSVALUE, MEMBERED, XBOOLE_0, BHSP_3, COMPLEX1,
      COMSEQ_1, CSSPACE, SEQ_2, COMPLEX2, FUNCT_3;
 registrations FUNCT_1, SEQM_3, STRUCT_0, XREAL_0, RELSET_1, ARYTM_3, MEMBERED,
      ORDINAL2, COMPLEX1;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;
 theorems XBOOLE_0, RELAT_1, TARSKI, ABSVALUE, REAL_1, SEQ_2, SEQM_3, FUNCT_1,
      NAT_1, FUNCT_2, RLVECT_1, SEQ_4, XCMPLX_0, XBOOLE_1, CLVECT_1, COMPLEX1,
      REAL_2, AXIOMS, XREAL_0, NORMSP_1, CSSPACE, SUBSET_1, RELSET_1, BHSP_3;
 schemes SEQ_1, FRAENKEL, NAT_1, FUNCT_2;

begin :: Convergence in complex unitary space

 reserve X for ComplexUnitarySpace;
 reserve x, y, w, g, g1, g2 for Point of X;
 reserve z for Complex;
 reserve p, q, r, M, M1, M2 for Real;
 reserve seq, seq1, seq2, seq3 for sequence of X;
 reserve k,n,m for Nat;
 reserve Nseq for increasing Seq_of_Nat;

deffunc 0'(ComplexUnitarySpace) = 0. $1;

definition let X, seq;
attr seq is convergent means :Def1:
  ex g st for r st r > 0 ex m st for n st n >= m holds dist(seq.n, g) < r;
end;

theorem Th1:
seq is constant implies seq is convergent
proof
   assume seq is constant;
   then consider x such that
A1: for n holds seq.n = x by NORMSP_1:def 4;
   take g = x;
   let r such that
A2:r > 0;
   take m = 0;
   let n such that n >= m;
    dist((seq.n) , g) = dist(x , g) by A1
                    .= 0 by CSSPACE:53;
   hence dist((seq.n) , g) < r by A2;
end;

theorem Th2:
seq1 is convergent & (ex k st for n st k <= n holds seq2.n = seq1.n) implies
 seq2 is convergent
proof
   assume that
A1:seq1 is convergent and
A2:ex k st for n st k <= n holds seq2.n = seq1.n;
   consider g such that
A3: for r st r > 0 ex m st for n st n >= m holds
     dist((seq1.n) , g) < r by A1,Def1;
   consider k such that
A4: for n st n >= k holds seq2.n = seq1.n by A2;
     take h = g;
   let r; assume r > 0;
   then consider m1 be Nat such that
A5: for n st n >= m1 holds dist((seq1.n) , h) < r by A3;
A6:now assume
A7:  k <= m1;
     take m = m1;
     let n; assume
A8:  n >= m;
     then n >= k by A7,AXIOMS:22;
     then seq2.n = seq1.n by A4;
     hence dist((seq2.n) , h) < r by A5,A8;
   end;
    now assume
A9:  m1 <= k;
     take m = k;
     let n; assume
A10: n >= m;
     then n >= m1 by A9,AXIOMS:22;
     then dist((seq1.n) , g) < r by A5;
     hence dist((seq2.n) , h) < r by A4,A10;
   end;
   hence ex m st for n st n >= m holds
    dist((seq2.n) , h) < r by A6;
end;

theorem Th3:
seq1 is convergent & seq2 is convergent implies
 seq1 + seq2 is convergent
proof
     assume that
 A1:  seq1 is convergent and
 A2:  seq2 is convergent;
      consider g1 such that
 A3:  for r st r > 0 ex m st for n st n >= m holds
      dist((seq1.n) , g1) < r by A1,Def1;
      consider g2 be Point of X such that
 A4:  for r st r > 0 ex m st for n st n >= m holds
      dist((seq2.n) , g2) < r by A2,Def1;
      take g = g1 + g2;
      let r; assume r > 0;
 then A5:  r/2 > 0 by SEQ_2:3;
      then consider m1 be Nat such that
 A6:  for n st n >= m1 holds dist((seq1.n) , g1) < r/2 by A3;
      consider m2 be Nat such that
 A7:  for n st n >= m2 holds dist((seq2.n) , g2) < r/2 by A4,A5;
      take k = m1 + m2;
      let n such that
  A8:  n >= k;
         m1 + m2 >= m1 by NAT_1:37;
      then n >= m1 by A8,AXIOMS:22;
  then A9:  dist((seq1.n) , g1) < r/2 by A6;
         k >= m2 by NAT_1:37;
      then n >= m2 by A8,AXIOMS:22;
      then dist((seq2.n) , g2) < r/2 by A7;
      then A10: dist((seq1.n) , g1) + dist((seq2.n) , g2) < r/2 + r/2
      by A9,REAL_1:67;
         dist((seq1 + seq2).n , g) =
      dist((seq1.n) + (seq2.n) , (g1 + g2)) by CLVECT_1:def 11;
      then dist((seq1 + seq2).n , g) <= dist((seq1.n) , g1) +
      dist((seq2.n) , g2) by CSSPACE:59;
      hence dist((seq1 + seq2).n , g) < r by A10,AXIOMS:22;
  end;

theorem Th4:
seq1 is convergent & seq2 is convergent implies
 seq1 - seq2 is convergent
proof
     assume that
 A1:  seq1 is convergent and
 A2:  seq2 is convergent;
      consider g1 such that
 A3:  for r st r > 0 ex m st for n st n >= m holds
      dist((seq1.n) , g1) < r by A1,Def1;
      consider g2 be Point of X such that
 A4:  for r st r > 0 ex m st for n st n >= m holds
      dist((seq2.n) , g2) < r by A2,Def1;
      take g = g1 - g2;
      let r; assume r > 0;
 then A5:  r/2 > 0 by SEQ_2:3;
      then consider m1 be Nat such that
 A6:  for n st n >= m1 holds dist((seq1.n) , g1) < r/2 by A3;
      consider m2 be Nat such that
 A7:  for n st n >= m2 holds dist((seq2.n) , g2) < r/2 by A4,A5;
      take k = m1 + m2;
      let n such that
  A8:  n >= k;
         m1 + m2 >= m1 by NAT_1:37;
      then n >= m1 by A8,AXIOMS:22;
  then A9:  dist((seq1.n) , g1) < r/2 by A6;
         k >= m2 by NAT_1:37;
      then n >= m2 by A8,AXIOMS:22;
      then dist((seq2.n) , g2) < r/2 by A7;
      then A10: dist((seq1.n) , g1) + dist((seq2.n) , g2) < r/2 + r/2
      by A9,REAL_1:67;
         dist((seq1 - seq2).n , g) =
      dist((seq1.n) - (seq2.n) , (g1 - g2)) by CLVECT_1:def 12;
      then dist((seq1 - seq2).n , g) <= dist((seq1.n) , g1) +
      dist((seq2.n) , g2) by CSSPACE:60;
      hence dist((seq1 - seq2).n , g) < r by A10,AXIOMS:22;
  end;

theorem Th5:
seq is convergent implies z * seq is convergent
proof
     assume seq is convergent;
           then consider g such that
     A1:    for r st r > 0 ex m st for n
           st n >= m holds dist((seq.n) , g) < r by Def1;
           take h = z * g;
     A2:    now assume
     A3:   z = 0;
           let r; assume r > 0;
           then consider m1 be Nat such that
     A4:   for n st n >= m1 holds dist((seq.n) , g) < r by A1;
           take k = m1;
           let n; assume n >= k;
     then A5:   dist((seq.n) , g) < r by A4;
           dist(z * (seq.n) , z * g)
         = dist(0c * (seq.n) , 0'(X)) by A3,CLVECT_1:2
        .= dist(0'(X) , 0'(X)) by CLVECT_1:2
        .= 0 by CSSPACE:53;
           then dist(z * (seq.n) , h) < r by A5,CSSPACE:56;
           hence dist((z * seq).n , h) < r by CLVECT_1:def 14;
           end;
              now assume
A6:        z <> 0;
     then A7:   |.z.| > 0 by COMPLEX1:133;
           let r such that
     A8:   r > 0;
     A9:   |.z.| <> 0 by A6,COMPLEX1:133;
              0/|.z.| = 0;
           then r/|.z.| > 0 by A7,A8,REAL_1:73;
           then consider m1 be Nat such that
     A10:   for n st n >= m1 holds dist((seq.n) , g) < r/|.z.| by A1;
           take k = m1;
           let n; assume n >= k;
     then A11:   dist((seq.n) , g) < r/|.z.| by A10;
     A12:   dist(z * (seq.n) , z * g)
         = ||.(z * (seq.n)) - (z * g).|| by CSSPACE:def 16
        .= ||.z * ((seq.n) - g).|| by CLVECT_1:10
        .= |.z.| * ||.(seq.n) - g.|| by CSSPACE:45
        .= |.z.| * dist((seq.n) , g) by CSSPACE:def 16;
              |.z.| * (r/|.z.|) = |.z.| * (|.z.|" * r) by XCMPLX_0:def 9
                            .= |.z.| *|.z.|" * r
                            .= 1 * r by A9,XCMPLX_0:def 7
                            .= r;
           then dist((z *(seq.n)) , h) < r by A7,A11,A12,REAL_1:70;
           hence dist((z * seq).n , h) < r by CLVECT_1:def 14;
           end;
     hence thesis by A2;
end;

theorem Th6:
seq is convergent implies - seq is convergent
proof
     assume seq is convergent;
     then (-1r) * seq is convergent by Th5;
     hence thesis by CSSPACE:77;
end;

theorem Th7:
seq is convergent implies seq + x is convergent
proof
   assume seq is convergent;
   then consider g such that
A1: for r st r > 0 ex m st for n st n >= m holds
     dist((seq.n) , g) < r by Def1;
   take g + x;
   let r;
   assume r > 0;
   then consider m1 be Nat such that
A2: for n st n >= m1 holds dist((seq.n) , g) < r by A1;
   take k = m1;
   let n;
   assume n >= k;
then A3:dist((seq.n) , g) < r by A2;
    dist((seq.n)+x,g+x) <= dist((seq.n),g) + dist(x,x) by CSSPACE:59;
   then dist((seq.n) + x , g + x) <= dist((seq.n) , g) + 0 by CSSPACE:53;
   then dist((seq.n) + x , g + x) < r by A3,AXIOMS:22;
   hence thesis by CSSPACE:def 18;
end;

theorem Th8:
seq is convergent implies seq - x is convergent
proof
     assume seq is convergent;
     then seq + (-x) is convergent by Th7;
     hence thesis by CSSPACE:78;
end;

theorem Th9:
seq is convergent iff ex g st for r st r > 0 ex m st for n st
 n >= m holds ||.(seq.n) - g.|| < r
proof
     thus seq is convergent implies ex g st for r st r > 0 ex m st for n
     st n >= m holds ||.(seq.n) - g.|| < r
     proof
          assume seq is convergent;
          then consider g such that
     A1:   for r st r > 0 ex m st for n
          st n >= m holds dist((seq.n) , g) < r by Def1;
          take g;
          let r; assume r > 0;
          then consider m1 be Nat such that
     A2:   for n st n >= m1 holds dist((seq.n) , g) < r by A1;
          take k = m1;
          let n; assume n >= k;
          then dist((seq.n) , g) < r by A2;
          hence thesis by CSSPACE:def 16;
          end;

         ( ex g st for r st r > 0 ex m st for n
      st n >= m holds ||.(seq.n) - g.|| < r ) implies seq is convergent
      proof
        given g such that
      A3:   for r st r > 0 ex m st for n
           st n >= m holds ||.(seq.n) - g.|| < r;
           take g;
           let r; assume r > 0;
           then consider m1 be Nat such that
     A4:    for n st n >= m1 holds ||.(seq.n) - g.|| < r by A3;
           take k = m1;
           let n; assume n >= k;
           then ||.(seq.n) - g.|| < r by A4;
           hence thesis by CSSPACE:def 16;
      end;
      hence thesis;
end;

definition let X, seq;
  assume A1: seq is convergent;
   func lim seq -> Point of X means :Def2:
    for r st r > 0 ex m st for n st n >= m holds dist((seq.n) , it) < r;
existence by A1,Def1;
uniqueness
          proof
                  for x , y st
( for r st r > 0 ex m st for n st n >= m holds dist((seq.n) , x) < r ) &
( for r st r > 0 ex m st for n st n >= m holds dist((seq.n) , y) < r )
holds x = y
          proof
given x , y such that
A2: for r st r > 0 ex m st for n st n >= m holds dist((seq.n) , x) < r and
A3: for r st r > 0 ex m st for n st n >= m holds dist((seq.n) , y) < r and
A4: x <> y;
A5: dist(x , y) > 0 by A4,CSSPACE:57;
    then A6: dist(x , y)/4 > 0/4 by REAL_1:73;
    then consider m1 be Nat such that
A7: for n st n >= m1 holds dist((seq.n) , x) < dist(x , y)/4 by A2;
    consider m2 be Nat such that
A8: for n st n >= m2 holds dist((seq.n) , y) < dist(x , y)/4 by A3,A6;
A9:  now assume m1 <= m2;
then A10: dist((seq.m2) , x) < dist(x , y)/4 by A7;
       dist((seq.m2) , y) < dist(x , y)/4 by A8;
    then A11: dist((seq.m2) , x) + dist((seq.m2) , y) <
    dist(x , y)/4 + dist(x , y)/4 by A10,REAL_1:67;
       dist(x , y) = dist(x - (seq.m2) , y - (seq.m2)) by CSSPACE:61;
    then dist(x , y) <= dist((seq.m2) , x) + dist((seq.m2) , y) by CSSPACE:62;
    then dist(x , y) <= dist(x , y)/2 by A11,AXIOMS:22;
    hence contradiction by A5,SEQ_2:4;
    end;
      now assume
    m1 >= m2;
then A12: dist((seq.m1) , y) < dist(x , y)/4 by A8;
       dist((seq.m1) , x) < dist(x , y)/4 by A7;
    then A13: dist((seq.m1) , x) + dist((seq.m1) , y) < dist(x , y)/4 + dist(x
, y)
/4
    by A12,REAL_1:67;
       dist(x , y) = dist(x - (seq.m1) , y - (seq.m1)) by CSSPACE:61;
    then dist(x , y) <= dist((seq.m1) , x) + dist((seq.m1) , y) by CSSPACE:62;
    then dist(x , y) <= dist(x , y)/2 by A13,AXIOMS:22;
    hence contradiction by A5,SEQ_2:4;
    end;
    hence contradiction by A9;
    end;
hence thesis;
end;
end;

theorem Th10:
seq is constant & x in rng seq implies lim seq = x
proof
     assume that
A1:   seq is constant and
A2:   x in rng seq;
     consider y such that
A3:   rng seq = {y} by A1,NORMSP_1:27;
     consider w such that
A4:   for n holds seq.n = w by A1,NORMSP_1:def 4;
A5:   x = y by A2,A3,TARSKI:def 1;
A6:   seq is convergent by A1,Th1;
        now let r such that
A7:   r > 0;
     take m = 0;
     let n such that n >= m;
        n in NAT;
     then n in dom seq by NORMSP_1:17;
     then seq.n in rng seq by FUNCT_1:def 5;
     then w in rng seq by A4;
     then w = x by A3,A5,TARSKI:def 1;
     then seq.n = x by A4;
     hence dist((seq.n) , x) < r by A7,CSSPACE:53;
     end;
     hence lim seq = x by A6,Def2;
end;

theorem
 seq is constant & (ex n st seq.n = x) implies lim seq = x
proof
     assume that
A1:   seq is constant and
A2:   ex n st seq.n = x;
     consider n such that
A3:   seq.n = x by A2;
        n in NAT;
     then n in dom seq by NORMSP_1:17;
     then x in rng seq by A3,FUNCT_1:def 5;
     hence lim seq = x by A1,Th10;
end;

theorem
 seq1 is convergent & (ex k st for n st n >= k holds seq2.n = seq1.n) implies
 lim seq1 = lim seq2
proof
     assume that
A1:  seq1 is convergent and
A2:  ex k st for n st n >= k holds seq2.n = seq1.n;
A3:   seq2 is convergent by A1,A2,Th2;
     consider k such that
A4:  for n st n >= k holds seq2.n = seq1.n by A2;
        now let r; assume r > 0;
     then consider m1 be Nat such that
A5:  for n st n >= m1 holds dist((seq1.n) , (lim seq1)) < r by A1,Def2;
A6:   now assume
A7:  k <= m1;
     take m = m1;
     let n; assume
A8:  n >= m;
     then n >= k by A7,AXIOMS:22;
     then seq2.n = seq1.n by A4;
     hence dist((seq2.n) , (lim seq1)) < r by A5,A8;
     end;
        now assume
A9:  m1 <= k;
     take m = k;
     let n; assume
A10:  n >= m;
     then n >= m1 by A9,AXIOMS:22;
     then dist((seq1.n) , (lim seq1)) < r by A5;
     hence dist((seq2.n) , (lim seq1)) < r by A4,A10;
     end;
     hence ex m st for n st n >= m holds
     dist((seq2.n) , (lim seq1)) < r by A6;
     end;
     hence thesis by A3,Def2;
end;

theorem Th13:
seq1 is convergent & seq2 is convergent implies
 lim (seq1 + seq2) = (lim seq1) + (lim seq2)
proof
     assume that
 A1:  seq1 is convergent and
 A2:  seq2 is convergent;
 A3:   seq1 + seq2 is convergent by A1,A2,Th3;
      set g1 = lim seq1;
      set g2 = lim seq2;
      set g = g1 + g2;
         now let r; assume r > 0;
 then A4:  r/2 > 0 by SEQ_2:3;
      then consider m1 be Nat such that
 A5:  for n st n >= m1 holds dist((seq1.n) , g1) < r/2 by A1,Def2;
      consider m2 be Nat such that
 A6:  for n st n >= m2 holds dist((seq2.n) , g2) < r/2 by A2,A4,Def2;
      take k = m1 + m2;
      let n such that
  A7:  n >= k;
         m1 + m2 >= m1 by NAT_1:37;
      then n >= m1 by A7,AXIOMS:22;
  then A8:  dist((seq1.n) , g1) < r/2 by A5;
         k >= m2 by NAT_1:37;
      then n >= m2 by A7,AXIOMS:22;
      then dist((seq2.n) , g2) < r/2 by A6;
      then A9: dist((seq1.n) , g1) + dist((seq2.n) , g2) < r/2 + r/2
      by A8,REAL_1:67;
         dist((seq1 + seq2).n , g)
      = dist((seq1.n) + (seq2.n) , g1 + g2) by CLVECT_1:def 11;
      then dist((seq1 + seq2).n , g)
      <= dist((seq1.n) , g1) + dist((seq2.n) , g2) by CSSPACE:59;
      hence dist((seq1 + seq2).n , g) < r by A9,AXIOMS:22;
      end;
      hence thesis by A3,Def2;
end;

theorem Th14:
seq1 is convergent & seq2 is convergent implies
  lim (seq1 - seq2) = (lim seq1) - (lim seq2)
proof
     assume that
 A1:  seq1 is convergent and
 A2:  seq2 is convergent;
 A3:   seq1 - seq2 is convergent by A1,A2,Th4;
      set g1 = lim seq1;
      set g2 = lim seq2;
      set g = g1 - g2;
         now let r; assume r > 0;
 then A4:  r/2 > 0 by SEQ_2:3;
      then consider m1 be Nat such that
 A5:  for n st n >= m1 holds dist((seq1.n) , g1) < r/2 by A1,Def2;
      consider m2 be Nat such that
 A6:  for n st n >= m2 holds dist((seq2.n) , g2) < r/2 by A2,A4,Def2;
      take k = m1 + m2;
      let n such that
  A7:  n >= k;
         m1 + m2 >= m1 by NAT_1:37;
      then n >= m1 by A7,AXIOMS:22;
  then A8:  dist((seq1.n) , g1) < r/2 by A5;
         k >= m2 by NAT_1:37;
      then n >= m2 by A7,AXIOMS:22;
      then dist((seq2.n) , g2) < r/2 by A6;
      then A9: dist((seq1.n) , g1) + dist((seq2.n) , g2) < r/2 + r/2
      by A8,REAL_1:67;
         dist((seq1 - seq2).n , g)
      = dist((seq1.n) - (seq2.n) , g1 - g2) by CLVECT_1:def 12;
      then dist((seq1 - seq2).n , g)
      <= dist((seq1.n) , g1) + dist((seq2.n) , g2) by CSSPACE:60;
      hence dist((seq1 - seq2).n , g) < r by A9,AXIOMS:22;
      end;
      hence thesis by A3,Def2;
end;

theorem Th15:
seq is convergent implies lim (z * seq) = z * (lim seq)
proof
   assume
A1:seq is convergent;
then A2:z * seq is convergent by Th5;
   set g = lim seq;
   set h = z * g;
A3:now assume
A4:  z = 0;
     let r;
     assume
A5: r > 0;
     consider m1 be Nat;
     take k = m1;
     let n;
     assume n >= k;
      dist(z*(seq.n), z*g) = dist(0c*(seq.n), 0'(X)) by A4,CLVECT_1:2
        .= dist(0'(X), 0'(X)) by CLVECT_1:2
        .= 0 by CSSPACE:53;
     hence dist((z * seq).n , h) < r by A5,CLVECT_1:def 14;
   end;
    now assume
A6:  z <> 0;
then A7:  |.z.| > 0 by COMPLEX1:133;
     let r such that
A8:   r > 0;
A9: |.z.| <> 0 by A6,COMPLEX1:133;
      0/|.z.| =0;
     then r/|.z.| > 0 by A7,A8,REAL_1:73;
     then consider m1 be Nat such that
A10:  for n st n >= m1 holds dist((seq.n),g) < r/|.z.| by A1,Def2;
     take k = m1;
     let n;
     assume n >= k;
then A11: dist((seq.n) , g) < r/|.z.| by A10;
A12: dist(z*(seq.n), z*g) = ||.(z*(seq.n)) - (z*g).|| by CSSPACE:def 16
        .= ||.z * ((seq.n) - g).|| by CLVECT_1:10
        .= |.z.| * ||.(seq.n) - g.|| by CSSPACE:45
        .= |.z.| * dist((seq.n) , g) by CSSPACE:def 16;
      |.z.| * (r/|.z.|) = |.z.| * (|.z.|" * r) by XCMPLX_0:def 9
                      .= |.z.| *|.z.|" * r
                      .= 1 * r by A9,XCMPLX_0:def 7
                      .= r;
     then dist(z * (seq.n) , h) < r by A7,A11,A12,REAL_1:70;
     hence dist((z * seq).n , h) < r by CLVECT_1:def 14;
   end;
   hence thesis by A2,A3,Def2;
end;

theorem Th16:
seq is convergent implies lim (- seq) = - (lim seq)
proof
     assume
        seq is convergent;
     then lim ((-1r) * seq) = (-1r) * (lim seq) by Th15;
     then lim (- seq) = (-1r) * (lim seq) by CSSPACE:77;
     hence thesis by CLVECT_1:4;
end;

theorem Th17:
seq is convergent implies lim (seq + x) = (lim seq) + x
proof
   assume
A1:seq is convergent;
then A2:seq + x is convergent by Th7;
   set g = lim seq;
    now let r; assume
      r > 0;
     then consider m1 be Nat such that
A3:   for n st n >= m1 holds dist((seq.n) , g) < r by A1,Def2;
     take k = m1;
     let n; assume n >= k;
then A4:  dist((seq.n) , g) < r by A3;
      dist((seq.n) , g) = dist((seq.n) - (-x) , g - (-x)) by CSSPACE:61
      .= dist((seq.n) + (-(-x)) , g - (-x)) by RLVECT_1:def 11
      .= dist((seq.n) + x , g - (-x)) by RLVECT_1:30
      .= dist((seq.n) + x , g + (-(-x))) by RLVECT_1:def 11
      .= dist((seq.n) + x , g + x) by RLVECT_1:30;
     hence dist((seq + x).n , (g + x)) < r by A4,CSSPACE:def 18;
   end;
   hence thesis by A2,Def2;
end;

theorem
 seq is convergent implies lim (seq - x) = (lim seq) - x
proof
     assume
        seq is convergent;
     then lim (seq + (-x)) = (lim seq) + (-x) by Th17;
     then lim (seq - x) = (lim seq) + (-x) by CSSPACE:78;
     hence thesis by RLVECT_1:def 11;
end;

theorem Th19:
seq is convergent implies
 (lim seq = g iff for r st r > 0 ex m st for n st n >= m
    holds ||.(seq.n) - g.|| < r)
proof
     assume
A1:   seq is convergent;
     thus
        lim seq = g implies ( for r st r > 0 ex m st for n st n >= m
     holds ||.(seq.n) - g.|| < r )
     proof
          assume
     A2:   lim seq = g;
          let r; assume r > 0;
          then consider m1 be Nat such that
     A3:   for n st n >= m1 holds dist((seq.n) , g) < r by A1,A2,Def2;
          take k = m1;
          let n; assume n >= k;
         then dist((seq.n) , g) < r by A3;
         hence thesis by CSSPACE:def 16;
         end;

        ( for r st r > 0 ex m st for n st n >= m holds ||.(seq.n) - g.|| < r )
       implies lim seq = g
       proof
            assume
       A4:   ( for r st r > 0 ex m st for n st n >= m
            holds ||.(seq.n) - g.|| < r );
               now let r; assume r > 0;
            then consider m1 be Nat such that
      A5:    for n st n >= m1 holds ||.(seq.n) - g.|| < r by A4;
            take k = m1;
            let n; assume
             n >= k;
            then ||.(seq.n) - g.|| < r by A5;
            hence dist((seq.n) , g) < r by CSSPACE:def 16;
            end;
            hence thesis by A1,Def2;
       end;
       hence thesis;
end;

definition let X, seq;
  func ||.seq.|| -> Real_Sequence means
:Def3:  for n holds it.n =||.(seq.n).||;
existence
proof
   deffunc _F(Nat) = ||.(seq.$1).||;
   consider s being Real_Sequence such that
A1:for n holds s.n = _F(n) from SEQ_1:sch 1;
   take s;
   thus thesis by A1;
end;
uniqueness
          proof
               let s,t be Real_Sequence;
                    assume
               A2:   ( for n holds s.n = ||.(seq.n).|| ) &
                    ( for n holds t.n = ||.(seq.n).|| );
                       now let n;
                        s.n = ||.(seq.n).|| by A2; hence
                        s.n = t.n by A2;
                    end;
                    hence thesis by FUNCT_2:113;
         end;
end;

theorem Th20:
seq is convergent implies ||.seq.|| is convergent
proof
     assume
            seq is convergent;
           then consider g such that
     A1:    for r st r > 0 ex m st for n
           st n >= m holds ||.(seq.n) - g.|| < r by Th9;
              now let r be real number;
A2:  r is Real by XREAL_0:def 1;
           assume r > 0;
           then consider m1 be Nat such that
     A3:    for n st n >= m1 holds ||.(seq.n) - g.|| < r by A1,A2;
           take k = m1;
            now let n; assume
            n >= k;
     then A4:    ||.(seq.n) - g.|| < r by A3;
              abs(||.(seq.n).|| - ||.g.||) <= ||.(seq.n) - g.|| by CSSPACE:51;
           then abs(||.(seq.n).|| - ||.g.||) < r by A4,AXIOMS:22;
           hence abs(||.seq.||.n - ||.g.||) < r by Def3;
           end;
           hence for n st k <= n holds abs(||.seq.||.n - ||.g.||) < r;
           end;
     hence thesis by SEQ_2:def 6;
end;

theorem Th21:
seq is convergent & lim seq = g implies
  ||.seq.|| is convergent & lim ||.seq.|| = ||.g.||
proof
     assume that
A1:  seq is convergent and
A2:  lim seq = g;
A3:   ||.seq.|| is convergent by A1,Th20;
        now let r be real number;
A4:    r is Real by XREAL_0:def 1;
       assume r > 0;
          then consider m1 be Nat such that
     A5:   for n st n >= m1 holds ||.(seq.n) - g.|| < r
          by A1,A2,A4,Th19;
          take k = m1;
           now let n; assume n >= k;
          then A6:   ||.(seq.n) - g.|| < r by A5;
                  abs(||.(seq.n).|| - ||.g.||) <= ||.(seq.n) - g.||
 by CSSPACE:51;
               then abs(||.(seq.n).|| - ||.g.||) < r by A6,AXIOMS:22;
               hence abs(||.seq.||.n - ||.g.||) < r by Def3;
               end;
          hence for n st k <= n holds abs(||.seq.||.n - ||.g.||) < r;
          end;
    hence thesis by A3,SEQ_2:def 7;
end;

theorem Th22:
seq is convergent & lim seq = g implies
  ( ||.seq - g.|| is convergent & lim ||.seq - g.|| = 0 )
proof
     assume that
     A1:    seq is convergent and
     A2:    lim seq = g;
              seq - g is convergent by A1,Th8;
     then A3:    ||.seq - g.|| is convergent by Th20;
              now let r be real number;
A4:  r is Real by XREAL_0:def 1;
           assume r > 0;
           then consider m1 be Nat such that
     A5:    for n st n >= m1 holds ||.(seq.n) - g.|| < r by A1,A2,A4,Th19;
           take k = m1;
           let n; assume
            n >= k;
     then ||.(seq.n) - g.|| < r by A5;
     then A6:    ||.((seq.n) - g ) - 0'(X).|| < r by RLVECT_1:26;
              abs(||.(seq.n) - g.|| - ||.0'(X).||) <= ||.((seq.n) - g ) - 0'(X)
.||
           by CSSPACE:51;
           then abs(||.(seq.n) - g.|| - ||.0'(X).||) < r by A6,AXIOMS:22;
           then abs((||.(seq.n) - g.|| - 0)) < r by CSSPACE:44;
           then abs((||.(seq - g).n.|| - 0)) < r by CLVECT_1:def 13;
           hence abs((||.seq - g.||.n - 0)) < r by Def3;
           end;
    hence thesis by A3,SEQ_2:def 7;
end;

definition let X, seq, x;
 func dist(seq , x) -> Real_Sequence means :Def4:
  for n holds it.n =dist((seq.n) , x);
existence
         proof
             deffunc _F(Nat) = dist((seq.$1) , x);
              consider s being Real_Sequence such that
         A1:   for n holds s.n = _F(n) from SEQ_1:sch 1;
              take s;
              thus thesis by A1;
         end;
uniqueness
          proof
               let s,t be Real_Sequence;
                    assume
               A2:   ( for n holds s.n = dist((seq.n) , x) ) &
                    ( for n holds t.n = dist((seq.n) , x) );
                       now let n;
                        s.n = dist((seq.n) , x) by A2; hence
                        s.n = t.n by A2;
                    end;
                    hence thesis by FUNCT_2:113;
         end;
end;

theorem Th23:
( seq is convergent & lim seq = g ) implies dist(seq,g) is convergent
proof
     assume that
     A1:   seq is convergent and
     A2:   lim seq = g;
              now let r be real number;
A3:  r is Real by XREAL_0:def 1;
           assume r > 0;
           then consider m1 be Nat such that
     A4: for n st n >= m1 holds dist((seq.n) , g) < r by A1,A2,A3,Def2;
           take k = m1;
            now let n; assume
            n >= k;
     then A5:    dist((seq.n) , g) < r by A4;
           dist((seq.n) , g) >= 0 by CSSPACE:56;
           then abs((dist((seq.n) , g) - 0))
         = dist((seq.n) , g) by ABSVALUE:def 1;
           hence abs((dist(seq , g).n - 0)) < r by A5,Def4;
           end;
           hence for n st k <= n holds abs((dist(seq , g).n - 0)) < r;
           end;
     hence thesis by SEQ_2:def 6;
end;

theorem Th24:
seq is convergent & lim seq = g implies
  ( dist(seq,g) is convergent & lim dist(seq,g) = 0 )
proof
     assume that
     A1:    seq is convergent and
     A2:    lim seq = g;
     A3:    dist(seq , g) is convergent by A1,A2,Th23;
              now let r be real number;
A4:  r is Real by XREAL_0:def 1;
           assume r > 0;
           then consider m1 be Nat such that
     A5:  for n st n >= m1 holds dist((seq.n) , g) < r by A1,A2,A4,Def2;
           take k = m1;
           let n; assume
            n >= k;
     then A6:    dist((seq.n) , g) < r by A5;
           dist((seq.n) , g) >= 0 by CSSPACE:56;
           then abs((dist((seq.n) , g) - 0))
         = dist((seq.n) , g) by ABSVALUE:def 1;
           hence abs((dist(seq , g).n - 0)) < r by A6,Def4;
           end;
    hence thesis by A3,SEQ_2:def 7;
end;

theorem
 seq1 is convergent & lim seq1 = g1 &
seq2 is convergent & lim seq2 = g2 implies
 ||.seq1 + seq2.|| is convergent &
 lim ||.seq1 + seq2.|| = ||.g1 + g2.||
proof
     assume that
A1:  seq1 is convergent and
A2:  lim seq1 = g1 and
A3:  seq2 is convergent and
A4:  lim seq2 = g2;
A5:   seq1 + seq2 is convergent by A1,A3,Th3;
        lim (seq1 + seq2) = g1 + g2 by A1,A2,A3,A4,Th13;
     hence thesis by A5,Th21;
end;

theorem
 seq1 is convergent & lim seq1 = g1 &
seq2 is convergent & lim seq2 = g2 implies
 ||.(seq1 + seq2) - (g1 + g2).|| is convergent &
 lim ||.(seq1 + seq2) - (g1 + g2).|| = 0
proof
     assume that
A1:  seq1 is convergent and
A2:  lim seq1 = g1 and
A3:  seq2 is convergent and
A4:  lim seq2 = g2;
A5:   seq1 + seq2 is convergent by A1,A3,Th3;
        lim (seq1 + seq2) = g1 + g2 by A1,A2,A3,A4,Th13;
     hence thesis by A5,Th22;
end;

theorem
 seq1 is convergent & lim seq1 = g1 &
seq2 is convergent & lim seq2 = g2 implies
 ||.seq1 - seq2.|| is convergent &
 lim ||.seq1 - seq2.|| = ||.g1 - g2.||
proof
     assume that
A1:  seq1 is convergent and
A2:  lim seq1 = g1 and
A3:  seq2 is convergent and
A4:  lim seq2 = g2;
A5:   seq1 - seq2 is convergent by A1,A3,Th4;
        lim (seq1 - seq2) = g1 - g2 by A1,A2,A3,A4,Th14;
     hence thesis by A5,Th21;
end;

theorem
 seq1 is convergent & lim seq1 = g1 &
seq2 is convergent & lim seq2 = g2 implies
 ||.(seq1 - seq2) - (g1 - g2).|| is convergent &
 lim ||.(seq1 - seq2) - (g1 - g2).|| = 0
proof
     assume that
A1:  seq1 is convergent and
A2:  lim seq1 = g1 and
A3:  seq2 is convergent and
A4:  lim seq2 = g2;
A5:   seq1 - seq2 is convergent by A1,A3,Th4;
        lim (seq1 - seq2) = g1 - g2 by A1,A2,A3,A4,Th14;
     hence thesis by A5,Th22;
end;

theorem
 seq is convergent & lim seq = g implies
 ||.z * seq.|| is convergent & lim ||.z * seq.|| = ||.z * g.||
proof
     assume that
A1:   seq is convergent and
A2:   lim seq = g;
A3:   z * seq is convergent by A1,Th5;
        lim (z * seq) = z * g by A1,A2,Th15;
     hence thesis by A3,Th21;
end;

theorem
 seq is convergent & lim seq = g implies
 ||.(z * seq) - (z * g).|| is convergent & lim ||.(z * seq) - (z * g).|| = 0
proof
     assume that
A1:   seq is convergent and
A2:   lim seq = g;
A3:   z * seq is convergent by A1,Th5;
        lim (z * seq) = z * g by A1,A2,Th15;
     hence thesis by A3,Th22;
end;

theorem
 seq is convergent & lim seq = g implies
 ||.- seq.|| is convergent & lim ||.- seq.|| = ||.- g.||
proof
     assume that
A1:   seq is convergent and
A2:   lim seq = g;
A3:   - seq is convergent by A1,Th6;
        lim (- seq) = - g by A1,A2,Th16;
     hence thesis by A3,Th21;
end;

theorem
 seq is convergent & lim seq = g implies
 ||.(- seq) - (- g).|| is convergent & lim ||.(- seq) - (- g).|| = 0
proof
     assume that
A1:   seq is convergent and
A2:   lim seq = g;
A3:   - seq is convergent by A1,Th6;
        lim (- seq) = - g by A1,A2,Th16;
     hence thesis by A3,Th22;
end;

Lm1:
seq is convergent & lim seq = g implies
     ||.seq + x.|| is convergent &
     lim ||.seq + x.|| = ||.g + x.||
proof
     assume that
A1:   seq is convergent and
A2:   lim seq = g;
A3:   seq + x is convergent by A1,Th7;
        lim (seq + x) = g + x by A1,A2,Th17;
     hence thesis by A3,Th21;
end;

theorem Th33:
seq is convergent & lim seq = g implies
 ||.(seq + x) - (g + x).|| is convergent & lim ||.(seq + x) - (g + x).|| = 0
proof
     assume that
A1:   seq is convergent and
A2:   lim seq = g;
A3:   seq + x is convergent by A1,Th7;
        lim (seq + x) = g + x by A1,A2,Th17;
     hence thesis by A3,Th22;
end;

theorem
 seq is convergent & lim seq = g implies
 ||.seq - x.|| is convergent & lim ||.seq - x.|| = ||.g - x.||
proof
     assume seq is convergent & lim seq = g;
     then ||.seq + (-x).|| is convergent &
     lim ||.seq + (-x).|| = ||.g + (-x).|| by Lm1;
     then ||.seq - x.|| is convergent &
     lim ||.seq - x.|| = ||.g + (-x).|| by CSSPACE:78;
     hence thesis by RLVECT_1:def 11;
end;

theorem
 seq is convergent & lim seq = g implies
 ||.(seq - x) - (g - x).|| is convergent & lim ||.(seq - x) - (g - x).|| = 0
proof
     assume seq is convergent & lim seq = g;
     then ||.(seq + (-x)) - (g + (-x)).|| is convergent &
     lim ||.(seq + (-x)) - (g + (-x)).|| = 0 by Th33;
     then ||.(seq - x) - (g + (-x)).|| is convergent &
     lim ||.(seq - x) - (g + (-x)).|| = 0 by CSSPACE:78;
     hence thesis by RLVECT_1:def 11;
end;

theorem
 seq1 is convergent & lim seq1 = g1 &
seq2 is convergent & lim seq2 = g2 implies
 dist((seq1 + seq2) , (g1 + g2)) is convergent &
 lim dist((seq1 + seq2) , (g1 + g2)) = 0
proof
     assume that
A1:  seq1 is convergent and
A2:  lim seq1 = g1 and
A3:  seq2 is convergent and
A4:  lim seq2 = g2;
A5:   seq1 + seq2 is convergent by A1,A3,Th3;
        lim (seq1 + seq2) = g1 + g2 by A1,A2,A3,A4,Th13;
     hence thesis by A5,Th24;
end;

theorem
 seq1 is convergent & lim seq1 = g1 &
seq2 is convergent & lim seq2 = g2 implies
   dist((seq1 - seq2) , (g1 - g2)) is convergent &
   lim dist((seq1 - seq2) , (g1 - g2)) = 0
proof
     assume that
A1:  seq1 is convergent and
A2:  lim seq1 = g1 and
A3:  seq2 is convergent and
A4:  lim seq2 = g2;
A5:   seq1 - seq2 is convergent by A1,A3,Th4;
        lim (seq1 - seq2) = g1 - g2 by A1,A2,A3,A4,Th14;
     hence thesis by A5,Th24;
end;

theorem
 seq is convergent & lim seq = g implies
 dist((z * seq) , (z * g)) is convergent &
 lim dist((z * seq) , (z * g)) = 0
proof
     assume that
A1:   seq is convergent and
A2:   lim seq = g;
A3:   z * seq is convergent by A1,Th5;
        lim (z * seq) = z * g by A1,A2,Th15;
     hence thesis by A3,Th24;
end;

theorem
 seq is convergent & lim seq = g implies
  dist((seq + x) , (g + x)) is convergent &
  lim dist((seq + x) , (g + x)) = 0
proof
     assume that
A1:   seq is convergent and
A2:   lim seq = g;
A3:   seq + x is convergent by A1,Th7;
        lim (seq + x) = g + x by A1,A2,Th17;
     hence thesis by A3,Th24;
end;

definition let X, x, r;
func Ball(x,r) -> Subset of X equals :Def5:
  {y where y is Point of X : ||.x - y.|| < r};
coherence
proof defpred _P[Point of X] means ||.x - $1.|| < r;
     {y where y is Point of X : _P[y]} c= the carrier of X from FRAENKEL:sch 10
;
  hence thesis;
end;

func cl_Ball(x,r) -> Subset of X equals :Def6:
  {y where y is Point of X : ||.x - y.|| <= r};
coherence
proof defpred _P[Point of X] means ||.x - $1.|| <= r;
     {y where y is Point of X : _P[y]} c= the carrier of X from FRAENKEL:sch 10
;
  hence thesis;
end;

func Sphere(x,r) -> Subset of X equals :Def7:
  {y where y is Point of X : ||.x - y.|| = r};
coherence
proof defpred _P[Point of X] means ||.x - $1.|| = r;
     {y where y is Point of X : _P[y]} c= the carrier of X from FRAENKEL:sch 10
;
  hence thesis;
end;
end;

theorem Th40:
w in Ball(x,r) iff ||.x - w.|| < r
proof
     thus w in Ball(x,r) implies ||.x - w.|| < r
     proof
          assume
             w in Ball(x,r);
          then w in { y where y is Point of X : ||.x - y.|| < r} by Def5;
          then ex y be Point of X st w = y & ||.x - y.|| < r;
          hence ||.x - w.|| < r;
          end;
        ||.x - w.|| < r implies w in Ball(x,r)
     proof
          assume
             ||.x - w.|| < r;
          then w in {y where y is Point of X : ||.x - y.|| < r};
          hence w in Ball(x,r) by Def5;
          end;
     hence thesis;
end;

theorem Th41:
w in Ball(x,r) iff dist(x,w) < r
proof
     thus w in Ball(x,r) implies dist(x,w) < r
     proof
          assume w in Ball(x,r);
          then ||.x - w.|| < r by Th40;
          hence thesis by CSSPACE:def 16;
     end;
     assume dist(x,w) < r;
     then ||.x - w.|| < r by CSSPACE:def 16;
     hence w in Ball(x,r) by Th40;
end;

theorem
 r > 0 implies x in Ball(x,r)
proof
     assume A1: r > 0;
        dist(x,x) = 0 by CSSPACE:53;
     hence thesis by A1,Th41;
end;

theorem
 y in Ball(x,r) & w in Ball(x,r) implies dist(y,w) < 2 * r
proof
     assume that
A1:   y in Ball(x,r) and
A2:   w in Ball(x,r);
A3:   dist(x,y) < r by A1,Th41;
A4:   dist(x,w) < r by A2,Th41;
A5:   dist(x,y) + dist(x,w) < r + dist(x,w) by A3,REAL_1:53;
        r + dist(x,w) < r + r by A4,REAL_1:53;
then A6:   dist(x,y) + dist(x,w) < 2 * r by A5,AXIOMS:22;
        dist(y,w) <= dist(y,x) + dist(x,w) by CSSPACE:54;
     hence thesis by A6,AXIOMS:22;
end;

theorem
 y in Ball(x,r) implies y - w in Ball(x - w,r)
proof
     assume y in Ball(x,r);
then A1:   dist(x,y) < r by Th41;
        dist(x - w,y - w) = dist(x,y) by CSSPACE:61;
     hence thesis by A1,Th41;
end;

theorem
 y in Ball(x,r) implies (y - x) in Ball (0.(X),r)
proof
     assume y in Ball(x,r);
     then ||.x - y.|| < r by Th40;
     then ||.(-y) + x.|| < r by RLVECT_1:def 11;
     then ||.-(y - x).|| < r by RLVECT_1:47;
     then ||.0'(X) - (y - x).|| < r by RLVECT_1:27;
     hence thesis by Th40;
end;

theorem
 y in Ball(x,r) & r <= q implies y in Ball(x,q)
proof
     assume that
A1:   y in Ball(x,r) and
A2:   r <= q;
        ||.x - y.|| < r by A1,Th40;
     then ||.x - y.|| < q by A2,AXIOMS:22;
     hence thesis by Th40;
end;

theorem Th47:
w in cl_Ball(x,r) iff ||.x - w.|| <= r
proof
     thus w in cl_Ball(x,r) implies ||.x - w.|| <= r
     proof
          assume
             w in cl_Ball(x,r);
          then w in { y where y is Point of X : ||.x - y.|| <= r} by Def6;
          then ex y be Point of X st w = y & ||.x - y.|| <= r;
          hence ||.x - w.|| <= r;
     end;
     assume ||.x - w.|| <= r;
     then w in {y where y is Point of X : ||.x - y.|| <= r};
     hence w in cl_Ball(x,r) by Def6;
end;

theorem Th48:
w in cl_Ball(x,r) iff dist(x,w) <= r
proof
     thus w in cl_Ball(x,r) implies dist(x,w) <= r
     proof
          assume
             w in cl_Ball(x,r);
          then ||.x - w.|| <= r by Th47;
          hence thesis by CSSPACE:def 16;
     end;
     assume dist(x,w) <= r;
     then ||.x - w.|| <= r by CSSPACE:def 16;
     hence w in cl_Ball(x,r) by Th47;
end;

theorem
 r >= 0 implies x in cl_Ball(x,r)
proof
     assume r >= 0;
     then dist(x,x) <= r by CSSPACE:53;
     hence thesis by Th48;
end;

theorem Th50:
y in Ball(x,r) implies y in cl_Ball(x,r)
proof
     assume y in Ball(x,r);
     then ||.x - y.|| <= r by Th40;
     hence thesis by Th47;
end;

theorem Th51:
w in Sphere(x,r) iff ||.x - w.|| = r
proof
     thus w in Sphere(x,r) implies ||.x - w.|| = r
     proof
          assume w in Sphere(x,r);
          then w in { y where y is Point of X : ||.x - y.|| = r} by Def7;
          then ex y be Point of X st w = y & ||.x - y.|| = r;
          hence ||.x - w.|| = r;
     end;
     assume ||.x - w.|| = r;
     then w in {y where y is Point of X : ||.x - y.|| = r};
     hence w in Sphere(x,r) by Def7;
end;

theorem
 w in Sphere(x,r) iff dist(x,w) = r
proof
     thus w in Sphere(x,r) implies dist(x,w) = r
     proof
          assume
             w in Sphere(x,r);
          then ||.x - w.|| = r by Th51;
          hence thesis by CSSPACE:def 16;
     end;
     assume dist(x,w) = r;
     then ||.x - w.|| = r by CSSPACE:def 16;
     hence w in Sphere(x,r) by Th51;
end;

theorem
 y in Sphere(x,r) implies y in cl_Ball(x,r)
proof
     assume y in Sphere(x,r);
     then ||.x - y.|| = r by Th51;
     hence thesis by Th47;
end;

theorem Th54:
Ball(x,r) c= cl_Ball(x,r)
proof
        for y holds y in Ball(x,r) implies y in cl_Ball(x,r) by Th50;
     hence thesis by SUBSET_1:7;
end;

theorem Th55:
Sphere(x,r) c= cl_Ball(x,r)
proof
        now let y; assume y in Sphere(x,r);
     then ||.x - y.|| = r by Th51;
     hence y in cl_Ball(x,r) by Th47;
     end;
     hence thesis by SUBSET_1:7;
end;

theorem
 Ball(x,r) \/ Sphere(x,r) = cl_Ball(x,r)
proof
A1:Ball(x,r) \/ Sphere(x,r) c= cl_Ball(x,r)
   proof
      Ball(x,r) c= cl_Ball(x,r) &
     Sphere(x,r) c= cl_Ball(x,r) by Th54,Th55;
     hence thesis by XBOOLE_1:8;
   end;
    now
     let y;
     assume y in cl_Ball(x,r);
then A2:  ||.x - y.|| <= r by Th47;
      now per cases by A2,REAL_1:def 5;
       case ||.x - y.|| < r;
          hence y in Ball(x,r) by Th40;
       end;
       case ||.x - y.|| = r;
          hence y in Sphere(x,r) by Th51;
       end;
     end;
     hence y in Ball(x,r) \/ Sphere(x,r) by XBOOLE_0:def 2;
   end;
   then cl_Ball(x,r) c= Ball(x,r) \/ Sphere(x,r) by SUBSET_1:7;
   hence thesis by A1,XBOOLE_0:def 10;
end;

begin :: Cauchy sequence and Hilbert space with complex coefficient

deffunc 0'(ComplexUnitarySpace) = 0.$1;

definition let X; let seq;
  attr seq is Cauchy means :Def8:
  for r st r > 0 ex k st for n, m st ( n >= k & m >= k ) holds
   dist((seq.n), (seq.m)) < r;
end;

theorem
 seq is constant implies seq is Cauchy
proof
     assume
A1:     seq is constant;
     let r such that
A2:   r > 0;
     take k = 0;
     let n, m such that
        n >= k & m >= k;
        dist((seq.n), (seq.m)) = dist((seq.n), (seq.n)) by A1,CSSPACE:70
                            .= 0 by CSSPACE:53;
     hence dist((seq.n), (seq.m)) < r by A2;
  end;

theorem
 seq is Cauchy iff for r st r > 0 ex k st for n, m st
 n >= k & m >= k holds ||.(seq.n) - (seq.m).|| < r
proof
     thus seq is Cauchy implies for r st r > 0 ex k st for n, m st
      n >= k & m >= k holds ||.(seq.n) - (seq.m).|| < r
     proof
          assume
A1:          seq is Cauchy;
          let r; assume r > 0;
          then consider l being Nat such that
     A2:   for n, m st ( n >= l & m >= l ) holds
          dist((seq.n), (seq.m)) < r by A1,Def8;
          take k = l;
          let n, m; assume n >= k & m >= k;
          then dist((seq.n), (seq.m)) < r by A2;
          hence thesis by CSSPACE:def 16;
          end;
        ( for r st r > 0 ex k st for n, m st ( n >= k & m >= k ) holds
     ||.(seq.n) - (seq.m).|| < r ) implies seq is Cauchy
     proof
          assume
     A3:   for r st r > 0 ex k st for n, m st ( n >= k & m >= k )
          holds ||.(seq.n) - (seq.m).|| < r;
          let r; assume r > 0;
          then consider l being Nat such that
     A4:   for n, m st ( n >= l & m >= l ) holds
          ||.(seq.n) - (seq.m).|| < r by A3;
          take k = l;
          let n, m; assume n >= k & m >= k;
          then ||.(seq.n) - (seq.m).|| < r by A4;
          hence dist((seq.n), (seq.m)) < r by CSSPACE:def 16;
     end;
     hence thesis;
end;

theorem
 seq1 is Cauchy & seq2 is Cauchy implies
  seq1 + seq2 is Cauchy
proof
     assume that
 A1:  seq1 is Cauchy and
 A2:  seq2 is Cauchy;
      let r; assume r > 0;
 then A3:  r/2 > 0 by SEQ_2:3;
      then consider m1 be Nat such that
 A4:  for n, m st n >= m1 & m >= m1 holds
      dist((seq1.n), (seq1.m)) < r/2 by A1,Def8;
      consider m2 be Nat such that
 A5:  for n, m st ( n >= m2 & m >= m2 ) holds
      dist((seq2.n), (seq2.m)) < r/2 by A2,A3,Def8;
      take k = m1 + m2;
      let n, m such that
  A6:  n >= k & m >= k;
         m1 + m2 >= m1 by NAT_1:37;
      then n >= m1 & m >= m1 by A6,AXIOMS:22;
  then A7:  dist((seq1.n), (seq1.m)) < r/2 by A4;
         k >= m2 by NAT_1:37;
      then n >= m2 & m >= m2 by A6,AXIOMS:22;
      then dist((seq2.n), (seq2.m)) < r/2 by A5;
      then A8: dist((seq1.n), (seq1.m)) + dist((seq2.n), (seq2.m)) < r/2 + r/2
      by A7,REAL_1:67;
         dist((seq1 + seq2).n, (seq1 + seq2).m)
    = dist((seq1.n) + (seq2.n), (seq1 + seq2).m) by CLVECT_1:def 11
   .= dist((seq1.n) + (seq2.n), (seq1.m) + (seq2.m)) by CLVECT_1:def 11;
      then dist((seq1 + seq2).n, (seq1 + seq2).m) <=
      dist((seq1.n), (seq1.m)) + dist((seq2.n), (seq2.m)) by CSSPACE:59;
      hence dist((seq1 + seq2).n, (seq1 + seq2).m) < r by A8,AXIOMS:22;
  end;

theorem
 seq1 is Cauchy & seq2 is Cauchy implies
  seq1 - seq2 is Cauchy
proof
     assume that
 A1:  seq1 is Cauchy and
 A2:  seq2 is Cauchy;
      let r; assume r > 0;
 then A3:  r/2 > 0 by SEQ_2:3;
      then consider m1 be Nat such that
 A4:  for n, m st ( n >= m1 & m >= m1 ) holds
      dist((seq1.n), (seq1.m)) < r/2 by A1,Def8;
      consider m2 be Nat such that
 A5:  for n, m st ( n >= m2 & m >= m2 ) holds
      dist((seq2.n), (seq2.m)) < r/2 by A2,A3,Def8;
      take k = m1 + m2;
      let n, m such that
  A6:  n >= k & m >= k;
         m1 + m2 >= m1 by NAT_1:37;
      then n >= m1 & m >= m1 by A6,AXIOMS:22;
  then A7:  dist((seq1.n), (seq1.m)) < r/2 by A4;
         k >= m2 by NAT_1:37;
      then n >= m2 & m >= m2 by A6,AXIOMS:22;
      then dist((seq2.n), (seq2.m)) < r/2 by A5;
      then A8: dist((seq1.n), (seq1.m)) + dist((seq2.n), (seq2.m)) < r/2 + r/2
      by A7,REAL_1:67;
         dist((seq1 - seq2).n, (seq1 - seq2).m)
    = dist((seq1.n) - (seq2.n), (seq1 - seq2).m) by CLVECT_1:def 12
   .= dist((seq1.n) - (seq2.n), (seq1.m) - (seq2.m)) by CLVECT_1:def 12;
      then dist((seq1 - seq2).n, (seq1 - seq2).m) <=
      dist((seq1.n), (seq1.m)) + dist((seq2.n), (seq2.m)) by CSSPACE:60;
      hence dist((seq1 - seq2).n, (seq1 - seq2).m) < r by A8,AXIOMS:22;
  end;

theorem Th61:
seq is Cauchy implies z * seq is Cauchy
proof
     assume
A1:         seq is Cauchy;
     A2:    now assume
     A3:   z = 0;
           let r; assume r > 0;
           then consider m1 be Nat such that
     A4:   for n, m st ( n >= m1 & m >= m1 ) holds
           dist((seq.n), (seq.m)) < r by A1,Def8;
           take k = m1;
           let n, m; assume n >= k & m >= k;
     then A5:   dist((seq.n), (seq.m)) < r by A4;
           dist(z * (seq.n), z * (seq.m))
         = dist(0'(X), 0c * (seq.m)) by A3,CLVECT_1:2
        .= dist(0'(X), 0'(X)) by CLVECT_1:2
        .= 0 by CSSPACE:53;
           then dist(z * (seq.n), z * (seq.m)) < r by A5,CSSPACE:56;
           then dist((z * seq).n, z * (seq.m)) < r by CLVECT_1:def 14;
           hence dist((z * seq).n, (z * seq).m) < r by CLVECT_1:def 14;
           end;
              now assume
A6:        z <> 0;
     then A7:   |.z.| > 0 by COMPLEX1:133;
           let r such that
     A8:   r > 0;
     A9:   |.z.| <> 0 by A6,COMPLEX1:133;
              0/|.z.| = 0;
           then r/|.z.| > 0 by A7,A8,REAL_1:73;
           then consider m1 be Nat such that
     A10:   for n, m st ( n >= m1 & m >= m1 ) holds
           dist((seq.n), (seq.m)) < r/|.z.| by A1,Def8;
           take k = m1;
           let n, m; assume n >= k & m >= k;
     then A11:   dist((seq.n), (seq.m)) < r/|.z.| by A10;
     A12:   dist(z * (seq.n), z * (seq.m))
         = ||.(z * (seq.n)) - (z * (seq.m)).|| by CSSPACE:def 16
        .= ||.z * ((seq.n) - (seq.m)).|| by CLVECT_1:10
        .= |.z.| * ||.(seq.n) - (seq.m).|| by CSSPACE:45
        .= |.z.| * dist((seq.n), (seq.m)) by CSSPACE:def 16;
              |.z.| * (r/|.z.|) = |.z.| * (|.z.|" * r) by XCMPLX_0:def 9
                            .= |.z.| *|.z.|" * r
                            .= 1 * r by A9,XCMPLX_0:def 7
                            .= r;
           then dist((z * (seq.n)), (z * (seq.m))) < r
           by A7,A11,A12,REAL_1:70;
           then dist((z * seq).n, (z * (seq.m))) < r by CLVECT_1:def 14;
           hence dist((z * seq).n, (z * seq).m) < r by CLVECT_1:def 14;
           end;
     hence thesis by A2,Def8;
end;

theorem
 seq is Cauchy implies - seq is Cauchy
proof
     assume seq is Cauchy;
     then (-1r) * seq is Cauchy by Th61;
     hence thesis by CSSPACE:77;
end;

theorem Th63:
seq is Cauchy implies seq + x is Cauchy
proof
     assume
A1:         seq is Cauchy;
           let r; assume r > 0;
           then consider m1 be Nat such that
     A2:    for n, m st ( n >= m1 & m >= m1 ) holds
           dist((seq.n), (seq.m)) < r by A1,Def8;
           take k = m1;
           let n, m; assume n >= k & m >= k;
     then A3:    dist((seq.n), (seq.m)) < r by A2;
              dist((seq.n) + x, (seq.m) + x) <= dist((seq.n), (seq.m)) +
           dist(x, x) by CSSPACE:59;
           then dist((seq.n) + x, (seq.m) + x) <=
           dist((seq.n), (seq.m)) + 0 by CSSPACE:53;
           then dist((seq.n) + x, (seq.m) + x) < r by A3,AXIOMS:22;
           then dist((seq + x).n, (seq.m) + x) < r by CSSPACE:def 18;
           hence dist((seq + x).n, (seq + x).m) < r by CSSPACE:def 18;
end;

theorem
 seq is Cauchy implies seq - x is Cauchy
proof
     assume seq is Cauchy;
     then seq + (-x) is Cauchy by Th63;
     hence thesis by CSSPACE:78;
end;

theorem
 seq is convergent implies seq is Cauchy
proof
     assume seq is convergent;
     then consider h be Point of X such that
A1:   for r st r > 0 ex k st for n st n >= k holds
     dist((seq.n), h) < r by Def1;
     let r; assume r > 0;
     then r/2 > 0 by SEQ_2:3;
     then consider m1 be Nat such that
A2:   for n st n >= m1 holds dist((seq.n), h) < r/2 by A1;
     take k = m1;
     let n, m; assume
A3:   n >= k & m >= k;
then A4:   dist((seq.n), h) < r/2 by A2;
     A5: dist((seq.m), h) < r/2 by A2,A3;
A6:   dist((seq.n), (seq.m)) <= dist((seq.n), h) + dist(h, (seq.m))
     by CSSPACE:54;
        dist((seq.n), h) + dist(h, (seq.m)) < r/2 + r/2
     by A4,A5,REAL_1:67;
     hence dist((seq.n), (seq.m)) < r by A6,AXIOMS:22;
end;

definition let X; let seq1, seq2;
pred seq1 is_compared_to seq2 means :Def9:
  for r st r > 0 ex m st for n st n >= m holds
    dist(seq1.n, seq2.n) < r;
end;

theorem Th66:
seq is_compared_to seq
proof
     let r such that
A1:   r > 0;
     take m = 0;
     let n such that
        n >= m;
     thus dist((seq.n), (seq.n)) < r by A1,CSSPACE:53;
  end;

theorem Th67:
seq1 is_compared_to seq2 implies seq2 is_compared_to seq1
proof
     assume
A1:     seq1 is_compared_to seq2;
     let r; assume r > 0;
     then consider k such that
A2:   for n st n >= k holds
     dist((seq1.n), (seq2.n)) < r by A1,Def9;
     take m = k;
     let n; assume n >= m;
     hence thesis by A2;
end;

definition let X; let seq1, seq2;
redefine pred seq1 is_compared_to seq2;
  reflexivity by Th66;
  symmetry by Th67;
end;

theorem
 seq1 is_compared_to seq2 & seq2 is_compared_to seq3 implies
  seq1 is_compared_to seq3
proof
     assume that
A1:  seq1 is_compared_to seq2 and
A2:  seq2 is_compared_to seq3;
     let r; assume r > 0;
then A3:  r/2 > 0 by SEQ_2:3;
     then consider m1 be Nat such that
A4:  for n st n >= m1 holds
     dist((seq1.n), (seq2.n)) < r/2 by A1,Def9;
     consider m2 be Nat such that
A5:  for n st n >= m2 holds
     dist((seq2.n), (seq3.n)) < r/2 by A2,A3,Def9;
     take m = m1 + m2;
     let n such that
A6:   n >= m;
        m1 + m2 >= m1 by NAT_1:37;
     then n >= m1 by A6,AXIOMS:22;
then A7:   dist((seq1.n), (seq2.n)) < r/2 by A4;
        m >= m2 by NAT_1:37;
     then n >= m2 by A6,AXIOMS:22;
     then dist((seq2.n), (seq3.n)) < r/2 by A5;
     then A8: dist((seq1.n), (seq2.n)) + dist((seq2.n), (seq3.n)) < r/2 + r/2
     by A7,REAL_1:67;
        dist((seq1.n), (seq3.n)) <=
     dist((seq1.n), (seq2.n)) + dist((seq2.n), (seq3.n))
     by CSSPACE:54;
     hence dist((seq1.n), (seq3.n)) < r by A8,AXIOMS:22;
end;

theorem
 seq1 is_compared_to seq2 iff for r st r > 0 ex m st
  for n st n >= m holds ||.(seq1.n) - (seq2.n).|| < r
proof
     thus
        seq1 is_compared_to seq2 implies for r st r > 0 ex m st
     for n st n >= m holds ||.(seq1.n) - (seq2.n).|| < r
     proof
          assume
A1:          seq1 is_compared_to seq2;
          let r; assume r > 0;
          then consider m1 be Nat such that
     A2:   for n st n >= m1 holds
          dist((seq1.n), (seq2.n)) < r by A1,Def9;
          take m = m1;
          let n; assume n >= m;
          then dist((seq1.n), (seq2.n)) < r by A2;
          hence thesis by CSSPACE:def 16;
          end;
        ( for r st r > 0 ex m st for n st n >= m holds
     ||.(seq1.n) - (seq2.n).|| < r ) implies seq1 is_compared_to seq2
     proof
          assume
     A3:   for r st r > 0 ex m st for n st n >= m holds
          ||.(seq1.n) - (seq2.n).|| < r;
          let r; assume r > 0;
          then consider m1 be Nat such that
     A4:   for n st n >= m1 holds
          ||.(seq1.n) - (seq2.n).|| < r by A3;
          take m = m1;
          let n; assume n >= m;
          then ||.(seq1.n) - (seq2.n).|| < r by A4;
          hence thesis by CSSPACE:def 16;
          end;
     hence thesis;
end;

theorem
 ( ex k st for n st n >= k holds seq1.n = seq2.n ) implies
  seq1 is_compared_to seq2
proof
     assume
A1:   ex k st for n st n >= k holds seq1.n = seq2.n;
     let r such that
A2:   r > 0;
     consider m such that
A3:   for n st n >= m holds seq1.n = seq2.n by A1;
     take k = m;
     let n; assume n >= k;
     then dist((seq1.n), (seq2.n)) = dist((seq1.n), (seq1.n)) by A3
                              .= 0 by CSSPACE:53;
     hence dist((seq1.n), (seq2.n)) < r by A2;
end;

theorem
 seq1 is Cauchy & seq1 is_compared_to seq2 implies
  seq2 is Cauchy
proof
     assume that
A1:   seq1 is Cauchy and
A2:   seq1 is_compared_to seq2;
     let r; assume r > 0;
then A3:   r/3 > 0 by SEQ_4:4;
     then consider m1 be Nat such that
A4:   for n, m st ( n >= m1 & m >= m1 ) holds
     dist((seq1.n), (seq1.m)) < r/3 by A1,Def8;
     consider m2 be Nat such that
A5:   for n st n >= m2 holds
     dist((seq1.n), (seq2.n)) < r/3 by A2,A3,Def9;
     take k = m1 + m2;
     let n, m such that
A6:   n >= k & m >= k;
        m1 + m2 >= m1 by NAT_1:37;
     then n >= m1 & m >= m1 by A6,AXIOMS:22;
then A7:   dist((seq1.n), (seq1.m)) < r/3 by A4;
     A8:   k >= m2 by NAT_1:37;
     then n >= m2 by A6,AXIOMS:22;
     then A9: dist((seq1.n), (seq2.n)) < r/3 by A5;
        m >= m2 by A6,A8,AXIOMS:22;
then A10:   dist((seq1.m), (seq2.m)) < r/3 by A5;
A11:   dist((seq2.n), (seq1.n)) + dist((seq1.n), (seq1.m)) < r/3 + r/3
     by A7,A9,REAL_1:67;
        dist((seq2.n), (seq1.m)) <=
     dist((seq2.n), (seq1.n)) + dist((seq1.n), (seq1.m))
     by CSSPACE:54;
     then dist((seq2.n), (seq1.m)) < r/3 + r/3 by A11,AXIOMS:22;
     then A12: dist((seq2.n), (seq1.m)) + dist((seq1.m), (seq2.m)) <
     r/3 + r/3 + r/3 by A10,REAL_1:67;
        dist((seq2.n), (seq2.m)) <= dist((seq2.n), (seq1.m)) +
     dist((seq1.m), (seq2.m)) by CSSPACE:54;
     hence dist((seq2.n), (seq2.m)) < r by A12,AXIOMS:22;
end;

theorem
 seq1 is convergent & seq1 is_compared_to seq2 implies
  seq2 is convergent
proof
     assume that
A1:   seq1 is convergent and
A2:   seq1 is_compared_to seq2;
        now let r; assume r > 0;
then A3:   r/2 > 0 by SEQ_2:3;
     then consider m1 be Nat such that
A4:   for n st n >= m1 holds
     dist((seq1.n), (lim seq1)) < r/2 by A1,Def2;
     consider m2 be Nat such that
A5:   for n st n >= m2 holds
     dist((seq1.n), (seq2.n)) < r/2 by A2,A3,Def9;
     take m = m1 + m2;
     let n such that
A6:   n >= m;
        m1 + m2 >= m1 by NAT_1:37;
     then n >= m1 by A6,AXIOMS:22;
then A7:   dist((seq1.n), (lim seq1)) < r/2 by A4;
        m >= m2 by NAT_1:37;
     then n >= m2 by A6,AXIOMS:22;
     then dist((seq1.n), (seq2.n)) < r/2 by A5;
     then A8: dist((seq2.n), (seq1.n)) + dist((seq1.n), (lim seq1)) <
     r/2 + r/2 by A7,REAL_1:67;
        dist((seq2.n), (lim seq1)) <=
     dist((seq2.n), (seq1.n)) + dist((seq1.n), (lim seq1))
     by CSSPACE:54;
     hence dist((seq2.n), (lim seq1)) < r by A8,AXIOMS:22;
     end;
     hence thesis by Def1;
end;

theorem
 seq1 is convergent & lim seq1 = g & seq1 is_compared_to seq2 implies
  seq2 is convergent & lim seq2 = g
proof
     assume that
A1:  seq1 is convergent and
A2:  lim seq1 = g and
A3:   seq1 is_compared_to seq2;
A4:   now let r; assume r > 0;
then A5:   r/2 > 0 by SEQ_2:3;
     then consider m1 be Nat such that
A6:   for n st n >= m1 holds
     dist((seq1.n), g) < r/2 by A1,A2,Def2;
     consider m2 be Nat such that
A7:   for n st n >= m2 holds
     dist((seq1.n), (seq2.n)) < r/2 by A3,A5,Def9;
     take m = m1 + m2;
     let n such that
A8:   n >= m;
        m1 + m2 >= m1 by NAT_1:37;
     then n >= m1 by A8,AXIOMS:22;
then A9:   dist((seq1.n), g) < r/2 by A6;
        m >= m2 by NAT_1:37;
     then n >= m2 by A8,AXIOMS:22;
     then dist((seq1.n), (seq2.n)) < r/2 by A7;
     then A10: dist((seq2.n), (seq1.n)) + dist((seq1.n), g) <
     r/2 + r/2 by A9,REAL_1:67;
        dist((seq2.n), g) <=
     dist((seq2.n), (seq1.n)) + dist((seq1.n), g)
     by CSSPACE:54;
     hence dist((seq2.n), g) < r by A10,AXIOMS:22;
     end;
     then seq2 is convergent by Def1;
     hence thesis by A4,Def2;
end;

definition let X; let seq;
attr seq is bounded means :Def10:
  ex M st M > 0 & for n holds ||.seq.n.|| <= M;
end;

theorem Th74:
seq1 is bounded & seq2 is bounded implies seq1 + seq2 is bounded
proof
     assume that
A1:  seq1 is bounded and
A2:  seq2 is bounded;
     consider M1 such that
A3:  M1 > 0 and
A4:  for n holds ||.seq1.n.|| <= M1 by A1,Def10;
     consider M2 such that
A5:  M2 > 0 and
A6:  for n holds ||.seq2.n.|| <= M2 by A2,Def10;
A7:   M1 + M2 > 0 + 0 by A3,A5,REAL_1:67;
        now let n;
A8:  ||.seq1.n.|| <= M1 by A4;
        ||.seq2.n.|| <= M2 by A6;
then A9:   ||.seq1.n.|| + ||.seq2.n.|| <= M1 + M2 by A8,REAL_1:55;
        ||.(seq1 + seq2).n.|| = ||.seq1.n + seq2.n.|| by CLVECT_1:def 11;
     then ||.(seq1 + seq2).n.|| <= ||.seq1.n.|| + ||.seq2.n.|| by CSSPACE:48;
     hence ||.(seq1 + seq2).n.|| <= M1 + M2 by A9,AXIOMS:22;
     end;
     hence thesis by A7,Def10;
end;

theorem Th75:
seq is bounded implies -seq is bounded
proof
     assume seq is bounded;
    then consider M such that
A1:  M > 0 and
A2:  for n holds ||.seq.n.|| <= M by Def10;
       now let n;
       ||.(- seq).n.|| = ||.- (seq.n).|| by CSSPACE:def 17
                 .= ||.seq.n.|| by CSSPACE:49;
    hence ||.(- seq).n.|| <= M by A2;
    end;
    hence thesis by A1,Def10;
end;

theorem
 seq1 is bounded & seq2 is bounded implies seq1 - seq2 is bounded
proof
     assume that
A1:   seq1 is bounded and
A2:   seq2 is bounded;
A3:   - seq2 is bounded by A2,Th75;
        seq1 - seq2 = seq1 + (- seq2) by CSSPACE:71;
     hence thesis by A1,A3,Th74;
end;

theorem
 seq is bounded implies z * seq is bounded
proof
     assume seq is bounded;
    then consider M such that
A1:  M > 0 and
A2:  for n holds ||.seq.n.|| <= M by Def10;
A3:  z = 0 implies z * seq is bounded
    proof
    assume
A4: z = 0;
       now let n;
       ||.(z * seq).n.|| = ||.0c * (seq.n).|| by A4,CLVECT_1:def 14
                   .= ||.0'(X).|| by CLVECT_1:2
                   .= 0 by CSSPACE:44;
    hence ||.(z * seq).n.|| <= M by A1;
    end;
    hence thesis by A1,Def10;
    end;
       z <> 0 implies z * seq is bounded
    proof
    assume z <> 0;
    then |.z.| > 0 by COMPLEX1:133;
then A5:  |.z.| * M > 0 by A1,REAL_2:122;
       now let n;
A6:  ||.seq.n.|| <= M by A2;
A7:  ||.(z * seq).n.|| = ||.z * (seq.n).|| by CLVECT_1:def 14
                   .= |.z.| * ||.seq.n.|| by CSSPACE:45;
       |.z.| >= 0 by COMPLEX1:132;
    hence ||.(z * seq).n.|| <= |.z.| * M by A6,A7,AXIOMS:25;
    end;
    hence thesis by A5,Def10;
    end;
    hence thesis by A3;
end;

theorem
 seq is constant implies seq is bounded
proof
     assume
        seq is constant;
     then consider x such that
A1:   for n holds seq.n = x by NORMSP_1:def 4;
A2:   x = 0'(X) implies seq is bounded
     proof
     assume
A3:  x = 0'(X);
     consider M being real number such that
A4:  M > 0 by REAL_1:76;
     reconsider M as Real by XREAL_0:def 1;
        now let n;
        seq.n = 0'(X) by A1,A3;
     hence ||.seq.n.|| <= M by A4,CSSPACE:44;
     end;
     hence thesis by A4,Def10;
     end;
        x <> 0'(X) implies seq is bounded
     proof
     assume x <> 0'(X);
     then A5: ||.x.|| >= 0 & ||.x.|| <> 0 by CSSPACE:44,46;
     consider M being real number such that
A6:  ||.x.|| < M by REAL_1:76;
     reconsider M as Real by XREAL_0:def 1;
        for n holds ||.seq.n.|| <= M by A1,A6;
     hence thesis by A5,A6,Def10;
     end;
     hence thesis by A2;
end;

theorem Th79:
for m ex M st ( M > 0 & for n st n <= m holds ||.seq.n.|| < M )
proof
   defpred _P[Nat] means
   ex M st ( M > 0 & for n st n <= $1 holds ||.seq.n.|| < M );
A1: _P[0] proof take M = ||.(seq.0).|| + 1;
        ||.(seq.0).|| >= 0 by CSSPACE:46;
     then ||.(seq.0).|| + 1 > 0 + 0 by REAL_1:67;
     hence M > 0;
     let n such that
A2:   n <= 0;
       n = 0 by A2,NAT_1:18;
     then ||.seq.n.|| + 0 < M by REAL_1:67;
     hence ||.seq.n.|| < M;
     end;
A3:  for m st _P[m] holds _P[m+1] proof let m;
     given M1 such that
A4:   M1 > 0 and
A5:   for n st n <= m holds ||.seq.n.|| < M1;
A6:   now assume
A7:   ||.seq.(m+1).|| <= M1;
     take M = M1 + 1;
        M1 + 1 > 0 + 0 by A4,REAL_1:67;
     hence M > 0;
     let n such that
A8:   n <= m + 1;
A9:   now assume m >= n;
then A10:   ||.seq.n.|| < M1 by A5;
        M > M1 + 0 by REAL_1:67;
     hence ||.seq.n.|| < M by A10,AXIOMS:22;
     end;
        now assume A11: n = m + 1;
        M > M1 + 0 by REAL_1:67;
     hence ||.seq.n.|| < M by A7,A11,AXIOMS:22;
     end;
     hence ||.seq.n.|| < M by A8,A9,NAT_1:26;
     end;
        now assume
A12:   ||.seq.(m+1).|| >= M1;
     take M = ||.seq.(m+1).|| + 1;
        ||.seq.(m+1).|| >= 0 by CSSPACE:46;
     then M > 0 + 0 by REAL_1:67;
     hence M > 0;
     let n such that
A13:   n <= m + 1;
A14:   now assume m >= n;
     then ||.seq.n.|| < M1 by A5;
     then ||.seq.n.|| < ||.seq.(m+1).|| by A12,AXIOMS:22;
     then ||.seq.n.|| + 0 < M by REAL_1:67;
     hence ||.seq.n.|| < M;
     end;
        now assume n = m + 1;
     then ||.seq.n.|| + 0 < M by REAL_1:67;
     hence ||.seq.n.|| < M;
     end;
     hence ||.seq.n.|| < M by A13,A14,NAT_1:26;
     end;
     hence ex M st ( M > 0 & for n st n <= m + 1 holds ||.seq.n.|| < M )
     by A6;
     end;
     thus for m holds _P[m] from NAT_1:sch 1(A1,A3);
end;

theorem Th80:
seq is convergent implies seq is bounded
proof
     assume seq is convergent;
     then consider g such that
A1:   for r st r > 0 ex m st for n st n >= m
     holds ||.seq.n - g.|| < r by Th9;
     consider m1 be Nat such that
A2:   for n st n >= m1 holds
     ||.seq.n - g.|| < 1 by A1;
A3:   now take p = ||.g.|| + 1;
        ||.g.|| >= 0 by CSSPACE:46;
     then 0 + 0 < p by REAL_1:67;
     hence p > 0;
     let n; assume n >= m1;
then A4:   ||.seq.n - g.|| < 1 by A2;
        ||.seq.n.|| - ||.g.|| <= ||.seq.n - g.|| by CSSPACE:50;
     then ||.seq.n.|| - ||.g.|| < 1 by A4,AXIOMS:22;
     hence ||.seq.n.|| < p by REAL_1:84;
     end;
        now consider M1 such that
A5:   M1 > 0 and
A6:   for n st n >= m1 holds ||.seq.n.|| < M1 by A3;
     consider M2 such that
A7:   M2 > 0 and
A8:   for n st n <= m1 holds ||.seq.n.|| < M2 by Th79;
     take M = M1 + M2;
        M > 0 + 0 by A5,A7,REAL_1:67;
     hence M > 0;
     A9: M > M1 + 0 by A7,REAL_1:67;
     A10: M > 0 + M2 by A5,REAL_1:67;
     let n;
A11:   now assume n >= m1;
     then ||.seq.n.|| < M1 by A6;
     hence ||.seq.n.|| <= M by A9,AXIOMS:22;
     end;
        now assume n <= m1;
     then ||.seq.n.|| < M2 by A8;
     hence ||.seq.n.|| <= M by A10,AXIOMS:22;
     end;
     hence ||.seq.n.|| <= M by A11;
     end;
     hence thesis by Def10;
  end;

theorem
 seq1 is bounded & seq1 is_compared_to seq2 implies seq2 is bounded
proof
     assume that
A1:   seq1 is bounded and
A2:   seq1 is_compared_to seq2;
     consider p such that
A3:   p > 0 and
A4:   for n holds ||.seq1.n.|| <= p by A1,Def10;
     consider m1 be Nat such that
A5:   for n st n >= m1 holds
     dist((seq1.n), (seq2.n)) < 1 by A2,Def9;
A6:     p + 1 > 0 + 0 by A3,REAL_1:67;
A7:   ex M st ( M > 0 & for n st n >= m1 holds ||.seq2.n.|| < M )
     proof
     take M = p + 1;
        now let n; assume n >= m1;
     then dist((seq1.n), (seq2.n)) < 1 by A5;
then A8:   ||.seq2.n - seq1.n.|| < 1 by CSSPACE:def 16;
        ||.seq2.n.|| - ||.seq1.n.|| <= ||.seq2.n - seq1.n.|| by CSSPACE:50;
     then ||.seq2.n.|| - ||.seq1.n.|| < 1 by A8,AXIOMS:22;
then A9:   ||.seq2.n.|| < ||.seq1.n.|| + 1 by REAL_1:84;
        ||.seq1.n.|| <= p by A4;
     then ||.seq1.n.|| + 1 <= p + 1 by AXIOMS:24;
     hence ||.seq2.n.|| < M by A9,AXIOMS:22;
     end;
     hence thesis by A6;
     end;
        now consider M1 such that
A10:   M1 > 0 and
A11:   for n st n >= m1 holds ||.seq2.n.|| < M1 by A7;
     consider M2 such that
A12:   M2 > 0 and
A13:   for n st n <= m1 holds ||.seq2.n.|| < M2 by Th79;
     take M = M1 + M2;
        M > 0 + 0 by A10,A12,REAL_1:67;
     hence M > 0;
     A14: M > M1 + 0 by A12,REAL_1:67;
     A15: M > 0 + M2 by A10,REAL_1:67;
     let n;
A16:   now assume n >= m1;
     then ||.seq2.n.|| < M1 by A11;
     hence ||.seq2.n.|| <= M by A14,AXIOMS:22;
     end;
        now assume n <= m1;
     then ||.seq2.n.|| < M2 by A13;
     hence ||.seq2.n.|| <= M by A15,AXIOMS:22;
     end;
     hence ||.seq2.n.|| <= M by A16;
     end;
     hence thesis by Def10;
  end;

definition let X, Nseq, seq;
  redefine func seq * Nseq -> sequence of X;
coherence
proof
A1:dom Nseq = NAT by FUNCT_2:def 1;
A2:rng seq c= the carrier of X by RELSET_1:12;
    rng Nseq c= NAT by SEQM_3:def 8;
   then rng Nseq c= dom seq by FUNCT_2:def 1;
then A3:dom (seq * Nseq) = NAT by A1,RELAT_1:46;

    rng (seq * Nseq) c= rng seq by RELAT_1:45;
             then rng (seq * Nseq) c= the carrier of X
              by A2,XBOOLE_1:1;
   hence thesis by A3,FUNCT_2:def 1,RELSET_1:11;
end;
end;

theorem Th82:
for X being ComplexUnitarySpace, s being sequence of X,
 N being increasing Seq_of_Nat
  for n being Nat holds (s * N).n=s.(N.n)
proof
  let X be ComplexUnitarySpace,
      s be sequence of X,
      N be increasing Seq_of_Nat;
  let n be Nat;
     dom (s * N) = NAT by FUNCT_2:def 1;
  hence (s * N).n = s.(N.n) by FUNCT_1:22;
end;

theorem
 seq is subsequence of seq
proof
  reconsider Rseq = incl NAT as Real_Sequence;
    now let n;
      Rseq.n = n & Rseq.(n+1) = n+1 by FUNCT_1:35;
     hence Rseq.n < Rseq.(n+1) by NAT_1:38;
   end;
then A2:Rseq is increasing by SEQM_3:def 11;
    for n holds Rseq.n is Nat by FUNCT_1:35;
   then reconsider Rseq as increasing Seq_of_Nat by A2,SEQM_3:29;
   set Nseq = Rseq;
A3:now let n;
     thus (seq * Nseq).n = seq.(Nseq.n) by Th82
                        .= seq.n by FUNCT_1:35;
   end;
    ex Nseq being increasing Seq_of_Nat st
    seq = seq * Nseq
   proof
     take Nseq;
     thus thesis by A3,FUNCT_2:113;
   end;
   hence thesis by BHSP_3:def 4;
end;

theorem
 seq1 is subsequence of seq2 & seq2 is subsequence of seq3 implies
  seq1 is subsequence of seq3
proof
     assume that
A1:  seq1 is subsequence of seq2 and
A2:  seq2 is subsequence of seq3;
     consider Nseq1 be increasing Seq_of_Nat such that
A3:  seq1 = seq2 * Nseq1 by A1,BHSP_3:def 4;
     consider Nseq2 be increasing Seq_of_Nat such that
A4:  seq2 = seq3 * Nseq2 by A2,BHSP_3:def 4;
      ex Nseq being increasing Seq_of_Nat st
      seq1 = seq3 * Nseq
     proof
       take Nseq = Nseq2 * Nseq1;
       thus seq1 = seq3 * Nseq by A3,A4,RELAT_1:55;
     end;
     hence thesis by BHSP_3:def 4;
end;

theorem Th85:
seq is constant & seq1 is subsequence of seq implies seq1 is constant
proof
     assume that
A1:  seq is constant and
A2:  seq1 is subsequence of seq;
     consider Nseq such that
A3:   seq1 = seq * Nseq by A2,BHSP_3:def 4;
        now let n;
        seq.(Nseq.n) = seq.(Nseq.(n + 1)) by A1,CSSPACE:70;
     then (seq * Nseq).(n + 1) = seq.(Nseq.n) by Th82;
     hence seq1.n = seq1.(n+1) by A3,Th82;
     end;
     hence thesis by CSSPACE:68;
end;

theorem
 seq is constant & seq1 is subsequence of seq implies seq = seq1
proof
     assume that
A1:  seq is constant and
A2:  seq1 is subsequence of seq;
     consider Nseq such that
A3:   seq1 = seq * Nseq by A2,BHSP_3:def 4;
        now let n;
     thus seq1.n = seq.(Nseq.n) by A3,Th82
                .= seq.n by A1,CSSPACE:70;
     end;
     hence thesis by FUNCT_2:113;
  end;

theorem Th87:
seq is bounded & seq1 is subsequence of seq implies seq1 is bounded
proof
     assume that
A1:  seq is bounded and
A2:  seq1 is subsequence of seq;
     consider M1 such that
A3:  M1 > 0 and
A4:  for n holds ||.seq.n.|| <= M1 by A1,Def10;
     consider Nseq such that
A5:   seq1 = seq * Nseq by A2,BHSP_3:def 4;
     take M = M1;
        now let n;
        seq1.n = seq.(Nseq.n) by A5,Th82;
     hence ||.seq1.n.|| <= M by A4;
     end;
     hence thesis by A3;
end;

theorem Th88:
seq is convergent & seq1 is subsequence of seq implies seq1 is convergent
proof
     assume that
A1:   seq is convergent and
A2:   seq1 is subsequence of seq;
     consider g1 such that
A3:   for r st r > 0 ex m st for n st n >= m holds
     ||.(seq.n) - g1.|| < r by A1,Th9;
     consider Nseq such that
A4:   seq1 = seq * Nseq by A2,BHSP_3:def 4;
        now let r; assume r > 0;
     then consider m1 be Nat such that
A5:   for n st n >= m1 holds ||.(seq.n) - g1.|| < r by A3;
     take m = m1;
     let n such that
A6:   n >= m;
        Nseq.n >= n by SEQM_3:33;
then A7:   Nseq.n >= m by A6,AXIOMS:22;
        seq1.n = seq.(Nseq.n) by A4,Th82;
     hence ||.(seq1.n) - g1.|| < r by A5,A7;
     end;
     hence thesis by Th9;
end;

theorem Th89:
seq1 is subsequence of seq & seq is convergent implies lim seq1=lim seq
proof
     assume that
A1:   seq1 is subsequence of seq and
A2:   seq is convergent;
A3:   seq1 is convergent by A1,A2,Th88;
     consider Nseq such that
A4:   seq1 = seq * Nseq by A1,BHSP_3:def 4;
        now let r; assume r > 0;
     then consider m1 be Nat such that
A5:   for n st n >= m1 holds
     dist((seq.n), (lim seq)) < r by A2,Def2;
     take m = m1;
     let n such that
A6:   n >= m;
        Nseq.n >= n by SEQM_3:33;
then A7:   Nseq.n >= m by A6,AXIOMS:22;
        seq1.n = seq.(Nseq.n) by A4,Th82;
     hence dist((seq1.n), (lim seq)) < r by A5,A7;
     end;
    hence thesis by A3,Def2;
end;

theorem Th90:
seq is Cauchy & seq1 is subsequence of seq implies
  seq1 is Cauchy
proof
     assume that
A1:   seq is Cauchy and
A2:   seq1 is subsequence of seq;
     consider Nseq such that
A3:   seq1 = seq * Nseq by A2,BHSP_3:def 4;
        now let r; assume r > 0;
     then consider l be Nat such that
A4:   for n, m st ( n >= l & m >= l )
     holds dist((seq.n), (seq.m)) < r by A1,Def8;
     take k = l;
     let n, m such that
A5:   n >= k & m >= k;
        Nseq.n >= n & Nseq.m >= m by SEQM_3:33;
then A6:   Nseq.n >= k & Nseq.m >= k by A5,AXIOMS:22;
        seq1.n = seq.(Nseq.n) &
     seq1.m = seq.(Nseq.m) by A3,Th82;
     hence dist((seq1.n), (seq1.m)) < r by A4,A6;
     end;
     hence thesis by Def8;
end;

definition let X; let seq; let k;
func seq ^\k -> sequence of X means :Def11:
 for n holds it.n=seq.(n + k);
existence proof deffunc _F(Nat) = seq.($1 + k);
  thus ex IT being sequence of X st for n holds IT.n=_F(n)
    from FUNCT_2:sch 4;
end;
uniqueness
          proof
              let seq1, seq2;
              assume that
          A1: for n holds seq1.n = seq.(n + k) and
          A2: for n holds seq2.n = seq.(n + k);
                 now let n;
               seq1.n = seq.(n + k) by A1;
              hence seq1.n = seq2.n by A2;
              end;
              hence seq1 = seq2 by FUNCT_2:113;
          end;
end;

theorem
 seq ^\0 = seq
proof
        now let n;
     thus (seq ^\0).n = seq.(n + 0) by Def11
                    .= seq.n;
     end;
     hence thesis by FUNCT_2:113;
 end;

theorem
 (seq ^\k)^\m = (seq ^\m)^\k
proof
        now let n;
     thus ((seq ^\k)^\m).n = (seq ^\k).(n + m) by Def11
                        .= seq.(n + m + k) by Def11
                        .= seq.(n + k + m)
                        .= (seq ^\m).(n + k) by Def11
                        .= ((seq ^\m)^\k).n by Def11;
     end;
     hence thesis by FUNCT_2:113;
end;

theorem
 (seq ^\k)^\m=seq ^\(k + m)
proof
        now let n;
     thus
        ((seq ^\k)^\m).n = (seq ^\k).(n + m) by Def11
                   .= seq.(n + m + k) by Def11
                   .= seq.(n + (k + m))
                   .= (seq ^\(k + m)).n by Def11;
     end;
     hence thesis by FUNCT_2:113;
  end;

theorem Th94:
(seq1 + seq2) ^\k = (seq1 ^\k) + (seq2 ^\k)
proof
        now let n;
     thus
        ((seq1 + seq2) ^\k).n = (seq1 + seq2).(n + k) by Def11
                         .= seq1.(n + k) + seq2.(n + k) by CLVECT_1:def 11
                         .= (seq1 ^\k).n + seq2.(n + k) by Def11
                         .= (seq1 ^\k).n + (seq2 ^\k).n by Def11
                         .= ((seq1 ^\k) + (seq2 ^\k)).n by CLVECT_1:def 11;
     end;
     hence thesis by FUNCT_2:113;
end;

theorem Th95:
(-seq) ^\k = -(seq ^\k)
proof
        now let n;
     thus
        ((-seq) ^\k).n = (-seq).(n + k) by Def11
                  .= -(seq.(n + k)) by CSSPACE:def 17
                  .= -((seq ^\ k).n) by Def11
                  .= (-(seq ^\k)).n by CSSPACE:def 17;
     end;
     hence thesis by FUNCT_2:113;
end;

theorem
 (seq1 - seq2) ^\k = (seq1 ^\k) - (seq2 ^\k)
proof
     thus
        (seq1 - seq2) ^\k = (seq1 + (-seq2)) ^\k by CSSPACE:71
                     .= (seq1 ^\k) + ((-seq2) ^\k) by Th94
                     .= (seq1 ^\k) + -(seq2 ^\k) by Th95
                     .= (seq1 ^\k) - (seq2 ^\k) by CSSPACE:71;
end;

theorem
 (z * seq) ^\k = z * (seq ^\k)
proof
        now let n;
     thus
        ((z * seq) ^\k).n = (z * seq).(n + k) by Def11
                     .= z * (seq.(n + k)) by CLVECT_1:def 14
                     .= z * ((seq ^\k).n) by Def11
                     .= (z * (seq ^\k)).n by CLVECT_1:def 14;
     end;
     hence thesis by FUNCT_2:113;
end;

theorem
 (seq * Nseq) ^\k = seq * (Nseq ^\k)
proof
        now let n;
     thus
        ((seq * Nseq) ^\k).n = (seq * Nseq).(n + k) by Def11
                        .= seq.(Nseq.(n + k)) by Th82
                        .= seq.((Nseq ^\k).n) by SEQM_3:def 9
                        .= (seq * (Nseq ^\k)).n by Th82;
     end;
     hence thesis by FUNCT_2:113;
end;

theorem Th99:
seq ^\k is subsequence of seq
proof
   deffunc _F(Nat) = $1 + k;
   consider Rseq be Real_Sequence such that
A1: for n be Nat holds Rseq.n = _F(n) from SEQ_1:sch 1;
    now let n;
A2:  Rseq.n = n + k & Rseq.(n + 1) = n + 1 + k by A1;
      n + k < n + k + 1 by NAT_1:38;
     hence Rseq.n < Rseq.(n + 1) by A2;
   end;
then A3:Rseq is increasing by SEQM_3:def 11;
    now let n;
      n + k is Nat;
     hence Rseq.n is Nat by A1;
   end;
   then reconsider Rseq as increasing Seq_of_Nat by A3,SEQM_3:29;
    ex Nseq being increasing Seq_of_Nat st seq ^\k = seq * Nseq
   proof
     take Nseq=Rseq;
      now let n;
       thus (seq * Nseq).n = seq.(Nseq.n) by Th82
                        .= seq.(n + k) by A1
                        .= (seq ^\k).n by Def11;
     end;
     hence thesis by FUNCT_2:113;
   end;
   hence thesis by BHSP_3:def 4;
end;

theorem
 seq is convergent implies
  ((seq ^\k) is convergent & lim (seq ^\k)=lim seq)
proof
    assume
A1:  seq is convergent;
A2:  seq ^\k is subsequence of seq by Th99;
    hence seq ^\k is convergent by A1,Th88;
    thus lim (seq ^\k) = lim seq by A1,A2,Th89;
end;

theorem
 seq is convergent & (ex k st seq = seq1 ^\k) implies seq1 is convergent
proof
     assume that
A1:   seq is convergent and
A2:   ex k st seq = seq1 ^\k;
     consider k such that
A3:   seq = seq1 ^\k by A2;
     consider g1 such that
A4:   for r st r > 0 ex m st for n st n >= m holds
     ||.seq.n - g1.|| < r by A1,Th9;
        now take g = g1;
     let r; assume r > 0;
     then consider m1 be Nat such that
A5:   for n st n >= m1 holds ||.seq.n - g.|| < r by A4;
     take m = m1 + k;
     let n; assume
A6:   n >= m;
     then consider m2 be Nat such that
A7:   n = m1 + k + m2 by NAT_1:28;
        n - k = m1 + m2 by A7;
     then consider l being Nat such that
A8:   l = n - k;
A9:   now assume l < m1;
     then l + k < m1 + k by REAL_1:53;
     hence contradiction by A6,A8;
     end;
A10:   l + k = n by A8;
        ||.seq.l - g.|| < r by A5,A9;
     hence ||.seq1.n - g.|| < r by A3,A10,Def11;
     end;
     hence thesis by Th9;
 end;

theorem
 seq is Cauchy & (ex k st seq = seq1 ^\k) implies
  seq1 is Cauchy
proof
     assume that
A1:   seq is Cauchy and
A2:   ex k st seq = seq1 ^\k;
     consider k such that
A3:   seq = seq1 ^\k by A2;
     let r; assume r > 0;
     then consider l1 being Nat such that
A4:   for n, m st ( n >= l1 & m >= l1 ) holds
     dist((seq.n), (seq.m)) < r by A1,Def8;
     take l = l1 + k;
     let n, m; assume
A5:   n >= l & m >= l;
     then consider m1 be Nat such that
A6:   n = l1 + k + m1 by NAT_1:28;
A7:   n - k = l1 + m1 by A6;
     consider m2 be Nat such that
A8:   m = l1 + k + m2 by A5,NAT_1:28;
A9:   m - k = l1 + m2 by A8;
     consider l2 being Nat such that
A10:   l2 = n - k by A7;
A11:   now assume l2 < l1;
     then l2 + k < l1 + k by REAL_1:53;
     hence contradiction by A5,A10;
     end;
     consider l3 being Nat such that
A12:   l3 = m - k by A9;
A13:   now assume l3 < l1;
     then l3 + k < l1 + k by REAL_1:53;
     hence contradiction by A5,A12;
     end;
A14:   l2 + k = n by A10;
A15:   l3 + k = m by A12;
         dist((seq.l2), (seq.l3)) < r by A4,A11,A13;
      then dist((seq1.n), (seq.l3)) < r by A3,A14,Def11;
      hence dist((seq1.n), (seq1.m)) < r by A3,A15,Def11;
end;

theorem
 seq is Cauchy implies (seq ^\k) is Cauchy
proof
    assume
A1:  seq is Cauchy;
       seq ^\k is subsequence of seq by Th99;
    hence thesis by A1,Th90;
end;

theorem
 seq1 is_compared_to seq2 implies (seq1 ^\k) is_compared_to (seq2 ^\k)
proof
     assume
A1:     seq1 is_compared_to seq2;
     let r; assume r > 0;
     then consider m1 be Nat such that
A2:   for n st n >= m1 holds
     dist((seq1.n), (seq2.n)) < r by A1,Def9;
     take m = m1;
     let n such that
A3:   n >= m;
        n + k >= n by NAT_1:29;
     then n + k >= m by A3,AXIOMS:22;
     then dist((seq1.(n + k)), (seq2.(n + k))) < r by A2;
     then dist((seq1 ^\k).n, (seq2.(n + k))) < r by Def11;
     hence dist((seq1 ^\k).n, (seq2 ^\k).n) < r by Def11;
end;

theorem
 seq is bounded implies (seq ^\k) is bounded
proof
    assume
A1:  seq is bounded;
       seq ^\k is subsequence of seq by Th99;
    hence thesis by A1,Th87;
end;

theorem
 seq is constant implies (seq ^\k) is constant
proof
    assume
A1:  seq is constant;
     seq ^\k is subsequence of seq by Th99;
    hence thesis by A1,Th85;
end;

definition let X;
attr X is complete means :Def12:
 for seq holds seq is Cauchy implies seq is convergent;
end;

theorem
 X is complete & seq is Cauchy implies seq is bounded
proof
     assume that
A1:   X is complete and
A2:   seq is Cauchy;
        seq is convergent by A1,A2,Def12;
     hence thesis by Th80;
end;

definition let X;
  attr X is Hilbert means
   X is ComplexUnitarySpace & X is complete;
end;

