:: On the components of the complement of a special polygonal curve
::  by Andrzej Trybulec and Yatsuka Nakamura
::
:: Received January 21, 1999
:: Copyright (c) 1999 Association of Mizar Users

environ

 vocabularies PRE_TOPC, SPPOL_2, EUCLID, TOPREAL1, BOOLE, FINSEQ_4, JORDAN3,
      JORDAN5C, FINSEQ_1, GROUP_2, RELAT_1, FUNCT_1, ARYTM_1, SPRECT_2, SEQM_3,
      GOBOARD5, PSCOMP_1, GOBOARD9, SPRECT_1, GOBOARD2, TREES_1, MATRIX_1,
      ARYTM_3, TOPS_1, MCART_1, TOPREAL4, SPPOL_1, FINSEQ_5, FINSEQ_6, ARYTM;
 notations TARSKI, XBOOLE_0, SUBSET_1, NUMBERS, REAL_1, NAT_1,
      NAT_D, FUNCT_1,
      PARTFUN1, FINSEQ_1, FINSEQ_4, BINARITH, MATRIX_1, STRUCT_0, PRE_TOPC,
      TOPS_1, EUCLID, TOPREAL1, TOPREAL4, FINSEQ_5, FINSEQ_6, GOBOARD2,
      SPPOL_1, SPPOL_2, GOBOARD5, GOBOARD9, JORDAN3, PSCOMP_1, JORDAN5C,
      SPRECT_1, SPRECT_2, XXREAL_0;
 constructors XXREAL_0, REAL_1, NAT_1, FINSEQ_4, BINARITH, TOPS_1, TOPS_2,
      COMPTS_1, REALSET2, MATRIX_2, TOPMETR, TOPREAL4, GOBOARD2, SPPOL_1,
      PSCOMP_1, GOBOARD9, JORDAN3, JORDAN5C, SPRECT_1, SPRECT_2, GOBOARD1,
      NAT_D;
 registrations XBOOLE_0, RELSET_1, MEMBERED, REALSET1, FINSEQ_6, STRUCT_0,
      COMPTS_1, TOPREAL1, GOBOARD2, SPPOL_2, GOBOARD9, SPRECT_1, SPRECT_3,
      REVROT_1;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions PSCOMP_1;
 theorems TOPREAL1, JORDAN5C, JORDAN3, FINSEQ_3, SPPOL_2, TOPS_1, SPPOL_1,
      TOPREAL3, FINSEQ_5, NAT_1, SPRECT_1, JORDAN5B, SPRECT_2,
      GOBOARD5, FINSEQ_4, GOBOARD7, GOBOARD6, GOBOARD9, ZFMISC_1, TOPREAL4,
      PSCOMP_1, JORDAN4, FINSEQ_6, FINSEQ_1, TARSKI, EUCLID, SUBSET_1,
      JORDAN5D, SPRECT_3, REVROT_1, PARTFUN2, XBOOLE_0, XBOOLE_1, XREAL_1,
      BINARITH, XXREAL_0, PARTFUN1, CARD_1, MATRIX_1, NAT_D;

begin

reserve i,j,l for Element of NAT;

theorem Th1:
  for f being S-Sequence_in_R2, Q being closed Subset of TOP-REAL 2
  st L~f meets Q & not f/.1 in Q
  holds L~R_Cut(f,First_Point(L~f,f/.1,f/.len f,Q)) /\ Q =
  { First_Point(L~f,f/.1,f/.len f,Q) }
proof
  let f be S-Sequence_in_R2, Q be closed Subset of TOP-REAL 2 such that
A1: L~f meets Q and
A2: not f/.1 in Q;
  set p1 = f/.1, p2 = f/.len f, fp = First_Point(L~f,p1,p2,Q);
  len f >= 1+1 by TOPREAL1:def 10;
  then
A3: len f > 1 by NAT_1:13;
A4: L~f /\ Q is closed by TOPS_1:35;
  L~f is_an_arc_of p1,p2 by TOPREAL1:31;
  then
A5: fp in L~f /\ Q by A1,A4,JORDAN5C:def 1;
  then
A6: fp in Q by XBOOLE_0:def 3;
A7: fp in L~f by A5,XBOOLE_0:def 3;
  then
A8: 1<=Index(fp,f) & Index(fp,f)<=len f by JORDAN3:41;
  then
A9: Index(fp,f) in dom f by FINSEQ_3:27;
  1 in dom f by A3,FINSEQ_3:27;
  then fp <> f.1 by A2,A6,PARTFUN1:def 8;
  then fp in L~R_Cut(f,fp) by A7,JORDAN5B:23;
  then
A10: fp in L~R_Cut(f,fp) /\ Q by A6,XBOOLE_0:def 3;
  now
    assume not L~R_Cut(f,fp) /\ Q c= { fp };
    then consider q being set such that
A11: q in L~R_Cut(f,fp) /\ Q and
A12: not q in { fp } by TARSKI:def 3;
    reconsider q as Point of TOP-REAL 2 by A11;
A13: q <> fp by A12,TARSKI:def 1;
A14: q in Q by A11,XBOOLE_0:def 3;
A15: q in L~R_Cut(f,fp) by A11,XBOOLE_0:def 3;
    L~R_Cut(f,fp) c= L~f by A7,JORDAN3:76;
    then
A16: LE fp, q, L~f, p1, p2 by A4,A14,A15,JORDAN5C:15;
    per cases;
    suppose fp = f.1;
      then
A17:  q in L~<*fp*> by A15,JORDAN3:def 5;
      len<*fp*> = 1 by FINSEQ_1:56;
      hence contradiction by A17,TOPREAL1:28;
    end;
    suppose
A18:  fp <> f.1;
      set m = mid(f,1,Index(fp,f));
      len m = Index(fp,f)-'1+1 by A8,JORDAN4:20;
      then len m <> 0 by NAT_1:11;
      then
A19:  m is non empty by CARD_1:47;
      q in L~(m^<*fp*>) by A15,A18,JORDAN3:def 5;
      then
A20:  q in L~m \/ LSeg(m/.len m,fp) by A19,SPPOL_2:19;
A21:  Index(fp,f) < len f by A7,JORDAN3:41;
A22:  fp in LSeg(f,Index(fp,f)) by A7,JORDAN3:42;
      now per cases by A20,XBOOLE_0:def 2;
        suppose
A23:      q in L~m;
A24:      now
            assume Index(fp,f) <= 1;
            then Index(fp,f) = 1 by A8,XXREAL_0:1;
            then len m = 1 by A3,JORDAN4:27;
            hence contradiction by A23,TOPREAL1:28;
          end;
          then
A25:      LE q, f/.Index(fp,f), L~f, p1, p2 by A8,A23,SPRECT_3:33;
          f/.Index(fp,f) in LSeg(f/.Index(fp,f),fp) by TOPREAL1:6;
          then LE f/.Index(fp,f), fp, L~f, p1, p2 by A21,A22,A24,SPRECT_3:40;
          then LE q, fp, L~f, p1, p2 by A25,JORDAN5C:13;
          hence contradiction by A13,A16,JORDAN5C:12,TOPREAL1:31;
        end;
        suppose
A26:      q in LSeg(m/.len m,fp);
          len m in dom m by A19,FINSEQ_5:6;
          then m/.len m = m.len m by PARTFUN1:def 8
            .= f.Index(fp,f) by A8,JORDAN4:22
            .= f/.Index(fp,f) by A9,PARTFUN1:def 8;
          then LE q, fp, L~f, p1, p2 by A8,A21,A22,A26,SPRECT_3:40;
          hence contradiction by A13,A16,JORDAN5C:12,TOPREAL1:31;
        end;
      end;
      hence contradiction;
    end;
  end;
  hence L~R_Cut(f,fp) /\ Q = { fp } by A10,ZFMISC_1:39;
end;

theorem
  for f being non empty FinSequence of TOP-REAL 2,
  p being Point of TOP-REAL 2 st f is_S-Seq & p = f/.len f holds
  L~L_Cut (f,p) = {}
proof
  let f be non empty FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2;
  assume that
A1: f is_S-Seq and
A2: p = f/.len f;
  len f >= 2 by A1,TOPREAL1:def 10;
  then len f >= 1 by XXREAL_0:2;
  then len f in dom f by FINSEQ_3:27;
  then p = f.len f by A2,PARTFUN1:def 8;
  then L_Cut (f,p) = <*p*> by A1,JORDAN5B:17;
  then len L_Cut (f,p) = 1 by FINSEQ_1:56;
  hence thesis by TOPREAL1:28;
end;

canceled;

theorem Th4:
  for f being S-Sequence_in_R2, p being Point of TOP-REAL 2, j st
  1 <=j & j < len f & p in L~mid(f,j,len f)
  holds LE f/.j, p, L~f, f/.1, f/.len f
proof
  let f be S-Sequence_in_R2, p be Point of TOP-REAL 2, j such that
A1: 1 <=j & j < len f and
A2: p in L~mid(f,j,len f);
  consider i such that
A3: 1 <= i & i+1 <= len mid(f,j,len f) and
A4: p in LSeg(mid(f,j,len f),i) by A2,SPPOL_2:13;
A5: len mid(f,j,len f)=len f-'j+1 by A1,JORDAN4:20;
  then i <= len f -' j by A3,XREAL_1:8;
  then
A6: i + j <= len f by A1,NAT_D:54;
  j+1 <= j+i by A3,XREAL_1:8;
  then
A7: j <= j+i-'1 by NAT_D:49;
  1+1 <= j+i by A1,A3,XREAL_1:9;
  then
A8: 1 <= j+i-'1 by NAT_D:49;
  i<len f-'j+1 by A3,A5,NAT_1:13;
  then
A9: p in LSeg(f,j+i-'1) by A1,A3,A4,JORDAN4:31;
  j+i >= i by NAT_1:11;
  then
A10: j+i-'1+1 <= len f by A3,A6,XREAL_1:237,XXREAL_0:2;
  then
A11: LE f/.(j+i-'1), p, L~f, f/.1, f/.len f by A8,A9,JORDAN5C:25;
  j+i-'1+1 >= j+i-'1 by NAT_1:11;
  then len f >= j+i-'1 by A10,XXREAL_0:2;
  then LE f/.j, f/.(j+i-'1), L~f, f/.1, f/.len f by A1,A7,JORDAN5C:24;
  hence LE f/.j, p, L~f, f/.1, f/.len f by A11,JORDAN5C:13;
end;

theorem Th5:
  for f being S-Sequence_in_R2, p,q being Point of TOP-REAL 2, j st
  1 <=j & j < len f & p in LSeg(f,j) & q in LSeg(p,f/.(j+1))
  holds LE p, q, L~f, f/.1, f/.len f
proof
  let f be S-Sequence_in_R2, p,q be Point of TOP-REAL 2, j such that
A1: 1 <=j & j < len f and
A2: p in LSeg(f,j) and
A3: q in LSeg(p,f/.(j+1));
A4: j+1 <= len f by A1,NAT_1:13;
  then
A5: LSeg(f,j) = LSeg(f/.j,f/.(j+1)) by A1,TOPREAL1:def 5;
A6: f/.(j+1) in LSeg(f,j) by A1,A4,TOPREAL1:27;
  then
A7: LSeg(p,f/.(j+1)) c= LSeg(f,j) by A2,A5,TOPREAL1:12;
  then
A8: q in LSeg(f,j) by A3;
A9: LSeg(f,j) c= L~f by TOPREAL3:26;
  per cases;
  suppose p = q;
    hence thesis by A2,A9,JORDAN5C:9;
  end;
  suppose
A10: q <> p;
    for i, j being Element of NAT st p in LSeg(f,i) & q in LSeg(f,j)
    & 1 <= i & i+1 <= len f & 1 <= j & j+1 <= len f holds
    i <= j & (i = j implies LE p,q,f/.i,f/.(i+1))
    proof
      let i1, i2 be Element of NAT such that
A11:  p in LSeg(f,i1) and
A12:  q in LSeg(f,i2) and
A13:  1 <= i1 & i1+1 <= len f and
A14:  1 <= i2 & i2+1 <= len f;
A15:  now
        assume i1 + 1 > j & j+1 > i1;
        then i1 <= j & j <= i1 by NAT_1:13;
        hence i1 = j by XXREAL_0:1;
      end;
A16:  p in LSeg(f,i1) /\ LSeg(f,j) by A2,A11,XBOOLE_0:def 3;
      then LSeg(f,i1) meets LSeg(f,j) by XBOOLE_0:4;
      then
A17:  i1 + 1 >= j & j + 1 >= i1 by TOPREAL1:def 9;
A18:  now
        assume
A19:    j + 1 = i1;
        j+(1+1) = j+1+1;
        then p in {f/.(j+1)} by A1,A13,A16,A19,TOPREAL1:def 8;
        then p = f/.(j+1) by TARSKI:def 1;
        then q in {p} by A3,TOPREAL1:7;
        hence contradiction by A10,TARSKI:def 1;
      end;
      then i1 + 1 = j or i1 = j by A15,A17,XXREAL_0:1;
      then
A20:  i1 <= j by NAT_1:11;
A21:  now
        assume i2 + 1 > j & j+1 > i2;
        then i2 <= j & j <= i2 by NAT_1:13;
        hence i2 = j by XXREAL_0:1;
      end;
A22:  q in LSeg(f,i2) /\ LSeg(f,j) by A3,A7,A12,XBOOLE_0:def 3;
      then LSeg(f,i2) meets LSeg(f,j) by XBOOLE_0:4;
      then i2 + 1 >= j & j + 1 >= i2 by TOPREAL1:def 9;
      then
A23:  i2 + 1 = j or i2 = j or j + 1 = i2 by A21,XXREAL_0:1;
      1 <= j+1 by NAT_1:11;
      then
A24:  j+1 in dom f by A4,FINSEQ_3:27;
A25:  j in dom f by A1,FINSEQ_3:27;
A26:  now
        assume f/.(j+1)=f/.j;
        then j = j+1 by A24,A25,PARTFUN2:17;
        hence contradiction;
      end;
A27:  now
        assume
A28:    i2+1 = j;
        i2+(1+1) = i2+1+1;
        then q in {f/.j} by A4,A14,A22,A28,TOPREAL1:def 8;
        then q = f/.j by TARSKI:def 1;
        hence contradiction by A2,A3,A5,A6,A10,A26,SPPOL_1:24,TOPREAL1:12;
      end;
      then i2 >= j by A23,NAT_1:11;
      hence i1 <= i2 by A20,XXREAL_0:2;
      assume
A29:  i1 = i2;
      not p in LSeg(q,f/.(j+1)) by A3,A10,SPRECT_3:16;
      then not LE q,p,f/.j,f/.(j+1) by JORDAN3:65;
      then LT p,q,f/.j,f/.(j+1) by A2,A5,A12,A15,A17,A18,A26,A27,A29,JORDAN3:63
      ,XXREAL_0:1;
      hence LE p,q,f/.i1,f/.(i1+1) by A15,A17,A18,A27,A29,JORDAN3:def 7
      ,XXREAL_0:1;
    end;
    hence LE p, q, L~f, f/.1, f/.len f by A2,A8,A9,A10,JORDAN5C:30;
  end;
end;

theorem Th6:
  for f being S-Sequence_in_R2, Q being closed Subset of TOP-REAL 2
  st L~f meets Q & not f/.len f in Q
  holds L~L_Cut(f,Last_Point(L~f,f/.1,f/.len f,Q)) /\ Q =
  { Last_Point(L~f,f/.1,f/.len f,Q) }
proof
  let f be S-Sequence_in_R2, Q be closed Subset of TOP-REAL 2 such that
A1: L~f meets Q and
A2: not f/.len f in Q;
  set p1 = f/.1, p2 = f/.len f, lp = Last_Point(L~f,p1,p2,Q);
  len f >= 1+1 by TOPREAL1:def 10;
  then
A3: len f > 1 by NAT_1:13;
A4: L~f /\ Q is closed by TOPS_1:35;
  L~f is_an_arc_of p1,p2 by TOPREAL1:31;
  then
A5: lp in L~f /\ Q by A1,A4,JORDAN5C:def 2;
  then
A6: lp in Q by XBOOLE_0:def 3;
A7: lp in L~f by A5,XBOOLE_0:def 3;
  then
A8: 1<=Index(lp,f) & Index(lp,f)<=len f by JORDAN3:41;
  len f in dom f by A3,FINSEQ_3:27;
  then lp <> f.len f by A2,A6,PARTFUN1:def 8;
  then lp in L~L_Cut(f,lp) by A7,JORDAN5B:22;
  then
A9: lp in L~L_Cut(f,lp) /\ Q by A6,XBOOLE_0:def 3;
  now
    assume not L~L_Cut(f,lp) /\ Q c= { lp };
    then consider q being set such that
A10: q in L~L_Cut(f,lp) /\ Q and
A11: not q in { lp } by TARSKI:def 3;
    reconsider q as Point of TOP-REAL 2 by A10;
A12: q <> lp by A11,TARSKI:def 1;
A13: q in Q by A10,XBOOLE_0:def 3;
A14: q in L~L_Cut(f,lp) by A10,XBOOLE_0:def 3;
    L~L_Cut(f,lp) c= L~f by A7,JORDAN3:77;
    then
A15: LE q, lp, L~f, p1, p2 by A4,A13,A14,JORDAN5C:16;
    set m = mid(f,Index(lp,f)+1,len f);
A16: Index(lp,f) < len f by A7,JORDAN3:41;
    then
A17: Index(lp,f)+1 <= len f by NAT_1:13;
A18: 1 <= Index(lp,f)+1 by NAT_1:11;
    then len m = len f -' (Index(lp,f)+1)+1 by A17,JORDAN4:20;
    then len m <> 0 by NAT_1:11;
    then
A19: m is non empty by CARD_1:47;
    per cases;
    suppose lp=f.(Index(lp,f)+1);
      then
A20:  q in L~m by A14,JORDAN3:def 4;
A21:  lp in LSeg(f,Index(lp,f)) by A7,JORDAN3:42;
      now
        assume Index(lp,f)+1 >= len f;
        then Index(lp,f)+1 = len f by A17,XXREAL_0:1;
        then len m = 1 by A3,JORDAN4:27;
        hence contradiction by A20,TOPREAL1:28;
      end;
      then
A22:  LE f/.(Index(lp,f)+1), q, L~f, f/.1, f/.len f by A20,Th4,NAT_1:11;
      f/.(Index(lp,f)+1) in LSeg(lp,f/.(Index(lp,f)+1)) by TOPREAL1:6;
      then LE lp, f/.(Index(lp,f)+1), L~f, p1, p2 by A8,A16,A21,Th5;
      then LE lp, q, L~f, p1, p2 by A22,JORDAN5C:13;
      hence contradiction by A12,A15,JORDAN5C:12,TOPREAL1:31;
    end;
    suppose lp<>f.(Index(lp,f)+1);
      then q in L~(<*lp*>^m) by A14,JORDAN3:def 4;
      then
A23:  q in L~m \/ LSeg(m/.1,lp) by A19,SPPOL_2:20;
      1 <= Index(lp,f)+1 by NAT_1:11;
      then
A24:  Index(lp,f)+1 in dom f by A17,FINSEQ_3:27;
A25:  lp in LSeg(f,Index(lp,f)) by A7,JORDAN3:42;
      now per cases by A23,XBOOLE_0:def 2;
        suppose
A26:      q in L~m;
          now
            assume Index(lp,f)+1 >= len f;
            then Index(lp,f)+1 = len f by A17,XXREAL_0:1;
            then len m = 1 by A3,JORDAN4:27;
            hence contradiction by A26,TOPREAL1:28;
          end;
          then
A27:      LE f/.(Index(lp,f)+1), q, L~f, f/.1, f/.len f by A26,Th4,NAT_1:11;
          f/.(Index(lp,f)+1) in LSeg(lp,f/.(Index(lp,f)+1)) by TOPREAL1:6;
          then LE lp, f/.(Index(lp,f)+1), L~f, p1, p2 by A8,A16,A25,Th5;
          then LE lp, q, L~f, p1, p2 by A27,JORDAN5C:13;
          hence contradiction by A12,A15,JORDAN5C:12,TOPREAL1:31;
        end;
        suppose
A28:      q in LSeg(lp,m/.1);
          1 in dom m by A19,FINSEQ_5:6;
          then m/.1 = m.1 by PARTFUN1:def 8
            .= f.(Index(lp,f)+1) by A3,A17,A18,JORDAN3:27
            .= f/.(Index(lp,f)+1) by A24,PARTFUN1:def 8;
          then LE lp, q, L~f, p1, p2 by A8,A16,A25,A28,Th5;
          hence contradiction by A12,A15,JORDAN5C:12,TOPREAL1:31;
        end;
      end;
      hence contradiction;
    end;
  end;
  hence L~L_Cut(f,lp) /\ Q = { lp } by A9,ZFMISC_1:39;
end;

reserve q for Point of TOP-REAL 2;

Lm1: for f being
clockwise_oriented (non constant standard special_circular_sequence)
st f/.1 = N-min L~f holds LeftComp f <> RightComp f
proof

let f be clockwise_oriented (non constant standard special_circular_sequence);
  set A = N-min L~f;
  assume that
A1: f/.1 = A and
A2: LeftComp f = RightComp f;
A3: LeftComp SpStSeq L~f c= LeftComp f by A1,SPRECT_3:58;
  consider i such that
A4: 1 <= i and
A5: i < len GoB f and
A6: A = (GoB f)*(i,width GoB f) by SPRECT_3:45;
  set w = width GoB f;
A7: len f > 4 by GOBOARD7:36;
A8: 1+1 <= len f by GOBOARD7:36,XXREAL_0:2;
A9: w > 1 by GOBOARD7:35;
  then
A10: w-'1+1 = w by XREAL_1:237;
A11: [i,w] in Indices GoB f by A4,A5,A9,MATRIX_1:37;
A12: 1 <= i+1 & i+1 <= len GoB f by A5,NAT_1:11,13;
  then
A13: [i+1,w] in Indices GoB f by A9,MATRIX_1:37;
A14: 1 in dom f by FINSEQ_5:6;
A15: i in dom GoB f by A4,A5,FINSEQ_3:27;
  then
A16: f/.(1+1) = (GoB f)*(i+1,w) by A1,A6,SPRECT_3:46;
  then
A17: right_cell(f,1) = cell(GoB f,i,w-'1) by A1,A6,A8,A10,A11,A13,GOBOARD5:29;
  set z2 = 1/2*((GoB f)*(i,w-'1)+(GoB f)*(i+1,w)),
  p1 = 1/2*((GoB f)*(i,w)+(GoB f)*(i+1,w)),
  p2 = 1/2*((GoB f)*(i,w-'1)+(GoB f)*(i,w));
A18: 1 <= w-'1 by A9,NAT_D:49;
  then
A19: z2 in Int cell(GoB f,i,w-'1) by A4,A10,A12,GOBOARD6:34;
A20: Int right_cell(f,1) c= RightComp f by A8,GOBOARD9:28;
A21: z2 in RightComp SpStSeq L~f
  proof
A22: W-bound L~SpStSeq L~f = W-bound L~f &
    S-bound L~SpStSeq L~f = S-bound L~f &
    N-bound L~SpStSeq L~f = N-bound L~f &
    E-bound L~SpStSeq L~f = E-bound L~f by SPRECT_1:66,67,68,69;
A23: 1 < i+1 by A4,NAT_1:13;
A24: 1 <= len GoB f by A4,A5,XXREAL_0:2;
A25: w-'1 < w by A10,XREAL_1:31;
A26: z2 = 1/2*(A+(GoB f)*(i+1,w-'1)) by A4,A6,A10,A12,A18,GOBOARD7:11;
    then
A27: z2`1 = 1/2*(A+(GoB f)*(i+1,w-'1))`1 by TOPREAL3:9
      .= 1/2*(A`1+(GoB f)*(i+1,w-'1)`1) by TOPREAL3:7
      .= 1/2*A`1+1/2*((GoB f)*(i+1,w-'1))`1;
A28: z2`2 = 1/2*(A+(GoB f)*(i+1,w-'1))`2 by A26,TOPREAL3:9
      .= 1/2*(A`2+(GoB f)*(i+1,w-'1)`2) by TOPREAL3:7
      .= 1/2*(N-bound L~f+(GoB f)*(i+1,w-'1)`2) by EUCLID:56
      .= 1/2*N-bound L~f+1/2*((GoB f)*(i+1,w-'1))`2;
A29: 1/2*(W-bound L~f) + 1/2*(W-bound L~f) = (W-bound L~f);
A30: 1/2*(S-bound L~f) + 1/2*(S-bound L~f) = (S-bound L~f);
A31: 1/2*(N-bound L~f) + 1/2*(N-bound L~f) = N-bound L~f;
A32: 1/2*(E-bound L~f) + 1/2*(E-bound L~f) = (E-bound L~f);
    N-min L~f in L~f by SPRECT_1:13;
    then A`1 >= W-bound L~f by PSCOMP_1:71;
    then
A33: 1/2*A`1 >= 1/2*W-bound L~f by XREAL_1:66;
A34: (GoB f)*(1,w-'1)`1 = (GoB f)*(1,1)`1 by A18,A24,A25,GOBOARD5:3;
    (GoB f)*(i+1,w-'1)`1 > (GoB f)*(1,w-'1)`1 by A12,A18,A23,A25,GOBOARD5:4;
    then (GoB f)*(i+1,w-'1)`1 > W-bound L~f by A34,JORDAN5D:39;
    then 1/2*((GoB f)*(i+1,w-'1))`1 > 1/2*W-bound L~f by XREAL_1:70;
    then
A35: W-bound L~SpStSeq L~f < z2`1 by A22,A27,A29,A33,XREAL_1:10;
    N-max L~f in L~f by SPRECT_1:13;
    then (N-max L~f)`1 <= E-bound L~f by PSCOMP_1:71;
    then A`1 < E-bound L~f by SPRECT_2:55,XXREAL_0:2;
    then
A36: 1/2*A`1 < 1/2*E-bound L~f by XREAL_1:70;
A37: (GoB f)*(len GoB f,w-'1)`1 = (GoB f)*(len GoB f,1)`1
    by A18,A24,A25,GOBOARD5:3;
    (GoB f)*(i+1,w-'1)`1 <= (GoB f)*(len GoB f,w-'1)`1 by A12,A18,A25,
    SPRECT_3:25;
    then (GoB f)*(i+1,w-'1)`1 <= E-bound L~f by A37,JORDAN5D:41;
    then 1/2*((GoB f)*(i+1,w-'1))`1 <= 1/2*E-bound L~f by XREAL_1:66;
    then
A38: z2`1 < E-bound L~SpStSeq L~f by A22,A27,A32,A36,XREAL_1:10;
A39: 1/2*N-bound L~f > 1/2*S-bound L~f by SPRECT_1:34,XREAL_1:70;
A40: (GoB f)*(i+1,1)`2 = (GoB f)*(1,1)`2 by A9,A12,GOBOARD5:2;
    (GoB f)*(i+1,w-'1)`2 >= (GoB f)*(i+1,1)`2 by A12,A18,A25,SPRECT_3:24;
    then (GoB f)*(i+1,w-'1)`2 >= S-bound L~f by A40,JORDAN5D:40;
    then 1/2*((GoB f)*(i+1,w-'1))`2 >= 1/2*S-bound L~f by XREAL_1:66;
    then
A41: S-bound L~SpStSeq L~f < z2`2 by A22,A28,A30,A39,XREAL_1:10;
A42: (GoB f)*(i+1,w)`2 = (GoB f)*(1,width GoB f)`2 by A9,A12,GOBOARD5:2;
    (GoB f)*(i+1,w-'1)`2 < (GoB f)*(i+1,w)`2 by A12,A18,A25,GOBOARD5:5;
    then (GoB f)*(i+1,w-'1)`2 < N-bound L~f by A42,JORDAN5D:42;
    then 1/2*((GoB f)*(i+1,w-'1))`2 < 1/2*N-bound L~f by XREAL_1:70;
    then
A43: z2`2 < N-bound L~SpStSeq L~f by A22,A28,A31,XREAL_1:8;
    RightComp SpStSeq L~f =
    {q : W-bound L~SpStSeq L~f < q`1 & q`1 < E-bound L~SpStSeq L~f
    & S-bound L~SpStSeq L~f < q`2 & q`2 < N-bound L~SpStSeq L~f}
    by SPRECT_3:54;
    hence thesis by A35,A38,A41,A43;
  end;
  consider z1 being set such that
A44: z1 in LeftComp SpStSeq L~f by XBOOLE_0:def 1;
  LeftComp SpStSeq L~f misses RightComp SpStSeq L~f by SPRECT_1:96;
  then
A45: z1 <> z2 by A21,A44,XBOOLE_0:3;
  reconsider z1 as Point of TOP-REAL 2 by A44;
  consider P being Subset of TOP-REAL 2 such that
A46: P is_S-P_arc_joining z1,z2 and
A47: P c= RightComp f by A2,A3,A17,A19,A20,A44,A45,TOPREAL4:30;
  consider Red' being FinSequence of TOP-REAL 2 such that
A48: Red' is_S-Seq and
A49: P = L~Red' and
A50: z1 = Red'/.1 and
A51: z2 = Red'/.len Red' by A46,TOPREAL4:def 1;
A52: L~SpStSeq L~f meets L~Red' by A21,A44,A48,A50,A51,SPRECT_3:50;
A53: 2 in dom f by A8,FINSEQ_3:27;
A54: len f in dom f by FINSEQ_5:6;
A55: len f -' 1 >= 1 & len f -' 1 <= len f by A8,NAT_D:44,49;
  then
A56: len f -' 1 in dom f by FINSEQ_3:27;
  1 <= len f by A54,FINSEQ_3:27;
  then
A57: len f -' 1 + 1 = len f by XREAL_1:237;
  then
A58: len f -' 1 < len f by NAT_1:13;
A59: <*NW-corner L~f*> is_in_the_area_of f by SPRECT_2:30;
A60: w-'1 < width GoB f by A10,NAT_1:13;
  then Int cell(GoB f,i,w-'1) misses L~SpStSeq L~f by A4,A5,A18,SPRECT_3:72;
  then
A61: not z2 in L~SpStSeq L~f by A19,XBOOLE_0:3;
A62: LSeg(NW-corner L~f,NE-corner L~f) c= L~SpStSeq L~f by SPRECT_3:26;
A63: f/.1 = f/.len f by FINSEQ_6:def 1;
A64: NW-corner L~f in LSeg(NW-corner L~f,NE-corner L~f) by TOPREAL1:6;
A65: N-min L~f in LSeg(NW-corner L~f,NE-corner L~f) by SPRECT_3:28;
  then
A66: LSeg(NW-corner L~f,N-min L~f) c= LSeg(NW-corner L~f,NE-corner L~f )
  by A64,TOPREAL1:12;
A67: LSeg(NW-corner L~f,NE-corner L~f) is horizontal by SPRECT_3:29;
A68: LSeg(NW-corner L~f,N-min L~f) is horizontal by A66,SPRECT_1:11,SPRECT_3:29
  ;
  1 + 2 <= len f by GOBOARD7:36,XXREAL_0:2;
  then
A69: LSeg(f,1) /\ LSeg(f,1+1) = {f/.(1+1)} by TOPREAL1:def 8;
  len f >= 2 + 1 by GOBOARD7:36,XXREAL_0:2;
  then len f -' 1 >= 1 + 1 by NAT_D:49;
  then len f -' 1 -' 1 >= 1 by NAT_D:49;
  then
A70: len f -' 2 >= 1 by NAT_D:45;
A71: len f -' 2 + 1 = len f -' 1 -' 1 + 1 by NAT_D:45
    .= len f -' 1 by A55,XREAL_1:237;
  len f -' 2 + 2 = len f by A7,XREAL_1:237,XXREAL_0:2;
  then
A72: LSeg(f,len f -' 1) /\ LSeg(f,len f -' 2) = {f/.(len f -' 1)}
  by A70,A71,TOPREAL1:def 8;
A73: f/.2 in N-most L~f by A1,SPRECT_2:34;
  N-most L~f c= LSeg(NW-corner L~f,NE-corner L~f) by XBOOLE_1:17;
  then
A74: LSeg(f/.1, f/.2) c= LSeg(NW-corner L~f,NE-corner L~f)
  by A1,A65,A73,TOPREAL1:12;
A75: ((GoB f)*(i,w-'1))`1 = ((GoB f)*(i,1))`1 by A4,A5,A18,A60,GOBOARD5:3
    .= A`1 by A4,A5,A6,A9,GOBOARD5:3;
A76: (GoB f)*(i+1,w)`2 = (GoB f)*(1,w)`2 by A9,A12,GOBOARD5:2
    .= A`2 by A4,A5,A6,A9,GOBOARD5:2;
  then
A77: (f/.2)`2 = N-bound L~f by A16,EUCLID:56;
A78: <*(GoB f)*(i+1,w)*> is_in_the_area_of f by A9,A12,SPRECT_3:66;
  <*(GoB f)*(i,w-'1)*> is_in_the_area_of f by A4,A5,A18,A60,SPRECT_3:66;
  then <*(GoB f)*(i,w-'1),(GoB f)*(i+1,w)*> is_in_the_area_of f
  by A78,SPRECT_3:59;
  then <*z2*> is_in_the_area_of f by SPRECT_3:67;
  then
A79: <*z2*> is_in_the_area_of SpStSeq L~f by SPRECT_3:70;
A80: (GoB f)*(i,w-'1) = f/.(len f -' 1) by A1,A6,A15,SPRECT_3:46;
  then LSeg(A,f/.(len f -' 1)) is vertical by A75,SPPOL_1:37;
  then
A81: LSeg(A,f/.(len f -' 1)) /\ LSeg(NW-corner L~f,A) = { A }
  by A66,A67,SPRECT_1:11,SPRECT_3:22;
A82: (NW-corner L~f)`2 = A`2 by PSCOMP_1:95;
A83: (NW-corner L~f)`1 <= A`1 by PSCOMP_1:97;
  A`1 <= (f/.2)`1 by A73,PSCOMP_1:98;
  then A in LSeg(NW-corner L~f,f/.2) by A16,A76,A82,A83,GOBOARD7:9;
  then
A84: LSeg(A,f/.2) /\ LSeg(NW-corner L~f,A) = { A } by TOPREAL1:14;
  ((GoB f)*(i,w))`2 = ((GoB f)*(1,w))`2 by A4,A5,A9,GOBOARD5:2
    .= ((GoB f)*(i+1,w))`2 by A9,A12,GOBOARD5:2;
  then ((GoB f)*(i,w-'1)+(GoB f)*(i,w))`2
  = ((GoB f)*(i,w-'1))`2+((GoB f)*(i+1,w))`2 by TOPREAL3:7
    .= ((GoB f)*(i,w-'1)+(GoB f)*(i+1,w))`2 by TOPREAL3:7;
  then p2`2 = 1/2*((GoB f)*(i,w-'1)+(GoB f)*(i+1,w)) `2 by TOPREAL3:9
    .= z2`2 by TOPREAL3:9;
  then
A85: LSeg(p2,z2) is horizontal by SPPOL_1:36;
A86: f/.(len f -' 1) in LSeg(f/.(len f -' 1), f/.len f) by TOPREAL1:6;
A87: (GoB f)*(i,w) = f/.len f by A1,A6,FINSEQ_6:def 1;
  p2 = 1/2*((GoB f)*(i,w-'1))+(1 - 1/2)*((GoB f)*(i,w)) by EUCLID:36;
  then
A88: p2 in LSeg(f/.(len f -' 1), f/.len f) by A80,A87,SPPOL_1:22;
  then
A89: LSeg(p2,f/.(len f -' 1)) c= LSeg(f/.(len f -' 1), f/.len f)
  by A86,TOPREAL1:12;
  LSeg(f/.(len f -' 1), f/.len f) = LSeg(f,len f -' 1)
  by A55,A57,TOPREAL1:def 5;
  then
A90: LSeg(f/.(len f -' 1), f/.len f) c= L~f by TOPREAL3:26;
  then
A91: LSeg(p2,f/.(len f -' 1)) c= L~f by A89,XBOOLE_1:1;
A92: LSeg(p2,f/.(len f -' 1)) c= LSeg(f,len f -' 1)
  by A55,A57,A89,TOPREAL1:def 5;
A93: p2`1 = 1/2*((GoB f)*(i,w-'1)+A)`1 by A6,TOPREAL3:9
    .= 1/2*(A`1+A`1) by A75,TOPREAL3:7
    .= (N-min L~f)`1;
  then
A94: LSeg(p2,f/.(len f -' 1)) is vertical by A75,A80,SPPOL_1:37;
A95: p2 in LSeg(p2,f/.(len f -' 1)) by TOPREAL1:6;
  then
A96: p2 in L~f by A91;
A97: now
    assume p2 = A;
    then f/.(len f -' 1) = f/.len f by A1,A6,A63,A80,SPRECT_3:15;
    hence contradiction by A54,A56,A57,GOBOARD7:31;
  end;
  1 + 1 + 1 <= len f by GOBOARD7:36,XXREAL_0:2;
  then
A98: 1 + 1 <= len f -' 1 by NAT_D:49;
  then
A99: 1 <= len f -' 1 -' 1 by NAT_D:49;
A100: len f -' 1 -'1 + 1 = len f -' 1 by A98,XREAL_1:237,XXREAL_0:2;
A101: len f -' 1 -' 1 + 2 = len f -' 2 + 2 by NAT_D:45
    .= len f by A7,XREAL_1:237,XXREAL_0:2;
A102: for i,j st 1 <= i & i <= j & j < len f -' 1 holds
  L~mid(f,i,j) misses LSeg(p2,f/.(len f -' 1))
  proof
    let l,j such that
A103: 1 <= l and
A104: l <= j and
A105: j < len f -' 1;
    len f -' 1 > l by A104,A105,XXREAL_0:2;
    then len f -' 1 > 1 by A103,XXREAL_0:2;
    then
A106: len f -' 1 < len f by NAT_D:51;
    then
A107: j < len f by A105,XXREAL_0:2;
    assume L~mid(f,l,j) meets LSeg(p2,f/.(len f -' 1));
    then L~mid(f,l,j) /\ LSeg(p2,f/.(len f -' 1)) <> {} by XBOOLE_0:def 7;
    then consider p being Point of TOP-REAL 2 such that
A108: p in L~mid(f,l,j) /\ LSeg(p2,f/.(len f -' 1)) by SUBSET_1:10;
A109: p in LSeg(p2,f/.(len f -' 1)) by A108,XBOOLE_0:def 3;
    p in L~mid(f,l,j) by A108,XBOOLE_0:def 3;
    then consider ii being Element of NAT such that
A110: 1 <= ii and
A111: ii+1 <= len mid(f,l,j) and
A112: p in LSeg(mid(f,l,j),ii) by SPPOL_2:13;
    set k = ii+l-'1;
    len mid(f,l,j) = j-'l+1 by A103,A104,A107,JORDAN4:20;
    then
A113: ii<j-'l+1 by A111,NAT_1:13;
    per cases by A104,XXREAL_0:1;
    suppose l = j;
      then len mid(f,l,j) = 1 by A103,A107,JORDAN4:27;
      then L~mid(f,l,j) = {} by TOPREAL1:28;
      hence thesis by A112,SPPOL_2:17;
    end;
    suppose
A114: l < j;
      then
A115: LSeg(mid(f,l,j),ii)=LSeg(f,k) by A103,A107,A110,A113,JORDAN4:31;
A116: ii + l >= ii + 1 by A103,XREAL_1:8;
      ii + 1 >= 1 + 1 by A110,XREAL_1:8;
      then
A117: ii + l >= 1 + 1 by A116,XXREAL_0:2;
      then
A118: k >= 1 by NAT_D:49;
A119: ii + l >= 1 by A117,XXREAL_0:2;
A120: now
        assume k + 1 >= len f -' 1;
        then k >= len f -'1 -' 1 by NAT_D:53;
        then
A121:   ii + l >= len f -' 1 by A119,NAT_D:56;
        ii + l < j -' l + 1 + l by A113,XREAL_1:8;
        then ii + l < j -' l + l + 1;
        then ii + l < j + 1 by A114,XREAL_1:237;
        then len f -' 1 < j + 1 by A121,XXREAL_0:2;
        hence contradiction by A105,NAT_1:13;
      end;
A122: p in LSeg(f,k) /\ LSeg(f,len f -' 1) by A92,A109,A112,A115,XBOOLE_0:def 3
      ;
      then LSeg(f,k) meets LSeg(f,len f -' 1) by XBOOLE_0:4;
      then k <= 1 by A106,A120,GOBOARD5:def 4;
      then
A123: k = 1 by A118,XXREAL_0:1;
      LSeg(f,1) /\ LSeg(f,len f -' 1) = { f.1 } by JORDAN4:54
        .= {f/.1} by A14,PARTFUN1:def 8;
      then p = f/.1 by A122,A123,TARSKI:def 1;
      hence contradiction by A1,A63,A88,A97,A109,SPRECT_3:16;
    end;
  end;
A124: for j st 1 <= j & j < len f -' 1 holds
  L~mid(f,j,len f -' 1) /\ LSeg(p2,f/.(len f -' 1)) = {f/.(len f -' 1)}
  proof
    let j such that
A125: 1 <= j and
A126: j < len f -' 1;
A127: f/.(len f -' 1) in LSeg(p2,f/.(len f -' 1)) by TOPREAL1:6;
A128: 1 <= len f -' 1 by A125,A126,XXREAL_0:2;
A129: len f -' 1 -' 1 = len f -' 2 by NAT_D:45;
    then j <= len f -' 2 by A126,NAT_D:49;
    then
A130: LSeg(p2,f/.(len f -' 1)) misses L~mid(f,j,len f -' 2)
    by A102,A125,A128,A129,JORDAN5B:1;
    L~mid(f,j,len f -' 1) = LSeg(f,len f -' 2) \/ L~mid(f,j,len f -' 2)
    by A125,A126,A129,NAT_D:35,SPRECT_3:36;
    hence L~mid(f,j,len f -' 1) /\ LSeg(p2,f/.(len f -' 1))
    = LSeg(f,len f -' 2) /\ LSeg(p2,f/.(len f -' 1)) by A130,XBOOLE_1:78
      .= {f/.(len f -' 1)} by A72,A92,A127,SPRECT_3:2;
  end;
A131: LSeg(NW-corner L~f,N-min L~f) misses LSeg(f/.(len f -' 1),p2)
  proof
    assume LSeg(NW-corner L~f,A) meets LSeg(f/.(len f -' 1),p2);
    then LSeg(p2,f/.(len f -' 1)) /\
    LSeg(NW-corner L~f,A) <> {} by XBOOLE_0:def 7;
    then consider p being Point of TOP-REAL 2 such that
A132: p in LSeg(p2,f/.(len f -' 1)) /\ LSeg(NW-corner L~f,A) by SUBSET_1:10;
A133: p in LSeg(p2,f/.(len f -' 1)) by A132,XBOOLE_0:def 3;
    p in LSeg(NW-corner L~f,A) by A132,XBOOLE_0:def 3;
    then p in { A } by A1,A63,A81,A89,A133,XBOOLE_0:def 3;
    then p = A by TARSKI:def 1;
    hence contradiction by A1,A63,A88,A97,A133,SPRECT_3:16;
  end;
A134: p2`2 <> A`2 by A93,A97,TOPREAL3:11;
  set G = GoB f;
A135: Int cell(GoB f,i,w-'1) misses L~f by A5,A60,GOBOARD7:14;
  L~f /\ (Int cell(G,i,w-'1) \/ { p2 })
  = L~f /\ Int cell(G,i,w-'1) \/ L~f /\ { p2 } by XBOOLE_1:23
    .= {} \/ L~f /\ { p2 } by A135,XBOOLE_0:def 7
    .= L~f /\ { p2 };
  then
A136: LSeg(z2,p2) /\ L~f c= L~f /\ { p2 } by A4,A5,A10,A18,A60,GOBOARD6:43
  ,XBOOLE_1:26;
  L~f /\ { p2 } c= { p2 } by XBOOLE_1:17;
  then
A137: LSeg(z2,p2) /\ L~f c= { p2 } by A136,XBOOLE_1:1;
A138: for i,j st 1 <= i & i < j & j < len f holds
  L~mid(f,i,j) misses LSeg(z2,p2)
  proof
    let l,j such that
A139: 1 <= l and
A140: l < j and
A141: j < len f;
    assume L~mid(f,l,j) meets LSeg(z2,p2);
    then L~mid(f,l,j) /\ LSeg(z2,p2) <> {} by XBOOLE_0:def 7;
    then consider p being Point of TOP-REAL 2 such that
A142: p in L~mid(f,l,j) /\ LSeg(z2,p2) by SUBSET_1:10;
A143: p in LSeg(z2,p2) by A142,XBOOLE_0:def 3;
    p in L~mid(f,l,j) by A142,XBOOLE_0:def 3;
    then consider ii being Element of NAT such that
A144: 1 <= ii and
A145: ii+1 <= len mid(f,l,j) and
A146: p in LSeg(mid(f,l,j),ii) by SPPOL_2:13;
A147: len mid(f,l,j) = j-'l+1 by A139,A140,A141,JORDAN4:20;
    then ii<j-'l+1 by A145,NAT_1:13;
    then
A148: p in LSeg(f,ii+l-'1) by A139,A140,A141,A144,A146,JORDAN4:31;
    then
A149: p in LSeg(f,ii+l-'1) /\ LSeg(z2,p2) by A143,XBOOLE_0:def 3;
    set k = ii+l-'1;
A150: ii + l >= ii + 1 by A139,XREAL_1:8;
    ii + 1 >= 1 + 1 by A144,XREAL_1:8;
    then ii + l >= 1 + 1 by A150,XXREAL_0:2;
    then
A151: k >= 1 by NAT_D:49;
    len f >= 1 + 1 by GOBOARD7:36,XXREAL_0:2;
    then
A152: len f -' 1 >= 1 by NAT_D:49;
    LSeg(f,k) /\ LSeg(z2,p2) c= L~f /\ LSeg(z2,p2) by TOPREAL3:26,XBOOLE_1:26;
    then LSeg(f,k) /\ LSeg(z2,p2) c= { p2 } by A137,XBOOLE_1:1;
    then p = p2 by A149,TARSKI:def 1;
    then p2 in LSeg(f,k) /\ LSeg(f/.(len f -' 1), f/.len f)
    by A88,A148,XBOOLE_0:def 3;
    then
A153: p2 in LSeg(f,k) /\ LSeg(f,len f -' 1) by A57,A152,TOPREAL1:def 5;
    then
A154: LSeg(f,k) meets LSeg(f,len f -' 1) by XBOOLE_0:4;
    per cases by A58,A154,GOBOARD5:def 4;
    suppose k <= 1;
      then
A155: k = 1 by A151,XXREAL_0:1;
      LSeg(f,1) /\ LSeg(f,len f -' 1) = {f.1} by JORDAN4:54
        .= {f/.1} by A14,PARTFUN1:def 8;
      hence contradiction by A1,A97,A153,A155,TARSKI:def 1;
    end;
    suppose k+1 >= len f -' 1;
      then
A156: k >= len f -' 1 -' 1 by NAT_D:53;
      ii+l >= l by NAT_1:11;
      then
A157: ii+l >= 1 by A139,XXREAL_0:2;
      ii <= j -' l by A145,A147,XREAL_1:8;
      then ii + l <= j by A140,NAT_D:54;
      then ii+l < len f by A141,XXREAL_0:2;
      then ii+l-'1 < len f -' 1 by A157,NAT_D:56;
      then ii+l-'1 <= len f -' 1 -' 1 by NAT_D:49;
      then k = len f -' 1 -' 1 by A156,XXREAL_0:1;
      then p2 in {f/.(len f -' 1)} by A99,A100,A101,A153,TOPREAL1:def 8;
      then p2 = f/.(len f -' 1) by TARSKI:def 1;
      hence contradiction by A6,A80,A97,SPRECT_3:15;
    end;
  end;
A158: <*p2*> is_in_the_area_of f by A91,A95,SPRECT_2:25,SPRECT_3:63;
  then
A159: <*p2*> is_in_the_area_of SpStSeq L~f by SPRECT_3:70;
  <*p2,z2*> = <*p2*>^<*z2*> by FINSEQ_1:def 9;
  then
A160: L~SpStSeq L~f /\ LSeg(p2,z2) c= { p2 } by A61,A79,A159,SPRECT_2:28
  ,SPRECT_3:64;
  ((GoB f)*(i,w-'1))`1 = ((GoB f)*(i,1))`1 by A4,A5,A18,NAT_D:35,GOBOARD5:3
    .= ((GoB f)*(i,w))`1 by A4,A5,A9,GOBOARD5:3;
  then ((GoB f)*(i,w)+(GoB f)*(i+1,w))`1
  = ((GoB f)*(i,w-'1))`1+((GoB f)*(i+1,w))`1 by TOPREAL3:7
    .= ((GoB f)*(i,w-'1)+(GoB f)*(i+1,w))`1 by TOPREAL3:7;
  then p1`1 = 1/2*((GoB f)*(i,w-'1)+(GoB f)*(i+1,w))`1 by TOPREAL3:9
    .= z2`1 by TOPREAL3:9;
  then
A161: LSeg(p1,z2) is vertical by SPPOL_1:37;
A162: f/.2 in LSeg(f/.1, f/.(1+1)) by TOPREAL1:6;
  p1 = 1/2*((GoB f)*(i,w))+(1-1/2)*((GoB f)*(i+1,w)) by EUCLID:36;
  then
A163: p1 in LSeg(f/.1, f/.2) by A1,A6,A16,SPPOL_1:22;
  then
A164: LSeg(p1,f/.2) c= LSeg(f/.1, f/.2) by A162,TOPREAL1:12;
A165: LSeg(f/.1, f/.(1+1)) = LSeg(f,1) by A8,TOPREAL1:def 5;
  then
A166: LSeg(f/.1, f/.2) c= L~f by TOPREAL3:26;
  then
A167: LSeg(p1,f/.2) c= L~f by A164,XBOOLE_1:1;
A168: p1`2 = 1/2*(A+(GoB f)*(i+1,w))`2 by A6,TOPREAL3:9
    .= 1/2*(A`2+A`2) by A76,TOPREAL3:7
    .= N-bound L~f by EUCLID:56;
A169: p1 in LSeg(p1,f/.2) by TOPREAL1:6;
A170: p1 <> A by A1,A6,A14,A16,A53,GOBOARD7:31,SPRECT_3:15;
A171: for i,j st 2 < i & i <= j & j<= len f holds
  L~mid(f,i,j) misses LSeg(p1,f/.2)
  proof
    let l,j such that
A172: 2 < l and
A173: l <= j and
A174: j<= len f;
A175: 1 < l by A172,XXREAL_0:2;
    assume L~mid(f,l,j) meets LSeg(p1,f/.2);
    then L~mid(f,l,j) /\ LSeg(p1,f/.2) <> {} by XBOOLE_0:def 7;
    then consider p being Point of TOP-REAL 2 such that
A176: p in L~mid(f,l,j) /\ LSeg(p1,f/.2) by SUBSET_1:10;
A177: p in LSeg(p1,f/.2) by A176,XBOOLE_0:def 3;
    p in L~mid(f,l,j) by A176,XBOOLE_0:def 3;
    then consider ii being Element of NAT such that
A178: 1 <= ii and
A179: ii+1 <= len mid(f,l,j) and
A180: p in LSeg(mid(f,l,j),ii) by SPPOL_2:13;
    set k = ii+l-'1;
A181: len mid(f,l,j) = j-'l+1 by A173,A174,A175,JORDAN4:20;
    then
A182: ii<j-'l+1 by A179,NAT_1:13;
A183: ii + l > ii + 2 by A172,XREAL_1:8;
    ii + 2 >= 1 + 2 by A178,XREAL_1:8;
    then ii + l > 1 + 2 by A183,XXREAL_0:2;
    then
A184: k > 1+1 by NAT_D:52;
    per cases by A173,XXREAL_0:1;
    suppose l = j;
      then len mid(f,l,j) = 1 by A174,A175,JORDAN4:27;
      then L~mid(f,l,j) = {} by TOPREAL1:28;
      hence thesis by A180,SPPOL_2:17;
    end;
    suppose l < j;
      then LSeg(mid(f,l,j),ii)=LSeg(f,k) by A174,A175,A178,A182,JORDAN4:31;
      then
A185: p in LSeg(f,k) /\ LSeg(f,1) by A164,A165,A177,A180,XBOOLE_0:def 3;
      then LSeg(f,k) meets LSeg(f,1) by XBOOLE_0:4;
      then k+1 >= len f by A184,GOBOARD5:def 4;
      then
A186: k >= len f -' 1 by NAT_D:53;
      ii <= j -' l by A179,A181,XREAL_1:8;
      then ii + l <= j by A173,NAT_D:54;
      then ii+l <= len f by A174,XXREAL_0:2;
      then ii+l-'1 <= len f -' 1 by NAT_D:42;
      then
A187: k = len f -' 1 by A186,XXREAL_0:1;
      LSeg(f,1) /\ LSeg(f,len f -' 1) = { f.1 } by JORDAN4:54
        .= {f/.1} by A14,PARTFUN1:def 8;
      then p = f/.1 by A185,A187,TARSKI:def 1;
      hence contradiction by A1,A163,A170,A177,SPRECT_3:16;
    end;
  end;
A188: for j st 2 < j & j <= len f holds L~mid(f,2,j) /\ LSeg(p1,f/.2) = {f/.2}
  proof
    let j such that
A189: 2 < j and
A190: j <= len f;
    2+1 <= j by A189,NAT_1:13;
    then
A191: LSeg(p1,f/.2) misses L~mid(f,2+1,j) by A171,A190;
A192: f/.2 in LSeg(p1,f/.2) by TOPREAL1:6;
    L~mid(f,2,j) = LSeg(f,2) \/ L~mid(f,2+1,j) by A189,A190,SPRECT_3:35;
    hence L~mid(f,2,j) /\ LSeg(p1,f/.2)
    = LSeg(f,2) /\ LSeg(p1,f/.2) by A191,XBOOLE_1:78
      .= {f/.2} by A69,A162,A163,A165,A192,SPRECT_3:2,TOPREAL1:12;
  end;
A193: LSeg(p1,f/.2) misses LSeg(NW-corner L~f,A)
  proof
    assume LSeg(p1,f/.2) meets LSeg(NW-corner L~f,A);
    then LSeg(p1,f/.2) /\ LSeg(NW-corner L~f,A) <> {} by XBOOLE_0:def 7;
    then consider p being Point of TOP-REAL 2 such that
A194: p in LSeg(p1,f/.2) /\ LSeg(NW-corner L~f,A) by SUBSET_1:10;
A195: p in LSeg(p1,f/.2) by A194,XBOOLE_0:def 3;
    p in LSeg(NW-corner L~f,A) by A194,XBOOLE_0:def 3;
    then p in { A } by A1,A84,A164,A195,XBOOLE_0:def 3;
    then p = A by TARSKI:def 1;
    hence contradiction by A1,A163,A170,A195,SPRECT_3:16;
  end;
A196: LSeg(NW-corner L~f,N-min L~f) misses LSeg(z2,p1)
  proof
    assume LSeg(NW-corner L~f,N-min L~f) meets LSeg(z2,p1);
    then LSeg(NW-corner L~f,N-min L~f) /\ LSeg(z2,p1) <> {} by XBOOLE_0:def 7;
    then consider p being Point of TOP-REAL 2 such that
A197: p in LSeg(NW-corner L~f,N-min L~f) /\ LSeg(z2,p1) by SUBSET_1:10;
A198: p in LSeg(NW-corner L~f,N-min L~f) by A197,XBOOLE_0:def 3;
A199: p in LSeg(z2,p1) by A197,XBOOLE_0:def 3;
    LSeg(NW-corner L~f,NE-corner L~f) /\ LSeg(z2,p1) = {p1}
    by A74,A161,A163,SPRECT_3:23,29;
    then p in {p1} by A66,A198,A199,XBOOLE_0:def 3;
    then p1 in LSeg(NW-corner L~f,N-min L~f) by A198,TARSKI:def 1;

then p1 in LSeg(NW-corner L~f,N-min L~f) /\ L~f by A167,A169,XBOOLE_0:def 3;
    then p1 in { N-min L~f } by PSCOMP_1:102;
    hence contradiction by A170,TARSKI:def 1;
  end;
A200: for i,j st 1 < i & i < j & j<= len f holds
  L~mid(f,i,j) misses LSeg(z2,p1)
  proof
    let l,j such that
A201: 1 < l and
A202: l < j and
A203: j<= len f;
    assume L~mid(f,l,j) meets LSeg(z2,p1);
    then L~mid(f,l,j) /\ LSeg(z2,p1) <> {} by XBOOLE_0:def 7;
    then consider p being Point of TOP-REAL 2 such that
A204: p in L~mid(f,l,j) /\ LSeg(z2,p1) by SUBSET_1:10;
A205: p in LSeg(z2,p1) by A204,XBOOLE_0:def 3;
    p in L~mid(f,l,j) by A204,XBOOLE_0:def 3;
    then consider ii being Element of NAT such that
A206: 1 <= ii and
A207: ii+1 <= len mid(f,l,j) and
A208: p in LSeg(mid(f,l,j),ii) by SPPOL_2:13;
A209: len mid(f,l,j) = j-'l+1 by A201,A202,A203,JORDAN4:20;
    then ii<j-'l+1 by A207,NAT_1:13;
    then
A210: p in LSeg(f,ii+l-'1) by A201,A202,A203,A206,A208,JORDAN4:31;
    then
A211: p in LSeg(f,ii+l-'1) /\ LSeg(z2,p1) by A205,XBOOLE_0:def 3;
    set k = ii+l-'1, G = GoB f;
A212: ii + l > ii + 1 by A201,XREAL_1:8;
    ii + 1 >= 1 + 1 by A206,XREAL_1:8;
    then ii + l > 1 + 1 by A212,XXREAL_0:2;
    then
A213: k > 1 by NAT_D:52;
A214: Int cell(GoB f,i,w-'1) misses L~f by A5,A60,GOBOARD7:14;
    L~f /\ (Int cell(G,i,w-'1) \/ { p1 })
    = L~f /\ Int cell(G,i,w-'1) \/ L~f /\ { p1 } by XBOOLE_1:23
      .= {} \/ L~f /\ { p1 } by A214,XBOOLE_0:def 7
      .= L~f /\ { p1 };
    then
A215: LSeg(z2,p1) /\ L~f c= L~f /\ { p1 } by A4,A5,A10,A18,A60,GOBOARD6:44
    ,XBOOLE_1:26;
    L~f /\ { p1 } c= { p1 } by XBOOLE_1:17;
    then
A216: LSeg(z2,p1) /\ L~f c= { p1 } by A215,XBOOLE_1:1;
A217: len f >= 2 by GOBOARD7:36,XXREAL_0:2;
    LSeg(f,k) /\ LSeg(z2,p1) c= L~f /\ LSeg(z2,p1) by TOPREAL3:26,XBOOLE_1:26;
    then LSeg(f,k) /\ LSeg(z2,p1) c= { p1 } by A216,XBOOLE_1:1;
    then p = p1 by A211,TARSKI:def 1;
    then p1 in LSeg(f,k) /\ LSeg(f/.1,f/.(1+1)) by A163,A210,XBOOLE_0:def 3;
    then
A218: p1 in LSeg(f,k) /\ LSeg(f,1) by A217,TOPREAL1:def 5;
    then LSeg(f,k) meets LSeg(f,1) by XBOOLE_0:4;
    then
A219: k <= 1+1 or k+1 >= len f by GOBOARD5:def 4;
    per cases by A219;
    suppose
A220: k <= 2;
      k >= 1+1 by A213,NAT_1:13;
      then
A221: k = 2 by A220,XXREAL_0:1;
      1 + 2 <= len f by GOBOARD7:36,XXREAL_0:2;
      then p1 in {f/.(1+1)} by A218,A221,TOPREAL1:def 8;
      then p1 = f/.(1+1) by TARSKI:def 1;
      hence contradiction by A6,A16,A170,SPRECT_3:15;
    end;
    suppose k+1 >= len f;
      then
A222: k >= len f -' 1 by NAT_D:53;
      ii <= j -' l by A207,A209,XREAL_1:8;
      then ii + l <= j by A202,NAT_D:54;
      then ii+l <= len f by A203,XXREAL_0:2;
      then ii+l-'1 <= len f -' 1 by NAT_D:42;
      then
A223: k = len f -' 1 by A222,XXREAL_0:1;
      LSeg(f,1) /\ LSeg(f,len f -' 1) = { f.1 } by JORDAN4:54
        .= {f/.1} by A14,PARTFUN1:def 8;
      hence contradiction by A1,A170,A218,A223,TARSKI:def 1;
    end;
  end;
  LSeg(f/.1,f/.2) c= L~SpStSeq L~f by A1,SPRECT_3:48;
  then
A224: L~SpStSeq L~f /\ LSeg(p1,z2) = { p1 } by A61,A79,A163,SPRECT_3:65;
A225: LSeg(p1,f/.2) is horizontal by A77,A168,SPPOL_1:36;
A226: p1 in LSeg(p1,f/.2) by TOPREAL1:6;
  then
A227: p1 in L~f by A167;
A228: <*p1*> is_in_the_area_of f by A167,A226,SPRECT_2:25,SPRECT_3:63;
A229: L~f misses L~Red' by A47,A49,SPRECT_3:42,XBOOLE_1:63;
  reconsider Red' as S-Sequence_in_R2 by A48;
  len Red' in dom Red' by FINSEQ_5:6;
  then
A230: z2 in L~Red' by A51,SPPOL_2:48;
  set u1 = Last_Point(L~Red',Red'/.1,Red'/.len Red',L~SpStSeq L~f),
  Red = L_Cut(Red',u1),
  u2 = First_Point(L~Red,Red/.1,Red/.len Red,LSeg(z2,p1)),
  u3 = First_Point(L~Red,Red/.1,Red/.len Red,LSeg(z2,p2));
  NW-corner L~SpStSeq L~f = NW-corner L~f by SPRECT_1:70;
  then
A231: u1 <> NW-corner L~f by A21,A44,A50,A51,SPRECT_3:55;
A232: L~Red' is_an_arc_of z1,z2 by A50,A51,TOPREAL1:31;
  L~Red' /\ L~SpStSeq L~f is closed by TOPS_1:35;
  then
A233: u1 in L~Red' /\ L~SpStSeq L~f by A50,A51,A52,A232,JORDAN5C:def 2;
  then
A234: u1 in L~SpStSeq L~f by XBOOLE_0:def 3;
A235: u1 in L~Red' by A233,XBOOLE_0:def 3;
A236: now
    assume u1 in L~f;
    then u1 in L~f /\ L~Red' by A235,XBOOLE_0:def 3;
    hence contradiction by A229,XBOOLE_0:def 7;
  end;
  len Red' in dom Red' by FINSEQ_5:6;
  then u1 <> Red'.len Red' by A51,A61,A234,PARTFUN1:def 8;
  then reconsider Red as S-Sequence_in_R2 by A235,JORDAN3:69;
  1 in dom Red by FINSEQ_5:6;
  then
A237: Red/.1 =Red.1 by PARTFUN1:def 8
    .= u1 by A235,JORDAN3:58;
A238: L~SpStSeq L~f /\ L~Red = { u1 } by A51,A52,A61,Th6;
  len Red' in dom Red' by FINSEQ_5:6;
  then z2 = Red'.len Red' by A51,PARTFUN1:def 8;
  then
A239: z2 in L~Red by A61,A230,A234,A235,JORDAN5B:25;
  Red is_in_the_area_of SpStSeq L~f by A21,A44,A50,A51,SPRECT_3:71;
  then
A240: Red is_in_the_area_of f by SPRECT_3:70;
A241: L~Red c= L~Red' by A235,JORDAN3:77;
A242: L~f misses L~Red by A229,A235,JORDAN3:77,XBOOLE_1:63;
  z2 in LSeg(p1,z2) by TOPREAL1:6;
  then
A243: LSeg(p1,z2) meets L~Red by A239,XBOOLE_0:3;
  z2 in LSeg(p2,z2) by TOPREAL1:6;
  then
A244: LSeg(p2,z2) meets L~Red by A239,XBOOLE_0:3;
A245: L~Red /\ LSeg(p1,z2) is closed by TOPS_1:35;
A246: L~Red /\ LSeg(p2,z2) is closed by TOPS_1:35;
  L~Red is_an_arc_of Red/.1,Red/.len Red by TOPREAL1:31;
  then
A247: u3 in LSeg(p2,z2) /\ L~Red by A244,A246,JORDAN5C:def 1;
  then
A248: u3 in L~Red by XBOOLE_0:def 3;
A249: u3 in LSeg(p2,z2) by A247,XBOOLE_0:def 3;
A250: u3 <> p2 by A91,A95,A229,A241,A248,XBOOLE_0:3;
A251: p2 in LSeg(p2,z2) by TOPREAL1:6;
  then
A252: LSeg(p2,u3) c= LSeg(p2,z2) by A249,TOPREAL1:12;
  then
A253: LSeg(NW-corner L~f,N-min L~f) misses LSeg(u3,p2) by A68,A85,A134,
  SPRECT_3:21,XBOOLE_1:63;
A254: for i,j st 1 <= i & i < j & j < len f holds
  L~mid(f,i,j) misses LSeg(u3,p2)
  proof
    let i,j;
    assume 1 <= i & i < j & j < len f;
    then L~mid(f,i,j) misses LSeg(z2,p2) by A138;
    hence thesis by A249,A251,TOPREAL1:12,XBOOLE_1:63;
  end;
A255: now
    assume
A256: u3 = Red.1;
    1 in dom Red by FINSEQ_5:6;
    then u3 in L~SpStSeq L~f by A234,A237,A256,PARTFUN1:def 8;
    then u3 in LSeg(p2,z2) /\ L~SpStSeq L~f by A249,XBOOLE_0:def 3;
    hence contradiction by A160,A250,TARSKI:def 1;
  end;
  L~Red is_an_arc_of Red/.1,Red/.len Red by TOPREAL1:31;
  then
A257: u2 in LSeg(p1,z2) /\ L~Red by A243,A245,JORDAN5C:def 1;
  then
A258: u2 in L~Red by XBOOLE_0:def 3;
A259: u2 in LSeg(p1,z2) by A257,XBOOLE_0:def 3;
A260: u2 <> p1 by A167,A226,A229,A241,A258,XBOOLE_0:3;
A261: p1 in LSeg(p1,z2) by TOPREAL1:6;
  then
A262: LSeg(p1,u2) c= LSeg(p1,z2) by A259,TOPREAL1:12;
A263: LSeg(NW-corner L~f,N-min L~f) misses LSeg(u2,p1) by A196,A259,A261,
  TOPREAL1:12,XBOOLE_1:63;
A264: for i,j st 1 < i & i < j & j<= len f holds
  L~mid(f,i,j) misses LSeg(u2,p1)
  proof
    let i,j;
    assume 1 < i & i < j & j<= len f;
    then L~mid(f,i,j) misses LSeg(z2,p1) by A200;
    hence thesis by A259,A261,TOPREAL1:12,XBOOLE_1:63;
  end;
A265: now
    assume
A266: u2 = Red.1;
    1 in dom Red by FINSEQ_5:6;
    then u2 in L~SpStSeq L~f by A234,A237,A266,PARTFUN1:def 8;
    then u2 in { p1 } by A224,A259,XBOOLE_0:def 3;
    hence contradiction by A260,TARSKI:def 1;
  end;
  reconsider Red2=R_Cut(Red,u3) as S-Sequence_in_R2 by A248,A255,JORDAN3:70;
A267: Rev Red2 is S-Sequence_in_R2 by SPPOL_2:47;
A268: Red2/.1 = u1 by A237,A248,SPRECT_3:39;
A269: len Red2 in dom Red2 by FINSEQ_5:6;
A270: (Rev Red2)/.1 = Red2/.len Red2 by FINSEQ_5:68
    .= Red2.len Red2 by A269,PARTFUN1:def 8
    .= u3 by A248,JORDAN3:59;
  then
A271: ((Rev Red2)/.1)`2 = p2`2 & (Rev Red2)/.1 <> p2
  by A85,A91,A95,A229,A241,A248,A249,SPPOL_1:63,XBOOLE_0:3;
A272: L~Rev Red2 = L~Red2 by SPPOL_2:22;
  u3 in L~Red2 & u3 in LSeg(p2,u3) by A248,A255,JORDAN5B:23,TOPREAL1:6;
  then
A273: u3 in L~Red2 /\ LSeg(p2,u3) by XBOOLE_0:def 3;
  now
    assume u1 in LSeg(p2,z2);
    then u1 in L~SpStSeq L~f /\ LSeg(p2,z2) by A234,XBOOLE_0:def 3;
    hence contradiction by A96,A160,A236,TARSKI:def 1;
  end;
  then LSeg(p2,z2) /\ L~Red2={ u3 } by A237,A244,Th1;
  then LSeg(p2,u3) /\ L~Red2 c={ u3 } by A249,A251,TOPREAL1:12,XBOOLE_1:26;
  then LSeg(p2,u3) /\ L~Red2={ u3 } by A273,ZFMISC_1:39;
  then reconsider RB2 = <*p2*>^Rev Red2 as S-Sequence_in_R2
  by A267,A270,A271,A272,SPRECT_3:31;
A274: L~Red2 c= L~Red by A248,JORDAN3:76;
  LSeg(p2,f/.(len f -' 1)) misses L~Red by A89,A90,A242,XBOOLE_1:1,63;
  then LSeg(p2,f/.(len f -' 1)) misses L~Red2 by A248,JORDAN3:76,XBOOLE_1:63;
  then
A275: LSeg(p2,f/.(len f -' 1)) /\ L~Red2 = {} by XBOOLE_0:def 7;
  1 in dom Red2 by FINSEQ_5:6;
  then u1 in L~Red2 by A268,SPPOL_2:48;
  then u1 in L~SpStSeq L~f /\ L~Red2 & L~SpStSeq L~f /\ L~Red2 c= { u1 }
  by A234,A238,A274,XBOOLE_0:def 3,XBOOLE_1:26;
  then
A276: L~SpStSeq L~f /\ L~Red2 = { u1 } by ZFMISC_1:39;
  reconsider Red1=R_Cut(Red,u2) as S-Sequence_in_R2 by A258,A265,JORDAN3:70;
  len Red1 in dom Red1 by FINSEQ_5:6;
  then
A277: Red1/.len Red1 = Red1.len Red1 by PARTFUN1:def 8
    .= u2 by A258,JORDAN3:59;
A278: Red1/.1 = u1 by A237,A258,SPRECT_3:39;
A279: (Red1/.len Red1)`1 = p1`1 & Red1/.len Red1 <> p1
  by A161,A167,A226,A229,A241,A258,A259,A277,SPPOL_1:64,XBOOLE_0:3;
  u2 in L~Red1 & u2 in LSeg(p1,u2) by A258,A265,JORDAN5B:23,TOPREAL1:6;
  then
A280: u2 in L~Red1 /\ LSeg(p1,u2) by XBOOLE_0:def 3;
  now
    assume u1 in LSeg(p1,z2);
    then u1 in L~SpStSeq L~f /\ LSeg(p1,z2) by A234,XBOOLE_0:def 3;
    hence contradiction by A224,A227,A236,TARSKI:def 1;
  end;
  then LSeg(p1,z2) /\ L~Red1={ u2 } by A237,A243,Th1;
  then LSeg(p1,u2) /\ L~Red1 c={ u2 } by A259,A261,TOPREAL1:12,XBOOLE_1:26;
  then LSeg(p1,u2) /\ L~Red1={ u2 } by A280,ZFMISC_1:39;
  then reconsider RB1 = Red1^<*p1*> as S-Sequence_in_R2
  by A277,A279,SPRECT_2:65;
A281: L~Red1 c= L~Red by A258,JORDAN3:76;
  LSeg(p1,f/.2) misses L~Red by A164,A166,A242,XBOOLE_1:1,63;
  then LSeg(p1,f/.2) misses L~Red1 by A258,JORDAN3:76,XBOOLE_1:63;
  then
A282: LSeg(p1,f/.2) /\ L~Red1 = {} by XBOOLE_0:def 7;
  1 in dom Red1 by FINSEQ_5:6;
  then
A283: u1 in L~Red1 by A278,SPPOL_2:48;
  then u1 in L~SpStSeq L~f /\ L~Red1 & L~SpStSeq L~f /\ L~Red1 c= { u1 }
  by A234,A238,A281,XBOOLE_0:def 3,XBOOLE_1:26;
  then
A284: L~SpStSeq L~f /\ L~Red1 = { u1 } by ZFMISC_1:39;
  len Rev Red2 = len Red2 by FINSEQ_5:def 3;
  then
A285: RB2/.len RB2 = (Rev Red2)/.len Red2 by SPRECT_3:11
    .= u1 by A268,FINSEQ_5:68;
A286: RB2/.1 = p2 by FINSEQ_5:16;
  L~Rev Red2 = L~Red2 by SPPOL_2:22;
  then
A287: L~RB2 = L~Red2 \/ LSeg(p2,u3) by A270,SPPOL_2:20;
  then
A288: LSeg(p2,f/.(len f -' 1)) /\ L~RB2
  = {} \/ LSeg(p2,f/.(len f -' 1)) /\ LSeg(u3,p2) by A275,XBOOLE_1:23
    .={ p2 } by A85,A94,A252,SPRECT_1:11,SPRECT_3:22;
  <*u3*> is_in_the_area_of f by A240,A248,SPRECT_3:63;
  then Red2 is_in_the_area_of f by A240,A248,SPRECT_3:69;
  then Rev Red2 is_in_the_area_of f by SPRECT_3:68;
  then
A289: RB2 is_in_the_area_of f by A158,SPRECT_2:28;
  1 in dom Red1 by FINSEQ_5:6;
  then
A290: RB1/.1 = u1 by A278,FINSEQ_4:83;
  len <*p1*> = 1 by FINSEQ_1:56;
  then len RB1 = len Red1 + 1 by FINSEQ_1:35;
  then
A291: RB1/.len RB1 = p1 by FINSEQ_4:82;
A292: L~RB1 = L~Red1 \/ LSeg(u2,p1) by A277,SPPOL_2:19;
  then
A293: LSeg(p1,f/.2) /\ L~RB1
  = {} \/ LSeg(p1,f/.2) /\ LSeg(u2,p1) by A282,XBOOLE_1:23
    .={ p1 } by A161,A225,A262,SPRECT_1:12,SPRECT_3:22;
  <*u2*> is_in_the_area_of f by A240,A258,SPRECT_3:63;
  then Red1 is_in_the_area_of f by A240,A258,SPRECT_3:69;
  then
A294: RB1 is_in_the_area_of f by A228,SPRECT_2:28;
A295: L~Red' is_an_arc_of z1,z2 by A46,A49,TOPREAL4:3;
  L~Red' /\ L~SpStSeq L~f is closed by TOPS_1:35;
  then u1 in L~Red' /\ L~SpStSeq L~f by A50,A51,A52,A295,JORDAN5C:def 2;
  then u1 in L~SpStSeq L~f by XBOOLE_0:def 3;
  then u1 in
  (LSeg(NW-corner L~f,NE-corner L~f) \/ LSeg(NE-corner L~f,SE-corner L~f)) \/
  (LSeg(SE-corner L~f,SW-corner L~f) \/ LSeg(SW-corner L~f,NW-corner L~f))
  by SPRECT_1:43;
  then
A296: u1 in LSeg(NW-corner L~f,NE-corner L~f) \/ LSeg(NE-corner L~f,
  SE-corner L~f) or
  u1 in LSeg(SE-corner L~f,SW-corner L~f) \/ LSeg(SW-corner L~f,NW-corner L~f)
  by XBOOLE_0:def 2;
A297: N-most L~f c= LSeg(NW-corner L~f,NE-corner L~f) &
  A in N-most L~f by PSCOMP_1:101,XBOOLE_1:17;
  then LSeg(NW-corner L~f,NE-corner L~f)
  = LSeg(NW-corner L~f,A) \/ LSeg(A,NE-corner L~f) by TOPREAL1:11;
  then
A298: u1 in LSeg(NW-corner L~f,NE-corner L~f) implies
  u1 in LSeg(NW-corner L~f,A) or u1 in LSeg(A,NE-corner L~f) by XBOOLE_0:def 2;
  per cases by A296,A298,XBOOLE_0:def 2;
  suppose
A299: u1 in LSeg(NW-corner L~f,A);
    set i1 = (S-min L~f)..f, i2 = (E-min L~f)..f;
A300: S-min L~f in rng f by SPRECT_2:45;
    then
A301: i1 in dom f by FINSEQ_4:30;
A302: E-min L~f in rng f by SPRECT_2:49;
    then
A303: i2 in dom f by FINSEQ_4:30;
    (S-max L~f)..f < i1 & i2 <= (S-max L~f)..f by A1,SPRECT_2:76,77;
    then
A304: i2 < i1 by XXREAL_0:2;
    (W-min L~f)..f < len f & i1 <= (W-min L~f)..f by A1,SPRECT_2:78,80;
    then
A305: i1 < len f by XXREAL_0:2;
    (N-max L~f)..f > 1 by A1,SPRECT_2:73;
    then (N-max L~f)..f >= 1+1 by NAT_1:13;
    then 2 <= (E-max L~f)..f by A1,SPRECT_2:74,XXREAL_0:2;
    then
A306: 2 < i2 by A1,SPRECT_2:75,XXREAL_0:2;
    then
A307: 2 < i1 by A304,XXREAL_0:2;
    then 1 < i1 by XXREAL_0:2;

then reconsider M1 = mid(f,i1,len f) as S-Sequence_in_R2 by A305,JORDAN4:52;
    (NW-corner L~f)`2 = N-bound L~f & A`2 = N-bound L~f by EUCLID:56;
    then
A308: u1`2 = (NW-corner L~f)`2 by A299,GOBOARD7:6;
    u1 in LSeg(NW-corner L~f,u1) by TOPREAL1:6;
    then u1 in LSeg(NW-corner L~f,u1) /\ L~Red1 by A283,XBOOLE_0:def 3;
    then
A309: { u1 } c= LSeg(NW-corner L~f,u1) /\ L~Red1 by ZFMISC_1:37;
A310: NW-corner L~f in LSeg(NW-corner L~f,NE-corner L~f) by TOPREAL1:6;
    then LSeg(NW-corner L~f,A) c= LSeg(NW-corner L~f,NE-corner L~f)
    by A297,TOPREAL1:12;
    then LSeg(NW-corner L~f,u1) c= LSeg(NW-corner L~f,NE-corner L~f)
    by A299,A310,TOPREAL1:12;
    then LSeg(NW-corner L~f,u1) c= L~SpStSeq L~f by A62,XBOOLE_1:1;
    then LSeg(NW-corner L~f,u1) /\ L~Red1 c= { u1 } by A284,XBOOLE_1:26;
    then
A311: LSeg(NW-corner L~f,u1) /\ L~Red1={ u1 } by A309,XBOOLE_0:def 10;
A312: NW-corner L~f in LSeg(NW-corner L~f,A) by TOPREAL1:6;
    then
A313: LSeg(NW-corner L~f,u1) c= LSeg(NW-corner L~f,A) by A299,TOPREAL1:12;
    LSeg(NW-corner L~f,u1) misses LSeg(u2,p1) by A263,A299,A312,TOPREAL1:12
    ,XBOOLE_1:63;
    then LSeg(NW-corner L~f,u1) /\ LSeg(u2,p1) = {} by XBOOLE_0:def 7;
    then LSeg(NW-corner L~f,u1) /\ L~RB1
    = { u1 } \/ {} by A292,A311,XBOOLE_1:23
      .= { u1 };
    then reconsider M3 = <*NW-corner L~f*>^RB1 as S-Sequence_in_R2
    by A231,A290,A308,SPRECT_3:31;
A314: i2 <= len f by A303,FINSEQ_3:27;
    then reconsider M4 = mid(f,2,i2) as S-Sequence_in_R2 by A306,JORDAN4:52;
A315: M3/.len M3 = p1 by A291,SPRECT_3:11;
A316: M4/.1 = f/.2 by A53,A303,SPRECT_2:12;
A317: L~M3 = LSeg(NW-corner L~f,RB1/.1) \/ L~RB1 by SPPOL_2:20;
A318: now
      assume L~f meets LSeg(NW-corner L~f,u1);
      then consider u being set such that
A319: u in L~f and
A320: u in LSeg(NW-corner L~f,u1) by XBOOLE_0:3;
      reconsider u as Point of TOP-REAL 2 by A319;
A321: u in LSeg(NW-corner L~f,A) /\ L~f by A313,A319,A320,XBOOLE_0:def 3;
      LSeg(NW-corner L~f,A) /\ L~f = {A} by PSCOMP_1:102;
      then u = A by A321,TARSKI:def 1;
      hence contradiction by A236,A299,A319,A320,SPRECT_3:16;
    end;
    then
A322: L~M4 misses LSeg(NW-corner L~f,u1) by A53,A303,SPRECT_3:34,XBOOLE_1:63;
    L~M4 misses L~Red by A53,A242,A303,SPRECT_3:34,XBOOLE_1:63;
    then
A323: L~M4 misses L~Red1 by A258,JORDAN3:76,XBOOLE_1:63;
    L~M4 misses LSeg(u2,p1) by A264,A306,A314;
    then L~RB1 misses L~M4 by A292,A323,XBOOLE_1:70;
    then
A324: L~M3 misses L~M4 by A290,A317,A322,XBOOLE_1:70;
    LSeg(p1,f/.2) misses LSeg(NW-corner L~f,u1) by A193,A299,A312,TOPREAL1:12
    ,XBOOLE_1:63;
    then LSeg(p1,f/.2) /\ LSeg(NW-corner L~f,u1) = {} by XBOOLE_0:def 7;
    then
A325: LSeg(p1,f/.2) /\ L~M3
    = {} \/ LSeg(p1,f/.2) /\ L~RB1 by A290,A317,XBOOLE_1:23
      .= { p1 } by A293;
    LSeg(p1,f/.2) /\ L~M4 = { f/.2 }
    & (M3/.len M3)`2 = (M4/.1)`2 by A77,A168,A188,A291,A306,A314,A316,
    SPRECT_3:11;
    then reconsider M2 = M3^M4 as S-Sequence_in_R2
    by A315,A316,A324,A325,SPRECT_3:38;
A326: len M2 >= 2 & len M1 >= 2 by TOPREAL1:def 10;
A327: M1 is_in_the_area_of f by A54,A301,SPRECT_2:27;
A328: (M1/.1)`2 = (f/.i1)`2 by A54,A301,SPRECT_2:12
      .= (S-min L~f)`2 by A300,FINSEQ_5:41
      .= S-bound L~f by EUCLID:56;
    (M1/.len M1)`2 = (f/.len f)`2 by A54,A301,SPRECT_2:13
      .= (f/.1)`2 by FINSEQ_6:def 1
      .= N-bound L~f by A1,EUCLID:56;
    then
A329: M1 is_a_v.c._for f by A327,A328,SPRECT_2:def 3;
    <*NW-corner L~f*>^RB1 is_in_the_area_of f &
    mid(f,2,i2) is_in_the_area_of f by A53,A59,A294,A303,SPRECT_2:27,28;
    then
A330: M2 is_in_the_area_of f by SPRECT_2:28;
    M2 = <*NW-corner L~f*>^(RB1^mid(f,2,i2)) by FINSEQ_1:45;
    then
A331: (M2/.1)`1 = (NW-corner L~f)`1 by FINSEQ_5:16
      .= W-bound L~f by EUCLID:56;
    (M2/.len M2)`1 = (mid(f,2,i2)/.len mid(f,2,i2))`1 by SPRECT_3:11
      .= (f/.i2)`1 by A53,A303,SPRECT_2:13
      .= (E-min L~f)`1 by A302,FINSEQ_5:41
      .= E-bound L~f by EUCLID:56;
    then M2 is_a_h.c._for f by A330,A331,SPRECT_2:def 2;
    then
A332: L~M1 meets L~M2 by A326,A329,SPRECT_2:33;
    L~M2 = L~M3 \/ LSeg(M3/.len M3,M4/.1) \/ L~M4 &
    L~M1 misses L~M4 by A304,A305,A306,SPPOL_2:23,SPRECT_2:51;
    then L~M1 meets L~M3 \/ LSeg(M3/.len M3,M4/.1) &
    L~M1 misses LSeg(p1,f/.2) by A171,A305,A307,A332,XBOOLE_1:70;
    then
A333: L~M1 meets L~M3 by A315,A316,XBOOLE_1:70;
    1 < i1 by A307,XXREAL_0:2;
    then
A334: L~M1 misses LSeg(u2,p1) by A264,A305;
    L~M1 c= L~f by A54,A301,SPRECT_3:34;
    then L~M1 misses LSeg(NW-corner L~f,u1) &
    L~M3 = LSeg(NW-corner L~f,RB1/.1) \/ L~RB1 by A318,SPPOL_2:20,XBOOLE_1:63;
    then L~M1 meets L~RB1 by A290,A333,XBOOLE_1:70;
    then L~M1 meets L~Red1 by A292,A334,XBOOLE_1:70;
    then L~M1 meets L~Red by A258,JORDAN3:76,XBOOLE_1:63;
    hence contradiction by A54,A242,A301,SPRECT_3:34,XBOOLE_1:63;
  end;
  suppose that
A335: u1 in LSeg(A,NE-corner L~f) and
A336: N-min L~f = NW-corner L~f;
    set i1 = (S-max L~f)..f, i2 = (E-max L~f)..f;
A337: S-max L~f in rng f by SPRECT_2:46;
    then
A338: i1 in dom f by FINSEQ_4:30;
A339: E-max L~f in rng f by SPRECT_2:50;
    then
A340: i2 in dom f by FINSEQ_4:30;
    (E-max L~f)..f < (E-min L~f)..f by A1,SPRECT_2:75;
    then
A341: i2 < i1 by A1,SPRECT_2:76,XXREAL_0:2;
    (S-min L~f)..f <= (W-min L~f)..f by A1,SPRECT_2:78;
    then (S-max L~f)..f < (W-min L~f)..f by A1,SPRECT_2:77,XXREAL_0:2;
    then (S-max L~f)..f + 1 <= (W-min L~f)..f by NAT_1:13;
    then (S-max L~f)..f + 1 < len f by A1,SPRECT_2:80,XXREAL_0:2;
    then
A342: i1 < len f -' 1 by A57,XREAL_1:8;
    (N-max L~f)..f > 1 by A1,SPRECT_2:73;
    then
A343: 1 < i2 by A1,SPRECT_2:74,XXREAL_0:2;
    then
A344: 1 < i1 by A341,XXREAL_0:2;
    then reconsider M4 = mid(f,i1,len f -' 1) as S-Sequence_in_R2
    by A57,A342,JORDAN4:51;
A345: M4/.len M4 = f/.(len f -' 1) by A56,A338,SPRECT_2:13;
    L~M4 misses L~Red by A56,A242,A338,SPRECT_3:34,XBOOLE_1:63;
    then
A346: L~M4 misses L~Red2 by A248,JORDAN3:76,XBOOLE_1:63;
    L~M4 misses LSeg(u3,p2) by A58,A254,A342,A344;
    then
A347: L~RB2 misses L~M4 by A287,A346,XBOOLE_1:70;
    LSeg(M4/.len M4,p2) /\ L~M4={ M4/.len M4 } by A124,A342,A344,A345;
    then reconsider M1 = M4^RB2 as S-Sequence_in_R2
    by A75,A80,A93,A286,A288,A345,A347,SPRECT_3:38;
    i1 < len f by A342,NAT_D:44;
    then
A348: i2 < len f by A341,XXREAL_0:2;
    then i2+1 <= len f by NAT_1:13;
    then reconsider M2 = mid(f,1,i2) as S-Sequence_in_R2 by A343,JORDAN4:51;
A349: len M2 >= 2 & len M1 >= 2 by TOPREAL1:def 10;
    mid(f,i1,len f -' 1) is_in_the_area_of f by A56,A338,SPRECT_2:27;
    then
A350: M1 is_in_the_area_of f by A289,SPRECT_2:28;
    mid(f,i1,len f -' 1) is non empty by A56,A338,SPRECT_2:11;
    then 1 in dom mid(f,i1,len f -' 1) by FINSEQ_5:6;
    then
A351: (M1/.1)`2 = (mid(f,i1,len f -' 1)/.1)`2 by FINSEQ_4:83
      .= (f/.i1)`2 by A56,A338,SPRECT_2:12
      .= (S-max L~f)`2 by A337,FINSEQ_5:41
      .= S-bound L~f by EUCLID:56;
A352: (NE-corner L~f)`2 = N-bound L~f & A`2 = N-bound L~f by EUCLID:56;
    (M1/.len M1)`2 = (RB2/.len RB2)`2 by SPRECT_3:11
      .= N-bound L~f by A285,A335,A352,GOBOARD7:6;
    then
A353: M1 is_a_v.c._for f by A350,A351,SPRECT_2:def 3;
A354: M2 is_in_the_area_of f by A14,A340,SPRECT_2:27;
A355: (M2/.1)`1 = (f/.1)`1 by A14,A340,SPRECT_2:12
      .= W-bound L~f by A1,A336,EUCLID:56;
    (M2/.len M2)`1 = (f/.i2)`1 by A14,A340,SPRECT_2:13
      .= (E-max L~f)`1 by A339,FINSEQ_5:41
      .= E-bound L~f by EUCLID:56;
    then M2 is_a_h.c._for f by A354,A355,SPRECT_2:def 2;
    then
A356: L~M1 meets L~M2 by A349,A353,SPRECT_2:33;
A357: L~M1 = L~M4 \/ LSeg(M4/.len M4,p2) \/ L~RB2 by A286,SPPOL_2:23
      .= L~M4 \/ (LSeg(M4/.len M4,p2) \/ L~RB2) by XBOOLE_1:4;
A358: L~M2 misses LSeg(u3,p2) by A254,A343,A348;
    L~M2 misses L~M4 by A58,A341,A342,A343,SPRECT_2:51;
    then
A359: L~M2 meets L~RB2 \/ LSeg(p2,M4/.len M4) by A356,A357,XBOOLE_1:70;
    i2 < len f -' 1 by A341,A342,XXREAL_0:2;
    then L~M2 misses LSeg(p2,f/.(len f -' 1)) by A102,A343;
    then L~M2 meets L~RB2 by A345,A359,XBOOLE_1:70;
    then L~M2 meets L~Red2 by A287,A358,XBOOLE_1:70;
    then L~M2 meets L~Red by A248,JORDAN3:76,XBOOLE_1:63;
    hence contradiction by A14,A242,A340,SPRECT_3:34,XBOOLE_1:63;
  end;
  suppose that
A360: u1 in LSeg(A,NE-corner L~f) and
A361: N-min L~f <> NW-corner L~f;
A362: W-max L~f <> N-min L~f by A361,PSCOMP_1:124;
    set i1 = (S-min L~f)..f, i2 = (E-max L~f)..f;
A363: S-min L~f in rng f by SPRECT_2:45;
    then
A364: i1 in dom f by FINSEQ_4:30;
A365: E-max L~f in rng f by SPRECT_2:50;
    then
A366: i2 in dom f by FINSEQ_4:30;
    (E-max L~f)..f < (E-min L~f)..f &
    (E-min L~f)..f <= (S-max L~f)..f by A1,SPRECT_2:75,76;
    then (E-max L~f)..f < (S-max L~f)..f by XXREAL_0:2;
    then
A367: i2 < i1 by A1,SPRECT_2:77,XXREAL_0:2;
    (S-min L~f)..f <= (W-min L~f)..f &
    (W-min L~f)..f < (W-max L~f)..f by A1,A362,SPRECT_2:78,79;
    then (S-min L~f)..f < (W-max L~f)..f by XXREAL_0:2;
    then (S-min L~f)..f + 1 <= (W-max L~f)..f &
    (W-max L~f)..f < len f by A1,NAT_1:13,SPRECT_2:81;
    then (S-min L~f)..f + 1 < len f by XXREAL_0:2;
    then
A368: i1 < len f -' 1 by A57,XREAL_1:8;
    (N-max L~f)..f > 1 &
    (N-max L~f)..f <= (E-max L~f)..f by A1,SPRECT_2:73,74;
    then
A369: 1 < i2 by XXREAL_0:2;
    then
A370: 1 < i1 by A367,XXREAL_0:2;
    then reconsider M4 = mid(f,i1,len f -' 1) as S-Sequence_in_R2
    by A57,A368,JORDAN4:51;
A371: M4/.len M4 = f/.(len f -' 1) by A56,A364,SPRECT_2:13;
    L~M4 misses L~Red by A56,A242,A364,SPRECT_3:34,XBOOLE_1:63;
    then
A372: L~M4 misses L~Red2 by A248,JORDAN3:76,XBOOLE_1:63;
    L~M4 misses LSeg(u3,p2) by A58,A254,A368,A370;
    then
A373: L~RB2 misses L~M4 by A287,A372,XBOOLE_1:70;
    LSeg(M4/.len M4,p2) /\ L~M4={ M4/.len M4 } by A124,A368,A370,A371;
    then reconsider M1 = M4^RB2 as S-Sequence_in_R2
    by A75,A80,A93,A286,A288,A371,A373,SPRECT_3:38;
    i1 < len f by A368,NAT_D:44;
    then
A374: i2 < len f by A367,XXREAL_0:2;
    then i2+1 <= len f by NAT_1:13;
    then reconsider M3 = mid(f,1,i2) as S-Sequence_in_R2 by A369,JORDAN4:51;
A375: M3/.1 = f/.1 by A14,A366,SPRECT_2:12;
A376: (N-min L~f)`2 = (NW-corner L~f)`2 by PSCOMP_1:95;
A377: M3/.1 = N-min L~f by A1,A14,A366,SPRECT_2:12;
A378: LSeg(NW-corner L~f, N-min L~f) /\ L~f = {N-min L~f} by PSCOMP_1:102;
    then
A379: LSeg(NW-corner L~f,N-min L~f) /\ L~M3 c= { N-min L~f } by A14,A366,
    SPRECT_3:34,XBOOLE_1:26;
    1 in dom M3 by FINSEQ_5:6;
    then
A380: N-min L~f in L~M3 by A377,SPPOL_2:48;
    N-min L~f in LSeg(NW-corner L~f,N-min L~f) by TOPREAL1:6;
    then N-min L~f in LSeg(NW-corner L~f,N-min L~f) /\ L~M3 by A380,
    XBOOLE_0:def 3;
    then {N-min L~f} c= LSeg(NW-corner L~f,N-min L~f) /\ L~M3 by ZFMISC_1:37;
    then LSeg(NW-corner L~f,N-min L~f) /\ L~M3={ N-min L~f } by A379,
    XBOOLE_0:def 10;
    then reconsider M2 = <*NW-corner L~f*>^M3 as S-Sequence_in_R2
    by A361,A376,A377,SPRECT_3:31;
A381: len M2 >= 2 & len M1 >= 2 by TOPREAL1:def 10;
    mid(f,i1,len f -' 1) is_in_the_area_of f by A56,A364,SPRECT_2:27;
    then
A382: M1 is_in_the_area_of f by A289,SPRECT_2:28;
    mid(f,i1,len f -' 1) is non empty by A56,A364,SPRECT_2:11;
    then 1 in dom mid(f,i1,len f -' 1) by FINSEQ_5:6;
    then
A383: (M1/.1)`2 = (mid(f,i1,len f -' 1)/.1)`2 by FINSEQ_4:83
      .= (f/.i1)`2 by A56,A364,SPRECT_2:12
      .= (S-min L~f)`2 by A363,FINSEQ_5:41
      .= S-bound L~f by EUCLID:56;
A384: (NE-corner L~f)`2 = N-bound L~f & A`2 = N-bound L~f by EUCLID:56;
    (M1/.len M1)`2 = u1`2 by A285,SPRECT_3:11
      .= N-bound L~f by A360,A384,GOBOARD7:6;
    then
A385: M1 is_a_v.c._for f by A382,A383,SPRECT_2:def 3;
    mid(f,1,i2) is_in_the_area_of f by A14,A366,SPRECT_2:27;
    then
A386: M2 is_in_the_area_of f by A59,SPRECT_2:28;
A387: (M2/.1)`1 = (NW-corner L~f)`1 by FINSEQ_5:16
      .= W-bound L~f by EUCLID:56;
    (M2/.len M2)`1 = (mid(f,1,i2)/.len mid(f,1,i2))`1 by SPRECT_3:11
      .= (f/.i2)`1 by A14,A366,SPRECT_2:13
      .= (E-max L~f)`1 by A365,FINSEQ_5:41
      .= E-bound L~f by EUCLID:56;
    then M2 is_a_h.c._for f by A386,A387,SPRECT_2:def 2;
    then
A388: L~M1 meets L~M2 by A381,A385,SPRECT_2:33;
A389: L~M1 = L~M4 \/ LSeg(M4/.len M4,p2) \/ L~RB2 by A286,SPPOL_2:23
      .= L~M4 \/ (LSeg(M4/.len M4,p2) \/ L~RB2) by XBOOLE_1:4;
A390: now
A391: LSeg(NW-corner L~f,N-min L~f) /\ L~M4 c= { N-min L~f } by A56,A364,A378,
      SPRECT_3:34,XBOOLE_1:26;
      assume LSeg(NW-corner L~f,N-min L~f) meets L~M4;
      then LSeg(NW-corner L~f,N-min L~f) /\ L~M4 <> {} by XBOOLE_0:def 7;
      then LSeg(NW-corner L~f,N-min L~f) /\ L~M4 = { N-min L~f }
      by A391,ZFMISC_1:39;
      then N-min L~f in LSeg(NW-corner L~f,N-min L~f) /\ L~M4 by TARSKI:def 1;
      then N-min L~f in L~M4 by XBOOLE_0:def 3;
      hence contradiction by A1,A58,A368,A370,SPRECT_3:47;
    end;
    now
      NW-corner L~f in LSeg(NW-corner L~f,NE-corner L~f) &
      A in LSeg(NW-corner L~f,NE-corner L~f) by A297,TOPREAL1:6;
      then LSeg(NW-corner L~f,A) c= LSeg(NW-corner L~f,NE-corner L~f)
      by TOPREAL1:12;
      then LSeg(NW-corner L~f,N-min L~f) c= L~SpStSeq L~f by A62,XBOOLE_1:1;
      then
A392: LSeg(NW-corner L~f,N-min L~f) /\ L~Red2 c= { u1 } by A276,XBOOLE_1:26;
      assume LSeg(NW-corner L~f,N-min L~f) meets L~Red2;
      then LSeg(NW-corner L~f,N-min L~f) /\ L~Red2 <> {} by XBOOLE_0:def 7;
      then LSeg(NW-corner L~f,N-min L~f) /\ L~Red2 = { u1} by A392,ZFMISC_1:39;
      then u1 in LSeg(NW-corner L~f,N-min L~f) /\ L~Red2 by TARSKI:def 1;
      then u1 in LSeg(NW-corner L~f,N-min L~f) by XBOOLE_0:def 3;
      then
A393: u1 in LSeg(NW-corner L~f,A) /\
      LSeg(A,NE-corner L~f) by A360,XBOOLE_0:def 3;
      LSeg(NW-corner L~f,A) /\ LSeg(A,NE-corner L~f) = {A}
      by A297,TOPREAL1:14;
      then u1 = A by A393,TARSKI:def 1;
      hence contradiction by A236,SPRECT_1:13;
    end;
    then LSeg(NW-corner L~f,N-min L~f) misses L~RB2 by A253,A287,XBOOLE_1:70;
    then LSeg(NW-corner L~f,N-min L~f) misses LSeg(M4/.len M4,p2) \/ L~RB2
    by A131,A371,XBOOLE_1:70;
    then LSeg(NW-corner L~f,N-min L~f) misses L~M1 &
    L~M2 = LSeg(NW-corner L~f,M3/.1) \/
    L~M3 by A389,A390,SPPOL_2:20,XBOOLE_1:70;
    then
A394: L~M3 meets L~M1 by A1,A375,A388,XBOOLE_1:70;
    L~M3 misses L~M4 by A58,A367,A368,A369,SPRECT_2:51;
    then
A395: L~M3 meets L~RB2 \/ LSeg(p2,M4/.len M4) by A389,A394,XBOOLE_1:70;
    i2 < len f -' 1 by A367,A368,XXREAL_0:2;
    then L~M3 misses LSeg(p2,f/.(len f -' 1)) by A102,A369;
    then L~M3 meets L~RB2 &
    L~M3 misses LSeg(u3,p2) by A254,A369,A371,A374,A395,XBOOLE_1:70;
    then L~M3 meets L~Red2 by A287,XBOOLE_1:70;
    then L~M3 meets L~Red by A248,JORDAN3:76,XBOOLE_1:63;
    hence contradiction by A14,A242,A366,SPRECT_3:34,XBOOLE_1:63;
  end;
  suppose that
A396: u1 in LSeg(NE-corner L~f,SE-corner L~f) and
A397: N-min L~f = W-max L~f;
    set i1 = (S-max L~f)..f, i2 = (N-max L~f)..f, i3 = (W-min L~f)..f;
A398: S-max L~f in rng f by SPRECT_2:46;
    then
A399: i1 in dom f by FINSEQ_4:30;
A400: N-max L~f in rng f by SPRECT_2:44;
    then
A401: i2 in dom f by FINSEQ_4:30;
    W-min L~f in rng f by SPRECT_2:47;
    then
A402: i3 in dom f by FINSEQ_4:30;
    (S-max L~f)..f < (S-min L~f)..f by A1,SPRECT_2:77;
    then
A403: i3 > i1 by A1,SPRECT_2:78,XXREAL_0:2;
A404: 1 < i2 by A1,SPRECT_2:73;
    (N-max L~f)..f <= (E-max L~f)..f by A1,SPRECT_2:74;
    then (N-max L~f)..f < (E-min L~f)..f by A1,SPRECT_2:75,XXREAL_0:2;
    then
A405: i2 < i1 by A1,SPRECT_2:76,XXREAL_0:2;
    i1 <= len f by A399,FINSEQ_3:27;
    then
A406: mid(f,i2,i1) is S-Sequence_in_R2 by A404,A405,JORDAN4:52;
A407: mid(f,i1,i2) = Rev mid(f,i2,i1) by JORDAN4:30;
    then reconsider M1 = mid(f,i1,i2) as S-Sequence_in_R2 by A406,SPPOL_2:47;
A408: len M1 >= 2 & len RB2 >= 2 by TOPREAL1:def 10;
A409: M1 is_in_the_area_of f by A399,A401,SPRECT_2:27;
A410: (M1/.1)`2 = (f/.i1)`2 by A399,A401,SPRECT_2:12
      .= (S-max L~f)`2 by A398,FINSEQ_5:41
      .= S-bound L~f by EUCLID:56;
    (M1/.len M1)`2 = (f/.i2)`2 by A399,A401,SPRECT_2:13
      .= (N-max L~f)`2 by A400,FINSEQ_5:41
      .= N-bound L~f by EUCLID:56;
    then
A411: M1 is_a_v.c._for f by A409,A410,SPRECT_2:def 3;
A412: (RB2/.1)`1 = W-bound L~f by A93,A286,A397,EUCLID:56;
    (NE-corner L~f)`1 = E-bound L~f & (SE-corner L~f)`1 = E-bound L~f
    by EUCLID:56;
    then (RB2/.len RB2)`1 = E-bound L~f by A285,A396,GOBOARD7:5;
    then RB2 is_a_h.c._for f by A289,A412,SPRECT_2:def 2;
    then
A413: L~M1 meets L~RB2 by A408,A411,SPRECT_2:33;
    i3 <= len f by A402,FINSEQ_3:27;
    then i1 < len f by A403,XXREAL_0:2;
    then L~mid(f,i2,i1) misses LSeg(u3,p2) by A254,A404,A405;
    then L~M1 misses LSeg(u3,p2) by A407,SPPOL_2:22;
    then L~M1 meets L~Red2 by A287,A413,XBOOLE_1:70;
    then L~M1 meets L~Red by A248,JORDAN3:76,XBOOLE_1:63;
    hence contradiction by A242,A399,A401,SPRECT_3:34,XBOOLE_1:63;
  end;
  suppose that
A414: u1 in LSeg(NE-corner L~f,SE-corner L~f) and
A415: N-min L~f <> W-max L~f;
    set i1 = (S-max L~f)..f, i2 = (N-max L~f)..f, i3 = (W-min L~f)..f;
A416: S-max L~f in rng f by SPRECT_2:46;
    then
A417: i1 in dom f by FINSEQ_4:30;
A418: N-max L~f in rng f by SPRECT_2:44;
    then
A419: i2 in dom f by FINSEQ_4:30;
A420: W-min L~f in rng f by SPRECT_2:47;
    then
A421: i3 in dom f by FINSEQ_4:30;
    (S-max L~f)..f < (S-min L~f)..f by A1,SPRECT_2:77;
    then
A422: i3 > i1 by A1,SPRECT_2:78,XXREAL_0:2;
A423: 1 < i2 by A1,SPRECT_2:73;
    (N-max L~f)..f <= (E-max L~f)..f by A1,SPRECT_2:74;
    then (N-max L~f)..f < (E-min L~f)..f by A1,SPRECT_2:75,XXREAL_0:2;
    then
A424: i2 < i1 by A1,SPRECT_2:76,XXREAL_0:2;
    (W-max L~f)..f < len f by A1,SPRECT_2:81;
    then (W-max L~f)..f <= len f -' 1 by NAT_D:49;
    then
A425: i3 < len f -' 1 by A1,A415,SPRECT_2:79,XXREAL_0:2;
    i1 <= len f by A417,FINSEQ_3:27;
    then
A426: mid(f,i2,i1) is S-Sequence_in_R2 by A423,A424,JORDAN4:52;
A427: mid(f,i1,i2) = Rev mid(f,i2,i1) by JORDAN4:30;
    then reconsider M1 = mid(f,i1,i2) as S-Sequence_in_R2 by A426,SPPOL_2:47;
A428: 1 <= i3 by A421,FINSEQ_3:27;
    then reconsider M3 = mid(f,i3,len f -' 1) as S-Sequence_in_R2
    by A57,A425,JORDAN4:51;
A429: M3/.len M3 = f/.(len f -' 1) by A56,A421,SPRECT_2:13;
    L~M3 misses L~Red by A56,A242,A421,SPRECT_3:34,XBOOLE_1:63;
    then
A430: L~M3 misses L~Red2 by A248,JORDAN3:76,XBOOLE_1:63;
    L~M3 misses LSeg(u3,p2) by A58,A254,A425,A428;
    then
A431: L~RB2 misses L~M3 by A287,A430,XBOOLE_1:70;
    LSeg(M3/.len M3,p2) /\ L~M3={ M3/.len M3 } by A124,A425,A428,A429;
    then reconsider M2 = M3^RB2 as S-Sequence_in_R2
    by A75,A80,A93,A286,A288,A429,A431,SPRECT_3:38;
A432: len M2 >= 2 & len M1 >= 2 by TOPREAL1:def 10;
A433: M1 is_in_the_area_of f by A417,A419,SPRECT_2:27;
A434: (M1/.1)`2 = (f/.i1)`2 by A417,A419,SPRECT_2:12
      .= (S-max L~f)`2 by A416,FINSEQ_5:41
      .= S-bound L~f by EUCLID:56;
    (M1/.len M1)`2 = (f/.i2)`2 by A417,A419,SPRECT_2:13
      .= (N-max L~f)`2 by A418,FINSEQ_5:41
      .= N-bound L~f by EUCLID:56;
    then
A435: M1 is_a_v.c._for f by A433,A434,SPRECT_2:def 3;
    mid(f,i3,len f -' 1) is_in_the_area_of f by A56,A421,SPRECT_2:27;
    then
A436: M2 is_in_the_area_of f by A289,SPRECT_2:28;
    mid(f,i3,len f -' 1) is non empty by A56,A421,SPRECT_2:11;
    then 1 in dom mid(f,i3,len f -' 1) by FINSEQ_5:6;
    then
A437: (M2/.1)`1 = (mid(f,i3,len f -' 1)/.1)`1 by FINSEQ_4:83
      .= (f/.i3)`1 by A56,A421,SPRECT_2:12
      .= (W-min L~f)`1 by A420,FINSEQ_5:41
      .= W-bound L~f by EUCLID:56;
A438: (NE-corner L~f)`1 = E-bound L~f & (SE-corner L~f)`1 = E-bound L~f
    by EUCLID:56;
    (M2/.len M2)`1 = (RB2/.len RB2)`1 by SPRECT_3:11
      .= E-bound L~f by A285,A414,A438,GOBOARD7:5;
    then M2 is_a_h.c._for f by A436,A437,SPRECT_2:def 2;
    then
A439: L~M1 meets L~M2 by A432,A435,SPRECT_2:33;
A440: L~M2 = L~M3 \/ LSeg(M3/.len M3,p2) \/ L~RB2 by A286,SPPOL_2:23
      .= L~M3 \/ (LSeg(M3/.len M3,p2) \/ L~RB2) by XBOOLE_1:4;
    i3 < len f by A57,A425,NAT_1:13;
    then i1 < len f by A422,XXREAL_0:2;
    then L~mid(f,i2,i1) misses LSeg(u3,p2) by A254,A423,A424;
    then
A441: L~M1 misses LSeg(u3,p2) by A427,SPPOL_2:22;
    L~M1 misses L~M3 by A58,A422,A423,A424,A425,SPRECT_2:54;
    then
A442: L~M1 meets L~RB2 \/ LSeg(p2,M3/.len M3) by A439,A440,XBOOLE_1:70;
    i1 < len f -' 1 by A422,A425,XXREAL_0:2;
    then L~mid(f,i2,i1) misses LSeg(p2,f/.(len f -' 1)) by A102,A423,A424;
    then L~M1 misses LSeg(p2,f/.(len f -' 1)) by A427,SPPOL_2:22;
    then L~M1 meets L~RB2 by A429,A442,XBOOLE_1:70;
    then L~M1 meets L~Red2 by A287,A441,XBOOLE_1:70;
    then L~M1 meets L~Red by A248,JORDAN3:76,XBOOLE_1:63;
    hence contradiction by A242,A417,A419,SPRECT_3:34,XBOOLE_1:63;
  end;
  suppose
A443: u1 in LSeg(SE-corner L~f,SW-corner L~f);
    set i1 = (E-min L~f)..f, i2 = (W-min L~f)..f;
A444: E-min L~f in rng f by SPRECT_2:49;
    then
A445: i1 in dom f by FINSEQ_4:30;
A446: W-min L~f in rng f by SPRECT_2:47;
    then
A447: i2 in dom f by FINSEQ_4:30;
    (N-max L~f)..f > 1 by A1,SPRECT_2:73;
    then (E-max L~f)..f > 1 by A1,SPRECT_2:74,XXREAL_0:2;
    then
A448: 1 < i1 by A1,SPRECT_2:75,XXREAL_0:2;
    (E-min L~f)..f <= (S-max L~f)..f by A1,SPRECT_2:76;
    then (E-min L~f)..f < (S-min L~f)..f by A1,SPRECT_2:77,XXREAL_0:2;
    then
A449: i1 < i2 by A1,SPRECT_2:78,XXREAL_0:2;
A450: i2 <= len f by A447,FINSEQ_3:27;
    then
A451: mid(f,i1,i2) is S-Sequence_in_R2 by A448,A449,JORDAN4:52;
A452: mid(f,i2,i1) = Rev mid(f,i1,i2) by JORDAN4:30;
    then reconsider M2 = mid(f,i2,i1) as S-Sequence_in_R2 by A451,SPPOL_2:47;
A453: len M2 >= 2 & len RB1 >= 2 by TOPREAL1:def 10;
    (SE-corner L~f)`2 = S-bound L~f & (SW-corner L~f)`2 = S-bound L~f
    by EUCLID:56;
    then (RB1/.1)`2 = S-bound L~f by A290,A443,GOBOARD7:6;
    then
A454: RB1 is_a_v.c._for f by A168,A291,A294,SPRECT_2:def 3;
A455: M2 is_in_the_area_of f by A445,A447,SPRECT_2:27;
A456: (M2/.len M2)`1 = (f/.i1)`1 by A445,A447,SPRECT_2:13
      .= (E-min L~f)`1 by A444,FINSEQ_5:41
      .= E-bound L~f by EUCLID:56;
    (M2/.1)`1 = (f/.i2)`1 by A445,A447,SPRECT_2:12
      .= (W-min L~f)`1 by A446,FINSEQ_5:41
      .= W-bound L~f by EUCLID:56;
    then M2 is_a_h.c._for f by A455,A456,SPRECT_2:def 2;
    then
A457: L~RB1 meets L~M2 by A453,A454,SPRECT_2:33;
    L~mid(f,i1,i2) misses LSeg(u2,p1) by A264,A448,A449,A450;
    then L~M2 misses LSeg(u2,p1) by A452,SPPOL_2:22;
    then L~M2 meets L~Red1 by A292,A457,XBOOLE_1:70;
    then L~M2 meets L~Red by A258,JORDAN3:76,XBOOLE_1:63;
    hence contradiction by A242,A445,A447,SPRECT_3:34,XBOOLE_1:63;
  end;
  suppose
A458: u1 in LSeg(SW-corner L~f,NW-corner L~f);
    set i1 = (S-min L~f)..f, i3 = (E-min L~f)..f;
A459: S-min L~f in rng f by SPRECT_2:45;
    then
A460: i1 in dom f by FINSEQ_4:30;
A461: E-min L~f in rng f by SPRECT_2:49;
    then
A462: i3 in dom f by FINSEQ_4:30;
    (E-min L~f)..f <= (S-max L~f)..f by A1,SPRECT_2:76;
    then
A463: i3 < i1 by A1,SPRECT_2:77,XXREAL_0:2;
    (W-min L~f)..f < len f by A1,SPRECT_2:80;
    then
A464: i1 < len f by A1,SPRECT_2:78,XXREAL_0:2;
    (N-max L~f)..f > 1 by A1,SPRECT_2:73;
    then (E-max L~f)..f > 1 by A1,SPRECT_2:74,XXREAL_0:2;
    then (E-max L~f)..f >= 1+1 by NAT_1:13;
    then
A465: 2 < i3 by A1,SPRECT_2:75,XXREAL_0:2;
    then
A466: 2 < i1 by A463,XXREAL_0:2;
    then
A467: 1 < i1 by XXREAL_0:2;

then reconsider M1 = mid(f,i1,len f) as S-Sequence_in_R2 by A464,JORDAN4:52;
A468: i3 <= len f by A462,FINSEQ_3:27;
    then reconsider M3 = mid(f,2,i3) as S-Sequence_in_R2 by A465,JORDAN4:52;
A469: M3/.1 = f/.2 by A53,A462,SPRECT_2:12;
    L~M3 misses L~Red by A53,A242,A462,SPRECT_3:34,XBOOLE_1:63;
    then
A470: L~M3 misses L~Red1 by A258,JORDAN3:76,XBOOLE_1:63;
    L~M3 misses LSeg(u2,p1) by A264,A465,A468;
    then
A471: L~RB1 misses L~M3 by A292,A470,XBOOLE_1:70;
    LSeg(p1,M3/.1) /\ L~M3={ M3/.1 } by A188,A465,A468,A469;
    then reconsider M2 = RB1^M3 as S-Sequence_in_R2
    by A77,A168,A291,A293,A469,A471,SPRECT_3:38;
A472: len M2 >= 2 & len M1 >= 2 by TOPREAL1:def 10;
A473: M1 is_in_the_area_of f by A54,A460,SPRECT_2:27;
A474: (M1/.1)`2 = (f/.i1)`2 by A54,A460,SPRECT_2:12
      .= (S-min L~f)`2 by A459,FINSEQ_5:41
      .= S-bound L~f by EUCLID:56;
    (M1/.len M1)`2 = (f/.len f)`2 by A54,A460,SPRECT_2:13
      .= (f/.1)`2 by FINSEQ_6:def 1
      .= N-bound L~f by A1,EUCLID:56;
    then
A475: M1 is_a_v.c._for f by A473,A474,SPRECT_2:def 3;
    mid(f,2,i3) is_in_the_area_of f by A53,A462,SPRECT_2:27;
    then
A476: M2 is_in_the_area_of f by A294,SPRECT_2:28;
A477: (NW-corner L~f)`1 = W-bound L~f & (SW-corner L~f)`1 = W-bound L~f
    by EUCLID:56;
    1 in dom RB1 by FINSEQ_5:6;
    then
A478: (M2/.1)`1 = (RB1/.1)`1 by FINSEQ_4:83
      .= W-bound L~f by A290,A458,A477,GOBOARD7:5;
    (M2/.len M2)`1 = (mid(f,2,i3)/.len mid(f,2,i3))`1 by SPRECT_3:11
      .= (f/.i3)`1 by A53,A462,SPRECT_2:13
      .= (E-min L~f)`1 by A461,FINSEQ_5:41
      .= E-bound L~f by EUCLID:56;
    then M2 is_a_h.c._for f by A476,A478,SPRECT_2:def 2;
    then
A479: L~M1 meets L~M2 by A472,A475,SPRECT_2:33;
A480: L~M2 = L~RB1 \/ LSeg(p1,M3/.1) \/ L~M3 by A291,SPPOL_2:23;
A481: L~M1 misses LSeg(u2,p1) by A264,A464,A467;
    L~M1 misses L~M3 by A463,A464,A465,SPRECT_2:51;
    then
A482: L~M1 meets L~RB1 \/ LSeg(p1,M3/.1) by A479,A480,XBOOLE_1:70;
    L~M1 misses LSeg(p1,f/.2) by A171,A464,A466;
    then L~M1 meets L~RB1 by A469,A482,XBOOLE_1:70;
    then L~M1 meets L~Red1 by A292,A481,XBOOLE_1:70;
    then L~M1 meets L~Red by A258,JORDAN3:76,XBOOLE_1:63;
    hence contradiction by A54,A242,A460,SPRECT_3:34,XBOOLE_1:63;
  end;
end;

Lm2: for f being non constant standard special_circular_sequence
st f/.1 = N-min L~f holds LeftComp f <> RightComp f
proof
  let f be non constant standard special_circular_sequence such that
A1: f/.1 = N-min L~f;
  per cases by REVROT_1:38;
  suppose f is clockwise_oriented;
    hence LeftComp f <> RightComp f by A1,Lm1;
  end;
  suppose
A2: Rev f is clockwise_oriented;
A3: N-min L~Rev f = N-min L~f by SPPOL_2:22
      .= Rev f/.len f by A1,FINSEQ_5:68
      .= Rev f/.len Rev f by FINSEQ_5:def 3
      .= Rev f/.1 by FINSEQ_6:def 1;
    LeftComp Rev f = RightComp f &
    RightComp Rev f = LeftComp f by GOBOARD9:26,27;
    hence LeftComp f <> RightComp f by A2,A3,Lm1;
  end;
end;

theorem
  for f being non constant standard special_circular_sequence
  holds LeftComp f <> RightComp f
proof
  let f be non constant standard special_circular_sequence;
  set g = Rotate(f,N-min L~f);
A1: N-min L~f in rng f by SPRECT_2:43;
  L~f = L~g by REVROT_1:33;
  then
A2: g/.1 = N-min L~g by A1,FINSEQ_6:98;
  LeftComp g = LeftComp f & RightComp g = RightComp f by REVROT_1:36,37;
  hence LeftComp f <> RightComp f by A2,Lm2;
end;

