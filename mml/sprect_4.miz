:: On the components of the complement of a special polygonal curve
::  by Andrzej Trybulec and Yatsuka Nakamura
::
:: Received January 21, 1999
:: Copyright (c) 1999 Association of Mizar Users

environ

 vocabularies PRE_TOPC, SPPOL_2, EUCLID, TOPREAL1, BOOLE, FINSEQ_4, JORDAN3,
      JORDAN5C, FINSEQ_1, GROUP_2, RELAT_1, FUNCT_1, ARYTM_1, SPRECT_2, SEQM_3,
      GOBOARD5, PSCOMP_1, GOBOARD9, SPRECT_1, GOBOARD2, TREES_1, MATRIX_1,
      ARYTM_3, TOPS_1, MCART_1, TOPREAL4, SPPOL_1, FINSEQ_5, FINSEQ_6, NAT_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, NUMBERS, REAL_1, NAT_1, FUNCT_1,
      FINSEQ_1, FINSEQ_4, BINARITH, MATRIX_1, STRUCT_0, PRE_TOPC, TOPS_1,
      EUCLID, TOPREAL1, TOPREAL4, FINSEQ_5, FINSEQ_6, GOBOARD2, SPPOL_1,
      SPPOL_2, GOBOARD5, GOBOARD9, JORDAN3, PSCOMP_1, JORDAN5C, SPRECT_1,
      SPRECT_2, XXREAL_0;
 constructors XXREAL_0, REAL_1, NAT_1, FINSEQ_4, BINARITH, TOPS_1, TOPS_2,
      COMPTS_1, REALSET2, MATRIX_2, TOPMETR, TOPREAL4, GOBOARD2, SPPOL_1,
      PSCOMP_1, GOBOARD9, JORDAN3, JORDAN5C, SPRECT_1, SPRECT_2;
 registrations XBOOLE_0, RELSET_1, MEMBERED, REALSET1, FINSEQ_6, STRUCT_0,
      COMPTS_1, TOPREAL1, GOBOARD2, SPPOL_2, GOBOARD9, SPRECT_1, SPRECT_3,
      REVROT_1;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions PSCOMP_1;
 theorems TOPREAL1, JORDAN5C, JORDAN3, FINSEQ_3, SPPOL_2, TOPS_1, SPPOL_1,
      TOPREAL3, FINSEQ_5, REAL_1, NAT_1, SPRECT_1, JORDAN5B, SPRECT_2,
      GOBOARD5, FINSEQ_4, GOBOARD7, GOBOARD6, GOBOARD9, ZFMISC_1, TOPREAL4,
      PSCOMP_1, JORDAN4, FINSEQ_6, FINSEQ_1, TARSKI, EUCLID, SUBSET_1,
      JORDAN5D, SPRECT_3, REVROT_1, PARTFUN2, XBOOLE_0, XBOOLE_1, XREAL_1,
      BINARITH, XXREAL_0;

begin

reserve i,j,l for Element of NAT;

theorem Th1:
 for f being S-Sequence_in_R2, Q being closed Subset of TOP-REAL 2
  st L~f meets Q & not f/.1 in Q
holds L~R_Cut(f,First_Point(L~f,f/.1,f/.len f,Q)) /\ Q =
            { First_Point(L~f,f/.1,f/.len f,Q) }
proof
 let f be S-Sequence_in_R2, Q be closed Subset of TOP-REAL 2
 such that
A1: L~f meets Q and
A2: not f/.1 in Q;
  set p1 = f/.1, p2 = f/.len f, fp = First_Point(L~f,p1,p2,Q);
       len f >= 1+1 by TOPREAL1:def 10;
then A3: len f > 1 by NAT_1:13;
A4: L~f /\ Q is closed by TOPS_1:35;
A5: L~f is_an_arc_of p1,p2 by TOPREAL1:31;
then A6: fp in L~f /\ Q by A1,A4,JORDAN5C:def 1;
then A7: fp in Q by XBOOLE_0:def 3;
A8: fp in L~f by A6,XBOOLE_0:def 3;
   then A9: 1<=Index(fp,f) & Index(fp,f)<=len f by JORDAN3:41;
then A10: Index(fp,f) in dom f by FINSEQ_3:27;
      1 in dom f by A3,FINSEQ_3:27;
then fp <> f.1 by A2,A7,FINSEQ_4:def 4;
   then fp in L~R_Cut(f,fp) by A8,JORDAN5B:23;
   then A11:  fp in L~R_Cut(f,fp) /\ Q by A7,XBOOLE_0:def 3;
       now
   assume not L~R_Cut(f,fp) /\ Q c= { fp };
    then consider q being set such that
A12:  q in L~R_Cut(f,fp) /\ Q and
A13:  not q in { fp } by TARSKI:def 3;
    reconsider q as Point of TOP-REAL 2 by A12;
A14:  q <> fp by A13,TARSKI:def 1;
A15:  q in Q by A12,XBOOLE_0:def 3;
A16:  q in L~R_Cut(f,fp) by A12,XBOOLE_0:def 3;
         L~R_Cut(f,fp) c= L~f by A8,JORDAN3:76;
then A17:  LE fp, q, L~f, p1, p2 by A4,A15,A16,JORDAN5C:15;
   per cases;
   suppose
     fp = f.1;
then A18:  q in L~<*fp*> by A16,JORDAN3:def 5;
         len<*fp*> = 1 by FINSEQ_1:56;
   hence contradiction by A18,TOPREAL1:28;
   end;
   suppose
A19: fp <> f.1;
    set m = mid(f,1,Index(fp,f));
         len m = Index(fp,f)-'1+1 by A9,JORDAN4:20;
    then len m <> 0 by NAT_1:11;
then A20: m is non empty by FINSEQ_1:25;
         q in L~(m^<*fp*>) by A16,A19,JORDAN3:def 5;
    then A21:  q in L~m \/ LSeg(m/.len m,fp) by A20,SPPOL_2:19;
A22:    Index(fp,f) < len f by A8,JORDAN3:41;
A23:    fp in LSeg(f,Index(fp,f)) by A8,JORDAN3:42;
         now per cases by A21,XBOOLE_0:def 2;
     suppose
A24:   q in L~m;
A25:    now assume Index(fp,f) <= 1;
        then Index(fp,f) = 1 by A9,XXREAL_0:1;
        then len m = 1 by A3,JORDAN4:27;
       hence contradiction by A24,TOPREAL1:28;
      end;
then A26:    LE q, f/.Index(fp,f), L~f, p1, p2 by A9,A24,SPRECT_3:33;
           f/.Index(fp,f) in LSeg(f/.Index(fp,f),fp) by TOPREAL1:6;
      then LE f/.Index(fp,f), fp, L~f, p1, p2 by A22,A23,A25,SPRECT_3:40;
      then LE q, fp, L~f, p1, p2 by A26,JORDAN5C:13;
     hence contradiction by A5,A14,A17,JORDAN5C:12;
     end;
     suppose
A27:   q in LSeg(m/.len m,fp);
            len m in dom m by A20,FINSEQ_5:6;
       then m/.len m = m.len m by FINSEQ_4:def 4
              .= f.Index(fp,f) by A9,JORDAN4:22
              .= f/.Index(fp,f) by A10,FINSEQ_4:def 4;
       then LE q, fp, L~f, p1, p2 by A9,A22,A23,A27,SPRECT_3:40;
     hence contradiction by A5,A14,A17,JORDAN5C:12;
    end;
    end;
   hence contradiction;
  end;
  end;
 hence L~R_Cut(f,fp) /\ Q = { fp } by A11,ZFMISC_1:39;
end;

theorem
      for f being non empty FinSequence of TOP-REAL 2,
     p being Point of TOP-REAL 2 st f is_S-Seq & p = f/.len f holds
   L~L_Cut (f,p) = {}
proof
 let f be non empty FinSequence of TOP-REAL 2,
     p be Point of TOP-REAL 2;
 assume that
A1: f is_S-Seq and
A2: p = f/.len f;
       len f >= 2 by A1,TOPREAL1:def 10;
  then len f >= 1 by XXREAL_0:2;
  then len f in dom f by FINSEQ_3:27;
  then p = f.len f by A2,FINSEQ_4:def 4;
  then L_Cut (f,p) = <*p*> by A1,JORDAN5B:17;
  then len L_Cut (f,p) = 1 by FINSEQ_1:56;
 hence thesis by TOPREAL1:28;
end;

canceled;

theorem Th4:
 for f being S-Sequence_in_R2, p being Point of TOP-REAL 2, j st
   1 <=j & j < len f & p in L~mid(f,j,len f)
  holds LE f/.j, p, L~f, f/.1, f/.len f
proof
 let f be S-Sequence_in_R2, p be Point of TOP-REAL 2, j such that
A1: 1 <=j & j < len f and
A2: p in L~mid(f,j,len f);
  consider i such that
A3: 1 <= i & i+1 <= len mid(f,j,len f) and
A4: p in LSeg(mid(f,j,len f),i) by A2,SPPOL_2:13;
A5: len mid(f,j,len f)=len f-'j+1 by A1,JORDAN4:20;
    then i <= len f -' j by A3,XREAL_1:8;
    then A6: i + j <= len f by A1,BINARITH:72;
         j+1 <= j+i by A3,XREAL_1:8;
then A7: j <= j+i-'1 by BINARITH:67;
         1+1 <= j+i by A1,A3,XREAL_1:9;
then A8: 1 <= j+i-'1 by BINARITH:67;
        i<len f-'j+1 by A3,A5,NAT_1:13;
   then A9: p in LSeg(f,j+i-'1) by A1,A3,A4,JORDAN4:31;
        j+i >= i by NAT_1:11;
   then 1 <= j+i by A3,XXREAL_0:2;
then A10: j+i-'1+1 <= len f by A6,BINARITH:53;
  then A11:  LE f/.(j+i-'1), p, L~f, f/.1, f/.len f by A8,A9,JORDAN5C:25;
       j+i-'1+1 >= j+i-'1 by NAT_1:11;
  then len f >= j+i-'1 by A10,XXREAL_0:2;
  then LE f/.j, f/.(j+i-'1), L~f, f/.1, f/.len f by A1,A7,JORDAN5C:24;
 hence LE f/.j, p, L~f, f/.1, f/.len f by A11,JORDAN5C:13;
end;

theorem Th5:
 for f being S-Sequence_in_R2, p,q being Point of TOP-REAL 2, j st
   1 <=j & j < len f & p in LSeg(f,j) & q in LSeg(p,f/.(j+1))
  holds LE p, q, L~f, f/.1, f/.len f
proof let f be S-Sequence_in_R2, p,q be Point of TOP-REAL 2, j such that
A1: 1 <=j & j < len f and
A2: p in LSeg(f,j) and
A3: q in LSeg(p,f/.(j+1));
A4: j+1 <= len f by A1,NAT_1:13;
then A5: LSeg(f,j) = LSeg(f/.j,f/.(j+1)) by A1,TOPREAL1:def 5;
         f/.(j+1) in LSeg(f,j) by A1,A4,TOPREAL1:27;
    then A6:   LSeg(p,f/.(j+1)) c= LSeg(f,j) by A2,A5,TOPREAL1:12;
then A7: q in LSeg(f,j) by A3;
A8: LSeg(f,j) c= L~f by TOPREAL3:26;
  per cases;
  suppose p = q;
   hence thesis by A2,A8,JORDAN5C:9;
  end;
  suppose
A9: q <> p;
      for i, j being Element of NAT st p in LSeg(f,i) & q in LSeg(f,j)
  & 1 <= i & i+1 <= len f
  & 1 <= j & j+1 <= len f holds
       i <= j & (i = j implies LE p,q,f/.i,f/.(i+1))
  proof let i1, i2 be Element of NAT such that
A10: p in LSeg(f,i1) and
A11: q in LSeg(f,i2) and
A12: 1 <= i1 & i1+1 <= len f and
A13: 1 <= i2 & i2+1 <= len f;
A14: now assume i1 + 1 > j & j+1 > i1;
      then i1 <= j & j <= i1 by NAT_1:13;
     hence i1 = j by XXREAL_0:1;
    end;
A15: p in LSeg(f,i1) /\ LSeg(f,j) by A2,A10,XBOOLE_0:def 3;
     then LSeg(f,i1) meets LSeg(f,j) by XBOOLE_0:4;
then A16: i1 + 1 >= j & j + 1 >= i1 by TOPREAL1:def 9;
    A17: now assume
A18:   j + 1 = i1;
           j+(1+1) = j+1+1;
      then p in {f/.(j+1)} by A1,A12,A15,A18,TOPREAL1:def 8;
      then p = f/.(j+1) by TARSKI:def 1;
      then q in {p} by A3,TOPREAL1:7;
     hence contradiction by A9,TARSKI:def 1;
    end;
    then i1 + 1 = j or i1 = j by A14,A16,XXREAL_0:1;
    then A19: i1 <= j by NAT_1:11;
A20: now assume i2 + 1 > j & j+1 > i2;
      then i2 <= j & j <= i2 by NAT_1:13;
     hence i2 = j by XXREAL_0:1;
    end;
A21: q in LSeg(f,i2) /\ LSeg(f,j) by A3,A6,A11,XBOOLE_0:def 3;
    then LSeg(f,i2) meets LSeg(f,j) by XBOOLE_0:4;
    then i2 + 1 >= j & j + 1 >= i2 by TOPREAL1:def 9;
    then A22:   i2 + 1 = j or i2 = j or j + 1 = i2 by A20,XXREAL_0:1;
         1 <= j+1 by NAT_1:11;
then A23: j+1 in dom f by A4,FINSEQ_3:27;
A24: j in dom f by A1,FINSEQ_3:27;
A25:now assume f/.(j+1)=f/.j;
      then j = j+1 by A23,A24,PARTFUN2:17;
     hence contradiction;
    end;
A26: now assume
A27:   i2+1 = j;
           i2+(1+1) = i2+1+1;
      then q in {f/.j} by A4,A13,A21,A27,TOPREAL1:def 8;
      then q = f/.j by TARSKI:def 1;
     hence contradiction by A3,A5,A6,A9,A25,SPPOL_1:24;
    end;
    then i2 >= j by A22,NAT_1:11;
    hence i1 <= i2 by A19,XXREAL_0:2;
    assume
A28:   i1 = i2;
          not p in LSeg(q,f/.(j+1)) by A3,A9,SPRECT_3:16;
     then not LE q,p,f/.j,f/.(j+1) by JORDAN3:65;
     then LT p,q,f/.j,f/.(j+1) by A2,A5,A11,A14,A16,A17,A25,A26,A28,XXREAL_0:1
,JORDAN3:63;
    hence LE p,q,f/.i1,f/.(i1+1) by A14,A16,A17,A26,A28,XXREAL_0:1
,JORDAN3:def 7
;
  end;
 hence LE p, q, L~f, f/.1, f/.len f by A2,A7,A8,A9,JORDAN5C:30;
end;
end;

theorem Th6:
 for f being S-Sequence_in_R2, Q being closed Subset of TOP-REAL 2
  st L~f meets Q & not f/.len f in Q
holds L~L_Cut(f,Last_Point(L~f,f/.1,f/.len f,Q)) /\ Q =
            { Last_Point(L~f,f/.1,f/.len f,Q) }
proof
 let f be S-Sequence_in_R2, Q be closed Subset of TOP-REAL 2
 such that
A1: L~f meets Q and
A2: not f/.len f in Q;
  set p1 = f/.1, p2 = f/.len f, lp = Last_Point(L~f,p1,p2,Q);
       len f >= 1+1 by TOPREAL1:def 10;
then A3: len f > 1 by NAT_1:13;
A4: L~f /\ Q is closed by TOPS_1:35;
A5: L~f is_an_arc_of p1,p2 by TOPREAL1:31;
then A6: lp in L~f /\ Q by A1,A4,JORDAN5C:def 2;
then A7: lp in Q by XBOOLE_0:def 3;
A8: lp in L~f by A6,XBOOLE_0:def 3;
  then A9: 1<=Index(lp,f) & Index(lp,f)<=len f by JORDAN3:41;
         len f in dom f by A3,FINSEQ_3:27;
     then lp <> f.len f by A2,A7,FINSEQ_4:def 4;
   then lp in L~L_Cut(f,lp) by A8,JORDAN5B:22;
   then A10:  lp in L~L_Cut(f,lp) /\ Q by A7,XBOOLE_0:def 3;
       now
   assume not L~L_Cut(f,lp) /\ Q c= { lp };
    then consider q being set such that
A11:  q in L~L_Cut(f,lp) /\ Q and
A12:  not q in { lp } by TARSKI:def 3;
    reconsider q as Point of TOP-REAL 2 by A11;
A13:  q <> lp by A12,TARSKI:def 1;
A14:  q in Q by A11,XBOOLE_0:def 3;
A15:  q in L~L_Cut(f,lp) by A11,XBOOLE_0:def 3;
         L~L_Cut(f,lp) c= L~f by A8,JORDAN3:77;
then A16:  LE q, lp, L~f, p1, p2 by A4,A14,A15,JORDAN5C:16;
    set m = mid(f,Index(lp,f)+1,len f);
A17: Index(lp,f) < len f by A8,JORDAN3:41;
    then A18: Index(lp,f)+1 <= len f by NAT_1:13;
A19: 1 <= Index(lp,f)+1 by NAT_1:11;
    then len m = len f -' (Index(lp,f)+1)+1 by A18,JORDAN4:20;
    then len m <> 0 by NAT_1:11;
then A20: m is non empty by FINSEQ_1:25;
  per cases;
  suppose lp=f.(Index(lp,f)+1);
    then A21:  q in L~m by A15,JORDAN3:def 4;
A22:    lp in LSeg(f,Index(lp,f)) by A8,JORDAN3:42;
        now assume Index(lp,f)+1 >= len f;
        then Index(lp,f)+1 = len f by A18,XXREAL_0:1;
        then len m = 1 by A3,JORDAN4:27;
       hence contradiction by A21,TOPREAL1:28;
      end;
then A23:    LE f/.(Index(lp,f)+1), q, L~f, f/.1, f/.len f by A19,A21,Th4;
           f/.(Index(lp,f)+1) in LSeg(lp,f/.(Index(lp,f)+1)) by TOPREAL1:6;
      then LE lp, f/.(Index(lp,f)+1), L~f, p1, p2 by A9,A17,A22,Th5;
      then LE lp, q, L~f, p1, p2 by A23,JORDAN5C:13;
     hence contradiction by A5,A13,A16,JORDAN5C:12;
  end;
  suppose lp<>f.(Index(lp,f)+1);
    then q in L~(<*lp*>^m) by A15,JORDAN3:def 4;
    then A24:  q in L~m \/ LSeg(m/.1,lp) by A20,SPPOL_2:20;
         1 <= Index(lp,f)+1 by NAT_1:11;
then A25:    Index(lp,f)+1 in dom f by A18,FINSEQ_3:27;
A26:    lp in LSeg(f,Index(lp,f)) by A8,JORDAN3:42;
         now per cases by A24,XBOOLE_0:def 2;
     suppose
A27:   q in L~m;
        now assume Index(lp,f)+1 >= len f;
        then Index(lp,f)+1 = len f by A18,XXREAL_0:1;
        then len m = 1 by A3,JORDAN4:27;
       hence contradiction by A27,TOPREAL1:28;
      end;
then A28:    LE f/.(Index(lp,f)+1), q, L~f, f/.1, f/.len f by A19,A27,Th4;
           f/.(Index(lp,f)+1) in LSeg(lp,f/.(Index(lp,f)+1)) by TOPREAL1:6;
      then LE lp, f/.(Index(lp,f)+1), L~f, p1, p2 by A9,A17,A26,Th5;
      then LE lp, q, L~f, p1, p2 by A28,JORDAN5C:13;
     hence contradiction by A5,A13,A16,JORDAN5C:12;
     end;
     suppose
A29:   q in LSeg(lp,m/.1);
            1 in dom m by A20,FINSEQ_5:6;
       then m/.1 = m.1 by FINSEQ_4:def 4
              .= f.(Index(lp,f)+1) by A3,A18,A19,JORDAN3:27
              .= f/.(Index(lp,f)+1) by A25,FINSEQ_4:def 4;
       then LE lp, q, L~f, p1, p2 by A9,A17,A26,A29,Th5;
     hence contradiction by A5,A13,A16,JORDAN5C:12;
    end;
    end;
   hence contradiction;
  end;
  end;
 hence L~L_Cut(f,lp) /\ Q = { lp } by A10,ZFMISC_1:39;
end;

reserve q for Point of TOP-REAL 2;

Lm1:
 for f being
   clockwise_oriented (non constant standard special_circular_sequence)
  st f/.1 = N-min L~f
 holds LeftComp f <> RightComp f
proof
 let f be clockwise_oriented (non constant standard special_circular_sequence);
  set A = N-min L~f;
 assume that
A1: f/.1 = A and
A2: LeftComp f = RightComp f;
A3: LeftComp SpStSeq L~f c= LeftComp f by A1,SPRECT_3:58;
  consider i such that
A4: 1 <= i and
A5: i < len GoB f and
A6: A = (GoB f)*(i,width GoB f) by SPRECT_3:45;
  set w = width GoB f;
A7: len f > 4 by GOBOARD7:36;
then A8: 1+1 <= len f by XXREAL_0:2;
A9: w > 1 by GOBOARD7:35;
then A10: w-'1+1 = w by BINARITH:53;
A11: [i,w] in Indices GoB f by A4,A5,A9,GOBOARD7:10;
A12: 1 <= i+1 & i+1 <= len GoB f by A5,NAT_1:11,13;
then A13: [i+1,w] in Indices GoB f by A9,GOBOARD7:10;
A14: 1 in dom f by FINSEQ_5:6;
A15:   i in dom GoB f by A4,A5,FINSEQ_3:27;
   then A16: f/.(1+1) = (GoB f)*(i+1,w) by A1,A6,SPRECT_3:46;
   then A17: right_cell(f,1) = cell(GoB f,i,w-'1) by A1,A6,A8,A10,A11,A13,
GOBOARD5:29;
  set z2 = 1/2*((GoB f)*(i,w-'1)+(GoB f)*(i+1,w)),
      p1 = 1/2*((GoB f)*(i,w)+(GoB f)*(i+1,w)),
      p2 = 1/2*((GoB f)*(i,w-'1)+(GoB f)*(i,w));
A18:  1 <= w-'1 by A9,BINARITH:67;
   then A19: z2 in Int cell(GoB f,i,w-'1) by A4,A10,A12,GOBOARD6:34;
A20: Int right_cell(f,1) c= RightComp f by A8,GOBOARD9:28;
A21: z2 in RightComp SpStSeq L~f
  proof
A22: W-bound L~SpStSeq L~f = W-bound L~f &
   S-bound L~SpStSeq L~f = S-bound L~f &
   N-bound L~SpStSeq L~f = N-bound L~f &
   E-bound L~SpStSeq L~f = E-bound L~f by SPRECT_1:66,67,68,
69;
A23: 1 < i+1 by A4,NAT_1:13;
A24: 1 <= len GoB f by A4,A5,XXREAL_0:2;
A25: w-'1 < w by A10,XREAL_1:31;
A26: z2 = 1/2*(A+(GoB f)*(i+1,w-'1)) by A4,A6,A10,A12,A18,GOBOARD7:11;
    then A27:  z2`1 = 1/2*(A+(GoB f)*(i+1,w-'1))`1 by TOPREAL3:9
             .= 1/2*(A`1+(GoB f)*(i+1,w-'1)`1) by TOPREAL3:7
             .= 1/2*A`1+1/2*((GoB f)*(i+1,w-'1))`1;
A28:  z2`2 = 1/2*(A+(GoB f)*(i+1,w-'1))`2 by A26,TOPREAL3:9
             .= 1/2*(A`2+(GoB f)*(i+1,w-'1)`2) by TOPREAL3:7
             .= 1/2*(N-bound L~f+(GoB f)*(i+1,w-'1)`2) by EUCLID:56
             .= 1/2*N-bound L~f+1/2*((GoB f)*(i+1,w-'1))`2;
A29: 1/2*(W-bound L~f) + 1/2*(W-bound L~f)
        = (W-bound L~f);
A30: 1/2*(S-bound L~f) + 1/2*(S-bound L~f)
        = (S-bound L~f);
A31: 1/2*(N-bound L~f) + 1/2*(N-bound L~f)
        = N-bound L~f;
A32: 1/2*(E-bound L~f) + 1/2*(E-bound L~f)
        = (E-bound L~f);
          N-min L~f in L~f by SPRECT_1:13;
     then A`1 >= W-bound L~f by PSCOMP_1:71;
     then A33: 1/2*A`1 >= 1/2*W-bound L~f by XREAL_1:66;
A34: (GoB f)*(1,w-'1)`1 = (GoB f)*(1,1)`1 by A18,A24,A25,GOBOARD5:3;
          (GoB f)*(i+1,w-'1)`1 > (GoB f)*(1,w-'1)`1 by A12,A18,A23,A25,
GOBOARD5:4
;
     then (GoB f)*(i+1,w-'1)`1 > W-bound L~f by A34,JORDAN5D:39;
     then 1/2*((GoB f)*(i+1,w-'1))`1 > 1/2*W-bound L~f by XREAL_1:70;
    then A35: W-bound L~SpStSeq L~f < z2`1 by A22,A27,A29,A33,XREAL_1:10;
A36: A`1 < (N-max L~f)`1 by SPRECT_2:55;
         N-max L~f in L~f by SPRECT_1:13;
    then (N-max L~f)`1 <= E-bound L~f by PSCOMP_1:71;
    then A`1 < E-bound L~f by A36,XXREAL_0:2;
    then A37: 1/2*A`1 < 1/2*E-bound L~f by XREAL_1:70;
A38: (GoB f)*(len GoB f,w-'1)`1 = (GoB f)*(len GoB f,1)`1
                     by A18,A24,A25,GOBOARD5:3;
          (GoB f)*(i+1,w-'1)`1 <= (GoB f)*(len GoB f,w-'1)`1 by A12,A18,A25,
SPRECT_3:25;
     then (GoB f)*(i+1,w-'1)`1 <= E-bound L~f by A38,JORDAN5D:41;
     then 1/2*((GoB f)*(i+1,w-'1))`1 <= 1/2*E-bound L~f by XREAL_1:66;
    then A39: z2`1 < E-bound L~SpStSeq L~f by A22,A27,A32,A37,XREAL_1:10;
        N-bound L~f > S-bound L~f by SPRECT_1:34;
   then A40: 1/2*N-bound L~f > 1/2*S-bound L~f by XREAL_1:70;
A41: (GoB f)*(i+1,1)`2 = (GoB f)*(1,1)`2 by A9,A12,GOBOARD5:2;
        (GoB f)*(i+1,w-'1)`2 >= (GoB f)*(i+1,1)`2 by A12,A18,A25,SPRECT_3:24;
   then (GoB f)*(i+1,w-'1)`2 >= S-bound L~f by A41,JORDAN5D:40;
   then 1/2*((GoB f)*(i+1,w-'1))`2 >= 1/2*S-bound L~f by XREAL_1:66;
   then A42: S-bound L~SpStSeq L~f < z2`2 by A22,A28,A30,A40,XREAL_1:10;
A43: (GoB f)*(i+1,w)`2 = (GoB f)*(1,width GoB f)`2 by A9,A12,GOBOARD5:2;
        (GoB f)*(i+1,w-'1)`2 < (GoB f)*(i+1,w)`2 by A12,A18,A25,GOBOARD5:5;
   then (GoB f)*(i+1,w-'1)`2 < N-bound L~f by A43,JORDAN5D:42;
   then 1/2*((GoB f)*(i+1,w-'1))`2 < 1/2*N-bound L~f by XREAL_1:70;
   then A44: z2`2 < N-bound L~SpStSeq L~f by A22,A28,A31,XREAL_1:8;
        RightComp SpStSeq L~f =
     {q : W-bound L~SpStSeq L~f < q`1 & q`1 < E-bound L~SpStSeq L~f
        & S-bound L~SpStSeq L~f < q`2 & q`2 < N-bound L~SpStSeq L~f}
          by SPRECT_3:54;
   hence thesis by A35,A39,A42,A44;
  end;
   consider z1 being set such that
A45: z1 in LeftComp SpStSeq L~f by XBOOLE_0:def 1;
        LeftComp SpStSeq L~f misses RightComp SpStSeq L~f by SPRECT_1:96;
then A46: z1 <> z2 by A21,A45,XBOOLE_0:3;
   reconsider z1 as Point of TOP-REAL 2 by A45;
   consider P being Subset of TOP-REAL 2 such that
A47: P is_S-P_arc_joining z1,z2 and
A48: P c= RightComp f by A2,A3,A17,A19,A20,A45,A46,TOPREAL4:30;
   consider Red' being FinSequence of TOP-REAL 2 such that
A49: Red' is_S-Seq and
A50: P = L~Red' and
A51: z1 = Red'/.1 and
A52: z2 = Red'/.len Red' by A47,TOPREAL4:def 1;
A53: L~SpStSeq L~f meets L~Red' by A21,A45,A49,A51,A52,SPRECT_3:50;
A54: 2 in dom f by A8,FINSEQ_3:27;
A55: len f in dom f by FINSEQ_5:6;
A56: len f -' 1 >= 1 & len f -' 1 <= len f by A8,BINARITH:62,67;
then A57: len f -' 1 in dom f by FINSEQ_3:27;
          1 <= len f by A55,FINSEQ_3:27;
then A58: len f -' 1 + 1 = len f by BINARITH:53;
then A59: len f -' 1 < len f by NAT_1:13;
A60: <*NW-corner L~f*> is_in_the_area_of f by SPRECT_2:30;
A61:  w-'1 < width GoB f by A10,NAT_1:13;
  then Int cell(GoB f,i,w-'1) misses L~SpStSeq L~f by A4,A5,A18,SPRECT_3:72;
  then A62: not z2 in L~SpStSeq L~f by A19,XBOOLE_0:3;
A63: LSeg(NW-corner L~f,NE-corner L~f) c= L~SpStSeq L~f by SPRECT_3:26;
A64: f/.1 = f/.len f by FINSEQ_6:def 1;
A65:  NW-corner L~f in LSeg(NW-corner L~f,NE-corner L~f) by TOPREAL1:6;
A66:  N-min L~f in LSeg(NW-corner L~f,NE-corner L~f) by SPRECT_3:28;
    then A67: LSeg(NW-corner L~f,N-min L~f) c= LSeg(NW-corner L~f,NE-corner L~f
)
         by A65,TOPREAL1:12;
A68: LSeg(NW-corner L~f,NE-corner L~f) is horizontal by SPRECT_3:29;
  then A69: LSeg(NW-corner L~f,N-min L~f) is horizontal by A67,SPRECT_1:11;
         1 + 2 <= len f by A7,XXREAL_0:2;
    then A70: LSeg(f,1) /\ LSeg(f,1+1) = {f/.(1+1)} by TOPREAL1:def 8;
         len f >= 2 + 1 by A7,XXREAL_0:2;
    then len f -' 1 >= 1 + 1 by BINARITH:67;
    then len f -' 1 -' 1 >= 1 by BINARITH:67;
    then A71:  len f -' 2 >= 1 by BINARITH:63;
A72:  len f -' 2 + 1 = len f -' 1 -' 1 + 1 by BINARITH:63
        .= len f -' 1 by A56,BINARITH:53;
         len f >= 2 by A7,XXREAL_0:2;
    then len f -' 2 + 2 = len f by BINARITH:53;
    then A73: LSeg(f,len f -' 1) /\ LSeg(f,len f -' 2) = {f/.(len f -' 1)}
         by A71,A72,TOPREAL1:def 8;
A74: f/.2 in N-most L~f by A1,SPRECT_2:34;
         N-most L~f c= LSeg(NW-corner L~f,NE-corner L~f) by XBOOLE_1:17;
    then A75: LSeg(f/.1, f/.2) c= LSeg(NW-corner L~f,NE-corner L~f)
             by A1,A66,A74,TOPREAL1:12;
A76: ((GoB f)*(i,w-'1))`1 = ((GoB f)*(i,1))`1 by A4,A5,A18,A61,GOBOARD5:3
            .= A`1 by A4,A5,A6,A9,GOBOARD5:3;
A77: (GoB f)*(i+1,w)`2 = (GoB f)*(1,w)`2 by A9,A12,GOBOARD5:2
            .= A`2 by A4,A5,A6,A9,GOBOARD5:2;
then A78: (f/.2)`2 = N-bound L~f by A16,EUCLID:56;
A79:  <*(GoB f)*(i+1,w)*> is_in_the_area_of f by A9,A12,SPRECT_3:66;
          <*(GoB f)*(i,w-'1)*> is_in_the_area_of f by A4,A5,A18,A61,SPRECT_3:66
;
     then <*(GoB f)*(i,w-'1),(GoB f)*(i+1,w)*> is_in_the_area_of f
      by A79,SPRECT_3:59;
     then <*z2*> is_in_the_area_of f by SPRECT_3:67;
     then A80: <*z2*> is_in_the_area_of SpStSeq L~f by SPRECT_3:70;
A81: (GoB f)*(i,w-'1) = f/.(len f -' 1) by A1,A6,A15,SPRECT_3:46;
    then LSeg(A,f/.(len f -' 1)) is vertical by A76,SPPOL_1:37;
    then A82:  LSeg(A,f/.(len f -' 1)) /\ LSeg(NW-corner L~f,A) = { A }
              by A69,SPRECT_3:22;
A83: (NW-corner L~f)`2 = A`2 by PSCOMP_1:95;
A84: (NW-corner L~f)`1 <= A`1 by PSCOMP_1:97;
     A`1 <= (f/.2)`1 by A74,PSCOMP_1:98;
     then A in LSeg(NW-corner L~f,f/.2) by A16,A77,A83,A84,GOBOARD7:9;
     then A85:  LSeg(A,f/.2) /\ LSeg(NW-corner L~f,A) = { A } by TOPREAL1:14;
        ((GoB f)*(i,w))`2 = ((GoB f)*(1,w))`2 by A4,A5,A9,GOBOARD5:2
      .= ((GoB f)*(i+1,w))`2 by A9,A12,GOBOARD5:2;
   then ((GoB f)*(i,w-'1)+(GoB f)*(i,w))`2
     = ((GoB f)*(i,w-'1))`2+((GoB f)*(i+1,w))`2 by TOPREAL3:7
    .= ((GoB f)*(i,w-'1)+(GoB f)*(i+1,w))`2 by TOPREAL3:7;
   then p2`2 = 1/2*((GoB f)*(i,w-'1)+(GoB f)*(i+1,w)) `2 by TOPREAL3:9
     .= z2`2 by TOPREAL3:9;
then A86: LSeg(p2,z2) is horizontal by SPPOL_1:36;
A87: f/.(len f -' 1) in LSeg(f/.(len f -' 1), f/.len f) by TOPREAL1:6;
A88: (GoB f)*(i,w) = f/.len f by A1,A6,FINSEQ_6:def 1;
      p2 = 1/2*((GoB f)*(i,w-'1))+(1 - 1/2)*((GoB f)*(i,w)) by EUCLID:36;
 then A89: p2 in LSeg(f/.(len f -' 1), f/.len f)
   by A81,A88,SPPOL_1:22;
then A90: LSeg(p2,f/.(len f -' 1)) c= LSeg(f/.(len f -' 1), f/.len f)
           by A87,TOPREAL1:12;
      LSeg(f/.(len f -' 1), f/.len f) = LSeg(f,len f -' 1)
           by A56,A58,TOPREAL1:def 5;
 then LSeg(f/.(len f -' 1), f/.len f) c= L~f by TOPREAL3:26;
then A91: LSeg(p2,f/.(len f -' 1)) c= L~f by A90,XBOOLE_1:1;
A92: LSeg(p2,f/.(len f -' 1)) c= LSeg(f,len f -' 1)
          by A56,A58,A90,TOPREAL1:def 5;
A93: p2`1 = 1/2*((GoB f)*(i,w-'1)+A)`1 by A6,TOPREAL3:9
       .= 1/2*(A`1+A`1) by A76,TOPREAL3:7
       .= (N-min L~f)`1;
  then A94: LSeg(p2,f/.(len f -' 1)) is vertical by A76,A81,SPPOL_1:37;
  A95: p2 in LSeg(p2,f/.(len f -' 1)) by TOPREAL1:6;
then A96: p2 in L~f by A91;
A97: now assume p2 = A;
        then f/.(len f -' 1) = f/.len f by A1,A6,A64,A81,SPRECT_3:15;
       hence contradiction by A55,A57,A58,GOBOARD7:31;
      end;

          1 + 1 + 1 <= len f by A7,XXREAL_0:2;
     then A98: 1 + 1 <= len f -' 1 by BINARITH:67;
     then A99:  1 <= len f -' 1 -' 1 by BINARITH:67;
          1 <= len f -' 1 by A98,XXREAL_0:2;
     then A100:  len f -' 1 -'1 + 1 = len f -' 1 by BINARITH:53;
A101:  len f >= 2 by A7,XXREAL_0:2;
A102: len f -' 1 -' 1 + 2 = len f -' 2 + 2 by BINARITH:63
         .= len f by A101,BINARITH:53;
A103: for i,j st 1 <= i & i <= j & j < len f -' 1 holds
         L~mid(f,i,j) misses LSeg(p2,f/.(len f -' 1))
   proof let l,j such that
A104: 1 <= l and
A105: l <= j and
A106: j < len f -' 1;
          len f -' 1 > l by A105,A106,XXREAL_0:2;
     then len f -' 1 > 1 by A104,XXREAL_0:2;
     then A107:    len f -' 1 < len f by BINARITH:69;
     then A108: j < len f by A106,XXREAL_0:2;
    assume L~mid(f,l,j) meets LSeg(p2,f/.(len f -' 1));
     then L~mid(f,l,j) /\ LSeg(p2,f/.(len f -' 1)) <> {} by XBOOLE_0:def 7;
     then consider p being Point of TOP-REAL 2 such that
A109: p in L~mid(f,l,j) /\ LSeg(p2,f/.(len f -' 1)) by SUBSET_1:10;
A110:  p in LSeg(p2,f/.(len f -' 1)) by A109,XBOOLE_0:def 3;
          p in L~mid(f,l,j) by A109,XBOOLE_0:def 3;
     then consider ii being Element of NAT such that
A111:  1 <= ii and
A112:  ii+1 <= len mid(f,l,j) and
A113:  p in LSeg(mid(f,l,j),ii) by SPPOL_2:13;
     set k = ii+l-'1;
          len mid(f,l,j) = j-'l+1 by A104,A105,A108,JORDAN4:20;
     then A114:   ii<j-'l+1 by A112,NAT_1:13;
     per cases by A105,REAL_1:def 5;
     suppose l = j;
      then len mid(f,l,j) = 1 by A104,A108,JORDAN4:27;
      then L~mid(f,l,j) = {} by TOPREAL1:28;
     hence thesis by A113,SPPOL_2:17;
     end;
     suppose
A115:    l < j;
      then A116:    LSeg(mid(f,l,j),ii)=LSeg(f,k) by A104,A108,A111,A114,
JORDAN4:31;
A117:   ii + l >= ii + 1 by A104,XREAL_1:8;
          ii + 1 >= 1 + 1 by A111,XREAL_1:8;
     then A118:   ii + l >= 1 + 1 by A117,XXREAL_0:2;
     then A119:   k >= 1 by BINARITH:67;
A120:    ii + l >= 1 by A118,XXREAL_0:2;
A121:  now assume k + 1 >= len f -' 1;
         then k >= len f -'1 -' 1 by BINARITH:71;
         then A122:       ii + l >= len f -' 1 by A120,BINARITH:74;
              ii + l < j -' l + 1 + l by A114,XREAL_1:8;
         then ii + l < j -' l + l + 1;
         then ii + l < j + 1 by A115,BINARITH:53;
         then len f -' 1 < j + 1 by A122,XXREAL_0:2;
        hence contradiction by A106,NAT_1:13;
       end;
A123:  p in LSeg(f,k) /\ LSeg(f,len f -' 1) by A92,A110,A113,A116,
XBOOLE_0:def 3;
     then LSeg(f,k) meets LSeg(f,len f -' 1) by XBOOLE_0:4;
     then k <= 1 by A107,A121,GOBOARD5:def 4;
     then A124:   k = 1 by A119,XXREAL_0:1;
          LSeg(f,1) /\ LSeg(f,len f -' 1) = { f.1 } by JORDAN4:54
         .= {f/.1} by A14,FINSEQ_4:def 4;
     then p = f/.1 by A123,A124,TARSKI:def 1;
    hence contradiction by A1,A64,A89,A97,A110,SPRECT_3:16;
   end;
   end;

A125: for j st 1 <= j & j < len f -' 1 holds
   L~mid(f,j,len f -' 1) /\ LSeg(p2,f/.(len f -' 1)) = {f/.(len f -' 1)}
 proof let j such that
A126: 1 <= j and
A127: j < len f -' 1;
A128:  f/.(len f -' 1) in LSeg(p2,f/.(len f -' 1)) by TOPREAL1:6;
A129:  1 <= len f -' 1 by A126,A127,XXREAL_0:2;
A130:  len f -' 1 -' 1 = len f -' 2 by BINARITH:63;
then A131:  len f -' 2 < len f -' 1 by A129,JORDAN5B:1;
          j <= len f -' 2 by A127,A130,BINARITH:67;
then A132:  LSeg(p2,f/.(len f -' 1)) misses L~mid(f,j,len f -' 2)
           by A103,A126,A131;
        len f -' 1 <= len f by BINARITH:52;
   then L~mid(f,j,len f -' 1) = LSeg(f,len f -' 2) \/ L~mid(f,j,len f -' 2)
         by A126,A127,A130,SPRECT_3:36;
  hence L~mid(f,j,len f -' 1) /\ LSeg(p2,f/.(len f -' 1))
             = LSeg(f,len f -' 2) /\ LSeg(p2,f/.(len f -' 1))
             by A132,XBOOLE_1:78
            .= {f/.(len f -' 1)} by A73,A92,A128,SPRECT_3:2;
 end;

A133: LSeg(NW-corner L~f,N-min L~f) misses LSeg(f/.(len f -' 1),p2)
   proof assume LSeg(NW-corner L~f,A) meets LSeg(f/.(len f -' 1),p2);
     then LSeg(p2,f/.(len f -' 1)) /\
 LSeg(NW-corner L~f,A) <> {} by XBOOLE_0:def 7;
     then consider p being Point of TOP-REAL 2 such that
A134: p in LSeg(p2,f/.(len f -' 1)) /\ LSeg(NW-corner L~f,A) by SUBSET_1:10;
A135:  p in LSeg(p2,f/.(len f -' 1)) by A134,XBOOLE_0:def 3;
          p in LSeg(NW-corner L~f,A) by A134,XBOOLE_0:def 3;
     then p in { A } by A1,A64,A82,A90,A135,XBOOLE_0:def 3;
     then p = A by TARSKI:def 1;
    hence contradiction by A1,A64,A89,A97,A135,SPRECT_3:16;
   end;
        p2`2 <> A`2 by A93,A97,TOPREAL3:11;
   then A136: LSeg(NW-corner L~f,N-min L~f) misses LSeg(z2,p2)
         by A69,A86,SPRECT_3:21;
     set G = GoB f;
A137:  LSeg(z2,p2) c= Int cell(G,i,w-'1) \/ { p2 } by A4,A5,A10,A18,A61,
GOBOARD6:43;
A138: Int cell(GoB f,i,w-'1) misses L~f by A5,A61,GOBOARD7:14;
          L~f /\ (Int cell(G,i,w-'1) \/ { p2 })
       = L~f /\ Int cell(G,i,w-'1) \/ L~f /\ { p2 } by XBOOLE_1:23
      .= {} \/ L~f /\ { p2 } by A138,XBOOLE_0:def 7
      .= L~f /\ { p2 };
     then A139:   LSeg(z2,p2) /\ L~f c= L~f /\ { p2 } by A137,XBOOLE_1:26;
          L~f /\ { p2 } c= { p2 } by XBOOLE_1:17;
     then A140:  LSeg(z2,p2) /\ L~f c= { p2 } by A139,XBOOLE_1:1;
A141: for i,j st 1 <= i & i < j & j < len f holds
         L~mid(f,i,j) misses LSeg(z2,p2)
   proof let l,j such that
A142: 1 <= l and
A143: l < j and
A144: j < len f;
    assume L~mid(f,l,j) meets LSeg(z2,p2);
     then L~mid(f,l,j) /\ LSeg(z2,p2) <> {} by XBOOLE_0:def 7;
     then consider p being Point of TOP-REAL 2 such that
A145: p in L~mid(f,l,j) /\ LSeg(z2,p2) by SUBSET_1:10;
A146:  p in LSeg(z2,p2) by A145,XBOOLE_0:def 3;
          p in L~mid(f,l,j) by A145,XBOOLE_0:def 3;
     then consider ii being Element of NAT such that
A147:  1 <= ii and
A148:  ii+1 <= len mid(f,l,j) and
A149:  p in LSeg(mid(f,l,j),ii) by SPPOL_2:13;
A150:  len mid(f,l,j) = j-'l+1 by A142,A143,A144,JORDAN4:20;
     then ii<j-'l+1 by A148,NAT_1:13;
     then A151:   p in LSeg(f,ii+l-'1) by A142,A143,A144,A147,A149,JORDAN4:31;
     then A152:  p in LSeg(f,ii+l-'1) /\ LSeg(z2,p2) by A146,XBOOLE_0:def 3;
     set k = ii+l-'1;
A153:   ii + l >= ii + 1 by A142,XREAL_1:8;
          ii + 1 >= 1 + 1 by A147,XREAL_1:8;
     then ii + l >= 1 + 1 by A153,XXREAL_0:2;
     then A154:   k >= 1 by BINARITH:67;
          len f >= 1 + 1 by A7,XXREAL_0:2;
     then A155:  len f -' 1 >= 1 by BINARITH:67;
          LSeg(f,k) c= L~f by TOPREAL3:26;
     then LSeg(f,k) /\ LSeg(z2,p2) c= L~f /\ LSeg(z2,p2) by XBOOLE_1:26;
     then LSeg(f,k) /\ LSeg(z2,p2) c= { p2 } by A140,XBOOLE_1:1;
     then p = p2 by A152,TARSKI:def 1;
     then p2 in LSeg(f,k) /\ LSeg(f/.(len f -' 1), f/.len f)
          by A89,A151,XBOOLE_0:def 3;
     then A156:   p2 in LSeg(f,k) /\ LSeg(f,len f -' 1) by A58,A155,
TOPREAL1:def 5;
then A157:  LSeg(f,k) meets LSeg(f,len f -' 1) by XBOOLE_0:4;
    per cases by A59,A157,GOBOARD5:def 4;
    suppose k <= 1;
     then A158:   k = 1 by A154,XXREAL_0:1;
          LSeg(f,1) /\ LSeg(f,len f -' 1) = {f.1} by JORDAN4:54
         .= {f/.1} by A14,FINSEQ_4:def 4;
    hence contradiction by A1,A97,A156,A158,TARSKI:def 1;
    end;
    suppose k+1 >= len f -' 1;
     then A159:  k >= len f -' 1 -' 1 by BINARITH:71;
          ii+l >= l by NAT_1:11;
     then A160: ii+l >= 1 by A142,XXREAL_0:2;
          ii <= j -' l by A148,A150,XREAL_1:8;
     then ii + l <= j by A143,BINARITH:72;
     then ii+l < len f by A144,XXREAL_0:2;
     then ii+l-'1 < len f -' 1 by A160,BINARITH:74;
     then ii+l-'1 <= len f -' 1 -' 1 by BINARITH:67;
     then k = len f -' 1 -' 1 by A159,XXREAL_0:1;
     then p2 in {f/.(len f -' 1)} by A99,A100,A102,A156,TOPREAL1:def 8;
     then p2 = f/.(len f -' 1) by TARSKI:def 1;
    hence contradiction by A6,A81,A97,SPRECT_3:15;
   end;
   end;
        f is_in_the_area_of f by SPRECT_2:25;
then A161: <*p2*> is_in_the_area_of f by A91,A95,SPRECT_3:63;
  then A162: <*p2*> is_in_the_area_of SpStSeq L~f by SPRECT_3:70;
       <*p2,z2*> = <*p2*>^<*z2*> by FINSEQ_1:def 9;
  then <*p2,z2*> is_in_the_area_of SpStSeq L~f by A80,A162,SPRECT_2:28;
  then A163: L~SpStSeq L~f /\ LSeg(p2,z2) c= { p2 } by A62,SPRECT_3:64;
      w-'1 <= width(GoB f) by BINARITH:52;
 then ((GoB f)*(i,w-'1))`1 = ((GoB f)*(i,1))`1 by A4,A5,A18,GOBOARD5:3
      .= ((GoB f)*(i,w))`1 by A4,A5,A9,GOBOARD5:3;
 then ((GoB f)*(i,w)+(GoB f)*(i+1,w))`1
   = ((GoB f)*(i,w-'1))`1+((GoB f)*(i+1,w))`1 by TOPREAL3:7
  .= ((GoB f)*(i,w-'1)+(GoB f)*(i+1,w))`1 by TOPREAL3:7;
 then p1`1 = 1/2*((GoB f)*(i,w-'1)+(GoB f)*(i+1,w))`1 by TOPREAL3:9
   .= z2`1 by TOPREAL3:9;
then A164: LSeg(p1,z2) is vertical by SPPOL_1:37;
A165: f/.2 in LSeg(f/.1, f/.(1+1)) by TOPREAL1:6;
      p1 = 1/2*((GoB f)*(i,w))+(1-1/2)*((GoB f)*(i+1,w)) by EUCLID:36;
 then A166: p1 in LSeg(f/.1, f/.2) by A1,A6,A16,SPPOL_1:22;
then A167: LSeg(p1,f/.2) c= LSeg(f/.1, f/.2) by A165,TOPREAL1:12;
 A168:  LSeg(f/.1, f/.(1+1)) = LSeg(f,1) by A8,TOPREAL1:def 5;
 then LSeg(f/.1, f/.2) c= L~f by TOPREAL3:26;
then A169: LSeg(p1,f/.2) c= L~f by A167,XBOOLE_1:1;
A170: p1`2 = 1/2*(A+(GoB f)*(i+1,w))`2 by A6,TOPREAL3:9
       .= 1/2*(A`2+A`2) by A77,TOPREAL3:7
       .= N-bound L~f by EUCLID:56;
   A171: p1 in LSeg(p1,f/.2) by TOPREAL1:6;
A172: now assume p1 = A;
        then f/.1 = f/.(1+1) by A1,A6,A16,SPRECT_3:15;
       hence contradiction by A14,A54,GOBOARD7:31;
      end;
A173: for i,j st 2 < i & i <= j & j<= len f holds
         L~mid(f,i,j) misses LSeg(p1,f/.2)
   proof let l,j such that
A174: 2 < l and
A175: l <= j and
A176: j<= len f;
A177: 1 < l by A174,XXREAL_0:2;
    assume L~mid(f,l,j) meets LSeg(p1,f/.2);
     then L~mid(f,l,j) /\ LSeg(p1,f/.2) <> {} by XBOOLE_0:def 7;
     then consider p being Point of TOP-REAL 2 such that
A178: p in L~mid(f,l,j) /\ LSeg(p1,f/.2) by SUBSET_1:10;
A179:  p in LSeg(p1,f/.2) by A178,XBOOLE_0:def 3;
          p in L~mid(f,l,j) by A178,XBOOLE_0:def 3;
     then consider ii being Element of NAT such that
A180:  1 <= ii and
A181:  ii+1 <= len mid(f,l,j) and
A182:  p in LSeg(mid(f,l,j),ii) by SPPOL_2:13;
     set k = ii+l-'1;
A183:  len mid(f,l,j) = j-'l+1 by A175,A176,A177,JORDAN4:20;
     then A184:   ii<j-'l+1 by A181,NAT_1:13;
A185:   ii + l > ii + 2 by A174,XREAL_1:8;
          ii + 2 >= 1 + 2 by A180,XREAL_1:8;
     then ii + l > 1 + 2 by A185,XXREAL_0:2;
     then A186:   k > 1+1 by BINARITH:70;
     per cases by A175,REAL_1:def 5;
     suppose l = j;
      then len mid(f,l,j) = 1 by A176,A177,JORDAN4:27;
      then L~mid(f,l,j) = {} by TOPREAL1:28;
     hence thesis by A182,SPPOL_2:17;
     end;
     suppose l < j;
    then LSeg(mid(f,l,j),ii)=LSeg(f,k) by A176,A177,A180,A184,JORDAN4:31;
then A187:  p in LSeg(f,k) /\ LSeg(f,1) by A167,A168,A179,A182,XBOOLE_0:def 3;
     then LSeg(f,k) meets LSeg(f,1) by XBOOLE_0:4;
     then k+1 >= len f by A186,GOBOARD5:def 4;
     then A188:  k >= len f -' 1 by BINARITH:71;
          ii <= j -' l by A181,A183,XREAL_1:8;
     then ii + l <= j by A175,BINARITH:72;
     then ii+l <= len f by A176,XXREAL_0:2;
     then ii+l-'1 <= len f -' 1 by BINARITH:60;
     then A189:  k = len f -' 1 by A188,XXREAL_0:1;
          LSeg(f,1) /\ LSeg(f,len f -' 1) = { f.1 } by JORDAN4:54
         .= {f/.1} by A14,FINSEQ_4:def 4;
     then p = f/.1 by A187,A189,TARSKI:def 1;
    hence contradiction by A1,A166,A172,A179,SPRECT_3:16;
   end;
   end;
A190: for j st 2 < j & j <= len f holds
         L~mid(f,2,j) /\ LSeg(p1,f/.2) = {f/.2}
   proof let j such that
A191: 2 < j and
A192: j <= len f;
          2+1 <= j by A191,NAT_1:13;
then A193:  LSeg(p1,f/.2) misses L~mid(f,2+1,j) by A173,A192;
A194:  f/.2 in LSeg(p1,f/.2) by TOPREAL1:6;
          L~mid(f,2,j) = LSeg(f,2) \/ L~mid(f,2+1,j) by A191,A192,SPRECT_3:35;
    hence L~mid(f,2,j) /\ LSeg(p1,f/.2)
           = LSeg(f,2) /\ LSeg(p1,f/.2) by A193,XBOOLE_1:78
          .= {f/.2} by A70,A167,A168,A194,SPRECT_3:2;
   end;
A195:    LSeg(p1,f/.2) misses LSeg(NW-corner L~f,A)
   proof assume LSeg(p1,f/.2) meets LSeg(NW-corner L~f,A);
     then LSeg(p1,f/.2) /\ LSeg(NW-corner L~f,A) <> {} by XBOOLE_0:def 7;
     then consider p being Point of TOP-REAL 2 such that
A196: p in LSeg(p1,f/.2) /\ LSeg(NW-corner L~f,A) by SUBSET_1:10;
A197:  p in LSeg(p1,f/.2) by A196,XBOOLE_0:def 3;
          p in LSeg(NW-corner L~f,A) by A196,XBOOLE_0:def 3;
     then p in { A } by A1,A85,A167,A197,XBOOLE_0:def 3;
     then p = A by TARSKI:def 1;
    hence contradiction by A1,A166,A172,A197,SPRECT_3:16;
   end;
A198: LSeg(NW-corner L~f,N-min L~f) misses LSeg(z2,p1)
   proof assume LSeg(NW-corner L~f,N-min L~f) meets LSeg(z2,p1);
     then LSeg(NW-corner L~f,N-min L~f) /\ LSeg(z2,p1) <> {} by XBOOLE_0:def 7
;
     then consider p being Point of TOP-REAL 2 such that
A199: p in LSeg(NW-corner L~f,N-min L~f) /\ LSeg(z2,p1) by SUBSET_1:10;
A200:  p in LSeg(NW-corner L~f,N-min L~f) by A199,XBOOLE_0:def 3;
A201:  p in LSeg(z2,p1) by A199,XBOOLE_0:def 3;
          LSeg(NW-corner L~f,NE-corner L~f) /\ LSeg(z2,p1) = {p1}
            by A68,A75,A164,A166,SPRECT_3:23;
     then p in {p1} by A67,A200,A201,XBOOLE_0:def 3;
     then p1 in LSeg(NW-corner L~f,N-min L~f) by A200,TARSKI:def 1;
     then p1 in LSeg(NW-corner L~f,N-min L~f) /\ L~f by A169,A171,
XBOOLE_0:def 3;
     then p1 in { N-min L~f } by PSCOMP_1:102;
    hence contradiction by A172,TARSKI:def 1;
   end;
A202: for i,j st 1 < i & i < j & j<= len f holds
         L~mid(f,i,j) misses LSeg(z2,p1)
   proof let l,j such that
A203: 1 < l and
A204: l < j and
A205: j<= len f;
    assume L~mid(f,l,j) meets LSeg(z2,p1);
     then L~mid(f,l,j) /\ LSeg(z2,p1) <> {} by XBOOLE_0:def 7;
     then consider p being Point of TOP-REAL 2 such that
A206: p in L~mid(f,l,j) /\ LSeg(z2,p1) by SUBSET_1:10;
A207:  p in LSeg(z2,p1) by A206,XBOOLE_0:def 3;
          p in L~mid(f,l,j) by A206,XBOOLE_0:def 3;
     then consider ii being Element of NAT such that
A208:  1 <= ii and
A209:  ii+1 <= len mid(f,l,j) and
A210:  p in LSeg(mid(f,l,j),ii) by SPPOL_2:13;
A211:  len mid(f,l,j) = j-'l+1 by A203,A204,A205,JORDAN4:20;
     then ii<j-'l+1 by A209,NAT_1:13;
     then A212:   p in LSeg(f,ii+l-'1) by A203,A204,A205,A208,A210,JORDAN4:31;
     then A213:  p in LSeg(f,ii+l-'1) /\ LSeg(z2,p1) by A207,XBOOLE_0:def 3;
     set k = ii+l-'1, G = GoB f;
A214:   ii + l > ii + 1 by A203,XREAL_1:8;
          ii + 1 >= 1 + 1 by A208,XREAL_1:8;
     then ii + l > 1 + 1 by A214,XXREAL_0:2;
     then A215:   k > 1 by BINARITH:70;
A216:  LSeg(z2,p1) c= Int cell(G,i,w-'1) \/ { p1 } by A4,A5,A10,A18,A61,
GOBOARD6:44;
A217: Int cell(GoB f,i,w-'1) misses L~f by A5,A61,GOBOARD7:14;
          L~f /\ (Int cell(G,i,w-'1) \/ { p1 })
       = L~f /\ Int cell(G,i,w-'1) \/ L~f /\ { p1 } by XBOOLE_1:23
      .= {} \/ L~f /\ { p1 } by A217,XBOOLE_0:def 7
      .= L~f /\ { p1 };
     then A218:   LSeg(z2,p1) /\ L~f c= L~f /\ { p1 } by A216,XBOOLE_1:26;
          L~f /\ { p1 } c= { p1 } by XBOOLE_1:17;
     then A219:  LSeg(z2,p1) /\ L~f c= { p1 } by A218,XBOOLE_1:1;
A220:  len f >= 2 by A7,XXREAL_0:2;
          LSeg(f,k) c= L~f by TOPREAL3:26;
     then LSeg(f,k) /\ LSeg(z2,p1) c= L~f /\ LSeg(z2,p1) by XBOOLE_1:26;
     then LSeg(f,k) /\ LSeg(z2,p1) c= { p1 } by A219,XBOOLE_1:1;
     then p = p1 by A213,TARSKI:def 1;
     then p1 in LSeg(f,k) /\ LSeg(f/.1,f/.(1+1)) by A166,A212,XBOOLE_0:def 3;
     then A221:   p1 in LSeg(f,k) /\ LSeg(f,1) by A220,TOPREAL1:def 5;
     then LSeg(f,k) meets LSeg(f,1) by XBOOLE_0:4;
then A222:   k <= 1+1 or k+1 >= len f by GOBOARD5:def 4;
    per cases by A222;
    suppose
A223:   k <= 2;
          k >= 1+1 by A215,NAT_1:13;
     then A224:   k = 2 by A223,XXREAL_0:1;
          1 + 2 <= len f by A7,XXREAL_0:2;
     then p1 in {f/.(1+1)} by A221,A224,TOPREAL1:def 8;
     then p1 = f/.(1+1) by TARSKI:def 1;
    hence contradiction by A6,A16,A172,SPRECT_3:15;
    end;
    suppose k+1 >= len f;
     then A225:  k >= len f -' 1 by BINARITH:71;
          ii <= j -' l by A209,A211,XREAL_1:8;
     then ii + l <= j by A204,BINARITH:72;
     then ii+l <= len f by A205,XXREAL_0:2;
     then ii+l-'1 <= len f -' 1 by BINARITH:60;
     then A226:  k = len f -' 1 by A225,XXREAL_0:1;
          LSeg(f,1) /\ LSeg(f,len f -' 1) = { f.1 } by JORDAN4:54
         .= {f/.1} by A14,FINSEQ_4:def 4;
    hence contradiction by A1,A172,A221,A226,TARSKI:def 1;
   end;
   end;
       LSeg(f/.1,f/.2) c= L~SpStSeq L~f by A1,SPRECT_3:48;
  then A227: L~SpStSeq L~f /\ LSeg(p1,z2) = { p1 }
           by A62,A80,A166,SPRECT_3:65;
A228: LSeg(p1,f/.2) is horizontal by A78,A170,SPPOL_1:36;
   A229: p1 in LSeg(p1,f/.2) by TOPREAL1:6;
then A230: p1 in L~f by A169;
        f is_in_the_area_of f by SPRECT_2:25;
then A231: <*p1*> is_in_the_area_of f by A169,A229,SPRECT_3:63;
        RightComp f misses L~f by SPRECT_3:42;
then A232: L~f misses L~Red' by A48,A50,XBOOLE_1:63;
 reconsider Red' as S-Sequence_in_R2 by A49;
       len Red' in dom Red' by FINSEQ_5:6;
then A233:  z2 in L~Red' by A52,SPPOL_2:48;
  set u1 = Last_Point(L~Red',Red'/.1,Red'/.len Red',L~SpStSeq L~f),
      Red = L_Cut(Red',u1),
      u2 = First_Point(L~Red,Red/.1,Red/.len Red,LSeg(z2,p1)),
      u3 = First_Point(L~Red,Red/.1,Red/.len Red,LSeg(z2,p2));
           NW-corner L~SpStSeq L~f = NW-corner L~f by SPRECT_1:70;
then A234: u1 <> NW-corner L~f by A21,A45,A51,A52,SPRECT_3:55;
A235: L~Red' is_an_arc_of z1,z2 by A51,A52,TOPREAL1:31;
       L~Red' /\ L~SpStSeq L~f is closed by TOPS_1:35;
then A236: u1 in L~Red' /\ L~SpStSeq L~f by A51,A52,A53,A235,JORDAN5C:def 2;
  then A237: u1 in L~SpStSeq L~f by XBOOLE_0:def 3;
A238: u1 in L~Red' by A236,XBOOLE_0:def 3;
A239:
 now assume u1 in L~f;
   then u1 in L~f /\ L~Red' by A238,XBOOLE_0:def 3;
  hence contradiction by A232,XBOOLE_0:def 7;
 end;
       len Red' in dom Red' by FINSEQ_5:6;
then u1 <> Red'.len Red' by A52,A62,A237,FINSEQ_4:def 4;
  then reconsider Red as S-Sequence_in_R2 by A238,JORDAN3:69;
     1 in dom Red by FINSEQ_5:6;
then A240: Red/.1 =Red.1 by FINSEQ_4:def 4
     .= u1 by A238,JORDAN3:58;
A241: L~SpStSeq L~f /\ L~Red = { u1 } by A52,A53,A62,Th6;
       len Red' in dom Red' by FINSEQ_5:6;
  then z2 = Red'.len Red' by A52,FINSEQ_4:def 4;
then A242: z2 in L~Red by A62,A233,A237,A238,JORDAN5B:25;
        Red is_in_the_area_of SpStSeq L~f by A21,A45,A51,A52,SPRECT_3:71;
   then A243: Red is_in_the_area_of f by SPRECT_3:70;
     A244: L~Red c= L~Red' by A238,JORDAN3:77;
then A245: L~f misses L~Red by A232,XBOOLE_1:63;
      z2 in LSeg(p1,z2) by TOPREAL1:6;
then A246: LSeg(p1,z2) meets L~Red by A242,XBOOLE_0:3;
      z2 in LSeg(p2,z2) by TOPREAL1:6;
then A247: LSeg(p2,z2) meets L~Red by A242,XBOOLE_0:3;
A248: L~Red /\ LSeg(p1,z2) is closed by TOPS_1:35;
A249: L~Red /\ LSeg(p2,z2) is closed by TOPS_1:35;
       L~Red is_an_arc_of Red/.1,Red/.len Red by TOPREAL1:31;
then A250: u3 in LSeg(p2,z2) /\ L~Red by A247,A249,JORDAN5C:def 1;
then A251: u3 in L~Red by XBOOLE_0:def 3;
A252: u3 in LSeg(p2,z2) by A250,XBOOLE_0:def 3;
A253: u3 <> p2 by A91,A95,A232,A244,A251,XBOOLE_0:3;
      p2 in LSeg(p2,z2) by TOPREAL1:6;
then A254: LSeg(p2,u3) c= LSeg(p2,z2) by A252,TOPREAL1:12;
 then A255: LSeg(u3,p2) is horizontal by A86,SPRECT_1:11;
A256: LSeg(NW-corner L~f,N-min L~f) misses LSeg(u3,p2) by A136,A254,XBOOLE_1:63
;
A257: for i,j st 1 <= i & i < j & j < len f holds
         L~mid(f,i,j) misses LSeg(u3,p2)
   proof let i,j;
    assume 1 <= i & i < j & j < len f;
     then L~mid(f,i,j) misses LSeg(z2,p2) by A141;
    hence thesis by A254,XBOOLE_1:63;
   end;
A258:
  now assume
A259:  u3 = Red.1;
        1 in dom Red by FINSEQ_5:6;
   then u3 in L~SpStSeq L~f by A237,A240,A259,FINSEQ_4:def 4;
   then u3 in LSeg(p2,z2) /\ L~SpStSeq L~f by A252,XBOOLE_0:def 3;
   hence contradiction by A163,A253,TARSKI:def 1;
  end;
       L~Red is_an_arc_of Red/.1,Red/.len Red by TOPREAL1:31;
then A260: u2 in LSeg(p1,z2) /\ L~Red by A246,A248,JORDAN5C:def 1;
then A261: u2 in L~Red by XBOOLE_0:def 3;
A262: u2 in LSeg(p1,z2) by A260,XBOOLE_0:def 3;
A263: u2 <> p1 by A169,A229,A232,A244,A261,XBOOLE_0:3;
      p1 in LSeg(p1,z2) by TOPREAL1:6;
then A264: LSeg(p1,u2) c= LSeg(p1,z2) by A262,TOPREAL1:12;
  then A265: LSeg(p1,u2) is vertical by A164,SPRECT_1:12;
A266: LSeg(NW-corner L~f,N-min L~f) misses LSeg(u2,p1) by A198,A264,XBOOLE_1:63
;
A267: for i,j st 1 < i & i < j & j<= len f holds
         L~mid(f,i,j) misses LSeg(u2,p1)
   proof let i,j;
    assume 1 < i & i < j & j<= len f;
     then L~mid(f,i,j) misses LSeg(z2,p1) by A202;
    hence thesis by A264,XBOOLE_1:63;
   end;
A268:
  now assume
A269:  u2 = Red.1;
        1 in dom Red by FINSEQ_5:6;
   then u2 in L~SpStSeq L~f by A237,A240,A269,FINSEQ_4:def 4;
   then u2 in { p1 } by A227,A262,XBOOLE_0:def 3;
   hence contradiction by A263,TARSKI:def 1;
  end;
  reconsider Red2=R_Cut(Red,u3) as S-Sequence_in_R2 by A251,A258,JORDAN3:70;
A270: Rev Red2 is S-Sequence_in_R2 by SPPOL_2:47;
A271: Red2/.1 = u1 by A240,A251,SPRECT_3:39;
A272: len Red2 in dom Red2 by FINSEQ_5:6;
A273: (Rev Red2)/.1 = Red2/.len Red2 by FINSEQ_5:68
          .= Red2.len Red2 by A272,FINSEQ_4:def 4
          .= u3 by A251,JORDAN3:59;
then A274: ((Rev Red2)/.1)`2 = p2`2 & (Rev Red2)/.1 <> p2
             by A86,A91,A95,A232,A244,A251,A252,SPPOL_1:63,XBOOLE_0:3;
A275: L~Rev Red2 = L~Red2 by SPPOL_2:22;
       u3 in L~Red2 & u3 in LSeg(p2,u3) by A251,A258,JORDAN5B:23,TOPREAL1:6;
then A276: u3 in L~Red2 /\ LSeg(p2,u3) by XBOOLE_0:def 3;
       now assume u1 in LSeg(p2,z2);
    then u1 in L~SpStSeq L~f /\ LSeg(p2,z2) by A237,XBOOLE_0:def 3;
   hence contradiction by A96,A163,A239,TARSKI:def 1;
  end;
  then LSeg(p2,z2) /\ L~Red2={ u3 } by A240,A247,Th1;
  then LSeg(p2,u3) /\ L~Red2 c={ u3 } by A254,XBOOLE_1:26;
  then LSeg(p2,u3) /\ L~Red2={ u3 } by A276,ZFMISC_1:39;
  then reconsider RB2 = <*p2*>^Rev Red2 as S-Sequence_in_R2
            by A270,A273,A274,A275,SPRECT_3:31;
A277: L~Red2 c= L~Red by A251,JORDAN3:76;
        LSeg(p2,f/.(len f -' 1)) misses L~Red by A91,A245,XBOOLE_1:63;
   then LSeg(p2,f/.(len f -' 1)) misses L~Red2 by A277,XBOOLE_1:63;
   then A278: LSeg(p2,f/.(len f -' 1)) /\ L~Red2 = {} by XBOOLE_0:def 7;
       1 in dom Red2 by FINSEQ_5:6;
  then u1 in L~Red2 by A271,SPPOL_2:48;
  then u1 in L~SpStSeq L~f /\ L~Red2 & L~SpStSeq L~f /\ L~Red2 c= { u1 }
        by A237,A241,A277,XBOOLE_0:def 3,XBOOLE_1:26;
then A279: L~SpStSeq L~f /\ L~Red2 = { u1 } by ZFMISC_1:39;
  reconsider Red1=R_Cut(Red,u2) as S-Sequence_in_R2 by A261,A268,JORDAN3:70;
       len Red1 in dom Red1 by FINSEQ_5:6;
then A280: Red1/.len Red1 = Red1.len Red1 by FINSEQ_4:def 4
                  .= u2 by A261,JORDAN3:59;
A281: Red1/.1 = u1 by A240,A261,SPRECT_3:39;
A282: (Red1/.len Red1)`1 = p1`1 & Red1/.len Red1 <> p1
           by A164,A169,A229,A232,A244,A261,A262,A280,SPPOL_1:64,XBOOLE_0:3;
       u2 in L~Red1 & u2 in LSeg(p1,u2) by A261,A268,JORDAN5B:23,TOPREAL1:6;
then A283: u2 in L~Red1 /\ LSeg(p1,u2) by XBOOLE_0:def 3;
       now assume u1 in LSeg(p1,z2);
    then u1 in L~SpStSeq L~f /\ LSeg(p1,z2) by A237,XBOOLE_0:def 3;
   hence contradiction by A227,A230,A239,TARSKI:def 1;
  end;
  then LSeg(p1,z2) /\ L~Red1={ u2 } by A240,A246,Th1;
  then LSeg(p1,u2) /\ L~Red1 c={ u2 } by A264,XBOOLE_1:26;
  then LSeg(p1,u2) /\ L~Red1={ u2 } by A283,ZFMISC_1:39;
  then reconsider RB1 = Red1^<*p1*> as S-Sequence_in_R2
     by A280,A282,SPRECT_2:65;
A284: L~Red1 c= L~Red by A261,JORDAN3:76;
        LSeg(p1,f/.2) misses L~Red by A169,A245,XBOOLE_1:63;
   then LSeg(p1,f/.2) misses L~Red1 by A284,XBOOLE_1:63;
   then A285: LSeg(p1,f/.2) /\ L~Red1 = {} by XBOOLE_0:def 7;
      1 in dom Red1 by FINSEQ_5:6;
then A286: u1 in L~Red1 by A281,SPPOL_2:48;
 then u1 in L~SpStSeq L~f /\ L~Red1 & L~SpStSeq L~f /\ L~Red1 c= { u1 }
       by A237,A241,A284,XBOOLE_0:def 3,XBOOLE_1:26;
then A287: L~SpStSeq L~f /\ L~Red1 = { u1 } by ZFMISC_1:39;
       len Rev Red2 = len Red2 by FINSEQ_5:def 3;
then A288: RB2/.len RB2 = (Rev Red2)/.len Red2 by SPRECT_3:11
           .= u1 by A271,FINSEQ_5:68;
A289: RB2/.1 = p2 by FINSEQ_5:16;
      L~Rev Red2 = L~Red2 by SPPOL_2:22;
then A290: L~RB2 = L~Red2 \/ LSeg(p2,u3) by A273,SPPOL_2:20;
then A291: LSeg(p2,f/.(len f -' 1)) /\ L~RB2
         = {} \/ LSeg(p2,f/.(len f -' 1)) /\ LSeg(u3,p2) by A278,XBOOLE_1:23
        .={ p2 } by A94,A255,SPRECT_3:22;
       <*u3*> is_in_the_area_of f by A243,A251,SPRECT_3:63;
 then Red2 is_in_the_area_of f by A243,A251,SPRECT_3:69;
 then Rev Red2 is_in_the_area_of f by SPRECT_3:68;
then A292: RB2 is_in_the_area_of f by A161,SPRECT_2:28;
      1 in dom Red1 by FINSEQ_5:6;
then A293: RB1/.1 = u1 by A281,FINSEQ_4:83;
      len <*p1*> = 1 by FINSEQ_1:56;
 then len RB1 = len Red1 + 1 by FINSEQ_1:35;
then A294: RB1/.len RB1 = p1 by FINSEQ_4:82;
A295: L~RB1 = L~Red1 \/ LSeg(u2,p1) by A280,SPPOL_2:19;
then A296: LSeg(p1,f/.2) /\ L~RB1
            = {} \/ LSeg(p1,f/.2) /\ LSeg(u2,p1) by A285,XBOOLE_1:23
           .={ p1 } by A228,A265,SPRECT_3:22;
       <*u2*> is_in_the_area_of f by A243,A261,SPRECT_3:63;
  then Red1 is_in_the_area_of f by A243,A261,SPRECT_3:69;
then A297: RB1 is_in_the_area_of f by A231,SPRECT_2:28;
A298: L~Red' is_an_arc_of z1,z2 by A47,A50,TOPREAL4:3;
       L~Red' /\ L~SpStSeq L~f is closed by TOPS_1:35;
  then u1 in L~Red' /\ L~SpStSeq L~f by A51,A52,A53,A298,JORDAN5C:def 2;
  then u1 in L~SpStSeq L~f by XBOOLE_0:def 3;
  then u1 in
   (LSeg(NW-corner L~f,NE-corner L~f) \/ LSeg(NE-corner L~f,SE-corner L~f)) \/
   (LSeg(SE-corner L~f,SW-corner L~f) \/ LSeg(SW-corner L~f,NW-corner L~f))
       by SPRECT_1:43;
then A299: u1 in LSeg(NW-corner L~f,NE-corner L~f) \/ LSeg(NE-corner L~f,
SE-corner L~f)
    or
   u1 in LSeg(SE-corner L~f,SW-corner L~f) \/ LSeg(SW-corner L~f,NW-corner L~f)
       by XBOOLE_0:def 2;
   A300:  N-most L~f c= LSeg(NW-corner L~f,NE-corner L~f) &
    A in N-most L~f by PSCOMP_1:101,XBOOLE_1:17;
   then LSeg(NW-corner L~f,NE-corner L~f)
     = LSeg(NW-corner L~f,A) \/ LSeg(A,NE-corner L~f) by TOPREAL1:11;
   then A301: u1 in LSeg(NW-corner L~f,NE-corner L~f) implies
    u1 in LSeg(NW-corner L~f,A) or u1 in LSeg(A,NE-corner L~f) by
XBOOLE_0:def 2;
 per cases by A299,A301,XBOOLE_0:def 2;
 suppose
A302: u1 in LSeg(NW-corner L~f,A);
  set i1 = (S-min L~f)..f, i2 = (E-min L~f)..f;
A303: S-min L~f in rng f by SPRECT_2:45;
then A304: i1 in dom f by FINSEQ_4:30;
A305: E-min L~f in rng f by SPRECT_2:49;
then A306: i2 in dom f by FINSEQ_4:30;
         (S-max L~f)..f < i1 &
    i2 <= (S-max L~f)..f by A1,SPRECT_2:76,77;
then A307: i2 < i1 by XXREAL_0:2;
         (W-min L~f)..f < len f &
    i1 <= (W-min L~f)..f by A1,SPRECT_2:78,80;
then A308: i1 < len f by XXREAL_0:2;
         (N-max L~f)..f > 1 by A1,SPRECT_2:73;
then A309: (N-max L~f)..f >= 1+1 by NAT_1:13;
         (N-max L~f)..f <= (E-max L~f)..f by A1,SPRECT_2:74;
then A310: 2 <= (E-max L~f)..f by A309,XXREAL_0:2;
         (E-max L~f)..f < (E-min L~f)..f by A1,SPRECT_2:75;
then A311: 2 < i2 by A310,XXREAL_0:2;
then A312: 2 < i1 by A307,XXREAL_0:2;
  then 1 < i1 by XXREAL_0:2;
  then reconsider M1 = mid(f,i1,len f) as S-Sequence_in_R2 by A308,JORDAN4:52;
       (NW-corner L~f)`2 = N-bound L~f & A`2 = N-bound L~f
    by EUCLID:56;
then A313: u1`2 = (NW-corner L~f)`2 by A302,GOBOARD7:6;
       u1 in LSeg(NW-corner L~f,u1) by TOPREAL1:6;
  then u1 in LSeg(NW-corner L~f,u1) /\ L~Red1 by A286,XBOOLE_0:def 3;
then A314: { u1 } c= LSeg(NW-corner L~f,u1) /\ L~Red1 by ZFMISC_1:37;
A315: NW-corner L~f in LSeg(NW-corner L~f,NE-corner L~f) by TOPREAL1:6;
     then LSeg(NW-corner L~f,A) c= LSeg(NW-corner L~f,NE-corner L~f)
       by A300,TOPREAL1:12;
  then LSeg(NW-corner L~f,u1) c= LSeg(NW-corner L~f,NE-corner L~f)
      by A302,A315,TOPREAL1:12;
  then LSeg(NW-corner L~f,u1) c= L~SpStSeq L~f by A63,XBOOLE_1:1;
  then LSeg(NW-corner L~f,u1) /\ L~Red1 c= { u1 } by A287,XBOOLE_1:26;
  then A316: LSeg(NW-corner L~f,u1) /\ L~Red1={ u1 } by A314,XBOOLE_0:def 10;
       NW-corner L~f in LSeg(NW-corner L~f,A) by TOPREAL1:6;
  then A317:  LSeg(NW-corner L~f,u1) c= LSeg(NW-corner L~f,A) by A302,
TOPREAL1:12;
  then LSeg(NW-corner L~f,u1) misses LSeg(u2,p1) by A266,XBOOLE_1:63;
  then LSeg(NW-corner L~f,u1) /\ LSeg(u2,p1) = {} by XBOOLE_0:def 7;
  then LSeg(NW-corner L~f,u1) /\ L~RB1
            = { u1 } \/ {} by A295,A316,XBOOLE_1:23
           .= { u1 };
  then reconsider M3 = <*NW-corner L~f*>^RB1 as S-Sequence_in_R2
                              by A234,A293,A313,SPRECT_3:31;
A318: i2 <= len f by A306,FINSEQ_3:27;
  then reconsider M4 = mid(f,2,i2) as S-Sequence_in_R2 by A311,JORDAN4:52;
A319: M3/.len M3 = p1 by A294,SPRECT_3:11;
A320: M4/.1 = f/.2 by A54,A306,SPRECT_2:12;
A321: L~M3 = LSeg(NW-corner L~f,RB1/.1) \/ L~RB1 by SPPOL_2:20;
A322: L~M4 c= L~f by A54,A306,SPRECT_3:34;
A323:  now assume L~f meets LSeg(NW-corner L~f,u1);
      then consider u being set such that
A324:    u in L~f and
A325:    u in LSeg(NW-corner L~f,u1) by XBOOLE_0:3;
      reconsider u as Point of TOP-REAL 2 by A324;
A326:   u in LSeg(NW-corner L~f,A) /\ L~f by A317,A324,A325,XBOOLE_0:def 3;
           LSeg(NW-corner L~f,A) /\ L~f = {A} by PSCOMP_1:102;
      then u = A by A326,TARSKI:def 1;
     hence contradiction by A239,A302,A324,A325,SPRECT_3:16;
    end;
then A327: L~M4 misses LSeg(NW-corner L~f,u1) by A322,XBOOLE_1:63;
         L~M4 misses L~Red by A245,A322,XBOOLE_1:63;
then A328: L~M4 misses L~Red1 by A284,XBOOLE_1:63;
         L~M4 misses LSeg(u2,p1) by A267,A311,A318;
    then L~RB1 misses L~M4 by A295,A328,XBOOLE_1:70;
then A329: L~M3 misses L~M4 by A293,A321,A327,XBOOLE_1:70;
         LSeg(p1,f/.2) misses LSeg(NW-corner L~f,u1) by A195,A317,XBOOLE_1:63
;
    then LSeg(p1,f/.2) /\ LSeg(NW-corner L~f,u1) = {} by XBOOLE_0:def 7;
then A330: LSeg(p1,f/.2) /\ L~M3
         = {} \/ LSeg(p1,f/.2) /\ L~RB1 by A293,A321,XBOOLE_1:23
        .= { p1 } by A296;
         LSeg(p1,f/.2) /\ L~M4 = { f/.2 }
      & (M3/.len M3)`2 = (M4/.1)`2 by A78,A170,A190,A294,A311,A318,A320,
SPRECT_3:11;
  then reconsider M2 = M3^M4 as S-Sequence_in_R2
          by A319,A320,A329,A330,SPRECT_3:38;
A331: len M2 >= 2 & len M1 >= 2 by TOPREAL1:def 10;
A332: M1 is_in_the_area_of f by A55,A304,SPRECT_2:27;
A333: (M1/.1)`2 = (f/.i1)`2 by A55,A304,SPRECT_2:12
         .= (S-min L~f)`2 by A303,FINSEQ_5:41
         .= S-bound L~f by EUCLID:56;
        (M1/.len M1)`2 = (f/.len f)`2 by A55,A304,SPRECT_2:13
         .= (f/.1)`2 by FINSEQ_6:def 1
         .= N-bound L~f by A1,EUCLID:56;
then A334: M1 is_a_v.c._for f by A332,A333,SPRECT_2:def 3;
         <*NW-corner L~f*>^RB1 is_in_the_area_of f &
   mid(f,2,i2) is_in_the_area_of f
     by A54,A60,A297,A306,SPRECT_2:27,28;
then A335: M2 is_in_the_area_of f by SPRECT_2:28;
        M2 = <*NW-corner L~f*>^(RB1^mid(f,2,i2)) by FINSEQ_1:45;
then A336: (M2/.1)`1 = (NW-corner L~f)`1 by FINSEQ_5:16
        .= W-bound L~f by EUCLID:56;
        (M2/.len M2)`1 = (mid(f,2,i2)/.len mid(f,2,i2))`1 by SPRECT_3:11
         .= (f/.i2)`1 by A54,A306,SPRECT_2:13
         .= (E-min L~f)`1 by A305,FINSEQ_5:41
         .= E-bound L~f by EUCLID:56;
   then M2 is_a_h.c._for f by A335,A336,SPRECT_2:def 2;
  then A337: L~M1 meets L~M2 by A331,A334,SPRECT_2:33;
         L~M2 = L~M3 \/ LSeg(M3/.len M3,M4/.1) \/ L~M4 &
   L~M1 misses L~M4 by A307,A308,A311,SPPOL_2:23,SPRECT_2:51;
   then L~M1 meets L~M3 \/ LSeg(M3/.len M3,M4/.1) &
   L~M1 misses LSeg(p1,f/.2) by A173,A308,A312,A337,XBOOLE_1:70;
then A338: L~M1 meets L~M3 by A319,A320,XBOOLE_1:70;
        1 < i1 by A312,XXREAL_0:2;
then A339: L~M1 misses LSeg(u2,p1) by A267,A308;
        L~M1 c= L~f by A55,A304,SPRECT_3:34;
   then L~M1 misses LSeg(NW-corner L~f,u1) &
  L~M3 = LSeg(NW-corner L~f,RB1/.1) \/ L~RB1 by A323,SPPOL_2:20,XBOOLE_1:63;
  then L~M1 meets L~RB1 by A293,A338,XBOOLE_1:70;
  then L~M1 meets L~Red1 by A295,A339,XBOOLE_1:70;
then A340: L~M1 meets L~Red by A284,XBOOLE_1:63;
       L~M1 c= L~f by A55,A304,SPRECT_3:34;
 hence contradiction by A245,A340,XBOOLE_1:63;
 end;
 suppose that
A341: u1 in LSeg(A,NE-corner L~f) and
A342: N-min L~f = NW-corner L~f;
  set i1 = (S-max L~f)..f, i2 = (E-max L~f)..f;
A343: S-max L~f in rng f by SPRECT_2:46;
then A344: i1 in dom f by FINSEQ_4:30;
A345: E-max L~f in rng f by SPRECT_2:50;
then A346: i2 in dom f by FINSEQ_4:30;
A347: (E-max L~f)..f < (E-min L~f)..f by A1,SPRECT_2:75;
         (E-min L~f)..f <= (S-max L~f)..f by A1,SPRECT_2:76;
then A348: i2 < i1 by A347,XXREAL_0:2;
A349: (S-min L~f)..f <= (W-min L~f)..f by A1,SPRECT_2:78;
         (S-max L~f)..f < (S-min L~f)..f by A1,SPRECT_2:77;
    then (S-max L~f)..f < (W-min L~f)..f by A349,XXREAL_0:2;
then A350: (S-max L~f)..f + 1 <= (W-min L~f)..f by NAT_1:13;
         (W-min L~f)..f < len f by A1,SPRECT_2:80;
    then (S-max L~f)..f + 1 < len f by A350,XXREAL_0:2;
then A351: i1 < len f -' 1 by A58,XREAL_1:8;
A352: (N-max L~f)..f > 1 by A1,SPRECT_2:73;
         (N-max L~f)..f <= (E-max L~f)..f by A1,SPRECT_2:74;
then A353: 1 < i2 by A352,XXREAL_0:2;
then A354: 1 < i1 by A348,XXREAL_0:2;
  then reconsider M4 = mid(f,i1,len f -' 1) as S-Sequence_in_R2
                                             by A58,A351,JORDAN4:51;
A355: M4/.len M4 = f/.(len f -' 1) by A57,A344,SPRECT_2:13;
         L~M4 c= L~f by A57,A344,SPRECT_3:34;
    then L~M4 misses L~Red by A245,XBOOLE_1:63;
then A356: L~M4 misses L~Red2 by A277,XBOOLE_1:63;
         L~M4 misses LSeg(u3,p2) by A59,A257,A351,A354;
then A357: L~RB2 misses L~M4 by A290,A356,XBOOLE_1:70;
      LSeg(M4/.len M4,p2) /\ L~M4={ M4/.len M4 } by A125,A351,A354,A355;
  then reconsider M1 = M4^RB2 as S-Sequence_in_R2
              by A76,A81,A93,A289,A291,A355,A357,SPRECT_3:38;
       i1 < len f by A351,BINARITH:62;
  then A358: i2 < len f by A348,XXREAL_0:2;
  then i2+1 <= len f by NAT_1:13;
  then reconsider M2 = mid(f,1,i2) as S-Sequence_in_R2 by A353,JORDAN4:51;
A359: len M2 >= 2 & len M1 >= 2 by TOPREAL1:def 10;
       mid(f,i1,len f -' 1) is_in_the_area_of f by A57,A344,SPRECT_2:27;
then A360: M1 is_in_the_area_of f by A292,SPRECT_2:28;
         mid(f,i1,len f -' 1) is non empty by A57,A344,SPRECT_2:11;
    then 1 in dom mid(f,i1,len f -' 1) by FINSEQ_5:6;
then A361: (M1/.1)`2 = (mid(f,i1,len f -' 1)/.1)`2 by FINSEQ_4:83
         .= (f/.i1)`2 by A57,A344,SPRECT_2:12
         .= (S-max L~f)`2 by A343,FINSEQ_5:41
         .= S-bound L~f by EUCLID:56;
A362: (NE-corner L~f)`2 = N-bound L~f & A`2 = N-bound L~f
     by EUCLID:56;
        (M1/.len M1)`2 = (RB2/.len RB2)`2 by SPRECT_3:11
         .= N-bound L~f by A288,A341,A362,GOBOARD7:6;
then A363: M1 is_a_v.c._for f by A360,A361,SPRECT_2:def 3;
A364: M2 is_in_the_area_of f by A14,A346,SPRECT_2:27;
A365: (M2/.1)`1 = (f/.1)`1 by A14,A346,SPRECT_2:12
        .= W-bound L~f by A1,A342,EUCLID:56;
        (M2/.len M2)`1 = (f/.i2)`1 by A14,A346,SPRECT_2:13
         .= (E-max L~f)`1 by A345,FINSEQ_5:41
         .= E-bound L~f by EUCLID:56;
   then M2 is_a_h.c._for f by A364,A365,SPRECT_2:def 2;
  then A366: L~M1 meets L~M2 by A359,A363,SPRECT_2:33;
A367: L~M1 = L~M4 \/ LSeg(M4/.len M4,p2) \/ L~RB2 by A289,SPPOL_2:23
       .= L~M4 \/ (LSeg(M4/.len M4,p2) \/ L~RB2) by XBOOLE_1:4;
A368: L~M2 misses LSeg(u3,p2) by A257,A353,A358;
       L~M2 misses L~M4 by A59,A348,A351,A353,SPRECT_2:51;
  then A369:  L~M2 meets L~RB2 \/ LSeg(p2,M4/.len M4) by A366,A367,XBOOLE_1:70;
       i2 < len f -' 1 by A348,A351,XXREAL_0:2;
  then L~M2 misses LSeg(p2,f/.(len f -' 1)) by A103,A353;
  then L~M2 meets L~RB2 by A355,A369,XBOOLE_1:70;
  then L~M2 meets L~Red2 by A290,A368,XBOOLE_1:70;
then A370: L~M2 meets L~Red by A277,XBOOLE_1:63;
       L~M2 c= L~f by A14,A346,SPRECT_3:34;
 hence contradiction by A245,A370,XBOOLE_1:63;
 end;
 suppose that
A371: u1 in LSeg(A,NE-corner L~f) and
A372: N-min L~f <> NW-corner L~f;
A373: W-max L~f <> N-min L~f by A372,PSCOMP_1:124;
  set i1 = (S-min L~f)..f, i2 = (E-max L~f)..f;
A374: S-min L~f in rng f by SPRECT_2:45;
then A375: i1 in dom f by FINSEQ_4:30;
A376: E-max L~f in rng f by SPRECT_2:50;
then A377: i2 in dom f by FINSEQ_4:30;
        (E-max L~f)..f < (E-min L~f)..f &
    (E-min L~f)..f <= (S-max L~f)..f by A1,SPRECT_2:75,76;
then A378: (E-max L~f)..f < (S-max L~f)..f by XXREAL_0:2;
         (S-max L~f)..f < (S-min L~f)..f by A1,SPRECT_2:77;
then A379: i2 < i1 by A378,XXREAL_0:2;
         (S-min L~f)..f <= (W-min L~f)..f &
    (W-min L~f)..f < (W-max L~f)..f by A1,A373,SPRECT_2:78,79;
    then (S-min L~f)..f < (W-max L~f)..f by XXREAL_0:2;
    then (S-min L~f)..f + 1 <= (W-max L~f)..f &
    (W-max L~f)..f < len f by A1,NAT_1:13,SPRECT_2:81;
    then (S-min L~f)..f + 1 < len f by XXREAL_0:2;
then A380: i1 < len f -' 1 by A58,XREAL_1:8;
         (N-max L~f)..f > 1 &
    (N-max L~f)..f <= (E-max L~f)..f by A1,SPRECT_2:73,74;
then A381: 1 < i2 by XXREAL_0:2;
then A382: 1 < i1 by A379,XXREAL_0:2;
  then reconsider M4 = mid(f,i1,len f -' 1) as S-Sequence_in_R2
                                             by A58,A380,JORDAN4:51;
A383: M4/.len M4 = f/.(len f -' 1) by A57,A375,SPRECT_2:13;
         L~M4 c= L~f by A57,A375,SPRECT_3:34;
    then L~M4 misses L~Red by A245,XBOOLE_1:63;
then A384: L~M4 misses L~Red2 by A277,XBOOLE_1:63;
         L~M4 misses LSeg(u3,p2) by A59,A257,A380,A382;
then A385: L~RB2 misses L~M4 by A290,A384,XBOOLE_1:70;
      LSeg(M4/.len M4,p2) /\ L~M4={ M4/.len M4 } by A125,A380,A382,A383;
  then reconsider M1 = M4^RB2 as S-Sequence_in_R2
         by A76,A81,A93,A289,A291,A383,A385,SPRECT_3:38;
       i1 < len f by A380,BINARITH:62;
  then A386: i2 < len f by A379,XXREAL_0:2;
  then i2+1 <= len f by NAT_1:13;
  then reconsider M3 = mid(f,1,i2) as S-Sequence_in_R2 by A381,JORDAN4:51;
A387: M3/.1 = f/.1 by A14,A377,SPRECT_2:12;
A388: (N-min L~f)`2 = (NW-corner L~f)`2 by PSCOMP_1:95;
A389: M3/.1 = N-min L~f by A1,A14,A377,SPRECT_2:12;
A390: LSeg(NW-corner L~f, N-min L~f) /\ L~f = {N-min L~f} by PSCOMP_1:102;
       L~M3 c= L~f by A14,A377,SPRECT_3:34;
  then A391: LSeg(NW-corner L~f,N-min L~f) /\ L~M3 c= { N-min L~f } by A390,
XBOOLE_1:26;
       1 in dom M3 by FINSEQ_5:6;
then A392: N-min L~f in L~M3 by A389,SPPOL_2:48;
         N-min L~f in LSeg(NW-corner L~f,N-min L~f) by TOPREAL1:6;
  then N-min L~f in LSeg(NW-corner L~f,N-min L~f) /\ L~M3 by A392,
XBOOLE_0:def 3;
  then {N-min L~f} c= LSeg(NW-corner L~f,N-min L~f) /\ L~M3 by ZFMISC_1:37;
  then LSeg(NW-corner L~f,N-min L~f) /\ L~M3={ N-min L~f } by A391,
XBOOLE_0:def 10;
  then reconsider M2 = <*NW-corner L~f*>^M3 as S-Sequence_in_R2
                              by A372,A388,A389,SPRECT_3:31;
A393: len M2 >= 2 & len M1 >= 2 by TOPREAL1:def 10;
       mid(f,i1,len f -' 1) is_in_the_area_of f by A57,A375,SPRECT_2:27;
then A394: M1 is_in_the_area_of f by A292,SPRECT_2:28;
         mid(f,i1,len f -' 1) is non empty by A57,A375,SPRECT_2:11;
    then 1 in dom mid(f,i1,len f -' 1) by FINSEQ_5:6;
then A395: (M1/.1)`2 = (mid(f,i1,len f -' 1)/.1)`2 by FINSEQ_4:83
         .= (f/.i1)`2 by A57,A375,SPRECT_2:12
         .= (S-min L~f)`2 by A374,FINSEQ_5:41
         .= S-bound L~f by EUCLID:56;
A396: (NE-corner L~f)`2 = N-bound L~f & A`2 = N-bound L~f
       by EUCLID:56;
        (M1/.len M1)`2 = u1`2 by A288,SPRECT_3:11
         .= N-bound L~f by A371,A396,GOBOARD7:6;
then A397: M1 is_a_v.c._for f by A394,A395,SPRECT_2:def 3;
        mid(f,1,i2) is_in_the_area_of f by A14,A377,SPRECT_2:27;
then A398: M2 is_in_the_area_of f by A60,SPRECT_2:28;
A399: (M2/.1)`1 = (NW-corner L~f)`1 by FINSEQ_5:16
        .= W-bound L~f by EUCLID:56;
        (M2/.len M2)`1 = (mid(f,1,i2)/.len mid(f,1,i2))`1 by SPRECT_3:11
         .= (f/.i2)`1 by A14,A377,SPRECT_2:13
         .= (E-max L~f)`1 by A376,FINSEQ_5:41
         .= E-bound L~f by EUCLID:56;
   then M2 is_a_h.c._for f by A398,A399,SPRECT_2:def 2;
  then A400: L~M1 meets L~M2 by A393,A397,SPRECT_2:33;
A401: L~M1 = L~M4 \/ LSeg(M4/.len M4,p2) \/ L~RB2 by A289,SPPOL_2:23
       .= L~M4 \/ (LSeg(M4/.len M4,p2) \/ L~RB2) by XBOOLE_1:4;

A402: now
       L~M4 c= L~f by A57,A375,SPRECT_3:34;
  then A403: LSeg(NW-corner L~f,N-min L~f) /\ L~M4 c= { N-min L~f } by A390,
XBOOLE_1:26;
     assume LSeg(NW-corner L~f,N-min L~f) meets L~M4;
      then LSeg(NW-corner L~f,N-min L~f) /\ L~M4 <> {} by XBOOLE_0:def 7;
      then LSeg(NW-corner L~f,N-min L~f) /\ L~M4 = { N-min L~f }
         by A403,ZFMISC_1:39;
      then N-min L~f in LSeg(NW-corner L~f,N-min L~f) /\ L~M4 by TARSKI:def 1;
      then N-min L~f in L~M4 by XBOOLE_0:def 3;
     hence contradiction by A1,A59,A380,A382,SPRECT_3:47;
    end;
        now
        NW-corner L~f in LSeg(NW-corner L~f,NE-corner L~f) &
   A in LSeg(NW-corner L~f,NE-corner L~f) by A300,TOPREAL1:6;
   then LSeg(NW-corner L~f,A) c= LSeg(NW-corner L~f,NE-corner L~f)
       by TOPREAL1:12;
   then LSeg(NW-corner L~f,N-min L~f) c= L~SpStSeq L~f by A63,XBOOLE_1:1;
then A404: LSeg(NW-corner L~f,N-min L~f) /\ L~Red2 c= { u1 } by A279,
XBOOLE_1:26;
    assume LSeg(NW-corner L~f,N-min L~f) meets L~Red2;
     then LSeg(NW-corner L~f,N-min L~f) /\ L~Red2 <> {} by XBOOLE_0:def 7;
     then LSeg(NW-corner L~f,N-min L~f) /\ L~Red2 = { u1} by A404,ZFMISC_1:39;
     then u1 in LSeg(NW-corner L~f,N-min L~f) /\ L~Red2 by TARSKI:def 1;
     then u1 in LSeg(NW-corner L~f,N-min L~f) by XBOOLE_0:def 3;
     then A405:  u1 in LSeg(NW-corner L~f,A) /\
 LSeg(A,NE-corner L~f) by A371,XBOOLE_0:def 3;
          LSeg(NW-corner L~f,A) /\ LSeg(A,NE-corner L~f) = {A}
     by A300,TOPREAL1:14;
     then u1 = A by A405,TARSKI:def 1;
    hence contradiction by A239,SPRECT_1:13;
   end;
   then LSeg(NW-corner L~f,N-min L~f) misses L~RB2 by A256,A290,XBOOLE_1:70;

   then LSeg(NW-corner L~f,N-min L~f) misses LSeg(M4/.len M4,p2) \/ L~RB2
                                         by A133,A383,XBOOLE_1:70;
   then LSeg(NW-corner L~f,N-min L~f) misses L~M1 &
  L~M2 = LSeg(NW-corner L~f,M3/.1) \/
 L~M3 by A401,A402,SPPOL_2:20,XBOOLE_1:70;
  then A406: L~M3 meets L~M1 by A1,A387,A400,XBOOLE_1:70;
       L~M3 misses L~M4 by A59,A379,A380,A381,SPRECT_2:51;
  then A407:  L~M3 meets L~RB2 \/ LSeg(p2,M4/.len M4) by A401,A406,XBOOLE_1:70;
       i2 < len f -' 1 by A379,A380,XXREAL_0:2;
  then L~M3 misses LSeg(p2,f/.(len f -' 1)) by A103,A381;
  then L~M3 meets L~RB2 &
  L~M3 misses LSeg(u3,p2) by A257,A381,A383,A386,A407,XBOOLE_1:70;
  then L~M3 meets L~Red2 by A290,XBOOLE_1:70;
then A408: L~M3 meets L~Red by A277,XBOOLE_1:63;
       L~M3 c= L~f by A14,A377,SPRECT_3:34;
 hence contradiction by A245,A408,XBOOLE_1:63;
 end;
 suppose that
A409: u1 in LSeg(NE-corner L~f,SE-corner L~f) and
A410: N-min L~f = W-max L~f;
  set i1 = (S-max L~f)..f, i2 = (N-max L~f)..f, i3 = (W-min L~f)..f;
A411: S-max L~f in rng f by SPRECT_2:46;
then A412: i1 in dom f by FINSEQ_4:30;
A413: N-max L~f in rng f by SPRECT_2:44;
then A414: i2 in dom f by FINSEQ_4:30;
      W-min L~f in rng f by SPRECT_2:47;
then A415: i3 in dom f by FINSEQ_4:30;
A416: (S-max L~f)..f < (S-min L~f)..f by A1,SPRECT_2:77;
         (S-min L~f)..f <= (W-min L~f)..f by A1,SPRECT_2:78;
then A417: i3 > i1 by A416,XXREAL_0:2;
A418: 1 < i2 by A1,SPRECT_2:73;
A419: (N-max L~f)..f <= (E-max L~f)..f by A1,SPRECT_2:74;
         (E-max L~f)..f < (E-min L~f)..f by A1,SPRECT_2:75;
then A420: (N-max L~f)..f < (E-min L~f)..f by A419,XXREAL_0:2;
         (E-min L~f)..f <= (S-max L~f)..f by A1,SPRECT_2:76;
then A421: i2 < i1 by A420,XXREAL_0:2;
         i1 <= len f by A412,FINSEQ_3:27;
then A422: mid(f,i2,i1) is S-Sequence_in_R2 by A418,A421,JORDAN4:52;
A423: mid(f,i1,i2) = Rev mid(f,i2,i1) by JORDAN4:30;
  then reconsider M1 = mid(f,i1,i2) as S-Sequence_in_R2 by A422,SPPOL_2:47;
A424: len M1 >= 2 & len RB2 >= 2 by TOPREAL1:def 10;
A425: M1 is_in_the_area_of f by A412,A414,SPRECT_2:27;
A426: (M1/.1)`2 = (f/.i1)`2 by A412,A414,SPRECT_2:12
         .= (S-max L~f)`2 by A411,FINSEQ_5:41
         .= S-bound L~f by EUCLID:56;
        (M1/.len M1)`2 = (f/.i2)`2 by A412,A414,SPRECT_2:13
         .= (N-max L~f)`2 by A413,FINSEQ_5:41
         .= N-bound L~f by EUCLID:56;
then A427: M1 is_a_v.c._for f by A425,A426,SPRECT_2:def 3;
A428: (RB2/.1)`1 = W-bound L~f by A93,A289,A410,EUCLID:56;
     (NE-corner L~f)`1 = E-bound L~f & (SE-corner L~f)`1 = E-bound L~f
                            by EUCLID:56;
   then (RB2/.len RB2)`1 = E-bound L~f by A288,A409,GOBOARD7:5;
   then RB2 is_a_h.c._for f by A292,A428,SPRECT_2:def 2;
  then A429: L~M1 meets L~RB2 by A424,A427,SPRECT_2:33;
       i3 <= len f by A415,FINSEQ_3:27;
  then i1 < len f by A417,XXREAL_0:2;
  then L~mid(f,i2,i1) misses LSeg(u3,p2) by A257,A418,A421;
 then L~M1 misses LSeg(u3,p2) by A423,SPPOL_2:22;
  then L~M1 meets L~Red2 by A290,A429,XBOOLE_1:70;
then A430: L~M1 meets L~Red by A277,XBOOLE_1:63;
       L~M1 c= L~f by A412,A414,SPRECT_3:34;
 hence contradiction by A245,A430,XBOOLE_1:63;
 end;
 suppose that
A431: u1 in LSeg(NE-corner L~f,SE-corner L~f) and
A432: N-min L~f <> W-max L~f;
  set i1 = (S-max L~f)..f, i2 = (N-max L~f)..f, i3 = (W-min L~f)..f;
A433: S-max L~f in rng f by SPRECT_2:46;
then A434: i1 in dom f by FINSEQ_4:30;
A435: N-max L~f in rng f by SPRECT_2:44;
then A436: i2 in dom f by FINSEQ_4:30;
A437: W-min L~f in rng f by SPRECT_2:47;
then A438: i3 in dom f by FINSEQ_4:30;
A439: (S-max L~f)..f < (S-min L~f)..f by A1,SPRECT_2:77;
         (S-min L~f)..f <= (W-min L~f)..f by A1,SPRECT_2:78;
then A440: i3 > i1 by A439,XXREAL_0:2;
A441: 1 < i2 by A1,SPRECT_2:73;
A442: (N-max L~f)..f <= (E-max L~f)..f by A1,SPRECT_2:74;
         (E-max L~f)..f < (E-min L~f)..f by A1,SPRECT_2:75;
then A443: (N-max L~f)..f < (E-min L~f)..f by A442,XXREAL_0:2;
         (E-min L~f)..f <= (S-max L~f)..f by A1,SPRECT_2:76;
then A444: i2 < i1 by A443,XXREAL_0:2;
         (W-max L~f)..f < len f by A1,SPRECT_2:81;
    then A445: (W-max L~f)..f <= len f -' 1 by BINARITH:67;
         (W-min L~f)..f < (W-max L~f)..f by A1,A432,SPRECT_2:79;
then A446: i3 < len f -' 1 by A445,XXREAL_0:2;
         i1 <= len f by A434,FINSEQ_3:27;
then A447: mid(f,i2,i1) is S-Sequence_in_R2 by A441,A444,JORDAN4:52;
A448: mid(f,i1,i2) = Rev mid(f,i2,i1) by JORDAN4:30;
  then reconsider M1 = mid(f,i1,i2) as S-Sequence_in_R2 by A447,SPPOL_2:47;
A449: 1 <= i3 by A438,FINSEQ_3:27;
  then reconsider M3 = mid(f,i3,len f -' 1) as S-Sequence_in_R2
                                          by A58,A446,JORDAN4:51;
A450: M3/.len M3 = f/.(len f -' 1) by A57,A438,SPRECT_2:13;
         L~M3 c= L~f by A57,A438,SPRECT_3:34;
    then L~M3 misses L~Red by A245,XBOOLE_1:63;
then A451: L~M3 misses L~Red2 by A277,XBOOLE_1:63;
         L~M3 misses LSeg(u3,p2) by A59,A257,A446,A449;
then A452: L~RB2 misses L~M3 by A290,A451,XBOOLE_1:70;
      LSeg(M3/.len M3,p2) /\ L~M3={ M3/.len M3 } by A125,A446,A449,A450;
  then reconsider M2 = M3^RB2 as S-Sequence_in_R2
       by A76,A81,A93,A289,A291,A450,A452,SPRECT_3:38;
A453: len M2 >= 2 & len M1 >= 2 by TOPREAL1:def 10;
A454: M1 is_in_the_area_of f by A434,A436,SPRECT_2:27;
A455: (M1/.1)`2 = (f/.i1)`2 by A434,A436,SPRECT_2:12
         .= (S-max L~f)`2 by A433,FINSEQ_5:41
         .= S-bound L~f by EUCLID:56;
        (M1/.len M1)`2 = (f/.i2)`2 by A434,A436,SPRECT_2:13
         .= (N-max L~f)`2 by A435,FINSEQ_5:41
         .= N-bound L~f by EUCLID:56;
then A456: M1 is_a_v.c._for f by A454,A455,SPRECT_2:def 3;
        mid(f,i3,len f -' 1) is_in_the_area_of f by A57,A438,SPRECT_2:27;
then A457: M2 is_in_the_area_of f by A292,SPRECT_2:28;
        mid(f,i3,len f -' 1) is non empty by A57,A438,SPRECT_2:11;
   then 1 in dom mid(f,i3,len f -' 1) by FINSEQ_5:6;
   then A458: (M2/.1)`1 = (mid(f,i3,len f -' 1)/.1)`1 by FINSEQ_4:83
           .= (f/.i3)`1 by A57,A438,SPRECT_2:12
           .= (W-min L~f)`1 by A437,FINSEQ_5:41
           .= W-bound L~f by EUCLID:56;
A459: (NE-corner L~f)`1 = E-bound L~f & (SE-corner L~f)`1 = E-bound L~f
                            by EUCLID:56;
        (M2/.len M2)`1 = (RB2/.len RB2)`1 by SPRECT_3:11
           .= E-bound L~f by A288,A431,A459,GOBOARD7:5;
   then M2 is_a_h.c._for f by A457,A458,SPRECT_2:def 2;
  then A460: L~M1 meets L~M2 by A453,A456,SPRECT_2:33;
A461: L~M2 = L~M3 \/ LSeg(M3/.len M3,p2) \/ L~RB2 by A289,SPPOL_2:23
       .= L~M3 \/ (LSeg(M3/.len M3,p2) \/ L~RB2) by XBOOLE_1:4;
       i3 < len f by A58,A446,NAT_1:13;
  then i1 < len f by A440,XXREAL_0:2;
  then L~mid(f,i2,i1) misses LSeg(u3,p2) by A257,A441,A444;
then A462: L~M1 misses LSeg(u3,p2) by A448,SPPOL_2:22;
       L~M1 misses L~M3 by A59,A440,A441,A444,A446,SPRECT_2:54;
  then A463:  L~M1 meets L~RB2 \/ LSeg(p2,M3/.len M3) by A460,A461,XBOOLE_1:70;
       i1 < len f -' 1 by A440,A446,XXREAL_0:2;
  then L~mid(f,i2,i1) misses LSeg(p2,f/.(len f -' 1)) by A103,A441,A444;
  then L~M1 misses LSeg(p2,f/.(len f -' 1)) by A448,SPPOL_2:22;
  then L~M1 meets L~RB2 by A450,A463,XBOOLE_1:70;
  then L~M1 meets L~Red2 by A290,A462,XBOOLE_1:70;
then A464: L~M1 meets L~Red by A277,XBOOLE_1:63;
       L~M1 c= L~f by A434,A436,SPRECT_3:34;
 hence contradiction by A245,A464,XBOOLE_1:63;
 end;
 suppose
A465: u1 in LSeg(SE-corner L~f,SW-corner L~f);
  set i1 = (E-min L~f)..f, i2 = (W-min L~f)..f;
A466: E-min L~f in rng f by SPRECT_2:49;
then A467: i1 in dom f by FINSEQ_4:30;
A468: W-min L~f in rng f by SPRECT_2:47;
then A469: i2 in dom f by FINSEQ_4:30;
A470: (N-max L~f)..f > 1 by A1,SPRECT_2:73;
         (N-max L~f)..f <= (E-max L~f)..f by A1,SPRECT_2:74;
then A471: (E-max L~f)..f > 1 by A470,XXREAL_0:2;
         (E-max L~f)..f < (E-min L~f)..f by A1,SPRECT_2:75;
then A472: 1 < i1 by A471,XXREAL_0:2;
A473: (E-min L~f)..f <= (S-max L~f)..f by A1,SPRECT_2:76;
         (S-max L~f)..f < (S-min L~f)..f by A1,SPRECT_2:77;
then A474: (E-min L~f)..f < (S-min L~f)..f by A473,XXREAL_0:2;
         (S-min L~f)..f <= (W-min L~f)..f by A1,SPRECT_2:78;
then A475: i1 < i2 by A474,XXREAL_0:2;
A476: i2 <= len f by A469,FINSEQ_3:27;
then A477: mid(f,i1,i2) is S-Sequence_in_R2 by A472,A475,JORDAN4:52;
A478: mid(f,i2,i1) = Rev mid(f,i1,i2) by JORDAN4:30;
  then reconsider M2 = mid(f,i2,i1) as S-Sequence_in_R2 by A477,SPPOL_2:47;
A479: len M2 >= 2 & len RB1 >= 2 by TOPREAL1:def 10;
       (SE-corner L~f)`2 = S-bound L~f & (SW-corner L~f)`2 = S-bound L~f
     by EUCLID:56;
 then (RB1/.1)`2 = S-bound L~f by A293,A465,GOBOARD7:6;
then A480: RB1 is_a_v.c._for f by A170,A294,A297,SPRECT_2:def 3;
A481: M2 is_in_the_area_of f by A467,A469,SPRECT_2:27;
A482: (M2/.len M2)`1 = (f/.i1)`1 by A467,A469,SPRECT_2:13
         .= (E-min L~f)`1 by A466,FINSEQ_5:41
         .= E-bound L~f by EUCLID:56;
        (M2/.1)`1 = (f/.i2)`1 by A467,A469,SPRECT_2:12
         .= (W-min L~f)`1 by A468,FINSEQ_5:41
         .= W-bound L~f by EUCLID:56;
  then M2 is_a_h.c._for f by A481,A482,SPRECT_2:def 2;
  then A483: L~RB1 meets L~M2 by A479,A480,SPRECT_2:33;
      L~mid(f,i1,i2) misses LSeg(u2,p1) by A267,A472,A475,A476;
  then L~M2 misses LSeg(u2,p1) by A478,SPPOL_2:22;
  then L~M2 meets L~Red1 by A295,A483,XBOOLE_1:70;
  then A484: L~M2 meets L~Red by A284,XBOOLE_1:63;
       L~M2 c= L~f by A467,A469,SPRECT_3:34;
 hence contradiction by A245,A484,XBOOLE_1:63;
 end;
 suppose
A485: u1 in LSeg(SW-corner L~f,NW-corner L~f);
  set i1 = (S-min L~f)..f, i3 = (E-min L~f)..f;
A486: S-min L~f in rng f by SPRECT_2:45;
then A487: i1 in dom f by FINSEQ_4:30;
A488: E-min L~f in rng f by SPRECT_2:49;
then A489: i3 in dom f by FINSEQ_4:30;
A490: (E-min L~f)..f <= (S-max L~f)..f by A1,SPRECT_2:76;
         (S-max L~f)..f < (S-min L~f)..f by A1,SPRECT_2:77;
then A491: i3 < i1 by A490,XXREAL_0:2;
A492: (W-min L~f)..f < len f by A1,SPRECT_2:80;
         (S-min L~f)..f <= (W-min L~f)..f by A1,SPRECT_2:78;
then A493: i1 < len f by A492,XXREAL_0:2;
A494: (N-max L~f)..f > 1 by A1,SPRECT_2:73;
         (N-max L~f)..f <= (E-max L~f)..f by A1,SPRECT_2:74;
    then (E-max L~f)..f > 1 by A494,XXREAL_0:2;
then A495: (E-max L~f)..f >= 1+1 by NAT_1:13;
         (E-max L~f)..f < (E-min L~f)..f by A1,SPRECT_2:75;
then A496: 2 < i3 by A495,XXREAL_0:2;
then A497: 2 < i1 by A491,XXREAL_0:2;
  then A498: 1 < i1 by XXREAL_0:2;
  then reconsider M1 = mid(f,i1,len f) as S-Sequence_in_R2 by A493,JORDAN4:52;
A499:  i3 <= len f by A489,FINSEQ_3:27;
  then reconsider M3 = mid(f,2,i3) as S-Sequence_in_R2 by A496,JORDAN4:52;
A500: M3/.1 = f/.2 by A54,A489,SPRECT_2:12;
         L~M3 c= L~f by A54,A489,SPRECT_3:34;
    then L~M3 misses L~Red by A245,XBOOLE_1:63;
then A501: L~M3 misses L~Red1 by A284,XBOOLE_1:63;
         L~M3 misses LSeg(u2,p1) by A267,A496,A499;
then A502: L~RB1 misses L~M3 by A295,A501,XBOOLE_1:70;
        LSeg(p1,M3/.1) /\ L~M3={ M3/.1 } by A190,A496,A499,A500;
   then reconsider M2 = RB1^M3 as S-Sequence_in_R2
        by A78,A170,A294,A296,A500,A502,SPRECT_3:38;
A503: len M2 >= 2 & len M1 >= 2 by TOPREAL1:def 10;
A504: M1 is_in_the_area_of f by A55,A487,SPRECT_2:27;
A505: (M1/.1)`2 = (f/.i1)`2 by A55,A487,SPRECT_2:12
         .= (S-min L~f)`2 by A486,FINSEQ_5:41
         .= S-bound L~f by EUCLID:56;
        (M1/.len M1)`2 = (f/.len f)`2 by A55,A487,SPRECT_2:13
         .= (f/.1)`2 by FINSEQ_6:def 1
         .= N-bound L~f by A1,EUCLID:56;
then A506: M1 is_a_v.c._for f by A504,A505,SPRECT_2:def 3;
        mid(f,2,i3) is_in_the_area_of f by A54,A489,SPRECT_2:27;
then A507: M2 is_in_the_area_of f by A297,SPRECT_2:28;
A508: (NW-corner L~f)`1 = W-bound L~f & (SW-corner L~f)`1 = W-bound L~f
                            by EUCLID:56;
         1 in dom RB1 by FINSEQ_5:6;
then A509: (M2/.1)`1 = (RB1/.1)`1 by FINSEQ_4:83
         .= W-bound L~f by A293,A485,A508,GOBOARD7:5;
        (M2/.len M2)`1 = (mid(f,2,i3)/.len mid(f,2,i3))`1 by SPRECT_3:11
           .= (f/.i3)`1 by A54,A489,SPRECT_2:13
           .= (E-min L~f)`1 by A488,FINSEQ_5:41
           .= E-bound L~f by EUCLID:56;
  then M2 is_a_h.c._for f by A507,A509,SPRECT_2:def 2;
  then A510: L~M1 meets L~M2 by A503,A506,SPRECT_2:33;
A511: L~M2 = L~RB1 \/ LSeg(p1,M3/.1) \/ L~M3 by A294,SPPOL_2:23;
A512: L~M1 misses LSeg(u2,p1) by A267,A493,A498;
       L~M1 misses L~M3 by A491,A493,A496,SPRECT_2:51;
  then A513:  L~M1 meets L~RB1 \/ LSeg(p1,M3/.1) by A510,A511,XBOOLE_1:70;
       L~M1 misses LSeg(p1,f/.2) by A173,A493,A497;
  then L~M1 meets L~RB1 by A500,A513,XBOOLE_1:70;
  then L~M1 meets L~Red1 by A295,A512,XBOOLE_1:70;
then A514: L~M1 meets L~Red by A284,XBOOLE_1:63;
       L~M1 c= L~f by A55,A487,SPRECT_3:34;
 hence contradiction by A245,A514,XBOOLE_1:63;
end;
end;

Lm2:
 for f being non constant standard special_circular_sequence
  st f/.1 = N-min L~f
 holds LeftComp f <> RightComp f
proof let f be non constant standard special_circular_sequence such that
A1: f/.1 = N-min L~f;
 per cases by REVROT_1:38;
 suppose f is clockwise_oriented;
 hence LeftComp f <> RightComp f by A1,Lm1;
 end;
 suppose
A2: Rev f is clockwise_oriented;
A3: N-min L~Rev f = N-min L~f by SPPOL_2:22
    .= Rev f/.len f by A1,FINSEQ_5:68
    .= Rev f/.len Rev f by FINSEQ_5:def 3
    .= Rev f/.1 by FINSEQ_6:def 1;
       LeftComp Rev f = RightComp f &
  RightComp Rev f = LeftComp f by GOBOARD9:26,27;
 hence LeftComp f <> RightComp f by A2,A3,Lm1;
end;
end;

theorem
      for f being non constant standard special_circular_sequence
 holds LeftComp f <> RightComp f
proof let f be non constant standard special_circular_sequence;
  set g = Rotate(f,N-min L~f);
A1: N-min L~f in rng f by SPRECT_2:43;
       L~f = L~g by REVROT_1:33;
  then A2: g/.1 = N-min L~g by A1,FINSEQ_6:98;
       LeftComp g = LeftComp f & RightComp g = RightComp f by REVROT_1:36,
37;
 hence LeftComp f <> RightComp f by A2,Lm2;
end;

