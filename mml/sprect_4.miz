:: On the components of the complement of a special polygonal curve
::  by Andrzej Trybulec and Yatsuka Nakamura
::
:: Received January 21, 1999
:: Copyright (c) 1999 Association of Mizar Users
 
environ

 vocabularies NUMBERS, SUBSET_1, SPPOL_2, RCOMP_1, EUCLID, TOPREAL1, XBOOLE_0,
      PARTFUN1, JORDAN3, JORDAN5C, FINSEQ_1, XXREAL_0, ARYTM_3, GROUP_2,
      RELAT_1, TARSKI, PRE_TOPC, FUNCT_1, ARYTM_1, RLTOPSP1, ORDINAL4,
      SPRECT_2, GOBOARD5, PSCOMP_1, GOBOARD9, SPRECT_1, GOBOARD2, TREES_1,
      MATRIX_1, TOPS_1, MCART_1, TOPREAL4, SPPOL_1, FINSEQ_5, FINSEQ_4,
      FINSEQ_6;
 notations TARSKI, XBOOLE_0, SUBSET_1, NUMBERS, REAL_1, NAT_1, NAT_D, FUNCT_1,
      PARTFUN1, FINSEQ_1, FINSEQ_4, MATRIX_1, STRUCT_0, PRE_TOPC, TOPS_1,
      RLVECT_1, RLTOPSP1, EUCLID, TOPREAL1, TOPREAL4, FINSEQ_5, FINSEQ_6,
      GOBOARD2, SPPOL_1, SPPOL_2, GOBOARD5, GOBOARD9, JORDAN3, PSCOMP_1,
      JORDAN5C, SPRECT_1, SPRECT_2, XXREAL_0;
 constructors XXREAL_0, REAL_1, NAT_1, FINSEQ_4, BINARITH, TOPS_1, TOPS_2,
      COMPTS_1, REALSET2, MATRIX_2, TOPMETR, TOPREAL4, GOBOARD2, SPPOL_1,
      PSCOMP_1, GOBOARD9, JORDAN3, JORDAN5C, SPRECT_1, SPRECT_2, GOBOARD1,
      NAT_D;
 registrations XBOOLE_0, RELSET_1, MEMBERED, REALSET1, FINSEQ_6, STRUCT_0,
      COMPTS_1, TOPREAL1, GOBOARD2, SPPOL_2, GOBOARD9, SPRECT_1, SPRECT_3,
      REVROT_1, FUNCT_1, EUCLID;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions PSCOMP_1, RLTOPSP1;
 theorems TOPREAL1, JORDAN5C, JORDAN3, FINSEQ_3, SPPOL_2, TOPS_1, SPPOL_1,
      TOPREAL3, FINSEQ_5, NAT_1, SPRECT_1, JORDAN5B, SPRECT_2, GOBOARD5,
      FINSEQ_4, GOBOARD7, GOBOARD6, GOBOARD9, ZFMISC_1, TOPREAL4, PSCOMP_1,
      JORDAN4, FINSEQ_6, FINSEQ_1, TARSKI, EUCLID, SUBSET_1, JORDAN5D,
      SPRECT_3, REVROT_1, PARTFUN2, XBOOLE_0, XBOOLE_1, XREAL_1, XXREAL_0,
      PARTFUN1, CARD_1, MATRIX_1, NAT_D, RLTOPSP1;

begin

reserve i,j,l for Element of NAT;

theorem Th1:
  for f being S-Sequence_in_R2, Q being closed Subset of TOP-REAL 2
st L~f meets Q & not f/.1 in Q holds L~R_Cut(f,First_Point(L~f,f/.1,f/.len f,Q)
  ) /\ Q = { First_Point(L~f,f/.1,f/.len f,Q) }
proof
  let f be S-Sequence_in_R2, Q be closed Subset of TOP-REAL 2 such that
A1: L~f meets Q and
A2: not f/.1 in Q;
  set p1 = f/.1, p2 = f/.len f, fp = First_Point(L~f,p1,p2,Q);
A3: L~f /\ Q is closed by TOPS_1:35;
  len f >= 1+1 by TOPREAL1:def 10;
  then
A4: len f > 1 by NAT_1:13;
  L~f is_an_arc_of p1,p2 by TOPREAL1:31;
  then
A5: fp in L~f /\ Q by A1,A3,JORDAN5C:def 1;
  then
A6: fp in L~f by XBOOLE_0:def 4;
  then
A7: 1<=Index(fp,f) by JORDAN3:41;
A8: Index(fp,f)<=len f by A6,JORDAN3:41;
  then
A9: Index(fp,f) in dom f by A7,FINSEQ_3:27;
A10: now
    assume not L~R_Cut(f,fp) /\ Q c= { fp };
    then consider q being set such that
A11: q in L~R_Cut(f,fp) /\ Q and
A12: not q in { fp } by TARSKI:def 3;
    reconsider q as Point of TOP-REAL 2 by A11;
A13: q in L~R_Cut(f,fp) by A11,XBOOLE_0:def 4;
A14: L~R_Cut(f,fp) c= L~f by A6,JORDAN3:76;
A15: q <> fp by A12,TARSKI:def 1;
    q in Q by A11,XBOOLE_0:def 4;
    then
A16: LE fp, q, L~f, p1, p2 by A3,A13,A14,JORDAN5C:15;
    per cases;
    suppose
A17:  fp = f.1;
A18:  len<*fp*> = 1 by FINSEQ_1:56;
      q in L~<*fp*> by A13,A17,JORDAN3:def 5;
      hence contradiction by A18,TOPREAL1:28;
    end;
    suppose
A19:  fp <> f.1;
      set m = mid(f,1,Index(fp,f));
A20:  Index(fp,f) < len f by A6,JORDAN3:41;
      len m = Index(fp,f)-'1+1 by A7,A8,JORDAN4:20;
      then
A21:  m is non empty by CARD_1:47,NAT_1:11;
A22:  fp in LSeg(f,Index(fp,f)) by A6,JORDAN3:42;
      q in L~(m^<*fp*>) by A13,A19,JORDAN3:def 5;
      then
A23:  q in L~m \/ LSeg(m/.len m,fp) by A21,SPPOL_2:19;
      now
        per cases by A23,XBOOLE_0:def 3;
        suppose
A24:      q in L~m;
A25:      now
            assume Index(fp,f) <= 1;
            then Index(fp,f) = 1 by A7,XXREAL_0:1;
            then len m = 1 by A4,JORDAN4:27;
            hence contradiction by A24,TOPREAL1:28;
          end;
          then
A26:      LE q, f/.Index(fp,f), L~f, p1, p2 by A8,A24,SPRECT_3:33;
          f/.Index(fp,f) in LSeg(f/.Index(fp,f),fp) by RLTOPSP1:69;
          then LE f/.Index(fp,f), fp, L~f, p1, p2 by A20,A22,A25,SPRECT_3:40;
          then LE q, fp, L~f, p1, p2 by A26,JORDAN5C:13;
          hence contradiction by A15,A16,JORDAN5C:12,TOPREAL1:31;
        end;
        suppose
A27:      q in LSeg(m/.len m,fp);
          len m in dom m by A21,FINSEQ_5:6;
          then m/.len m = m.len m by PARTFUN1:def 8
            .= f.Index(fp,f) by A7,A8,JORDAN4:22
            .= f/.Index(fp,f) by A9,PARTFUN1:def 8;
          then LE q, fp, L~f, p1, p2 by A7,A20,A22,A27,SPRECT_3:40;
          hence contradiction by A15,A16,JORDAN5C:12,TOPREAL1:31;
        end;
      end;
      hence contradiction;
    end;
  end;
A28: fp in Q by A5,XBOOLE_0:def 4;
  1 in dom f by A4,FINSEQ_3:27;
  then fp <> f.1 by A2,A28,PARTFUN1:def 8;
  then fp in L~R_Cut(f,fp) by A6,JORDAN5B:23;
  then fp in L~R_Cut(f,fp) /\ Q by A28,XBOOLE_0:def 4;
  hence thesis by A10,ZFMISC_1:39;
end;

theorem
  for f being non empty FinSequence of TOP-REAL 2, p being Point of
  TOP-REAL 2 st f is being_S-Seq & p = f/.len f holds L~L_Cut (f,p) = {}
proof
  let f be non empty FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2;
  assume that
A1: f is being_S-Seq and
A2: p = f/.len f;
  len f >= 2 by A1,TOPREAL1:def 10;
  then len f >= 1 by XXREAL_0:2;
  then len f in dom f by FINSEQ_3:27;
  then p = f.len f by A2,PARTFUN1:def 8;
  then L_Cut (f,p) = <*p*> by A1,JORDAN5B:17;
  then len L_Cut (f,p) = 1 by FINSEQ_1:56;
  hence thesis by TOPREAL1:28;
end;

canceled;

theorem Th4:
  for f being S-Sequence_in_R2, p being Point of TOP-REAL 2, j st 1
  <=j & j < len f & p in L~mid(f,j,len f) holds LE f/.j, p, L~f, f/.1, f/.len f
proof
  let f be S-Sequence_in_R2, p be Point of TOP-REAL 2, j such that
A1: 1 <=j and
A2: j < len f and
A3: p in L~mid(f,j,len f);
  consider i such that
A4: 1 <= i and
A5: i+1 <= len mid(f,j,len f) and
A6: p in LSeg(mid(f,j,len f),i) by A3,SPPOL_2:13;
A7: len mid(f,j,len f)=len f-'j+1 by A1,A2,JORDAN4:20;
  then i <= len f -' j by A5,XREAL_1:8;
  then
A8: i + j <= len f by A2,NAT_D:54;
  j+i >= i by NAT_1:11;
  then
A9: j+i-'1+1 <= len f by A4,A8,XREAL_1:237,XXREAL_0:2;
  1+1 <= j+i by A1,A4,XREAL_1:9;
  then
A10: 1 <= j+i-'1 by NAT_D:49;
  j+1 <= j+i by A4,XREAL_1:8;
  then
A11: j <= j+i-'1 by NAT_D:49;
  j+i-'1+1 >= j+i-'1 by NAT_1:11;
  then len f >= j+i-'1 by A9,XXREAL_0:2;
  then
A12: LE f/.j, f/.(j+i-'1), L~f, f/.1, f/.len f by A1,A11,JORDAN5C:24;
  i<len f-'j+1 by A5,A7,NAT_1:13;
  then p in LSeg(f,j+i-'1) by A1,A2,A4,A6,JORDAN4:31;
  then LE f/.(j+i-'1), p, L~f, f/.1, f/.len f by A10,A9,JORDAN5C:25;
  hence thesis by A12,JORDAN5C:13;
end;

theorem Th5:
  for f being S-Sequence_in_R2, p,q being Point of TOP-REAL 2, j st
1 <=j & j < len f & p in LSeg(f,j) & q in LSeg(p,f/.(j+1)) holds LE p, q, L~f,
  f/.1, f/.len f
proof
  let f be S-Sequence_in_R2, p,q be Point of TOP-REAL 2, j such that
A1: 1 <=j and
A2: j < len f and
A3: p in LSeg(f,j) and
A4: q in LSeg(p,f/.(j+1));
A5: j+1 <= len f by A2,NAT_1:13;
  then
A6: LSeg(f,j) = LSeg(f/.j,f/.(j+1)) by A1,TOPREAL1:def 5;
A7: f/.(j+1) in LSeg(f,j) by A1,A5,TOPREAL1:27;
  then
A8: LSeg(p,f/.(j+1)) c= LSeg(f,j) by A3,A6,TOPREAL1:12;
  then
A9: q in LSeg(f,j) by A4;
A10: LSeg(f,j) c= L~f by TOPREAL3:26;
  per cases;
  suppose
    p = q;
    hence thesis by A3,A10,JORDAN5C:9;
  end;
  suppose
A11: q <> p;
    for i, j being Element of NAT st p in LSeg(f,i) & q in LSeg(f,j) & 1
<= i & i+1 <= len f & 1 <= j & j+1 <= len f holds i <= j & (i = j implies LE p,
    q,f/.i,f/.(i+1))
    proof
      1 <= j+1 by NAT_1:11;
      then
A12:  j+1 in dom f by A5,FINSEQ_3:27;
      let i1, i2 be Element of NAT such that
A13:  p in LSeg(f,i1) and
A14:  q in LSeg(f,i2) and
      1 <= i1 and
A15:  i1+1 <= len f and
A16:  1 <= i2 and
      i2+1 <= len f;
A17:  p in LSeg(f,i1) /\ LSeg(f,j) by A3,A13,XBOOLE_0:def 4;
      then
A18:  LSeg(f,i1) meets LSeg(f,j) by XBOOLE_0:4;
      then
A19:  i1 + 1 >= j by TOPREAL1:def 9;
A20:  now
A21:    j+(1+1) = j+1+1;
        assume j + 1 = i1;
        then p in {f/.(j+1)} by A1,A15,A17,A21,TOPREAL1:def 8;
        then p = f/.(j+1) by TARSKI:def 1;
        then q in {p} by A4,RLTOPSP1:71;
        hence contradiction by A11,TARSKI:def 1;
      end;
A22:  now
        assume that
A23:    i2 + 1 > j and
A24:    j+1 > i2;
A25:    j <= i2 by A23,NAT_1:13;
        i2 <= j by A24,NAT_1:13;
        hence i2 = j by A25,XXREAL_0:1;
      end;
A26:  now
        assume that
A27:    i1 + 1 > j and
A28:    j+1 > i1;
A29:    j <= i1 by A27,NAT_1:13;
        i1 <= j by A28,NAT_1:13;
        hence i1 = j by A29,XXREAL_0:1;
      end;
A30:  q in LSeg(f,i2) /\ LSeg(f,j) by A4,A8,A14,XBOOLE_0:def 4;
      then
A31:  LSeg(f,i2) meets LSeg(f,j) by XBOOLE_0:4;
      then
A32:  j + 1 >= i2 by TOPREAL1:def 9;
A33:  j in dom f by A1,A2,FINSEQ_3:27;
A34:  now
        assume f/.(j+1)=f/.j;
        then j = j+1 by A12,A33,PARTFUN2:17;
        hence contradiction;
      end;
A35:  now
A36:    i2+(1+1) = i2+1+1;
        assume i2+1 = j;
        then q in {f/.j} by A5,A16,A30,A36,TOPREAL1:def 8;
        then q = f/.j by TARSKI:def 1;
        hence contradiction by A3,A4,A6,A7,A11,A34,SPPOL_1:24,TOPREAL1:12;
      end;
      i2 + 1 >= j by A31,TOPREAL1:def 9;
      then i2 + 1 = j or i2 = j or j + 1 = i2 by A22,A32,XXREAL_0:1;
      then
A37:  i2 >= j by A35,NAT_1:11;
A38:  j + 1 >= i1 by A18,TOPREAL1:def 9;
      then i1 + 1 = j or i1 = j by A26,A19,A20,XXREAL_0:1;
      then i1 <= j by NAT_1:11;
      hence i1 <= i2 by A37,XXREAL_0:2;
      assume
A39:  i1 = i2;
      not p in LSeg(q,f/.(j+1)) by A4,A11,SPRECT_3:16;
      then not LE q,p,f/.j,f/.(j+1) by JORDAN3:65;
      then LT p,q,f/.j,f/.(j+1) by A3,A6,A14,A26,A19,A38,A20,A34,A35,A39,
JORDAN3:63,XXREAL_0:1;
      hence thesis by A26,A19,A38,A20,A35,A39,JORDAN3:def 7,XXREAL_0:1;
    end;
    hence thesis by A3,A9,A10,A11,JORDAN5C:30;
  end;
end;

theorem Th6:
  for f being S-Sequence_in_R2, Q being closed Subset of TOP-REAL 2
st L~f meets Q & not f/.len f in Q holds L~L_Cut(f,Last_Point(L~f,f/.1,f/.len f
  ,Q)) /\ Q = { Last_Point(L~f,f/.1,f/.len f,Q) }
proof
  let f be S-Sequence_in_R2, Q be closed Subset of TOP-REAL 2 such that
A1: L~f meets Q and
A2: not f/.len f in Q;
  set p1 = f/.1, p2 = f/.len f, lp = Last_Point(L~f,p1,p2,Q);
A3: L~f /\ Q is closed by TOPS_1:35;
  len f >= 1+1 by TOPREAL1:def 10;
  then
A4: len f > 1 by NAT_1:13;
  L~f is_an_arc_of p1,p2 by TOPREAL1:31;
  then
A5: lp in L~f /\ Q by A1,A3,JORDAN5C:def 2;
  then
A6: lp in L~f by XBOOLE_0:def 4;
  then
A7: 1<=Index(lp,f) by JORDAN3:41;
A8: now
    set m = mid(f,Index(lp,f)+1,len f);
    assume not L~L_Cut(f,lp) /\ Q c= { lp };
    then consider q being set such that
A9: q in L~L_Cut(f,lp) /\ Q and
A10: not q in { lp } by TARSKI:def 3;
    reconsider q as Point of TOP-REAL 2 by A9;
A11: q in L~L_Cut(f,lp) by A9,XBOOLE_0:def 4;
A12: L~L_Cut(f,lp) c= L~f by A6,JORDAN3:77;
A13: Index(lp,f) < len f by A6,JORDAN3:41;
    then
A14: Index(lp,f)+1 <= len f by NAT_1:13;
A15: 1 <= Index(lp,f)+1 by NAT_1:11;
    then len m = len f -' (Index(lp,f)+1)+1 by A14,JORDAN4:20;
    then
A16: m is non empty by CARD_1:47,NAT_1:11;
A17: q <> lp by A10,TARSKI:def 1;
    q in Q by A9,XBOOLE_0:def 4;
    then
A18: LE q, lp, L~f, p1, p2 by A3,A11,A12,JORDAN5C:16;
    per cases;
    suppose
      lp=f.(Index(lp,f)+1);
      then
A19:  q in L~m by A11,JORDAN3:def 4;
      now
        assume Index(lp,f)+1 >= len f;
        then Index(lp,f)+1 = len f by A14,XXREAL_0:1;
        then len m = 1 by A4,JORDAN4:27;
        hence contradiction by A19,TOPREAL1:28;
      end;
      then
A20:  LE f/.(Index(lp,f)+1), q, L~f, f/.1, f/.len f by A19,Th4,NAT_1:11;
A21:  f/.(Index(lp,f)+1) in LSeg(lp,f/.(Index(lp,f)+1)) by RLTOPSP1:69;
      lp in LSeg(f,Index(lp,f)) by A6,JORDAN3:42;
      then LE lp, f/.(Index(lp,f)+1), L~f, p1, p2 by A7,A13,A21,Th5;
      then LE lp, q, L~f, p1, p2 by A20,JORDAN5C:13;
      hence contradiction by A17,A18,JORDAN5C:12,TOPREAL1:31;
    end;
    suppose
A22:  lp<>f.(Index(lp,f)+1);
A23:  lp in LSeg(f,Index(lp,f)) by A6,JORDAN3:42;
      1 <= Index(lp,f)+1 by NAT_1:11;
      then
A24:  Index(lp,f)+1 in dom f by A14,FINSEQ_3:27;
      q in L~(<*lp*>^m) by A11,A22,JORDAN3:def 4;
      then
A25:  q in L~m \/ LSeg(m/.1,lp) by A16,SPPOL_2:20;
      now
        per cases by A25,XBOOLE_0:def 3;
        suppose
A26:      q in L~m;
          now
            assume Index(lp,f)+1 >= len f;
            then Index(lp,f)+1 = len f by A14,XXREAL_0:1;
            then len m = 1 by A4,JORDAN4:27;
            hence contradiction by A26,TOPREAL1:28;
          end;
          then
A27:      LE f/.(Index(lp,f)+1), q, L~f, f/.1, f/.len f by A26,Th4,NAT_1:11;
          f/.(Index(lp,f)+1) in LSeg(lp,f/.(Index(lp,f)+1)) by RLTOPSP1:69;
          then LE lp, f/.(Index(lp,f)+1), L~f, p1, p2 by A7,A13,A23,Th5;
          then LE lp, q, L~f, p1, p2 by A27,JORDAN5C:13;
          hence contradiction by A17,A18,JORDAN5C:12,TOPREAL1:31;
        end;
        suppose
A28:      q in LSeg(lp,m/.1);
          1 in dom m by A16,FINSEQ_5:6;
          then m/.1 = m.1 by PARTFUN1:def 8
            .= f.(Index(lp,f)+1) by A4,A14,A15,FINSEQ_6:124
            .= f/.(Index(lp,f)+1) by A24,PARTFUN1:def 8;
          then LE lp, q, L~f, p1, p2 by A7,A13,A23,A28,Th5;
          hence contradiction by A17,A18,JORDAN5C:12,TOPREAL1:31;
        end;
      end;
      hence contradiction;
    end;
  end;
A29: lp in Q by A5,XBOOLE_0:def 4;
  len f in dom f by A4,FINSEQ_3:27;
  then lp <> f.len f by A2,A29,PARTFUN1:def 8;
  then lp in L~L_Cut(f,lp) by A6,JORDAN5B:22;
  then lp in L~L_Cut(f,lp) /\ Q by A29,XBOOLE_0:def 4;
  hence thesis by A8,ZFMISC_1:39;
end;

reserve q for Point of TOP-REAL 2;

Lm1: for f being clockwise_oriented (non constant standard
special_circular_sequence) st f/.1 = N-min L~f holds LeftComp f <> RightComp f
proof
  let f be clockwise_oriented (non constant standard special_circular_sequence
  );
  set A = N-min L~f;
  assume that
A1: f/.1 = A and
A2: LeftComp f = RightComp f;
A3: LeftComp SpStSeq L~f c= LeftComp f by A1,SPRECT_3:58;
  consider i such that
A4: 1 <= i and
A5: i < len GoB f and
A6: A = (GoB f)*(i,width GoB f) by SPRECT_3:45;
  set w = width GoB f;
A7: len f > 4 by GOBOARD7:36;
A8: 1+1 <= len f by GOBOARD7:36,XXREAL_0:2;
A9: w > 1 by GOBOARD7:35;
  then
A10: w-'1+1 = w by XREAL_1:237;
A11: [i,w] in Indices GoB f by A4,A5,A9,MATRIX_1:37;
A12: 1 <= i+1 by NAT_1:11;
A13: i+1 <= len GoB f by A5,NAT_1:13;
  then
A14: [i+1,w] in Indices GoB f by A9,A12,MATRIX_1:37;
A15: 1 in dom f by FINSEQ_5:6;
A16: i in dom GoB f by A4,A5,FINSEQ_3:27;
  then
A17: f/.(1+1) = (GoB f)*(i+1,w) by A1,A6,SPRECT_3:46;
  then
A18: right_cell(f,1) = cell(GoB f,i,w-'1) by A1,A6,A8,A10,A11,A14,GOBOARD5:29;
  set z2 = 1/2*((GoB f)*(i,w-'1)+(GoB f)*(i+1,w)), p1 = 1/2*((GoB f)*(i,w)+(
  GoB f)*(i+1,w)), p2 = 1/2*((GoB f)*(i,w-'1)+(GoB f)*(i,w));
A19: 1 <= w-'1 by GOBOARD7:35,NAT_D:49;
  then
A20: z2 in Int cell(GoB f,i,w-'1) by A4,A10,A13,GOBOARD6:34;
A21: Int right_cell(f,1) c= RightComp f by A8,GOBOARD9:28;
A22: W-bound L~SpStSeq L~f = W-bound L~f by SPRECT_1:66;
A23: S-bound L~SpStSeq L~f = S-bound L~f by SPRECT_1:67;
A24: N-bound L~SpStSeq L~f = N-bound L~f by SPRECT_1:68;
A25: E-bound L~SpStSeq L~f = E-bound L~f by SPRECT_1:69;
A26: 1 < i+1 by A4,NAT_1:13;
A27: 1 <= len GoB f by A4,A5,XXREAL_0:2;
A28: w-'1 < w by A10,XREAL_1:31;
A29: z2 = 1/2*(A+(GoB f)*(i+1,w-'1)) by A4,A6,A10,A13,A19,GOBOARD7:11;
  then
A30: z2`1 = 1/2*(A+(GoB f)*(i+1,w-'1))`1 by TOPREAL3:9
    .= 1/2*(A`1+(GoB f)*(i+1,w-'1)`1) by TOPREAL3:7
    .= 1/2*A`1+1/2*((GoB f)*(i+1,w-'1))`1;
A31: z2`2 = 1/2*(A+(GoB f)*(i+1,w-'1))`2 by A29,TOPREAL3:9
    .= 1/2*(A`2+(GoB f)*(i+1,w-'1)`2) by TOPREAL3:7
    .= 1/2*(N-bound L~f+(GoB f)*(i+1,w-'1)`2) by EUCLID:56
    .= 1/2*N-bound L~f+1/2*((GoB f)*(i+1,w-'1))`2;
A32: 1/2*(W-bound L~f) + 1/2*(W-bound L~f) = (W-bound L~f);
A33: 1/2*(S-bound L~f) + 1/2*(S-bound L~f) = (S-bound L~f);
A34: 1/2*(N-bound L~f) + 1/2*(N-bound L~f) = N-bound L~f;
A35: 1/2*(E-bound L~f) + 1/2*(E-bound L~f) = (E-bound L~f);
  N-min L~f in L~f by SPRECT_1:13;
  then A`1 >= W-bound L~f by PSCOMP_1:71;
  then
A36: 1/2*A`1 >= 1/2*W-bound L~f by XREAL_1:66;
A37: (GoB f)*(1,w-'1)`1 = (GoB f)*(1,1)`1 by A19,A27,A28,GOBOARD5:3;
  (GoB f)*(i+1,w-'1)`1 > (GoB f)*(1,w-'1)`1 by A13,A19,A26,A28,GOBOARD5:4;
  then (GoB f)*(i+1,w-'1)`1 > W-bound L~f by A37,JORDAN5D:39;
  then 1/2*((GoB f)*(i+1,w-'1))`1 > 1/2*W-bound L~f by XREAL_1:70;
  then
A38: W-bound L~SpStSeq L~f < z2`1 by A22,A30,A32,A36,XREAL_1:10;
  N-max L~f in L~f by SPRECT_1:13;
  then (N-max L~f)`1 <= E-bound L~f by PSCOMP_1:71;
  then A`1 < E-bound L~f by SPRECT_2:55,XXREAL_0:2;
  then
A39: 1/2*A`1 < 1/2*E-bound L~f by XREAL_1:70;
A40: (GoB f)*(len GoB f,w-'1)`1 = (GoB f)*(len GoB f,1)`1 by A19,A27,A28,
GOBOARD5:3;
  (GoB f)*(i+1,w-'1)`1 <= (GoB f)*(len GoB f,w-'1)`1 by A12,A13,A19,A28,
SPRECT_3:25;
  then (GoB f)*(i+1,w-'1)`1 <= E-bound L~f by A40,JORDAN5D:41;
  then 1/2*((GoB f)*(i+1,w-'1))`1 <= 1/2*E-bound L~f by XREAL_1:66;
  then
A41: z2`1 < E-bound L~SpStSeq L~f by A25,A30,A35,A39,XREAL_1:10;
A42: 1/2*N-bound L~f > 1/2*S-bound L~f by SPRECT_1:34,XREAL_1:70;
A43: (GoB f)*(i+1,1)`2 = (GoB f)*(1,1)`2 by A9,A12,A13,GOBOARD5:2;
  (GoB f)*(i+1,w-'1)`2 >= (GoB f)*(i+1,1)`2 by A12,A13,A19,A28,SPRECT_3:24;
  then (GoB f)*(i+1,w-'1)`2 >= S-bound L~f by A43,JORDAN5D:40;
  then 1/2*((GoB f)*(i+1,w-'1))`2 >= 1/2*S-bound L~f by XREAL_1:66;
  then
A44: S-bound L~SpStSeq L~f < z2`2 by A23,A31,A33,A42,XREAL_1:10;
A45: (GoB f)*(i+1,w)`2 = (GoB f)*(1,width GoB f)`2 by A9,A12,A13,GOBOARD5:2;
  (GoB f)*(i+1,w-'1)`2 < (GoB f)*(i+1,w)`2 by A12,A13,A19,A28,GOBOARD5:5;
  then (GoB f)*(i+1,w-'1)`2 < N-bound L~f by A45,JORDAN5D:42;
  then 1/2*((GoB f)*(i+1,w-'1))`2 < 1/2*N-bound L~f by XREAL_1:70;
  then
A46: z2`2 < N-bound L~SpStSeq L~f by A24,A31,A34,XREAL_1:8;
  RightComp SpStSeq L~f = {q : W-bound L~SpStSeq L~f < q`1 & q`1 <
  E-bound L~SpStSeq L~f & S-bound L~SpStSeq L~f < q`2 & q`2 < N-bound L~SpStSeq
  L~f} by SPRECT_3:54;
  then
A47: z2 in RightComp SpStSeq L~f by A38,A41,A44,A46;
  consider z1 being set such that
A48: z1 in LeftComp SpStSeq L~f by XBOOLE_0:def 1;
  LeftComp SpStSeq L~f misses RightComp SpStSeq L~f by SPRECT_1:96;
  then
A49: z1 <> z2 by A47,A48,XBOOLE_0:3;
  reconsider z1 as Point of TOP-REAL 2 by A48;
  consider P being Subset of TOP-REAL 2 such that
A50: P is_S-P_arc_joining z1,z2 and
A51: P c= RightComp f by A2,A3,A18,A20,A21,A48,A49,TOPREAL4:30;
  consider Red9 being FinSequence of TOP-REAL 2 such that
A52: Red9 is being_S-Seq and
A53: P = L~Red9 and
A54: z1 = Red9/.1 and
A55: z2 = Red9/.len Red9 by A50,TOPREAL4:def 1;
A56: L~SpStSeq L~f meets L~Red9 by A47,A48,A52,A54,A55,SPRECT_3:50;
A57: 2 in dom f by A8,FINSEQ_3:27;
A58: len f in dom f by FINSEQ_5:6;
A59: len f -' 1 >= 1 by A8,NAT_D:49;
  len f -' 1 <= len f by NAT_D:44;
  then
A60: len f -' 1 in dom f by A59,FINSEQ_3:27;
  1 <= len f by A58,FINSEQ_3:27;
  then
A61: len f -' 1 + 1 = len f by XREAL_1:237;
  then
A62: len f -' 1 < len f by NAT_1:13;
A63: <*NW-corner L~f*> is_in_the_area_of f by SPRECT_2:30;
A64: w-'1 < width GoB f by A10,NAT_1:13;
  then Int cell(GoB f,i,w-'1) misses L~SpStSeq L~f by A4,A5,A19,SPRECT_3:72;
  then
A65: not z2 in L~SpStSeq L~f by A20,XBOOLE_0:3;
A66: LSeg(NW-corner L~f,NE-corner L~f) c= L~SpStSeq L~f by SPRECT_3:26;
A67: f/.1 = f/.len f by FINSEQ_6:def 1;
A68: NW-corner L~f in LSeg(NW-corner L~f,NE-corner L~f) by RLTOPSP1:69;
A69: N-min L~f in LSeg(NW-corner L~f,NE-corner L~f) by SPRECT_3:28;
  then
A70: LSeg(NW-corner L~f,N-min L~f) c= LSeg(NW-corner L~f,NE-corner L~f ) by A68
,TOPREAL1:12;
A71: LSeg(NW-corner L~f,NE-corner L~f) is horizontal;
A72: LSeg(NW-corner L~f,N-min L~f) is horizontal by A70,SPRECT_1:11;
  1 + 2 <= len f by GOBOARD7:36,XXREAL_0:2;
  then
A73: LSeg(f,1) /\ LSeg(f,1+1) = {f/.(1+1)} by TOPREAL1:def 8;
  len f >= 2 + 1 by GOBOARD7:36,XXREAL_0:2;
  then len f -' 1 >= 1 + 1 by NAT_D:49;
  then len f -' 1 -' 1 >= 1 by NAT_D:49;
  then
A74: len f -' 2 >= 1 by NAT_D:45;
A75: len f -' 2 + 1 = len f -' 1 -' 1 + 1 by NAT_D:45
    .= len f -' 1 by A59,XREAL_1:237;
  len f -' 2 + 2 = len f by A7,XREAL_1:237,XXREAL_0:2;
  then
A76: LSeg(f,len f -' 1) /\ LSeg(f,len f -' 2) = {f/.(len f -' 1)} by A74,A75,
TOPREAL1:def 8;
A77: f/.2 in N-most L~f by A1,SPRECT_2:34;
  N-most L~f c= LSeg(NW-corner L~f,NE-corner L~f) by XBOOLE_1:17;
  then
A78: LSeg(f/.1, f/.2) c= LSeg(NW-corner L~f,NE-corner L~f) by A1,A69,A77,
TOPREAL1:12;
A79: ((GoB f)*(i,w-'1))`1 = ((GoB f)*(i,1))`1 by A4,A5,A19,A64,GOBOARD5:3
    .= A`1 by A4,A5,A6,A9,GOBOARD5:3;
A80: (GoB f)*(i+1,w)`2 = (GoB f)*(1,w)`2 by A9,A12,A13,GOBOARD5:2
    .= A`2 by A4,A5,A6,A9,GOBOARD5:2;
  then
A81: (f/.2)`2 = N-bound L~f by A17,EUCLID:56;
A82: <*(GoB f)*(i+1,w)*> is_in_the_area_of f by A9,A12,A13,SPRECT_3:66;
  <*(GoB f)*(i,w-'1)*> is_in_the_area_of f by A4,A5,A19,A64,SPRECT_3:66;
  then <*(GoB f)*(i,w-'1),(GoB f)*(i+1,w)*> is_in_the_area_of f by A82,
SPRECT_3:59;
  then <*z2*> is_in_the_area_of f by SPRECT_3:67;
  then
A83: <*z2*> is_in_the_area_of SpStSeq L~f by SPRECT_3:70;
A84: (GoB f)*(i,w-'1) = f/.(len f -' 1) by A1,A6,A16,SPRECT_3:46;
  then LSeg(A,f/.(len f -' 1)) is vertical by A79,SPPOL_1:37;
  then
A85: LSeg(A,f/.(len f -' 1)) /\ LSeg(NW-corner L~f,A) = { A } by A70,
SPRECT_1:11,SPRECT_3:22;
A86: (NW-corner L~f)`2 = A`2 by PSCOMP_1:95;
A87: (NW-corner L~f)`1 <= A`1 by PSCOMP_1:97;
  A`1 <= (f/.2)`1 by A77,PSCOMP_1:98;
  then A in LSeg(NW-corner L~f,f/.2) by A17,A80,A86,A87,GOBOARD7:9;
  then
A88: LSeg(A,f/.2) /\ LSeg(NW-corner L~f,A) = { A } by TOPREAL1:14;
  ((GoB f)*(i,w))`2 = ((GoB f)*(1,w))`2 by A4,A5,A9,GOBOARD5:2
    .= ((GoB f)*(i+1,w))`2 by A9,A12,A13,GOBOARD5:2;
  then ((GoB f)*(i,w-'1)+(GoB f)*(i,w))`2 = ((GoB f)*(i,w-'1))`2+((GoB f)*(i+
  1,w))`2 by TOPREAL3:7
    .= ((GoB f)*(i,w-'1)+(GoB f)*(i+1,w))`2 by TOPREAL3:7;
  then p2`2 = 1/2*((GoB f)*(i,w-'1)+(GoB f)*(i+1,w)) `2 by TOPREAL3:9
    .= z2`2 by TOPREAL3:9;
  then
A89: LSeg(p2,z2) is horizontal by SPPOL_1:36;
A90: f/.(len f -' 1) in LSeg(f/.(len f -' 1), f/.len f) by RLTOPSP1:69;
A91: (GoB f)*(i,w) = f/.len f by A1,A6,FINSEQ_6:def 1;
  p2 = 1/2*((GoB f)*(i,w-'1))+(1 - 1/2)*((GoB f)*(i,w)) by EUCLID:36;
  then
A92: p2 in LSeg(f/.(len f -' 1), f/.len f) by A84,A91;
  then
A93: LSeg(p2,f/.(len f -' 1)) c= LSeg(f/.(len f -' 1), f/.len f) by A90,
TOPREAL1:12;
  LSeg(f/.(len f -' 1), f/.len f) = LSeg(f,len f -' 1) by A59,A61,
TOPREAL1:def 5;
  then
A94: LSeg(f/.(len f -' 1), f/.len f) c= L~f by TOPREAL3:26;
  then
A95: LSeg(p2,f/.(len f -' 1)) c= L~f by A93,XBOOLE_1:1;
A96: LSeg(p2,f/.(len f -' 1)) c= LSeg(f,len f -' 1) by A59,A61,A93,
TOPREAL1:def 5;
A97: p2`1 = 1/2*((GoB f)*(i,w-'1)+A)`1 by A6,TOPREAL3:9
    .= 1/2*(A`1+A`1) by A79,TOPREAL3:7
    .= (N-min L~f)`1;
  then
A98: LSeg(p2,f/.(len f -' 1)) is vertical by A79,A84,SPPOL_1:37;
A99: p2 in LSeg(p2,f/.(len f -' 1)) by RLTOPSP1:69;
  then
A100: p2 in L~f by A95;
A101: now
    assume p2 = A;
    then f/.(len f -' 1) = f/.len f by A1,A6,A67,A84,SPRECT_3:15;
    hence contradiction by A58,A60,A61,GOBOARD7:31;
  end;
  1 + 1 + 1 <= len f by GOBOARD7:36,XXREAL_0:2;
  then
A102: 1 + 1 <= len f -' 1 by NAT_D:49;
  then
A103: 1 <= len f -' 1 -' 1 by NAT_D:49;
A104: len f -' 1 -'1 + 1 = len f -' 1 by A102,XREAL_1:237,XXREAL_0:2;
A105: len f -' 1 -' 1 + 2 = len f -' 2 + 2 by NAT_D:45
    .= len f by A7,XREAL_1:237,XXREAL_0:2;
A106: for i,j st 1 <= i & i <= j & j < len f -' 1 holds L~mid(f,i,j) misses
  LSeg(p2,f/.(len f -' 1))
  proof
    let l,j such that
A107: 1 <= l and
A108: l <= j and
A109: j < len f -' 1;
    assume L~mid(f,l,j) meets LSeg(p2,f/.(len f -' 1));
    then L~mid(f,l,j) /\ LSeg(p2,f/.(len f -' 1)) <> {} by XBOOLE_0:def 7;
    then consider p being Point of TOP-REAL 2 such that
A110: p in L~mid(f,l,j) /\ LSeg(p2,f/.(len f -' 1)) by SUBSET_1:10;
    p in L~mid(f,l,j) by A110,XBOOLE_0:def 4;
    then consider ii being Element of NAT such that
A111: 1 <= ii and
A112: ii+1 <= len mid(f,l,j) and
A113: p in LSeg(mid(f,l,j),ii) by SPPOL_2:13;
    len f -' 1 > l by A108,A109,XXREAL_0:2;
    then len f -' 1 > 1 by A107,XXREAL_0:2;
    then
A114: len f -' 1 < len f by NAT_D:51;
    then
A115: j < len f by A109,XXREAL_0:2;
    then len mid(f,l,j) = j-'l+1 by A107,A108,JORDAN4:20;
    then
A116: ii<j-'l+1 by A112,NAT_1:13;
    set k = ii+l-'1;
A117: p in LSeg(p2,f/.(len f -' 1)) by A110,XBOOLE_0:def 4;
    per cases by A108,XXREAL_0:1;
    suppose
      l = j;
      then len mid(f,l,j) = 1 by A107,A115,JORDAN4:27;
      then L~mid(f,l,j) = {} by TOPREAL1:28;
      hence thesis by A113,SPPOL_2:17;
    end;
    suppose
A118: l < j;
A119: ii + 1 >= 1 + 1 by A111,XREAL_1:8;
      ii + l >= ii + 1 by A107,XREAL_1:8;
      then
A120: ii + l >= 1 + 1 by A119,XXREAL_0:2;
      then
A121: k >= 1 by NAT_D:49;
A122: ii + l >= 1 by A120,XXREAL_0:2;
A123: now
        assume k + 1 >= len f -' 1;
        then k >= len f -'1 -' 1 by NAT_D:53;
        then
A124:   ii + l >= len f -' 1 by A122,NAT_D:56;
        ii + l < j -' l + 1 + l by A116,XREAL_1:8;
        then ii + l < j -' l + l + 1;
        then ii + l < j + 1 by A118,XREAL_1:237;
        then len f -' 1 < j + 1 by A124,XXREAL_0:2;
        hence contradiction by A109,NAT_1:13;
      end;
A125: LSeg(f,1) /\ LSeg(f,len f -' 1) = { f.1 } by JORDAN4:54
        .= {f/.1} by A15,PARTFUN1:def 8;
      LSeg(mid(f,l,j),ii)=LSeg(f,k) by A107,A115,A111,A116,A118,JORDAN4:31;
      then
A126: p in LSeg(f,k) /\ LSeg(f,len f -' 1) by A96,A117,A113,XBOOLE_0:def 4;
      then LSeg(f,k) meets LSeg(f,len f -' 1) by XBOOLE_0:4;
      then k <= 1 by A114,A123,GOBOARD5:def 4;
      then k = 1 by A121,XXREAL_0:1;
      then p = f/.1 by A126,A125,TARSKI:def 1;
      hence contradiction by A1,A67,A92,A101,A117,SPRECT_3:16;
    end;
  end;
A127: for j st 1 <= j & j < len f -' 1 holds L~mid(f,j,len f -' 1) /\ LSeg(
  p2,f/.(len f -' 1)) = {f/.(len f -' 1)}
  proof
    let j such that
A128: 1 <= j and
A129: j < len f -' 1;
A130: 1 <= len f -' 1 by A128,A129,XXREAL_0:2;
A131: len f -' 1 -' 1 = len f -' 2 by NAT_D:45;
    then
A132: L~mid(f,j,len f -' 1) = LSeg(f,len f -' 2) \/ L~mid(f,j,len f -' 2)
    by A128,A129,NAT_D:35,SPRECT_3:36;
    j <= len f -' 2 by A129,A131,NAT_D:49;
    then LSeg(p2,f/.(len f -' 1)) misses L~mid(f,j,len f -' 2) by A106,A128
,A130,A131,JORDAN5B:1;
    hence
    L~mid(f,j,len f -' 1) /\ LSeg(p2,f/.(len f -' 1)) = LSeg(f,len f -' 2
    ) /\ LSeg(p2,f/.(len f -' 1)) by A132,XBOOLE_1:78
      .= {f/.(len f -' 1)} by A76,A96,RLTOPSP1:69,ZFMISC_1:148;
  end;
A133: LSeg(NW-corner L~f,N-min L~f) misses LSeg(f/.(len f -' 1),p2)
  proof
    assume LSeg(NW-corner L~f,A) meets LSeg(f/.(len f -' 1),p2);
    then LSeg(p2,f/.(len f -' 1)) /\ LSeg(NW-corner L~f,A) <> {} by
XBOOLE_0:def 7;
    then consider p being Point of TOP-REAL 2 such that
A134: p in LSeg(p2,f/.(len f -' 1)) /\ LSeg(NW-corner L~f,A) by SUBSET_1:10;
A135: p in LSeg(p2,f/.(len f -' 1)) by A134,XBOOLE_0:def 4;
    p in LSeg(NW-corner L~f,A) by A134,XBOOLE_0:def 4;
    then p in { A } by A1,A67,A85,A93,A135,XBOOLE_0:def 4;
    then p = A by TARSKI:def 1;
    hence contradiction by A1,A67,A92,A101,A135,SPRECT_3:16;
  end;
A136: p2`2 <> A`2 by A97,A101,TOPREAL3:11;
  set G = GoB f;
A137: Int cell(GoB f,i,w-'1) misses L~f by A5,A64,GOBOARD7:14;
  L~f /\ (Int cell(G,i,w-'1) \/ { p2 }) = L~f /\ Int cell(G,i,w-'1) \/
  L~f /\ { p2 } by XBOOLE_1:23
    .= {} \/ L~f /\ { p2 } by A137,XBOOLE_0:def 7
    .= L~f /\ { p2 };
  then
A138: LSeg(z2,p2) /\ L~f c= L~f /\ { p2 } by A4,A5,A10,A19,A64,GOBOARD6:43
,XBOOLE_1:26;
  L~f /\ { p2 } c= { p2 } by XBOOLE_1:17;
  then
A139: LSeg(z2,p2) /\ L~f c= { p2 } by A138,XBOOLE_1:1;
A140: for i,j st 1 <= i & i < j & j < len f holds L~mid(f,i,j) misses LSeg(
  z2,p2)
  proof
    len f >= 1 + 1 by GOBOARD7:36,XXREAL_0:2;
    then
A141: len f -' 1 >= 1 by NAT_D:49;
    let l,j such that
A142: 1 <= l and
A143: l < j and
A144: j < len f;
    assume L~mid(f,l,j) meets LSeg(z2,p2);
    then L~mid(f,l,j) /\ LSeg(z2,p2) <> {} by XBOOLE_0:def 7;
    then consider p being Point of TOP-REAL 2 such that
A145: p in L~mid(f,l,j) /\ LSeg(z2,p2) by SUBSET_1:10;
    p in L~mid(f,l,j) by A145,XBOOLE_0:def 4;
    then consider ii being Element of NAT such that
A146: 1 <= ii and
A147: ii+1 <= len mid(f,l,j) and
A148: p in LSeg(mid(f,l,j),ii) by SPPOL_2:13;
A149: len mid(f,l,j) = j-'l+1 by A142,A143,A144,JORDAN4:20;
    then ii<j-'l+1 by A147,NAT_1:13;
    then
A150: p in LSeg(f,ii+l-'1) by A142,A143,A144,A146,A148,JORDAN4:31;
    set k = ii+l-'1;
A151: ii + 1 >= 1 + 1 by A146,XREAL_1:8;
    LSeg(f,k) /\ LSeg(z2,p2) c= L~f /\ LSeg(z2,p2) by TOPREAL3:26,XBOOLE_1:26;
    then
A152: LSeg(f,k) /\ LSeg(z2,p2) c= { p2 } by A139,XBOOLE_1:1;
    p in LSeg(z2,p2) by A145,XBOOLE_0:def 4;
    then p in LSeg(f,ii+l-'1) /\ LSeg(z2,p2) by A150,XBOOLE_0:def 4;
    then p = p2 by A152,TARSKI:def 1;
    then p2 in LSeg(f,k) /\ LSeg(f/.(len f -' 1), f/.len f) by A92,A150,
XBOOLE_0:def 4;
    then
A153: p2 in LSeg(f,k) /\ LSeg(f,len f -' 1) by A61,A141,TOPREAL1:def 5;
    then
A154: LSeg(f,k) meets LSeg(f,len f -' 1) by XBOOLE_0:4;
    ii + l >= ii + 1 by A142,XREAL_1:8;
    then ii + l >= 1 + 1 by A151,XXREAL_0:2;
    then
A155: k >= 1 by NAT_D:49;
    per cases by A62,A154,GOBOARD5:def 4;
    suppose
A156: k <= 1;
A157: LSeg(f,1) /\ LSeg(f,len f -' 1) = {f.1} by JORDAN4:54
        .= {f/.1} by A15,PARTFUN1:def 8;
      k = 1 by A155,A156,XXREAL_0:1;
      hence contradiction by A1,A101,A153,A157,TARSKI:def 1;
    end;
    suppose
A158: k+1 >= len f -' 1;
      ii <= j -' l by A147,A149,XREAL_1:8;
      then ii + l <= j by A143,NAT_D:54;
      then
A159: ii+l < len f by A144,XXREAL_0:2;
      ii+l >= l by NAT_1:11;
      then ii+l >= 1 by A142,XXREAL_0:2;
      then ii+l-'1 < len f -' 1 by A159,NAT_D:56;
      then
A160: ii+l-'1 <= len f -' 1 -' 1 by NAT_D:49;
      k >= len f -' 1 -' 1 by A158,NAT_D:53;
      then k = len f -' 1 -' 1 by A160,XXREAL_0:1;
      then p2 in {f/.(len f -' 1)} by A103,A104,A105,A153,TOPREAL1:def 8;
      then p2 = f/.(len f -' 1) by TARSKI:def 1;
      hence contradiction by A6,A84,A101,SPRECT_3:15;
    end;
  end;
A161: <*p2*> is_in_the_area_of f by A95,A99,SPRECT_2:25,SPRECT_3:63;
  then
A162: <*p2*> is_in_the_area_of SpStSeq L~f by SPRECT_3:70;
  <*p2,z2*> = <*p2*>^<*z2*> by FINSEQ_1:def 9;
  then
A163: L~SpStSeq L~f /\ LSeg(p2,z2) c= { p2 } by A65,A83,A162,SPRECT_2:28
,SPRECT_3:64;
  ((GoB f)*(i,w-'1))`1 = ((GoB f)*(i,1))`1 by A4,A5,A19,GOBOARD5:3,NAT_D:35
    .= ((GoB f)*(i,w))`1 by A4,A5,A9,GOBOARD5:3;
  then ((GoB f)*(i,w)+(GoB f)*(i+1,w))`1 = ((GoB f)*(i,w-'1))`1+((GoB f)*(i+1
  ,w))`1 by TOPREAL3:7
    .= ((GoB f)*(i,w-'1)+(GoB f)*(i+1,w))`1 by TOPREAL3:7;
  then p1`1 = 1/2*((GoB f)*(i,w-'1)+(GoB f)*(i+1,w))`1 by TOPREAL3:9
    .= z2`1 by TOPREAL3:9;
  then
A164: LSeg(p1,z2) is vertical by SPPOL_1:37;
A165: f/.2 in LSeg(f/.1, f/.(1+1)) by RLTOPSP1:69;
  p1 = 1/2*((GoB f)*(i,w))+(1-1/2)*((GoB f)*(i+1,w)) by EUCLID:36;
  then
A166: p1 in LSeg(f/.1, f/.2) by A1,A6,A17;
  then
A167: LSeg(p1,f/.2) c= LSeg(f/.1, f/.2) by A165,TOPREAL1:12;
A168: LSeg(f/.1, f/.(1+1)) = LSeg(f,1) by A8,TOPREAL1:def 5;
  then
A169: LSeg(f/.1, f/.2) c= L~f by TOPREAL3:26;
  then
A170: LSeg(p1,f/.2) c= L~f by A167,XBOOLE_1:1;
A171: p1`2 = 1/2*(A+(GoB f)*(i+1,w))`2 by A6,TOPREAL3:9
    .= 1/2*(A`2+A`2) by A80,TOPREAL3:7
    .= N-bound L~f by EUCLID:56;
A172: p1 in LSeg(p1,f/.2) by RLTOPSP1:69;
A173: p1 <> A by A1,A6,A15,A17,A57,GOBOARD7:31,SPRECT_3:15;
A174: for i,j st 2 < i & i <= j & j<= len f holds L~mid(f,i,j) misses LSeg(
  p1,f/.2)
  proof
    let l,j such that
A175: 2 < l and
A176: l <= j and
A177: j<= len f;
    assume L~mid(f,l,j) meets LSeg(p1,f/.2);
    then L~mid(f,l,j) /\ LSeg(p1,f/.2) <> {} by XBOOLE_0:def 7;
    then consider p being Point of TOP-REAL 2 such that
A178: p in L~mid(f,l,j) /\ LSeg(p1,f/.2) by SUBSET_1:10;
A179: p in LSeg(p1,f/.2) by A178,XBOOLE_0:def 4;
    p in L~mid(f,l,j) by A178,XBOOLE_0:def 4;
    then consider ii being Element of NAT such that
A180: 1 <= ii and
A181: ii+1 <= len mid(f,l,j) and
A182: p in LSeg(mid(f,l,j),ii) by SPPOL_2:13;
A183: 1 < l by A175,XXREAL_0:2;
    then
A184: len mid(f,l,j) = j-'l+1 by A176,A177,JORDAN4:20;
    then
A185: ii<j-'l+1 by A181,NAT_1:13;
    set k = ii+l-'1;
A186: ii + 2 >= 1 + 2 by A180,XREAL_1:8;
    ii + l > ii + 2 by A175,XREAL_1:8;
    then ii + l > 1 + 2 by A186,XXREAL_0:2;
    then
A187: k > 1+1 by NAT_D:52;
    per cases by A176,XXREAL_0:1;
    suppose
      l = j;
      then len mid(f,l,j) = 1 by A177,A183,JORDAN4:27;
      then L~mid(f,l,j) = {} by TOPREAL1:28;
      hence thesis by A182,SPPOL_2:17;
    end;
    suppose
A188: l < j;
A189: LSeg(f,1) /\ LSeg(f,len f -' 1) = { f.1 } by JORDAN4:54
        .= {f/.1} by A15,PARTFUN1:def 8;
      ii <= j -' l by A181,A184,XREAL_1:8;
      then ii + l <= j by A176,NAT_D:54;
      then ii+l <= len f by A177,XXREAL_0:2;
      then
A190: ii+l-'1 <= len f -' 1 by NAT_D:42;
      LSeg(mid(f,l,j),ii)=LSeg(f,k) by A177,A183,A180,A185,A188,JORDAN4:31;
      then
A191: p in LSeg(f,k) /\ LSeg(f,1) by A167,A168,A179,A182,XBOOLE_0:def 4;
      then LSeg(f,k) meets LSeg(f,1) by XBOOLE_0:4;
      then k+1 >= len f by A187,GOBOARD5:def 4;
      then k >= len f -' 1 by NAT_D:53;
      then k = len f -' 1 by A190,XXREAL_0:1;
      then p = f/.1 by A191,A189,TARSKI:def 1;
      hence contradiction by A1,A166,A173,A179,SPRECT_3:16;
    end;
  end;
A192: for j st 2 < j & j <= len f holds L~mid(f,2,j) /\ LSeg(p1,f/.2) = {f/. 2}
  proof
A193: f/.2 in LSeg(p1,f/.2) by RLTOPSP1:69;
    let j such that
A194: 2 < j and
A195: j <= len f;
    2+1 <= j by A194,NAT_1:13;
    then
A196: LSeg(p1,f/.2) misses L~mid(f,2+1,j) by A174,A195;
    L~mid(f,2,j) = LSeg(f,2) \/ L~mid(f,2+1,j) by A194,A195,SPRECT_3:35;
    hence L~mid(f,2,j) /\ LSeg(p1,f/.2) = LSeg(f,2) /\ LSeg(p1,f/.2) by A196,
XBOOLE_1:78
      .= {f/.2} by A73,A165,A166,A168,A193,TOPREAL1:12,ZFMISC_1:148;
  end;
A197: LSeg(p1,f/.2) misses LSeg(NW-corner L~f,A)
  proof
    assume LSeg(p1,f/.2) meets LSeg(NW-corner L~f,A);
    then LSeg(p1,f/.2) /\ LSeg(NW-corner L~f,A) <> {} by XBOOLE_0:def 7;
    then consider p being Point of TOP-REAL 2 such that
A198: p in LSeg(p1,f/.2) /\ LSeg(NW-corner L~f,A) by SUBSET_1:10;
A199: p in LSeg(p1,f/.2) by A198,XBOOLE_0:def 4;
    p in LSeg(NW-corner L~f,A) by A198,XBOOLE_0:def 4;
    then p in { A } by A1,A88,A167,A199,XBOOLE_0:def 4;
    then p = A by TARSKI:def 1;
    hence contradiction by A1,A166,A173,A199,SPRECT_3:16;
  end;
A200: LSeg(NW-corner L~f,N-min L~f) misses LSeg(z2,p1)
  proof
    assume LSeg(NW-corner L~f,N-min L~f) meets LSeg(z2,p1);
    then LSeg(NW-corner L~f,N-min L~f) /\ LSeg(z2,p1) <> {} by XBOOLE_0:def 7;
    then consider p being Point of TOP-REAL 2 such that
A201: p in LSeg(NW-corner L~f,N-min L~f) /\ LSeg(z2,p1) by SUBSET_1:10;
A202: p in LSeg(z2,p1) by A201,XBOOLE_0:def 4;
A203: p in LSeg(NW-corner L~f,N-min L~f) by A201,XBOOLE_0:def 4;
    LSeg(NW-corner L~f,NE-corner L~f) /\ LSeg(z2,p1) = {p1} by A78,A164,A166,
SPRECT_3:23;
    then p in {p1} by A70,A203,A202,XBOOLE_0:def 4;
    then p1 in LSeg(NW-corner L~f,N-min L~f) by A203,TARSKI:def 1;
    then p1 in LSeg(NW-corner L~f,N-min L~f) /\ L~f
    by A170,A172,XBOOLE_0:def 4;
    then p1 in { N-min L~f } by PSCOMP_1:102;
    hence contradiction by A173,TARSKI:def 1;
  end;
A204: for i,j st 1 < i & i < j & j<= len f holds L~mid(f,i,j) misses LSeg(z2
  ,p1)
  proof
A205: len f >= 2 by GOBOARD7:36,XXREAL_0:2;
A206: Int cell(GoB f,i,w-'1) misses L~f by A5,A64,GOBOARD7:14;
A207: L~f /\ { p1 } c= { p1 } by XBOOLE_1:17;
    let l,j such that
A208: 1 < l and
A209: l < j and
A210: j<= len f;
    assume L~mid(f,l,j) meets LSeg(z2,p1);
    then L~mid(f,l,j) /\ LSeg(z2,p1) <> {} by XBOOLE_0:def 7;
    then consider p being Point of TOP-REAL 2 such that
A211: p in L~mid(f,l,j) /\ LSeg(z2,p1) by SUBSET_1:10;
    p in L~mid(f,l,j) by A211,XBOOLE_0:def 4;
    then consider ii being Element of NAT such that
A212: 1 <= ii and
A213: ii+1 <= len mid(f,l,j) and
A214: p in LSeg(mid(f,l,j),ii) by SPPOL_2:13;
A215: len mid(f,l,j) = j-'l+1 by A208,A209,A210,JORDAN4:20;
    then ii<j-'l+1 by A213,NAT_1:13;
    then
A216: p in LSeg(f,ii+l-'1) by A208,A209,A210,A212,A214,JORDAN4:31;
    set k = ii+l-'1, G = GoB f;
A217: LSeg(f,k) /\ LSeg(z2,p1) c= L~f /\ LSeg(z2,p1) by TOPREAL3:26,XBOOLE_1:26
;
    L~f /\ (Int cell(G,i,w-'1) \/ { p1 }) = L~f /\ Int cell(G,i,w-'1) \/
    L~f /\ { p1 } by XBOOLE_1:23
      .= {} \/ L~f /\ { p1 } by A206,XBOOLE_0:def 7
      .= L~f /\ { p1 };
    then LSeg(z2,p1) /\ L~f c= L~f /\ { p1 } by A4,A5,A10,A19,A64,GOBOARD6:44
,XBOOLE_1:26;
    then LSeg(z2,p1) /\ L~f c= { p1 } by A207,XBOOLE_1:1;
    then
A218: LSeg(f,k) /\ LSeg(z2,p1) c= { p1 } by A217,XBOOLE_1:1;
    p in LSeg(z2,p1) by A211,XBOOLE_0:def 4;
    then p in LSeg(f,ii+l-'1) /\ LSeg(z2,p1) by A216,XBOOLE_0:def 4;
    then p = p1 by A218,TARSKI:def 1;
    then p1 in LSeg(f,k) /\ LSeg(f/.1,f/.(1+1)) by A166,A216,XBOOLE_0:def 4;
    then
A219: p1 in LSeg(f,k) /\ LSeg(f,1) by A205,TOPREAL1:def 5;
    then LSeg(f,k) meets LSeg(f,1) by XBOOLE_0:4;
    then
A220: k <= 1+1 or k+1 >= len f by GOBOARD5:def 4;
A221: ii + 1 >= 1 + 1 by A212,XREAL_1:8;
    ii + l > ii + 1 by A208,XREAL_1:8;
    then ii + l > 1 + 1 by A221,XXREAL_0:2;
    then
A222: k > 1 by NAT_D:52;
    per cases by A220;
    suppose
A223: k <= 2;
      k >= 1+1 by A222,NAT_1:13;
      then
A224: k = 2 by A223,XXREAL_0:1;
      1 + 2 <= len f by GOBOARD7:36,XXREAL_0:2;
      then p1 in {f/.(1+1)} by A219,A224,TOPREAL1:def 8;
      then p1 = f/.(1+1) by TARSKI:def 1;
      hence contradiction by A6,A17,A173,SPRECT_3:15;
    end;
    suppose
A225: k+1 >= len f;
A226: LSeg(f,1) /\ LSeg(f,len f -' 1) = { f.1 } by JORDAN4:54
        .= {f/.1} by A15,PARTFUN1:def 8;
      ii <= j -' l by A213,A215,XREAL_1:8;
      then ii + l <= j by A209,NAT_D:54;
      then ii+l <= len f by A210,XXREAL_0:2;
      then
A227: ii+l-'1 <= len f -' 1 by NAT_D:42;
      k >= len f -' 1 by A225,NAT_D:53;
      then k = len f -' 1 by A227,XXREAL_0:1;
      hence contradiction by A1,A173,A219,A226,TARSKI:def 1;
    end;
  end;
  LSeg(f/.1,f/.2) c= L~SpStSeq L~f by A1,SPRECT_3:48;
  then
A228: L~SpStSeq L~f /\ LSeg(p1,z2) = { p1 } by A65,A83,A166,SPRECT_3:65;
A229: LSeg(p1,f/.2) is horizontal by A81,A171,SPPOL_1:36;
A230: p1 in LSeg(p1,f/.2) by RLTOPSP1:69;
  then
A231: p1 in L~f by A170;
A232: <*p1*> is_in_the_area_of f by A170,A230,SPRECT_2:25,SPRECT_3:63;
A233: L~f misses L~Red9 by A51,A53,SPRECT_3:42,XBOOLE_1:63;
  reconsider Red9 as S-Sequence_in_R2 by A52;
  len Red9 in dom Red9 by FINSEQ_5:6;
  then
A234: z2 in L~Red9 by A55,SPPOL_2:48;
  set u1 = Last_Point(L~Red9,Red9/.1,Red9/.len Red9,L~SpStSeq L~f), Red =
  L_Cut(Red9,u1), u2 = First_Point(L~Red,Red/.1,Red/.len Red,LSeg(z2,p1)), u3 =
  First_Point(L~Red,Red/.1,Red/.len Red,LSeg(z2,p2));
  NW-corner L~SpStSeq L~f = NW-corner L~f by SPRECT_1:70;
  then
A235: u1 <> NW-corner L~f by A47,A48,A54,A55,SPRECT_3:55;
A236: L~Red9 is_an_arc_of z1,z2 by A54,A55,TOPREAL1:31;
  L~Red9 /\ L~SpStSeq L~f is closed by TOPS_1:35;
  then
A237: u1 in L~Red9 /\ L~SpStSeq L~f by A54,A55,A56,A236,JORDAN5C:def 2;
  then
A238: u1 in L~SpStSeq L~f by XBOOLE_0:def 4;
A239: u1 in L~Red9 by A237,XBOOLE_0:def 4;
A240: now
    assume u1 in L~f;
    then u1 in L~f /\ L~Red9 by A239,XBOOLE_0:def 4;
    hence contradiction by A233,XBOOLE_0:def 7;
  end;
  len Red9 in dom Red9 by FINSEQ_5:6;
  then u1 <> Red9.len Red9 by A55,A65,A238,PARTFUN1:def 8;
  then reconsider Red as S-Sequence_in_R2 by A239,JORDAN3:69;
  1 in dom Red by FINSEQ_5:6;
  then
A241: Red/.1 =Red.1 by PARTFUN1:def 8
    .= u1 by A239,JORDAN3:58;
A242: L~SpStSeq L~f /\ L~Red = { u1 } by A55,A56,A65,Th6;
  len Red9 in dom Red9 by FINSEQ_5:6;
  then z2 = Red9.len Red9 by A55,PARTFUN1:def 8;
  then
A243: z2 in L~Red by A65,A234,A238,A239,JORDAN5B:25;
  Red is_in_the_area_of SpStSeq L~f by A47,A48,A54,A55,SPRECT_3:71;
  then
A244: Red is_in_the_area_of f by SPRECT_3:70;
A245: L~Red c= L~Red9 by A239,JORDAN3:77;
A246: L~f misses L~Red by A233,A239,JORDAN3:77,XBOOLE_1:63;
  z2 in LSeg(p1,z2) by RLTOPSP1:69;
  then
A247: LSeg(p1,z2) meets L~Red by A243,XBOOLE_0:3;
  z2 in LSeg(p2,z2) by RLTOPSP1:69;
  then
A248: LSeg(p2,z2) meets L~Red by A243,XBOOLE_0:3;
A249: L~Red /\ LSeg(p1,z2) is closed by TOPS_1:35;
A250: L~Red /\ LSeg(p2,z2) is closed by TOPS_1:35;
  L~Red is_an_arc_of Red/.1,Red/.len Red by TOPREAL1:31;
  then
A251: u3 in LSeg(p2,z2) /\ L~Red by A248,A250,JORDAN5C:def 1;
  then
A252: u3 in L~Red by XBOOLE_0:def 4;
A253: u3 in LSeg(p2,z2) by A251,XBOOLE_0:def 4;
A254: u3 <> p2 by A95,A99,A233,A245,A252,XBOOLE_0:3;
A255: p2 in LSeg(p2,z2) by RLTOPSP1:69;
  then
A256: LSeg(p2,u3) c= LSeg(p2,z2) by A253,TOPREAL1:12;
  then
A257: LSeg(NW-corner L~f,N-min L~f) misses LSeg(u3,p2) by A72,A89,A136,
SPRECT_3:21,XBOOLE_1:63;
A258: for i,j st 1 <= i & i < j & j < len f holds L~mid(f,i,j) misses LSeg(
  u3,p2)
  proof
    let i,j;
    assume that
A259: 1 <= i and
A260: i < j and
A261: j < len f;
    L~mid(f,i,j) misses LSeg(z2,p2) by A140,A259,A260,A261;
    hence thesis by A253,A255,TOPREAL1:12,XBOOLE_1:63;
  end;
A262: now
A263: 1 in dom Red by FINSEQ_5:6;
    assume u3 = Red.1;
    then u3 in L~SpStSeq L~f by A238,A241,A263,PARTFUN1:def 8;
    then u3 in LSeg(p2,z2) /\ L~SpStSeq L~f by A253,XBOOLE_0:def 4;
    hence contradiction by A163,A254,TARSKI:def 1;
  end;
  L~Red is_an_arc_of Red/.1,Red/.len Red by TOPREAL1:31;
  then
A264: u2 in LSeg(p1,z2) /\ L~Red by A247,A249,JORDAN5C:def 1;
  then
A265: u2 in L~Red by XBOOLE_0:def 4;
A266: u2 in LSeg(p1,z2) by A264,XBOOLE_0:def 4;
A267: u2 <> p1 by A170,A230,A233,A245,A265,XBOOLE_0:3;
A268: p1 in LSeg(p1,z2) by RLTOPSP1:69;
  then
A269: LSeg(p1,u2) c= LSeg(p1,z2) by A266,TOPREAL1:12;
A270: LSeg(NW-corner L~f,N-min L~f) misses LSeg(u2,p1) by A200,A266,A268,
TOPREAL1:12,XBOOLE_1:63;
A271: for i,j st 1 < i & i < j & j<= len f holds L~mid(f,i,j) misses LSeg(u2
  ,p1)
  proof
    let i,j;
    assume that
A272: 1 < i and
A273: i < j and
A274: j<= len f;
    L~mid(f,i,j) misses LSeg(z2,p1) by A204,A272,A273,A274;
    hence thesis by A266,A268,TOPREAL1:12,XBOOLE_1:63;
  end;
A275: now
A276: 1 in dom Red by FINSEQ_5:6;
    assume u2 = Red.1;
    then u2 in L~SpStSeq L~f by A238,A241,A276,PARTFUN1:def 8;
    then u2 in { p1 } by A228,A266,XBOOLE_0:def 4;
    hence contradiction by A267,TARSKI:def 1;
  end;
  reconsider Red2=R_Cut(Red,u3) as S-Sequence_in_R2 by A252,A262,JORDAN3:70;
A277: Rev Red2 is S-Sequence_in_R2;
A278: Red2/.1 = u1 by A241,A252,SPRECT_3:39;
A279: len Red2 in dom Red2 by FINSEQ_5:6;
A280: (Rev Red2)/.1 = Red2/.len Red2 by FINSEQ_5:68
    .= Red2.len Red2 by A279,PARTFUN1:def 8
    .= u3 by A252,JORDAN3:59;
  then
A281: ((Rev Red2)/.1)`2 = p2`2 by A89,A253,SPPOL_1:63;
A282: (Rev Red2)/.1 <> p2 by A95,A99,A233,A245,A252,A280,XBOOLE_0:3;
A283: L~Rev Red2 = L~Red2 by SPPOL_2:22;
A284: u3 in L~Red2 by A252,A262,JORDAN5B:23;
  u3 in LSeg(p2,u3) by RLTOPSP1:69;
  then
A285: u3 in L~Red2 /\ LSeg(p2,u3) by A284,XBOOLE_0:def 4;
  now
    assume u1 in LSeg(p2,z2);
    then u1 in L~SpStSeq L~f /\ LSeg(p2,z2) by A238,XBOOLE_0:def 4;
    hence contradiction by A100,A163,A240,TARSKI:def 1;
  end;
  then LSeg(p2,z2) /\ L~Red2={ u3 } by A241,A248,Th1;
  then LSeg(p2,u3) /\ L~Red2 c={ u3 } by A253,A255,TOPREAL1:12,XBOOLE_1:26;
  then LSeg(p2,u3) /\ L~Red2={ u3 } by A285,ZFMISC_1:39;
  then reconsider
  RB2 = <*p2*>^Rev Red2 as S-Sequence_in_R2 by A280,A281,A282,A283,
SPRECT_3:31;
  LSeg(p2,f/.(len f -' 1)) misses L~Red by A93,A94,A246,XBOOLE_1:1,63;
  then LSeg(p2,f/.(len f -' 1)) misses L~Red2 by A252,JORDAN3:76,XBOOLE_1:63;
  then
A286: LSeg(p2,f/.(len f -' 1)) /\ L~Red2 = {} by XBOOLE_0:def 7;
  1 in dom Red2 by FINSEQ_5:6;
  then u1 in L~Red2 by A278,SPPOL_2:48;
  then
A287: u1 in L~SpStSeq L~f /\ L~Red2 by A238,XBOOLE_0:def 4;
  L~SpStSeq L~f /\ L~Red2 c= { u1 } by A242,A252,JORDAN3:76,XBOOLE_1:26;
  then
A288: L~SpStSeq L~f /\ L~Red2 = { u1 } by A287,ZFMISC_1:39;
  reconsider Red1=R_Cut(Red,u2) as S-Sequence_in_R2 by A265,A275,JORDAN3:70;
  len Red1 in dom Red1 by FINSEQ_5:6;
  then
A289: Red1/.len Red1 = Red1.len Red1 by PARTFUN1:def 8
    .= u2 by A265,JORDAN3:59;
A290: Red1/.1 = u1 by A241,A265,SPRECT_3:39;
A291: (Red1/.len Red1)`1 = p1`1 by A164,A266,A289,SPPOL_1:64;
A292: Red1/.len Red1 <> p1 by A170,A230,A233,A245,A265,A289,XBOOLE_0:3;
A293: u2 in L~Red1 by A265,A275,JORDAN5B:23;
  u2 in LSeg(p1,u2) by RLTOPSP1:69;
  then
A294: u2 in L~Red1 /\ LSeg(p1,u2) by A293,XBOOLE_0:def 4;
  now
    assume u1 in LSeg(p1,z2);
    then u1 in L~SpStSeq L~f /\ LSeg(p1,z2) by A238,XBOOLE_0:def 4;
    hence contradiction by A228,A231,A240,TARSKI:def 1;
  end;
  then LSeg(p1,z2) /\ L~Red1={ u2 } by A241,A247,Th1;
  then LSeg(p1,u2) /\ L~Red1 c={ u2 } by A266,A268,TOPREAL1:12,XBOOLE_1:26;
  then LSeg(p1,u2) /\ L~Red1={ u2 } by A294,ZFMISC_1:39;
  then reconsider RB1 = Red1^<*p1*> as S-Sequence_in_R2 by A289,A291,A292,
SPRECT_2:65;
  LSeg(p1,f/.2) misses L~Red by A167,A169,A246,XBOOLE_1:1,63;
  then LSeg(p1,f/.2) misses L~Red1 by A265,JORDAN3:76,XBOOLE_1:63;
  then
A295: LSeg(p1,f/.2) /\ L~Red1 = {} by XBOOLE_0:def 7;
  1 in dom Red1 by FINSEQ_5:6;
  then
A296: u1 in L~Red1 by A290,SPPOL_2:48;
  then
A297: u1 in L~SpStSeq L~f /\ L~Red1 by A238,XBOOLE_0:def 4;
  L~SpStSeq L~f /\ L~Red1 c= { u1 } by A242,A265,JORDAN3:76,XBOOLE_1:26;
  then
A298: L~SpStSeq L~f /\ L~Red1 = { u1 } by A297,ZFMISC_1:39;
  len Rev Red2 = len Red2 by FINSEQ_5:def 3;
  then
A299: RB2/.len RB2 = (Rev Red2)/.len Red2 by SPRECT_3:11
    .= u1 by A278,FINSEQ_5:68;
A300: RB2/.1 = p2 by FINSEQ_5:16;
  L~Rev Red2 = L~Red2 by SPPOL_2:22;
  then
A301: L~RB2 = L~Red2 \/ LSeg(p2,u3) by A280,SPPOL_2:20;
  then
A302: LSeg(p2,f/.(len f -' 1)) /\ L~RB2 = {} \/ LSeg(p2,f/.(len f -' 1)) /\
  LSeg(u3,p2) by A286,XBOOLE_1:23
    .={ p2 } by A89,A98,A256,SPRECT_1:11,SPRECT_3:22;
  <*u3*> is_in_the_area_of f by A244,A252,SPRECT_3:63;
  then Red2 is_in_the_area_of f by A244,A252,SPRECT_3:69;
  then Rev Red2 is_in_the_area_of f by SPRECT_3:68;
  then
A303: RB2 is_in_the_area_of f by A161,SPRECT_2:28;
  1 in dom Red1 by FINSEQ_5:6;
  then
A304: RB1/.1 = u1 by A290,FINSEQ_4:83;
  len <*p1*> = 1 by FINSEQ_1:56;
  then len RB1 = len Red1 + 1 by FINSEQ_1:35;
  then
A305: RB1/.len RB1 = p1 by FINSEQ_4:82;
A306: L~RB1 = L~Red1 \/ LSeg(u2,p1) by A289,SPPOL_2:19;
  then
A307: LSeg(p1,f/.2) /\ L~RB1 = {} \/ LSeg(p1,f/.2) /\ LSeg(u2,p1) by A295,
XBOOLE_1:23
    .={ p1 } by A164,A229,A269,SPRECT_1:12,SPRECT_3:22;
  <*u2*> is_in_the_area_of f by A244,A265,SPRECT_3:63;
  then Red1 is_in_the_area_of f by A244,A265,SPRECT_3:69;
  then
A308: RB1 is_in_the_area_of f by A232,SPRECT_2:28;
A309: L~Red9 is_an_arc_of z1,z2 by A50,A53,TOPREAL4:3;
  L~Red9 /\ L~SpStSeq L~f is closed by TOPS_1:35;
  then u1 in L~Red9 /\ L~SpStSeq L~f by A54,A55,A56,A309,JORDAN5C:def 2;
  then u1 in L~SpStSeq L~f by XBOOLE_0:def 4;
  then u1 in (LSeg(NW-corner L~f,NE-corner L~f) \/ LSeg(NE-corner L~f,
  SE-corner L~f)) \/ (LSeg(SE-corner L~f,SW-corner L~f) \/ LSeg(SW-corner L~f,
  NW-corner L~f)) by SPRECT_1:43;
  then
A310: u1 in LSeg(NW-corner L~f,NE-corner L~f) \/ LSeg(NE-corner L~f,
SE-corner L~f) or u1 in LSeg(SE-corner L~f,SW-corner L~f) \/ LSeg(SW-corner L~f
  ,NW-corner L~f) by XBOOLE_0:def 3;
A311: N-most L~f c= LSeg(NW-corner L~f,NE-corner L~f) by XBOOLE_1:17;
A312: A in N-most L~f by PSCOMP_1:101;
  then LSeg(NW-corner L~f,NE-corner L~f) = LSeg(NW-corner L~f,A) \/ LSeg(A,
  NE-corner L~f) by A311,TOPREAL1:11;
  then
A313: u1 in LSeg(NW-corner L~f,NE-corner L~f) implies u1 in LSeg(NW-corner
  L~f,A) or u1 in LSeg(A,NE-corner L~f) by XBOOLE_0:def 3;
  per cases by A310,A313,XBOOLE_0:def 3;
  suppose
A314: u1 in LSeg(NW-corner L~f,A);
A315: NW-corner L~f in LSeg(NW-corner L~f,NE-corner L~f) by RLTOPSP1:69;
    then LSeg(NW-corner L~f,A) c= LSeg(NW-corner L~f,NE-corner L~f) by A311
,A312,TOPREAL1:12;
    then LSeg(NW-corner L~f,u1) c= LSeg(NW-corner L~f,NE-corner L~f) by A314
,A315,TOPREAL1:12;
    then LSeg(NW-corner L~f,u1) c= L~SpStSeq L~f by A66,XBOOLE_1:1;
    then
A316: LSeg(NW-corner L~f,u1) /\ L~Red1 c= { u1 } by A298,XBOOLE_1:26;
A317: A`2 = N-bound L~f by EUCLID:56;
A318: NW-corner L~f in LSeg(NW-corner L~f,A) by RLTOPSP1:69;
    then LSeg(NW-corner L~f,u1) misses LSeg(u2,p1) by A270,A314,TOPREAL1:12
,XBOOLE_1:63;
    then
A319: LSeg(NW-corner L~f,u1) /\ LSeg(u2,p1) = {} by XBOOLE_0:def 7;
    u1 in LSeg(NW-corner L~f,u1) by RLTOPSP1:69;
    then u1 in LSeg(NW-corner L~f,u1) /\ L~Red1 by A296,XBOOLE_0:def 4;
    then { u1 } c= LSeg(NW-corner L~f,u1) /\ L~Red1 by ZFMISC_1:37;
    then LSeg(NW-corner L~f,u1) /\ L~Red1={ u1 } by A316,XBOOLE_0:def 10;
    then
A320: LSeg(NW-corner L~f,u1) /\ L~RB1 = { u1 } \/ {} by A306,A319,XBOOLE_1:23
      .= { u1 };
    (NW-corner L~f)`2 = N-bound L~f by EUCLID:56;
    then u1`2 = (NW-corner L~f)`2 by A314,A317,GOBOARD7:6;
    then reconsider
    M3 = <*NW-corner L~f*>^RB1 as S-Sequence_in_R2 by A235,A304,A320,
SPRECT_3:31;
A321: L~M3 = LSeg(NW-corner L~f,RB1/.1) \/ L~RB1 by SPPOL_2:20;
    set i1 = (S-min L~f)..f, i2 = (E-min L~f)..f;
    (N-max L~f)..f > 1 by A1,SPRECT_2:73;
    then (N-max L~f)..f >= 1+1 by NAT_1:13;
    then 2 <= (E-max L~f)..f by A1,SPRECT_2:74,XXREAL_0:2;
    then
A322: 2 < i2 by A1,SPRECT_2:75,XXREAL_0:2;
    LSeg(p1,f/.2) misses LSeg(NW-corner L~f,u1) by A197,A314,A318,TOPREAL1:12
,XBOOLE_1:63;
    then LSeg(p1,f/.2) /\ LSeg(NW-corner L~f,u1) = {} by XBOOLE_0:def 7;
    then
A323: LSeg(p1,f/.2) /\ L~M3 = {} \/ LSeg(p1,f/.2) /\ L~RB1 by A304,A321,
XBOOLE_1:23
      .= { p1 } by A307;
A324: L~M3 = LSeg(NW-corner L~f,RB1/.1) \/ L~RB1 by SPPOL_2:20;
    (W-min L~f)..f < len f by A1,SPRECT_2:80;
    then
A325: i1 < len f by A1,SPRECT_2:78,XXREAL_0:2;
A326: E-min L~f in rng f by SPRECT_2:49;
    then
A327: i2 in dom f by FINSEQ_4:30;
    then
A328: i2 <= len f by FINSEQ_3:27;
    then reconsider M4 = mid(f,2,i2) as S-Sequence_in_R2 by A322,JORDAN4:52;
    L~M4 misses L~Red by A57,A246,A327,SPRECT_3:34,XBOOLE_1:63;
    then
A329: L~M4 misses L~Red1 by A265,JORDAN3:76,XBOOLE_1:63;
    (S-max L~f)..f < i1 by A1,SPRECT_2:77;
    then
A330: i2 < i1 by A1,SPRECT_2:76,XXREAL_0:2;
    then
A331: 2 < i1 by A322,XXREAL_0:2;
    then 1 < i1 by XXREAL_0:2;
    then reconsider M1 = mid(f,i1,len f) as S-Sequence_in_R2 by A325,JORDAN4:52
;
A332: L~M1 misses L~M4 by A330,A325,A322,SPRECT_2:51;
A333: L~M1 misses LSeg(p1,f/.2) by A174,A325,A331;
A334: len M1 >= 2 by TOPREAL1:def 10;
A335: M4/.1 = f/.2 by A57,A327,SPRECT_2:12;
    L~M4 misses LSeg(u2,p1) by A271,A322,A328;
    then
A336: L~RB1 misses L~M4 by A306,A329,XBOOLE_1:70;
A337: LSeg(NW-corner L~f,u1) c= LSeg(NW-corner L~f,A) by A314,A318,TOPREAL1:12;
A338: now
A339: LSeg(NW-corner L~f,A) /\ L~f = {A} by PSCOMP_1:102;
      assume L~f meets LSeg(NW-corner L~f,u1);
      then consider u being set such that
A340: u in L~f and
A341: u in LSeg(NW-corner L~f,u1) by XBOOLE_0:3;
      reconsider u as Point of TOP-REAL 2 by A340;
      u in LSeg(NW-corner L~f,A) /\ L~f by A337,A340,A341,XBOOLE_0:def 4;
      then u = A by A339,TARSKI:def 1;
      hence contradiction by A240,A314,A340,A341,SPRECT_3:16;
    end;
    then L~M4 misses LSeg(NW-corner L~f,u1) by A57,A327,SPRECT_3:34,XBOOLE_1:63
;
    then
A342: L~M3 misses L~M4 by A304,A321,A336,XBOOLE_1:70;
A343: S-min L~f in rng f by SPRECT_2:45;
    then
A344: i1 in dom f by FINSEQ_4:30;
    then
A345: M1 is_in_the_area_of f by A58,SPRECT_2:27;
A346: (M1/.len M1)`2 = (f/.len f)`2 by A58,A344,SPRECT_2:13
      .= (f/.1)`2 by FINSEQ_6:def 1
      .= N-bound L~f by A1,EUCLID:56;
    (M1/.1)`2 = (f/.i1)`2 by A58,A344,SPRECT_2:12
      .= (S-min L~f)`2 by A343,FINSEQ_5:41
      .= S-bound L~f by EUCLID:56;
    then
A347: M1 is_a_v.c._for f by A345,A346,SPRECT_2:def 3;
A348: <*NW-corner L~f*>^RB1 is_in_the_area_of f by A63,A308,SPRECT_2:28;
    1 < i1 by A331,XXREAL_0:2;
    then
A349: L~M1 misses LSeg(u2,p1) by A271,A325;
A350: M3/.len M3 = p1 by A305,SPRECT_3:11;
    LSeg(p1,f/.2) /\ L~M4 = { f/.2 } by A192,A322,A328;
    then reconsider
    M2 = M3^M4 as S-Sequence_in_R2 by A81,A171,A350,A335,A342,A323,SPRECT_3:38;
    M2 = <*NW-corner L~f*>^(RB1^mid(f,2,i2)) by FINSEQ_1:45;
    then
A351: (M2/.1)`1 = (NW-corner L~f)`1 by FINSEQ_5:16
      .= W-bound L~f by EUCLID:56;
    mid(f,2,i2) is_in_the_area_of f by A57,A327,SPRECT_2:27;
    then
A352: M2 is_in_the_area_of f by A348,SPRECT_2:28;
    (M2/.len M2)`1 = (mid(f,2,i2)/.len mid(f,2,i2))`1 by SPRECT_3:11
      .= (f/.i2)`1 by A57,A327,SPRECT_2:13
      .= (E-min L~f)`1 by A326,FINSEQ_5:41
      .= E-bound L~f by EUCLID:56;
    then
A353: M2 is_a_h.c._for f by A352,A351,SPRECT_2:def 2;
A354: L~M2 = L~M3 \/ LSeg(M3/.len M3,M4/.1) \/ L~M4 by SPPOL_2:23;
    len M2 >= 2 by TOPREAL1:def 10;
    then L~M1 meets L~M2 by A334,A347,A353,SPRECT_2:33;
    then L~M1 meets L~M3 \/ LSeg(M3/.len M3,M4/.1) by A354,A332,XBOOLE_1:70;
    then
A355: L~M1 meets L~M3 by A350,A335,A333,XBOOLE_1:70;
    L~M1 misses LSeg(NW-corner L~f,u1) by A58,A344,A338,SPRECT_3:34,XBOOLE_1:63
;
    then L~M1 meets L~RB1 by A304,A355,A324,XBOOLE_1:70;
    then L~M1 meets L~Red1 by A306,A349,XBOOLE_1:70;
    then L~M1 meets L~Red by A265,JORDAN3:76,XBOOLE_1:63;
    hence contradiction by A58,A246,A344,SPRECT_3:34,XBOOLE_1:63;
  end;
  suppose that
A356: u1 in LSeg(A,NE-corner L~f) and
A357: N-min L~f = NW-corner L~f;
    set i1 = (S-max L~f)..f, i2 = (E-max L~f)..f;
    (N-max L~f)..f > 1 by A1,SPRECT_2:73;
    then
A358: 1 < i2 by A1,SPRECT_2:74,XXREAL_0:2;
    (S-min L~f)..f <= (W-min L~f)..f by A1,SPRECT_2:78;
    then (S-max L~f)..f < (W-min L~f)..f by A1,SPRECT_2:77,XXREAL_0:2;
    then (S-max L~f)..f + 1 <= (W-min L~f)..f by NAT_1:13;
    then (S-max L~f)..f + 1 < len f by A1,SPRECT_2:80,XXREAL_0:2;
    then
A359: i1 < len f -' 1 by A61,XREAL_1:8;
    (E-max L~f)..f < (E-min L~f)..f by A1,SPRECT_2:75;
    then
A360: i2 < i1 by A1,SPRECT_2:76,XXREAL_0:2;
    then
A361: 1 < i1 by A358,XXREAL_0:2;
    then reconsider M4 = mid(f,i1,len f -' 1) as S-Sequence_in_R2 by A61,A359,
JORDAN4:51;
A362: L~M4 misses LSeg(u3,p2) by A62,A258,A359,A361;
    i1 < len f by A359,NAT_D:44;
    then
A363: i2 < len f by A360,XXREAL_0:2;
    then i2+1 <= len f by NAT_1:13;
    then reconsider M2 = mid(f,1,i2) as S-Sequence_in_R2 by A358,JORDAN4:51;
A364: L~M2 misses L~M4 by A62,A360,A359,A358,SPRECT_2:51;
    i2 < len f -' 1 by A360,A359,XXREAL_0:2;
    then
A365: L~M2 misses LSeg(p2,f/.(len f -' 1)) by A106,A358;
A366: len M2 >= 2 by TOPREAL1:def 10;
A367: L~M2 misses LSeg(u3,p2) by A258,A358,A363;
A368: E-max L~f in rng f by SPRECT_2:50;
    then
A369: i2 in dom f by FINSEQ_4:30;
    then
A370: (M2/.len M2)`1 = (f/.i2)`1 by A15,SPRECT_2:13
      .= (E-max L~f)`1 by A368,FINSEQ_5:41
      .= E-bound L~f by EUCLID:56;
A371: S-max L~f in rng f by SPRECT_2:46;
    then
A372: i1 in dom f by FINSEQ_4:30;
    then L~M4 misses L~Red by A60,A246,SPRECT_3:34,XBOOLE_1:63;
    then L~M4 misses L~Red2 by A252,JORDAN3:76,XBOOLE_1:63;
    then
A373: L~RB2 misses L~M4 by A301,A362,XBOOLE_1:70;
A374: M2 is_in_the_area_of f by A15,A369,SPRECT_2:27;
    (M2/.1)`1 = (f/.1)`1 by A15,A369,SPRECT_2:12
      .= W-bound L~f by A1,A357,EUCLID:56;
    then
A375: M2 is_a_h.c._for f by A374,A370,SPRECT_2:def 2;
A376: A`2 = N-bound L~f by EUCLID:56;
A377: (NE-corner L~f)`2 = N-bound L~f by EUCLID:56;
A378: M4/.len M4 = f/.(len f -' 1) by A60,A372,SPRECT_2:13;
    then LSeg(M4/.len M4,p2) /\ L~M4={ M4/.len M4 } by A127,A359,A361;
    then reconsider
    M1 = M4^RB2 as S-Sequence_in_R2 by A79,A84,A97,A300,A302,A378,A373,
SPRECT_3:38;
    mid(f,i1,len f -' 1) is_in_the_area_of f by A60,A372,SPRECT_2:27;
    then
A379: M1 is_in_the_area_of f by A303,SPRECT_2:28;
A380: (M1/.len M1)`2 = (RB2/.len RB2)`2 by SPRECT_3:11
      .= N-bound L~f by A299,A356,A377,A376,GOBOARD7:6;
    mid(f,i1,len f -' 1) is non empty by A60,A372,SPRECT_2:11;
    then 1 in dom mid(f,i1,len f -' 1) by FINSEQ_5:6;
    then (M1/.1)`2 = (mid(f,i1,len f -' 1)/.1)`2 by FINSEQ_4:83
      .= (f/.i1)`2 by A60,A372,SPRECT_2:12
      .= (S-max L~f)`2 by A371,FINSEQ_5:41
      .= S-bound L~f by EUCLID:56;
    then
A381: M1 is_a_v.c._for f by A379,A380,SPRECT_2:def 3;
A382: L~M1 = L~M4 \/ LSeg(M4/.len M4,p2) \/ L~RB2 by A300,SPPOL_2:23
      .= L~M4 \/ (LSeg(M4/.len M4,p2) \/ L~RB2) by XBOOLE_1:4;
    len M1 >= 2 by TOPREAL1:def 10;
    then L~M1 meets L~M2 by A366,A381,A375,SPRECT_2:33;
    then L~M2 meets L~RB2 \/ LSeg(p2,M4/.len M4) by A382,A364,XBOOLE_1:70;
    then L~M2 meets L~RB2 by A378,A365,XBOOLE_1:70;
    then L~M2 meets L~Red2 by A301,A367,XBOOLE_1:70;
    then L~M2 meets L~Red by A252,JORDAN3:76,XBOOLE_1:63;
    hence contradiction by A15,A246,A369,SPRECT_3:34,XBOOLE_1:63;
  end;
  suppose that
A383: u1 in LSeg(A,NE-corner L~f) and
A384: N-min L~f <> NW-corner L~f;
    set i1 = (S-min L~f)..f, i2 = (E-max L~f)..f;
A385: (S-min L~f)..f <= (W-min L~f)..f by A1,SPRECT_2:78;
    W-max L~f <> N-min L~f by A384,PSCOMP_1:124;
    then (S-min L~f)..f < (W-max L~f)..f by A1,A385,SPRECT_2:79,XXREAL_0:2;
    then (S-min L~f)..f + 1 <= (W-max L~f)..f by NAT_1:13;
    then (S-min L~f)..f + 1 < len f by A1,SPRECT_2:81,XXREAL_0:2;
    then
A386: i1 < len f -' 1 by A61,XREAL_1:8;
A387: N-min L~f in LSeg(NW-corner L~f,N-min L~f) by RLTOPSP1:69;
A388: (N-min L~f)`2 = (NW-corner L~f)`2 by PSCOMP_1:95;
    (N-max L~f)..f > 1 by A1,SPRECT_2:73;
    then
A389: 1 < i2 by A1,SPRECT_2:74,XXREAL_0:2;
    (E-max L~f)..f < (E-min L~f)..f by A1,SPRECT_2:75;
    then (E-max L~f)..f < (S-max L~f)..f by A1,SPRECT_2:76,XXREAL_0:2;
    then
A390: i2 < i1 by A1,SPRECT_2:77,XXREAL_0:2;
    then
A391: 1 < i1 by A389,XXREAL_0:2;
    then reconsider M4 = mid(f,i1,len f -' 1) as S-Sequence_in_R2 by A61,A386,
JORDAN4:51;
A392: L~M4 misses LSeg(u3,p2) by A62,A258,A386,A391;
    i1 < len f by A386,NAT_D:44;
    then
A393: i2 < len f by A390,XXREAL_0:2;
    then i2+1 <= len f by NAT_1:13;
    then reconsider M3 = mid(f,1,i2) as S-Sequence_in_R2 by A389,JORDAN4:51;
A394: L~M3 misses L~M4 by A62,A390,A386,A389,SPRECT_2:51;
    i2 < len f -' 1 by A390,A386,XXREAL_0:2;
    then
A395: L~M3 misses LSeg(p2,f/.(len f -' 1)) by A106,A389;
A396: L~M3 misses LSeg(u3,p2) by A258,A389,A393;
A397: E-max L~f in rng f by SPRECT_2:50;
    then
A398: i2 in dom f by FINSEQ_4:30;
    then
A399: M3/.1 = N-min L~f by A1,A15,SPRECT_2:12;
A400: S-min L~f in rng f by SPRECT_2:45;
    then
A401: i1 in dom f by FINSEQ_4:30;
    then L~M4 misses L~Red by A60,A246,SPRECT_3:34,XBOOLE_1:63;
    then L~M4 misses L~Red2 by A252,JORDAN3:76,XBOOLE_1:63;
    then
A402: L~RB2 misses L~M4 by A301,A392,XBOOLE_1:70;
A403: M4/.len M4 = f/.(len f -' 1) by A60,A401,SPRECT_2:13;
    then LSeg(M4/.len M4,p2) /\ L~M4={ M4/.len M4 } by A127,A386,A391;
    then reconsider
    M1 = M4^RB2 as S-Sequence_in_R2 by A79,A84,A97,A300,A302,A403,A402,
SPRECT_3:38;
A404: L~M1 = L~M4 \/ LSeg(M4/.len M4,p2) \/ L~RB2 by A300,SPPOL_2:23
      .= L~M4 \/ (LSeg(M4/.len M4,p2) \/ L~RB2) by XBOOLE_1:4;
    1 in dom M3 by FINSEQ_5:6;
    then N-min L~f in L~M3 by A399,SPPOL_2:48;
    then N-min L~f in LSeg(NW-corner L~f,N-min L~f) /\ L~M3 by A387,
XBOOLE_0:def 4;
    then
A405: {N-min L~f} c= LSeg(NW-corner L~f,N-min L~f) /\ L~M3 by ZFMISC_1:37;
A406: LSeg(NW-corner L~f, N-min L~f) /\ L~f = {N-min L~f} by PSCOMP_1:102;
    then LSeg(NW-corner L~f,N-min L~f) /\ L~M3 c= { N-min L~f } by A15,A398,
SPRECT_3:34,XBOOLE_1:26;
    then LSeg(NW-corner L~f,N-min L~f) /\ L~M3={ N-min L~f } by A405,
XBOOLE_0:def 10;
    then reconsider
    M2 = <*NW-corner L~f*>^M3 as S-Sequence_in_R2 by A384,A388,A399,SPRECT_3:31
;
A407: (M2/.1)`1 = (NW-corner L~f)`1 by FINSEQ_5:16
      .= W-bound L~f by EUCLID:56;
A408: now
      assume LSeg(NW-corner L~f,N-min L~f) meets L~M4;
      then
A409: LSeg(NW-corner L~f,N-min L~f) /\ L~M4 <> {} by XBOOLE_0:def 7;
      LSeg(NW-corner L~f,N-min L~f) /\ L~M4 c= { N-min L~f } by A60,A401,A406,
SPRECT_3:34,XBOOLE_1:26;
      then LSeg(NW-corner L~f,N-min L~f) /\ L~M4 = { N-min L~f } by A409,
ZFMISC_1:39;
      then N-min L~f in LSeg(NW-corner L~f,N-min L~f) /\ L~M4 by TARSKI:def 1;
      then N-min L~f in L~M4 by XBOOLE_0:def 4;
      hence contradiction by A1,A62,A386,A391,SPRECT_3:47;
    end;
A410: A`2 = N-bound L~f by EUCLID:56;
A411: (NE-corner L~f)`2 = N-bound L~f by EUCLID:56;
    mid(f,1,i2) is_in_the_area_of f by A15,A398,SPRECT_2:27;
    then
A412: M2 is_in_the_area_of f by A63,SPRECT_2:28;
    (M2/.len M2)`1 = (mid(f,1,i2)/.len mid(f,1,i2))`1 by SPRECT_3:11
      .= (f/.i2)`1 by A15,A398,SPRECT_2:13
      .= (E-max L~f)`1 by A397,FINSEQ_5:41
      .= E-bound L~f by EUCLID:56;
    then
A413: M2 is_a_h.c._for f by A412,A407,SPRECT_2:def 2;
A414: L~M2 = LSeg(NW-corner L~f,M3/.1) \/ L~M3 by SPPOL_2:20;
    mid(f,i1,len f -' 1) is_in_the_area_of f by A60,A401,SPRECT_2:27;
    then
A415: M1 is_in_the_area_of f by A303,SPRECT_2:28;
A416: (M1/.len M1)`2 = u1`2 by A299,SPRECT_3:11
      .= N-bound L~f by A383,A411,A410,GOBOARD7:6;
    mid(f,i1,len f -' 1) is non empty by A60,A401,SPRECT_2:11;
    then 1 in dom mid(f,i1,len f -' 1) by FINSEQ_5:6;
    then (M1/.1)`2 = (mid(f,i1,len f -' 1)/.1)`2 by FINSEQ_4:83
      .= (f/.i1)`2 by A60,A401,SPRECT_2:12
      .= (S-min L~f)`2 by A400,FINSEQ_5:41
      .= S-bound L~f by EUCLID:56;
    then
A417: M1 is_a_v.c._for f by A415,A416,SPRECT_2:def 3;
A418: len M1 >= 2 by TOPREAL1:def 10;
    now
      NW-corner L~f in LSeg(NW-corner L~f,NE-corner L~f) by RLTOPSP1:69;
      then LSeg(NW-corner L~f,A) c= LSeg(NW-corner L~f,NE-corner L~f) by A311
,A312,TOPREAL1:12;
      then LSeg(NW-corner L~f,N-min L~f) c= L~SpStSeq L~f by A66,XBOOLE_1:1;
      then
A419: LSeg(NW-corner L~f,N-min L~f) /\ L~Red2 c= { u1 } by A288,XBOOLE_1:26;
      assume LSeg(NW-corner L~f,N-min L~f) meets L~Red2;
      then LSeg(NW-corner L~f,N-min L~f) /\ L~Red2 <> {} by XBOOLE_0:def 7;
      then LSeg(NW-corner L~f,N-min L~f) /\ L~Red2 = { u1} by A419,ZFMISC_1:39;
      then u1 in LSeg(NW-corner L~f,N-min L~f) /\ L~Red2 by TARSKI:def 1;
      then u1 in LSeg(NW-corner L~f,N-min L~f) by XBOOLE_0:def 4;
      then
A420: u1 in LSeg(NW-corner L~f,A) /\ LSeg(A,NE-corner L~f) by A383,
XBOOLE_0:def 4;
      LSeg(NW-corner L~f,A) /\ LSeg(A,NE-corner L~f) = {A} by A311,A312,
TOPREAL1:14;
      then u1 = A by A420,TARSKI:def 1;
      hence contradiction by A240,SPRECT_1:13;
    end;
    then LSeg(NW-corner L~f,N-min L~f) misses L~RB2 by A257,A301,XBOOLE_1:70;
    then LSeg(NW-corner L~f,N-min L~f) misses LSeg(M4/.len M4,p2) \/ L~RB2 by
A133,A403,XBOOLE_1:70;
    then
A421: LSeg(NW-corner L~f,N-min L~f) misses L~M1 by A404,A408,XBOOLE_1:70;
    len M2 >= 2 by TOPREAL1:def 10;
    then
A422: L~M1 meets L~M2 by A418,A417,A413,SPRECT_2:33;
    M3/.1 = f/.1 by A15,A398,SPRECT_2:12;
    then L~M3 meets L~M1 by A1,A422,A421,A414,XBOOLE_1:70;
    then L~M3 meets L~RB2 \/ LSeg(p2,M4/.len M4) by A404,A394,XBOOLE_1:70;
    then L~M3 meets L~RB2 by A403,A395,XBOOLE_1:70;
    then L~M3 meets L~Red2 by A301,A396,XBOOLE_1:70;
    then L~M3 meets L~Red by A252,JORDAN3:76,XBOOLE_1:63;
    hence contradiction by A15,A246,A398,SPRECT_3:34,XBOOLE_1:63;
  end;
  suppose that
A423: u1 in LSeg(NE-corner L~f,SE-corner L~f) and
A424: N-min L~f = W-max L~f;
A425: (RB2/.1)`1 = W-bound L~f by A97,A300,A424,EUCLID:56;
A426: (SE-corner L~f)`1 = E-bound L~f by EUCLID:56;
    (NE-corner L~f)`1 = E-bound L~f by EUCLID:56;
    then (RB2/.len RB2)`1 = E-bound L~f by A299,A423,A426,GOBOARD7:5;
    then
A427: RB2 is_a_h.c._for f by A303,A425,SPRECT_2:def 2;
    set i1 = (S-max L~f)..f, i2 = (N-max L~f)..f, i3 = (W-min L~f)..f;
A428: mid(f,i1,i2) = Rev mid(f,i2,i1) by JORDAN4:30;
    (N-max L~f)..f <= (E-max L~f)..f by A1,SPRECT_2:74;
    then (N-max L~f)..f < (E-min L~f)..f by A1,SPRECT_2:75,XXREAL_0:2;
    then
A429: i2 < i1 by A1,SPRECT_2:76,XXREAL_0:2;
    W-min L~f in rng f by SPRECT_2:47;
    then i3 in dom f by FINSEQ_4:30;
    then
A430: i3 <= len f by FINSEQ_3:27;
A431: 1 < i2 by A1,SPRECT_2:73;
A432: S-max L~f in rng f by SPRECT_2:46;
    then
A433: i1 in dom f by FINSEQ_4:30;
    then i1 <= len f by FINSEQ_3:27;
    then mid(f,i2,i1) is S-Sequence_in_R2 by A431,A429,JORDAN4:52;
    then reconsider M1 = mid(f,i1,i2) as S-Sequence_in_R2 by A428;
A434: len RB2 >= 2 by TOPREAL1:def 10;
    (S-max L~f)..f < (S-min L~f)..f by A1,SPRECT_2:77;
    then i3 > i1 by A1,SPRECT_2:78,XXREAL_0:2;
    then i1 < len f by A430,XXREAL_0:2;
    then L~mid(f,i2,i1) misses LSeg(u3,p2) by A258,A431,A429;
    then
A435: L~M1 misses LSeg(u3,p2) by A428,SPPOL_2:22;
A436: N-max L~f in rng f by SPRECT_2:44;
    then
A437: i2 in dom f by FINSEQ_4:30;
    then
A438: (M1/.len M1)`2 = (f/.i2)`2 by A433,SPRECT_2:13
      .= (N-max L~f)`2 by A436,FINSEQ_5:41
      .= N-bound L~f by EUCLID:56;
A439: M1 is_in_the_area_of f by A433,A437,SPRECT_2:27;
    (M1/.1)`2 = (f/.i1)`2 by A433,A437,SPRECT_2:12
      .= (S-max L~f)`2 by A432,FINSEQ_5:41
      .= S-bound L~f by EUCLID:56;
    then
A440: M1 is_a_v.c._for f by A439,A438,SPRECT_2:def 3;
    len M1 >= 2 by TOPREAL1:def 10;
    then L~M1 meets L~RB2 by A434,A440,A427,SPRECT_2:33;
    then L~M1 meets L~Red2 by A301,A435,XBOOLE_1:70;
    then L~M1 meets L~Red by A252,JORDAN3:76,XBOOLE_1:63;
    hence contradiction by A246,A433,A437,SPRECT_3:34,XBOOLE_1:63;
  end;
  suppose that
A441: u1 in LSeg(NE-corner L~f,SE-corner L~f) and
A442: N-min L~f <> W-max L~f;
    set i1 = (S-max L~f)..f, i2 = (N-max L~f)..f, i3 = (W-min L~f)..f;
    (W-max L~f)..f <= len f -' 1 by A1,NAT_D:49,SPRECT_2:81;
    then
A443: i3 < len f -' 1 by A1,A442,SPRECT_2:79,XXREAL_0:2;
A444: W-min L~f in rng f by SPRECT_2:47;
    then
A445: i3 in dom f by FINSEQ_4:30;
    then
A446: 1 <= i3 by FINSEQ_3:27;
    then reconsider M3 = mid(f,i3,len f -' 1) as S-Sequence_in_R2 by A61,A443,
JORDAN4:51;
    L~M3 misses L~Red by A60,A246,A445,SPRECT_3:34,XBOOLE_1:63;
    then
A447: L~M3 misses L~Red2 by A252,JORDAN3:76,XBOOLE_1:63;
    L~M3 misses LSeg(u3,p2) by A62,A258,A443,A446;
    then
A448: L~RB2 misses L~M3 by A301,A447,XBOOLE_1:70;
A449: M3/.len M3 = f/.(len f -' 1) by A60,A445,SPRECT_2:13;
    then LSeg(M3/.len M3,p2) /\ L~M3={ M3/.len M3 } by A127,A443,A446;
    then reconsider
    M2 = M3^RB2 as S-Sequence_in_R2 by A79,A84,A97,A300,A302,A449,A448,
SPRECT_3:38;
    mid(f,i3,len f -' 1) is_in_the_area_of f by A60,A445,SPRECT_2:27;
    then
A450: M2 is_in_the_area_of f by A303,SPRECT_2:28;
A451: mid(f,i1,i2) = Rev mid(f,i2,i1) by JORDAN4:30;
A452: 1 < i2 by A1,SPRECT_2:73;
    (N-max L~f)..f <= (E-max L~f)..f by A1,SPRECT_2:74;
    then (N-max L~f)..f < (E-min L~f)..f by A1,SPRECT_2:75,XXREAL_0:2;
    then
A453: i2 < i1 by A1,SPRECT_2:76,XXREAL_0:2;
A454: S-max L~f in rng f by SPRECT_2:46;
    then
A455: i1 in dom f by FINSEQ_4:30;
    then i1 <= len f by FINSEQ_3:27;
    then mid(f,i2,i1) is S-Sequence_in_R2 by A452,A453,JORDAN4:52;
    then reconsider M1 = mid(f,i1,i2) as S-Sequence_in_R2 by A451;
    (S-max L~f)..f < (S-min L~f)..f by A1,SPRECT_2:77;
    then
A456: i3 > i1 by A1,SPRECT_2:78,XXREAL_0:2;
    then
A457: L~M1 misses L~M3 by A62,A452,A453,A443,SPRECT_2:54;
    i3 < len f by A61,A443,NAT_1:13;
    then i1 < len f by A456,XXREAL_0:2;
    then L~mid(f,i2,i1) misses LSeg(u3,p2) by A258,A452,A453;
    then
A458: L~M1 misses LSeg(u3,p2) by A451,SPPOL_2:22;
A459: len M1 >= 2 by TOPREAL1:def 10;
A460: N-max L~f in rng f by SPRECT_2:44;
    then
A461: i2 in dom f by FINSEQ_4:30;
    then
A462: (M1/.len M1)`2 = (f/.i2)`2 by A455,SPRECT_2:13
      .= (N-max L~f)`2 by A460,FINSEQ_5:41
      .= N-bound L~f by EUCLID:56;
    i1 < len f -' 1 by A456,A443,XXREAL_0:2;
    then L~mid(f,i2,i1) misses LSeg(p2,f/.(len f -' 1)) by A106,A452,A453;
    then
A463: L~M1 misses LSeg(p2,f/.(len f -' 1)) by A451,SPPOL_2:22;
A464: (SE-corner L~f)`1 = E-bound L~f by EUCLID:56;
A465: (NE-corner L~f)`1 = E-bound L~f by EUCLID:56;
A466: (M2/.len M2)`1 = (RB2/.len RB2)`1 by SPRECT_3:11
      .= E-bound L~f by A299,A441,A465,A464,GOBOARD7:5;
    mid(f,i3,len f -' 1) is non empty by A60,A445,SPRECT_2:11;
    then 1 in dom mid(f,i3,len f -' 1) by FINSEQ_5:6;
    then (M2/.1)`1 = (mid(f,i3,len f -' 1)/.1)`1 by FINSEQ_4:83
      .= (f/.i3)`1 by A60,A445,SPRECT_2:12
      .= (W-min L~f)`1 by A444,FINSEQ_5:41
      .= W-bound L~f by EUCLID:56;
    then
A467: M2 is_a_h.c._for f by A450,A466,SPRECT_2:def 2;
A468: L~M2 = L~M3 \/ LSeg(M3/.len M3,p2) \/ L~RB2 by A300,SPPOL_2:23
      .= L~M3 \/ (LSeg(M3/.len M3,p2) \/ L~RB2) by XBOOLE_1:4;
A469: M1 is_in_the_area_of f by A455,A461,SPRECT_2:27;
    (M1/.1)`2 = (f/.i1)`2 by A455,A461,SPRECT_2:12
      .= (S-max L~f)`2 by A454,FINSEQ_5:41
      .= S-bound L~f by EUCLID:56;
    then
A470: M1 is_a_v.c._for f by A469,A462,SPRECT_2:def 3;
    len M2 >= 2 by TOPREAL1:def 10;
    then L~M1 meets L~M2 by A459,A470,A467,SPRECT_2:33;
    then L~M1 meets L~RB2 \/ LSeg(p2,M3/.len M3) by A468,A457,XBOOLE_1:70;
    then L~M1 meets L~RB2 by A449,A463,XBOOLE_1:70;
    then L~M1 meets L~Red2 by A301,A458,XBOOLE_1:70;
    then L~M1 meets L~Red by A252,JORDAN3:76,XBOOLE_1:63;
    hence contradiction by A246,A455,A461,SPRECT_3:34,XBOOLE_1:63;
  end;
  suppose
A471: u1 in LSeg(SE-corner L~f,SW-corner L~f);
A472: (SW-corner L~f)`2 = S-bound L~f by EUCLID:56;
    (SE-corner L~f)`2 = S-bound L~f by EUCLID:56;
    then (RB1/.1)`2 = S-bound L~f by A304,A471,A472,GOBOARD7:6;
    then
A473: RB1 is_a_v.c._for f by A171,A305,A308,SPRECT_2:def 3;
A474: len RB1 >= 2 by TOPREAL1:def 10;
    set i1 = (E-min L~f)..f, i2 = (W-min L~f)..f;
A475: mid(f,i2,i1) = Rev mid(f,i1,i2) by JORDAN4:30;
    (E-min L~f)..f <= (S-max L~f)..f by A1,SPRECT_2:76;
    then (E-min L~f)..f < (S-min L~f)..f by A1,SPRECT_2:77,XXREAL_0:2;
    then
A476: i1 < i2 by A1,SPRECT_2:78,XXREAL_0:2;
    (N-max L~f)..f > 1 by A1,SPRECT_2:73;
    then (E-max L~f)..f > 1 by A1,SPRECT_2:74,XXREAL_0:2;
    then
A477: 1 < i1 by A1,SPRECT_2:75,XXREAL_0:2;
A478: W-min L~f in rng f by SPRECT_2:47;
    then
A479: i2 in dom f by FINSEQ_4:30;
    then
A480: i2 <= len f by FINSEQ_3:27;
    then mid(f,i1,i2) is S-Sequence_in_R2 by A477,A476,JORDAN4:52;
    then reconsider M2 = mid(f,i2,i1) as S-Sequence_in_R2 by A475;
    L~mid(f,i1,i2) misses LSeg(u2,p1) by A271,A477,A476,A480;
    then
A481: L~M2 misses LSeg(u2,p1) by A475,SPPOL_2:22;
A482: E-min L~f in rng f by SPRECT_2:49;
    then
A483: i1 in dom f by FINSEQ_4:30;
    then
A484: M2 is_in_the_area_of f by A479,SPRECT_2:27;
A485: (M2/.1)`1 = (f/.i2)`1 by A483,A479,SPRECT_2:12
      .= (W-min L~f)`1 by A478,FINSEQ_5:41
      .= W-bound L~f by EUCLID:56;
    (M2/.len M2)`1 = (f/.i1)`1 by A483,A479,SPRECT_2:13
      .= (E-min L~f)`1 by A482,FINSEQ_5:41
      .= E-bound L~f by EUCLID:56;
    then
A486: M2 is_a_h.c._for f by A484,A485,SPRECT_2:def 2;
    len M2 >= 2 by TOPREAL1:def 10;
    then L~RB1 meets L~M2 by A474,A473,A486,SPRECT_2:33;
    then L~M2 meets L~Red1 by A306,A481,XBOOLE_1:70;
    then L~M2 meets L~Red by A265,JORDAN3:76,XBOOLE_1:63;
    hence contradiction by A246,A483,A479,SPRECT_3:34,XBOOLE_1:63;
  end;
  suppose
A487: u1 in LSeg(SW-corner L~f,NW-corner L~f);
A488: (SW-corner L~f)`1 = W-bound L~f by EUCLID:56;
    set i1 = (S-min L~f)..f, i3 = (E-min L~f)..f;
A489: (NW-corner L~f)`1 = W-bound L~f by EUCLID:56;
    (N-max L~f)..f > 1 by A1,SPRECT_2:73;
    then (E-max L~f)..f > 1 by A1,SPRECT_2:74,XXREAL_0:2;
    then (E-max L~f)..f >= 1+1 by NAT_1:13;
    then
A490: 2 < i3 by A1,SPRECT_2:75,XXREAL_0:2;
A491: E-min L~f in rng f by SPRECT_2:49;
    then
A492: i3 in dom f by FINSEQ_4:30;
    then
A493: i3 <= len f by FINSEQ_3:27;
    then reconsider M3 = mid(f,2,i3) as S-Sequence_in_R2 by A490,JORDAN4:52;
    L~M3 misses L~Red by A57,A246,A492,SPRECT_3:34,XBOOLE_1:63;
    then
A494: L~M3 misses L~Red1 by A265,JORDAN3:76,XBOOLE_1:63;
    L~M3 misses LSeg(u2,p1) by A271,A490,A493;
    then
A495: L~RB1 misses L~M3 by A306,A494,XBOOLE_1:70;
A496: M3/.1 = f/.2 by A57,A492,SPRECT_2:12;
    then LSeg(p1,M3/.1) /\ L~M3={ M3/.1 } by A192,A490,A493;
    then reconsider
    M2 = RB1^M3 as S-Sequence_in_R2 by A81,A171,A305,A307,A496,A495,SPRECT_3:38
;
A497: (M2/.len M2)`1 = (mid(f,2,i3)/.len mid(f,2,i3))`1 by SPRECT_3:11
      .= (f/.i3)`1 by A57,A492,SPRECT_2:13
      .= (E-min L~f)`1 by A491,FINSEQ_5:41
      .= E-bound L~f by EUCLID:56;
    mid(f,2,i3) is_in_the_area_of f by A57,A492,SPRECT_2:27;
    then
A498: M2 is_in_the_area_of f by A308,SPRECT_2:28;
    1 in dom RB1 by FINSEQ_5:6;
    then (M2/.1)`1 = (RB1/.1)`1 by FINSEQ_4:83
      .= W-bound L~f by A304,A487,A489,A488,GOBOARD7:5;
    then
A499: M2 is_a_h.c._for f by A498,A497,SPRECT_2:def 2;
A500: L~M2 = L~RB1 \/ LSeg(p1,M3/.1) \/ L~M3 by A305,SPPOL_2:23;
    (W-min L~f)..f < len f by A1,SPRECT_2:80;
    then
A501: i1 < len f by A1,SPRECT_2:78,XXREAL_0:2;
    (E-min L~f)..f <= (S-max L~f)..f by A1,SPRECT_2:76;
    then
A502: i3 < i1 by A1,SPRECT_2:77,XXREAL_0:2;
    then
A503: 2 < i1 by A490,XXREAL_0:2;
    then
A504: 1 < i1 by XXREAL_0:2;
    then reconsider M1 = mid(f,i1,len f) as S-Sequence_in_R2 by A501,JORDAN4:52
;
A505: L~M1 misses L~M3 by A502,A501,A490,SPRECT_2:51;
A506: L~M1 misses LSeg(p1,f/.2) by A174,A501,A503;
A507: S-min L~f in rng f by SPRECT_2:45;
    then
A508: i1 in dom f by FINSEQ_4:30;
    then
A509: M1 is_in_the_area_of f by A58,SPRECT_2:27;
A510: (M1/.len M1)`2 = (f/.len f)`2 by A58,A508,SPRECT_2:13
      .= (f/.1)`2 by FINSEQ_6:def 1
      .= N-bound L~f by A1,EUCLID:56;
    (M1/.1)`2 = (f/.i1)`2 by A58,A508,SPRECT_2:12
      .= (S-min L~f)`2 by A507,FINSEQ_5:41
      .= S-bound L~f by EUCLID:56;
    then
A511: M1 is_a_v.c._for f by A509,A510,SPRECT_2:def 3;
A512: L~M1 misses LSeg(u2,p1) by A271,A501,A504;
A513: len M1 >= 2 by TOPREAL1:def 10;
    len M2 >= 2 by TOPREAL1:def 10;
    then L~M1 meets L~M2 by A513,A511,A499,SPRECT_2:33;
    then L~M1 meets L~RB1 \/ LSeg(p1,M3/.1) by A500,A505,XBOOLE_1:70;
    then L~M1 meets L~RB1 by A496,A506,XBOOLE_1:70;
    then L~M1 meets L~Red1 by A306,A512,XBOOLE_1:70;
    then L~M1 meets L~Red by A265,JORDAN3:76,XBOOLE_1:63;
    hence contradiction by A58,A246,A508,SPRECT_3:34,XBOOLE_1:63;
  end;
end;

Lm2: for f being non constant standard special_circular_sequence st f/.1 =
N-min L~f holds LeftComp f <> RightComp f
proof
  let f be non constant standard special_circular_sequence such that
A1: f/.1 = N-min L~f;
  per cases by REVROT_1:38;
  suppose
    f is clockwise_oriented;
    hence thesis by A1,Lm1;
  end;
  suppose
A2: Rev f is clockwise_oriented;
A3: LeftComp Rev f = RightComp f by GOBOARD9:26;
A4: RightComp Rev f = LeftComp f by GOBOARD9:27;
    N-min L~Rev f = N-min L~f by SPPOL_2:22
      .= Rev f/.len f by A1,FINSEQ_5:68
      .= Rev f/.len Rev f by FINSEQ_5:def 3
      .= Rev f/.1 by FINSEQ_6:def 1;
    hence thesis by A2,A3,A4,Lm1;
  end;
end;

theorem
  for f being non constant standard special_circular_sequence holds
  LeftComp f <> RightComp f
proof
  let f be non constant standard special_circular_sequence;
  set g = Rotate(f,N-min L~f);
A1: L~f = L~g by REVROT_1:33;
  N-min L~f in rng f by SPRECT_2:43;
  then
A2: g/.1 = N-min L~g by A1,FINSEQ_6:98;
A3: RightComp g = RightComp f by REVROT_1:37;
  LeftComp g = LeftComp f by REVROT_1:36;
  hence thesis by A2,A3,Lm2;
end;
