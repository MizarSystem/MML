:: Properties of Groups
::  by Gijs Geleijnse and Grzegorz Bancerek
::
:: Received May 31, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies CARD_1, REALSET1, GROUP_2, BOOLE, GRAPH_1, INT_1, NAT_1, ARYTM_1,
      ARYTM_3, GROUP_1, FINSET_1, GROUP_4, VECTSP_1, PBOOLE, TARSKI, RELAT_1,
      FINSEQ_1, FUNCT_1, FILTER_0, GROUP_8, SETFAM_1, LATTICES;
 notations XBOOLE_0, CARD_1, TARSKI, ZFMISC_1, SUBSET_1, XCMPLX_0, XREAL_0,
      RELAT_1, FUNCT_1, FUNCT_2, PBOOLE, BINOP_1, INT_1, NAT_1, RLVECT_1,
      FINSET_1, GROUP_2, SETFAM_1, GROUP_4, GROUP_6, GR_CY_1, FINSEQ_1,
      STRUCT_0, GROUP_1, INT_2;
 constructors REAL_1, BINOP_1, NAT_1, GROUP_4, GROUP_5, GR_CY_1, NAT_LAT,
      MEMBERED, PREPOWER, SETFAM_1, IRRAT_1, ARYTM_3, DOMAIN_1, ZFMISC_1,
      MCART_1, PBOOLE, ABIAN, WELLORD2, GROUP_6, REALSET2, BINOP_2;
 registrations INT_1, GR_CY_1, STRUCT_0, XREAL_0, GROUP_2, FINSEQ_1, RELSET_1,
      GROUP_1;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, FUNCT_1, WELLORD2, GROUP_1;
 theorems GROUP_2, GR_CY_1, GR_CY_2, NAT_1, TARSKI, XBOOLE_0, GROUP_1,
      RLVECT_1, CARD_1, GROUP_6, ALTCAT_1, PBOOLE, FUNCT_2, GROUP_4, INT_2,
      XCMPLX_1, INT_1, REAL_2, REAL_1, FINSET_1, FINSEQ_1, FUNCT_1, EULER_2,
      PYTHTRIP, REALSET2, RELSET_1, NEWTON;
 schemes SETFAM_1, GROUP_2, NAT_1, FINSEQ_1, FUNCT_2;

begin

 reserve G for strict Group,
         a,b,x,y,z for Element of the carrier of G,
         H,K for strict Subgroup of G,
         h for Element of the carrier of H,
         p,n for Nat,
         A for Subset of the carrier of G;

theorem
  p is prime & ord G = p & G is finite implies
  ex a st ord a = p
proof
assume A1: p is prime & ord G = p & G is finite;
 G is cyclic Group by A1,GR_CY_1:45;
 hence thesis by A1,GR_CY_1:43;
end;

theorem Th2:
for a1,a2 being Element of the carrier of H
for b1,b2 being Element of the carrier of G st a1 = b1 & a2 = b2
 holds a1*a2 = b1*b2
proof
let a1,a2 be Element of the carrier of H;
A1: the carrier of H c= the carrier of G by GROUP_2:def 5;
let b1,b2 be Element of the carrier of G such that A2: a1 = b1 & a2 = b2;
dom the mult of G = [:the carrier of G, the carrier of G:] by FUNCT_2:def 1;
then
A3: the mult of G is ManySortedSet of [:the carrier of G, the carrier of G:]
 by PBOOLE:def 3;
A4: the mult of H =
     (the mult of G) | [:the carrier of H ,the carrier of H :]
       by GROUP_2:def 5;
a1*a2 = (the mult of H).(a1,a2) by GROUP_1:def 1
     .= (the mult of G).(b1,b2) by A1,A2,A3,A4,ALTCAT_1:7
     .= b1*b2 by GROUP_1:def 1;
hence thesis;
end;

theorem Th3:
for a being Element of the carrier of H
for b being Element of the carrier of G st a = b
for n being Nat
 holds a|^n = b|^n
proof
 let a be Element of the carrier of H;
let b be Element of the carrier of G such that A1: a = b;
let n be Nat;
defpred P[Nat] means a|^$1 = b|^$1;
A2: P[0]
proof
A3: a |^ 0 = 1.H by GROUP_1:43;
  b |^ 0 = 1.G by GROUP_1:43;
  hence thesis by A3,GROUP_2:53;
end;
A4: for k being Nat st P[k] holds P[k + 1]
proof
let k be Nat such that A5: P[k];
A6: a |^ (k +1) = (a|^ k) * a by GROUP_1:49;
   b |^ (k +1) = (b|^ k) * b by GROUP_1:49;
hence thesis by A1,A5,A6,Th2;
end;
for k being Nat holds P[k] from NAT_1:sch 1(A2,A4);
hence thesis;
end;

theorem Th4:
for a being Element of the carrier of H
for b being Element of the carrier of G st a = b
for i being Integer
 holds a|^i = b|^i
proof
let a be Element of the carrier of H;
let b be Element of the carrier of G such that A1: a = b;
let i be Integer;
consider k being Nat such that
A2: i = k or i = - k by INT_1:8;
per cases by A2;
   suppose i = k;
   hence thesis by A1,Th3;
   end;
   suppose A3: i = -k;
   a" = b" by A1,GROUP_2:57; then
   a" |^ k = b" |^ k by Th3; then
   (a |^ k)" = b" |^ k by GROUP_1:72; then
   (a |^ k)" = (b |^ k)" by GROUP_1:72; then
   a |^ -k = (b |^ k)" by GROUP_1:71;
   hence thesis by A3,GROUP_1:71;
   end;
end;

theorem
for a being Element of the carrier of H
for b being Element of the carrier of G st a = b & G is finite
 holds ord a = ord b
proof
let a be Element of the carrier of H;
let b be Element of the carrier of G such that A1: a =b & G is finite;
H is finite by A1,GROUP_2:48;
then A2: not(a is_of_order_0) & not(b is_of_order_0) by A1,GR_CY_1:26;
then A3: a |^ (ord a) = 1.H by GROUP_1:def 12;
A4: b |^ ord b = 1.G by A2,GROUP_1:def 12;
A5: a |^ (ord a) = b |^ (ord b) by A3,A4,GROUP_2:53;
a |^ (ord a) = b |^ (ord a) by A1,Th3;
then A6: ord b divides ord a by A4,A5,GROUP_1:86;
 a |^ (ord b) = 1.G by A1,A4,Th3;
then a |^ (ord b) = 1.H by GROUP_2:53;
then A7: ord a divides ord b by GROUP_1:86;
thus thesis by A6,A7,NAT_1:52;
end;

theorem
for h being Element of the carrier of G st
    h in H holds   H  * h c= the carrier of H
proof
let h be Element of the carrier of G such that A1: h in H;
 for a being set holds a in H * h implies a in the carrier of H
 proof
   let a be set;
     assume a in H * h;
     then consider g being Element of the carrier of G
          such that A2: a = g * h & g in H by GROUP_2:126;
     g * h in H by A1,A2,GROUP_2:59;
     hence thesis by A2,RLVECT_1:def 1;
 end;
hence thesis by TARSKI:def 3;
end;

theorem Th7:
  for a st a <> 1.G holds gr {a} <> (1).G
proof
  let a such that A1: a <> 1.G;
  assume A2: gr {a} = (1).G;
A3: the carrier of (1).G = {1.G} by GROUP_2:def 7;
A4: {a} c= the carrier of gr {a} by GROUP_4:def 5;
   consider xx being set;
   a = 1.G
   proof
     for xx being set holds xx = a implies xx = 1.G
     proof
       let xx be set;
       assume xx = a;
       then xx in {a} by TARSKI:def 1;
       hence thesis by A2,A3,A4,TARSKI:def 1;
     end;
     hence thesis;
   end;
   hence thesis by A1;
 end;

theorem Th8:
for m being Integer holds (1.G) |^ m = 1.G
proof
let m be Integer;
consider k being Nat such that A1: m = k or m = - k by INT_1:8;
per cases by A1;
   suppose m = k;
   hence thesis by GROUP_1:42;
   end;
   suppose A2: m = -k;
   (1.G) |^ -k = ((1.G) |^ k)" by GROUP_1:70
              .= (1.G)" |^ k by GROUP_1:72
              .= (1.G) |^ k by GROUP_1:16
              .= 1.G by GROUP_1:42;
   hence thesis by A2;
   end;
end;

theorem Th9:
for m being Integer holds a |^ (m * ord a) = 1.G
proof
let m be Integer;
per cases;
    suppose a is_of_order_0; then
    ord a = 0 by GROUP_1:def 12;
    hence thesis by GROUP_1:43;
    end;
    suppose a is_not_of_order_0; then
A1:  a |^ ord a = 1.G by GROUP_1:def 12;
    (1.G) |^ m = 1.G by Th8;
    hence thesis by A1,GROUP_1:67;
   end;
end;

theorem Th10:
for a st a is_not_of_order_0
for m being Integer holds a |^ m = a |^(m mod ord a)
proof
let a such that A1: a is_not_of_order_0;
let m be Integer;
ord a <> 0 by A1,GROUP_1:def 12; then
m mod ord a = m - (m div ord a) * ord a by INT_1:def 8; then
a |^(m mod ord a) = a |^ (m + - 1 * (m div ord a) * ord a)
                 .= (a|^m) * a |^((-(m div ord a)) * ord a) by GROUP_1:63
                 .= (a|^m) * 1.G by Th9
                 .= (a|^m) by GROUP_1:def 5;
hence thesis;
end;

theorem Th11:
b is_not_of_order_0 implies gr {b} is finite
proof
assume A1: b is_not_of_order_0;
ord b <> 0 by A1,GROUP_1:def 12; then
A2: ord b > 0 by NAT_1:19;
deffunc B(Nat) = b |^ $1;
consider f being FinSequence such that
A3: len f = ord b & for i being Nat st i in Seg ord b holds
f.i = B(i) from FINSEQ_1:sch 2;
A4: dom f = Seg ord b by A3,FINSEQ_1:def 3;
the carrier of gr {b} c= rng f
 proof
   let x be set;
   assume x in the carrier of gr {b}; then
A5: x in gr {b} by RLVECT_1:def 1; then
   x in G by GROUP_2:49; then
   reconsider a = x as Element of the carrier of G by RLVECT_1:def 1;
   consider m being Integer such that
A6: a = b|^m by A5,GR_CY_1:25;
   set k = m mod ord b;
   ord b >= 0 by NAT_1:18; then
   k >= 0 by NEWTON:78; then
   reconsider k as Nat by INT_1:16;
A7: a = b|^k by A1,A6,Th10;
   per cases;
   suppose
A8:   k = 0;
     ord b >= 0+1 & ord b <= ord b by A2,NAT_1:38; then
A9:  ord b in Seg ord b by FINSEQ_1:3;
     a = 1.G by A7,A8,GROUP_1:43
      .= b |^ ord b by A1,GROUP_1:def 12
      .= f.ord b by A3,A9;
   hence x in rng f by A4,A9,FUNCT_1:def 5;
   end;
   suppose k <> 0; then
   k > 0 & k < ord b by A2,NEWTON:79,NAT_1:19; then
   k >= 0+1 & k <= ord b by NAT_1:38; then
A10:k in Seg ord b by FINSEQ_1:3; then
   a = f.k by A3,A7;
   hence x in rng f by A4,A10,FUNCT_1:def 5;
   end;
 end;
hence the carrier of gr {b} is finite by FINSET_1:13;
end;

theorem Th12:
b is_of_order_0 implies b" is_of_order_0
proof
  assume b is_of_order_0;
  then A1: b |^n = 1.G implies n = 0 by GROUP_1:def 11;
  b" is_of_order_0
   proof
     b" |^ n = 1.G implies n = 0
     proof
       assume b" |^ n = 1.G;
       then (b |^n)" = 1.G by GROUP_1:51;
       then (b |^n)" = (1.G)" by GROUP_1:16;
       then b |^n = 1.G by GROUP_1:17;
       hence thesis by A1;
     end;
     hence thesis by GROUP_1:def 11;
   end;
hence thesis;
end;

theorem Th13:
b is_of_order_0 iff
for n being Integer holds b |^n = 1.G implies n = 0
proof
A1:  b is_of_order_0 implies for n being Integer holds
    b |^n = 1.G implies n = 0
          proof
            assume A2: b is_of_order_0;
            then A3: for m being Nat holds b|^m = 1.G
                    implies m = 0 by GROUP_1:def 11;
            A4: for m being Nat holds b |^ - m = 1.G implies m = 0
                    proof
                      let m be Nat;
                      assume A5: b |^ - m = 1.G;
                      A6: b |^ -m = (b |^ m)" by GROUP_1:71;
                      A7: b" |^ m = 1.G by A5,A6,GROUP_1:72;
                      b" is_of_order_0 by A2,Th12;
                      hence thesis by A7,GROUP_1:def 11;
                    end;
            for n being Integer holds b |^ n = 1.G implies n = 0
            proof
              let n be Integer;
              assume A8: b |^ n = 1.G;
              consider k being Nat such that A9: n = k or n = - k by INT_1:8;
              per cases by A9;
              suppose n = k;
                hence thesis by A3,A8;
              end;
              suppose A10: n = -k;
                then k = 0 by A4,A8;
                hence thesis by A10;
              end;
            end;
            hence thesis;
          end;
(for n being Integer holds b |^n = 1.G implies n = 0) implies b is_of_order_0
         proof
           assume for n being Integer holds b|^n = 1.G implies n = 0;
           then for n being Nat holds b |^n = 1.G implies n = 0;
           hence thesis by GROUP_1:def 11;
         end;
hence thesis by A1;
end;

theorem
for G st ex a st a <> 1.G holds
(for H holds H = G or H = (1).G) iff
G is cyclic Group & G is finite & ex p being Nat st ord G = p & p is prime
proof
let G such that A1: ex a st a <> 1.G;
consider b such that A2: b <> 1.G by A1;
A3: (for H holds H = G or H = (1).G) implies
G is cyclic Group & G is finite & ex p being Nat st ord G = p & p is prime
   proof
     assume A4: for H holds H = G or H = (1).G;
     A5: gr {b} is cyclic Group by GR_CY_2:10;
     gr {b} <> (1).G by A2,Th7;
     then A6: gr {b} = G by A4;
     then A7: G is cyclic Group by A5;
     A8: b is_not_of_order_0
        proof
          assume A9: b is_of_order_0;
          then b|^2 <> 1.G by GROUP_1:def 11;
          then A10: gr{b|^2} <> (1).G by Th7;
          A11: b in G by A6,GR_CY_2:8;
          A12: gr{b|^2} <> G
          proof
            not b in gr{b|^2}
            proof
              assume b in gr{b|^2};
              then consider j1 being Integer such that
A13:           b = (b|^2)|^j1 by GR_CY_1:25;
              b = b |^ (2 *j1) by A13,GROUP_1:67;
              then b" * (b |^ (2 * j1)) = 1.G by GROUP_1:def 6;
              then (b|^ -1) * (b |^(2 * j1)) = 1.G by GROUP_1:62;
              then b |^ (-1 + (2 * j1)) = 1.G by GROUP_1:63;
              then A14: -1 + (2 * j1) = 0 by A9,Th13;
              -1 + (2* j1) <> 0
              proof
                assume -1 + (2 * j1) = 0; then
                2 * j1 = 1 by XCMPLX_1:136;
                hence thesis by INT_1:22;
              end;
              hence thesis by A14;
            end;
            hence thesis by A11;
            end;
          thus contradiction by A4,A10,A12;
          end;
     then consider n being Nat such that A15: n = ord b & b is_not_of_order_0;
     A16: G is finite by A6,A8,Th11;
     A17: ord G = ord gr{b} by A6;
     A18: ord b = ord gr{b} by A16,GR_CY_1:27;
     A19: ord G = n by A15,A16,A17,GR_CY_1:27;
     n is prime
       proof
        assume A20: not n is prime;
        ex u,v being Nat st u >1 & v > 1 & n = u * v
        proof
          not (n>1 & for m being Nat holds
              m divides n implies m = 1 or m = n)
                by A20,INT_2:def 5;
          then A21: n <= 1 or
             ex m being Nat st  m <> 1 & m <> n & (m divides n);
          A22: n > 1
            proof
              A23: n = ord G by A19;
              then A24: n >= 1 by A16,GROUP_1:90;
              n <> 1 by A2,A17,A18,A23,GROUP_1:85;
              hence thesis by A24,REAL_1:def 5;
              end;
          then consider m being Nat such that
A25:       m <> 1 & m <> n & m divides n by A21;
          consider o being Nat such that
A26:       n = m * o by A25,NAT_1:def 3;
          take u = m;
          take v = o;
          A27: u > 1
              proof
                u <> 0
                  proof
                    assume u = 0;
                    then n = 0 * (n div 0) by A25,NAT_1:49;
                    hence contradiction by A22;
                  end;
              then 0 < u by NAT_1:19;
              then 0 + 1 <= u by INT_1:20;
              hence thesis by A25,REAL_1:def 5;
              end;
            v > 1
            proof
               v <> 0 by A22,A26;
              then 0 < v by NAT_1:19;
              then 0 + 1 <= v by INT_1:20;
              then A28: 1 <= v;
              v <> 1 by A25,A26;
              hence thesis by A28,REAL_1:def 5;
            end;
          hence thesis by A26,A27;
        end;
        then consider u, v being Nat such that A29: u > 1 & v > 1 & n = u *v;
        ord (b |^v) = u by A6,A16,A19,A29,GR_CY_2:14;
        then A30: ord gr {b |^v } = u by A16,GR_CY_1:27;
        u <> n by A29,XCMPLX_1:7;
        then A32: gr {b |^v} <> G by A19,A30;
        gr {b |^v } <> (1).G by A29,A30,GROUP_2:81;
        hence thesis by A4,A32;
       end;
     hence thesis by A7,A16,A19;
     end;
:::::::::::::::::::    :: <=
A33: G is cyclic Group & G is finite & ord G = p & p is prime
   implies (for H holds H = G or H =(1).G)
   proof
   assume A34: G is cyclic Group & G is finite & ord G = p & p is prime;
   let H;
   A35: ord G = ord H * index H by A34,GROUP_2:177;
   ord H divides ord G by A35,NAT_1:def 3;
   then A36: ord H = ord G or ord H = 1 by A34,INT_2:def 5;
   per cases by A36;
   suppose ord H = ord G;
     hence thesis by A34,GROUP_2:85;
   end;
   suppose A37: ord H = 1;
A38:  H is finite by A34,GROUP_2:48;
     H is finite & ord H = 1 by A37,A38;
     hence thesis by GROUP_2:82;
   end;
 end;
 thus thesis by A3,A33;
end;

theorem Th15:
  for x,y,z being Element of the carrier of G
  for A being Subset of the carrier of G holds
  z in x * A * y iff ex a being Element of the carrier of G st
    z = x * a * y & a in A
proof
let x,y,z be Element of the carrier of G;
let A be Subset of the carrier of G;
A1: z in x * A * y implies
   ex a being Element of the carrier of G st z = x * a * y & a in A
   proof
   assume z in x * A * y;
   then consider b such that
   A2: z = b * y & b in x * A by GROUP_2:34;
   consider u being Element of the carrier of G such that
   A3: b = x * u & u in A by A2,GROUP_2:33;
   take a = u;
   thus thesis by A2,A3;
   end;
   (ex a being Element of the carrier of G st z = x * a * y & a in A)
               implies z in x * A * y
   proof
     assume ex a being Element of the carrier of G st
                z = x * a * y & a in A; then
     consider a being Element of the carrier of G such that
A4:   z = x * a * y & a in A;
     ex h being Element of the carrier of G st z = h * y & h in x * A
     proof
       take h = x * a;
       thus thesis by A4,GROUP_2:33;
     end;
     hence thesis by GROUP_2:34;
     end;
hence thesis by A1;
end;

theorem
  for A being non empty Subset of G holds
  for x being Element of the carrier of G holds
  Card A = Card (x" * A * x)
proof
let A be non empty Subset of G;
let x be Element of the carrier of G;
set B = x" * A * x;
A is non empty & B is non empty
     proof consider a being Element of A;
       x" * a * x in B by Th15;
       hence thesis;
     end;
then reconsider B as non empty Subset of G;
deffunc F(Element of the carrier of G) = x"*$1*x;
consider f being Function of A, the carrier of G such that
A1: for a being Element of A holds f.a = F(a) from FUNCT_2:sch 4;
A2: dom f = A by FUNCT_2:def 1;
A3:rng f = B
proof
A4:   rng f c= B
      proof
        let s be set; assume s in rng f; then
        consider a being set such that
        A5:  a in A & s = f.a by A2,FUNCT_1:def 5;
        reconsider a as Element of A by A5;
        s = F(a) by A1,A5;
        hence thesis by Th15;
      end;
   B c= rng f
       proof
         let s be set;
         assume s in B; then
         consider a being Element of the carrier of G such that
A6:       s = x" * a * x & a in A by Th15;
         ex x st x in dom f & s = f.x
              proof
                take x = a;
                thus thesis by A1,A6,FUNCT_2:def 1;
              end;
         hence thesis by FUNCT_1:def 5;
       end;
hence thesis by A4,XBOOLE_0:def 10;
end; then
reconsider f as Function of A,B by A2,FUNCT_2:4;
deffunc FF(Element of the carrier of G) = x*$1*x";
consider g being Function of B, the carrier of G such that
A7: for b being Element of B holds g.b = FF(b) from FUNCT_2:sch 4;
A8: dom g = B by FUNCT_2:def 1;
rng g = A
proof
A9:   rng g c= A
      proof
        let s be set; assume s in rng g; then
        consider a being set such that
        A10:  a in B & s = g.a by A8,FUNCT_1:def 5;
        reconsider a as Element of B by A10;
A11:    s = FF(a) by A7,A10;
        consider c being Element of the carrier of G such that
        A12: a = x" * c * x & c in A by Th15;
        s = x * (x" * c) * x * x" by A11,A12,GROUP_1:def 4
          .= (x * x") * c * x * x" by GROUP_1:def 4
          .= (x * x") * c * (x * x") by GROUP_1:def 4
          .= 1.G * c * (x * x") by GROUP_1:def 6
          .= 1.G * c * 1.G by GROUP_1:def 6
          .= 1.G * c by GROUP_1:def 5
          .= c by GROUP_1:def 5;
        hence thesis by A12;
      end;
   A c= rng g
       proof
         let s be set;
         assume A13: s in A; reconsider s as Element of A by A13;
         consider b being Element of the carrier of G such that
         A14: b = x" * s * x;
         A15: b in B by A14,Th15;
         ex u being set st u in dom g & s = g.u
         proof
           take u = b;
           u in dom g by A8,A15; then
           g.u = x * (x" * s * x) * x" by A7,A8,A14
              .= x * (x" * s) * x * x" by GROUP_1:def 4
              .= x * x" * s * x * x" by GROUP_1:def 4
              .= (x * x") * s * (x * x") by GROUP_1:def 4
              .= 1.G * s * (x * x") by GROUP_1:def 6
              .= 1.G * s * 1.G by GROUP_1:def 6
              .= 1.G * s by GROUP_1:def 5
              .= s by GROUP_1:def 5;
           hence thesis by A8,A15;
         end;
         hence thesis by FUNCT_1:def 5;
       end;
hence thesis by A9,XBOOLE_0:def 10;
end; then
reconsider g as Function of B,A by A8,FUNCT_2:4;
A16:for a,b being Element of A st f.a = f.b holds a = b
            proof
              let a,b be Element of A such that A17: f.a = f.b;
              A18: g.(f.a) = g.(f.b) by A17;
              A19:  x" * a * x in B by Th15;
              A20:  x" * b * x in B by Th15;
              g.(f.a) = g.(x" * a * x) by A1
                     .= x * (x" * a * x) * x" by A7,A19
                     .= x * (x" * a) * x * x" by GROUP_1:def 4
                     .= (x * x") * a * x * x" by GROUP_1:def 4
                     .= (x * x") * a * (x * x") by GROUP_1:def 4
                     .= 1.G * a * (x * x") by GROUP_1:def 6
                     .= a * (x * x") by GROUP_1:def 5
                     .= a * 1.G by GROUP_1:def 6
                     .= a by GROUP_1:def 5; then
              A21: g.(f.a) = a;
              g.(f.b) = g.(x" * b * x) by A1
                     .= x * (x" * b * x) * x" by A7,A20
                     .= x * (x" * b) * x * x" by GROUP_1:def 4
                     .= (x * x") * b * x * x" by GROUP_1:def 4
                     .= (x * x") * b * (x * x") by GROUP_1:def 4
                     .= 1.G * b * (x * x") by GROUP_1:def 6
                     .= b * (x * x") by GROUP_1:def 5
                     .= b * 1.G by GROUP_1:def 6
                     .= b by GROUP_1:def 5;
              hence thesis by A18,A21;
              end;
A,B are_equipotent
proof take f;
  thus thesis by A2,A3,A16,GROUP_6:1;
end;
hence thesis by CARD_1:21;
end;

definition let G, H, K;
  func Double_Cosets (H, K) -> Subset-Family of the carrier of G means
    A in it iff ex a st A = H * a * K;
existence
proof
  defpred P[set] means ex a st $1 = H * a * K;
  ex F being Subset-Family of the carrier of G st
  for A being Subset of the carrier of G holds
  A in F iff P[A] from SETFAM_1:sch 1;
  hence thesis;
end;
uniqueness
   proof let F1,F2 be Subset-Family of the carrier of G;
     defpred P[set] means ex a st $1 = H * a * K;
     assume A1: for A holds A in F1 iff P[A];
     assume A2: for A holds A in F2 iff P[A];
    thus thesis from GROUP_2:sch 1(A1,A2);
   end;
end;

theorem Th17:
  z in H * x * K iff ex g,h being Element of the carrier of G st
    z = g * x * h & g in H & h in K
proof
A1: z in H * x * K implies  ex g,h being Element of the carrier of G st
            z = g * x * h & g in H & h in K
proof
assume z in H * x * K;
       then consider g1, g2 being Element of the carrier of G such that
A2:               z = g1 * g2 & g1 in H * x & g2 in K by GROUP_2:114;
       consider h1 being Element of the carrier of G such that
A3:               g1 = h1 * x & h1 in H by A2,GROUP_2:126;
       thus thesis by A2,A3;
     end;
A4:  (ex g,h being Element of the carrier of G
           st z = g * x * h & g in H & h in K) implies z in H * x * K
           proof
             assume (ex g,h being Element of the carrier of G st
                     z = g * x * h & g in H & h in K);
             then consider g,h being Element of the carrier of G such that
A5:           z = g * x * h & g in H & h in K;
             ex g1, g2 being Element of the carrier of G st
             z = g1 * g2 & g1 in H * x & g2 in K
             proof
               set g2 = h;
               set g1 = g * x;
               g1 in H * x by A5,GROUP_2:126;
               hence thesis by A5;
              end;
              hence thesis by GROUP_2:114;
           end;
  thus thesis by A1,A4;
end;

theorem
  for H, K holds H * x * K = H * y * K or
    not ex z st z in H * x * K & z in H * y * K
proof
  let H, K;
  per cases;
  suppose not ex z st z in H * x * K & z in H * y * K;
  hence thesis;
  end;
  suppose ex z st z in H * x * K &  z in H * y * K;
  then consider z such that A1: z in H * x * K & z in H * y * K;
  consider h1, k1 being Element of the carrier of G such that
A2:z = h1 * x * k1 & h1 in H & k1 in K by A1,Th17;
  consider h2, k2 being Element of the carrier of G such that
A3:z = h2 * y * k2 & h2 in H & k2 in K by A1,Th17;
A4: H * x * K c= H * y * K
       proof
         for a be set st a in H * x * K holds a in H * y * K
         proof
           let a be set;
           assume a in H * x * K;
           then consider h,k being Element of the carrier of G such that
A5:                      a = h * x * k & h in H & k in K by Th17;
           ex c,d being Element of the carrier of G st
              a = c * y * d & c in H & d in K
              proof
           h = h * 1.G by GROUP_1:def 5; then
           A6: h * x * k = h * 1.G * x * 1.G * k by GROUP_1:def 5;
           1.G = h1" * h1 by GROUP_1:def 6; then
           h * x * k = h * (h1" * h1) * x * (k1 * k1") * k
                                             by A6,GROUP_1:def 6
                    .= h * (h1" * h1) * x * k1 * k1" * k by GROUP_1:def 4
                    .= h * h1" * h1 * x * k1 * k1" * k by GROUP_1:def 4
                    .= h * h1" * (h1 * x) * k1 * k1" * k by GROUP_1:def 4
                    .= h * h1" * (h2 * y * k2) * k1" * k
                      by A2,A3,GROUP_1:def 4
                    .= h * h1" * (h2 * y) * k2 * k1" * k by GROUP_1:def 4
                    .= (h * h1") * h2 * y * k2 * k1" * k by GROUP_1:def 4
                    .= (h * h1" * h2) * y * (k2 * k1") * k by GROUP_1:def 4
                    .= (h * h1" * h2) * y * (k2 * k1" * k)
                                            by GROUP_1:def 4; then
           A7: h * x * k = (h * h1" * h2) * y * (k2 * k1" * k);
              take c = h * h1" * h2;
              take d = k2 * k1" * k;
              A8: a = c * y * d by A5,A7;
              A9: h * h1" * h2 in H
                   proof
                     h in H & h1" in H by A2,A5,GROUP_2:60; then
                     h * h1" in H by GROUP_2:59;
                     hence thesis by A3,GROUP_2:59;
                   end;
             k2 * k1" * k in K
             proof
               k2 in K & k1" in K by A2,A3,GROUP_2:60; then
               k2 * k1" in K by GROUP_2:59;
               hence thesis by A5,GROUP_2:59;
             end;
             hence thesis by A8,A9;
           end;
           hence thesis by Th17;
         end;
         hence thesis by TARSKI:def 3;
         end;
     H * y * K c= H * x * K
     proof
       for a be set st
       a in H * y * K holds a in H * x * K
       proof
         let a be set;
         assume a in H * y * K;
         then consider h,k being Element of the carrier of G such that
A10:       a = h * y * k & h in H & k in K by Th17;
         ex c,d being Element of the carrier of G st
             a = c * x * d & c in H & d in K
         proof
         h = h * 1.G by GROUP_1:def 5; then
         A11: h * y * k = h * 1.G * y * 1.G * k by GROUP_1:def 5;
         1.G = h2" * h2 by GROUP_1:def 6; then
A12:       h * y * k = h * (h2" * h2) * y * (k2 * k2") * k
                                          by A11,GROUP_1:def 6
                  .= h * (h2" * h2) * y * k2 * k2" * k
                                          by GROUP_1:def 4
                  .= h * h2" * h2 * y * k2 * k2" * k
                                          by GROUP_1:def 4
                  .= h * h2" * (h2 * y) * k2 * k2" * k by GROUP_1:def 4
                  .= h * h2" * (h1 * x * k1) * k2" * k
                          by A2,A3,GROUP_1:def 4
                  .= h * h2" * (h1 * x) * k1 * k2" * k by GROUP_1:def 4
                  .= (h * h2") * h1 * x * k1 * k2" * k by GROUP_1:def 4
                  .= (h * h2" * h1) * x * (k1 * k2") * k by GROUP_1:def 4
                  .= (h * h2" * h1) * x * (k1 * k2" * k) by GROUP_1:def 4;
            take c = h * h2" * h1;
            take d = k1 * k2" * k;
            A13: a = c * x * d by A10,A12;
            A14: h * h2" * h1 in H
            proof
              h in H & h2" in H by A3,A10,GROUP_2:60; then
              h * h2" in H by GROUP_2:59;
              hence thesis by A2,GROUP_2:59;
            end;
            k1 * k2" * k in K
            proof
             k1 in K & k2" in K by A2,A3,GROUP_2:60; then
             k1 * k2" in K by GROUP_2:59;
             hence thesis by A10,GROUP_2:59;
             end;
          hence thesis by A13,A14;
            end;
         hence thesis by Th17;
       end;
    hence thesis by TARSKI:def 3;
    end;
  hence thesis by A4,XBOOLE_0:def 10;
end;
end;

 reserve C for strict Subgroup of G,
         B, A for strict Subgroup of G,
         D for strict Subgroup of A,
         E for strict Subgroup of B;

registration let G,A;
  cluster Left_Cosets A -> non empty;
  coherence by GROUP_2:165;
end;

notation let G;
  let H be Subgroup of G;
  synonym index (G,H) for index H;
end;

theorem Th19:
  G = A "\/" B & D = A /\ B & G is finite implies
    index(G,B) >= index(A,D)
proof
  assume A1: G = A "\/" B & D = A /\ B & G is finite; then
  reconsider LCB = Left_Cosets B as finite non empty set by GROUP_2:164;
  A is finite by A1,GROUP_2:48; then
  reconsider LCD = Left_Cosets D as finite non empty set by GROUP_2:164;
    reconsider DD = D as Subgroup of G;
A2: now let x,y be Element of G;
      let x',y' be Element of A such that
A3:   x' = x & y' = y;
A4:   y'" = y" by A3,GROUP_2:57;
A5:   y'"*x' in A by RLVECT_1:def 1;
      x*B = y*B iff y"*x in B by GROUP_2:137; then
      x*B = y*B iff y'"*x' in B by A3,A4,GROUP_2:52; then
      x*B = y*B iff y'"*x' in D by A1,A5,GROUP_2:99;
      hence x*B = y*B iff x'*D = y'*D by GROUP_2:137;
    end;
    defpred P[set, set] means
      ex a being Element of G, a' being Element of A st
          a = a' & $2 = a*B & $1 = a'*D;
A6:  for x being Element of LCD ex y being Element of LCB st P[x,y]
    proof
      let x be Element of LCD;
      x in LCD; then
      consider a' being Element of A such that
A7:   x = a'*D by GROUP_2:def 15;
      reconsider a = a' as Element of G by GROUP_2:51;
      reconsider y = a*B as Element of LCB by GROUP_2:def 15;
      take y,a,a'; thus thesis by A7;
    end;
    consider F being Function of LCD, LCB such that
A8:  for a being Element of LCD holds P[a,F.a] from FUNCT_2:sch 3(A6);
A9: dom F = LCD & rng F c= LCB by FUNCT_2:def 1,RELSET_1:12;
A10: index D = card LCD by GROUP_2:def 18;
A11: index B = card LCB by GROUP_2:def 18;
    F is one-to-one
    proof
      let x1,x2 be set;
      assume x1 in dom F & x2 in dom F; then
      reconsider z1 = x1, z2 = x2 as Element of LCD by A9;
      consider a being Element of G, a' being Element of A such that
A12:   a = a' & F.z1 = a*B & z1 = a'*D by A8;
      consider b being Element of G, b' being Element of A such that
A13:   b = b' & F.z2 = b*B & z2 = b'*D by A8;
      thus thesis by A2,A12,A13;
    end; then
    index D c= index B by A9,A10,A11,CARD_1:26;
   hence thesis by CARD_1:56;
end;

theorem Th20:
G is finite implies index (G,H) > 0
proof
assume A1: G is finite;then
ord G = ord H * index H by GROUP_2:177; then
index H <> 0 by A1,GROUP_1:90;
hence thesis by NAT_1:19;
end;

theorem
for G being strict Group st G is finite
for C being strict Subgroup of G
for A,B being strict Subgroup of C st C = A "\/" B
for D being strict Subgroup of A st D = A /\ B
for E being strict Subgroup of B st E = A /\ B
for F being strict Subgroup of C st F = A /\ B
holds
  Left_Cosets(B) is finite & Left_Cosets(A) is finite &
  index (C,A), index (C,B) are_relative_prime
implies index (C,B) = index (A,D) & index (C,A) = index (B,E)
proof
let G such that A1: G is finite;
let C be strict Subgroup of G;
let A,B be strict Subgroup of C such that A2: C = A "\/" B;
let D be strict Subgroup of A such that A3: D = A /\ B;
let E be strict Subgroup of B such that A4: E = A /\ B;
let F be strict Subgroup of C such that A5: F = A /\ B;
assume A6: Left_Cosets(B) is finite &
    Left_Cosets(A) is finite  & index A, index B are_relative_prime;
A7: C is finite & F = E & F = D by A1,A3,A4,A5,GROUP_2:48; then
  index F = index E * index B by GROUP_2:179; then
A8: index E * index B = index D * index A by A7,GROUP_2:179;
A9: (index B qua Integer) divides
          ((index A qua Integer) * (index D qua Integer))
             by A8,INT_1:def 9;
 index A, (index B qua Integer) are_relative_prime by A6,EULER_2:1; then
A10:(index B qua Integer) divides (index D qua Integer) by A9,INT_2:40;
A11:(index B qua Integer) divides index D by A10;
A12: index B divides index D
      proof
        ex n being Nat st index D = (index B) * n
           proof
             consider i being Integer such that
A13:           index D = (index B qua Integer) * i by A11,INT_1:def 9;
              A14: C is finite by A1,GROUP_2:48; then
              A15: index B >= 0 by Th20;
              0 <= i
                proof
                  assume 0 > i; then
                  A16: 0 >= i;
                  A is finite by A14,GROUP_2:48; then
                  index D > 0 by Th20;
                  hence contradiction by A13,A15,A16,REAL_2:123;
                  end;
             then reconsider i as Nat by INT_1:16;
             take n = i;
             thus thesis by A13;
             end;
        hence thesis by NAT_1:def 3;
        end;
A17: index(C,B) >= index(A,D) by A2,A3,A7,Th19;
A18: index B = index D
   proof
     assume A19: index B <> index D;
     A is finite by A1,GROUP_2:48;then
     index D > 0 by Th20;then
     index B <= index D by A12,NAT_1:54;
     hence contradiction by A17,A19,REAL_1:def 5;
     end;
A20: (index A qua Integer) divides
     ((index B qua Integer) * (index E qua Integer)) by A8,INT_1:def 9;
     index B, index A are_relative_prime
     proof
       for k being Nat st k divides index B & k divides index A holds k=1
         by A6,PYTHTRIP:def 1;
       hence thesis by PYTHTRIP:def 1;
     end; then
(index B qua Integer), (index A qua Integer) are_relative_prime
                         by EULER_2:1; then
A21: (index A qua Integer) divides index E by A20,INT_2:40;
A22:  index A divides index E
      proof
        ex n being Nat st index E = (index A) * n
           proof
             consider i being Integer such that
A23:           index E = (index A qua Integer) * i by A21,INT_1:def 9;
             0 <= i
                proof
                  assume 0 > i; then
                  A24: 0 >= i;
                  A25: C is finite by A1,GROUP_2:48; then
                  A26: index A >= 0 by Th20;
                  B is finite by A25,GROUP_2:48; then
                  index E > 0 by Th20;
                  hence contradiction by A23,A24,A26,REAL_2:123;
                  end;
             then reconsider i as Nat by INT_1:16;
             take n = i;
             thus thesis by A23;
             end;
        hence thesis by NAT_1:def 3;
        end;
A27: index A >= index E
   proof
     C = B "\/" A by A2,GROUP_4:74;
     hence thesis by A4,A7,Th19;
   end;
   index A = index E
   proof
     assume A28: index A <> index E;
     B is finite by A1,GROUP_2:48;then
     index E > 0 by Th20;then
     index A <= index E by A22,NAT_1:54;
     hence contradiction by A27,A28,REAL_1:def 5;
     end;
     hence thesis by A18;
end;

theorem Th22:
for a being Element of the carrier of G holds a in H implies
for j being Integer holds a |^ j in H
proof
let a be Element of the carrier of G;
assume A1: a in H;
let j be Integer;
consider k being Nat such that
A2:   j = k or j = - k by INT_1:8;
   per cases by A2;
   suppose A3: j =k;
   defpred P[Nat] means a |^ $1 in H;
   A4: P[0]
      proof
        a |^0 = 1.G by GROUP_1:43;
        hence thesis by GROUP_2:55;
      end;
   A5: for n being Nat st P[n] holds P[n + 1]
             proof
               let n be Nat such that A6: P[n];
               (a |^ n) * a in H by A1,A6,GROUP_2:59;
               hence thesis by GROUP_1:49;
               end;
   for n being Nat holds P[n] from NAT_1:sch 1(A4,A5);
   hence thesis by A3;
   end;
   suppose A7: j = - k;
   defpred PP[Nat] means a |^ (- $1) in H;
   A8: PP[0]
       proof
         a |^0 = 1.G by GROUP_1:43;
        hence thesis by GROUP_2:55;
         end;
   A9: for n being Nat st PP[n] holds PP[n +1 ]
         proof
           let n be Nat such that A10: PP[n];
           a" in H by A1,GROUP_2:60; then
           a |^(-n) * a" in H by A10,GROUP_2:59; then
           a |^(-n) * a |^ (-1) in H by GROUP_1:62; then
           a |^(- n + (- 1)) in H by GROUP_1:63;
         hence thesis;
       end;
   for n being Nat holds PP[n] from NAT_1:sch 1(A8,A9);
   hence thesis by A7;
   end;
  end;

 reserve a for Element of the carrier of G;

theorem Th23:
  for G being strict Group st G <> (1).G
    ex b being Element of the carrier of G st b <> 1.G
proof
  let G be strict Group such that A1: G <> (1).G;
  assume A2: not(ex b being Element of the carrier of G st b <> 1.G);
  G is trivial
  proof
    for x,y being Element of the carrier of G holds x = y
    proof
      let x,y be Element of the carrier of G;
      x = 1.G by A2;
      hence thesis by A2;
    end;
    hence thesis by REALSET2:def 7;
  end;
  hence contradiction by A1,GROUP_6:13;
end;

theorem Th24:
for G being strict Group
for a being Element of the carrier of G st G = gr{a} & G <> (1).G
for H being strict Subgroup of G st H <> (1).G holds
ex k being Nat st 0 < k & a |^k in H
proof
let G be strict Group;
let a be Element of the carrier of G such that A1: G = gr{a} & G <> (1).G;
let H be strict Subgroup of G such that A2: H <> (1).G;
A3: H <> (1).H by A2,GROUP_2:75;
consider b being Element of the carrier of H;
b in G by GROUP_2:50; then
reconsider b as Element of the carrier of G by RLVECT_1:def 1;
consider c being Element of the carrier of H such that
A4: c <> 1.H by A3,Th23;
A5: c in H by RLVECT_1:def 1; then
c in G by GROUP_2:49; then
reconsider c as Element of the carrier of G by RLVECT_1:def 1;
A6: c in gr{a} by A1,A5,GROUP_2:49;
ex j being Integer st c = a |^j & j <>0
    proof
      assume not(ex j being Integer st c = a |^j & j <> 0); then
      A7: for j being Integer st c = a |^j holds j = 0;
      consider i being Integer such that
      A8: c = a |^ i by A6,GR_CY_1:25;
      A9: i = 0 by A7,A8;
      a |^i <> 1.G by A4,A8,GROUP_2:53;
      hence contradiction by A9,GROUP_1:59;
    end; then consider j being Integer such that
A10:  c = a |^ j & j <> 0;
    consider n being Nat such that
A11:  j = n or j = -n by INT_1:8;
    per cases by A11;
      suppose A12: j = n;
      A13: c = a |^ n by A10,A12;
      0 < n by A10,A12,NAT_1:19;
      hence thesis by A5,A13;
      end;
      suppose A14: j = - n;
      n <> 0 by A10,A14; then
      A15: 0 < n by NAT_1:19;
      (a |^ n)" in H by A5,A10,A14,GROUP_1:71; then
      (a |^ n)"" in H by GROUP_2:60; then
      a |^ n in H by GROUP_1:19;
      hence thesis by A15;
    end;
end;

theorem
  for G being strict cyclic Group st G <> (1).G
  for H being strict Subgroup of G st H <> (1).G holds
  H is cyclic Group
proof
  let G be strict cyclic Group such that A1: G <> (1).G;
  let H be strict Subgroup of G such that A2: H <> (1).G;
  consider b being Element of the carrier of G such that
A3:the HGrStr of G = gr{b} by GR_CY_1:def 9;
  consider i being Integer;
  ex m being Nat st 0 < m & b |^m in H & for j being Nat st
    0 < j & b |^j in H holds m <= j
    proof
      defpred P[Nat] means 0 < $1 & b |^$1 in H;
  A4: ex k being Nat st P[k] by A1,A2,A3,Th24;
      ex m being Nat st P[m] & for j being Nat st P[j] holds m <= j
        from NAT_1:sch 5(A4);
      hence thesis;
    end; then
  consider m being Nat such that
A5: 0 < m & b |^m in H & for j being Nat st 0 < j & b |^ j in H holds m <= j;
set c = b |^ m;
reconsider c as Element of the carrier of H by A5,RLVECT_1:def 1;
A6: for a being Element of the carrier of H holds
ex i being Integer st a = c |^ i
       proof
         let a be Element of the carrier of H;
         ex t being Integer st b |^t in H & b|^t = a
         proof
           A7: a in G by GROUP_2:50; then
           A8: a in gr{b} by A3;
           a is Element of the carrier of G
                 by A7,RLVECT_1:def 1; then
           ex j1 being Integer st a = b|^j1
                 by A8,GR_CY_1:25; then
           consider j1 being Integer such that A9: a = b |^ j1;
           b |^ j1 in H by A9,RLVECT_1:def 1;
           hence thesis by A9;
         end;
         then consider t being Integer such that A10: b |^ t in H & b |^t = a;
         ex r,s being Integer st 0 <= s & s < m & t = m * r + s
         proof
           take r = t div m;
           take s = t mod m;
           A11: t = r * m + s by A5,NEWTON:80;
           m >= 0 by NAT_1:18; then
           A12: t mod m >= 0 by NEWTON:78;
           t mod m < m by A5,NEWTON:79;
           hence thesis by A11,A12;
         end; then consider r,s being Integer such that A13:
         0 <= s & s < m & t = m * r + s;
         reconsider s as Nat by A13,INT_1:16;
         A14: b |^ t = b |^ (m * r + s) by A13
      .= b |^(m *r) * (b |^s) by GROUP_1:63
      .= ((b |^m)|^ r) * (b |^ s) by GROUP_1:67;
      A15: (b |^m) |^r in H by A5,Th22;
      A16: b |^s in H
      proof
        set u = b |^t;
        set v = (b |^m) |^ r;
        A17: u = v * b|^s & u in H & v in H by A10,A14,A15; then
        A18: v" * u = (v" * v) * b|^s by GROUP_1:def 4
           .= 1.G * b |^ s by GROUP_1:def 6
           .= b |^ s by GROUP_1:def 5;
           v" in H by A17,GROUP_2:60;
           hence thesis by A17,A18,GROUP_2:59;
         end;
   b |^ s = 1.G
   proof
     s = 0 by A5,A13,A16;
     hence thesis by GROUP_1:43;
   end; then
   b |^ t = (b |^ m) |^r by A14,GROUP_1:def 5;then
A19:a = (b|^m) |^ r by A10;
    (b |^m)|^ r = (c) |^ r by Th4;
    hence thesis by A19;
  end;
  H = gr{c} by A6,GR_CY_2:11;
  hence thesis by GR_CY_2:10;
end; 
