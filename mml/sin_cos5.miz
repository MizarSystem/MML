:: Formulas And Identities of Trigonometric Functions
::  by Yuzhong Ding and Xiquan Liang
:: 
:: Received March 18, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabulary SIN_COS4, ARYTM_3, SIN_COS, ARYTM_1, SQUARE_1, GROUP_1, ABSVALUE,
      SIN_COS2, SIN_COS5, ORDINAL2, FUNCT_1;
 notation TARSKI, XCMPLX_0, XREAL_0, REAL_1, SQUARE_1, SIN_COS4, ABSVALUE,
      SIN_COS, NEWTON, RAT_1, NAT_1, SIN_COS2, SEQ_1;
 constructors REAL_1, SQUARE_1, PREPOWER, SIN_COS4, SIN_COS, MEMBERED, NEWTON,
      INT_1, NAT_1, ABSVALUE, SIN_COS2, COMSEQ_3, SEQ_2;
 clusters INT_1, COMSEQ_2, COMSEQ_3, XREAL_0, RELSET_1, FDIFF_1, RCOMP_1,
      SEQ_1, COMSEQ_1, SEQM_3, NEWTON, COMPLEX1, MEMBERED, ORDINAL2, NUMBERS,
      SIN_COS;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 theorems XCMPLX_1, SIN_COS, SIN_COS4, SQUARE_1, NEWTON, ABSVALUE, SIN_COS2,
      REAL_1, REAL_2;

begin

reserve p,q,r,th, th1, th2, th3 for Real,
         k,m for Nat;
theorem Th1:
    cos(th)<>0 implies cosec(th)=sec(th)/tan(th)
proof
    assume A1:cos(th)<>0;
    sec(th)/tan(th)=(1/cos(th))/tan(th) by SIN_COS4:def 4
    .=(1/cos(th))/(sin(th)/cos(th)) by SIN_COS4:def 1
    .=((1/cos(th))*cos(th))/((sin(th)/cos(th))*cos(th)) by XCMPLX_1:92,A1
    .=1/((sin(th)/cos(th))*cos(th)) by XCMPLX_1:88,A1
    .=1/sin(th) by XCMPLX_1:88,A1;
  hence thesis by SIN_COS4:def 3;
end;
theorem Th4:
  sin(th)<>0 implies cos(th) = sin(th)*cot(th)
proof
  assume A1: sin(th)<>0;
  cos(th)= (sin(th)/sin(th))*cos(th) by XCMPLX_1:89,A1
  .= sin(th)*(cos(th)/sin(th)) by XCMPLX_1:76
  .= sin(th)*cot(th) by SIN_COS4:def 2;
  hence thesis;
end;

theorem 
sin(th1)<>0 & sin(th2)<>0  & sin(th3)<>0 implies
sin(th1+th2+th3)=sin(th1)*sin(th2)*sin(th3)*(cot(th2)*cot(th3)+
                 cot(th1)*cot(th3)+cot(th1)*cot(th2)-1)
proof
    assume
 A1:sin(th1)<>0 & sin(th2)<>0  & sin(th3)<>0 ;
    sin(th1+th2+th3) = sin(th1+(th2+th3)) 
  .=sin(th1)*cos(th2+th3)+cos(th1)*sin(th2+th3) by SIN_COS:80
  .=sin(th1)*(cos(th2)*cos(th3)-sin(th2)*sin(th3)) + cos(th1)*sin(th2+th3)
  by SIN_COS:80
  .=sin(th1)*(cos(th2)*cos(th3)-sin(th2)*sin(th3))
  +cos(th1)*(sin(th2)*cos(th3)+cos(th2)*sin(th3)) by SIN_COS:80
  .=sin(th1)*(cos(th2)*cos(th3))-sin(th1)*(sin(th2)*sin(th3))
  +cos(th1)*(sin(th2)*cos(th3)+cos(th2)*sin(th3)) 
  .=(sin(th1))*(cos(th2)*cos(th3))-sin(th1)*(sin(th2)*sin(th3))
  +(cos(th1)*(sin(th2)*cos(th3))+cos(th1)*(cos(th2)*sin(th3))) 
  .=(sin(th1))*(cos(th2)*cos(th3))-sin(th1)*(sin(th2)*sin(th3))
  +(cos(th1)*(sin(th2)*cos(th3))+cos(th1)*(cos(th2)*sin(th3))) 
  .=sin(th1)*cos(th2)*cos(th3)-sin(th1)*sin(th2)*sin(th3)
  +(cos(th1)*sin(th2)*cos(th3)+cos(th1)*cos(th2)*sin(th3)) 
  .=sin(th1)*(sin(th2)*cot(th2))*cos(th3)-sin(th1)*sin(th2)*sin(th3)
  +(cos(th1)*sin(th2)*cos(th3)+cos(th1)*cos(th2)*sin(th3)) by Th4,A1
  .=sin(th1)*(sin(th2)*cot(th2))*(sin(th3)*cot(th3))-sin(th1)*sin(th2)
  *sin(th3)+(cos(th1)*sin(th2)*cos(th3)+cos(th1)*cos(th2)*sin(th3)) by Th4,A1
  .=sin(th1)*sin(th2)*cot(th2)*sin(th3)*cot(th3)-sin(th1)*sin(th2)*sin(th3
   )+(cos(th1)*sin(th2)*cos(th3)+cos(th1)*cos(th2)*sin(th3)) 
  .=sin(th1)*sin(th2)*(cot(th2)*sin(th3))*cot(th3)-sin(th1)*sin(th2)*sin(th3
   )+(cos(th1)*sin(th2)*cos(th3)+cos(th1)*cos(th2)*sin(th3)) 
  .=(sin(th1)*sin(th2)*sin(th3))*cot(th2)*cot(th3)-sin(th1)*sin(th2)*sin(th3
   )*1+(cos(th1)*sin(th2)*cos(th3)+cos(th1)*cos(th2)*sin(th3)) 

  .=(sin(th1)*sin(th2)*sin(th3))*(cot(th2)*cot(th3)-1)+(cos(th1)*sin(th2)
     *cos(th3)+cos(th1)*cos(th2)*sin(th3)) 
  .=(sin(th1)*sin(th2)*sin(th3))*(cot(th2)*cot(th3)-1)+((sin(th1)*cot(th1)
    )*sin(th2)*cos(th3)+cos(th1)*cos(th2)*sin(th3)) by Th4,A1
  .=(sin(th1)*sin(th2)*sin(th3))*(cot(th2)*cot(th3)-1)+((sin(th1)*cot(th1)
    )*sin(th2)*(sin(th3)*cot(th3))+cos(th1)*cos(th2)*sin(th3)) by Th4,A1
  .=(sin(th1)*sin(th2)*sin(th3))*(cot(th2)*cot(th3)-1)+(sin(th1)*cot(th1)
    *sin(th2)*sin(th3)*cot(th3)+cos(th1)*cos(th2)*sin(th3))
  .=(sin(th1)*sin(th2)*sin(th3))*(cot(th2)*cot(th3)-1)+(sin(th1)*(cot(th1)
    *(sin(th2)*sin(th3)))*cot(th3)+cos(th1)*cos(th2)*sin(th3)) 
  .=(sin(th1)*sin(th2)*sin(th3))*(cot(th2)*cot(th3)-1)+(sin(th1)*sin(th2
    )*sin(th3)*cot(th1)*cot(th3)+cos(th1)*cos(th2)*sin(th3)) 
  .=(sin(th1)*sin(th2)*sin(th3))*(cot(th2)*cot(th3)-1)+sin(th1)*sin(th2
    )*sin(th3)*cot(th1)*cot(th3)+cos(th1)*cos(th2)*sin(th3)
  .=(sin(th1)*sin(th2)*sin(th3))*((cot(th2)*cot(th3)-1)+cot(th1)*cot(th3))
    +cos(th1)*cos(th2)*sin(th3)
  .=(sin(th1)*sin(th2)*sin(th3))*((cot(th2)*cot(th3)-1)+cot(th1)*cot(th3))
    +(sin(th1)*cot(th1))*cos(th2)*sin(th3) by Th4,A1
  .=(sin(th1)*sin(th2)*sin(th3))*((cot(th2)*cot(th3)-1)+cot(th1)*cot(th3))
    +(sin(th1)*cot(th1))*(sin(th2)*cot(th2))*sin(th3) by Th4,A1
  .=(sin(th1)*sin(th2)*sin(th3))*((cot(th2)*cot(th3)-1)+cot(th1)*cot(th3))
    +sin(th1)*cot(th1)*sin(th2)*cot(th2)*sin(th3) 
  .=(sin(th1)*sin(th2)*sin(th3))*((cot(th2)*cot(th3)-1)+cot(th1)*cot(th3))
    +sin(th1)*(cot(th1)*sin(th2))*(cot(th2)*sin(th3)) 
  .=(sin(th1)*sin(th2)*sin(th3))*((cot(th2)*cot(th3)-1)+cot(th1)*cot(th3))
    +sin(th1)*sin(th2)*sin(th3)*cot(th1)*cot(th2) 
  .=(sin(th1)*sin(th2)*sin(th3))*(((cot(th2)*cot(th3)-1)+cot(th1)*cot(th3))
    +cot(th1)*cot(th2)) 
  .=(sin(th1)*sin(th2)*sin(th3))*(((cot(th2)*cot(th3)-1)+(cot(th1)*cot(th3))
    +cot(th1)*cot(th2))) 
  .=(sin(th1)*sin(th2)*sin(th3))*(cot(th2)*cot(th3)+cot(th1)*cot(th3)
    +cot(th1)*cot(th2)-1) ;
   hence thesis;
end;

theorem 
sin(th1)<>0 & sin(th2)<>0  & sin(th3)<>0 implies
cos(th1+th2+th3)=-sin(th1)*sin(th2)*sin(th3)*(cot(th1)+cot(th2)+cot(th3)-
                 cot(th1)*cot(th2)*cot(th3))
proof
    assume
 A1:sin(th1)<>0 & sin(th2)<>0  & sin(th3)<>0;
    cos(th1+th2+th3)=cos(th1+(th2+th3)) 
  .=cos(th1)*cos(th2+th3)-sin(th1)*sin(th2+th3) by SIN_COS:80
  .=cos(th1)*(cos(th2)*cos(th3)-sin(th2)*sin(th3))-sin(th1)*sin(th2+th3) 
           by SIN_COS:80
  .=cos(th1)*(cos(th2)*cos(th3)-sin(th2)*sin(th3))-sin(th1)*(sin(th2)*cos(th3)
    +cos(th2)*sin(th3)) by SIN_COS:80
  .=cos(th1)*(cos(th2)*cos(th3))-cos(th1)*(sin(th2)*sin(th3))
    -(sin(th1))*(sin(th2)*cos(th3)+cos(th2)*sin(th3)) 
  .=cos(th1)*cos(th2)*cos(th3)-cos(th1)*sin(th2)*sin(th3)
    -sin(th1)*(sin(th2)*cos(th3)+cos(th2)*sin(th3)) 
  .=cos(th1)*cos(th2)*cos(th3)-cos(th1)*sin(th2)*sin(th3)-(sin(th1)
    *(sin(th2)*cos(th3))+sin(th1)*(cos(th2)*sin(th3))) 
  .=cos(th1)*cos(th2)*cos(th3)-cos(th1)*sin(th2)*sin(th3)-(sin(th1)
    *sin(th2)*cos(th3)+sin(th1)*sin(th3)*cos(th2)) 
  .=cos(th1)*cos(th2)*cos(th3)-cos(th1)*sin(th2)*sin(th3)-(sin(th1)
    *sin(th2)*(sin(th3)*cot(th3))+sin(th1)*sin(th3)*cos(th2)) by Th4,A1
  .=cos(th1)*cos(th2)*cos(th3)-cos(th1)*sin(th2)*sin(th3)-(sin(th1)
    *sin(th2)*(sin(th3)*cot(th3))+sin(th1)*sin(th3)*(sin(th2)*cot(th2))) 
      by Th4,A1
  .=cos(th1)*cos(th2)*cos(th3)-cos(th1)*sin(th2)*sin(th3)-(sin(th1)
    *sin(th2)*sin(th3)*cot(th3)+sin(th1)*sin(th2)*sin(th3)*cot(th2)) 
  .=cos(th1)*cos(th2)*cos(th3)-cos(th1)*sin(th2)*sin(th3)-(sin(th1)
    *sin(th2)*sin(th3))*(cot(th3)+cot(th2))  
  .=sin(th1)*cot(th1)*cos(th2)*cos(th3)-cos(th1)*sin(th2)*sin(th3)-(sin(th1)
    *sin(th2)*sin(th3))*(cot(th3)+cot(th2))  by Th4,A1
  .=sin(th1)*cot(th1)*(sin(th2)*cot(th2))*cos(th3)-cos(th1)*sin(th2)
   *sin(th3)-(sin(th1)*sin(th2)*sin(th3))*(cot(th3)+cot(th2))  by Th4,A1
  .=sin(th1)*cot(th1)*(sin(th2)*cot(th2))*(sin(th3)*cot(th3))-cos(th1)
   *sin(th2)*sin(th3)-(sin(th1)*sin(th2)*sin(th3))*(cot(th3)+cot(th2)) 
    by Th4,A1
  .=sin(th1)*sin(th2)*sin(th3)*(cot(th1)*cot(th2)*cot(th3))-cos(th1)
   *sin(th2)*sin(th3)-(sin(th1)*sin(th2)*sin(th3))*(cot(th3)+cot(th2)) 
  .=sin(th1)*sin(th2)*sin(th3)*(cot(th1)*cot(th2)*cot(th3))-sin(th1)*cot(th1)
   *sin(th2)*sin(th3)-(sin(th1)*sin(th2)*sin(th3))*(cot(th3)+cot(th2)) 
    by Th4,A1
  .=sin(th1)*sin(th2)*sin(th3)*(cot(th1)*cot(th2)*cot(th3))-sin(th1)*sin(th2)
   *sin(th3)*cot(th1)-(sin(th1)*sin(th2)*sin(th3))*(cot(th3)+cot(th2)) 
  .= -(-(sin(th1)*sin(th2)*sin(th3)*(cot(th1)*cot(th2)*cot(th3)))+sin(th1)
     *sin(th2)*sin(th3)*cot(th1)+(sin(th1)*sin(th2)*sin(th3))*(cot(th3)
     +cot(th2))) 
  .=-(sin(th1)*sin(th2)*sin(th3)*(-(cot(th1)*cot(th2)*cot(th3)))+sin(th1)
     *sin(th2)*sin(th3)*cot(th1)+(sin(th1)*sin(th2)*sin(th3))*(cot(th3)
     +cot(th2))) 
  .=-(sin(th1)*sin(th2)*sin(th3)*((-(cot(th1)*cot(th2)*cot(th3)))+cot(th1)+
    (cot(th3)+cot(th2)))) 
  .=-(sin(th1)*sin(th2)*sin(th3)*((-(cot(th1)*cot(th2)*cot(th3)))+cot(th1)+
    (cot(th3)+cot(th2)))) 
  .=-(sin(th1)*sin(th2)*sin(th3)*(cot(th1)+(cot(th3)+cot(th2))-cot(th1)
     *cot(th2)*cot(th3)))  
  .=-(sin(th1)*sin(th2)*sin(th3))*(cot(th1)+cot(th2)+cot(th3)-cot(th1)
     *cot(th2)*cot(th3)) 
  .=-sin(th1)*sin(th2)*sin(th3)*(cot(th1)+cot(th2)+cot(th3)-cot(th1)
     *cot(th2)*cot(th3)) ;
  hence thesis;
end;

theorem Th7:
 sin(2*th) = 2*sin(th)*cos(th)
proof
 sin(2*th)=sin(th+th) 
         .=sin(th)*cos(th)+cos(th)*sin(th) by SIN_COS:80
         .=2*(sin(th)*cos(th)) 
         .=2*sin(th)*cos(th) ;
 hence thesis;
end;

theorem Th8:
 cos(th)<>0 implies
 sin(2*th)=(2*tan(th))/(1+(tan(th))^2)
proof
  assume
 A1: cos(th)<>0;then
 A3:(cos(th))^2<>0 by SQUARE_1:74; 
   sin(2*th)=2*sin(th)*cos(th)*1 by Th7
   .=2*sin(th)*cos(th)*(cos(th)/cos(th)) by XCMPLX_1:60,A1
   .=2*sin(th)*cos(th)*cos(th)/cos(th) by XCMPLX_1:75
   .=2*sin(th)*(cos(th)*cos(th))/cos(th)
   .=2*sin(th)*(cos(th))^2/cos(th) by SQUARE_1:def 3
   .=2*(cos(th))^2*sin(th)/cos(th) 
   .=2*(cos(th))^2*(sin(th)/cos(th)) by XCMPLX_1:75
   .=2*(cos(th))^2*tan(th)/1 by SIN_COS4:def 1
   .=2*(cos(th))^2*tan(th)/((sin(th))^2+(cos(th))^2) by SIN_COS:32
   .=2*tan(th)*(cos(th))^2/((sin(th))^2+(cos(th))^2) 
   .=2*tan(th)/(((sin(th))^2+(cos(th))^2)/(cos(th))^2) by XCMPLX_1:78
   .=2*tan(th)/((sin(th))^2/(cos(th))^2+(cos(th))^2/(cos(th))^2) 
         by XCMPLX_1:63
   .=2*tan(th)/((sin(th))^2/(cos(th))^2+1)   by XCMPLX_1:60,A3
   .=2*tan(th)/((sin(th)/cos(th))^2+1)   by SQUARE_1:69
   .=2*tan(th)/((tan(th))^2+1)   by SIN_COS4:def 1;
   hence thesis;
end;

theorem Th9:
    cos(2*th)=(cos(th))^2-(sin(th))^2 &
    cos(2*th)=2*(cos(th))^2-1 &
    cos(2*th)=1-2*(sin(th))^2
proof
  A1:cos(2*th) = cos(th+th) 
     .=cos(th)*cos(th)-sin(th)*sin(th) by SIN_COS:80
     .=(cos(th))^2 -sin(th)*sin(th) by SQUARE_1:def 3
     .=(cos(th))^2 -(sin(th))^2 by SQUARE_1:def 3;
  A3:cos(2*th)=(cos(th))^2 -(sin(th))^2+1-1 by A1
     .=(cos(th))^2 -(sin(th))^2+((cos(th))^2+(sin(th))^2)-1 by SIN_COS:32
     .=(cos(th))^2 -(sin(th))^2+(sin(th))^2+(cos(th))^2-1 
     .=(cos(th))^2+(cos(th))^2-1 
     .=2*(cos(th))^2-1 ;
     cos(2*th)=(cos(th))^2 -(sin(th))^2-1+1 by A1
     .=(cos(th))^2 -(sin(th))^2-((cos(th))^2+(sin(th))^2)+1 by SIN_COS:32
     .=(cos(th))^2 -(sin(th))^2-(sin(th))^2-(cos(th))^2+1
     .=(cos(th))^2 -((sin(th))^2+(sin(th))^2)-(cos(th))^2+1 
     .=(cos(th))^2 -2*(sin(th))^2-(cos(th))^2+1 
     .=1+ -2*(sin(th))^2 
     .=-(-1+ 2*(sin(th))^2) ;
   hence thesis by A1,A3;
end;

theorem Th10:
  cos(th)<>0 implies
  cos(2*th)=(1-(tan(th))^2)/(1+(tan(th))^2)
proof
    assume
    cos(th)<>0 ;then
 A1:(cos(th))^2<>0 by SQUARE_1:74; 
    cos(2*th) = ((cos(th))^2-(sin(th))^2)*1 by Th9
    .=((cos(th))^2-(sin(th))^2)*((cos(th))^2/(cos(th))^2) by XCMPLX_1:60,A1
    .=((cos(th))^2-(sin(th))^2)/(cos(th))^2*(cos(th))^2 by XCMPLX_1:76
    .=((cos(th))^2/(cos(th))^2-(sin(th))^2/(cos(th))^2)*(cos(th))^2 
        by XCMPLX_1:121
    .=(1-(sin(th))^2/(cos(th))^2)*(cos(th))^2     by XCMPLX_1:60,A1
    .=(1-(sin(th)/cos(th))^2)*(cos(th))^2     by SQUARE_1:69
    .=(1-(tan(th))^2)*(cos(th))^2/1     by SIN_COS4:def 1
    .=(1-(tan(th))^2)*(cos(th))^2/((cos(th))^2+(sin(th))^2) by SIN_COS:32
    .=(1-(tan(th))^2)/(((cos(th))^2+(sin(th))^2)/(cos(th))^2) by XCMPLX_1:78
    .=(1-(tan(th))^2)/(((cos(th))^2/(cos(th))^2+(sin(th))^2/(cos(th))^2)) 
        by XCMPLX_1:63
    .=(1-(tan(th))^2)/((1+(sin(th))^2/(cos(th))^2))  by XCMPLX_1:60,A1
    .=(1-(tan(th))^2)/((1+(sin(th)/cos(th))^2))  by SQUARE_1:69
    .=(1-(tan(th))^2)/((1+(tan(th))^2))  by SIN_COS4:def 1;
   hence thesis;
end;

theorem 
    cos(th)<>0 implies
    tan(2*th)=(2*tan(th))/(1-(tan(th))^2)
proof
    assume
 A1: cos(th)<>0;
    tan(2*th) = tan(th+th) 
    .=(tan(th)+tan(th))/(1-tan(th)*tan(th)) by SIN_COS4:11,A1
    .=(2*tan(th))/(1-tan(th)*tan(th)) ;
   hence thesis by SQUARE_1:def 3;
end;

theorem 
 sin(th)<>0 implies
 cot(2*th)=((cot(th))^2-1)/(2*cot(th))
proof
 assume
 A:sin(th)<>0;
   cot(2*th)=cot(th+th) 
    .=(cot(th)*cot(th)-1)/(cot(th)+cot(th)) by SIN_COS4:13,A
    .=(cot(th)*cot(th)-1)/(2*cot(th));
   hence thesis by SQUARE_1:def 3;
end;
 
theorem Th13:
 cos(th)<>0 implies (sec(th))^2 = 1 +(tan(th))^2
proof
 assume cos(th)<>0 ;then
 A:(cos(th))^2 <>0 by  SQUARE_1:74; 
 (sec(th))^2=(1/cos(th))^2 by SIN_COS4:def 4
 .=1^2/(cos(th))^2 by SQUARE_1:69
 .=1/(cos(th))^2 by SQUARE_1:59
 .=((sin(th))^2+(cos(th))^2)/(cos(th))^2 by SIN_COS:32
 .=(sin(th))^2/(cos(th))^2+(cos(th))^2/(cos(th))^2 by XCMPLX_1:63
 .=(sin(th))^2/(cos(th))^2+1 by XCMPLX_1:60,A
 .=(sin(th)/cos(th))^2+1 by SQUARE_1:69;
 hence thesis by SIN_COS4:def 1;
end;

theorem Th14:
  cot(th) = 1/tan(th)
proof
  cot(th) = cos(th)/sin(th) by SIN_COS4:def 2
  .=1/(sin(th)/cos(th)) by XCMPLX_1:57
  .=1/tan(th) by SIN_COS4:def 1;
  hence thesis;
end;

theorem Th15:
    cos(th)<>0 & sin(th)<>0 implies 
         sec(2*th) = (sec(th))^2/(1-(tan(th))^2) &
         sec(2*th)=(cot(th)+tan(th))/(cot(th)-tan(th))
proof
 assume
 A1:cos(th)<>0 & sin(th)<>0;then
    sin(th)/cos(th)<>0 by XCMPLX_1:50 ;then
 A2:tan(th)<>0 by SIN_COS4:def 1;
 A3:sec(2*th)=1/(cos(2*th)) by SIN_COS4:def 4
    .=1/((1-(tan(th))^2)/(1+(tan(th))^2)) by A1,Th10
    .=(1+(tan(th))^2)/(1-(tan(th))^2) by XCMPLX_1:57
    .=((sec(th))^2)/(1-(tan(th))^2) by Th13,A1;
    sec(2*th)=((sec(th))^2)/(1-(tan(th))^2) by A3
    .=(1+(tan(th))^2)/(1-(tan(th))^2) by Th13,A1
    .=((1+(tan(th))^2)/tan(th))/((1-(tan(th))^2)/tan(th)) by XCMPLX_1:55,A2
    .=(1/tan(th)+(tan(th))^2/tan(th))/((1-(tan(th))^2)/tan(th)) by XCMPLX_1:63
    .=(cot(th)+(tan(th))^2/tan(th))/((1-(tan(th))^2)/tan(th)) by Th14
    .=(cot(th)+(tan(th))^2/tan(th))/(1/tan(th)-(tan(th))^2/tan(th)) 
       by XCMPLX_1:121
    .=(cot(th)+(tan(th))^2/tan(th))/(cot(th)-(tan(th))^2/tan(th)) by Th14
    .=(cot(th)+tan(th)*tan(th)/tan(th))/(cot(th)-(tan(th))^2/tan(th))
          by SQUARE_1:def 3
    .=(cot(th)+tan(th)*tan(th)/tan(th))/(cot(th)-tan(th)*tan(th)/tan(th))
          by SQUARE_1:def 3
    .=(cot(th)+tan(th))/(cot(th)-tan(th)*tan(th)/tan(th)) by XCMPLX_1:90,A2;
    hence thesis by XCMPLX_1:90,A2,A3;
end;

theorem 
  sin(th)<>0 implies (cosec(th))^2 = 1 +(cot(th))^2
proof
 assume sin(th)<>0 ;then
 A:(sin(th))^2 <>0 by  SQUARE_1:74; 
 (cosec(th))^2=(1/sin(th))^2 by SIN_COS4:def 3
 .=1^2/(sin(th))^2 by SQUARE_1:69
 .=1/(sin(th))^2 by SQUARE_1:59
 .=((sin(th))^2+(cos(th))^2)/(sin(th))^2 by SIN_COS:32
 .=(sin(th))^2/(sin(th))^2+(cos(th))^2/(sin(th))^2 by XCMPLX_1:63
 .=1+(cos(th))^2/(sin(th))^2by XCMPLX_1:60,A
 .=1+(cos(th)/sin(th))^2 by SQUARE_1:69;
 hence thesis by SIN_COS4:def 2;
end;

theorem 
   cos(th)<>0 & sin(th)<>0 implies
   cosec(2*th)=sec(th)*cosec(th)/2 &
   cosec(2*th)=(tan(th)+cot(th))/2
proof
   assume
 A1:cos(th)<>0 & sin(th)<>0;then
    sin(th)/cos(th)<>0 by XCMPLX_1:50 ;then
 A2:tan(th)<>0 by SIN_COS4:def 1;    
 A3:cosec(2*th)=1/sin(2*th) by SIN_COS4:def 3
   .=1/((2*tan(th))/(1+(tan(th))^2)) by A1,Th8
   .=(1+(tan(th))^2)/(2*tan(th)) by XCMPLX_1:57
   .=(1+(tan(th))^2)/tan(th)/2 by XCMPLX_1:79
   .=(1/tan(th)+(tan(th))^2/tan(th))/2 by XCMPLX_1:63
   .=(1/(sin(th)/cos(th))+(tan(th))^2/tan(th))/2 by SIN_COS4:def 1
   .=(cos(th)/sin(th)+(tan(th))^2/tan(th))/2 by XCMPLX_1:57
   .=(cot(th)+(tan(th))^2/tan(th))/2 by SIN_COS4:def 2
   .=(cot(th)+tan(th)*tan(th)/tan(th))/2 by SQUARE_1:def 3
   .=(cot(th)+tan(th))/2 by XCMPLX_1:90,A2;
   cosec(2*th)=1/sin(2*th) by SIN_COS4:def 3
   .=1/((2*tan(th))/(1+(tan(th))^2)) by A1,Th8
   .=(1+(tan(th))^2)/(2*tan(th)) by XCMPLX_1:57
   .=(sec(th))^2/(2*tan(th)) by Th13,A1
   .=sec(th)*sec(th)/(2*tan(th)) by SQUARE_1:def 3
   .=sec(th)*sec(th)/tan(th)/2 by XCMPLX_1:79
   .=sec(th)*(sec(th)/tan(th))/2 by XCMPLX_1:75
   .=sec(th)*(sec(th)/(sin(th)/cos(th)))/2 by SIN_COS4:def 1
   .=sec(th)*(sec(th)*cos(th)/sin(th))/2 by XCMPLX_1:78
   .=sec(th)*(1/sin(th))/2 by SIN_COS4:5,A1
   .=sec(th)*cosec(th)/2 by SIN_COS4:def 3;
  hence thesis by A3;
end;

theorem Th18:
   sin(3*th)=-4*(sin(th))|^3+3*sin(th)
proof
 sin(3*th)=sin((th+th)+th) 
 .=sin(th+th)*cos(th)+cos(th+th)*sin(th) by SIN_COS:80
 .=sin(2*th)*cos(th)+cos(th+th)*sin(th) 
 .=sin(2*th)*cos(th)+cos(2*th)*sin(th) 
 .=(2*sin(th)*cos(th))*cos(th)+cos(2*th)*sin(th) by Th7
 .=(2*sin(th)*cos(th))*cos(th)+(1-2*(sin(th))^2)*sin(th) by Th9
 .=2*sin(th)*(cos(th)*cos(th))+(1-2*(sin(th))^2)*sin(th) 
 .=2*sin(th)*(1-sin(th)*sin(th))+(1-2*(sin(th))^2)*sin(th) by SIN_COS4:7
 .=2*sin(th)*(1-(sin(th))^2)+(1-2*(sin(th))^2)*sin(th) by SQUARE_1:def 3
 .=2*sin(th)*1-2*sin(th)*(sin(th))^2+(1-2*(sin(th))^2)*sin(th) 
 .=2*sin(th)*1-2*sin(th)*(sin(th))^2+(1*sin(th)-2*(sin(th))^2*sin(th)) 
 .=2*sin(th)*1-2*sin(th)*(sin(th)*sin(th))+(1*sin(th)-2*(sin(th))^2*sin(th)) 
       by SQUARE_1:def 3
 .=2*sin(th)*1-2*(sin(th))|^1*(sin(th)*sin(th))
   +(1*sin(th)-2*(sin(th))^2*sin(th))  by NEWTON:10
 .=2*sin(th)*1-2*(sin(th))|^1*sin(th)*sin(th)
    +(1*sin(th)-2*(sin(th))^2*sin(th)) 
 .=2*sin(th)*1-2*((sin(th))|^1*(sin(th)))*sin(th)
    +(1*sin(th)-2*(sin(th))^2*sin(th)) 
 .=2*sin(th)*1-2*((sin(th))|^(1+1))*sin(th)
   +(1*sin(th)-2*(sin(th))^2*sin(th))  by NEWTON:11
 .=2*sin(th)*1-2*((sin(th))|^2*sin(th))
   +(1*sin(th)-2*(sin(th))^2*sin(th))  by XCMPLX_1:4
 .=2*sin(th)*1-2*(sin(th))|^(2+1)
   +(1*sin(th)-2*(sin(th))^2*sin(th))  by NEWTON:11
 .=2*sin(th)-2*(sin(th))|^3+(sin(th)-2*
     (sin(th)*sin(th))*sin(th))  by SQUARE_1:def 3
 .=2*sin(th)-2*(sin(th))|^3+(sin(th)-2*
     ((sin(th))|^1*sin(th))*sin(th))  by NEWTON:10
 .=2*sin(th)-2*(sin(th))|^3+(sin(th)-2*((sin(th))|^(1+1))*sin(th)) 
     by NEWTON:11
 .=2*sin(th)-2*(sin(th))|^3+(sin(th)-2*((sin(th))|^(1+1)*sin(th)))
 .=2*sin(th)-2*(sin(th))|^3+(sin(th)-2*((sin(th))|^(2+1))) by NEWTON:11
 .=2*sin(th)+(sin(th)-2*((sin(th))|^3))-2*(sin(th))|^3 by XCMPLX_1:29
 .=2*sin(th)-(2*((sin(th))|^3)-sin(th))-2*(sin(th))|^3
 .=2*sin(th)-2*((sin(th))|^3)+sin(th)-2*(sin(th))|^3 
 .=2*sin(th)+1*sin(th)-2*((sin(th))|^3)-2*(sin(th))|^3 
 .=(2+1)*sin(th)-2*((sin(th))|^3)-2*(sin(th))|^3 
 .=3*sin(th)-(2*((sin(th))|^3)+2*(sin(th))|^3) 
 .=3*sin(th)-(2+2)*(sin(th))|^3 
 .=-(-3*sin(th)+4*(sin(th))|^3);
 hence thesis ;
end;

theorem Th19:
   cos(3*th)=4*(cos(th))|^3-3*cos(th)
proof
 cos(3*th)=cos((th+th)+th) 
 .=cos(th+th)*cos(th)-sin(th+th)*sin(th) by SIN_COS:80
 .=cos(2*th)*cos(th)-sin(th+th)*sin(th) 
 .=cos(2*th)*cos(th)-sin(2*th)*sin(th)
 .=(2*(cos(th))^2-1)*cos(th)-sin(2*th)*sin(th) by Th9
 .=2*(cos(th))^2*cos(th)-1*cos(th)-sin(2*th)*sin(th) 
 .=2*((cos(th))^2*cos(th))-1*cos(th)-sin(2*th)*sin(th) 
 .=2*(cos(th)*cos(th)*cos(th))-1*cos(th)-sin(2*th)*sin(th) by SQUARE_1:def 3
 .=2*((cos(th))|^1*cos(th)*cos(th))-1*cos(th)-sin(2*th)*sin(th) 
          by NEWTON:10
 .=2*((cos(th))|^(1+1)*cos(th))-1*cos(th)-sin(2*th)*sin(th) by NEWTON:11
 .=2*((cos(th))|^(2+1))-cos(th)-sin(2*th)*sin(th) by NEWTON:11
 .=2*(cos(th))|^3-cos(th)-(2*sin(th)*cos(th))*sin(th) by Th7
 .=2*(cos(th))|^3-cos(th)-2*cos(th)*(sin(th)*sin(th)) 
 .=2*(cos(th))|^3-cos(th)-2*cos(th)*(1-cos(th)*cos(th)) by SIN_COS4:6
 .=2*(cos(th))|^3-cos(th)-(2*cos(th)*1-2*cos(th)*(cos(th)*cos(th)))
 .=2*(cos(th))|^3-cos(th)-(2*cos(th)-2*(cos(th)*cos(th)*cos(th)))
 .=2*(cos(th))|^3-cos(th)-(2*cos(th)-2*((cos(th))|^1*cos(th)*cos(th)))
            by NEWTON:10
 .=2*(cos(th))|^3-cos(th)-(2*cos(th)-2*((cos(th))|^(1+1)*cos(th)))
            by NEWTON:11
 .=2*(cos(th))|^3-cos(th)-(2*cos(th)-2*((cos(th))|^(2+1)))  by NEWTON:11
 .=2*(cos(th))|^3-cos(th)-2*cos(th)+2*((cos(th))|^(2+1))  
 .=2*(cos(th))|^3-(1*cos(th)+2*cos(th))+2*(cos(th))|^3 
 .=2*(cos(th))|^3-(1+2)*cos(th)+2*(cos(th))|^3  
 .=2*(cos(th))|^3+2*(cos(th))|^3-3*cos(th);
 hence thesis ;
end; 

theorem 
  cos(th)<>0 implies
  tan(3*th)=(3*tan(th)-(tan(th))|^3)/(1-3*(tan(th))^2)
proof
  assume
 A1:cos(th)<>0;
  tan(3*th)=tan(th+th+th) 
  .=(tan(th)+tan(th)+tan(th)-tan(th)*tan(th)*tan(th))
  /(1-tan(th)*tan(th)-tan(th)*tan(th)-tan(th)*tan(th)) by A1,SIN_COS4:17
  .=(3*tan(th)-tan(th)*tan(th)*tan(th))/(1-tan(th)*tan(th)
     -tan(th)*tan(th)-tan(th)*tan(th)) 
  .=(3*tan(th)-tan(th)*tan(th)*tan(th))/(1-(tan(th)*tan(th)
     +tan(th)*tan(th)+tan(th)*tan(th))) 
  .=(3*tan(th)-tan(th)*tan(th)*tan(th))/(1-3*tan(th)*tan(th)) 
  .=(3*tan(th)-(tan(th))|^1*tan(th)*tan(th))/(1-3*tan(th)*tan(th))
         by NEWTON:10
  .=(3*tan(th)-(tan(th))|^(1+1)*tan(th))/(1-3*tan(th)*tan(th)) by NEWTON:11
  .=(3*tan(th)-(tan(th))|^(2+1))/(1-3*tan(th)*tan(th)) by NEWTON:11
  .=(3*tan(th)-(tan(th))|^(2+1))/(1-3*(tan(th)*tan(th)));
  hence thesis by SQUARE_1:def 3;
end;

theorem 
  sin(th)<>0 implies
  cot(3*th)=((cot(th))|^3-3*cot(th))/(3*(cot(th))^2-1)
proof
  assume
 A1:sin(th)<>0;
  cot(3*th)=cot(th+th+th) 
  .=(cot(th)*cot(th)*cot(th)-cot(th)-cot(th)-cot(th))
  /(cot(th)*cot(th)+cot(th)*cot(th)+cot(th)*cot(th)-1) by SIN_COS4:18,A1
  .=(cot(th)*cot(th)*cot(th)-cot(th)-cot(th)-cot(th))
  /(3*(cot(th)*cot(th))-1) 
  .=(cot(th)*cot(th)*cot(th)-(cot(th)+cot(th)+cot(th)))
  /(3*(cot(th)*cot(th))-1) 
  .=(cot(th)*cot(th)*cot(th)-(cot(th)+cot(th)+cot(th)))/(3*(cot(th))^2-1)
     by SQUARE_1:def 3
  .=(cot(th)*cot(th)*cot(th)-3*cot(th))/(3*(cot(th))^2-1) 
  .=((cot(th))|^1*cot(th)*cot(th)-3*cot(th))/(3*(cot(th))^2-1) by NEWTON:10
  .=((cot(th))|^(1+1)*cot(th)-3*cot(th))/(3*(cot(th))^2-1) by NEWTON:11
  .=((cot(th))|^(2+1)-3*cot(th))/(3*(cot(th))^2-1) by NEWTON:11;
  hence thesis;
end;

theorem 
  (sin(th))^2=(1-cos(2*th))/2
proof
  (1-cos(2*th))/2=(1-(1-2*(sin(th))^2))/2 by Th9
  .=(1-1+2*(sin(th))^2)/2 
  .=(sin(th))^2*2/2 ;
  hence thesis ;
end;

theorem 
  (cos(th))^2=(1+cos(2*th))/2 
proof
  (1+cos(2*th))/2=(1+(2*(cos(th))^2-1))/2 by Th9
  .=(1-(1-2*(cos(th))^2))/2 
  .=(1-1+2*(cos(th))^2)/2 
  .=(cos(th))^2*2/2 ;
  hence thesis;
end;

theorem 
   (sin(th))|^3=(3*sin(th)-sin(3*th))/4
proof
    (3*sin(th)-sin(3*th))/4=(3*sin(th)-(-4*(sin(th))|^3+3*sin(th)))/4 by Th18
    .=(3*sin(th)-(-4*(sin(th))|^3)-3*sin(th))/4 
    .=(3*sin(th)+4*(sin(th))|^3-3*sin(th))/4 
    .=(4*(sin(th))|^3)/4 ;
    hence thesis;
end;

theorem 
   (cos(th))|^3=(3*cos(th)+cos(3*th))/4
proof
    (3*cos(th)+cos(3*th))/4
    =(3*cos(th)+(4*(cos(th))|^3 - 3*cos(th)))/4 by Th19
    .=(3*cos(th)-(3*cos(th)-4*(cos(th))|^3))/4 
    .=(3*cos(th)-3*cos(th)+4*(cos(th))|^3)/4 
    .=(4*(cos(th))|^3)/4 ;
    hence thesis ;
end;

theorem 
    (sin(th))|^4=(3-4*cos(2*th)+cos(4*th))/8
proof
   (3-4*cos(2*th)+cos(2*2*th))/8
  =(3-4*cos(2*th)+cos(2*(2*th)))/8 
 .=(3-4*cos(2*th)+(1-2*(sin(2*th))^2))/8 by Th9
 .=(3-4*cos(2*th)+(1-2*(2*sin(th)*cos(th))^2))/8 by Th7
 .=(3-4*cos(2*th)+(1-2*((2*sin(th))^2*(cos(th))^2)))/8 by SQUARE_1:68
 .=(3-4*cos(2*th)+(1-2*(2^2*(sin(th))^2*(cos(th))^2)))/8 by SQUARE_1:68
 .=(3-4*cos(2*th)+(1-2*(2^2*((sin(th))^2*(cos(th))^2))))/8 
 .=(3-4*cos(2*th)+(1-2*2^2*((sin(th))^2*(cos(th))^2)))/8 
 .=(3-4*cos(2*th)+(1-2*(2*2)*((sin(th))^2*(cos(th))^2)))/8 by SQUARE_1:def 3
 .=(3-4*cos(2*th)+(1-8*(sin(th))^2*(cos(th))^2))/8 
 .=(3-4*(1-2*(sin(th))^2)+(1-8*(sin(th))^2*(cos(th))^2))/8 by Th9
 .=(3-(4*1-4*(2*(sin(th))^2))+(1-8*(sin(th))^2*(cos(th))^2))/8 
 .=(3-(4-4*2*(sin(th))^2)+(1-8*(sin(th))^2*(cos(th))^2))/8 
 .=(3-4+8*(sin(th))^2+(1-8*(sin(th))^2*(cos(th))^2))/8 
 .=(3-4+8*(sin(th))^2-(8*(sin(th))^2*(cos(th))^2-1))/8 
 .=(3-4+8*(sin(th))^2-8*(sin(th))^2*(cos(th))^2+1)/8 
 .=(-1-8*(sin(th))^2*(cos(th))^2+8*(sin(th))^2+1)/8 
 .=(8*(sin(th))^2*1-8*(sin(th))^2*(cos(th))^2+-1+1)/8 
 .=(8*(sin(th))^2*(1-(cos(th))^2)+-1+1)/8 
 .=(8*(sin(th))^2*(1-(cos(th))^2))/8 
 .=(8*((sin(th))^2*(1-(cos(th))^2)))/8 
 .=(sin(th))^2*(1-(cos(th))^2) 
 .=(sin(th))^2*(1-cos(th)*cos(th)) by SQUARE_1:def 3
 .=(sin(th))^2*(sin(th)*sin(th)) by SIN_COS4:6
 .=sin(th)*sin(th)*(sin(th)*sin(th)) by SQUARE_1:def 3
 .=(sin(th))|^1*sin(th)*(sin(th)*sin(th)) by NEWTON:10
 .=(sin(th))|^(1+1)*(sin(th)*sin(th)) by NEWTON:11
 .=(sin(th))|^(1+1)*sin(th)*sin(th) 
 .=(sin(th))|^(2+1)*sin(th) by NEWTON:11
 .=(sin(th))|^(3+1) by NEWTON:11;
  hence thesis ;
end; 

theorem 
   (cos(th))|^4 = (3+4*cos(2*th)+cos(4*th))/8
proof
   (3+4*cos(2*th)+cos(4*th))/8
  =(3+4*cos(2*th)+cos(2*(2*th)))/8 
 .=(3+4*cos(2*th)+(1-2*(sin(2*th))^2))/8 by Th9
 .=(3+4*cos(2*th)+(1-2*(2*sin(th)*cos(th))^2))/8 by Th7
 .=(3+4*cos(2*th)+(1-2*((2*sin(th))^2*(cos(th))^2)))/8 by SQUARE_1:68
 .=(3+4*cos(2*th)+(1-2*(2^2*(sin(th))^2*(cos(th))^2)))/8 by SQUARE_1:68
 .=(3+4*cos(2*th)+(1-2*(2^2*((sin(th))^2*(cos(th))^2))))/8 
 .=(3+4*cos(2*th)+(1-2*2^2*((sin(th))^2*(cos(th))^2)))/8 
 .=(3+4*cos(2*th)+(1-2*(2*2)*((sin(th))^2*(cos(th))^2)))/8 by SQUARE_1:def 3
 .=(3+4*cos(2*th)+(1-8*(sin(th))^2*(cos(th))^2))/8 
 .=(3+4*(1-2*(sin(th))^2)+(1-8*(sin(th))^2*(cos(th))^2))/8 by Th9
 .=(3+(4*1-4*(2*(sin(th))^2))+(1-8*(sin(th))^2*(cos(th))^2))/8 
 .=(3+(4-4*2*(sin(th))^2)+(1-8*(sin(th))^2*(cos(th))^2))/8 
 .=(3-(8*(sin(th))^2-4)+(1-8*(sin(th))^2*(cos(th))^2))/8 
 .=(3-8*(sin(th))^2+4+(1-8*(sin(th))^2*(cos(th))^2))/8 
 .=(3+4-8*(sin(th))^2+(1-8*(sin(th))^2*(cos(th))^2))/8 
 .=(7-8*(sin(th))^2-(8*(sin(th))^2*(cos(th))^2-1))/8 
 .=(7-8*(sin(th))^2-8*(sin(th))^2*(cos(th))^2+1)/8 
 .=(7-8*(sin(th))^2+1-8*(sin(th))^2*(cos(th))^2)/8 
 .=(8-(8*(sin(th))^2*1+8*(sin(th))^2*(cos(th))^2))/8 
 .=(8*1-8*(sin(th))^2*(1+(cos(th))^2))/8 
 .=8*(1-(sin(th))^2*(1+(cos(th))^2))/8 
 .=1-(sin(th))^2*(1+(cos(th))^2) 
 .=1-(sin(th)*sin(th))*(1+(cos(th))^2) by SQUARE_1:def 3
 .=1-(1-cos(th)*cos(th))*(1+(cos(th))^2) by SIN_COS4:6
 .=1-(1-(cos(th))^2)*(1+(cos(th))^2) by SQUARE_1:def 3
 .=1-(1^2-(cos(th))^2)*(1^2+(cos(th))^2) by SQUARE_1:59
 .=1-((1^2)^2-((cos(th))^2)^2) by SQUARE_1:67
 .=1-(1-((cos(th))^2)^2) by SQUARE_1:59
 .=1-1+((cos(th))^2)^2 
 .=(cos(th))^2*(cos(th))^2 by SQUARE_1:def 3
 .=(cos(th)*cos(th))*(cos(th))^2 by SQUARE_1:def 3
 .=(cos(th)*cos(th))*(cos(th)*cos(th)) by SQUARE_1:def 3
 .=cos(th)*cos(th)*cos(th)*cos(th) 
 .=(cos(th))|^1*cos(th)*cos(th)*cos(th) by NEWTON:10
 .=(cos(th))|^(1+1)*cos(th)*cos(th) by NEWTON:11
 .=(cos(th))|^(2+1)*cos(th) by NEWTON:11
 .=(cos(th))|^(3+1) by NEWTON:11;
 hence thesis ;
end;

::Half Angle

theorem 
  sin(th/2)=sqrt((1-cos(th))/2) or  sin(th/2)=-sqrt((1-cos(th))/2)
proof
 A1:sqrt((1-cos(th))/2)=sqrt((1-cos(2*th/2))/2) 
   .=sqrt((1-cos(2*(th/2)))/2) 
   .=sqrt((1-(1-2*(sin(th/2))^2))/2) by Th9
   .=sqrt((1-1+2*(sin(th/2))^2)/2) 
   .=sqrt((2*(sin(th/2))^2)/2) 
   .=sqrt((sin(th/2))^2) 
   .=abs(sin(th/2)) by SQUARE_1:91;
   now per cases;
   suppose sin(th/2)>=0;
    hence  thesis by  ABSVALUE:def 1,A1;
   end;
   suppose sin(th/2)<0;then
    sqrt((1-cos(th))/2)*(-1)=(-sin(th/2))*(-1) by  ABSVALUE:def 1,A1;
    hence  thesis by  XCMPLX_1:180;
   end;
end;
 hence thesis;
end;

theorem 
  cos(th/2)=sqrt((1+cos(th))/2) or  cos(th/2)=-sqrt((1+cos(th))/2)
proof
 A1:sqrt((1+cos(th))/2)=sqrt((1+cos(2*th/2))/2) 
   .=sqrt((1+cos(2*(th/2)))/2)
   .=sqrt((1+(2*(cos(th/2))^2-1))/2) by Th9
   .=sqrt((1-(1-2*(cos(th/2))^2))/2) 
   .=sqrt((1-1+2*(cos(th/2))^2)/2) 
   .=sqrt((2*(cos(th/2))^2)/2)
   .=sqrt((cos(th/2))^2) 
   .=abs(cos(th/2)) by SQUARE_1:91;
   now per cases;
   suppose cos(th/2)>=0;
    hence  thesis by  ABSVALUE:def 1,A1;
   end;
   suppose cos(th/2)<0;then
    sqrt((1+cos(th))/2)*(-1)=(-cos(th/2))*(-1) by  ABSVALUE:def 1,A1;
    hence  thesis by  XCMPLX_1:180;
   end;
end;
 hence thesis;
end;

theorem 
   sin(th/2)<>0 implies tan(th/2)=(1-cos(th))/sin(th)
proof
   assume
   sin(th/2)<>0 ;then
 A1:2*sin(th/2)<>0 by XCMPLX_1:6;
   (1-cos(th))/sin(th)=(1-cos(2*th/2))/sin(2*th/2)
   .=(1-cos(2*(th/2)))/sin(2*(th/2)) 
   .=(1-(1-2*(sin(th/2))^2))/sin(2*(th/2)) by Th9
   .=(1-(1-2*(sin(th/2))^2))/(2*sin(th/2)*cos(th/2)) by Th7
   .=(1-1+2*(sin(th/2))^2)/(2*sin(th/2)*cos(th/2)) 
   .=(2*(sin(th/2))^2)/(2*sin(th/2)*cos(th/2))
   .=(2*(sin(th/2)*sin(th/2)))/(2*sin(th/2)*cos(th/2)) by SQUARE_1:def 3
   .=(2*sin(th/2)*sin(th/2))/(2*sin(th/2)*cos(th/2)) 
   .=(sin(th/2))/(cos(th/2)) by XCMPLX_1:92,A1
   .=tan(th/2) by SIN_COS4:def 1;
 hence thesis;
end;

theorem 
   cos(th/2)<>0 implies  tan(th/2)=sin(th)/(1+cos(th))
proof
   assume cos(th/2)<>0;then
 A1:2*cos(th/2)<>0 by XCMPLX_1:6;
   sin(th)/(1+cos(th))=sin(2*th/2)/(1+cos(2*th/2)) 
   .=sin(2*(th/2))/(1+cos(2*(th/2))) 
   .=(2*sin(th/2)*cos(th/2))/(1+cos(2*(th/2))) by Th7
   .=(2*sin(th/2)*cos(th/2))/(1+(2*(cos(th/2))^2-1)) by Th9
   .=(2*sin(th/2)*cos(th/2))/(1-(1-2*(cos(th/2))^2)) 
   .=(2*sin(th/2)*cos(th/2))/(1-1+2*(cos(th/2))^2) 
   .=(2*sin(th/2)*cos(th/2))/(2*(cos(th/2))^2) 
   .=(2*sin(th/2)*cos(th/2))/(2*(cos(th/2)*cos(th/2))) by SQUARE_1:def 3
   .=(2*sin(th/2)*cos(th/2))/(2*cos(th/2)*cos(th/2)) 
   .=(2*cos(th/2)*sin(th/2))/(2*cos(th/2)*cos(th/2)) 
   .=(sin(th/2))/(cos(th/2)) by XCMPLX_1:92,A1;
 hence thesis by SIN_COS4:def 1;
end;


theorem 
     tan(th/2)=sqrt((1-cos(th))/(1+cos(th))) 
 or  tan(th/2)=-sqrt((1-cos(th))/(1+cos(th)))
proof
 A1:sqrt((1-cos(th))/(1+cos(th))) 
    =sqrt((1-cos(2*th/2))/(1+cos(2*th/2)))
   .=sqrt((1-cos(2*(th/2)))/(1+cos(2*(th/2)))) 
   .=sqrt((1-(1-2*(sin(th/2))^2))/(1+cos(2*(th/2)))) by Th9
   .=sqrt((1-(1-2*(sin(th/2))^2))/(1+(2*(cos(th/2))^2-1))) by Th9
   .=sqrt((1-1+2*(sin(th/2))^2)/(1+(2*(cos(th/2))^2-1))) 
   .=sqrt((2*(sin(th/2))^2)/(1+(2*(cos(th/2))^2-1))) 
   .=sqrt((2*(sin(th/2))^2)/(1-(1-2*(cos(th/2))^2))) 
   .=sqrt((2*(sin(th/2))^2)/(1-1+2*(cos(th/2))^2)) 
   .=sqrt((2*(sin(th/2))^2)/(2*(cos(th/2))^2))
   .=sqrt((sin(th/2))^2/(cos(th/2))^2) by XCMPLX_1:92
   .=sqrt((sin(th/2)/cos(th/2))^2) by SQUARE_1:69
   .=sqrt((tan(th/2))^2) by SIN_COS4:def 1
   .=abs(tan(th/2)) by SQUARE_1:91;
   now per cases;
   suppose  tan(th/2)>=0;
      hence thesis by ABSVALUE:def 1,A1;
   end;
   suppose  tan(th/2)<0;then
    sqrt((1-cos(th))/(1+cos(th)))*(-1) =(-tan(th/2))*(-1)
                                             by ABSVALUE:def 1,A1;
    hence thesis by XCMPLX_1:180;
   end;
   end;
 hence thesis; 
end;

theorem 
   cos(th/2)<>0 implies cot(th/2)=(1+cos(th))/sin(th) 
proof
   assume
   cos(th/2)<>0 ;then
 A1:2*cos(th/2)<>0 by XCMPLX_1:6;
   (1+cos(th))/sin(th)=(1+cos(2*th/2))/sin(2*th/2) 
   .=(1+cos(2*(th/2)))/sin(2*(th/2))
   .=(1+(2*(cos(th/2))^2-1))/sin(2*(th/2)) by Th9
   .=(1+(2*(cos(th/2))^2-1))/(2*sin(th/2)*cos(th/2)) by Th7
   .=(1-(1-2*(cos(th/2))^2))/(2*sin(th/2)*cos(th/2))
   .=(1-1+2*(cos(th/2))^2)/(2*sin(th/2)*cos(th/2)) 
   .=(2*(cos(th/2))^2)/(2*sin(th/2)*cos(th/2)) 
   .=(2*(cos(th/2)*cos(th/2)))/(2*sin(th/2)*cos(th/2)) by SQUARE_1:def 3
   .=(2*cos(th/2)*cos(th/2))/(2*sin(th/2)*cos(th/2)) 
   .=(2*cos(th/2)*cos(th/2))/(2*cos(th/2)*sin(th/2)) 
   .=(cos(th/2))/(sin(th/2)) by XCMPLX_1:92,A1
   .=cot(th/2) by SIN_COS4:def 2;
 hence thesis;
end;

theorem 
  sin(th/2)<>0 implies  cot(th/2)=sin(th)/(1-cos(th))
proof
   assume sin(th/2)<>0;then
 A1:2*sin(th/2)<>0 by XCMPLX_1:6;
   sin(th)/(1-cos(th))=sin(2*th/2)/(1-cos(2*th/2)) 
   .=sin(2*(th/2))/(1-cos(2*(th/2))) 
   .=(2*sin(th/2)*cos(th/2))/(1-cos(2*(th/2))) by Th7
   .=(2*sin(th/2)*cos(th/2))/(1-(1-2*(sin(th/2))^2)) by Th9
   .=(2*sin(th/2)*cos(th/2))/(1-1+2*(sin(th/2))^2) 
   .=(2*sin(th/2)*cos(th/2))/(2*(sin(th/2))^2) 
   .=(2*sin(th/2)*cos(th/2))/(2*(sin(th/2)*sin(th/2))) by SQUARE_1:def 3
   .=(2*sin(th/2)*cos(th/2))/(2*sin(th/2)*sin(th/2)) 
   .=(cos(th/2))/(sin(th/2)) by XCMPLX_1:92,A1;
 hence thesis by SIN_COS4:def 2;
end;

theorem 
     cot(th/2) = sqrt((1+cos(th))/(1-cos(th))) 
  or cot(th/2) =-sqrt((1+cos(th))/(1-cos(th)))
proof
 A1:sqrt((1+cos(th))/(1-cos(th))) 
    =sqrt((1+cos(2*th/2))/(1-cos(2*th/2))) 
   .=sqrt((1+cos(2*(th/2)))/(1-cos(2*(th/2)))) 
   .=sqrt((1+(2*(cos(th/2))^2-1))/(1-cos(2*(th/2)))) by Th9
   .=sqrt((1+(2*(cos(th/2))^2-1))/(1-(1-2*(sin(th/2))^2))) by Th9
   .=sqrt((1-(1-2*(cos(th/2))^2))/(1-(1-2*(sin(th/2))^2))) 
   .=sqrt((1-1+2*(cos(th/2))^2)/(1-1+2*(sin(th/2))^2)) 
   .=sqrt((2*(cos(th/2))^2)/(2*(sin(th/2))^2)) 
   .=sqrt((cos(th/2))^2/(sin(th/2))^2) by XCMPLX_1:92
   .=sqrt((cos(th/2)/sin(th/2))^2) by SQUARE_1:69
   .=sqrt((cot(th/2))^2) by SIN_COS4:def 2
   .=abs(cot(th/2)) by SQUARE_1:91;
   now per cases;
   suppose  cot(th/2)>=0;
      hence thesis by ABSVALUE:def 1,A1;
   end;
   suppose  cot(th/2)<0;then
    sqrt((1+cos(th))/(1-cos(th)))*(-1) =(-cot(th/2))*(-1)
                                             by ABSVALUE:def 1,A1;
    hence thesis by XCMPLX_1:180;
   end;
   end;
 hence thesis; 
end;

theorem 
  sin(th/2)<>0 & cos(th/2)<>0 & 1-(tan(th/2))^2<>0 implies
  sec(th/2)= sqrt((2*sec(th))/(sec(th)+1)) or
  sec(th/2)=-sqrt((2*sec(th))/(sec(th)+1)) 
proof
    assume 
 A1:sin(th/2)<>0 & cos(th/2)<>0 & 1-(tan(th/2))^2<>0;
 A2:sqrt((2*sec(th))/(sec(th)+1))
   =sqrt((2*sec(2*th/2))/(sec(2*th/2)+1)) 
   .=sqrt((2*sec(2*(th/2)))/(sec(2*(th/2))+1)) 
   .=sqrt((2*((sec(th/2))^2/(1-(tan(th/2))^2)))/(sec(2*(th/2))+1)) 
        by Th15,A1
   .=sqrt((2*((sec(th/2))^2/(1-(tan(th/2))^2)))
       /(((sec(th/2))^2/(1-(tan(th/2))^2))+1))    by Th15,A1;
   set a=1-(tan(th/2))^2;
   set b=(sec(th/2))^2;
 A3:a+b=(sec(th/2))^2+(1-(tan(th/2))^2)
      .=(1+(tan(th/2))^2)+(1-(tan(th/2))^2) by Th13,A1
      .=1+(tan(th/2))^2-((tan(th/2))^2-1) 
      .=1+(tan(th/2))^2-(tan(th/2))^2+1 
      .=1+1 ;
 A4:sqrt((2*sec(th))/(sec(th)+1))=sqrt((2*(b/a))/(b/a+1)) by A2
   .=sqrt(((2*(b/a))*a)/((b/a+1)*a)) by A1,XCMPLX_1:92
   .=sqrt((2*(b/a)*a)/(b/a*a+1*a))
   .=sqrt((2*((b/a)*a))/(b/a*a+1*a)) 
   .=sqrt((2*((b/a)*a))/(b+a)) by A1,XCMPLX_1:88
   .=sqrt((2*b)/(b+a)) by A1,XCMPLX_1:88
   .=sqrt(2*b/2) by A3
   .=sqrt(b) 
   .=abs(sec(th/2))  by SQUARE_1:91;
   now per cases;
   suppose  sec(th/2)>=0;
      hence thesis by ABSVALUE:def 1,A4;
   end;
   suppose  sec(th/2)<0;then
    sqrt((2*sec(th))/(sec(th)+1))*(-1) =(-sec(th/2))*(-1)
                                             by ABSVALUE:def 1,A4;
    hence thesis by XCMPLX_1:180;
   end;
   end;
 hence thesis; 
end;

theorem 
  sin(th/2)<>0 & cos(th/2)<>0 & 1-(tan(th/2))^2<>0 implies
  cosec(th/2)= sqrt((2*sec(th))/(sec(th)-1)) or
  cosec(th/2)=-sqrt((2*sec(th))/(sec(th)-1)) 
proof
    assume 
 A1:sin(th/2)<>0 & cos(th/2)<>0 & 1-(tan(th/2))^2<>0 ;
 A2:sqrt((2*sec(th))/(sec(th)-1))
   =sqrt((2*sec(2*th/2))/(sec(2*th/2)-1)) 
   .=sqrt((2*sec(2*(th/2)))/(sec(2*(th/2))-1)) 
   .=sqrt((2*((sec(th/2))^2/(1-(tan(th/2))^2)))/(sec(2*(th/2))-1)) 
        by Th15,A1
   .=sqrt((2*((sec(th/2))^2/(1-(tan(th/2))^2)))
       /(((sec(th/2))^2/(1-(tan(th/2))^2))-1))    by Th15,A1;
   set a=1-(tan(th/2))^2;
   set b=(sec(th/2))^2;
 A3:b-a= (sec(th/2))^2-(1-(tan(th/2))^2)
       .=(1+(tan(th/2))^2)-(1-(tan(th/2))^2) by Th13,A1
       .=1+(tan(th/2))^2-1+(tan(th/2))^2 
       .=(tan(th/2))^2+(tan(th/2))^2 
       .=2*(tan(th/2))^2 ;
 A4:sqrt((2*sec(th))/(sec(th)-1))=sqrt((2*(b/a))/(b/a-1)) by A2
   .=sqrt(((2*(b/a))*a)/((b/a-1)*a)) by A1,XCMPLX_1:92
   .=sqrt((2*(b/a)*a)/(b/a*a-1*a)) 
   .=sqrt((2*((b/a)*a))/(b/a*a-1*a)) 
   .=sqrt((2*((b/a)*a))/(b-a)) by A1,XCMPLX_1:88
   .=sqrt((2*b)/(b-a)) by A1,XCMPLX_1:88
   .=sqrt(2*b/(2*(tan(th/2))^2)) by A3
   .=sqrt((sec(th/2))^2/(tan(th/2))^2) by XCMPLX_1:92
   .=sqrt((sec(th/2)/(tan(th/2)))^2) by SQUARE_1:69
   .=sqrt((cosec(th/2))^2) by Th1,A1
   .=abs(cosec(th/2))  by SQUARE_1:91;
   now per cases;
   suppose  cosec(th/2)>=0;
      hence thesis by ABSVALUE:def 1,A4;
   end;
   suppose  cosec(th/2)<0;then
    sqrt((2*sec(th))/(sec(th)-1))*(-1) =(-cosec(th/2))*(-1)
                                             by ABSVALUE:def 1,A4;
    hence thesis by XCMPLX_1:180;
   end;
   end;
 hence thesis; 
end;

definition let th;
  func coth(th) -> Real equals 
 :Def1: cosh(th)/sinh(th);
  correctness;
end;

definition let th;
  func sech(th) -> Real equals 
 :Def2: 1/cosh(th);
  correctness;
end;

definition let th;
  func cosech(th) -> Real equals 
 :Def3: 1/sinh(th);
  correctness;
end;

theorem Th37:
  coth(th)=(exp(th)+exp(-th))/(exp(th)-exp(-th))  &
  sech(th)=2/(exp(th)+exp(-th)) &
  cosech(th)=2/(exp(th)-exp(-th))  
proof
 A1:coth(th)=cosh(th)/sinh(th) by Def1
  .=cosh.th/sinh(th) by SIN_COS2:def 4
  .=cosh.th/sinh.th by SIN_COS2:def 2
  .=((exp.(th)+exp.(-th))/2)/sinh.th by SIN_COS2:def 3
  .=((exp.(th)+exp.(-th))/2)/((exp.(th)-exp.(-th))/2) by SIN_COS2:def 1
  .=((exp.(th)+exp.(-th))/2*2)/((exp.(th)-exp.(-th))/2*2) by XCMPLX_1:92
  .=(exp.(th)+exp.(-th))/((exp.(th)-exp.(-th))/2*2) 
  .=(exp.(th)+exp.(-th))/(exp.(th)-exp.(-th)) 
  .=(exp.(th)+exp.(-th))/(exp.(th)-exp(-th)) by SIN_COS:def 27
  .=(exp.(th)+exp.(-th))/(exp(th)-exp(-th)) by SIN_COS:def 27
  .=(exp.(th)+exp(-th))/(exp(th)-exp(-th)) by SIN_COS:def 27
  .=(exp(th)+exp(-th))/(exp(th)-exp(-th)) by SIN_COS:def 27;
 A2:sech(th)=1/cosh(th) by Def2
    .=1/cosh.th by SIN_COS2:def 4
    .=1/((exp.(th)+exp.(-th))/2) by SIN_COS2:def 3
    .=1*2/((exp.(th)+exp.(-th))/2*2) by XCMPLX_1:92
    .=2/(exp.(th)+exp.(-th))
    .=2/(exp.(th)+exp(-th)) by SIN_COS:def 27
    .=2/(exp(th)+exp(-th)) by SIN_COS:def 27;
    cosech(th)=1/sinh(th) by Def3
    .=1/sinh.th by SIN_COS2:def 2
    .=1/((exp.(th)-exp.(-th))/2) by SIN_COS2:def 1
    .=1*2/((exp.(th)-exp.(-th))/2*2) by XCMPLX_1:92
    .=2/(exp.(th)-exp.(-th)) 
    .=2/(exp.(th)-exp(-th)) by SIN_COS:def 27;

  hence thesis by  SIN_COS:def 27,A1,A2;
end;

theorem 
  exp(th)-exp(-th)<>0   implies    tanh(th)*coth(th)=1
proof
   assume  A0:exp(th)-exp(-th)<>0;
 A1:tanh(th)*coth(th)=tanh.th*coth(th) by SIN_COS2:def 6
   .=(exp.th-exp.(-th))/(exp.th+exp.(-th))*coth(th) by SIN_COS2:def 5
   .=(exp.th-exp.(-th))/(exp.th+exp.(-th))*
     ((exp(th)+exp(-th))/(exp(th)-exp(-th))) by Th37
   .=(exp.th-exp.(-th))/(exp.th+exp(-th))*
     ((exp(th)+exp(-th))/(exp(th)-exp(-th))) by SIN_COS:def 27
   .=(exp(th)-exp.(-th))/(exp.th+exp(-th))*
     ((exp(th)+exp(-th))/(exp(th)-exp(-th))) by SIN_COS:def 27
   .=(exp(th)-exp(-th))/(exp.th+exp(-th))*
     ((exp(th)+exp(-th))/(exp(th)-exp(-th))) by SIN_COS:def 27
   .=(exp(th)-exp(-th))/(exp(th)+exp(-th))*
     ((exp(th)+exp(-th))/(exp(th)-exp(-th))) by SIN_COS:def 27
   .=(exp(th)-exp(-th))/(exp(th)+exp(-th))*
     (exp(th)+exp(-th))/(exp(th)-exp(-th)) by XCMPLX_1:75;
     exp(th)>0 & exp(-th)>0 by SIN_COS:60;then
   exp(th) + exp(-th)>0+0 by REAL_1:67;then
  tanh(th)*coth(th)
   =(exp(th)-exp(-th)) / (exp(th)-exp(-th)) by XCMPLX_1:88,A1
  .=1 by XCMPLX_1:60,A0;  
  hence thesis;
end;

theorem 
   (sech(th))^2+(tanh(th))^2=1
proof
     cosh.th <>0 by SIN_COS2:15;then
 A1: (cosh.th)^2<>0 by  SQUARE_1:74;
   (sech(th))^2+(tanh(th))^2
   =(1/cosh(th))^2+(tanh(th))^2 by Def2
  .=(1/cosh(th))^2 + (tanh.th)^2 by SIN_COS2:def 6
  .=(1/cosh.(th))^2+(tanh.(th))^2 by SIN_COS2:def 4
  .=(1^2/(cosh.(th))^2)+(tanh.(th))^2 by SQUARE_1:69
  .=(1^2/(cosh.(th))^2)+(sinh.(th)/cosh.(th))^2 by SIN_COS2:17
  .=(1^2/(cosh.(th))^2)+((sinh.(th))^2/(cosh.(th))^2) by SQUARE_1:69
  .=(1/(cosh.(th))^2)+((sinh.(th))^2/(cosh.(th))^2) by SQUARE_1:59
  .=(1+(sinh.(th))^2)/(cosh.(th))^2 by XCMPLX_1:63
  .=((cosh.th)^2-(sinh.th)^2+(sinh.(th))^2)/(cosh.(th))^2 by SIN_COS2:14
  .=(cosh.th)^2/(cosh.(th))^2 ;
  hence thesis by A1,XCMPLX_1:60;
end;

theorem 
 sinh(th)<>0 implies (coth(th))^2-(cosech(th))^2=1
proof
  assume sinh(th)<>0;then
 A1:(sinh(th))^2<>0 by  SQUARE_1:74;
    (coth(th))^2-(cosech(th))^2
   =(cosh(th)/sinh(th))^2-(cosech(th))^2 by Def1
  .=(cosh(th)/sinh(th))^2-(1/sinh(th))^2 by Def3
  .=(cosh(th))^2/(sinh(th))^2-(1/sinh(th))^2 by SQUARE_1:69
  .=(cosh(th))^2/(sinh(th))^2-1^2/(sinh(th))^2 by SQUARE_1:69
  .=(cosh(th))^2/(sinh(th))^2-1/(sinh(th))^2 by SQUARE_1:59
  .=((cosh(th))^2-1)/(sinh(th))^2 by XCMPLX_1:121
  .=((cosh(th))^2-((cosh.th)^2-(sinh.th)^2))/(sinh(th))^2 by SIN_COS2:14
  .=((cosh(th))^2-(cosh.th)^2+(sinh.th)^2)/(sinh(th))^2 
  .=((cosh(th))^2-(cosh(th))^2+(sinh.th)^2)/(sinh(th))^2 by SIN_COS2:def 4
  .=((cosh(th))^2-(cosh(th))^2+(sinh(th))^2)/(sinh(th))^2 by SIN_COS2:def 2
  .=(0+(sinh(th))^2)/(sinh(th))^2 ;
  hence thesis by A1,XCMPLX_1:60;
end;

Lm1:coth(-th)=-coth(th)    
 proof
   coth(-th)=cosh(-th)/sinh(-th) by Def1
   .=cosh.(-th)/sinh(-th) by SIN_COS2:def 4
   .=cosh.(-th)/sinh.(-th) by SIN_COS2:def 2
   .=cosh.(th)/sinh.(-th) by SIN_COS2:19
   .=cosh.(th)/(-sinh.(th)) by SIN_COS2:19
   .=-cosh.(th)/sinh.(th) by XCMPLX_1:189
   .= - cosh(th)/sinh.(th) by SIN_COS2:def 4
   .=-cosh(th)/sinh(th) by SIN_COS2:def 2
   .=-coth(th) by Def1;
   hence thesis;
 end;

 
theorem Th410:
   sinh(th1)<>0 & sinh(th2)<>0 implies
   coth(th1+th2)=(1+coth(th1)*coth(th2))/(coth(th1)+coth(th2))
proof
    assume  sinh(th1)<>0 & sinh(th2)<>0;then
 A0: sinh.(th1)<>0 & sinh.(th2)<>0 by SIN_COS2:def 2;then
 A1: sinh.(th1)*sinh.(th2)<>0 by XCMPLX_1:6;
     coth(th1+th2)=cosh(th1+th2)/sinh(th1+th2) by Def1
   .=cosh.(th1+th2)/sinh(th1+th2) by SIN_COS2:def 4
   .=cosh.(th1+th2)/sinh.(th1+th2) by SIN_COS2:def 2
   .=((cosh.th1)*(cosh.th2) + (sinh.th1)*(sinh.th2))
            /sinh.(th1+th2) by SIN_COS2:20
   .=((cosh.th1)*(cosh.th2) + (sinh.th1)*(sinh.th2))
     /((sinh.th1)*(cosh.th2) + (cosh.th1)*(sinh.th2)) by SIN_COS2:21
 .=(((cosh.th1)*(cosh.th2) + (sinh.th1)*(sinh.th2))/((sinh.th1)*(sinh.th2)))
  /(((sinh.th1)*(cosh.th2) + (cosh.th1)*(sinh.th2))/((sinh.th1)*(sinh.th2)))
        by XCMPLX_1:55,A1
 .=(((cosh.th1)*(cosh.th2)/((sinh.th1)*(sinh.th2)))
   + (sinh.th1)*(sinh.th2)/((sinh.th1)*(sinh.th2)))
  /(((sinh.th1)*(cosh.th2) + (cosh.th1)*(sinh.th2))/((sinh.th1)*(sinh.th2)))
        by XCMPLX_1:63
 .=(((cosh.th1)*(cosh.th2)/((sinh.th1)*(sinh.th2)))+ 1)
  /(((sinh.th1)*(cosh.th2)+(cosh.th1)*(sinh.th2))/((sinh.th1)*(sinh.th2)))
        by XCMPLX_1:60,A1
 .=(((cosh.th1)/(sinh.th1)*(cosh.th2)/(sinh.th2))+ 1)
  /(((sinh.th1)*(cosh.th2)+(cosh.th1)*(sinh.th2))/((sinh.th1)*(sinh.th2)))
        by XCMPLX_1:84
 .=(((cosh.th1)/(sinh.th1)*((cosh.th2)/(sinh.th2)))+ 1)
  /(((sinh.th1)*(cosh.th2)+(cosh.th1)*(sinh.th2))/((sinh.th1)*(sinh.th2)))
        by XCMPLX_1:75
 .=(((cosh.th1)/(sinh.th1))*((cosh.th2)/(sinh.th2))+ 1)/(((sinh.th1)
   *(cosh.th2))/((sinh.th1)*(sinh.th2))+((cosh.th1)*(sinh.th2))/((sinh.th1)
   *(sinh.th2)))   by XCMPLX_1:63
 .=(((cosh.th1)/(sinh.th1))*((cosh.th2)/(sinh.th2))+ 1)/(((sinh.th1)/(sinh.th1)
   *(cosh.th2))/(sinh.th2)+((cosh.th1)*(sinh.th2))/((sinh.th1)
   *(sinh.th2)))   by XCMPLX_1:84
 .=(((cosh.th1)/(sinh.th1))*((cosh.th2)/(sinh.th2))+ 1)/(((sinh.th1)/(sinh.th1)
   *(cosh.th2))/(sinh.th2)+((cosh.th1)/(sinh.th1)*(sinh.th2))/(
   (sinh.th2)))   by XCMPLX_1:84
 .=(((cosh.th1)/(sinh.th1))*((cosh.th2)/(sinh.th2))+ 1)/(1*
   cosh.th2/sinh.th2+((cosh.th1)/(sinh.th1)*(sinh.th2))/(
   (sinh.th2)))   by XCMPLX_1:60,A0
 .=(((cosh.th1)/(sinh.th1))*((cosh.th2)/(sinh.th2))+ 1)/(
   cosh.th2/sinh.th2+cosh.th1/sinh.th1)   by XCMPLX_1:90,A0
 .=(((cosh(th1))/(sinh.th1))*((cosh.th2)/(sinh.th2))+ 1)/(
   cosh.th2/sinh.th2+cosh.th1/sinh.th1)   by SIN_COS2:def 4
 .=(((cosh(th1))/(sinh.th1))*((cosh(th2))/(sinh.th2))+ 1)/(
   cosh.th2/sinh.th2+cosh.th1/sinh.th1)   by SIN_COS2:def 4
 .=(((cosh(th1))/(sinh.th1))*((cosh(th2))/(sinh.th2))+ 1)/(
   cosh(th2)/sinh.th2+cosh.th1/sinh.th1)   by SIN_COS2:def 4
 .=(((cosh(th1))/(sinh.th1))*((cosh(th2))/(sinh.th2))+ 1)/(
   cosh(th2)/sinh.th2+cosh(th1)/sinh.th1)   by SIN_COS2:def 4
 .=(((cosh(th1))/(sinh(th1)))*((cosh(th2))/(sinh.th2))+ 1)/(
   cosh(th2)/sinh.th2+cosh(th1)/sinh.th1)   by SIN_COS2:def 2
 .=(((cosh(th1))/(sinh(th1)))*((cosh(th2))/(sinh.th2))+ 1)/(
   cosh(th2)/sinh(th2)+cosh(th1)/sinh.th1)   by SIN_COS2:def 2
 .=(((cosh(th1))/(sinh(th1)))*((cosh(th2))/(sinh.th2))+ 1)/(
   cosh(th2)/sinh(th2)+cosh(th1)/sinh(th1))   by SIN_COS2:def 2
 .=(((cosh(th1))/(sinh(th1)))*((cosh(th2))/(sinh(th2)))+ 1)/(
   cosh(th2)/sinh(th2)+cosh(th1)/sinh(th1))   by SIN_COS2:def 2
 .=(coth(th1)*((cosh(th2))/(sinh(th2)))+ 1)/(cosh(th2)/sinh(th2)+cosh(th1)
    /sinh(th1))   by Def1
 .=(coth(th1)*coth(th2)+ 1)/(cosh(th2)/sinh(th2)+cosh(th1)/sinh(th1)) by Def1
 .=(coth(th1)*coth(th2)+ 1)/(coth(th2)+cosh(th1)/sinh(th1)) by Def1
 .=(coth(th1)*coth(th2)+ 1)/(coth(th2)+coth(th1)) by Def1;
 hence thesis;
end;

theorem 
   sinh(th1)<>0 & sinh(th2)<>0 implies
   coth(th1-th2)=(1-coth(th1)*coth(th2))/(coth(th1)-coth(th2))
proof
    assume A: sinh(th1)<>0 & sinh(th2)<>0;then
   (-1)*sinh(th2)<>0 by XCMPLX_1:6;then
   -sinh(th2)<>0 ;then
   -sinh.(th2)<>0 by  SIN_COS2:def 2;then
   sinh.(-th2)<>0 by  SIN_COS2:19;then
 A5:sinh(-th2)<>0 by  SIN_COS2:def 2;
  coth(th1-th2)=coth(th1+(-th2)) 
   .=(1+coth(th1)*coth(-th2))/(coth(th1)+coth(-th2)) by A,A5,Th410
   .=(1+coth(th1)*(-coth(th2)))/(coth(th1)+coth(-th2)) by Lm1
   .=(1+-coth(th1)*(coth(th2)))/(coth(th1)+coth(-th2)) 
   .=(1-coth(th1)*(coth(th2)))/(coth(th1)+coth(-th2)) 
   .=(1-coth(th1)*(coth(th2)))/(coth(th1)+-coth(th2)) by Lm1
   .=(1-coth(th1)*(coth(th2)))/(coth(th1)-coth(th2)) ;
 hence thesis;
end;

theorem 
  sinh(th1)<>0 & sinh(th2)<>0 implies
  coth(th1)+coth(th2)=sinh(th1+th2)/(sinh(th1)*sinh(th2)) &
  coth(th1)-coth(th2)=-(sinh(th1-th2))/(sinh(th1)*sinh(th2)) 
proof
   assume sinh(th1)<>0 & sinh(th2)<>0 ;then
 A1: sinh.(th1)<>0 & sinh.(th2)<>0 by SIN_COS2:def 2;
 A5:sinh(th1+th2)/(sinh(th1)*sinh(th2))
   =sinh.(th1+th2)/(sinh(th1)*sinh(th2)) by SIN_COS2:def 2
   .=((sinh.th1)*(cosh.th2)+(cosh.th1)*(sinh.th2))/(sinh(th1)*sinh(th2))
                           by SIN_COS2:21
   .=((sinh.th1)*(cosh.th2)+(cosh.th1)*(sinh.th2))/(sinh.(th1)*sinh(th2))
                           by SIN_COS2:def 2
   .=((sinh.th1)*(cosh.th2)+(cosh.th1)*(sinh.th2))/(sinh.(th1)*sinh.(th2))
                           by SIN_COS2:def 2
   .=(sinh.th1)*(cosh.th2)/(sinh.(th1)*sinh.(th2))
     +(cosh.th1)*(sinh.th2)/(sinh.(th1)*sinh.(th2))      by XCMPLX_1:63
   .=(sinh.th1)/sinh.(th1)*(cosh.th2)/(sinh.(th2))
     +(cosh.th1)*(sinh.th2)/(sinh.(th1)*sinh.(th2))      by XCMPLX_1:84
   .=1*(cosh.th2)/(sinh.(th2)) +(cosh.th1)*(sinh.th2)/(sinh.(th1)*sinh.(th2)) 
                                 by XCMPLX_1:60,A1
   .=(cosh.th2)/(sinh.(th2)) +(cosh.th1)/sinh.(th1)*(sinh.th2)/(sinh.(th2))      
                                 by XCMPLX_1:84
   .=(cosh.th2)/(sinh.(th2)) +(cosh.th1)/sinh.(th1)*((sinh.th2)/(sinh.(th2)))      
                                 by XCMPLX_1:75
   .=(cosh.th2)/(sinh.(th2)) +(cosh.th1)/sinh.(th1)*1 by XCMPLX_1:60,A1
   .=(cosh.th2)/(sinh.(th2)) +(cosh.th1)/sinh(th1) by SIN_COS2:def 2
   .=(cosh.th2)/(sinh(th2)) +(cosh.th1)/sinh(th1) by SIN_COS2:def 2
   .=cosh(th2)/(sinh(th2)) +(cosh.th1)/sinh(th1) by SIN_COS2:def 4
   .=cosh(th2)/(sinh(th2)) +cosh(th1)/sinh(th1) by SIN_COS2:def 4
   .=coth(th2)+cosh(th1)/sinh(th1) by Def1
   .=coth(th2)+coth(th1) by Def1;
   -(sinh(th1-th2))/(sinh(th1)*sinh(th2))
   =-(sinh.(th1-th2))/(sinh(th1)*sinh(th2)) by SIN_COS2:def 2
   .=-(sinh.(th1-th2))/(sinh.(th1)*sinh(th2)) by SIN_COS2:def 2
   .=-(sinh.(th1-th2))/(sinh.(th1)*sinh.(th2)) by SIN_COS2:def 2
   .=-((sinh.th1)*(cosh.th2)-(cosh.th1)*(sinh.th2))/(sinh.(th1)*sinh.(th2)) 
                                                         by SIN_COS2:21
   .=-((sinh.th1)*(cosh.th2)/(sinh.(th1)*sinh.(th2))
      -(cosh.th1)*(sinh.th2)/(sinh.(th1)*sinh.(th2))) by XCMPLX_1:121
   .=-((sinh.th1)/sinh.(th1)*(cosh.th2)/(sinh.(th2))
      -(cosh.th1)*(sinh.th2)/(sinh.(th1)*sinh.(th2))) by XCMPLX_1:84
   .=-((sinh.th1)/sinh.(th1)*(cosh.th2)/(sinh.(th2))
      -((sinh.th2)/sinh.(th2)*cosh.th1)/(sinh.(th1))) by XCMPLX_1:84
   .=-((sinh.th1)/sinh.(th1)*(cosh.th2)/(sinh.(th2))
      -((sinh.th2)/sinh.(th2)*cosh.th1)/(sinh.(th1))) 
   .=-(1*(cosh.th2)/(sinh.(th2))
      -((sinh.th2)/sinh.(th2)*cosh.th1)/(sinh.(th1))) by XCMPLX_1:60,A1
   .=-(cosh.th2/(sinh.(th2))-(1*cosh.th1)/(sinh.(th1))) by XCMPLX_1:60,A1
   .=-(cosh(th2)/(sinh.(th2))-(cosh.th1)/(sinh.(th1))) by SIN_COS2:def 4
   .=-(cosh(th2)/(sinh.(th2))-(cosh(th1))/(sinh.(th1))) by SIN_COS2:def 4
   .=-(cosh(th2)/(sinh(th2))-(cosh(th1))/(sinh.(th1))) by SIN_COS2:def 2
   .=-(cosh(th2)/(sinh(th2))-(cosh(th1))/(sinh(th1))) by SIN_COS2:def 2
   .=-(coth(th2)-(cosh(th1))/(sinh(th1))) by Def1
   .=-(coth(th2)-coth(th1)) by Def1
   .=coth(th1)-coth(th2) ;
  hence thesis by A5;
end;

Lm43:(cosh.th)^2=1+(sinh.th)^2
 proof
    (cosh.th)^2-(sinh.th)^2+(sinh.th)^2=1+(sinh.th)^2 by SIN_COS2:14;
   hence thesis by  XCMPLX_1:27;
 end;

Lm44:(cosh.th)^2-1=(sinh.th)^2 
 proof
    (cosh.th)^2-(sinh.th)^2+(sinh.th)^2=1+(sinh.th)^2 by SIN_COS2:14;then
    (cosh.th)^2=1+(sinh.th)^2 by  XCMPLX_1:27;
   hence thesis ; 
 end;


theorem 
  sinh(3*th)=3*sinh(th)+4*(sinh(th))|^3
proof
    sinh(3*th)=sinh(th+th+th) 
  .=sinh.(th+th+th) by SIN_COS2:def 2
  .=(sinh.(th+th))*(cosh.th) + (cosh.(th+th))*(sinh.th) by SIN_COS2:21
  .=(sinh.(2*th))*(cosh.th) + (cosh.(2*th))*(sinh.th) 
  .=2*sinh.th*cosh.th*(cosh.th) + (cosh.(2*th))*(sinh.th) by SIN_COS2:23
  .=2*sinh.th*cosh.th*(cosh.th)+(2*(cosh.th)^2-1)*(sinh.th) by SIN_COS2:23
  .=2*sinh.th*(cosh.th*cosh.th)+(2*(cosh.th)^2-1)*(sinh.th) 
  .=2*sinh.th*(cosh.th)^2+(2*(cosh.th)^2-1)*(sinh.th) by SQUARE_1:def 3
  .=2*sinh.th*(1+(sinh.th)^2)+(2*(cosh.th)^2-1)*(sinh.th)   by Lm43
  .=2*sinh.th*(1+(sinh.th)^2)+(2*(1+(sinh.th)^2)-1)*(sinh.th) by Lm43
  .=2*sinh.th*1+2*sinh.th*(sinh.th)^2+(2*1+2*(sinh.th)^2-1)*(sinh.th)
  .=2*sinh.th+2*sinh.th*(sinh.th)^2+(2-1+2*(sinh.th)^2)*(sinh.th)
  .=2*sinh.th+2*sinh.th*(sinh.th)^2+(1*sinh.th+2*(sinh.th)^2*sinh.th)
  .=2*sinh.th+(2*sinh.th*(sinh.th)^2+2*(sinh.th)^2*sinh.th)+sinh.th
  .=2*sinh.th+(2*sinh.th*(sinh.th)^2+2*sinh.th*(sinh.th)^2)+sinh.th
  .=2*sinh.th+(2*sinh.th+2*sinh.th)*(sinh.th)^2+sinh.th
  .=2*sinh.th+(2+2)*sinh.th*(sinh.th)^2+sinh.th  
  .=2*sinh.th+4*(sinh.th)|^1*(sinh.th)^2+sinh.th  by NEWTON:10
  .=2*sinh.th+1*sinh.th+4*(sinh.th)|^1*(sinh.th)^2  
  .=(2+1)*sinh.th+4*(sinh.th)|^1*(sinh.th)^2 
  .=3*sinh.th+4*(sinh.th)|^1*(sinh.th*sinh.th)  by SQUARE_1:def 3
  .=3*sinh.th+4*((sinh.th)|^1*sinh.th*sinh.th)  
  .=3*sinh.th+4*((sinh.th)|^(1+1)*sinh.th)  by NEWTON:11
  .=3*sinh.th+4*(sinh.th)|^(2+1)  by NEWTON:11
  .=3*sinh(th)+4*(sinh.th)|^3  by SIN_COS2:def 2;
 hence thesis by SIN_COS2:def 2;
end;

theorem 
  cosh(3*th)=4*(cosh(th))|^3-3*cosh(th)
proof
  cosh(3*th)=cosh(th+th+th) 
  .=cosh.(th+th+th) by SIN_COS2:def 4
  .=(cosh.(th+th))*(cosh.th) + (sinh.(th+th))*(sinh.th) by SIN_COS2:20
  .=(cosh.(2*th))*(cosh.th) + (sinh.(2*th))*(sinh.th) 
  .=(2*(cosh.th)^2-1)*(cosh.th)+(sinh.(2*th))*(sinh.th) by SIN_COS2:23
  .=(2*(cosh.th)^2-1)*(cosh.th)+(2*(sinh.th)*(cosh.th))*(sinh.th) 
                                by SIN_COS2:23
  .=(2*(cosh.th)^2-1)*cosh.th+2*(sinh.th*sinh.th)*cosh.th 
  .=(2*(cosh.th)^2-1)*cosh.th+2*(sinh.th)^2*cosh.th by SQUARE_1:def 3
  .=(2*(cosh.th)^2-1)*cosh.th+2*((cosh.th)^2-1)*cosh.th by Lm44
  .=2*(cosh.th)^2*cosh.th-cosh.th+(2*(cosh.th)^2*cosh.th-2*cosh.th)
  .=2*(cosh.th)^2*cosh.th-cosh.th-(2*cosh.th-2*(cosh.th)^2*cosh.th)
  .=2*(cosh.th)^2*cosh.th-(1*cosh.th+2*cosh.th)+2*(cosh.th)^2*cosh.th
  .=2*(cosh.th)^2*cosh.th-(1+2)*cosh.th+2*(cosh.th)^2*cosh.th
  .=2*(cosh.th)^2*cosh.th+2*(cosh.th)^2*cosh.th-3*cosh.th 
  .=(2+2)*(cosh.th)^2*cosh.th-3*cosh.th 
  .=4*(cosh.th*cosh.th)*cosh.th-3*cosh.th by SQUARE_1:def 3
  .=4*(cosh.th*cosh.th*cosh.th)-3*cosh.th 
  .=4*((cosh.th)|^1*cosh.th*cosh.th)-3*cosh.th by NEWTON:10
  .=4*((cosh.th)|^(1+1)*cosh.th)-3*cosh.th by NEWTON:11
  .=4*((cosh.th)|^(2+1))-3*cosh.th by NEWTON:11
  .=4*(cosh.th)|^3-3*cosh(th) by SIN_COS2:def 4;
  hence thesis by SIN_COS2:def 4;
end;

theorem 
   sinh(th)<>0 implies coth(2*th)=(1+(coth(th))^2)/(2*coth(th))
proof
  assume sinh(th)<>0 ;then
 A1:sinh.th<>0 by SIN_COS2:def 2;then
 A2: (sinh.(th))^2 <>0 by SQUARE_1:74;
    coth(2*th)=cosh(2*th)/sinh(2*th) by Def1
    .=cosh.(2*th)/sinh(2*th) by SIN_COS2:def 4
    .=cosh.(2*th)/sinh.(2*th) by SIN_COS2:def 2
    .=(2*(cosh.th)^2 - 1)/sinh.(2*th) by SIN_COS2:23
    .=(2*(cosh.th)^2 - 1)/(2*(sinh.th)*(cosh.th)) by SIN_COS2:23
    .=((2*(cosh.th)^2 - 1)/(sinh.(th))^2)/((2*(sinh.th)*(cosh.th))/
        (sinh.(th))^2) by XCMPLX_1:55,A2
    .=((2*(cosh.th)^2-((cosh.th)^2-(sinh.th)^2))/(sinh.(th))^2)/((2*
     (sinh.th)*(cosh.th))/(sinh.(th))^2) by SIN_COS2:14
    .=((2*(cosh.th)^2-1*(cosh.th)^2+(sinh.th)^2)/(sinh.(th))^2)/((2*
     (sinh.th)*(cosh.th))/(sinh.(th))^2) 
    .=(((2-1)*(cosh.th)^2+(sinh.th)^2)/(sinh.(th))^2)/((2*
     (sinh.th)*(cosh.th))/(sinh.(th))^2) 
    .=((cosh.th)^2/(sinh.(th))^2+(sinh.th)^2/(sinh.(th))^2)/((2*
     (sinh.th)*(cosh.th))/(sinh.(th))^2) by XCMPLX_1:63
   .=((cosh.th/sinh.th)^2+(sinh.th)^2/(sinh.(th))^2)/((2*
     (sinh.th)*(cosh.th))/(sinh.(th))^2) by SQUARE_1:69
    .=((cosh.th/sinh.th)^2+(sinh.th/sinh.th)^2)/((2*
     (sinh.th)*(cosh.th))/(sinh.(th))^2) by SQUARE_1:69
    .=((cosh.th/sinh.th)^2+1^2)/((2*
     (sinh.th)*(cosh.th))/(sinh.(th))^2) by XCMPLX_1:60,A1
  .=((cosh.th/sinh.th)^2+1)/(2*sinh.th*cosh.th/(sinh.(th))^2) by SQUARE_1:59
  .=((cosh.th/sinh.th)^2+1)/(2*sinh.th*cosh.th/(sinh.th*sinh.th)) 
           by SQUARE_1:def 3
  .=((cosh.th/sinh.th)^2+1)/(2*cosh.th*sinh.th/(sinh.th*sinh.th)) 
  .=((cosh.th/sinh.th)^2+1)/(2*cosh.th*sinh.th/sinh.th/sinh.th)
           by XCMPLX_1:79
  .=((cosh.th/sinh.th)^2+1)/(2*cosh.th/sinh.th)  by XCMPLX_1:90,A1
  .=((cosh.th/sinh.th)^2+1)/(2*(cosh.th/sinh.th))  by XCMPLX_1:75
  .=((cosh(th)/sinh.th)^2+1)/(2*(cosh.(th)/sinh.th))  by SIN_COS2:def 4
  .=((cosh(th)/sinh.th)^2+1)/(2*(cosh(th)/sinh.th))  by SIN_COS2:def 4
  .=((cosh(th)/sinh(th))^2+1)/(2*(cosh(th)/sinh.th))  by SIN_COS2:def 2
  .=((cosh(th)/sinh(th))^2+1)/(2*(cosh(th)/sinh(th)))  by SIN_COS2:def 2
  .=((coth(th))^2+1)/(2*(cosh(th)/sinh(th)))  by Def1;
  hence thesis by Def1;
end;

theorem 
  th>0 implies sinh(th)>=0
proof
  assume
 A1:th>0;then  th+(-th)>0+(-th) by REAL_1:67;then
 A3:exp.(-th)<=1 by SIN_COS:58;
    exp.(th)>=1 by SIN_COS:57,A1;then
    exp.(th)-exp.(-th)>=1-1 by REAL_1:92,A3;then
    (exp.(th)-exp.(-th))/2>=0 by REAL_2:125;then
    sinh.(th)>=0 by SIN_COS2:def 1;
   hence thesis by SIN_COS2:def 2;
end;

theorem 
  th<0 implies sinh(th)<=0
proof
  assume
 A1:th<0;then  
    -th>0 by REAL_1:66;then
 A3:exp.(-th)>=1 by SIN_COS:57;
    exp.(th)<=1 by SIN_COS:58,A1;then
    exp.(th)-exp.(-th)<=1-1 by REAL_1:92,A3;then
    (exp.(th)-exp.(-th))/2<=0 by REAL_2:126;then
    sinh.(th)<=0 by SIN_COS2:def 1;
   hence thesis by SIN_COS2:def 2;
end;


theorem 
  cosh(th/2)=sqrt((cosh(th)+1)/2)
proof
 A1:cosh.(th/2)>0 by SIN_COS2:15;
   sqrt((cosh(th)+1)/2) = sqrt((cosh(2*th/2)+1)/2) 
   .= sqrt((cosh(2*(th/2))+1)/2) 
   .= sqrt((cosh.(2*(th/2))+1)/2) by SIN_COS2:def 4
   .= sqrt((2*(cosh.(th/2))^2 - 1 +1)/2) by SIN_COS2:23
   .= sqrt((2*(cosh.(th/2))^2)/2) 
   .= sqrt((cosh.(th/2))^2) 
   .= cosh.(th/2) by SQUARE_1:89,A1;
  hence thesis by SIN_COS2:def 4;
end;

theorem 
  sinh(th/2)<>0 implies
  tanh(th/2)=(cosh(th)-1)/(sinh(th)) 
proof 
    assume sinh(th/2)<>0 ;then sinh.(th/2)<>0 by SIN_COS2:def 2;then
 A0:2*sinh.(th/2)<>0 by XCMPLX_1:6;
    (cosh(th)-1)/(sinh(th))=(cosh(2*th/2)-1)/(sinh(2*th/2)) 
   .=(cosh(2*(th/2))-1)/(sinh(2*(th/2)))
   .=(cosh.(2*(th/2))-1)/(sinh(2*(th/2))) by SIN_COS2:def 4
   .=(2*(cosh.(th/2))^2-1-1)/(sinh(2*(th/2))) by SIN_COS2:23
   .=(2*(cosh.(th/2))^2-(1+1))/(sinh(2*(th/2))) 
   .=(2*((cosh.(th/2))^2-1))/(sinh(2*(th/2))) 
   .=(2*(sinh.(th/2))^2)/(sinh(2*(th/2))) by Lm44
   .=(2*(sinh.(th/2))^2)/(sinh.(2*(th/2))) by SIN_COS2:def 2
   .=(2*(sinh.(th/2))^2)/(2*sinh.(th/2)*cosh.(th/2)) by SIN_COS2:23
   .=(2*(sinh.(th/2)*sinh.(th/2)))/(2*sinh.(th/2)*cosh.(th/2)) 
                               by SQUARE_1:def 3
   .=(2*sinh.(th/2)*sinh.(th/2))/(2*sinh.(th/2)*cosh.(th/2)) 
   .=sinh.(th/2)/cosh.(th/2) by XCMPLX_1:92,A0
   .=tanh.(th/2) by SIN_COS2:17
   .=tanh(th/2) by SIN_COS2:def 6;
  hence thesis;
end;

theorem 
  cosh(th/2)<>0 implies
  tanh(th/2)=(sinh(th))/(cosh(th)+1)
proof 
    assume cosh(th/2)<>0 ;then cosh.(th/2)<>0 by SIN_COS2:def 4;then
 A0:2*cosh.(th/2)<>0 by XCMPLX_1:6;
   (sinh(th))/(cosh(th)+1)=(sinh(2*th/2))/(cosh(2*th/2)+1) 
   .=(sinh(2*(th/2)))/(cosh(2*(th/2))+1) 
   .=(sinh.(2*(th/2)))/(cosh(2*(th/2))+1) by SIN_COS2:def 2
   .=(sinh.(2*(th/2)))/(cosh.(2*(th/2))+1) by SIN_COS2:def 4
   .=(2*sinh.(th/2)*cosh.(th/2))/(cosh.(2*(th/2))+1) by SIN_COS2:23
   .=(2*sinh.(th/2)*cosh.(th/2))/(2*(cosh.(th/2))^2-1+1) by SIN_COS2:23
   .=(2*sinh.(th/2)*cosh.(th/2))/(2*(cosh.(th/2))^2) 
   .=(2*sinh.(th/2)*cosh.(th/2))/(2*(cosh.(th/2)*cosh.(th/2))) 
             by SQUARE_1:def 3
   .=(2*cosh.(th/2)*sinh.(th/2))/(2*cosh.(th/2)*cosh.(th/2)) 
   .=sinh.(th/2)/cosh.(th/2) by XCMPLX_1:92,A0
   .=tanh.(th/2) by SIN_COS2:17;
  hence thesis by SIN_COS2:def 6;
end;

theorem 
   sinh(th/2)<>0 implies
   coth(th/2)=(sinh(th))/(cosh(th)-1)
proof
      assume sinh(th/2)<>0 ;then sinh.(th/2)<>0 by SIN_COS2:def 2;then
 A0:2*sinh.(th/2)<>0 by XCMPLX_1:6;
   (sinh(th))/(cosh(th)-1)=(sinh(2*th/2))/(cosh(2*th/2)-1) 
   .=(sinh(2*(th/2)))/(cosh(2*(th/2))-1) 
   .=(sinh.(2*(th/2)))/(cosh(2*(th/2))-1) by SIN_COS2:def 2
   .=(sinh.(2*(th/2)))/(cosh.(2*(th/2))-1) by SIN_COS2:def 4
   .=(2*sinh.(th/2)*cosh.(th/2))/(cosh.(2*(th/2))-1) by SIN_COS2:23
   .=(2*sinh.(th/2)*cosh.(th/2))/(2*(cosh.(th/2))^2-1-1) by SIN_COS2:23
   .=(2*sinh.(th/2)*cosh.(th/2))/(2*(cosh.(th/2))^2-(1+1)) 
   .=(2*sinh.(th/2)*cosh.(th/2))/(2*((cosh.(th/2))^2-1)) 
   .=(2*sinh.(th/2)*cosh.(th/2))/(2*(sinh.(th/2))^2) by Lm44
  .=(2*sinh.(th/2)*cosh.(th/2))/(2*(sinh.(th/2)*sinh.(th/2)))
                                by SQUARE_1:def 3
  .=(2*sinh.(th/2)*cosh.(th/2))/(2*sinh.(th/2)*sinh.(th/2)) 
   .=cosh.(th/2)/sinh.(th/2) by XCMPLX_1:92,A0
   .=cosh(th/2)/sinh.(th/2) by SIN_COS2:def 4
   .=cosh(th/2)/sinh(th/2) by SIN_COS2:def 2;
   hence thesis by Def1;
end;

theorem 
   cosh(th/2)<>0 implies
   coth(th/2)=(cosh(th)+1)/(sinh(th)) 
proof
    assume cosh(th/2)<>0 ;then cosh.(th/2)<>0 by SIN_COS2:def 4;then
 A0:2*cosh.(th/2)<>0 by XCMPLX_1:6;
    (cosh(th)+1)/(sinh(th))=(cosh(2*th/2)+1)/(sinh(2*th/2)) 
   .=(cosh(2*(th/2))+1)/(sinh(2*(th/2)))
   .=(cosh.(2*(th/2))+1)/(sinh(2*(th/2))) by SIN_COS2:def 4
   .=(2*(cosh.(th/2))^2-1+1)/(sinh(2*(th/2))) by SIN_COS2:23
   .=(2*(cosh.(th/2))^2)/(sinh(2*(th/2))) 
   .=(2*(cosh.(th/2))^2)/(sinh.(2*(th/2))) by SIN_COS2:def 2
   .=(2*(cosh.(th/2))^2)/(2*sinh.(th/2)*cosh.(th/2)) by SIN_COS2:23
   .=(2*(cosh.(th/2)*cosh.(th/2)))/(2*sinh.(th/2)*cosh.(th/2)) 
                               by SQUARE_1:def 3
   .=(2*cosh.(th/2)*cosh.(th/2))/(2*cosh.(th/2)*sinh.(th/2)) 
   .=cosh.(th/2)/sinh.(th/2) by XCMPLX_1:92,A0
   .=cosh(th/2)/sinh.(th/2) by SIN_COS2:def 4
   .=cosh(th/2)/sinh(th/2) by SIN_COS2:def 2;
  hence thesis by Def1;
end;





