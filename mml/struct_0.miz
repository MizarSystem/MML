:: Preliminaries to Structures
::  by Library Committee
::
:: Received January 6, 1995
:: Copyright (c) 1995 Association of Mizar Users

environ

 vocabularies SETFAM_1, FUNCT_1, FINSEQ_1, PBOOLE, SUBSET_1, BOOLE, NAT_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, SETFAM_1, FUNCT_2, FINSEQ_1, PBOOLE;
 constructors PBOOLE, NAT_D;
 registrations SUBSET_1, XBOOLE_0;
 requirements BOOLE, SUBSET;
 definitions SUBSET_1;

begin

definition
  struct 1-sorted(# carrier -> set #);
end;

definition
  struct (1-sorted) ZeroStr(# carrier -> set,
                             Zero -> Element of the carrier #);
end;

definition let S be 1-sorted;
 attr S is empty means
:Def1:  the carrier of S is empty;
end;

registration
  cluster empty 1-sorted;
  existence
   proof take T = 1-sorted(#{}#);
    thus the carrier of T is empty;
   end;
end;

registration
 cluster non empty 1-sorted;
 existence
  proof consider A being non empty set;
   take 1-sorted(#A#);
   thus the carrier of 1-sorted(#A#) is non empty;
  end;
end;

registration
 cluster non empty ZeroStr;
 existence
  proof consider A being non empty set, a being Element of A;
   take ZeroStr(#A,a#);
   thus the carrier of ZeroStr(#A,a#) is non empty;
  end;
end;

registration let S be non empty 1-sorted;
 cluster the carrier of S -> non empty;
 coherence by Def1;
end;

definition let S be 1-sorted;
 mode Element of S is Element of the carrier of S;
 mode Subset of S is Subset of the carrier of S;
 mode Subset-Family of S is Subset-Family of the carrier of S;
end;

registration let S be non empty 1-sorted;
 cluster non empty Subset of S;
 existence
  proof consider A being non empty Subset of S;
   take A;
   thus thesis;
  end;
end;

:: Added by AK on 2005.09.22
:: Moved from ALG_1, GROUP_6, PRE_TOPC, POLYNOM1

definition
  let S be 1-sorted, X be set;
  mode Function of S,X is Function of the carrier of S, X;
  mode Function of X,S is Function of X, the carrier of S;
end;

definition
  let S, T be 1-sorted;
  mode Function of S,T is Function of the carrier of S, the carrier of T;
end;

:: from PRE_TOPC, 2006.12.02, AT

definition let T be 1-sorted;
  func {}T -> Subset of T equals {};
   coherence
   proof
     {} = {}the carrier of T;
    hence thesis;
   end;
  func [#]T -> Subset of T equals the carrier of T;
   coherence
   proof
     the carrier of T = [#]the carrier of T;
    hence thesis;
   end;
end;

registration let T be 1-sorted;
  cluster {}T -> empty;
  coherence;
end;

registration let T be empty 1-sorted;
  cluster [#]T -> empty;
  coherence by Def1;
end;

registration let T be non empty 1-sorted;
  cluster [#]T -> non empty;
  coherence;
end;

::Moved from TOPREAL1 on 2005.09.22

definition
  let S be 1-sorted;
  mode FinSequence of S is FinSequence of the carrier of S;
end;

::Moved from YELLOW18, AK, 21.02.2006

definition
 let S be 1-sorted;
 mode ManySortedSet of S is ManySortedSet of the carrier of S;
end;

