:: The Banach Space $l^p$
::  by Yasumasa Suzuki
::
:: Received September 25, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies PRE_TOPC, NORMSP_1, RLVECT_1, FUNCT_1, ARYTM, ARYTM_1, ARYTM_3,
      RELAT_1, ABSVALUE, ORDINAL2, RLSUB_1, SEQ_1, SEQ_2, SERIES_1, SUPINF_2,
      PROB_1, BHSP_3, RSSPACE, RSSPACE3, POWER, LP_SPACE, LOPBAN_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, MCART_1, RELAT_1, FUNCT_1,
      PARTFUN1, FUNCT_2, PRE_TOPC, DOMAIN_1, STRUCT_0, XCMPLX_0, XREAL_0,
      ORDINAL1, NUMBERS, REAL_1, NAT_1, RLVECT_1, COMPLEX1, RLSUB_1, NORMSP_1,
      SEQ_1, SEQ_2, SEQM_3, SERIES_1, POWER, RSSPACE, RSSPACE3, LOPBAN_1;
 constructors MCART_1, FUNCT_3, BINOP_1, ARYTM_0, REAL_1, XREAL_0, NAT_1,
      ABSVALUE, SQUARE_1, FUNCT_2, XCMPLX_0, SUBSET_1, MEMBERED, DOMAIN_1,
      SEQ_1, SEQ_2, SEQM_3, SERIES_1, SEQ_4, SUPINF_2, METRIC_1, FUNCOP_1,
      FUNCSDOM, PROB_1, PREPOWER, POWER, RLVECT_1, VECTSP_1, FRAENKEL,
      PARTFUN1, RLSUB_1, NORMSP_1, BHSP_2, BHSP_3, RSSPACE, RSSPACE3, LOPBAN_1;
 registrations SUBSET_1, RELSET_1, STRUCT_0, ORDINAL2, RLVECT_1, XREAL_0,
      MEMBERED, SEQ_1, RSSPACE3, LOPBAN_1;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;
 definitions TARSKI;
 theorems XBOOLE_0, RELAT_1, AXIOMS, TARSKI, ABSVALUE, ZFMISC_1, REAL_1, SEQ_1,
      SEQM_3, SERIES_1, COMSEQ_3, FUNCT_1, NAT_1, SEQ_2, FUNCT_2, RLVECT_1,
      RLSUB_1, NORMSP_1, XREAL_0, RSSPACE2, XCMPLX_1, SEQ_4, REAL_2, POWER,
      RSSPACE, RSSPACE3, LOPBAN_1, HOLDER_1;
 schemes NAT_1, SEQ_1, FUNCT_2, XBOOLE_0;

begin :: The Real Norm Space of l^p Real Sequences

definition
  let x be Real_Sequence;
  let p be Real;
  func x rto_power p -> Real_Sequence means :Def1:
  for n be Nat holds it.n = abs(x.n) to_power p;
     existence
     proof
    deffunc F(set)=( abs(x.$1) ) to_power p;
A1:     ex q1 be  Real_Sequence st
        for n be Nat holds q1.n=F(n) from SEQ_1:sch 1;
        consider q1 be Real_Sequence such that
A2:    for n be Nat holds
        q1.n= abs(x.n) to_power p by A1;
        take IT=q1;
        thus thesis by A2;
     end;
     uniqueness
     proof
      let a1,a2 be Real_Sequence;
      assume that
A3:   for n be Nat holds a1.n = abs(x.n) to_power p and
A4:   for n be Nat holds a2.n = abs(x.n) to_power p;
        for s be set st s in NAT holds a1.s = a2.s
       proof
        let s be set such that
A5:      s in NAT;
        a1.s = abs(x.s) to_power p by A3,A5
              .= a2.s by A4,A5;
        hence thesis;
       end;
       hence thesis by SEQ_1:8;
     end;
     end;

definition
  let p be Real;
  assume A1:p >= 1;
  func the_set_of_RealSequences_l^p
  -> non empty Subset of the carrier of Linear_Space_of_RealSequences means
     :Def2:
     for x being set holds x in it iff
     (x in the_set_of_RealSequences & seq_id(x) rto_power p is summable);
   existence
   proof
   defpred P[set] means seq_id($1) rto_power p is summable;
   consider IT being set such that
A2:for x being set holds x in IT
    iff x in the_set_of_RealSequences & P[x] from XBOOLE_0:sch 1;
A3:IT is non empty
   proof
      seq_id(Zeroseq) rto_power p is summable
      proof
A4:    p > 0 by A1;
         reconsider rseq=seq_id(Zeroseq) rto_power p as Real_Sequence;
         now let n be Nat;
         thus rseq.n =abs((seq_id(Zeroseq)).n) to_power p by Def1
                    .=abs(0) to_power p by RSSPACE:def 6
                    .=0 to_power p by ABSVALUE:7
                    .=0 by A4,POWER:def 2;
       end;
       then rseq is absolutely_summable by COMSEQ_3:3;
       hence thesis by SERIES_1:40;
     end;
     hence thesis by A2;
  end;
IT is Subset of the_set_of_RealSequences
   proof
     for x be set st x in IT holds x in the_set_of_RealSequences by A2;
     hence thesis by TARSKI:def 3;
   end;
   hence thesis by A2,A3,RSSPACE:def 7;
  end;
uniqueness
     proof
   let X1,X2 be non empty Subset of the carrier
   of Linear_Space_of_RealSequences;
   assume that
A5: for x being set holds x in X1 iff
     (x in the_set_of_RealSequences &  seq_id(x) rto_power p is summable)
   and
A6: for x being set holds x in X2 iff
     (x in the_set_of_RealSequences &  seq_id(x) rto_power p is summable);
A7: X1 c= X2
    proof
      let x be set;
      assume x in X1; then
      x in the_set_of_RealSequences &
      (x in the_set_of_RealSequences & seq_id(x) rto_power p is summable)
      by A5;
      hence x in X2 by A6;
   end;
   X2 c= X1
   proof
       let x be set;
        assume x in X2; then
        x in the_set_of_RealSequences & (x in the_set_of_RealSequences
        & seq_id(x) rto_power p is summable) by A6;
      hence x in X1 by A5;
    end;
  hence thesis by A7,XBOOLE_0:def 10;
  end;
end;

 reserve x1,x2,y1,y2,p,q,a,b,c for Real;

Lm1:
x1 >= 0 & y1 > 0 implies x1 to_power y1 >= 0
proof
assume that
A1:x1 >= 0 and
A2:y1 > 0;
   x1>0 or x1=0 by A1;
hence x1 to_power y1 >= 0 by A2,POWER:39,def 2;
end;

Lm2:
y1>0 & x1>=0 & x2>=0 implies
  (x1*x2) to_power y1 = (x1 to_power y1) * (x2 to_power y1)
proof
assume
A1: y1>0 & x1>=0 & x2>=0;
per cases by A1;
   suppose x1>0 & x2>0;
    hence (x1*x2) to_power y1=(x1 to_power y1) * (x2 to_power y1) by POWER:35;
  end;
    suppose A2:x1>0 & x2=0;
    x2 to_power y1 = 0 by A1,A2,POWER:def 2;
      hence (x1*x2) to_power y1=(x1 to_power y1) * (x2 to_power y1) by A2;
    end;
    suppose A3:x1=0 & x2>0;
    x1 to_power y1 = 0 by A1,A3,POWER:def 2;
      hence (x1*x2) to_power y1=(x1 to_power y1) * (x2 to_power y1) by A3;
    end;
    suppose A4: x1=0 & x2=0;
     x2 to_power y1 = 0 by A1,A4,POWER:def 2;
      hence (x1*x2) to_power y1=(x1 to_power y1) * (x2 to_power y1) by A4;
    end;
end;

theorem Th1:
  a >= 0 & a < b & c > 0 implies a to_power c < b to_power c
proof
   a = 0 & c > 0 implies a to_power c = 0 by POWER:def 2;
  hence thesis by POWER:39,42;
end;

Lm3:
 for p be Real st 1 = p
 for a,b be Real_Sequence
 for n be Nat holds
  (Partial_Sums((a + b) rto_power p).n) to_power (1/p) <=
  (Partial_Sums(a rto_power p).n) to_power (1/p) +
  (Partial_Sums(b rto_power p).n) to_power (1/p)
   proof
      let p be Real such that
A1:p=1;
      let a,b be Real_Sequence;
      let n be Nat;
A2: now let n be Nat;
      thus ((a + b) rto_power p).n = abs((a + b).n) to_power p by Def1
      .=abs((a + b).n) by A1,POWER:30;thus
    (a rto_power p).n = abs(a .n) to_power p by Def1
      .=abs(a .n) by A1,POWER:30;thus
    (b rto_power p).n = abs(b .n) to_power p by Def1
      .=abs(b .n) by A1,POWER:30;
      end; then
A3: (a + b) rto_power p = abs(a + b) by SEQ_1:16;
A4: a rto_power p = abs(a) by A2,SEQ_1:16;
A5: b rto_power p = abs(b) by A2,SEQ_1:16;
A6: (Partial_Sums((a + b) rto_power p).n) to_power (1/p)
      =(Partial_Sums(abs(a + b)).n) by A1,A3,POWER:30;
A7: (Partial_Sums(a rto_power p).n) to_power (1/p)
      =(Partial_Sums(abs(a)).n) by A1,A4,POWER:30;
A8: (Partial_Sums(b rto_power p).n) to_power (1/p)
      =(Partial_Sums(abs(b)).n) by A1,A5,POWER:30;
A9:for n be Nat holds abs((a + b).n) <= abs(b .n) + abs(a .n)
   proof
    let n be Nat;
    abs((a + b).n)=abs(a.n + b.n) by SEQ_1:11;
    hence thesis by ABSVALUE:13;
    end;
   deffunc F(Nat) =abs(a .($1)) + abs(b .($1));
   consider c be Real_Sequence such that
A10:for n be Nat holds c.n= F(n) from SEQ_1:sch 1;
A11:now let n be Nat;
A12:abs((a + b).n) <= abs(b .n) + abs(a .n) by A9;
A13:abs((a + b).n)=abs(a+b).n by SEQ_1:16;
   thus abs(a+b).n <= c.n by A10,A12,A13;
 end;
A14:now let n be Nat;
A15: abs(b.n)=abs (b).n by SEQ_1:16;
A16: abs(a.n)=abs (a).n by SEQ_1:16;
    thus c.n=abs (a).n + abs (b).n by A10,A15,A16
        .=(abs a + abs b).n by SEQ_1:11;
     end;
 A17: c = abs a + abs b
       proof
       for n be set st n in NAT holds
       c.n = (abs (a) + abs (b)).n by A14;
   hence c = (abs (a) + abs (b)) by SEQ_1:8;
  end;
 Partial_Sums(abs(a+b)).n<=Partial_Sums(c).n by A11,SERIES_1:17; then
A18: Partial_Sums(abs(a+b)).n <=
      Partial_Sums(abs (a) + abs (b)).n by A17;
      Partial_Sums(abs (a) + abs (b)) =
      Partial_Sums(abs a) + Partial_Sums(abs(b)) by SERIES_1:8;
       hence (Partial_Sums((a + b) rto_power p).n) to_power (1/p)
       <=(Partial_Sums(a rto_power p).n) to_power (1/p)
       + (Partial_Sums(b rto_power p).n) to_power (1/p)
         by A6,A7,A8,A18,SEQ_1:11;
   end;

theorem Th2:
 for p be Real st 1 <= p
 for a,b be Real_Sequence
 for n be Nat holds
  (Partial_Sums((a + b) rto_power p).n) to_power (1/p) <=
  (Partial_Sums(a rto_power p).n) to_power (1/p) +
  (Partial_Sums(b rto_power p).n) to_power (1/p)
   proof
   let p be Real such that
   A1: 1<=p;
   let a,b be Real_Sequence;
   set ap = a rto_power p;
   set bp = b rto_power p;
   set ab= ((a + b) rto_power p);
   let n be Nat;
   now per cases by A1,REAL_1:def 5;
   case
   A2:p > 1;
now let n be Nat;
A3:  (a rto_power p).n =abs(a .n) to_power p by Def1;
A4:  ((a + b) rto_power p).n =abs((a + b).n) to_power p by Def1
      .=abs(a.n+b.n) to_power p by SEQ_1:11;
        thus ap.n = abs(a .n) to_power p by A3;
        thus bp.n = abs(b .n) to_power p by Def1;
        thus ab.n = abs(a.n+b.n) to_power p by A4;
    end;
    hence (Partial_Sums((a + b) rto_power p).n) to_power (1/p) <=
        (Partial_Sums(a rto_power p).n) to_power (1/p) +
        (Partial_Sums(b rto_power p).n) to_power (1/p) by A2,HOLDER_1:7;
    end;
    case p=1;
    hence (Partial_Sums((a + b) rto_power p).n) to_power (1/p) <=
     (Partial_Sums(a rto_power p).n) to_power (1/p) +
     (Partial_Sums(b rto_power p).n) to_power (1/p) by Lm3;
    end;
   end;
 hence thesis;
 end;

Lm4:
for a,b be Real_Sequence
for p be Real st 1 = p &
  ((a rto_power p) is summable) &
  ((b rto_power p) is summable) holds
  ((a+b) rto_power p) is summable &
 (Sum((a + b) rto_power p)) to_power (1/p) <=
 (Sum(a rto_power p)) to_power (1/p) + (Sum(b rto_power p)) to_power (1/p)
proof
 let a,b be Real_Sequence;
 let p be Real such that
A1:p=1 and
A2:a rto_power p is summable and
A3:b rto_power p is summable;
A4: now let n be Nat;thus
       ((a + b) rto_power p).n = abs((a + b).n) to_power p by Def1
      .=abs((a + b).n) by A1,POWER:30;thus
      (a rto_power p).n = abs(a .n) to_power p by Def1
      .=abs(a .n) by A1,POWER:30;
      thus (b rto_power p).n = abs(b .n) to_power p by Def1
      .=abs(b .n) by A1,POWER:30;
      end; then
A5: (a + b) rto_power p = abs(a + b) by SEQ_1:16;
A6: a rto_power p = abs(a) by A4,SEQ_1:16;
A7: b rto_power p = abs(b) by A4,SEQ_1:16;
A8:(Sum((a + b) rto_power p)) to_power (1/p)
      =Sum(abs(a + b)) by A1,A5,POWER:30;
A9:(Sum((a) rto_power p)) to_power (1/p)
      =Sum(abs(a)) by A1,A6,POWER:30;
A10:(Sum((b) rto_power p)) to_power (1/p)
      =Sum(abs(b)) by A1,A7,POWER:30;
A11:abs(a) is summable by A2,A6;
A12:abs(b) is summable by A3,A7;
A13:a is absolutely_summable by A11,SERIES_1:def 5;
A14:b is absolutely_summable by A12,SERIES_1:def 5;
A15:a = seq_id (a) & b = seq_id (b) by RSSPACE:3;
A16:seq_id (a) is absolutely_summable &
      seq_id (b) is absolutely_summable by A13,A14,A15;
      reconsider a1=a as VECTOR of l1_Space by A16,RSSPACE3:8;
      reconsider b1=b as VECTOR of l1_Space by A16,RSSPACE3:8;
A17: ||.a1+b1.|| <= ||.a1.|| + ||.b1.|| by RSSPACE3:9;
A18: ||.a1+b1.|| = (the norm of l1_Space).(a1+b1) by NORMSP_1:def 1
       .=Sum(abs(seq_id(a1+b1))) by RSSPACE3:def 2,def 3
       .=Sum(abs(seq_id(seq_id(a1)+seq_id(b1)))) by RSSPACE3:8
       .=Sum(abs(seq_id(a1)+seq_id(b1))) by RSSPACE:3;
A19: ||.a1.|| = (the norm of l1_Space).(a1) by NORMSP_1:def 1
       .=Sum(abs(seq_id(a1))) by RSSPACE3:def 2,def 3;
A20: ||.b1.|| = (the norm of l1_Space).(b1) by NORMSP_1:def 1
       .=Sum(abs(seq_id(b1))) by RSSPACE3:def 2,def 3;
A21: Sum(abs(seq_id(a1)+seq_id(b1))) <=
       Sum(abs(seq_id(a1)))+ Sum(abs(seq_id(b1))) by A17,A18,A19,A20;
A22: seq_id(a1)=a by RSSPACE:3;
A23: seq_id(b1)=b by RSSPACE:3;
A24: Sum(abs(a+b)) <= Sum(abs(a)) + Sum(abs(b)) by A21,A22,A23;
A25:(abs(a) + abs(b) is summable) &
       Sum(abs(a) + abs(b))=Sum(abs(a))+Sum(abs(b))
     by A11,A12,SERIES_1:10;
    deffunc F(Nat) =abs(a .($1)) + abs(b .($1));
    consider c be Real_Sequence such that
A26:for n be Nat holds c.n= F(n) from SEQ_1:sch 1;
A27:now let n be Nat;
A28:abs(a+b).n=abs((a+b).n) by SEQ_1:16;
    hence abs(a+b).n>= 0 by ABSVALUE:5;
    abs((a+b).n) =abs((a.n)+(b.n)) by SEQ_1:11; then
A29:abs(a+b).n <= abs(a.n)+abs(b.n) by A28,ABSVALUE:13; thus
 c.n=abs(a.n)+abs(b.n) by A26;
A30: abs(a.n)+abs(b.n) = abs(a).n+abs(b.n) by SEQ_1:16
     .=abs(a).n+abs(b).n by SEQ_1:16;thus
     c.n =abs(a).n+abs(b).n by A26,A30;
     thus abs(a+b).n <= c.n by A26,A29;
end;
c = abs(a) + abs(b) by A27,SEQ_1:11;
 hence (a + b) rto_power p is summable by A5,A25,A27,SERIES_1:24;
thus (Sum((a + b) rto_power p)) to_power (1/p)
       <=(Sum((a) rto_power p)) to_power (1/p) +
          (Sum((b) rto_power p)) to_power (1/p) by A8,A9,A10,A24;
end;

theorem Th3:
for a,b be Real_Sequence
for p be Real st 1 <= p &
  (a rto_power p) is summable &
  (b rto_power p) is summable
 holds ((a+b) rto_power p) is summable &
 (Sum((a + b) rto_power p)) to_power (1/p) <=
 (Sum(a rto_power p)) to_power (1/p) +
 (Sum(b rto_power p)) to_power (1/p)
 proof
     let a,b be Real_Sequence;
     let p be Real such that
A1: 1<=p and
A2:((a rto_power p) is summable) and
A3:((b rto_power p) is summable);
    set ap = a rto_power p;
    set bp = b rto_power p;
    set ab= ((a + b) rto_power p);
A4:now let n be Nat;
      A5:  (a rto_power p).n =abs(a .n) to_power p by Def1;
      A6:  (b rto_power p).n =abs(b .n) to_power p by Def1;
      A7:  ((a + b) rto_power p).n =abs((a + b).n) to_power p by Def1
            .=abs(a.n+b.n) to_power p by SEQ_1:11;
            thus ap.n= abs(a .n) to_power p by A5;
            thus bp.n= abs(b .n) to_power p by A6;
            thus ab.n= abs(a.n+b.n) to_power p by A7;
          end;
   now per cases by A1,REAL_1:def 5;
      case
      A8:p > 1;
        thus Sum(ab) to_power (1/p) <= Sum ap to_power (1/p)
         + Sum(bp) to_power (1/p)
         & ab is summable by A2,A3,A4,A8,HOLDER_1:13;
         end;
       case
       A9:p=1;
        thus Sum(ab) to_power (1/p) <= Sum(ap) to_power (1/p)
                 + Sum(bp) to_power (1/p)
         & ab is summable by A2,A3,A9,Lm4;
       end;
      end;
    hence thesis;
 end;

theorem Th4:
for p be Real st 1 <= p holds
the_set_of_RealSequences_l^p is lineary-closed
proof
 let p be Real such that
 A1: p >=1;
 set W = the_set_of_RealSequences_l^p;
  A2:for v,u be VECTOR of Linear_Space_of_RealSequences st
      v in the_set_of_RealSequences_l^p &
      u in the_set_of_RealSequences_l^p
        holds v + u in the_set_of_RealSequences_l^p
 proof
     let v,u be VECTOR of Linear_Space_of_RealSequences such that
A3: v in W & u in W;
A4:  seq_id(v +u) rto_power p is summable
    proof
    A5: seq_id(v) rto_power p is summable by A1,A3,Def2;
    A6: seq_id(u) rto_power p is summable by A1,A3,Def2;
A7: p > 0 by A1;
    then A8: p <> 0;
A9:  1/p > 0 by A7,REAL_2:127;
    set p1=1/p;
    set vp=seq_id(v) rto_power p;
    set up=seq_id(u) rto_power p;
A10:  p * (1/p) = (p*1)/p by XCMPLX_1:75
              .= 1 by A8,XCMPLX_1:60;
A11:  vp is summable by A5;
A12:  up is summable by A6;
A13: now
        let n be Nat;
     thus A14: vp.n=abs((seq_id(v)).n) to_power p by Def1; thus
           A15: up.n=abs((seq_id(u)).n) to_power p by Def1; thus
           A16: 0 <= abs((seq_id(v)).n) by ABSVALUE:5; thus
           A17: 0 < abs((seq_id(v)).n) or 0=abs((seq_id(v)).n) by A16;
           thus 0 <= vp.n by A7,A14,A17,POWER:39,def 2; thus
           A18: 0 <= abs((seq_id(u)).n) by ABSVALUE:5; thus
           A19: 0 < abs((seq_id(u)).n) or 0=abs((seq_id(u)).n)
         by A18; thus 0 <= up.n by A7,A15,A19,POWER:39,def 2;
    end;
A20: Partial_Sums(up) is non-decreasing by A13,SERIES_1:19;
A21: Partial_Sums(up) is bounded_above by A12,A13,SERIES_1:20;
        consider r1 be real number such that
         A22:for n be set st n in dom Partial_Sums(up)
        holds Partial_Sums(up).n<r1 by A21,SEQ_2:def 1;
        reconsider r1 as Real by XREAL_0:def 1;
        A23: dom Partial_Sums(up)=NAT by SEQ_1:4;
       A24:now
          let n be set such that A25:n in dom Partial_Sums(up);
          A26:Partial_Sums(up).n<r1 by A22,A25;
          reconsider n1=n as Nat by A23,A25;
          A27:Partial_Sums(up).n1<r1 by A26;
   A28: Partial_Sums(up).0<=Partial_Sums(up).n1 by A20,SEQM_3:21;
   0<=up.0 by A13;
   then 0<=Partial_Sums(up).0 by SERIES_1:def 1;
hence (Partial_Sums(up).n) to_power p1 < r1 to_power p1
 by A9,A27,A28,Th1;
end;
deffunc F1(set)=(Partial_Sums(up).$1) to_power p1;
A29:ex q1 be Real st for n be set st n in dom Partial_Sums(up)
     holds (Partial_Sums(up).n) to_power p1 <q1 by A24;
A30: Partial_Sums(vp) is non-decreasing by A13,SERIES_1:19;
A31: Partial_Sums(vp) is bounded_above by A11,A13,SERIES_1:20;
      consider r be real number such that
A32:for n be set st n in dom Partial_Sums(vp)
     holds Partial_Sums(vp).n<r by A31,SEQ_2:def 1;
     reconsider r as Real by XREAL_0:def 1;
A33: dom Partial_Sums(vp)=NAT by SEQ_1:4;
A34:now
let n be set such that A35:n in dom Partial_Sums(vp);
A36:Partial_Sums(vp).n<r by A32,A35;
reconsider n1=n as Nat by A33,A35;
A37:Partial_Sums(vp).n1<r by A36;
   A38: Partial_Sums(vp).0<=Partial_Sums(vp).n1 by A30,SEQM_3:21;
   0<=vp.0 by A13;
   then 0<=Partial_Sums(vp).0 by SERIES_1:def 1;
hence (Partial_Sums(vp).n) to_power p1 < r to_power p1
 by A9,A37,A38,Th1;
end;
A39:ex q be Real st for n be set st n in dom Partial_Sums(vp)
     holds (Partial_Sums(vp).n) to_power p1 <q by A34;
consider q be Real such that
A40: for n be set st n in dom Partial_Sums(vp)
     holds (Partial_Sums(vp).n) to_power p1 <q by A39;
consider q1 be Real such that
A41: for n be set st n in dom Partial_Sums(up)
     holds (Partial_Sums(up).n) to_power p1 <q1 by A29;
set g = q + q1;
v in the_set_of_RealSequences by A1,A3,Def2;then
reconsider vq=v as Real_Sequence by RSSPACE:def 1;
u in the_set_of_RealSequences by A1,A3,Def2;then
reconsider uq=u as Real_Sequence by RSSPACE:def 1;
A42:  vp = vq rto_power p by RSSPACE:3;
A43:  up = uq rto_power p by RSSPACE:3;
A44:  for n be Nat holds
 (Partial_Sums((vq + uq) rto_power p).n) to_power (1/p) <=
 (Partial_Sums(up).n) to_power (1/p) +
 (Partial_Sums(vp).n) to_power (1/p) by A1,A42,A43,Th2;
A45: now
let n be Nat;
    NAT=dom Partial_Sums(up) by SEQ_1:4; then
A46:(Partial_Sums(up).n) to_power p1 <q1 by A41;
   NAT=dom Partial_Sums(vp) by SEQ_1:4; then
   (Partial_Sums(vp).n) to_power (1/p) < q by A40; then
 A47:(Partial_Sums(up).n) to_power (1/p) +
    (Partial_Sums(vp).n) to_power (1/p) < g by A46,REAL_1:67;
 (Partial_Sums((vq + uq) rto_power p).n) to_power (1/p) <=
 (Partial_Sums(up).n) to_power (1/p) +
 (Partial_Sums(vp).n) to_power (1/p) by A44; then
A48:  (Partial_Sums((vq + uq) rto_power p).n) to_power (1/p)
       < g by A47,AXIOMS:22;
A49:now
assume
(Partial_Sums((vq + uq) rto_power p).n) > 0;
hence (Partial_Sums((vq + uq) rto_power p).n) to_power (1/p) to_power p =
(Partial_Sums((vq + uq) rto_power p).n) to_power ((1/p)*p) by POWER:38
 .= (Partial_Sums((vq + uq) rto_power p).n) by A10,POWER:30;
end;
A50:now
assume
A51:(Partial_Sums((vq + uq) rto_power p).n) = 0;
hence (Partial_Sums((vq + uq) rto_power p).n) to_power (1/p) to_power p =
0 to_power p by A9,POWER:def 2
   .= (Partial_Sums((vq + uq) rto_power p).n) by A7,A51,POWER:def 2;
end;
thus A52: now
     let n be Nat;
A53:((vq + uq) rto_power p).n =abs((vq + uq).n) to_power p by Def1;
thus
A54:0 < abs((vq + uq).n) or 0=abs((vq + uq).n) by ABSVALUE:5;
thus 0 <= ((vq + uq) rto_power p).n by A7,A53,A54,POWER:39,def 2;
end;
then
A55:Partial_Sums((vq + uq) rto_power p) is non-decreasing by SERIES_1:19;
A56:now
   A57: Partial_Sums((vq + uq) rto_power p).0
       <=Partial_Sums((vq + uq) rto_power p).n by A55,SEQM_3:21;
   0<=((vq + uq) rto_power p).0 by A52;
  hence 0<=Partial_Sums((vq + uq) rto_power p).0 by SERIES_1:def 1;
   hence 0<=Partial_Sums((vq + uq) rto_power p).n by A57;
end; then
(Partial_Sums((vq + uq) rto_power p).n) to_power (1/p)
         >=0 by A9,Lm1;
hence (Partial_Sums((vq + uq) rto_power p).n) < g to_power p
             by A7,A48,A49,A50,A56,Th1;
end;

for n be Nat holds (Partial_Sums((vq + uq) rto_power p) is bounded_above &
 0 <= ((vq + uq) rto_power p).n ) by A45,SEQ_2:def 3; then
A58:((vq + uq) rto_power p) is summable by SERIES_1:20;
A59:seq_id(v)=vq by RSSPACE:3;
seq_id(u)=uq by RSSPACE:3; then
A60: ((seq_id(v)+seq_id(u)) rto_power p) is summable by A58,A59;
seq_id(v)+seq_id(u) =seq_id(seq_id(v)+seq_id(u)) by RSSPACE:3
                   .=seq_id(v+u) by RSSPACE:4,def 7;
hence ((seq_id(v+u)) rto_power p) is summable by A60;
end;
A61:v + u =seq_id(v)+seq_id(u) by RSSPACE:4,def 7
                   .=seq_id(seq_id(v)+seq_id(u)) by RSSPACE:3
                   .=seq_id(v+u) by RSSPACE:4,def 7;
   reconsider w = v + u as Real_Sequence by A61;
   reconsider w as set;
   w in the_set_of_RealSequences by RSSPACE:def 1;
hence v +u in W by A1,A4,Def2;
end;
    for a be Real for v be VECTOR of Linear_Space_of_RealSequences
    st v in W holds a * v in W
proof
let a be Real;
let v be VECTOR of Linear_Space_of_RealSequences such that
    A62: v in W;
 seq_id(a*v) rto_power p is summable
proof
  set vp = seq_id(v) rto_power p;
A63:vp is summable by A1,A62,Def2;
  1 > 0 & p >= 1 by A1; then
A64: p > 0;
  set p1=1/p;
A65:  1 <= p by A1;
A66:  0 <  p by A65;
A67:now
let n be Nat;
A68:vp.n=abs((seq_id(v)).n) to_power p by Def1;thus
A69:  0 <= abs((seq_id(v)).n) by ABSVALUE:5; thus
A70:  0 < abs((seq_id(v)).n) or 0=abs((seq_id(v)).n) by A69;
   thus 0 <= vp.n by A66,A68,A70,POWER:39,def 2;
end;
A71: Partial_Sums(vp) is bounded_above by A63,A67,SERIES_1:20;
A72: ex r be real number st for n be set st n in dom Partial_Sums(vp)
     holds Partial_Sums(vp).n<r by A71,SEQ_2:def 1;
 set q1 = seq_id(a*v) rto_power p;
 reconsider a1=a as real number;
      v in the_set_of_RealSequences by A1,A62,Def2;then
 reconsider vq=v as Real_Sequence by RSSPACE:def 1;
 reconsider v1=v as VECTOR of
      RLSStruct(# the_set_of_RealSequences,Zeroseq,l_add,l_mult #)
     by A1,A62,Def2;
A73:  seq_id(a*v)= seq_id(a(#)seq_id(v)) by RSSPACE:5,def 7
                .=(a(#)seq_id(v)) by RSSPACE:3;
A74:  for n be Nat holds
    Partial_Sums(seq_id(a*v) rto_power p).n
    = (abs(a) to_power p)* Partial_Sums(seq_id(v) rto_power p).n
proof
A75:now
let n be Nat;
A76: abs(a)>=0 by ABSVALUE:5;
A77: abs((seq_id(v)).n) >= 0 by ABSVALUE:5;
A78:     (seq_id(a*v) rto_power p).n
       = ((a(#)seq_id(v)) rto_power p).n by A73;
A79:    (a(#)seq_id(v)).n = a * (seq_id(v)).n by SEQ_1:13;
        ((a(#)seq_id(v)) rto_power p).n
       =  abs((a(#)seq_id(v)).n) to_power p by Def1
      .=((abs(a)* abs((seq_id(v)).n))) to_power p by A79,ABSVALUE:10
      .=(abs(a) to_power p)
                  * ((abs((seq_id(v)).n)) to_power p) by A64,A76,A77,Lm2;
hence (seq_id(a*v) rto_power p).n
       = (abs(a) to_power p)
            * ((seq_id(v)) rto_power p).n by A78,Def1
      .= ((abs(a) to_power p)
           (#) ((seq_id(v)) rto_power p)).n by SEQ_1:13;
end;
A80:(seq_id(a*v) rto_power p)= ((abs(a) to_power p)
                             (#) ((seq_id(v)) rto_power p))
proof
for n be set st n in  NAT holds
(seq_id(a*v) rto_power p).n= ((abs(a) to_power p)
                   (#) ((seq_id(v)) rto_power p)).n by A75;
hence
(seq_id(a*v) rto_power p)= ((abs(a) to_power p)
                             (#) ((seq_id(v)) rto_power p)) by SEQ_1:8;
end;
A81:Partial_Sums(((abs(a) to_power p)
                             (#) ((seq_id(v)) rto_power p)))
   = ((abs(a) to_power p) (#)Partial_Sums( ((seq_id(v)) rto_power p)))
                       by SERIES_1:12;
A82:Partial_Sums(seq_id(a*v) rto_power p) = ((abs(a) to_power p)
                 (#)Partial_Sums( ((seq_id(v)) rto_power p))) by A80,A81;
let n be Nat;
thus Partial_Sums(seq_id(a*v) rto_power p).n
               =(abs(a) to_power p)
                * Partial_Sums ((seq_id(v)) rto_power p).n by A82,SEQ_1:13;
end;
consider r be real number such that
A83:for n be set st n in dom Partial_Sums(seq_id(v) rto_power p)
     holds Partial_Sums(vp).n<r by A72;
  0 <=abs(a) by ABSVALUE:5; then
A84: (0<(abs(a) to_power p)) or 0=(abs(a) to_power p) by A66,Lm1;
A85: now
     let n be set such that
A86:  n in dom Partial_Sums(seq_id(v) rto_power p);
  dom Partial_Sums(seq_id(v) rto_power p)= NAT by SEQ_1:3;
hence n in dom Partial_Sums(seq_id(a*v) rto_power p) by A86,SEQ_1:3;
A87:  Partial_Sums(seq_id(v) rto_power p).n<r by A83,A86;
thus ( ((abs(a) to_power p) *
              Partial_Sums(seq_id(v) rto_power p).n
                 < (abs(a) to_power p) * r )) or ( ((abs(a) to_power p) *
              Partial_Sums(seq_id(v) rto_power p).n )
                = (abs(a) to_power p) * r ) by A84,A87,REAL_1:70;
 end;
A88: for n be set st
     n in dom Partial_Sums(seq_id(a*v) rto_power p)
     holds Partial_Sums(seq_id(a*v) rto_power p).n
       <(abs(a) to_power p) * r or
     Partial_Sums(seq_id(a*v) rto_power p).n = (abs(a) to_power p) * r
proof
 let n be set such that
A89:n in dom Partial_Sums(seq_id(a*v) rto_power p);
A90: n in NAT by A89,SEQ_1:3;
then n in dom Partial_Sums(seq_id(v) rto_power p) by SEQ_1:3;
then
A91:(abs(a) to_power p)
        * Partial_Sums(seq_id(v) rto_power p).n
            < (abs(a) to_power p) * r or
     ( ((abs(a) to_power p) *
              Partial_Sums(seq_id(v) rto_power p).n )
                = (abs(a) to_power p) * r ) by A85;
reconsider n1=n as Nat by A90;
Partial_Sums(seq_id(a*v) rto_power p).n1
       <(abs(a) to_power p) * r or
     Partial_Sums(seq_id(a*v) rto_power p).n1
      =(abs(a) to_power p) * r by A74,A91;
hence Partial_Sums(seq_id(a*v) rto_power p).n
       <(abs(a) to_power p) * r or
     Partial_Sums(seq_id(a*v) rto_power p).n
      =(abs(a) to_power p) * r;
end;
A92:  ex r1 be real number st for n be set st n in
    dom Partial_Sums(seq_id(a*v) rto_power p) holds
     Partial_Sums(seq_id(a*v) rto_power p).n<r1
proof
take r1 = (abs(a) to_power p) * r + 1;
reconsider r1 as real number;
A93:for n be set st
     n in dom Partial_Sums(seq_id(a*v) rto_power p)
     holds Partial_Sums(seq_id(a*v) rto_power p).n<r1
     proof
        let n be set such that
A94:n in dom Partial_Sums(seq_id(a*v) rto_power p);
A95: n in NAT by A94,SEQ_1:3;
reconsider n1=n as Nat by A95;
A96:(abs(a) to_power p) * r <(abs(a) to_power p) * r +1 by REAL_1:69;
A97:Partial_Sums(seq_id(a*v) rto_power p).n1
       <(abs(a) to_power p) * r implies
   Partial_Sums(seq_id(a*v) rto_power p).n1
       <(abs(a) to_power p) * r +1 by A96,AXIOMS:22;
 Partial_Sums(seq_id(a*v) rto_power p).n1
      =(abs(a) to_power p) * r implies
   Partial_Sums(seq_id(a*v) rto_power p).n1
       <(abs(a) to_power p) * r +1 by REAL_1:69;
 hence thesis by A88,A94,A97;
     end;
thus thesis by A93;
end;
A98:for n be Nat holds (seq_id(a*v) rto_power p).n >= 0
 proof
   let n be Nat;
A99:     (seq_id(a*v) rto_power p).n
       = ((a(#)seq_id(v)) rto_power p).n by A73;
A100:    (a(#)seq_id(v)).n = a * (seq_id(v)).n by SEQ_1:13;
     set b = (a(#)seq_id(v));
A101:    (b rto_power p).n = abs(a * (seq_id(v)).n) to_power p by A100,Def1;
A102: 1 <= p by A1;
    1 > 0 & p >= 1 by A102; then
A103: p > 0;
 abs(a * (seq_id(v)).n) >= 0 by ABSVALUE:5;
hence (seq_id(a*v) rto_power p).n >= 0 by A99,A101,A103,Lm1;
end;
      Partial_Sums(seq_id(a*v) rto_power p) is bounded_above
    by A92,SEQ_2:def 1;
hence (seq_id(a*v) rto_power p) is summable by A98,SERIES_1:20;
 end;
hence a*v in W by A1,Def2,RSSPACE:def 7;
end;
hence thesis by A2,RLSUB_1:def 1;
end;

theorem Th5:
for p be Real st 1 <= p holds
RLSStruct (# the_set_of_RealSequences_l^p,
        Zero_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
        Add_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
        Mult_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences) #)
is Subspace of Linear_Space_of_RealSequences
proof
  let p be Real such that
A1: 1 <= p;
  the_set_of_RealSequences_l^p is lineary-closed by A1,Th4;
  hence thesis by RSSPACE:13;
end;

theorem Th6:
for p be Real st 1 <= p holds
  RLSStruct (# the_set_of_RealSequences_l^p,
        Zero_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
        Add_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
        Mult_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences)
        #)
      is Abelian add-associative right_zeroed right_complementable
        RealLinearSpace-like by Th5;

theorem Th7:
   for p be Real st 1 <= p holds
   RLSStruct (# the_set_of_RealSequences_l^p,
        Zero_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
        Add_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
        Mult_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences) #)
   is RealLinearSpace by Th6;

Lm5:
 for p be Real holds
  ex NORM be Function of the_set_of_RealSequences_l^p,REAL st
   for x be set st x in the_set_of_RealSequences_l^p holds
    NORM.x =( Sum(seq_id(x) rto_power p) ) to_power (1/p)
proof
  let p be Real;
  deffunc F(set) =( Sum(seq_id($1) rto_power p) ) to_power (1/p);
A1:for z be set st z in the_set_of_RealSequences_l^p holds F(z) in REAL;
   ex f being Function of the_set_of_RealSequences_l^p,REAL st
     for x being set st x in the_set_of_RealSequences_l^p holds
       f.x = F(x) from FUNCT_2:sch 2(A1);
   hence thesis;
end;

definition
  let p be Real;
  func l_norm^p -> Function of the_set_of_RealSequences_l^p, REAL means
    :Def3: for x be set st x in the_set_of_RealSequences_l^p  holds
    it.x = ( Sum(seq_id(x) rto_power p) ) to_power (1/p);
existence by Lm5;
uniqueness
proof
   let NORM1,NORM2 be Function of the_set_of_RealSequences_l^p , REAL such that
A1:(for x be set st x in the_set_of_RealSequences_l^p holds
     NORM1.x = ( Sum(seq_id(x) rto_power p) ) to_power (1/p) ) and
A2:(for x be set st x in the_set_of_RealSequences_l^p holds
     NORM2.x = ( Sum(seq_id(x) rto_power p) ) to_power (1/p));
A3:dom NORM1 = the_set_of_RealSequences_l^p &
     dom NORM2 = the_set_of_RealSequences_l^p by FUNCT_2:def 1;
   for z be set st z in the_set_of_RealSequences_l^p holds NORM1.z = NORM2.z
   proof
     let z be set such that
A4:  z in the_set_of_RealSequences_l^p;
     NORM1.z = ( Sum(seq_id(z) rto_power p) ) to_power (1/p) by A1,A4;
     hence thesis by A2,A4;
  end;
  hence thesis by A3,FUNCT_1:9;
end;
end;

theorem Th8:
for p be Real st 1 <= p holds
   NORMSTR (# the_set_of_RealSequences_l^p,
        Zero_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
        Add_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
        Mult_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
         l_norm^p #) is RealLinearSpace
proof
let p be Real such that
  A1: 1 <= p;
   RLSStruct (# the_set_of_RealSequences_l^p,
          Zero_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
          Add_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
          Mult_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences) #)
     is RealLinearSpace by A1,Th7;
   hence thesis by RSSPACE3:4;
end;

theorem Th9:
for p be Real st p >= 1 holds
NORMSTR (# the_set_of_RealSequences_l^p,
     Zero_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
     Add_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
      Mult_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
     l_norm^p  #) is Subspace of Linear_Space_of_RealSequences
proof
  let p be Real such that
   A1: 1 <= p;
   set l_Space_p = NORMSTR (# the_set_of_RealSequences_l^p,
        Zero_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
        Add_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
        Mult_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
         l_norm^p  #);
 A2:l_Space_p is RealLinearSpace by A1,Th8;
set w1=
RLSStruct (# the_set_of_RealSequences_l^p,
        Zero_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
        Add_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
        Mult_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences)
        #);
set V =Linear_Space_of_RealSequences;
w1 is Subspace of V by A1,Th5;
then the carrier of w1 c= the carrier of V &
    the Zero of w1 = the Zero of V &
    the add of w1 = (the add of V) | [:the carrier of w1,the carrier of w1:] &
    the Mult of w1 = (the Mult of V) | [:REAL, the carrier of w1:]
  by RLSUB_1:def 2;
 hence l_Space_p is Subspace of V by A2,RLSUB_1:def 2;
end;

begin :: The Banach Space of l^p Real Sequences

theorem Th10:
for p be Real st 1 <=p holds
  for lp be non empty NORMSTR st lp
     =  NORMSTR (# the_set_of_RealSequences_l^p,
       Zero_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
       Add_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
       Mult_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
     l_norm^p #) holds
  the carrier of lp = the_set_of_RealSequences_l^p &
 ( for x be set holds x is VECTOR of lp
    iff x is Real_Sequence & seq_id(x) rto_power p is summable ) &
 0.lp = Zeroseq &
 ( for x be VECTOR of lp holds x =seq_id(x) ) &
 ( for x,y be VECTOR of lp holds x+y =seq_id(x)+seq_id(y) ) &
( for r be Real for x be VECTOR of lp holds r*x = r(#)seq_id(x) ) &
( for x be VECTOR of lp holds
   -x = -seq_id(x) & seq_id(-x) = -seq_id(x) ) &
( for x,y be VECTOR of lp holds x-y =seq_id(x)-seq_id(y) ) &
( for x be VECTOR of lp holds
seq_id(x) rto_power p is summable ) &
( for x be VECTOR of
lp holds ||.x.|| = ( Sum(seq_id(x) rto_power p) ) to_power (1/p) )
proof
 let p be Real such that
  A1: 1<= p;
 let lp be non empty NORMSTR such that
  A2:   lp = NORMSTR (# the_set_of_RealSequences_l^p,
           Zero_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
           Add_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
           Mult_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
         l_norm^p  #);
A3:for x be set holds x is Element of lp
     iff x is Real_Sequence & seq_id(x) rto_power p is summable
   proof
     let x be set;
   A4:   x is Element of the carrier of lp iff x is Element of
          the_set_of_RealSequences_l^p by A2;
     x in the_set_of_RealSequences_l^p iff seq_id(x) rto_power p
          is summable & x in the_set_of_RealSequences by A1,Def2;
     hence thesis by A4,RSSPACE:def 1;
     end;
 A5: for x be set holds x is VECTOR of lp implies x =seq_id(x)
   proof
     let x be set;
A6:     x in the_set_of_RealSequences_l^p iff seq_id(x) rto_power p
          is summable & x in the_set_of_RealSequences by A1,Def2;
  x in the_set_of_RealSequences iff x is Real_Sequence by RSSPACE:def 1;
 hence thesis by A2,A6,RSSPACE:3;
 end;
A7:0.lp = Zeroseq
   proof
A8:     the_set_of_RealSequences_l^p is lineary-closed by A1,Th4;
         thus 0.lp = Zero_(the_set_of_RealSequences_l^p
                                       ,Linear_Space_of_RealSequences)
                 by A2,RLVECT_1:def 2
              .= 0.Linear_Space_of_RealSequences
                        by A8,RSSPACE:def 10
              .= Zeroseq by RLVECT_1:def 2,RSSPACE:def 7;
  end;
A9:for x,y be VECTOR of lp holds x+y =seq_id(x)+seq_id(y)
   proof
     let x,y be VECTOR of lp;
A10:   lp is Subspace of Linear_Space_of_RealSequences by A1,A2,Th9;
        reconsider x1=x as
           VECTOR of Linear_Space_of_RealSequences by A10,RLSUB_1:18;
        reconsider y1=y as
           VECTOR of Linear_Space_of_RealSequences by A10,RLSUB_1:18;
     set L1=Linear_Space_of_RealSequences;
     set W = the_set_of_RealSequences_l^p;
A11:        W is lineary-closed by A1,Th4;
          dom (the add of L1)
            = [:the carrier of L1,the carrier of L1:] by FUNCT_2:def 1; then
          dom ((the add of Linear_Space_of_RealSequences) | [:W,W:])
            =[:W,W:] by RELAT_1:91; then
A12:    [x,y] in dom ((the add of Linear_Space_of_RealSequences)|[:W,W:])
          by A2;
         x+y=(the add of lp).[x,y] by RLVECT_1:def 3
            .= ((the add of Linear_Space_of_RealSequences)|[:W,W:]).[x,y]
              by A2,A11,RSSPACE:def 8
             .=(the add of Linear_Space_of_RealSequences).[x,y]
            by A12,FUNCT_1:70
             .=x1+y1 by RLVECT_1:def 3;
          hence thesis by RSSPACE:4,def 7;
          end;
A13:for r be Real for x be VECTOR of lp holds r*x =r(#)seq_id(x)
   proof
     let r be Real;
     let x be VECTOR of lp;
 A14:   lp is Subspace of Linear_Space_of_RealSequences by A1,A2,Th9;
         reconsider x1=x as
           VECTOR of Linear_Space_of_RealSequences by A14,RLSUB_1:18;
     set L1=Linear_Space_of_RealSequences;
     set W = the_set_of_RealSequences_l^p;
A15: [:REAL,W:] c= [:REAL,the carrier of L1:] by ZFMISC_1:119;
     dom (the Mult of L1) = [:REAL,the carrier of L1:] by FUNCT_2:def 1; then
     dom ((the Mult of Linear_Space_of_RealSequences) | [:REAL,W:])
       =[:REAL,W:] by A15,RELAT_1:91; then
A16:  [r,x] in dom ((the Mult of Linear_Space_of_RealSequences)|[:REAL,W:])
     by A2;
A17:        W is lineary-closed by A1,Th4;
    r*x =(the Mult of lp).[r,x] by RLVECT_1:def 4
        .=((the Mult of Linear_Space_of_RealSequences)|[:REAL,W:]).[r,x]
        by A2,A17,RSSPACE:def 9
        .=(the Mult of Linear_Space_of_RealSequences).[r,x]
        by A16,FUNCT_1:70
        .=r*x1 by RLVECT_1:def 4;
     hence thesis by RSSPACE:5,def 7;
   end;
A18:for x be VECTOR of lp holds (-x =-seq_id(x) & seq_id(-x)=-seq_id(x))
   proof
     let x be VECTOR of lp;
     lp is Subspace of Linear_Space_of_RealSequences by A1,A2,Th9;
     then -x = (-1)*x by RLVECT_1:29
         .= (-1) (#) seq_id(x) by A13
        .= -seq_id(x) by SEQ_1:25;
     hence thesis by A5;
   end;
   A19:for x,y be VECTOR of lp holds x-y =seq_id(x)-seq_id(y)
      proof
     let x,y be VECTOR of lp;
     thus x-y = x+(-y) by RLVECT_1:def 11
                .= seq_id(x)+seq_id(-y) by A9
                .= seq_id(x)+(-seq_id(y)) by A18
                .= seq_id(x)-seq_id(y) by SEQ_1:15;
      end;
   for x be VECTOR of lp holds ||.x.|| =
           ( Sum(seq_id(x) rto_power p) ) to_power (1/p)
   proof
     let x be VECTOR of lp;
     thus ||.x.|| = (the norm of lp).x by NORMSP_1:def 1
       .= ( Sum(seq_id(x) rto_power p) ) to_power (1/p) by A2,Def3;
   end;
   hence thesis by A2,A3,A5,A7,A9,A13,A18,A19;
end;

theorem Th11:
for p be Real st p>=1 holds
for rseq be Real_Sequence
   st (for n be Nat holds rseq.n=0) holds
    (rseq rto_power p is summable
 &  ( Sum(rseq rto_power p) ) to_power (1/p)=0 )
proof
     let p be Real such that
A1:      p>=1;
A2: p>0 by A1;
A3:  1/p > 0 by A2,REAL_2:127;
     let rseq be Real_Sequence such that
A4: for n be Nat holds rseq.n=0;
A5: for n be Nat holds (rseq rto_power p).n=0
   proof
       let n be Nat;
       rseq.n=0 by A4;then
       abs((rseq).n) =0 by ABSVALUE:7;then
 abs((rseq).n) to_power p =0 by A2,POWER:def 2;
       hence thesis by Def1;
     end;
A6:  for m be Nat holds Partial_Sums (rseq rto_power p).m = 0
     proof
       let m be Nat;
       defpred P[Nat] means (rseq rto_power p).$1
       = (Partial_Sums(rseq rto_power p)).$1;
A7:    P[0] by SERIES_1:def 1;
A8:    for k be Nat st P[k] holds P[k+1]
       proof
         let k be Nat such that
A9:      (rseq rto_power p).k
             = (Partial_Sums (rseq rto_power p)).k;
         thus (rseq rto_power p).(k+1) = 0 + (rseq rto_power p).(k+1)
          .= (rseq rto_power p).k + (rseq rto_power p).(k+1) by A5
          .= (Partial_Sums ((rseq rto_power p))).(k+1) by A9,SERIES_1:def 1;
       end;
       for n be Nat holds P[n] from NAT_1:sch 1(A7,A8);
       hence (Partial_Sums ((rseq rto_power p))).m
                = (rseq rto_power p).m .= 0 by A5;
     end;
  Sum(rseq rto_power p) =0 & (rseq rto_power p) is summable
     proof
A10:    for e be real number st 0<e
        ex n be Nat st for m be Nat st n<=m holds
         abs((Partial_Sums (rseq rto_power p)).m-0)<e
       proof
         let e be real number such that
A11:   0<e;
         take 0;
         let m be Nat such that 0<=m;
         abs((Partial_Sums (rseq rto_power p)).m-0)
            = abs(0-0) by A6
           .= 0 by ABSVALUE:def 1;
         hence abs((Partial_Sums (rseq rto_power p)).m-0)<e by A11;
       end; then
A12:    Partial_Sums (rseq rto_power p) is convergent
        by SEQ_2:def 6; then
A13:   (rseq rto_power p) is summable by SERIES_1:def 2;
       lim (Partial_Sums (rseq rto_power p)) =0 by A10,A12,SEQ_2:def 7;
       hence thesis by A13,SERIES_1:def 3;
     end;
     hence thesis by A3,POWER:def 2;
end;

theorem Th12:
  for p be Real st 1 <=p
  for rseq be Real_Sequence st
  (rseq rto_power p is summable
   & ( Sum(rseq rto_power p) ) to_power (1/p)=0 ) holds
  for n be Nat holds rseq.n = 0
proof
   let p be Real such that
A1: 1 <=p;
A2: p>0 by A1; then
A3:  p <> 0;
A4:  1/p > 0 by A2,REAL_2:127;
   let rseq being Real_Sequence such that
A5:rseq rto_power p is summable and
A6: Sum(rseq rto_power p) to_power (1/p)=0;
A7:  for i be Nat holds ((rseq) rto_power p).i >= 0
        proof
         let i be Nat;
A8:      ( (rseq) rto_power p).i
           =abs((rseq).i) to_power p by Def1;
       abs((rseq).i) >=0 by ABSVALUE:5;
          hence ((rseq) rto_power p).i >= 0 by A2,A8,Lm1;
        end;
 A9:(Sum(rseq rto_power p) ) >= 0 by A5,A7,SERIES_1:21;
A10: ( (Sum(rseq rto_power p) ) to_power (1/p)) to_power p
      =(Sum(rseq rto_power p) ) to_power((1/p)*p)
    by A2,A4,A9,HOLDER_1:2
     .=(Sum(rseq rto_power p) ) to_power(1) by A3,XCMPLX_1:107
     .=(Sum(rseq rto_power p) ) by POWER:30;
 0 to_power p =0 by A2,POWER:def 2; then
A11: (Sum(rseq rto_power p) ) = 0 by A6,A10;
A12: for n be Nat holds (rseq rto_power p).n =0
     by A5,A7,A11,RSSPACE:21;
     now
       let n be Nat;
       (rseq rto_power p).n =abs(rseq.n) to_power p by Def1; then
 A13: abs(rseq.n) to_power p =0 by A12;
 A14: abs(rseq.n) >= 0 by ABSVALUE:5;
 A15: (abs(rseq.n) to_power p) to_power (1/p)
        =abs(rseq.n) to_power (p*(1/p)) by A2,A4,A14,HOLDER_1:2
        .=abs(rseq.n) to_power (1) by A3,XCMPLX_1:107
        .=abs(rseq.n) by POWER:30;
        0 to_power (1/p) =0 by A4,POWER:def 2;
        hence rseq.n =0 by A13,A15,ABSVALUE:7;
       end;
   hence thesis;
end;

Lm6:
for p be Real st 1 <=p
  for lp be non empty NORMSTR st lp
     =  NORMSTR (# the_set_of_RealSequences_l^p,
       Zero_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
       Add_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
       Mult_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
     l_norm^p #) holds
for x being Point of lp, a be Real holds
(seq_id(a *(x)) rto_power p )=
((abs(a) to_power p)(#)(seq_id(x) rto_power p))
proof
 let p be Real such that
  A1: 1<= p;
  A2: p > 0 by A1;
 let lp be non empty NORMSTR such that
A3:  lp = NORMSTR (# the_set_of_RealSequences_l^p,
       Zero_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
       Add_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
       Mult_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
     l_norm^p #);
   let x be Point of lp;
   let a be Real;
now let n1 be set such that A4:n1 in NAT;
reconsider n =n1 as Nat by A4;
 A5:abs(a) >=0 & abs((seq_id(x)).n) >=0 by ABSVALUE:5;
 A6:(seq_id(a *(x)) rto_power p ).n
  =abs(((seq_id(a*x)).n)) to_power p by Def1
  .=abs(((seq_id(a(#)seq_id(x))).n)) to_power p by A1,A3,Th10
.=abs(((a(#)seq_id(x)).n)) to_power p by RSSPACE:3
.=abs(a*(seq_id(x)).n ) to_power p by SEQ_1:13
.=(abs(a) * abs((seq_id(x)).n)) to_power p by ABSVALUE:10
.=abs(a) to_power p * abs((seq_id(x)).n) to_power p by A2,A5,Lm2
.=abs(a) to_power p*(seq_id(x) rto_power p).n by Def1
.=((abs(a) to_power p)(#)(seq_id(x) rto_power p)).n by SEQ_1:13;
reconsider n as set;
thus (seq_id(a *(x)) rto_power p ).n1=
((abs(a) to_power p)(#)(seq_id(x) rto_power p)).n1 by A6;
end;
hence (seq_id(a *(x)) rto_power p )=
((abs(a) to_power p)(#)(seq_id(x) rto_power p)) by SEQ_1:8;
end;

Lm7:
for p be Real st 1 <=p
  for lp be non empty NORMSTR st lp
     = NORMSTR (# the_set_of_RealSequences_l^p,
       Zero_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
       Add_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
       Mult_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
     l_norm^p #) holds
for x being Point of lp, a be Real holds
 ( Sum(seq_id(a*x) rto_power p) )
 =(abs(a) to_power p) * Sum(seq_id(x) rto_power p)
 proof
  let p be Real such that
   A1: 1<= p;
 let lp be non empty NORMSTR such that
A2:  lp = NORMSTR (# the_set_of_RealSequences_l^p,
       Zero_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
       Add_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
       Mult_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
     l_norm^p #);
    let x be Point of lp;
    let a be Real;
A3: (seq_id(x) rto_power p) is summable by A1,A2,Th10;
thus ( Sum(seq_id(a*x) rto_power p) )
 = ( Sum((abs(a) to_power p)(#)(seq_id(x) rto_power p)) ) by A1,A2,Lm6
 .=(abs(a) to_power p) * Sum(seq_id(x) rto_power p) by A3,SERIES_1:13;
 end;

Lm8:
for p be Real st 1 <=p
 for lp be non empty NORMSTR st lp
     = NORMSTR (# the_set_of_RealSequences_l^p,
       Zero_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
       Add_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
       Mult_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
     l_norm^p #) holds
for x being Point of lp, a be Real holds
 ( Sum(seq_id(a*x) rto_power p) ) to_power (1/p)
 =(abs(a) )*(Sum(seq_id(x) rto_power p) to_power (1/p))
proof
   let p be Real such that
    A1: 1<= p;
  let lp be non empty NORMSTR such that
 A2:  lp = NORMSTR (# the_set_of_RealSequences_l^p,
       Zero_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
       Add_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
       Mult_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
     l_norm^p #);
    A3:  1 <= p by A1;
  A4:  0 < p by A3; then
  A5:p <>0;
  A6:(1/p)>0 by A4,REAL_2:127;
  let x be Point of lp;
   let a be Real;
 A7: (seq_id(x) rto_power p) is summable by A1,A2,Th10;
A8: now let n be Nat;
 A9: (seq_id(x) rto_power p).n =abs((seq_id(x)).n) to_power p by Def1;
abs((seq_id(x)).n)>=0 by ABSVALUE:5;
hence (seq_id(x) rto_power p).n >= 0 by A4,A9,Lm1;
 end;
 A10: abs(a) >= 0 by ABSVALUE:5;
 A11:0<=Sum(seq_id(x) rto_power p) by A7,A8,SERIES_1:21;
A12: (abs(a) to_power p) >= 0 by A4,A10,Lm1;
thus ( Sum(seq_id(a*x) rto_power p) ) to_power (1/p)
  =((abs(a) to_power p) * Sum(seq_id(x) rto_power p) )
                                    to_power (1/p) by A1,A2,Lm7
 .= ((abs(a) to_power p) to_power (1/p)) *
      ( Sum(seq_id(x) rto_power p) to_power (1/p)) by A6,A11,A12,Lm2
 .= (abs(a) to_power (p*(1/p))) *
       ( Sum(seq_id(x) rto_power p) to_power (1/p))
      by A4,A6,A10,HOLDER_1:2
.= (abs(a) to_power 1) *
    ( Sum(seq_id(x) rto_power p) to_power (1/p)) by A5,XCMPLX_1:107
.= abs(a)* ( Sum(seq_id(x) rto_power p) to_power (1/p)) by POWER:30;
 end;

theorem Th13:
 for p be Real st 1 <= p
 for lp be non empty NORMSTR st lp
     = NORMSTR (# the_set_of_RealSequences_l^p,
       Zero_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
       Add_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
       Mult_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
     l_norm^p #) holds
for x, y being Point of lp, a be Real holds
  ( ||.x.|| = 0 iff x = 0.lp ) &
  0 <= ||.x.|| &
  ||.x+y.|| <= ||.x.|| + ||.y.|| &
  ||.(a*x).|| = abs(a) * ||.x.||
proof
  let p be Real such that
  A1: 1<= p;
  let lp be non empty NORMSTR such that
  A2: lp = NORMSTR (# the_set_of_RealSequences_l^p,
           Zero_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
           Add_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
           Mult_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
         l_norm^p  #);
 A3:  1 <= p by A1;
 A4:  0 < p by A3;
   let x, y be Point of lp;
   let a be Real;
   1 > 0 & p >= 1 by A1; then
    p > 0; then
   A5:  1/p > 0 by REAL_2:127;
A6:now assume
A7: ||.x.|| = 0;
A8: ||.x.|| = (the norm of lp). x by NORMSP_1:def 1
            .= ( Sum(seq_id(x) rto_power p) ) to_power (1/p) by A2,Def3;
A9:  seq_id(x) rto_power p is summable by A1,A2,Th10;
A10: for n be Nat holds 0 = (seq_id(x)).n by A1,A7,A8,A9,Th12;
     x in the_set_of_RealSequences by A1,A2,Def2;
     hence x = Zeroseq by A10,RSSPACE:def 6
     .=0.lp by A1,A2,Th10;
   end;
A11:now assume
A12:x=0.lp;
    x=Zeroseq by A1,A2,A12,Th10; then
A13: for n be Nat holds (seq_id(x)).n=0 by RSSPACE:def 6;
thus ||.x.|| = (the norm of lp). x by NORMSP_1:def 1
            .=  ( Sum(seq_id(x) rto_power p) ) to_power (1/p) by A2,Def3
            .= 0 by A1,A13,Th11;
   end;
A14:for n be Nat holds 0 <= (seq_id(x) rto_power p).n
proof
 let n be Nat;
 set xp=seq_id(x) rto_power p;
 A15:   xp.n=abs((seq_id(x)).n) to_power p by Def1;
 A16:  0 <= abs((seq_id(x)).n) by ABSVALUE:5;
 A17:  0 < abs((seq_id(x)).n) or 0=abs((seq_id(x)).n) by A16; thus
   0 <= xp.n by A4,A15,A17,POWER:39,def 2;
   end;
   seq_id(x) rto_power p is summable by A1,A2,Def2;then
 A18: 0<= ( Sum(seq_id(x) rto_power p) ) by A14,SERIES_1:21;
 A19:( Sum(seq_id(x) rto_power p) ) to_power (1/p) >=0 by A5,A18,Lm1;
     ( Sum(seq_id(x) rto_power p) ) to_power (1/p) = (the norm of lp). x
     by A2,Def3
       .= ||.x.|| by NORMSP_1:def 1; then
A20:     ||.x.|| >=0 by A19;
A21:now
 A22:||.x.|| = (the norm of lp). x by NORMSP_1:def 1
            .= ( Sum(seq_id(x) rto_power p) ) to_power (1/p) by A2,Def3;
 A23:||.y.|| = (the norm of lp). y by NORMSP_1:def 1
           .= ( Sum(seq_id(y) rto_power p) ) to_power (1/p) by A2,Def3;
A24:((seq_id(x))+(seq_id(y)))
=seq_id((seq_id(x))+(seq_id(y))) by RSSPACE:3
.=seq_id(x+y) by A1,A2,Th10;
A25: Sum((((seq_id(x))+(seq_id(y))) rto_power p)) to_power (1/p)
      = (the norm of lp).(x+y) by A2,A24,Def3
     .= ||.x + y.|| by NORMSP_1:def 1;
A26:(seq_id(x) rto_power p) is summable by A1,A2,Th10;
(seq_id(y) rto_power p) is summable by A1,A2,Th10;
hence ||.x + y.|| <= ||.x.|| + ||.y.|| by A1,A22,A23,A25,A26,Th3;
end;
 A27:||.x.|| = (the norm of lp). x by NORMSP_1:def 1
            .= ( Sum(seq_id(x) rto_power p) ) to_power (1/p) by A2,Def3;
 ||.(a*x).|| = (the norm of lp). (a*x) by NORMSP_1:def 1
 .= ( Sum(seq_id(a*x) rto_power p) ) to_power (1/p) by A2,Def3
 .=(abs(a))* ||.x.|| by A1,A2,A27,Lm8;
 hence thesis by A6,A11,A20,A21;
end;

theorem Th14:
for p be Real st p >= 1
  for lp be non empty NORMSTR
     st lp = NORMSTR (# the_set_of_RealSequences_l^p,
       Zero_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
       Add_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
       Mult_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
     l_norm^p #) holds
 lp is RealNormSpace-like
 proof
 let p be Real such that
A1: p >=1;
 let lp be non empty NORMSTR such that
A2: lp = NORMSTR (# the_set_of_RealSequences_l^p,
       Zero_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
       Add_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
       Mult_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
     l_norm^p #);
     for x, y being Point of lp, a be Real holds
    ( ||.x.|| = 0 iff x = 0.lp ) &
    0 <= ||.x.|| &
    ||.x+y.|| <= ||.x.|| + ||.y.|| &
    ||.(a*x).|| = abs(a) * ||.x.|| by A1,A2,Th13;
 hence thesis by NORMSP_1:def 2;
 end;

theorem Th15:
 for p be Real st p >= 1
 for lp be non empty NORMSTR st lp
      = NORMSTR (# the_set_of_RealSequences_l^p,
        Zero_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
        Add_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
        Mult_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
      l_norm^p #) holds
 lp is RealNormSpace by Th9,Th14;

 reserve NRM for non empty RealNormSpace,
         seq for sequence of NRM;

Lm9:
for c be Real, seq be Real_Sequence st seq is convergent
for rseq be Real_Sequence st
 ( for i be Nat holds rseq .i = abs(seq.i-c) ) holds
 ( rseq is convergent & lim rseq = abs(lim seq-c) ) by RSSPACE3:1;

Lm10:
for p be Real st 0 < p
for c be Real, seq be Real_Sequence st seq is convergent
for rseq be Real_Sequence st
 ( for i be Nat holds rseq .i = (abs(seq.i -c)) to_power p )
 holds
 ( rseq is convergent & lim rseq = (abs(lim seq-c)) to_power p )
proof
  let p be Real such that
A1:0 <p;
  let c be Real;
  let seq be Real_Sequence such that
A2:seq is convergent;
  let rseq be Real_Sequence such that
A3: ( for i be Nat holds rseq .i = (abs(seq.i -c)) to_power p );
 deffunc F(set) = abs(seq.$1-c);
   consider b be Real_Sequence such that
A4:for n be Nat holds b.n=F(n) from SEQ_1:sch 1;
A5:b is convergent by A2,A4,Lm9;
A6:lim b = abs(lim seq - c) by A2,A4,Lm9;
A7:for n be Nat holds 0 <=b.n
  proof
  let n be Nat;
  b.n=abs(seq.n-c) by A4;
  hence thesis by ABSVALUE:5;
  end;
  for n be Nat holds rseq.n=(b.n) to_power p
  proof
    let n be Nat;
    rseq.n=(abs(seq.n -c)) to_power p by A3
         .=(b.n) to_power p by A4;
    hence thesis;
  end;
  hence thesis by A1,A5,A6,A7,HOLDER_1:10;
end;

Lm11:
for p be Real st 0 < p
for c be Real, seq, seq1 be Real_Sequence st (seq is convergent
& seq1 is convergent)
for rseq be Real_Sequence st
 ( for i be Nat holds rseq .i = (abs(seq.i -c)) to_power p +seq1.i )
 holds
 ( rseq is convergent & lim rseq = (abs(lim seq-c)) to_power p + lim seq1 )
proof
  let p be Real such that
A1:0 < p;
  let c be Real;
  let seq,seq1 be Real_Sequence such that
A2:seq is convergent and
A3:seq1 is convergent;
  let rseq be Real_Sequence such that
A4:  for i be Nat holds rseq .i =( (abs(seq.i -c)) to_power p ) + seq1.i;
 deffunc F(set) = abs(seq.$1-c) to_power p;
   consider b be Real_Sequence such that
A5:for n be Nat holds b.n=F(n) from  SEQ_1:sch 1;
A6:b is convergent by A1,A2,A5,Lm10;
A7:lim b = (abs(lim seq-c)) to_power p by A1,A2,A5,Lm10;
now let n be Nat;thus
  rseq .n = ( (abs(seq.n -c)) to_power p ) + seq1.n by A4
           .=b.n + seq1.n by A5;
  end; then
 A8:rseq = b + seq1 by SEQ_1:11;
 A9: lim rseq
     =(abs(lim seq-c)) to_power p + lim seq1 by A3,A6,A7,A8,SEQ_2:20;
     thus rseq is convergent by A3,A6,A8,SEQ_2:19;
     thus lim rseq = (abs(lim seq-c)) to_power p + lim seq1 by A9;
end;

Lm12:
 for e be Real, seq be Real_Sequence st ( seq is convergent &
  ex k be Nat st (for i be Nat st k <= i holds seq.i <=e )) holds
   lim seq <=e by RSSPACE2:6;

Lm13:
for a,b be Real_Sequence holds a=a+b-b
proof
 let a,b be Real_Sequence;
 now let n be Nat;
 A1: a+b-b = a+b+-b by SEQ_1:15
   .=a+(b+(-b)) by SEQ_1:20;
   (a+(b+(-b))).n=a.n+(b+(-b)).n by SEQ_1:11
   .=a.n+(b.n+(-b).n) by SEQ_1:11
   .=a.n+(b.n+(-b.n)) by SEQ_1:14
   .=a.n;
   hence (a+b-b).n=a.n by A1;
 end;
hence thesis by FUNCT_2:113;
end;

Lm14:
for p be Real st p >= 1
for a,b be Real_Sequence st (a rto_power p is summable &
    b rto_power p is summable) holds (a + b) rto_power p is summable
proof
      let p be Real such that
A1:p >= 1;
      let a,b be Real_Sequence such that
A2: a rto_power p is summable and
A3: b rto_power p is summable;
      reconsider a1=a, b1=b as set;
A4:a1 in the_set_of_RealSequences by RSSPACE:def 1;
A5:b1 in the_set_of_RealSequences by RSSPACE:def 1;
A6:seq_id(a1) = a by A4,RSSPACE:def 2;
A7:seq_id(b1) = b by A5,RSSPACE:def 2;
A8:seq_id(a1) rto_power p is summable by A2,A6;
A9:seq_id(b1) rto_power p is summable by A3,A7;
A10:a1 in the_set_of_RealSequences_l^p by A1,A4,A8,Def2;
A11:b1 in the_set_of_RealSequences_l^p by A1,A5,A9,Def2;
     reconsider a1 as VECTOR of Linear_Space_of_RealSequences by A10;
     reconsider b1 as VECTOR of Linear_Space_of_RealSequences by A11;
     the_set_of_RealSequences_l^p is lineary-closed by A1,Th4;
     then a1+b1 in the_set_of_RealSequences_l^p by A10,A11,RLSUB_1:def 1;
     then
A12:seq_id(a1+b1) rto_power p is summable by A1,Def2;
     seq_id(a1+b1)=seq_id(seq_id(a1)+seq_id(b1)) by RSSPACE:4,def 7
     .=a+b by A6,A7,RSSPACE:3;
     hence (a+b) rto_power p is summable by A12;
end;

theorem Th16:
 for p be Real st 1 <=p
 for lp be RealNormSpace
    st lp = NORMSTR (# the_set_of_RealSequences_l^p,
      Zero_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
      Add_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
      Mult_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
      l_norm^p #) holds
 for vseq be sequence of lp st vseq is Cauchy_sequence_by_Norm
 holds vseq is convergent
proof
  let p be Real such that
  A1: 1<= p;
 A2:  p >0 by A1;
 then A3: p <> 0;
  A4:  1/p > 0 by A2,REAL_2:127;
  A5:  p * (1/p) = (p*1)/p by XCMPLX_1:75
                .= 1 by A2,XCMPLX_1:60;
   let lp be RealNormSpace such that
     A6:  lp = NORMSTR (# the_set_of_RealSequences_l^p,
           Zero_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
           Add_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
           Mult_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
         l_norm^p #);
 let vseq be sequence of lp such that
 A7:vseq is Cauchy_sequence_by_Norm;
   defpred P[set,set] means
        ex i be Nat st $1=i &
     ex rseqi be Real_Sequence st
      (for n be Nat holds rseqi.n=(seq_id(vseq.n)).i) &
     rseqi is convergent & $2 = lim rseqi;
A8:for x be set st x in NAT ex y be set st y in REAL & P[x,y]
   proof
     let x be set such that
A9: x in NAT;
     reconsider i=x as Nat by A9;
     deffunc F(Nat) = (seq_id(vseq.$1)).i;
     consider rseqi be Real_Sequence such that
A10:  for n be Nat holds rseqi.n= F(n) from SEQ_1:sch 1;
A11:  rseqi is convergent
      proof
      now
         let e1 be real number such that
A12:     e1 > 0;
         reconsider e=e1 as Real by XREAL_0:def 1;
    thus ex k be Nat st
          for m be Nat st k <= m holds abs(rseqi.m -rseqi.k) < e1
          proof
           consider k be Nat such that
A13:       for n, m be Nat st ( n >= k & m >= k ) holds
           ||.(vseq.n) - (vseq.m).|| < e by A7,A12,RSSPACE3:10;
 for m being Nat st k <= m holds abs(rseqi.m-rseqi.k) < e
           proof
            let m be Nat such that
A14:        k<=m;
A15:        0 <= ||.(vseq.m) - (vseq.k).|| by NORMSP_1:8;
A16:       ||.(vseq.m) - (vseq.k).||
            = (the norm of lp).((vseq.m)-(vseq.k)) by NORMSP_1:def 1
           .= ( Sum(seq_id((vseq.m)-(vseq.k)) rto_power p) )
             to_power (1/p) by A6,Def3; then
             ( Sum(seq_id((vseq.m)-(vseq.k)) rto_power p) ) to_power
                                               (1/p) < e by A13,A14; then
A17:        (( Sum(seq_id((vseq.m)-(vseq.k)) rto_power p) ) to_power
                                (1/p)) to_power p < e to_power p
                              by A2,A15,A16,Th1;
 A18: (seq_id((vseq.m)-(vseq.k)) rto_power p) is summable by A1,A6,Th10;

A19:now
 let i be Nat;
   A20:  (seq_id((vseq.m)-(vseq.k)) rto_power p).i
             =abs((seq_id((vseq.m)-(vseq.k))).i) to_power p
           by Def1;
  abs((seq_id((vseq.m)-(vseq.k))).i) >=0 by ABSVALUE:5;
 hence (seq_id((vseq.m)-(vseq.k)) rto_power p).i >= 0 by A2,A20,Lm1;
end;
A21:0<=Sum(seq_id((vseq.m)-(vseq.k)) rto_power p) by A18,A19,SERIES_1:21;
A22:    (( Sum(seq_id((vseq.m)-(vseq.k)) rto_power p) ) to_power
                                (1/p)) to_power p
         =( Sum(seq_id((vseq.m)-(vseq.k)) rto_power p) )
           to_power ((1/p)*p) by A2,A4,A21,HOLDER_1:2
        .= ( Sum(seq_id((vseq.m)-(vseq.k)) rto_power p) ) by A5,POWER:30;
A23:(e to_power p) to_power (1/p)
        = e to_power ((1/p) * p) by A12,POWER:38
        .= e to_power(1) by A3,XCMPLX_1:107
        .=e by POWER:30;
A24: Sum(seq_id((vseq.m)-(vseq.k)) rto_power p) < e to_power p by A17,A22;
 A25:(seq_id((vseq.m)-(vseq.k)) rto_power p).i <=
      Sum(seq_id((vseq.m)-(vseq.k)) rto_power p) by A18,A19,RSSPACE2:4;
 A26: abs((seq_id((vseq.m)-(vseq.k))).i) >=0 by ABSVALUE:5;
A27: now
       let a,b,c be Real such that A28:a =0 & b>0 & c>0;
       a to_power b =0 by A28,POWER:def 2;
       hence (a to_power b) to_power c =0 by A28,POWER:def 2;
       end;
 A29: now
       let a,b,c be Real such that A30:a =0 & b>0 & c>0;
       b*c >0 by A30,REAL_2:122;
       hence a to_power (b *c) =0 by A30,POWER:def 2;
       end;
  A31: now
       let a,b,c be Real such that A32:a =0 & b>0 & c>0;
       thus (a to_power b) to_power c
         = 0 by A27,A32
        .= a to_power (b *c) by A29,A32;
       end;
  A33:for a,b,c be Real st a >0 & b>0 & c>0
       holds (a to_power b) to_power c =
       a to_power (b*c) by POWER:38;
  A34:for a,b,c be Real st (a >0 or a=0) & b>0 & c>0
       holds (a to_power b) to_power c =
       a to_power (b*c) by A31,A33;
 A35: for a,b,c be Real st a>=0 & b>0 & c>0 holds
      (a to_power b) to_power c = a to_power (b*c)
      proof
       let a,b,c be Real such that
       A36:a>=0 & b>0 & c>0;
       ((a >0) or (a=0)) & b>0 & c>0 by A36;
      hence (a to_power b) to_power c = a to_power (b*c) by A34;
       end;
 A37: (seq_id((vseq.m)-(vseq.k)) rto_power p).i
     =abs((seq_id((vseq.m)-(vseq.k))).i) to_power p by Def1;
 A38:((seq_id((vseq.m)-(vseq.k)) rto_power p).i) to_power (1/p)
    =abs((seq_id((vseq.m)-(vseq.k))).i) to_power (1) by A2,A4,A5,A26,A35,A37
    .=abs((seq_id((vseq.m)-(vseq.k))).i) by POWER:30;
A39:  (seq_id((vseq.m)-(vseq.k)) rto_power p).i >= 0 by A19;
    (seq_id((vseq.m)-(vseq.k)) rto_power p).i <e to_power p
      by A24,A25,AXIOMS:22;
  then ((seq_id((vseq.m)-(vseq.k)) rto_power p).i) to_power (1/p)
  < (e to_power p) to_power (1/p) by A4,A39,Th1; then
A40:   abs((seq_id((vseq.m)-(vseq.k))).i) <e by A23,A38;
 A41:  rseqi.m=(seq_id(vseq.m)).i & rseqi.k=(seq_id(vseq.k)).i by A10;
   reconsider vsem = (vseq.m), vsek = (vseq.k) as VECTOR of lp;
(vsem)-(vsek) =(seq_id(vsem)) - (seq_id(vsek)) by A1,A6,Th10; then
  abs((seq_id((vseq.m)-(vseq.k))).i)
   =abs((seq_id(vseq.m)-seq_id(vseq.k)).i) by RSSPACE:3
  .=abs((seq_id(vseq.m)+-seq_id(vseq.k)).i) by SEQ_1:15
  .=abs((seq_id(vseq.m)).i+(-(seq_id(vseq.k))).i) by SEQ_1:11
  .=abs((seq_id(vseq.m)).i +-(seq_id(vseq.k)).i) by SEQ_1:14
  .=abs(rseqi.m -rseqi.k) by A41;
 hence abs(rseqi.m -rseqi.k) < e by A40;
end;
hence thesis;
end;
end;
hence rseqi is convergent by SEQ_4:58;
end;
   take y = lim rseqi;
   thus thesis by A10,A11;
 end;
   consider f be Function of NAT,REAL such that
A42:for x be set st x in NAT holds P[x,f.x] from FUNCT_2:sch 1(A8);
   reconsider tseq=f as Real_Sequence;
A43:now
      let i be Nat;
      reconsider x=i as set;
      ex i0 be Nat st x=i0 &
       ex rseqi be Real_Sequence st
        ( for n be Nat holds rseqi.n=(seq_id(vseq.n)).i0 ) &
       rseqi is convergent &
       f.x=lim rseqi by A42;
       hence ex rseqi be Real_Sequence st
        ( for n be Nat holds rseqi.n=(seq_id(vseq.n)).i ) &
        rseqi is convergent &
        tseq.i=lim rseqi;
      end;
A44:for e be Real st e >0
   ex k be Nat st for n be Nat st n >= k holds
    ((seq_id(tseq)-seq_id(vseq.n)) rto_power p ) is summable &
      Sum((seq_id(tseq)-seq_id(vseq.n)) rto_power p) < e
   proof
       let e2 be Real such that
A45: e2 >0;
       set e=e2/2;
A46: e >0 by A45,SEQ_2:3;
A47: e2 >e by A45,SEQ_2:4;
       set e1=e to_power (1/p);
A48: e1>0 by A46,POWER:39;
       consider k be Nat such that
A49: for n, m be Nat st ( n >= k & m >= k ) holds
         ||.(vseq.n) - (vseq.m).|| < e1 by A7,A48,RSSPACE3:10;
A50: for m,n be Nat st ( n >= k & m >= k ) holds
        ((seq_id((vseq.m)-(vseq.n)) rto_power p) is summable &
           Sum( (seq_id((vseq.m)-(vseq.n)) rto_power p) ) < e &
    for i be Nat holds 0 <=  ((seq_id((vseq.m)-(vseq.n))) rto_power p).i)
        proof
        let m,n be Nat such that
A51:  n >= k & m >= k;
A52:  (seq_id((vseq.m)-(vseq.n)) rto_power p) is summable by A1,A6,Th10;
A53:       for i be Nat holds (seq_id((vseq.m)-(vseq.n)) rto_power p).i >= 0
       proof
         let i be Nat;
A54:      (seq_id((vseq.m)-(vseq.n)) rto_power p).i
         =abs((seq_id((vseq.m)-(vseq.n))).i) to_power p by Def1;
      abs((seq_id((vseq.m)-(vseq.n))).i) >=0 by ABSVALUE:5;
        hence
        (seq_id((vseq.m)-(vseq.n)) rto_power p).i >= 0 by A2,A54,Lm1;
       end;
       ||.(vseq.m) - (vseq.n).|| < e1 by A49,A51; then
     (the norm of lp).((vseq.m)-(vseq.n))<e1 by NORMSP_1:def 1;
     then
A55:  (( Sum(seq_id((vseq.m)-(vseq.n)) rto_power p) )
                                       to_power (1/p)) < e1 by A6,Def3;
 A56: (seq_id((vseq.m)-(vseq.n)) rto_power p) is summable by A1,A6,Th10;
 A57:0<=Sum(seq_id((vseq.m)-(vseq.n)) rto_power p) by A53,A56,SERIES_1:21;
 A58:    (( Sum(seq_id((vseq.m)-(vseq.n)) rto_power p) ) to_power
                                 (1/p)) to_power p
          =( Sum(seq_id((vseq.m)-(vseq.n)) rto_power p) )
            to_power ((1/p)*p) by A2,A4,A57,HOLDER_1:2
        .= ( Sum(seq_id((vseq.m)-(vseq.n)) rto_power p) ) by A5,POWER:30;
 A59:e1 to_power p = e to_power ((1/p) * p) by A46,POWER:38
     .= e to_power(1) by A3,XCMPLX_1:107
     .= e by POWER:30;
  (( Sum(seq_id((vseq.m)-(vseq.n)) rto_power p) ) to_power
                                 (1/p)) >=0 by A4,A57,Lm1;
 then ( Sum(seq_id((vseq.m)-(vseq.n)) rto_power p) ) < e by A2,A55,A58,A59,Th1;
hence thesis by A52,A53;
 end;
A60:  for n be Nat for i be Nat holds
      for rseq be Real_Sequence st
       ( for m be Nat holds
         rseq.m = Partial_Sums ((seq_id((vseq.m)-(vseq.n))) rto_power p).i )
      holds
       ( rseq is convergent & lim rseq =
              Partial_Sums ((seq_id(tseq)-seq_id(vseq.n)) rto_power p) .i )
     proof
       let n be Nat;
A61:   for m,k be Nat holds
         seq_id((vseq.m) - (vseq.k)) = seq_id(vseq.m)-seq_id(vseq.k)
         proof
             let m,k be Nat;
             seq_id((vseq.m) - (vseq.k))
              = seq_id(seq_id((vseq.m))-seq_id((vseq.k))) by A1,A6,Th10;
             hence thesis by RSSPACE:3;
          end;
         defpred P[Nat] means
         for rseq be Real_Sequence st for m be Nat holds
         rseq.m= Partial_Sums
                 ((seq_id((vseq.m)-(vseq.n))) rto_power p).$1 holds
         rseq is convergent &
         lim rseq = Partial_Sums
                  ((seq_id(tseq)-seq_id(vseq.n)) rto_power p).$1;
A62:  P[0]
       proof
         now
           let rseq be Real_Sequence such that
A63:     for m be Nat holds
           rseq.m= Partial_Sums
                 ((seq_id((vseq.m)-(vseq.n))) rto_power p).0;
           thus
           rseq is convergent &
           lim rseq = Partial_Sums
                  ((seq_id(tseq)-seq_id(vseq.n)) rto_power p).0
           proof
            consider rseq0 be Real_Sequence such that
A64:       ( for m be Nat holds rseq0.m=(seq_id(vseq.m)).0 ) &
            rseq0 is convergent &
            tseq.0=lim rseq0 by A43;
A65:       for m being Nat holds rseq.m =
            abs(rseq0.m -(seq_id(vseq.n)).0) to_power p
            proof
               let m be Nat;
              rseq.m =Partial_Sums
                 ((seq_id((vseq.m)-(vseq.n))) rto_power p).0 by A63
              .= ((seq_id((vseq.m)-(vseq.n))) rto_power p).0
                                                        by SERIES_1:def 1
              .= ((seq_id(vseq.m)-seq_id(vseq.n)) rto_power p).0 by A61
              .=((seq_id(vseq.m)+-seq_id(vseq.n)) rto_power p).0 by SEQ_1:15
              .=abs((seq_id(vseq.m)+-seq_id(vseq.n)).0) to_power p by Def1
              .=abs((seq_id(vseq.m)).0+(-seq_id(vseq.n)).0) to_power p
                                                        by SEQ_1:11
              .=abs((seq_id(vseq.m)).0+-(seq_id(vseq.n)).0) to_power p
                                                        by SEQ_1:14
              .=abs((seq_id(vseq.m)).0-(seq_id(vseq.n)).0) to_power p;
              hence thesis by A64;
           end;
A66:rseq is convergent by A2,A64,A65,Lm10;
lim rseq = abs(lim(rseq0) -(seq_id(vseq.n)).0) to_power p
                                                by A2,A64,A65,Lm10
           .= abs(tseq.0+-(seq_id(vseq.n)).0) to_power p by A64
           .=abs(tseq.0+(-(seq_id(vseq.n))).0) to_power p by SEQ_1:14
           .=abs((tseq+(-seq_id((vseq.n)))).0) to_power p by SEQ_1:11
           .=abs((tseq-(seq_id((vseq.n)))).0) to_power p by SEQ_1:15
           .=abs((seq_id(tseq)-(seq_id((vseq.n)))).0) to_power p by RSSPACE:3
           .=(abs(seq_id(tseq)-(seq_id(vseq.n))).0) to_power p by SEQ_1:16
          .=(abs(seq_id(tseq)+-(seq_id(vseq.n))).0) to_power p by SEQ_1:15
          .=abs((seq_id(tseq)+-(seq_id(vseq.n))).0) to_power p by SEQ_1:16
          .=((seq_id(tseq)+-(seq_id(vseq.n))) rto_power p).0 by Def1
          .= Partial_Sums
             ((seq_id(tseq)+-(seq_id(vseq.n))) rto_power p).0
                        by SERIES_1:def 1
          .=Partial_Sums
             ((seq_id(tseq)-(seq_id(vseq.n))) rto_power p).0 by SEQ_1:15;
           hence thesis by A66;
          end;
         end;
         hence thesis;
        end;
A67:  for i be Nat st P[i] holds P[i+1]
          proof
           now
                let i be Nat such that
       A68:    for rseq be Real_Sequence st
                 ( for m be Nat holds rseq.m= Partial_Sums
                 ((seq_id((vseq.m)-(vseq.n))) rto_power p).i ) holds
                 ( rseq is convergent & lim rseq =Partial_Sums
                  ((seq_id(tseq)-seq_id(vseq.n)) rto_power p).i);
                thus for rseq be Real_Sequence st
                 ( for m be Nat holds rseq.m = Partial_Sums
                 ((seq_id((vseq.m)-(vseq.n))) rto_power p).(i+1) ) holds
                 ( rseq is convergent & lim rseq =Partial_Sums
                  ((seq_id(tseq)-seq_id(vseq.n)) rto_power p).(i+1))
                proof
                  let rseq be Real_Sequence such that
       A69:     ( for m be Nat holds rseq.m = Partial_Sums
                 ((seq_id((vseq.m)-(vseq.n))) rto_power p).(i+1) );
                 deffunc F(Nat) = Partial_Sums
                 ((seq_id((vseq.$1)-(vseq.n))) rto_power p).i;
                  consider rseqb be Real_Sequence such that
       A70:       for m be Nat holds
                    rseqb.m = F(m) from SEQ_1:sch 1;
       A71:     rseqb is convergent &
                  lim rseqb = Partial_Sums
                  ((seq_id(tseq)-seq_id(vseq.n)) rto_power p).i by A68,A70;
                  consider rseq0 be Real_Sequence such that
       A72:       ( for m be Nat holds rseq0.m=(seq_id(vseq.m)).(i+1) ) &
                  rseq0 is convergent & tseq.(i+1)=lim rseq0 by A43;
     A73:       now
                    let m be Nat;
                    thus rseq.m = Partial_Sums
                   ((seq_id((vseq.m)-(vseq.n))) rto_power p).(i+1) by A69
                      .=((seq_id((vseq.m)-(vseq.n))) rto_power p).(i+1)
                        +Partial_Sums((seq_id((vseq.m)-(vseq.n)))
                                   rto_power p).i by SERIES_1:def 1
                      .=((seq_id(vseq.m)-seq_id(vseq.n)) rto_power p).(i+1)
                        +Partial_Sums((seq_id((vseq.m)-(vseq.n)))
                                   rto_power p).i by A61
                     .=((seq_id(vseq.m)-seq_id(vseq.n)) rto_power p).(i+1)
                      + rseqb.m by A70
                     .= abs((seq_id(vseq.m)-seq_id(vseq.n)).(i+1))
                          to_power p + rseqb.m by Def1
                      .= abs((seq_id(vseq.m)+-seq_id(vseq.n)).(i+1))
                          to_power p + rseqb.m by SEQ_1:15
                      .= abs((seq_id(vseq.m)).(i+1)+(-seq_id(vseq.n)).(i+1))
                          to_power p + rseqb.m by SEQ_1:11
                      .= abs((seq_id(vseq.m)).(i+1)+-(seq_id(vseq.n)).(i+1))
                          to_power p + rseqb.m by SEQ_1:14
                       .= abs((seq_id(vseq.m)).(i+1)-(seq_id(vseq.n)).(i+1))
                          to_power p + rseqb.m
                       .= abs(rseq0.m-(seq_id(vseq.n)).(i+1))
                          to_power p + rseqb.m by A72;
                  end;
                  lim rseq = abs(lim rseq0-(seq_id(vseq.n)).(i+1))
                  to_power p + lim rseqb by A2,A71,A72,A73,Lm11
                  .= abs(tseq.(i+1)+-(seq_id(vseq.n)).(i+1))
                  to_power p + lim rseqb by A72
                  .= abs(tseq.(i+1)+(-seq_id(vseq.n)).(i+1))
                  to_power p + lim rseqb by SEQ_1:14
                  .= abs((tseq+(-seq_id(vseq.n))).(i+1))
                  to_power p + lim rseqb by SEQ_1:11
                  .= (abs(tseq+(-seq_id(vseq.n))).(i+1))
                  to_power p + lim rseqb by SEQ_1:16
                  .= (abs(tseq-(seq_id(vseq.n))).(i+1))
                  to_power p + lim rseqb by SEQ_1:15
                  .= (abs((tseq-(seq_id(vseq.n))).(i+1))
                  to_power p) + lim rseqb by SEQ_1:16
                  .= ((tseq-(seq_id(vseq.n)))
                  rto_power p).(i+1) + Partial_Sums
                  ((seq_id(tseq)-seq_id(vseq.n)) rto_power p).i by A71,Def1
                  .= ((seq_id(tseq)-(seq_id(vseq.n)))
                  rto_power p).(i+1) + Partial_Sums
                  ((seq_id(tseq)-seq_id(vseq.n)) rto_power p).i by RSSPACE:3
                  .=Partial_Sums
                  ((seq_id(tseq)-seq_id(vseq.n)) rto_power p).(i+1)
                 by SERIES_1:def 1;
                  hence thesis by A2,A71,A72,A73,Lm11;
                end;
              end;
              hence thesis;
            end;
      for i be Nat holds P[i] from NAT_1:sch 1(A62,A67);
       hence thesis;
     end;
  for n be Nat st n >= k holds
       ((seq_id(tseq)-seq_id(vseq.n)) rto_power p ) is summable &
        Sum ((seq_id(tseq)-seq_id(vseq.n)) rto_power p ) < e2
  proof
    let n be Nat such that
A74:     n >= k;
A75:     for i be Nat st 0 <= i holds
           Partial_Sums ((seq_id(tseq)-seq_id(vseq.n)) rto_power p).i <=e
          proof
            let i be Nat such that 0 <=i;
            deffunc F(Nat)=
               Partial_Sums ((seq_id((vseq.$1)-(vseq.n))) rto_power p).i;
               consider rseq be Real_Sequence such that
A76:     for m be Nat holds rseq.m = F(m) from SEQ_1:sch 1;
A77:     rseq is convergent by A60,A76;
A78:     lim rseq =
                Partial_Sums ((seq_id(tseq)-seq_id(vseq.n)) rto_power p) .i
          by A60,A76;
         for m be Nat st m >= k holds rseq.m <= e
         proof
            let m be Nat;
            assume m >= k; then
A79:      ((seq_id((vseq.m)-(vseq.n)) rto_power p) is summable &
            Sum( (seq_id((vseq.m)-(vseq.n)) rto_power p) ) < e &
            for i be Nat holds
               0 <= ((seq_id((vseq.m)-(vseq.n))) rto_power p).i )
                by A50,A74; then
A80:        Partial_Sums ((seq_id((vseq.m)-(vseq.n))) rto_power p).i
               <=Sum( (seq_id((vseq.m)-(vseq.n)) rto_power p) )
                    by RSSPACE2:4;
             rseq.m = Partial_Sums ((seq_id((vseq.m)-(vseq.n))) rto_power p).i
                                    by A76;
            hence thesis by A79,A80,AXIOMS:22;
          end;
          then lim rseq <=e by A77,Lm12;
           hence Partial_Sums
           ((seq_id(tseq)-seq_id(vseq.n)) rto_power p) .i <=e by A78;
          end;
A81:   for i be Nat holds
          0 <= ((seq_id(tseq)-seq_id(vseq.n)) rto_power p) .i
       proof
         let i be Nat;
A82:      ((seq_id(tseq)-seq_id(vseq.n)) rto_power p) .i
         =(abs((seq_id(tseq)-seq_id(vseq.n)).i)) to_power p by Def1;
A83:      abs((seq_id(tseq)-seq_id(vseq.n)).i) >=0 by ABSVALUE:5;
        abs((seq_id(tseq)-seq_id(vseq.n)).i) to_power p >= 0 by A2,A83,Lm1;
        hence ((seq_id(tseq)-seq_id(vseq.n)) rto_power p) .i >= 0 by A82;
       end;
A84:   Partial_Sums
           ((seq_id(tseq)-seq_id(vseq.n)) rto_power p) is bounded_above
       proof
         now
          take e2=e+1;
A85:       e2>e by REAL_1:69;
           let i be Nat;
           0 <= i by NAT_1:18; then
           Partial_Sums
           ((seq_id(tseq)-seq_id(vseq.n)) rto_power p) .i <=e by A75;
           hence Partial_Sums
           ((seq_id(tseq)-seq_id(vseq.n)) rto_power p) .i <e2
           by A85,AXIOMS:22;
         end;
         hence thesis by SEQ_2:def 3;
       end;
A86:    Sum ((seq_id(tseq)-seq_id(vseq.n)) rto_power p)
         = lim Partial_Sums((seq_id(tseq)-seq_id(vseq.n)) rto_power p)
         by SERIES_1:def 3;
         ((seq_id(tseq)-seq_id(vseq.n)) rto_power p) is summable
       by A81,A84,SERIES_1:20; then
         Partial_Sums((seq_id(tseq)-seq_id(vseq.n)) rto_power p)
         is convergent by SERIES_1:def 2; then
         lim Partial_Sums
         ((seq_id(tseq)-seq_id(vseq.n)) rto_power p) <= e by A75,Lm12;
         then Sum((seq_id(tseq)-seq_id(vseq.n)) rto_power p) <= e by A86;
         hence thesis by A47,A81,A84,AXIOMS:22,SERIES_1:20;
       end;
   hence thesis;
  end;
A87: (seq_id(tseq)) rto_power p is summable
   proof
      consider m be Nat such that
A88:  for n be Nat st n >= m holds
         ((seq_id(tseq)-seq_id(vseq.n)) rto_power p ) is summable &
         Sum((seq_id(tseq)-seq_id(vseq.n)) rto_power p) < 1 by A44;
A89: ((seq_id(tseq)-seq_id(vseq.m))
         rto_power p ) is summable by A88;
A90:  seq_id( (vseq.m) ) rto_power p is summable by A1,A6,Def2;
       set a=(seq_id(tseq) -seq_id(vseq.m));
       set b=(seq_id(vseq.m));
       set d=(seq_id(tseq));
A91:  a +b =seq_id(tseq)+-seq_id(vseq.m)+seq_id(vseq.m) by SEQ_1:15
          .=(seq_id(tseq)+seq_id(vseq.m))+(-seq_id(vseq.m)) by SEQ_1:20
          .=seq_id(tseq)+seq_id(vseq.m)-seq_id(vseq.m) by SEQ_1:15
          .=d by Lm13;
       (a + b) rto_power p is summable by A1,A89,A90,Lm14;
    hence seq_id(tseq) rto_power p is summable by A91;
end;
 reconsider tv=tseq as Point of lp by A1,A6,A87,Th10;
 for e be Real st
     e > 0 ex m be Nat st for n be Nat st
      n >= m holds ||.(vseq.n) - tv.|| < e
    proof
      let e be Real such that
A92:  e > 0;
      set e1=e to_power p;
A93: e1>0 by A92,POWER:39;
      consider m be Nat such that
 A94: for n be Nat st n >= m holds ((seq_id(tseq)-seq_id(vseq.n))
       rto_power p ) is summable &
         Sum((seq_id(tseq)-seq_id(vseq.n)) rto_power p) < e1 by A44,A93;
     now
     let n be Nat such that
A95:  n >= m;
A96: for i be Nat holds ((seq_id(tseq)-seq_id(vseq.n)) rto_power p).i >= 0
    proof
      let i be Nat;
A97:   ((seq_id(tseq)-seq_id(vseq.n)) rto_power p).i
       =abs((seq_id(tseq)-seq_id(vseq.n)).i) to_power p by Def1;
A98:   abs((seq_id(tseq)-seq_id(vseq.n)).i) >=0 by ABSVALUE:5;
      abs((seq_id(tseq)-seq_id(vseq.n)).i) to_power p >= 0 by A2,A98,Lm1;
      hence ((seq_id(tseq)-seq_id(vseq.n)) rto_power p).i >= 0 by A97;
  end;
   ((seq_id(tseq)-seq_id(vseq.n)) rto_power p ) is summable by A94,A95;
   then
A99:  Sum((seq_id(tseq)-seq_id(vseq.n)) rto_power p) >=0
               by A96,SERIES_1:21;
         Sum((seq_id(tseq)-seq_id(vseq.n)) rto_power p) < e1 by A94,A95; then
A100:   (Sum((seq_id(tseq)-seq_id(vseq.n)) rto_power p)) to_power (1/p )
                             <e1 to_power (1/p ) by A4,A99,Th1;
        e1 to_power(1/p) = e to_power (p * (1/p)) by A92,POWER:38
         .= e to_power 1 by A3,XCMPLX_1:107
         .= e by POWER:30;
      then
A101:  (Sum((seq_id(tv)-seq_id(vseq.n)) rto_power p)) to_power (1/p) <e by A100
;
       (Sum((seq_id(tv)-seq_id(vseq.n)) rto_power p)) to_power (1/p )
       =(Sum(seq_id(seq_id(tv)-seq_id(vseq.n)) rto_power p)) to_power (1/p )
       by RSSPACE:3
      .=(Sum(seq_id((tv)-(vseq.n)) rto_power p)) to_power (1/p )
       by A1,A6,Th10
      .=(the norm of lp).((tv)-(vseq.n)) by A6,Def3
      .= ||.(tv)-(vseq.n).|| by NORMSP_1:def 1
      .= ||.(tv)+(-(vseq.n)).|| by RLVECT_1:def 11
      .=||.-(vseq.n-tv).|| by RLVECT_1:47
      .=||.vseq.n-tv.|| by NORMSP_1:6;
        hence ||.vseq.n-tv.|| <e by A101;
      end;
      hence thesis;
    end;
 hence thesis by NORMSP_1:def 9;
 end;

definition
  let p be Real such that A1:1 <= p;
  func l_Space^p -> RealBanachSpace equals
   NORMSTR (# the_set_of_RealSequences_l^p,
        Zero_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
        Add_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
        Mult_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
         l_norm^p #);
  coherence
  proof
  set lp= NORMSTR (# the_set_of_RealSequences_l^p,
          Zero_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
          Add_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
          Mult_(the_set_of_RealSequences_l^p,Linear_Space_of_RealSequences),
         l_norm^p #);
   reconsider lp as RealNormSpace by A1,Th15;
   for vseq be sequence of lp st vseq is Cauchy_sequence_by_Norm
     holds vseq is convergent by A1,Th16; then
   A2: lp is complete by LOPBAN_1:def 16;
   thus thesis by A2;
  end;
end;
