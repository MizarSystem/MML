:: On Powers of Cardinals
::  by Grzegorz Bancerek
::
:: Received August 24, 1992
:: Copyright (c) 1992 Association of Mizar Users

environ

 vocabularies ORDINAL1, BOOLE, ORDINAL2, FINSEQ_1, FUNCT_1, CARD_1, PROB_1,
      RELAT_1, TARSKI, FINSET_1, WELLORD1, WELLORD2, ZFREFLE1, CARD_2,
      ORDINAL3, FUNCT_2, CARD_3, FUNCOP_1, RLVECT_1, MCART_1, CARD_5, ARYTM;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, CARD_1, NUMBERS, NAT_1,
      RELAT_1, FUNCT_1, FINSET_1, ORDINAL1, FUNCT_2, WELLORD1, WELLORD2,
      MCART_1, FUNCOP_1, ORDINAL2, ORDINAL3, CARD_2, CARD_3, ORDINAL4,
      XXREAL_0;
 constructors WELLORD1, WELLORD2, FUNCOP_1, ORDINAL3, XXREAL_0, NAT_1,
      FINSEQ_1, CARD_2, CARD_3, ORDINAL4, RELSET_1;
 registrations XBOOLE_0, FUNCT_1, ORDINAL1, FUNCOP_1, ORDINAL2, ORDINAL3,
      XXREAL_0, NAT_1, CARD_1, CARD_3, ORDINAL4;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, FUNCT_1, ORDINAL2, CARD_1, CARD_3, XBOOLE_0;
 theorems TARSKI, FUNCT_1, FUNCT_2, FUNCOP_1, NAT_1, FINSET_1, MCART_1,
      ORDINAL1, ORDINAL2, ORDINAL3, ORDINAL4, WELLORD1, WELLORD2, CARD_1,
      CARD_2, CARD_3, CARD_4, ZFMISC_1, FUNCT_4, FUNCT_5, RELAT_1, XBOOLE_0,
      XBOOLE_1;
 schemes FUNCT_1, ORDINAL1, PARTFUN1, CARD_1, CARD_3, ORDINAL2, CLASSES1,
      XBOOLE_0;

begin :: Results of [(30),AXIOMS].

reserve k,n,m for Element of NAT,
  A,B,C for Ordinal,
  X for set,
  x,y,z for set;

Lm1: 0 = card 0 & 1 = card 1 & 2 = card 2 by CARD_1:def 5;

canceled 8;

begin :: Infinity, alephs and cofinality

reserve f,g,h,fx for Function,
  K,M,N for Cardinal,
  phi,psi for Ordinal-Sequence;

theorem Th9:
  nextcard card X = nextcard X
proof
  card card X in nextcard X & for M st card card X in M holds nextcard
  X c= M by CARD_1:def 6;
  hence thesis by CARD_1:def 6;
end;

theorem Th10:
  y in Union f iff ex x st x in dom f & y in f.x
proof
  thus y in Union f implies ex x st x in dom f & y in f.x
  proof
    assume y in Union f;
    then consider X such that
A1: y in X & X in rng f by TARSKI:def 4;
    consider x such that
A2: x in dom f & X = f.x by A1,FUNCT_1:def 5;
    take x; thus thesis by A1,A2;
  end;
  given x such that
A3: x in dom f & y in f.x;
  f.x in rng f by A3,FUNCT_1:def 5;
  hence thesis by A3,TARSKI:def 4;
end;

theorem Th11:
  alef A is infinite
proof
  {} c= A by XBOOLE_1:2;
  then omega c= alef A by CARD_1:43,83;
  hence thesis by CARD_3:102;
end;

theorem Th12:
  M is infinite implies ex A st M = alef A
proof
  defpred P[set] means $1 is infinite implies ex A st $1 = alef A;
A1: P[{}];
A2: P[K] implies P[nextcard K]
  proof
    assume that
A3: P[K] and
A4: not nextcard K is finite;
    now
      assume K is finite;
      then reconsider K' = K as finite set;
      card K = card card K' & card K = K by CARD_1:def 5;
      then nextcard K = card (card K' + 1) by NAT_1:43;
      hence contradiction by A4;
    end;
    then consider A such that
A5: K = alef A by A3;
    take succ A; thus thesis by A5,CARD_1:39;
  end;
A6: K <> {} & K is limit_cardinal & (for N st N in K holds P[N]) implies P[K]
  proof
    assume that
A7: K <> {} & K is limit_cardinal and
A8: for N st N in K holds P[N] and
A9: not K is finite;
    defpred P[set] means ex N st N = $1 & not N is finite;
    consider X such that
A10: x in X iff x in K & P[x] from XBOOLE_0:sch 1;
    defpred R[set,set] means ex A st $1 = alef A & $2 = A;
A12: for x st x in X ex y st R[x,y]
    proof
      let x;
      assume
A13:  x in X;
      then consider N such that
A14:  N = x & not N is finite by A10;
      N in K by A10,A13,A14;
      then ex A st x = alef A by A8,A14;
      hence thesis;
    end;
    consider f such that
A15: dom f = X & for x st x in X holds R[x,f.x] from CLASSES1:sch 1(A12);
    now
      let x;
      assume x in rng f;
      then consider y such that
A16:  y in X & x = f.y by A15,FUNCT_1:def 5;
      consider A such that
A17:  y = alef A & x = A by A15,A16;
      thus x is Ordinal by A17;
      thus x c= rng f
      proof
        let z;
        assume
A18:    z in x;
        then reconsider z' = z as Ordinal by A17;
        alef z' in alef A & alef A in K by A10,A16,A17,A18,CARD_1:41;
        then alef z' in K & not alef z' is finite by Th11,ORDINAL1:19;
        then
A19:    alef z' in X by A10;
        then ex A st alef z' = alef A & f.(alef z') = A by A15;
        then z' = f.(alef z') by CARD_1:42;
        hence thesis by A15,A19,FUNCT_1:def 5;
      end;
    end;
    then reconsider A = rng f as Ordinal by ORDINAL1:31;
    take A;
    deffunc a(Ordinal) = alef $1;
    consider L being T-Sequence such that
A20: dom L = A & for B st B in A holds L.B = a(B) from ORDINAL2:sch 2;
    now
      let B;
      assume B in A;
      then consider x such that
A21:  x in X & B = f.x by A15,FUNCT_1:def 5;
      consider C such that
A22:  x = alef C & B = C by A15,A21;
      alef succ C = nextcard alef C & alef C in K by A10,A21,A22,CARD_1:39;
      then alef succ C <> K & alef succ C c= K by A7,CARD_1:def 7,CARD_3:107;
      then alef succ C in K & not alef succ C is finite by Th11,CARD_1:13;
      then
A23:  alef succ C in X by A10;
      then consider D being Ordinal such that
A24:  alef succ C = alef D & f.(alef succ C) = D by A15;
      succ C = D by A24,CARD_1:42;
      hence succ B in A by A15,A22,A23,A24,FUNCT_1:def 5;
    end;
    then A is limit_ordinal by ORDINAL1:41;
    then
A25: A = {} or alef A = card sup L by A20,CARD_1:40;
    sup L c= K
    proof
      let x;
      assume
A26:  x in sup L;
      then reconsider x' = x as Ordinal;
      consider C such that
A27:  C in rng L & x' c= C by A26,ORDINAL2:29;
      consider y such that
A28:  y in dom L & C = L.y by A27,FUNCT_1:def 5;
      reconsider y as Ordinal by A28;
A29:  C = alef y & not alef y is finite by A20,A28,Th11;
      consider z such that
A30:  z in dom f & y = f.z by A20,A28,FUNCT_1:def 5;
      ex D being Ordinal st z = alef D & y = D by A15,A30;
      then C in K & K = K by A10,A15,A29,A30;
      hence thesis by A27,ORDINAL1:22;
    end;
    then card sup L c= card K by CARD_1:27;
    then
A31: card sup L c= K by CARD_1:def 5;
    now per cases;
      case A = {};
        then not omega in X & not omega is finite by A15,RELAT_1:65;
        then not omega in K by A10;
        then K c= omega & omega c= K by A9,CARD_1:14,CARD_3:102;
        hence K = omega by XBOOLE_0:def 10;
      end;
      case
A32:    A <> {};
        assume K <> alef A;
        then card sup L in K & not alef A is finite by A25,A31,A32,Th11,
CARD_1:13;
        then
A33:    card sup L in X by A10,A25,A32;
        then consider B such that
A34:    card sup L = alef B & f.(card sup L) = B by A15;
        A = B by A25,A32,A34,CARD_1:42;
        then A in A by A15,A33,A34,FUNCT_1:def 5;
        hence contradiction;
      end;
    end;
    hence thesis by CARD_1:83;
  end;
  for M holds P[M] from CARD_1:sch 1(A1,A2,A6);
  hence thesis;
end;

registration
  let phi;
  cluster Union phi -> ordinal;
  coherence
  proof ex A st rng phi c= A by ORDINAL2:def 8;
    then On rng phi = rng phi & union rng phi = Union phi by ORDINAL3:8;
    hence thesis by ORDINAL3:7;
  end;
end;

canceled;

theorem Th14:
  X c= A implies ex phi st
  phi = canonical_isomorphism_of(RelIncl order_type_of RelIncl X, RelIncl X) &
  phi is increasing & dom phi = order_type_of RelIncl X & rng phi = X
proof
  set R = RelIncl X;
  set B = order_type_of R;
  set phi = canonical_isomorphism_of (RelIncl B,R);
  assume
A1: X c= A;
  then
A2: R is well-ordering & RelIncl B is well-ordering & B c= A by WELLORD2:9,17;
  then R, RelIncl B are_isomorphic by WELLORD2:def 2;
  then RelIncl B, R are_isomorphic by WELLORD1:50;
  then
A3: phi is_isomorphism_of RelIncl B,R & field RelIncl B = B & field R = X
  by A2,WELLORD1:def 9,WELLORD2:def 1;
  then
A4: dom phi = B & rng phi = X & phi is one-to-one &
  for x,y holds [x,y] in RelIncl B iff x in B & y in B & [phi.x,phi.y] in R
  by WELLORD1:def 7;
  then reconsider phi as T-Sequence by ORDINAL1:def 7;
  reconsider phi as Ordinal-Sequence by A1,A4,ORDINAL2:def 8;
  take phi;
  thus phi = canonical_isomorphism_of (RelIncl order_type_of RelIncl X,
  RelIncl X);
  thus phi is increasing
  proof
    let a,b be Ordinal;
    assume
A5: a in b & b in dom phi;
    then
A6: a in dom phi & a c= b & a <> b by ORDINAL1:19,def 2;
    reconsider a' = phi.a, b' = phi.b as Ordinal;
    [a,b] in RelIncl B by A4,A5,A6,WELLORD2:def 1;
    then [a',b'] in R & a' in X & b' in X by A4,A5,A6,FUNCT_1:def 5;
    then a' c= b' & a' <> b' by A4,A5,A6,FUNCT_1:def 8,WELLORD2:def 1;
    then a' c< b' & a' <> b' by XBOOLE_0:def 8;
    hence thesis by ORDINAL1:21;
  end;
  thus thesis by A3,WELLORD1:def 7;
end;

theorem Th15:
  X c= A implies sup X is_cofinal_with order_type_of RelIncl X
proof
  assume
A1: X c= A;
  then consider phi such that
A2: phi = canonical_isomorphism_of (RelIncl order_type_of RelIncl X,
  RelIncl X) & phi is increasing &
  dom phi = order_type_of RelIncl X & rng phi = X by Th14;
  take phi;
  On X = X by A1,ORDINAL3:8;
  hence thesis by A2,ORDINAL2:def 7;
end;

theorem Th16:
  X c= A implies card X = card order_type_of RelIncl X
proof
  set R = RelIncl X;
  set B = order_type_of R;
  set phi = canonical_isomorphism_of (RelIncl B,R);
  assume X c= A;
  then
A1: R is well-ordering & RelIncl B is well-ordering & B c= A by WELLORD2:9,17;
  then R, RelIncl B are_isomorphic by WELLORD2:def 2;
  then RelIncl B, R are_isomorphic by WELLORD1:50;
  then phi is_isomorphism_of RelIncl B,R & field RelIncl B = B & field R = X
  by A1,WELLORD1:def 9,WELLORD2:def 1;
  then dom phi = B & rng phi = X & phi is one-to-one &
  for x,y holds [x,y] in RelIncl B iff x in B & y in B & [phi.x,phi.y] in R
  by WELLORD1:def 7;
  then B,X are_equipotent by WELLORD2:def 4;
  hence thesis by CARD_1:21;
end;

theorem Th17:
  ex B st B c= card A & A is_cofinal_with B
proof
  set M = card A;
  M,A are_equipotent by CARD_1:def 5;
  then consider f such that
A1: f is one-to-one & dom f = M & rng f = A by WELLORD2:def 4;
  defpred P[set] means not f.$1 in Union (f|$1);
  consider X such that
A2: x in X iff x in M & P[x] from XBOOLE_0:sch 1;
  reconsider f as T-Sequence by A1,ORDINAL1:def 7;
  reconsider f as Ordinal-Sequence by A1,ORDINAL2:def 8;
  set R = RelIncl X;
  set B = order_type_of R;
  set phi = canonical_isomorphism_of (RelIncl B,R);
  take B;
A3: X c= M
  proof
    let x; thus thesis by A2;
  end;
  then
A4: R is well-ordering & RelIncl B is well-ordering by WELLORD2:9;
  then R, RelIncl B are_isomorphic by WELLORD2:def 2;
  then RelIncl B, R are_isomorphic by WELLORD1:50;
  then
A5: phi is_isomorphism_of RelIncl B,R & field RelIncl B = B & field R = X
  by A4,WELLORD1:def 9,WELLORD2:def 1;
  then
A6: dom phi = B & rng phi = X & phi is one-to-one &
  for x,y holds [x,y] in RelIncl B iff x in B & y in B & [phi.x,phi.y] in R
  by WELLORD1:def 7;
  then reconsider phi as T-Sequence by ORDINAL1:def 7;
  reconsider phi as Ordinal-Sequence by A3,A6,ORDINAL2:def 8;
  thus B c= card A by A3,WELLORD2:17;
A7: dom (f*phi) = B & rng (f*phi) c= A by A1,A3,A6,RELAT_1:45,46;
  then reconsider xi = f*phi as T-Sequence by ORDINAL1:def 7;
  reconsider xi as Ordinal-Sequence by A7,ORDINAL2:def 8;
  take xi;
  thus dom xi = B & rng xi c= A by A1,A3,A6,RELAT_1:45,46;
  thus xi is increasing
  proof
    let a,b be Ordinal;
    assume
A8: a in b & b in dom xi;
    then
A9: a in dom xi & a c= b & a <> b & xi is one-to-one
    by A1,A6,ORDINAL1:19,def 2;
    then
A10: phi.a <> phi.b & [a,b] in RelIncl B & phi.a in X & phi.b in X
    by A6,A7,A8,FUNCT_1:def 5,def 8,WELLORD2:def 1;
    reconsider a' = phi.a, b' = phi.b as Ordinal;
    reconsider a'' = f.a', b'' = f.b' as Ordinal;
    [phi.a,phi.b] in R by A5,A10,WELLORD1:def 7;
    then a' c= b' by A10,WELLORD2:def 1;
    then a' c< b' by A10,XBOOLE_0:def 8;
    then
A11: a' in b' & not b'' in Union (f|b') & a'' <> b''
    by A1,A2,A3,A10,FUNCT_1:def 8,ORDINAL1:21;
    then a'' in rng (f|b') & Union (f|b') = union rng (f|b')
    by A1,A3,A10,FUNCT_1:73;
    then a'' c= Union (f|b') & Union (f|b') c= b''
    by A11,ORDINAL1:26,ZFMISC_1:92;
    then
A12: a'' c= b'' & a'' = xi.a & b'' = xi.b by A8,A9,FUNCT_1:22,XBOOLE_1:1;
    then a'' c< b'' by A11,XBOOLE_0:def 8;
    hence thesis by A12,ORDINAL1:21;
  end;
  thus A c= sup xi
  proof
    let x;
    assume x in A;
    then consider y such that
A14: y in dom f & x = f.y by A1,FUNCT_1:def 5;
    reconsider x' = x, y as Ordinal by A14;
    now per cases;
      suppose not f.y in Union (f|y);
        then y in X by A1,A2,A14;
        then consider z such that
A15:    z in B & y = phi.z by A6,FUNCT_1:def 5;
        x' = xi.z & xi.z in rng xi by A7,A14,A15,FUNCT_1:22,def 5;
        hence thesis by ORDINAL2:27;
      end;
      suppose f.y in Union (f|y);
        then consider z such that
A16:    z in dom (f|y) & f.y in (f|y).z by Th10;
        reconsider z as Ordinal by A16;
        defpred P[Ordinal] means $1 in y & f.y in f.$1;
        dom (f|y) = dom f /\ y by RELAT_1:90;
        then (f|y).z = f.z & z in y by A16,FUNCT_1:70,XBOOLE_0:def 4;
        then
A17:    ex C st P[C] by A16;
        consider C such that
A18:    P[C] & for B st P[B] holds C c= B from ORDINAL1:sch 1(A17);
        now thus C in M by A1,A14,A18,ORDINAL1:19;
          assume f.C in Union (f|C);
          then consider a be set such that
A19:      a in dom (f|C) & f.C in (f|C).a by Th10;
          reconsider a as Ordinal by A19;
          reconsider fa = (f|C).a, fc = f.C, fy = f.y as Ordinal;
          dom (f|C) = dom f /\ C & f.a = fa & fc in fa
          by A19,FUNCT_1:70,RELAT_1:90;
          then
A20:      a in C & fy in f.a by A18,A19,ORDINAL1:19,XBOOLE_0:def 4;
          then a in y & not C c= a by A18,ORDINAL1:7,19;
          hence contradiction by A18,A20;
        end;
        then C in X by A2;
        then consider z such that
A21:    z in B & C = phi.z by A6,FUNCT_1:def 5;
        reconsider z as Ordinal by A21;
        reconsider xz = xi.z as Ordinal;
        xz = f.C & xz in rng xi by A7,A21,FUNCT_1:22,def 5;
        then xz in sup xi & x' in xz by A14,A18,ORDINAL2:27;
        hence thesis by ORDINAL1:19;
      end;
    end;
    hence thesis;
  end;
  sup A = A by ORDINAL2:26;
  hence thesis by A7,ORDINAL2:30;
end;

theorem Th18:
  ex M st M c= card A & A is_cofinal_with M &
  for B st A is_cofinal_with B holds M c= B
proof
  defpred P[Ordinal] means $1 c= card A & A is_cofinal_with $1;
A1: ex B st P[B] by Th17;
  consider C such that
A2: P[C] and
A3: for B st P[B] holds C c= B from ORDINAL1:sch 1(A1);
  take M = card C;
  consider B such that
A4: B c= M & C is_cofinal_with B by Th17;
A5: M c= C by CARD_1:24;
  then
A6: B c= C by A4,XBOOLE_1:1;
  then B c= card A & A is_cofinal_with B by A2,A4,ORDINAL4:39,XBOOLE_1:1;
  then C c= B by A3;
  then
A7: C = B by A6,XBOOLE_0:def 10;
  then
A8: C = M by A4,A5,XBOOLE_0:def 10;
  thus M c= card A & A is_cofinal_with M by A2,A4,A5,A7,XBOOLE_0:def 10;
  let B;
  assume A is_cofinal_with B & not M c= B;
  then not B c= card A & B c= M by A3,A8;
  hence contradiction by A2,A8,XBOOLE_1:1;
end;

Lm2: rng phi = rng psi & phi is increasing & psi is increasing implies
for A st A in dom phi holds A in dom psi & phi.A = psi.A
proof
  assume
A1: rng phi = rng psi & phi is increasing;
  assume
A2: for A,B st A in B & B in dom psi holds psi.A in psi.B;
  defpred P[Ordinal] means
  $1 in dom phi implies $1 in dom psi & phi.$1 = psi.$1;
A3: for A st for B st B in A holds P[B] holds P[A]
  proof
    let A;
    assume that
A4: for B st B in A & B in dom phi holds B in dom psi & phi.B = psi.B and
A5: A in dom phi;
    phi.A in rng phi by A5,FUNCT_1:def 5;
    then consider x such that
A6: x in dom psi & phi.A = psi.x by A1,FUNCT_1:def 5;
    reconsider x as Ordinal by A6;
A7: now
      assume
A8:   x in A;
      then phi.x in phi.A & x in dom phi by A1,A5,ORDINAL1:19,ORDINAL2:def 16;
      then phi.A in phi.A by A4,A6,A8;
      hence contradiction;
    end;
    now
      assume A in x;
      then
A9:   psi.A in psi.x & A in dom psi by A2,A6,ORDINAL1:19;
      then psi.A in rng psi by FUNCT_1:def 5;
      then consider y such that
A10:  y in dom phi & psi.A = phi.y by A1,FUNCT_1:def 5;
      reconsider y as Ordinal by A10;
      not A c= y by A1,A6,A9,A10,ORDINAL1:7,ORDINAL4:9;
      then y in A by ORDINAL1:26;
      then psi.y in psi.A & psi.y = phi.y by A2,A4,A9,A10;
      hence contradiction by A10;
    end;
    hence thesis by A6,A7,ORDINAL1:24;
  end;
  thus P[A] from ORDINAL1:sch 2(A3);
end;

theorem
  rng phi = rng psi & phi is increasing & psi is increasing implies phi = psi
proof
  assume
A1: rng phi = rng psi & phi is increasing & psi is increasing;
A2: dom phi = dom psi
  proof
    thus dom phi c= dom psi
    proof
      let x;
      assume x in dom phi;
      hence thesis by A1,Lm2;
    end;
    let x;
    assume x in dom psi;
    hence thesis by A1,Lm2;
  end;
  for x st x in dom phi holds phi.x = psi.x by A1,Lm2;
  hence thesis by A2,FUNCT_1:9;
end;

theorem Th20:
  phi is increasing implies phi is one-to-one
proof
  assume
A1: for A,B st A in B & B in dom phi holds phi.A in phi.B;
  let x,y;
  assume
A2: x in dom phi & y in dom phi & phi.x = phi.y;
  then reconsider A = x, B = y as Ordinal;
  not phi.A in phi.B & (A in B or A = B or B in A) by A2,ORDINAL1:24;
  hence thesis by A1,A2;
end;

theorem Th21:
  (phi^psi)|(dom phi) = phi
proof
  dom (phi^psi) = (dom phi)+^(dom psi) &
  for A st A in dom phi holds (phi^psi).A = phi.A by ORDINAL4:def 1;
  then dom phi c= dom (phi^psi) by ORDINAL3:27;
  then
A1: dom phi = (dom (phi^psi))/\(dom phi) by XBOOLE_1:28;
  for x st x in dom phi holds phi.x = (phi^psi).x by ORDINAL4:def 1;
  hence thesis by A1,FUNCT_1:68;
end;

theorem
  X <> {} implies
  card { Y where Y is Subset of X: card Y in M } c= M*`exp(card X,M)
proof
  assume
Z:  X <> {};
  X,card X are_equipotent by CARD_1:def 5;
  then consider f such that
A2: f is one-to-one & dom f = X & rng f = card X by WELLORD2:def 4;
  set Z = { Y where Y is Subset of X: card Y in M };
  defpred P[set,set] means ex A be Ordinal, phi be Ordinal-Sequence st
  $2 = [A,phi] & dom phi = M & phi|A is increasing &
  rng (phi|A) = f.:$1 & for B st A c= B & B in M holds phi.B = {};
A10: for x st x in Z ex y st P[x,y]
  proof
    let x;
    assume x in Z;
    then
A11: ex Y being Subset of X st x = Y & card Y in M;
    set A = order_type_of RelIncl (f.:x);
    card (f.:x) = card A & card (f.:x) c= card x
     by Th16,A2,CARD_2:3,RELAT_1:144;
    then
A13: card A in M by A11,ORDINAL1:22;
    consider xi1 being Ordinal-Sequence such that
A14: xi1 = canonical_isomorphism_of (RelIncl A, RelIncl (f.:x)) &
    xi1 is increasing & dom xi1 = A & rng xi1 = f.:x by A2,Th14,RELAT_1:144;
    deffunc f(set) = {};
    consider xi2 being Ordinal-Sequence such that
A15: dom xi2 = M -^ A & for B st B in M -^ A holds xi2.B = f(B)
    from ORDINAL2:sch 3;
    set phi = xi1^xi2;
    take y = [A,phi], A, phi;
    A in M by A13,CARD_3:60;
    then A c= M by ORDINAL1:def 2;
    then A+^( M -^ A) = M & phi|A = xi1 by A14,Th21,ORDINAL3:def 6;
    hence y = [A,phi] & dom phi = M & phi|A is increasing & rng (phi|A) = f.:x
    by A14,A15,ORDINAL4:def 1;
    let B;
    assume A c= B & B in M;
    then B = A+^(B-^A) & B-^A in M-^A by ORDINAL3:66,def 6;
    then phi.B = xi2.(B-^A) & xi2.(B-^A) = {} by A14,A15,ORDINAL4:def 1;
    hence thesis;
  end;
  consider g such that
A16: dom g = Z & for x st x in Z holds P[x,g.x] from CLASSES1:sch 1(A10);
  g is one-to-one
  proof
    let x,y;
    assume
A17: x in dom g & y in dom g & g.x = g.y;
    then
A18: (ex Y being Subset of X st x = Y & card Y in M) &
    (ex Y being Subset of X st y = Y & card Y in M) by A16;
    consider A1 be Ordinal, phi1 be Ordinal-Sequence such that
A19: g.x = [A1,phi1] & dom phi1 = M & phi1|A1 is increasing &
    rng (phi1|A1) = f.:x & for B st A1 c= B & B in M holds phi1.B = {}
    by A16,A17;
    consider A2 be Ordinal, phi2 be Ordinal-Sequence such that
A20: g.y = [A2,phi2] & dom phi2 = M & phi2|A2 is increasing &
    rng (phi2|A2) = f.:y & for B st A2 c= B & B in M holds phi2.B = {}
    by A16,A17;
A21: A1 = A2 & phi1 = phi2 by A17,A19,A20,ZFMISC_1:33;
    thus x c= y
    proof
      let z;
      assume
A22:  z in x;
      then f.z in f.:x by A2,A18,FUNCT_1:def 12;
      then ex x1 being set st
      x1 in dom f & x1 in y & f.z = f.x1 by A19,A20,A21,FUNCT_1:def 12;
      hence thesis by A2,A18,A22,FUNCT_1:def 8;
    end;
    let z;
    assume
A23: z in y;
    then f.z in f.:y by A2,A18,FUNCT_1:def 12;
    then ex x1 being set st
    x1 in dom f & x1 in x & f.z = f.x1 by A19,A20,A21,FUNCT_1:def 12;
    hence thesis by A2,A18,A23,FUNCT_1:def 8;
  end;
  then
A24: Z,rng g are_equipotent by A16,WELLORD2:def 4;
  rng g c= [:M,Funcs(M,card X):]
  proof
    let x;
    assume x in rng g;
    then consider y such that
A25: y in dom g & x = g.y by FUNCT_1:def 5;
    consider A,phi such that
A26: x = [A,phi] & dom phi = M & phi|A is increasing &
    rng (phi|A) = f.:y & for B st A c= B & B in M holds phi.B = {} by A16,A25;
A27: ex Y being Subset of X st y = Y & card Y in M by A16,A25;
    phi|A is one-to-one by A26,Th20;
    then dom (phi|A),f.:y are_equipotent by A26,WELLORD2:def 4;
    then card dom (phi|A) = card (f.:y) & card (f.:y) c= card y
    by CARD_1:21,CARD_2:3;
    then card dom (phi|A) in M by A27,ORDINAL1:22;
    then
A28: dom (phi|A) in M by CARD_3:60;
    then dom (phi|A) <> M & (A c= M or M c= A);
    then
A29: A in M by A26,A28,RELAT_1:91,97;
    rng phi c= card X
    proof
      let x;
      assume x in rng phi;
      then consider z such that
A30:  z in dom phi & x = phi.z by FUNCT_1:def 5;
      reconsider z as Ordinal by A30;
      z in A or A c= z by ORDINAL1:26;
      then x in f.:y & f.:y c= card X or x = {}
      by A2,A26,A30,FUNCT_1:73,RELAT_1:144;
      hence thesis by Z,ORDINAL3:10;
    end;
    then phi in Funcs(M,card X) by A26,FUNCT_2:def 2;
    hence thesis by A26,A29,ZFMISC_1:106;
  end;
  then
A31: card rng g c= card [:M,Funcs(M,card X):] by CARD_1:27;
  card [:M,Funcs(M,card X):] = card [:M,card Funcs(M,card X):] by CARD_2:14
    .= M*`card Funcs(M,card X) by CARD_2:def 2
    .= M*`exp(card X,M) by CARD_2:def 3;
  hence thesis by A24,A31,CARD_1:21;
end;

theorem Th23:
  M in exp(2,M)
proof
  card bool M = exp(2,card M) & card M in card bool M & card M = M
  by CARD_1:30,def 5,CARD_2:44;
  hence thesis;
end;

registration
  cluster infinite Cardinal;
  existence
  proof
    take omega; thus thesis;
  end;
end;

registration
  cluster infinite -> non empty set;
  coherence;
end;

definition
  mode Aleph is infinite Cardinal;
  let M;
  canceled;
  func cf M -> Cardinal means
  :Def2:
  M is_cofinal_with it &
  for N st M is_cofinal_with N holds it c= N;
  existence
  proof
    defpred P[Ordinal] means M is_cofinal_with $1 & $1 is Cardinal;
A1: ex A st P[A];
    consider A such that
A2: P[A] & for B st P[B] holds A c= B from ORDINAL1:sch 1(A1);
    reconsider K = A as Cardinal by A2;
    take K; thus M is_cofinal_with K by A2;
    let N;
    assume M is_cofinal_with N;
    hence thesis by A2;
  end;
  uniqueness
  proof
    let K1,K2 be Cardinal;
    assume M is_cofinal_with K1 &
    (for N st M is_cofinal_with N holds K1 c= N) & M is_cofinal_with K2 &
    for N st M is_cofinal_with N holds K2 c= N;
    then K1 c= K2 & K2 c= K1;
    hence thesis by XBOOLE_0:def 10;
  end;
  let N;
  func N-powerfunc_of M -> Cardinal-Function means
  :
  Def3: (for x holds x in dom it iff x in M & x is Cardinal) &
  for K st K in M holds it.K = exp(K,N);
  existence
  proof
    defpred P[set] means $1 is Cardinal;
    consider X such that
A3: x in X iff x in M & P[x] from XBOOLE_0:sch 1;
    deffunc f(set) = exp(card $1,N);
    consider f being Cardinal-Function such that
A4: dom f = X & for x st x in X holds f.x = f(x) from CARD_3:sch 1;
    take f; thus x in dom f iff x in M & x is Cardinal by A3,A4;
    let K;
    assume K in M;
    then K = card K & K in X by A3,CARD_1:def 5;
    hence thesis by A4;
  end;
  uniqueness
  proof
    let f1,f2 be Cardinal-Function;
    defpred P[set] means $1 in M & $1 is Cardinal;
    assume that
A5: for x holds x in dom f1 iff P[x] and
A6: for K st K in M holds f1.K = exp(K,N) and
A7: for x holds x in dom f2 iff P[x] and
A8: for K st K in M holds f2.K = exp(K,N);
A9: dom f1 = dom f2 from XBOOLE_0:sch 2(A5,A7);
    now
      let x;
      assume
A10:  x in dom f1;
      then reconsider K = x as Cardinal by A5;
A11:  K in M by A5,A10;
      hence f1.x = exp(K,N) by A6
        .= f2.x by A8,A11;
    end;
    hence thesis by A9,FUNCT_1:9;
  end;
end;

registration
  let A;
  cluster alef A -> infinite;
  coherence by Th11;
end;

begin :: Arithmetics of alephs

reserve a,b for Aleph;

theorem
  ex A st a = alef A by Th12;

theorem Th25:
  a <> 0 & a <> 1 & a <> 2 & a <> card n & card n in a & omega c= a
proof
  omega c= a & card 0 in omega & card 1 in omega &
  card 2 in omega & card n in omega by CARD_3:102;
  hence thesis;
end;

theorem Th26:
  a c= M or a in M implies M is Aleph
proof
  assume a c= M or a in M;
  then omega c= a & a c= M by Th25,CARD_1:13;
  then omega c= M by XBOOLE_1:1;
  hence thesis by CARD_3:102;
end;

theorem Th27:
  a c= M or a in M implies a +` M = M & M +` a = M & a *` M = M & M *` a = M
proof
  assume
A1: a c= M or a in M;
  then M is infinite & card 0 in a & card 0 = 0 by Th25,Th26;
  hence thesis by A1,CARD_3:118,CARD_4:78;
end;

theorem
  a +` a = a & a *` a = a by CARD_3:117,CARD_4:77;

canceled 2;

theorem Th31:
  M c= exp(M,a)
proof 1 in a by Lm1,Th25;
  then M = 0 & {} c= exp(M,a) or exp(M,1) c= exp(M,a) & exp(M,1) = M
  by CARD_2:40,CARD_3:139,XBOOLE_1:2;
  hence thesis;
end;

theorem
  union a = a by ORDINAL1:def 6;

registration
  let a,M;
  cluster a +` M -> infinite;
  coherence
  proof
    a c= M or M c= a;
    then a +` M = M & M is Aleph or a +` M = a by Th26,Th27,CARD_3:118;
    hence thesis;
  end;
end;

registration
  let M,a;
  cluster M +` a -> infinite;
  coherence;
end;

registration
  let a,b;
  cluster a *` b -> infinite;
  coherence
  proof
     a c= b or b c= a;
    hence thesis by Th27;
  end;
  cluster exp(a,b) -> infinite;
  coherence
  proof 0 <> a & a c= a & 1 in b by Lm1,Th25;
    then exp(a,1) = a & exp(a,1) c= exp(a,b) & omega c= a
    by Th25,CARD_2:40,CARD_3:138;
    then omega c= exp(a,b) by XBOOLE_1:1;
    hence thesis by CARD_3:102;
  end;
end;

begin :: Regular alephs

definition
  let IT be Aleph;
  attr IT is regular means

  cf IT = IT;
end;

notation
  let IT be Aleph;
  antonym IT is irregular for IT is regular;
end;

registration
  let a;
  cluster nextcard a -> infinite;
  coherence
  proof not a is finite & a in nextcard a by CARD_1:32;
    then omega c= nextcard a;
    hence thesis by CARD_3:102;
  end;
end;

canceled;

theorem Th34:
  cf omega = omega
proof
  assume
A1: cf omega <> omega;
  cf omega c= omega by Def2;
  then cf omega in omega by A1,CARD_1:13;
  then reconsider B = cf omega as finite set;
  set n = card B;
  card cf omega = cf omega & n = card n &
  card cf omega = card n by CARD_1:def 5;
  then omega is_cofinal_with n by Def2;
  then consider xi being Ordinal-Sequence such that
A2: dom xi = n & rng xi c= omega & xi is increasing &
  omega = sup xi by ORDINAL2:def 21;
  reconsider rxi = rng xi as finite set by A2,FINSET_1:26;
  defpred P[set,set] means $2 c= $1;
A3: rxi <> {} by A2,ORDINAL2:26;
A4: for x,y st P[x,y] & P[y,x] holds x = y by XBOOLE_0:def 10;
A5: for x,y,z st P[x,y] & P[y,z] holds P[x,z] by XBOOLE_1:1;
  consider x such that
A6: x in rxi & for y st y in rxi & y <> x holds not P[y,x]
  from CARD_3:sch 2(A3,A4,A5);
  reconsider x as Ordinal by A2,A6;
  now
    let A;
    assume A in rng xi;
    then A c= x or not x c= A by A6;
    hence A in succ x by ORDINAL1:34;
  end;
  then omega c= succ x & succ x in omega by A2,A6,ORDINAL1:41,ORDINAL2:28;
  hence contradiction by ORDINAL1:7;
end;

Lm4: 1 = card 1 by CARD_1:def 5;

theorem Th35:
  cf nextcard a = nextcard a
proof
  nextcard a is_cofinal_with cf nextcard a by Def2;
  then consider xi being Ordinal-Sequence such that
A1: dom xi = cf nextcard a & rng xi c= nextcard a &
  xi is increasing & nextcard a = sup xi by ORDINAL2:def 21;
A2: dom Card xi = dom xi & dom (cf nextcard a --> a) = cf nextcard a
  by CARD_3:def 2,FUNCOP_1:19;
  now
    let x;
    assume
Z:     x in cf nextcard a;
    then
A3: (Card xi).x = card (xi.x) & (cf nextcard a --> a).x = a &
    xi.x in rng xi by A1,CARD_3:def 2,FUNCOP_1:13,FUNCT_1:def 5;
    reconsider A = xi.x as Ordinal by Z;
    card (xi.x) in nextcard a by A1,A3,CARD_1:24,ORDINAL1:22;
    hence (Card xi).x c= (cf nextcard a --> a).x by A3,CARD_3:108;
  end;
  then
A4: card Union xi c= Sum Card xi & Sum Card xi c= Sum
  (cf nextcard a --> a) & Sum (cf nextcard a --> a) = (cf nextcard a)*`a
  by A1,A2,CARD_3:43,52,54;
  ex A st rng xi c= A by ORDINAL2:def 8;
  then On rng xi = rng xi & sup rng xi c= succ union On rng xi &
  union rng xi = Union xi & card Union xi c= (cf nextcard a)*`a &
  sup rng xi = sup xi by A4,ORDINAL3:8,85,XBOOLE_1:1;
  then
A5: card nextcard a c= card succ Union xi &
  card nextcard a = nextcard a & succ Union xi = (Union xi) +^ 1 &
  (card Union xi) +` 1 c= (cf nextcard a)*`a +` 1 &
  card ((Union xi) +^ 1) = (card Union xi) +` card 1
  by A1,CARD_1:27,def 5,CARD_2:24,CARD_3:126,ORDINAL2:48;
  then
A6: nextcard a c= (cf nextcard a)*`a +` 1 &
  cf nextcard a c= nextcard a by Def2,Lm4,XBOOLE_1:1;
  now per cases;
    suppose cf nextcard a = 0;
      then (cf nextcard a)*`a = 0 & 0+`1 = 1 & 1 in nextcard a
      by Lm1,Th25,CARD_2:29,32;
      hence thesis by A5,Lm4,CARD_1:14;
    end;
    suppose
A8:   cf nextcard a <> 0;
      0 c= cf nextcard a by XBOOLE_1:2;
      then 0 in cf nextcard a & 1 in a &
      (cf nextcard a c= a or a c= cf nextcard a)
      by A8,Lm1,Th25,CARD_1:13;
      then (cf nextcard a)*`a = a & a+`1 = a & a in nextcard a or
      (cf nextcard a)*`a = cf nextcard a & cf nextcard a is Aleph
      by Th26,Th27,CARD_1:32,CARD_3:118,CARD_4:78;
      then nextcard a c= (cf nextcard a) +` 1 & cf nextcard a is Aleph &
      1 in cf nextcard a by A5,Lm1,Th25,CARD_1:14,XBOOLE_1:1;
      then nextcard a c= cf nextcard a by CARD_3:118;
      hence thesis by A6,XBOOLE_0:def 10;
    end;
  end;
  hence thesis;
end;

theorem Th36:
  omega c= cf a
proof
A1: a is_cofinal_with cf a & a is limit_ordinal by Def2;
  then
A2: cf a is limit_ordinal by ORDINAL4:40;
  cf a <> {} by A1,ORDINAL2:68;
  then {} in cf a by ORDINAL3:10;
  hence thesis by A2,ORDINAL1:def 12;
end;

theorem
  cf 0 = 0 & cf card (n+1) = 1
proof cf 0 c= 0 & 0 c= cf 0 by Def2,XBOOLE_1:2;
  hence cf 0 = 0 by XBOOLE_0:def 10;
A1: card (n+1) = n+1 & n+1 = succ n &
  card (n+1) is_cofinal_with cf card (n+1) &
  succ n is_cofinal_with 1 by Def2,CARD_1:def 5,NAT_1:39,ORDINAL3:86;
  then cf card (n+1) c= 1 by Def2;
  then cf card (n+1) = 1 or cf card (n+1) = 0 & {} c= n &
  n in succ n by ORDINAL1:10,ORDINAL3:19,XBOOLE_1:2;
  hence thesis by A1,ORDINAL2:68;
end;

theorem Th38:
  X c= M & card X in cf M implies sup X in M & union X in M
proof
  assume
A1: X c= M & card X in cf M;
  set A = order_type_of (RelIncl X);
A2: sup X is_cofinal_with A & A c= M & card A = card X
  by A1,Th15,Th16,WELLORD2:17;
  consider N such that
A3: N c= card A & A is_cofinal_with N &
  for C st A is_cofinal_with C holds N c= C by Th18;
A4: N in cf M & sup X is_cofinal_with N & sup X c= sup M &
  sup M = M by A1,A2,A3,ORDINAL1:22,ORDINAL2:26,30,ORDINAL4:39;
  now
    assume sup X = M;
    then cf M c= N by A4,Def2;
    hence contradiction by A1,A2,A3,CARD_1:14;
  end;
  then
A5: sup X c< M by A4,XBOOLE_0:def 8;
  hence sup X in M by ORDINAL1:21;
  for x st x in X holds x is Ordinal by A1;
  then reconsider A = union X as Ordinal by ORDINAL1:35;
  A c= sup X
  proof
    let x;
    assume x in A;
    then consider Y being set such that
A6: x in Y & Y in X by TARSKI:def 4;
    reconsider Y as Ordinal by A1,A6;
    Y in sup X by A6,ORDINAL2:27;
    then Y c= sup X by ORDINAL1:def 2;
    hence thesis by A6;
  end;
  hence thesis by A5,ORDINAL1:21,22;
end;

theorem Th39:
  dom phi = M & rng phi c= N & M in cf N implies sup phi in N & Union phi in N
proof
  assume
A1: dom phi = M & rng phi c= N & M in cf N;
  then card rng phi c= card M & card M = M by CARD_1:28,def 5;
  then card rng phi in cf N & Union phi = union rng phi & sup phi = sup rng
  phi by A1,ORDINAL1:22;
  hence thesis by A1,Th38;
end;

registration
  let a;
  cluster cf a -> infinite;
  coherence by Th26,Th36;
end;

theorem Th40:
  cf a in a implies a is limit_cardinal
proof
  assume
A1: cf a in a;
  given M such that
A2: a = nextcard M;
  cf a c= M by A1,A2,CARD_3:108;
  then reconsider M as Aleph by Th26;
  nextcard M in nextcard M by A1,A2,Th35;
  hence contradiction;
end;

theorem Th41:
  cf a in a implies ex xi being Ordinal-Sequence st
  dom xi = cf a & rng xi c= a & xi is increasing & a = sup xi &
  xi is Cardinal-Function & not 0 in rng xi
proof
  assume cf a in a;
  then
A1: a is limit_cardinal by Th40;
  a is_cofinal_with cf a by Def2;
  then consider xi being Ordinal-Sequence such that
A2: dom xi = cf a & rng xi c= a &
  xi is increasing & a = sup xi by ORDINAL2:def 21;
  deffunc f(T-Sequence) = (nextcard (xi.dom $1)) \/ nextcard sup $1;
  consider phi being T-Sequence such that
A3: dom phi = cf a &
  for A for psi being T-Sequence st A in cf a & psi = phi|A holds
  phi.A = f(psi) from ORDINAL1:sch 4;
A4: cf a = cf a & a = a & sup a = a by ORDINAL2:26;
  phi is Ordinal-yielding
  proof
    take sup rng phi;
    let x;
    assume
A5: x in rng phi;
    then consider y such that
A6: y in dom phi & x = phi.y by FUNCT_1:def 5;
    reconsider y as Ordinal by A6;
    y c= dom phi by A6,ORDINAL1:def 2;
    then dom (phi|y) = y by RELAT_1:91;
    then x = ( nextcard (xi.y)) \/ nextcard sup (phi|y) by A3,A6;
    hence thesis by A5,ORDINAL2:27;
  end;
  then reconsider phi as Ordinal-Sequence;
  take phi; thus dom phi = cf a by A3;
  defpred P[Ordinal] means $1 in cf a implies phi.$1 in a;
A7: now
    let A such that
A8: for B st B in A holds P[B];
    thus P[A]
    proof
      assume
A9:   A in cf a;
      then A c= dom phi by A3,ORDINAL1:def 2;
      then
A10:  dom (phi|A) = A by RELAT_1:91;
      then
A11:  phi.A = (nextcard (xi.A)) \/ nextcard sup (phi|A) by A3,A9;
      xi.A in rng xi & sup xi = sup rng xi by A2,A9,FUNCT_1:def 5;
      then card (xi.A) in a by A2,CARD_1:25;
      then
A12:  nextcard card (xi.A) c= a & a <> nextcard card (xi.A) &
      nextcard card (xi.A) = nextcard (xi.A) by A1,Th9,CARD_1:def 7,CARD_3:107;
      (phi|A).:A = rng (phi|A) by A10,RELAT_1:146;
      then card rng (phi|A) c= card A & card A in cf a
      by A9,CARD_1:25,CARD_2:3;
      then
A13:  card rng (phi|A) in cf a by ORDINAL1:22;
      rng (phi|A) c= a
      proof
        let x;
        assume x in rng (phi|A);
        then consider y such that
A14:    y in dom (phi|A) & x = (phi|A).y by FUNCT_1:def 5;
        reconsider y as Ordinal by A14;
        x = phi.y & y in cf a by A9,A10,A14,FUNCT_1:70,ORDINAL1:19;
        hence thesis by A8,A10,A14;
      end;
      then sup rng (phi|A) in a & sup rng (phi|A) = sup (phi|A) by A13,Th38;
      then card sup (phi|A) in a by CARD_1:25;
      then nextcard card sup (phi|A) c= a & nextcard card sup (phi|A) <> a &
      nextcard card sup (phi|A) = nextcard sup (phi|A)
      by A1,Th9,CARD_1:def 7,CARD_3:107;
      then (phi.A = nextcard (xi.A) or phi.A = nextcard sup (phi|A)) &
      nextcard (xi.A) = nextcard (xi.A) &
      nextcard sup (phi|A) = nextcard sup (phi|A) &
      nextcard (xi.A) in a & nextcard sup (phi|A) in a
      by A11,A12,CARD_1:13,ORDINAL3:15;
      hence thesis;
    end;
  end;
A15: for A holds P[A] from ORDINAL1:sch 2(A7);
  thus rng phi c= a
  proof
    let x;
    assume x in rng phi;
    then ex y st y in dom phi & x = phi.y by FUNCT_1:def 5;
    hence thesis by A3,A15;
  end;
  thus phi is increasing
  proof
    let A,B;
    assume
A17: A in B & B in dom phi;
    then
A18: phi.B = ( nextcard (xi.dom (phi|B))) \/ nextcard sup (phi|B) by A3;
A19: A in dom phi by A17,ORDINAL1:19;
    reconsider C = phi.A as Ordinal;
    C in rng (phi|B) & sup (phi|B) = sup rng (phi|B) by A17,A19,FUNCT_1:73;
    then C in sup (phi|B) & sup (phi|B) in nextcard sup (phi|B) &
    nextcard sup (phi|B) = nextcard sup (phi|B) by CARD_1:36,ORDINAL2:27;
    then C in nextcard sup (phi|B) by ORDINAL1:19;
    hence thesis by A18,XBOOLE_0:def 3;
  end;
  thus a c= sup phi
  proof
    let x;
    assume x in a;
    then reconsider x as Element of a;
    consider A such that
A20: A in rng xi & x c= A by A2,ORDINAL2:29;
    consider y such that
A21: y in dom xi & A = xi.y by A20,FUNCT_1:def 5;
    reconsider y as Ordinal by A21;
    y c= cf a by A2,A21,ORDINAL1:def 2;
    then dom (phi|y) = y by A3,RELAT_1:91;
    then A in nextcard A & nextcard A = nextcard A &
    phi.y = ( nextcard A) \/ nextcard sup (phi|y) by A2,A3,A21,CARD_1:36;
    then A in phi.y by XBOOLE_0:def 3;
    then A c= phi.y by ORDINAL1:def 2;
    then
A22: x c= phi.y & phi.y in rng phi by A2,A3,A20,A21,FUNCT_1:def 5,XBOOLE_1:1;
    then phi.y in sup phi by ORDINAL2:27;
    hence thesis by A22,ORDINAL1:22;
  end;
  rng phi c= a
  proof
    let x;
    assume x in rng phi;
    then ex y st y in dom phi & x = phi.y by FUNCT_1:def 5;
    hence thesis by A3,A15;
  end;
  hence sup phi c= a by A4,ORDINAL2:30;
  phi is Cardinal-yielding
  proof
    let y;
    assume
A24: y in dom phi;
    then reconsider y as Ordinal;
    y c= dom phi by A24,ORDINAL1:def 2;
    then dom (phi|y) = y by RELAT_1:91;
    then phi.y = ( nextcard (xi.y)) \/ nextcard sup (phi|y) &
    (( nextcard (xi.y)) \/ nextcard sup (phi|y) = nextcard (xi.y) or
    ( nextcard (xi.y)) \/ nextcard sup (phi|y) = nextcard sup (phi|y)) &
    nextcard (xi.y) = nextcard (xi.y) &
    nextcard sup (phi|y) = nextcard sup (phi|y) by A3,A24,ORDINAL3:15;
    hence thesis;
  end;
  hence phi is Cardinal-Function;
  assume 0 in rng phi;
  then consider x such that
A25: x in dom phi & 0 = phi.x by FUNCT_1:def 5;
  reconsider x as Ordinal by A25;
  x c= dom phi by A25,ORDINAL1:def 2;
  then dom (phi|x) = x by RELAT_1:91;
  then 0 = ( nextcard (xi.x)) \/ nextcard sup (phi|x) &
  nextcard (xi.x) = nextcard (xi.x) &
  nextcard sup (phi|x) = nextcard sup (phi|x) by A3,A25;
  then 0 = nextcard (xi.x) or 0 = nextcard sup (phi|x);
  hence contradiction by CARD_1:33;
end;

theorem
  omega is regular & nextcard a is regular
proof
  thus cf omega = omega by Th34;
  thus cf nextcard a = nextcard a by Th35;
end;

begin :: Infinite powers

reserve a,b for Aleph;

theorem Th43:
  a c= b implies exp(a,b) = exp(2,b)
proof
  assume
A1: a c= b;
  b c= b & 2 in a & card 2 <> card 0 & a <> 0 & card 0 = 0 & card 2 = 2
  by Lm1,Th25;
  then
A2: exp(2,b) c= exp(a,b) by CARD_3:138;
  card a = a & card a in card bool a & card bool a = exp(2,card a)
  by CARD_1:30,def 5,CARD_2:44;
  then exp(a,b) c= exp(exp(2,a),b) & exp(exp(2,a),b) = exp(2,a*`b) & a*`b = b
  by A1,Th27,CARD_2:43,CARD_3:138;
  hence thesis by A2,XBOOLE_0:def 10;
end;

theorem
  exp(nextcard a,b) = exp(a,b) *` nextcard a
proof
  now per cases by CARD_1:14;
    suppose a in b;
      then nextcard a c= b & b in exp(2,b) & a c= b
      by Th23,CARD_1:13,CARD_3:107;
      then exp(nextcard a,b) = exp(2,b) & nextcard a in exp(2,b) &
      exp(a,b) = exp(2,b) by Th43,ORDINAL1:22;
      hence thesis by Th27;
    end;
    suppose b c= a;
      then
A1:   b in nextcard a & cf nextcard a = nextcard a by Th35,CARD_3:108;
      deffunc f(Ordinal) = Funcs(b,$1);
      consider phi being T-Sequence such that
A2:   dom phi = nextcard a &
      for A st A in nextcard a holds phi.A = f(A) from ORDINAL2:sch 2;
      Funcs(b,nextcard a) c= Union phi
      proof
        let x;
        assume x in Funcs(b,nextcard a);
        then consider f be Function such that
A3:     x = f & dom f = b & rng f c= nextcard a by FUNCT_2:def 2;
        reconsider f as T-Sequence by A3,ORDINAL1:def 7;
        reconsider f as Ordinal-Sequence by A3,ORDINAL2:def 8;
        sup f in nextcard a & rng f c= sup f by A1,A3,Th39,ORDINAL2:67;
        then f in Funcs(b,sup f) & phi.sup f = Funcs(b,sup f) &
        Union phi = union rng phi & phi.sup f in rng phi
        by A2,A3,FUNCT_1:def 5,FUNCT_2:def 2;
        hence thesis by A3,TARSKI:def 4;
      end;
      then card Funcs(b,nextcard a) c= card Union phi &
      card Funcs(b,nextcard a) = exp(nextcard a,b) & card Union phi c= Sum
      Card phi by CARD_1:27,CARD_2:def 3,CARD_3:54;
      then
A4:   exp(nextcard a,b) c= Sum Card phi & dom Card phi = dom phi &
      dom (nextcard a --> exp(a,b)) = nextcard a
      by CARD_3:def 2,FUNCOP_1:19,XBOOLE_1:1;
      now
        let x;
        assume
A5:     x in nextcard a;
        then reconsider x' = x as Ordinal;
A6:     (nextcard a --> exp(a,b)).x = exp(a,b) & Card phi.x = card (phi.x) &
        phi.x' = Funcs(b,x') by A2,A5,CARD_3:def 2,FUNCOP_1:13;
A7:     card card x = card x & card x' c= x' & card b = card b by CARD_1:24;
        card x in nextcard a by A5,CARD_1:24,ORDINAL1:22;
        then card x c= a by CARD_3:108;
        then Funcs(b,card x) c= Funcs(b,a) by FUNCT_5:63;
        then card Funcs(b,card x) c= card Funcs(b,a) &
        card Funcs(b,a) = exp(a,b) by CARD_1:27,CARD_2:def 3;
        hence Card phi.x c= (nextcard a --> exp(a,b)).x by A6,A7,FUNCT_5:68;
      end;
      then Sum Card phi c= Sum (nextcard a --> exp(a,b)) &
      Sum (nextcard a --> exp(a,b)) = (nextcard a)*`exp(a,b) &
      (nextcard a)*`exp(a,b) = exp(a,b)*`(nextcard a) by A2,A4,CARD_3:43,52;
      then
A8:   exp(nextcard a,b) c= exp(a,b)*`nextcard a by A4,XBOOLE_1:1;
      a in nextcard a & b c= b & a <> 0 & exp(nextcard a,1) = nextcard a &
      nextcard a <> 0 & 1 in b by Lm1,Th25,CARD_1:32,CARD_2:40;
      then exp(nextcard a,b) *` exp(nextcard a,b) = exp(nextcard a,b) &
      exp(a,b) c= exp(nextcard a,b) & nextcard a c= exp(nextcard a,b)
      by CARD_3:138,CARD_4:77;
      then exp(a,b)*`nextcard a c= exp(nextcard a,b) by CARD_3:136;
      hence thesis by A8,XBOOLE_0:def 10;
    end;
  end;
  hence thesis;
end;

theorem Th45:
  Sum (b-powerfunc_of a) c= exp(a,b)
proof
  set X = { c where c is Element of a: c is Cardinal};
  set f = X --> exp(a,b);
A1: X c= a
  proof
    let x;
    assume x in X;
    then ex c being Element of a st x = c & c is Cardinal;
    hence thesis;
  end;
A2: now
    let x;
    assume
A3: x in X;
    then consider c being Element of a such that
A4: x = c & c is Cardinal;
    reconsider c as Cardinal by A4;
    f.x = exp(a,b) & (b-powerfunc_of a).x = exp(c,b) & exp(c,b) c= exp( a, b
    ) by A3,A4,Def3,CARD_3:139,FUNCOP_1:13;
    hence (b-powerfunc_of a).x c= f.x;
  end;
A5: dom f = X & dom (b-powerfunc_of a) = X
  proof thus dom f = X by FUNCOP_1:19;
    thus dom (b-powerfunc_of a) c= X
    proof
      let x;
      assume x in dom (b-powerfunc_of a);
      then x in a & x is Cardinal by Def3;
      hence thesis;
    end;
    let x;
    assume x in X;
    then ex c being Element of a st x = c & c is Cardinal;
    hence thesis by Def3;
  end;
  1 in b & a <> 0 & exp(a,1) = a by Lm1,Th25,CARD_2:40;
  then f c= a --> exp(a,b) & a c= exp(a,b) & Sum (a --> exp(a,b)) = a*`exp(
  a,b) by A1,CARD_3:52,139,FUNCT_4:5;
  then Sum (b-powerfunc_of a) c= Sum f & Sum
  f c= a*`exp(a,b) & a*`exp(a,b) = exp(a,b) by A2,A5,Th27,CARD_3:43,46;
  hence thesis by XBOOLE_1:1;
end;

theorem
  a is limit_cardinal & b in cf a implies exp(a,b) = Sum (b-powerfunc_of a)
proof
  assume
A1: a is limit_cardinal & b in cf a;
  deffunc f(Ordinal) = Funcs(b,$1);
  consider L being T-Sequence such that
A2: dom L = a & for A st A in a holds L.A = f(A) from ORDINAL2:sch 2;
  Funcs(b,a) c= Union L
  proof
    let x;
    assume x in Funcs(b,a);
    then consider f such that
A3: x = f & dom f = b & rng f c= a by FUNCT_2:def 2;
    reconsider f as T-Sequence by A3,ORDINAL1:def 7;
    reconsider f as Ordinal-Sequence by A3,ORDINAL2:def 8;
    sup f in a & rng f c= sup f by A1,A3,Th39,ORDINAL2:67;
    then x in Funcs(b,sup f) & L.sup f = Funcs(b,sup f) & L.sup f in rng L
    by A2,A3,FUNCT_1:def 5,FUNCT_2:def 2;
    hence thesis by TARSKI:def 4;
  end;
  then card Funcs(b,a) c= card Union L & card Union L c= Sum Card L
  by CARD_1:27,CARD_3:54;
  then card Funcs(b,a) c= Sum Card L by XBOOLE_1:1;
  then
A4: exp(a,b) c= Sum Card L by CARD_2:def 3;
A5: Sum (b-powerfunc_of a) c= exp(a,b) by Th45;
A6: dom (a --> Sum (b-powerfunc_of a)) = a & dom Card L = dom L
  by CARD_3:def 2,FUNCOP_1:19;
  now
    let x;
    assume
A7: x in a;
    then reconsider x' = x as Ordinal;
    set m = card x';
    b,b are_equipotent & x',m are_equipotent by CARD_1:def 5;
    then L.x = Funcs(b,x') & (Card L).x = card (L.x) &
    card Funcs(b,x') = card Funcs(b,card x') & card x' c= x'
    by A2,A7,CARD_1:24,CARD_3:def 2,FUNCT_5:67;
    then
A8: (Card L).x = exp(m,b) & m in a by A7,CARD_2:def 3,ORDINAL1:22;
    then m in dom (b-powerfunc_of a) by Def3;
    then (b-powerfunc_of a).(card x) = exp(card x,b) &
    (b-powerfunc_of a).(card x) in rng (b-powerfunc_of a)
    by A8,Def3,FUNCT_1:def 5;
    then card exp(card x,b) c= card Union (b-powerfunc_of a) &
    card Union (b-powerfunc_of a) c= Sum (b-powerfunc_of a)
    by CARD_1:27,CARD_3:55,ZFMISC_1:92;
    then card exp(card x,b) c= Sum (b-powerfunc_of a) &
    card exp(card x,b) = exp(card x,b) by CARD_1:def 5,XBOOLE_1:1;
    hence (Card L).x c= (a --> Sum (b-powerfunc_of a)).x by A7,A8,FUNCOP_1:13;
  end;
  then Sum Card L c= Sum (a --> Sum (b-powerfunc_of a)) &
  Sum (a --> Sum (b-powerfunc_of a)) = a*`Sum (b-powerfunc_of a)
  by A2,A6,CARD_3:43,52;
  then
A9: exp(a,b) c= a*`Sum (b-powerfunc_of a) by A4,XBOOLE_1:1;
  a c= Sum (b-powerfunc_of a)
  proof
    let x;
    assume
A10: x in a;
    then reconsider x' = x as Ordinal;
    set m = card x';
    m in a by A10,CARD_1:24,ORDINAL1:22;
    then nextcard m c= a & nextcard m <> a by A1,CARD_1:def 7,CARD_3:107;
    then
A11: nextcard m in a by CARD_1:13;
    then nextcard m in dom (b-powerfunc_of a) by Def3;
    then (b-powerfunc_of a).(nextcard m) in rng (b-powerfunc_of a) &
    (b-powerfunc_of a).(nextcard m) = exp(nextcard m,b)
    by A11,Def3,FUNCT_1:def 5;
    then exp(nextcard m,b) c= Union (b-powerfunc_of a) by ZFMISC_1:92;
    then card exp(nextcard m,b) = exp(nextcard m,b) &
    card exp(nextcard m,b) c= card Union (b-powerfunc_of a) &
    card Union (b-powerfunc_of a) c= Sum (b-powerfunc_of a)
    by CARD_1:27,def 5,CARD_3:55;
    then exp(nextcard m,b) c= Sum (b-powerfunc_of a) &
    nextcard m c= exp(nextcard m,b) by Th31,XBOOLE_1:1;
    then
A12: nextcard card x c= Sum (b-powerfunc_of a) by XBOOLE_1:1;
    card x = card card x;
    then x' in nextcard x' & nextcard card x = nextcard x by CARD_1:36
,CARD_3:105;
    hence thesis by A12;
  end;
  then a*`Sum (b-powerfunc_of a) = Sum (b-powerfunc_of a) by Th27;
  hence thesis by A5,A9,XBOOLE_0:def 10;
end;

theorem
  cf a c= b & b in a implies exp(a,b) = exp(Sum (b-powerfunc_of a), cf a)
proof
  assume
A1: cf a c= b & b in a;
  cf a <> 0 & Sum (b-powerfunc_of a) c= exp(a,b) by Th45;
  then
A2: exp(Sum (b-powerfunc_of a), cf a) c= exp(exp(a,b), cf a) &
  b*`cf a = b & exp(exp(a,b), cf a) = exp(a,b*`cf a)
  by A1,Th27,CARD_2:43,CARD_3:139;
  consider phi such that
A3: dom phi = cf a & rng phi c= a & phi is increasing & a = sup phi &
  phi is Cardinal-Function & not 0 in rng phi by Th41,A1,ORDINAL1:22;
A4: exp(a,b) = card Funcs(b,a) & a = a & 0 = 0 &
  exp(Sum (b-powerfunc_of a), cf a) = card Funcs(cf a, Sum
  (b-powerfunc_of a)) &
  Sum (b-powerfunc_of a) = card Union disjoin (b-powerfunc_of a) &
  Sum (b-powerfunc_of a) = card Sum (b-powerfunc_of a) & card cf a = cf a
  by CARD_1:def 5,CARD_2:def 3;
  defpred R[set,set] means
  ex g,h st g = $1 & h = $2 & dom g = b & rng g c= a &
  dom h = cf a & for y st y in cf a ex fx st h.y = [fx,phi.y] & dom fx = b &
  for z st z in b holds (g.z in phi.y implies fx.z = g.z) &
  (not g.z in phi.y implies fx.z = 0);
A5: for x st x in Funcs(b,a) ex x1 being set st R[x,x1]
  proof
    let x;
    assume x in Funcs(b,a);
    then consider g such that
A6: x = g & dom g = b & rng g c= a by FUNCT_2:def 2;
    defpred P[set,set] means ex fx st $2 = [fx,phi.$1] & dom fx = b &
    for z st z in b holds (g.z in phi.$1 implies fx.z = g.z) &
    (not g.z in phi.$1 implies fx.z = 0);
A7: for y st y in cf a ex x2 being set st P[y,x2]
    proof
      let y such that y in cf a;
      deffunc f(set) = g.$1;
      deffunc g(set) = 0;
      defpred C[set] means g.$1 in phi.y;
      consider fx such that
A8:   dom fx = b & for z st z in b holds (C[z] implies fx.z = f(z)) &
      (not C[z] implies fx.z = g(z)) from PARTFUN1:sch 1;
      take [fx,phi.y], fx; thus thesis by A8;
    end;
    consider h such that
A9: dom h = cf a & for y st y in cf a holds P[y,h.y] from CLASSES1:sch 1 (A7);
    take h, g, h; thus thesis by A6,A9;
  end;
  consider f such that
A10: dom f = Funcs(b,a) & for x st x in Funcs(b,a) holds R[x,f.x]
  from CLASSES1:sch 1(A5);
A11: f is one-to-one
  proof
    let x,y;
    assume
A12: x in dom f & y in dom f & f.x = f.y;
    then consider g1, h1 being Function such that
A13: g1 = x & h1 = f.x & dom g1 = b & rng g1 c= a & dom h1 = cf a &
    for x1 being set st x1 in cf a ex fx st h1.x1 = [fx,phi.x1] & dom fx = b &
    for z st z in b holds (g1.z in phi.x1 implies fx.z = g1.z) &
    (not g1.z in phi.x1 implies fx.z = 0) by A10;
    consider g2, h2 being Function such that
A14: g2 = y & h2 = f.y & dom g2 = b & rng g2 c= a & dom h2 = cf a &
    for x2 being set st x2 in cf a ex fx st h2.x2 = [fx,phi.x2] & dom fx = b &
    for z st z in b holds (g2.z in phi.x2 implies fx.z = g2.z) &
    (not g2.z in phi.x2 implies fx.z = 0) by A10,A12;
    now
      let x1 be set;
      assume x1 in b;
      then reconsider X = x1 as Element of b;
      g1.X in rng g1 & g2.X in rng g2 by A13,A14,FUNCT_1:def 5;
      then reconsider A1 = g1.x1, A2 = g2.x1 as Element of a by A13,A14;
      set A = A1 \/ A2;
      (A = A1 or A = A2) & a is limit_ordinal
      by ORDINAL3:15;
      then succ A in a & sup phi = sup rng phi by ORDINAL1:41;
      then consider B such that
A15:  B in rng phi & succ A c= B by A3,ORDINAL2:29;
      consider x2 being set such that
A16:  x2 in dom phi & B = phi.x2 by A15,FUNCT_1:def 5;
      consider f1 being Function such that
A17:  h1.x2 = [f1,phi.x2] & dom f1 = b &
      for z st z in b holds (g1.z in phi.x2 implies f1.z = g1.z) &
      (not g1.z in phi.x2 implies f1.z = 0) by A3,A13,A16;
      consider f2 being Function such that
A18:  h2.x2 = [f2,phi.x2] & dom f2 = b &
      for z st z in b holds (g2.z in phi.x2 implies f2.z = g2.z) &
      (not g2.z in phi.x2 implies f2.z = 0) by A3,A14,A16;
      A1 c= A & A2 c= A & A in succ A by ORDINAL1:10,XBOOLE_1:7;
      then A1 in B & A2 in B & f1 = f2
      by A12,A13,A14,A15,A17,A18,ORDINAL1:22,ZFMISC_1:33;
      then f1.X = g1.x1 & f1.X = g2.x1 by A16,A17,A18;
      hence g1.x1 = g2.x1;
    end;
    hence thesis by A13,A14,FUNCT_1:9;
  end;
  deffunc f(set) = Funcs(b,$1);
  consider F being Function such that
A19: dom F = dom (b-powerfunc_of a) &
  for x st x in dom (b-powerfunc_of a) holds F.x = f(x) from FUNCT_1:sch 3;
  rng f c= Funcs(cf a, Union disjoin F)
  proof
    let y;
    assume y in rng f;
    then consider x such that
A20: x in dom f & y = f.x by FUNCT_1:def 5;
    consider g,h such that
A21: g = x & h = f.x & dom g = b & rng g c= a &
    dom h = cf a & for y st y in cf a ex fx st h.y = [fx,phi.y] & dom fx = b &
    for z st z in b holds (g.z in phi.y implies fx.z = g.z) &
    (not g.z in phi.y implies fx.z = 0) by A10,A20;
    rng h c= Union disjoin F
    proof
      let x1 be set;
      assume x1 in rng h;
      then consider x2 being set such that
A22:  x2 in dom h & x1 = h.x2 by FUNCT_1:def 5;
      consider fx such that
A23:  x1 = [fx,phi.x2] & dom fx = b &
      for z st z in b holds (g.z in phi.x2 implies fx.z = g.z) &
      (not g.z in phi.x2 implies fx.z = 0) by A21,A22;
      rng fx c= phi.x2
      proof
        let z;
        assume z in rng fx;
        then consider z' being set such that
A24:    z' in dom fx & z = fx.z' by FUNCT_1:def 5;
        reconsider x2 as Ordinal by A21,A22;
        reconsider A = phi.x2 as Ordinal;
        (g.z' in phi.x2 or not g.z' in phi.x2) & A <> 0 by A3,A21,A22,
FUNCT_1:def 5;
        then (z = g.z' & g.z' in phi.x2 or z = 0) & 0 in A
        by A23,A24,ORDINAL3:10;
        hence thesis;
      end;
      then
A25:  fx in Funcs(b,phi.x2) by A23,FUNCT_2:def 2;
      phi.x2 in rng phi by A3,A21,A22,FUNCT_1:def 5;
      then phi.x2 is Cardinal & phi.x2 in a by A3,A21,A22,CARD_3:def 1;
      then
A26:  phi.x2 in dom (b-powerfunc_of a) by Def3;
      then F.(phi.x2) = Funcs(b,phi.x2) & [fx,phi.x2]`1 = fx &
      [fx,phi.x2]`2 = phi.x2 by A19,MCART_1:7;
      hence thesis by A19,A23,A25,A26,CARD_3:33;
    end;
    hence thesis by A20,A21,FUNCT_2:def 2;
  end;
  then
A27: exp(a,b) c= card Funcs(cf a, Union disjoin F) by A4,A10,A11,CARD_1:26;
  card card Union disjoin F = card Union disjoin F & card cf a = cf a
  by CARD_1:def 5;
  then
A28: card Funcs(cf a, Union disjoin F)
  = card Funcs(cf a, card Union disjoin F) by FUNCT_5:68
    .= exp(card Union disjoin F, cf a) by CARD_2:def 3;
A29: dom Card disjoin F = dom disjoin F & dom disjoin F = dom F &
  dom Card F = dom F by CARD_3:def 2,def 3;
  now
    let x;
    assume x in dom F;
    then (Card F).x = card (F.x) & (Card disjoin F).x = card ((disjoin F).x)
    & (disjoin F).x = [:F.x,{x}:] by A29,CARD_3:def 2,def 3;
    hence (Card disjoin F).x = (Card F).x by CARD_2:13;
  end;
  then
A30: Card F = Card disjoin F by A29,FUNCT_1:9;
  now
    let x;
    assume
A31: x in dom F;
    then
A32: (Card F).x = card (F.x) & F.x = Funcs(b,x) by A19,CARD_3:def 2;
    reconsider M = x as Cardinal by A19,A31,Def3;
    M in a by A19,A31,Def3;
    then (b-powerfunc_of a).M = exp(M,b) by Def3;
    hence (Card F).x = (b-powerfunc_of a).x by A32,CARD_2:def 3;
  end;
  then Card F = b-powerfunc_of a by A19,A29,FUNCT_1:9;
  then cf a <> 0 & card Union disjoin F c= Sum (b-powerfunc_of a)
  by A30,CARD_3:54;
  then exp(card Union disjoin F, cf a) c= exp(Sum (b-powerfunc_of a), cf a)
  by CARD_3:139;
  then exp(a,b) c= exp(Sum (b-powerfunc_of a), cf a) by A27,A28,XBOOLE_1:1;
  hence thesis by A2,XBOOLE_0:def 10;
end;

