:: On the Decompositions of Intervals and Simple Closed Curves
::  by Adam Grabowski
::
:: Received August 7, 2002
:: Copyright (c) 2002 Association of Mizar Users

environ

 vocabularies BOOLE, TOPREAL1, EUCLID, ARYTM_1, PRE_TOPC, COMPTS_1, SEQ_1,
      TOPREAL2, RELAT_2, BORSUK_1, SUBSET_1, RCOMP_1, TOPS_2, TOPMETR,
      CONNSP_1, ARYTM_3, RELAT_1, ORDINAL2, LATTICES, T_0TOPSP, SEQ_2,
      INTEGRA1, REALSET1, FUNCT_1, BORSUK_4, PARTFUN1, TREAL_1, FUNCT_4, ARYTM,
      JORDAN1, MEMBERED, JORDAN2C, COMPLEX1, SEQ_4;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0,
      REAL_1, RELAT_1, FUNCT_1, RELSET_1, PARTFUN1, FUNCT_2, REALSET1,
      MEMBERED, DOMAIN_1, RCOMP_1, FUNCT_4, XXREAL_0, XXREAL_2,
      STRUCT_0,
      PRE_TOPC, COMPTS_1,
      RLTOPSP1, EUCLID, TOPREAL1, TOPREAL2, SEQ_4, TREAL_1, CONNSP_1,
      BORSUK_1, BORSUK_3, TSEP_1, JORDAN1, TOPMETR, PSCOMP_1, INTEGRA1, TOPS_2,
      JORDAN2C;
 constructors FUNCT_4, CONNSP_1, TOPS_2, COMPTS_1, REALSET2, TSEP_1,
      TOPREAL1, TOPREAL2, TREAL_1, JORDAN1, PSCOMP_1, JORDAN2C, BORSUK_3,
      INTEGRA1, XXREAL_2, SEQ_4, CONVEX1;
 registrations XBOOLE_0, RELSET_1, FUNCT_2, XXREAL_0, XREAL_0, MEMBERED,
      RCOMP_1, REALSET1, STRUCT_0, PRE_TOPC, TOPS_1, COMPTS_1, BORSUK_1, TEX_2,
      EUCLID, TOPREAL1, TOPREAL2, BORSUK_2, REVROT_1, TOPREAL6, INTEGRA1,
      XXREAL_2, ZFMISC_1, RLTOPSP1;
 requirements NUMERALS, REAL, SUBSET, BOOLE, ARITHM;
 definitions XBOOLE_0, TARSKI, CONNSP_1, PARTFUN1, JORDAN1, SUBSET_1, STRUCT_0,
      RELAT_1;
 theorems TOPS_2, T_0TOPSP, BORSUK_1, PRE_TOPC, COMPTS_1, TARSKI,
      TOPMETR, TOPREAL1, RCOMP_1, CONNSP_1, ZFMISC_1, JORDAN5A, XREAL_0,
      STRUCT_0, TOPREAL6, PSCOMP_1, TREAL_1, XBOOLE_0, XBOOLE_1,
      JORDAN6, SEQ_4, INTEGRA1, INTEGRA2, TSEP_1, TOPREAL2, JORDAN5B, FUNCT_2,
      FUNCT_1, GOBOARD9, RELAT_1, PARTFUN1, RFUNCT_2, BORSUK_3, FUNCT_4,
      TOPS_1, JGRAPH_2, TOPMETR2, TEX_2, REALSET1, XREAL_1, FRECHET, ENUMSET1,
      JORDAN2C, XXREAL_0, XXREAL_1, XXREAL_2;

begin :: Preliminaries

registration
  cluster -> non trivial Simple_closed_curve;
  coherence
  proof
    let D be Simple_closed_curve;
    ex p1,p2 being Point of TOP-REAL 2 st
    p1 <> p2 & p1 in D & p2 in D by TOPREAL2:5;
    hence thesis by REALSET1:14;
  end;
end;

theorem Th1:
  for X being non empty set, A, B being non empty Subset of X st A c< B holds
  ex p being Element of X st p in B & A c= B \ {p}
proof
  let X be non empty set, A, B be non empty Subset of X;
  assume
A1: A c< B;
  then B \ A <> {} by XBOOLE_1:105;
  then consider p being set such that
A2: p in B \ A by XBOOLE_0:def 1;
A3: p in B & not p in A by A2,XBOOLE_0:def 5;
  reconsider p as Element of X by A2;
  take p;
  A c= B by A1,XBOOLE_0:def 8;
  hence thesis by A3,ZFMISC_1:40;
end;

theorem Th2:
  for X being non empty set, A being non empty Subset of X holds
  A is trivial implies ex x being Element of X st A = {x}
proof
  let X be non empty set, A be non empty Subset of X;
    assume A is trivial;
    then ex s being Element of A st A = {s} by TEX_2:def 1;
   hence thesis;
end;

registration
  let T be non trivial 1-sorted;
  cluster non trivial Subset of T;
  existence
  proof
    consider x, y being Element of T such that
A1: x <> y by STRUCT_0:def 10;
    take X = {x, y};
    x in X & y in X by TARSKI:def 2;
    hence thesis by A1,REALSET1:15;
  end;
end;

theorem Th3:
  for X being non trivial set, p being set ex q being Element of X st q <> p
proof
  let X be non trivial set, p be set;
  X \ { p } is non empty by REALSET1:4;
  then consider q being set such that
A1: q in X \ { p } by XBOOLE_0:def 1;
  reconsider q as Element of X by A1;
  take q;
  thus thesis by A1,ZFMISC_1:64;
end;

registration
  let X be non trivial set;
  cluster non trivial Subset of X;
  existence
  proof
    take [#]X;
    thus thesis;
  end;
end;

theorem Th4:
  for T being non trivial set, X being non trivial Subset of T, p being set
  ex q being Element of T st q in X & q <> p
proof
  let T be non trivial set, X be non trivial Subset of T, p be set;
  set p' = p;
  X \ { p' } is non empty by REALSET1:4;
  then consider q being set such that
A1: q in X \ { p' } by XBOOLE_0:def 1;
  reconsider q as Element of T by A1;
  take q;
  thus thesis by A1,ZFMISC_1:64;
end;

theorem Th5:
  for f, g being Function, a being set st f is one-to-one & g is one-to-one &
  dom f /\ dom g = { a } & rng f /\ rng g = { f.a } holds f +* g is one-to-one
proof
  let f, g be Function, a be set;
  assume that
A1: f is one-to-one and
A2: g is one-to-one and
A3: dom f /\ dom g = { a } and
A4: rng f /\ rng g = { f.a };
  for x1,x2 being set st x1 in dom g & x2 in dom f \ dom g holds g.x1 <> f.x2
  proof
    let x1,x2 be set;
    assume
A5: x1 in dom g & x2 in dom f \ dom g;
    then
A6: g.x1 in rng g by FUNCT_1:12;
A7: f.x2 in rng f by A5,FUNCT_1:12;
    { a } c= dom f & { a } c= dom g by A3,XBOOLE_1:17;
    then
A8: a in dom f & a in dom g by ZFMISC_1:37;
    assume g.x1 = f.x2;
    then f.x2 in rng f /\ rng g by A6,A7,XBOOLE_0:def 4;
    then f.x2 = f.a by A4,TARSKI:def 1;
    then x2 = a by A1,A5,A8,FUNCT_1:def 8;
    then dom g meets (dom f \ dom g) by A5,A8,XBOOLE_0:3;
    hence thesis by XBOOLE_1:79;
  end;
  hence thesis by A1,A2,TOPMETR2:2;
end;

theorem Th6:
  for f, g being Function, a being set st f is one-to-one & g is one-to-one &
  dom f /\ dom g = { a } & rng f /\ rng g = { f.a } & f.a = g.a holds
  (f +* g)" = f" +* g"
proof
  let f, g be Function, a be set;
  assume that
A1: f is one-to-one and
A2: g is one-to-one and
A3: dom f /\ dom g = { a } and
A4: rng f /\ rng g = { f.a } and
A5: f.a = g.a;
  set gf = f" +* g", F = f +* g;
  for x being set st x in dom f /\ dom g holds f.x = g.x
  proof
    let x be set;
    assume x in dom f /\ dom g;
    then x = a by A3,TARSKI:def 1;
    hence thesis by A5;
  end;
  then
A6: f tolerates g by PARTFUN1:def 6;
A7: dom (f") = rng f & dom (g") = rng g by A1,A2,FUNCT_1:55;
  for x being set st x in dom (f") /\ dom (g") holds f".x = g".x
  proof
    let x be set;
    assume
A8: x in dom (f") /\ dom (g");
    { a } c= dom f by A3,XBOOLE_1:17;
    then
A9: a in dom f by ZFMISC_1:37;
    { a } c= dom g by A3,XBOOLE_1:17;
    then
A10: a in dom g by ZFMISC_1:37;
    x = f.a by A4,A7,A8,TARSKI:def 1;
    then
A11: a = f".x by A1,A9,FUNCT_1:54;
    x = g.a by A4,A5,A7,A8,TARSKI:def 1;
    hence thesis by A2,A10,A11,FUNCT_1:54;
  end;
  then
A12: f" tolerates g" by PARTFUN1:def 6;
A13: F is one-to-one by A1,A2,A3,A4,Th5;
A14: rng F = (rng f) \/ (rng g) by A6,FRECHET:39;
  dom gf = (dom (f")) \/ (dom (g")) by FUNCT_4:def 1
    .= (rng f) \/ (dom (g")) by A1,FUNCT_1:55
    .= (rng f) \/ (rng g) by A2,FUNCT_1:55;
  then
A15: dom gf = rng F by A6,FRECHET:39;
A16: dom F = (dom f) \/ (dom g) by FUNCT_4:def 1;
  then
A17: dom f c= dom F by XBOOLE_1:7;
A18: dom g c= dom F by A16,XBOOLE_1:7;
  for y,x being set holds y in rng F & x = gf.y iff x in dom F & y = F.x
  proof
    let y,x be set;
    hereby
      assume
A19:  y in rng F & x = gf.y;
      per cases by A15,A19,FUNCT_4:13;
      suppose
A20:    y in dom (f");
        then
A21:    y in rng f by A1,FUNCT_1:55;
        x = f".y by A12,A19,A20,FUNCT_4:16;
        then x in dom f & y = f.x by A1,A21,FUNCT_1:54;
        hence x in dom F & y = F.x by A6,A17,FUNCT_4:16;
      end;
      suppose
A22:    y in dom (g");
        then
A23:    y in rng g by A2,FUNCT_1:55;
        x = g".y by A19,A22,FUNCT_4:14;
        then x in dom g & y = g.x by A2,A23,FUNCT_1:54;
        hence x in dom F & y = F.x by A18,FUNCT_4:14;
      end;
    end;
    assume
A24: x in dom F & y = F.x;
    per cases by A24,FUNCT_4:13;
    suppose
A25:  x in dom f;
      then
A26:  y = f.x by A6,A24,FUNCT_4:16;
      then
A27:  x = f".y by A1,A25,FUNCT_1:54;
A28:  y in rng f by A25,A26,FUNCT_1:12;
      rng F = (rng f) \/ (rng g) by A6,FRECHET:39;
      then
A29:  rng f c= rng F by XBOOLE_1:7;
      y in dom (f") by A1,A28,FUNCT_1:55;
      hence thesis by A12,A27,A28,A29,FUNCT_4:16;
    end;
    suppose
A30:  x in dom g;
      then
A31:  y = g.x by A24,FUNCT_4:14;
      then
A32:  x = g".y by A2,A30,FUNCT_1:54;
A33:  y in rng g by A30,A31,FUNCT_1:12;
A34:  rng g c= rng F by A14,XBOOLE_1:7;
      y in dom (g") by A2,A33,FUNCT_1:55;
      hence thesis by A32,A33,A34,FUNCT_4:14;
    end;
  end;
  hence thesis by A13,A15,FUNCT_1:54;
end;

theorem Th7:
  for n being Element of NAT, A being Subset of TOP-REAL n,
  p, q being Point of TOP-REAL n st
  A is_an_arc_of p, q holds A \ {p} is non empty
proof
  let n be Element of NAT, A be Subset of TOP-REAL n,
  p, q be Point of TOP-REAL n;
  assume A is_an_arc_of p, q;
  then
A1: A \ { p, q } <> {} by JORDAN6:56;
  { p } c= { p, q } by ZFMISC_1:12;
  hence thesis by A1,XBOOLE_1:3,34;
end;

canceled;

theorem
  for s1, s3, s4, l being real number st
  s1 <= s3 & s1 < s4 & 0 <= l & l <= 1 holds s1 <= (1-l) * s3+l*s4
proof
  let s1, s3, s4, l be real number;
  assume
A1: s1 <= s3 & s1 < s4 & 0 <= l & l <= 1;
  now per cases;
    suppose l=0;
      hence thesis by A1;
    end;
    suppose l=1;
      hence thesis by A1;
    end;
    suppose
A2:   not(l=0 or l=1);
      then
A3:   l>0 & l<1 by A1,XXREAL_0:1;
A4:   l*s1<l*s4 by A1,A2,XREAL_1:70;
      1-l>0 by A3,XREAL_1:52;
      then
A5:   (1-l)*s1<=(1-l)*s3 by A1,XREAL_1:66;
      (1-l)*s1+l*s1=1 * s1;
      hence thesis by A4,A5,XREAL_1:10;
    end;
  end;
  hence thesis;
end;

canceled 7;

theorem Th17:
  for A being Subset of I[01],
  a, b being real number st a < b & A = ].a,b.[ holds
  [.a,b.] c= the carrier of I[01]
proof
  let A be Subset of I[01], a, b be real number;
  assume
A1: a < b;
  assume
A2: A = ].a,b.[;
  then
A3: 0 <= a by A1,BORSUK_1:83,XXREAL_1:51;
  b <= 1 by A1,A2,BORSUK_1:83,XXREAL_1:51;
  hence thesis by A3,BORSUK_1:83,XXREAL_1:34;
end;

theorem Th18:
  for A being Subset of I[01],
  a, b being real number st a < b & A = ].a,b.] holds
  [.a,b.] c= the carrier of I[01]
proof
  let A be Subset of I[01], a, b be real number;
  assume
A1: a < b;
A2: ].a,b.[ c= ]. a,b .] by XXREAL_1:21;
  assume A = ].a,b.];
  then
A3: ].a,b.[ c= [. 0,1 .] by A2,BORSUK_1:83,XBOOLE_1:1;
  then
A4: 0 <= a by A1,XXREAL_1:51;
  b <= 1 by A1,A3,XXREAL_1:51;
  hence thesis by A4,BORSUK_1:83,XXREAL_1:34;
end;

theorem Th19:
  for A being Subset of I[01],
  a, b being real number st a < b & A = [.a,b.[ holds
  [.a,b.] c= the carrier of I[01]
proof
  let A be Subset of I[01], a, b be real number;
  assume
A1: a < b;
A2: ].a,b.[ c= [. a,b .[ by XXREAL_1:22;
  assume A = [.a,b.[;
  then
A3: ].a,b.[ c= [. 0,1 .] by A2,BORSUK_1:83,XBOOLE_1:1;
  then
A4: 0 <= a by A1,XXREAL_1:51;
  b <= 1 by A1,A3,XXREAL_1:51;
  hence thesis by A4,BORSUK_1:83,XXREAL_1:34;
end;

theorem Th20:
  for a, b being real number st a <> b holds Cl ].a,b.] = [.a,b.]
proof
  let a, b be real number;
  assume
A1: a <> b;
  ].a,b.] c= [.a,b.] by XXREAL_1:23;
  then
A2: Cl ].a,b.] c= [.a,b.] by PSCOMP_1:32;
  ].a,b.[ c= ].a,b.] by XXREAL_1:21;
  then Cl ].a,b.[ c= Cl ].a,b.] by PSCOMP_1:37;
  then [.a,b.] c= Cl ].a,b.] by A1,TOPREAL6:82;
  hence thesis by A2,XBOOLE_0:def 10;
end;

theorem Th21:
  for a, b being real number st a <> b holds Cl [.a,b.[ = [.a,b.]
proof
  let a, b be real number;
  assume
A1: a <> b;
  [.a,b.[ c= [.a,b.] by XXREAL_1:24;
  then
A2: Cl [.a,b.[ c= [.a,b.] by PSCOMP_1:32;
  ].a,b.[ c= [.a,b.[ by XXREAL_1:22;
  then Cl ].a,b.[ c= Cl [.a,b.[ by PSCOMP_1:37;
  then [.a,b.] c= Cl [.a,b.[ by A1,TOPREAL6:82;
  hence thesis by A2,XBOOLE_0:def 10;
end;

theorem
  for A being Subset of I[01],
  a, b being real number st a < b & A = ].a,b.[ holds Cl A = [.a,b.]
proof
  let A be Subset of I[01], a, b be real number;
  assume
A1: a < b & A = ].a,b.[;
  then
A2: Cl ].a,b.[ = [.a,b.] by TOPREAL6:82;
  reconsider A1 = ].a,b.[ as Subset of R^1 by TOPMETR:24;
  Cl A = (Cl A1) /\ [#]I[01] by A1,PRE_TOPC:47
    .= [.a,b.] /\ [#]I[01] by A2,TOPREAL6:80
    .= [.a,b.] by A1,Th17,XBOOLE_1:28;
  hence thesis;
end;

theorem Th23:
  for A being Subset of I[01],
  a, b being real number st a < b & A = ].a,b.] holds Cl A = [.a,b.]
proof
  let A be Subset of I[01], a, b be real number;
  assume
A1: a < b & A = ].a,b.];
  then
A2: Cl ].a,b.] = [.a,b.] by Th20;
  reconsider A1 = ].a,b.] as Subset of R^1 by TOPMETR:24;
  Cl A = (Cl A1) /\ [#]I[01] by A1,PRE_TOPC:47
    .= [.a,b.] /\ [#]I[01] by A2,TOPREAL6:80
    .= [.a,b.] by A1,Th18,XBOOLE_1:28;
  hence thesis;
end;

theorem Th24:
  for A being Subset of I[01],
  a, b being real number st a < b & A = [.a,b.[ holds Cl A = [.a,b.]
proof
  let A be Subset of I[01], a, b be real number;
  assume
A1: a < b & A = [.a,b.[;
  then
A2: Cl [.a,b.[ = [.a,b.] by Th21;
  reconsider A1 = [.a,b.[ as Subset of R^1 by TOPMETR:24;
  Cl A = (Cl A1) /\ [#]I[01] by A1,PRE_TOPC:47
    .= [.a,b.] /\ [#]I[01] by A2,TOPREAL6:80
    .= [.a,b.] by A1,Th19,XBOOLE_1:28;
  hence thesis;
end;

canceled 7;

theorem Th32:
  for A being Subset of I[01],
  a, b being real number st a <= b & A = [.a,b.] holds 0 <= a & b <= 1
proof
  let A be Subset of I[01], a, b be real number;
  assume a <= b & A = [.a,b.];
  then a in A & b in A by XXREAL_1:1;
  hence thesis by BORSUK_1:86;
end;

theorem Th33:
  for A, B being Subset of I[01], a, b, c being real number st a < b & b < c &
  A = [.a,b.[ & B = ].b,c.] holds A, B are_separated
proof
  let A, B be Subset of I[01], a, b, c be real number;
  assume
A1: a < b & b < c & A = [.a,b.[ & B = ].b,c.];
  then Cl A = [.a,b.] by Th24;
  hence Cl A misses B by A1,XXREAL_1:90;
  Cl B = [.b,c.] by A1,Th23;
  hence thesis by A1,XXREAL_1:95;
end;

canceled 9;

theorem
  for p1, p2 being Point of I[01] holds
  [. p1, p2 .] is Subset of I[01] by BORSUK_1:83,XXREAL_2:def 12;

theorem Th44:
  for a, b being Point of I[01] holds ]. a, b .[ is Subset of I[01]
proof
  let a, b be Point of I[01];
A1: ]. a,b .[ c= [. a,b .] by XXREAL_1:25;
  [. a,b .] is Subset of I[01] by BORSUK_1:83,XXREAL_2:def 12;
  hence thesis by A1,XBOOLE_1:1;
end;

begin :: Decompositions of Intervals

theorem
  for p being real number holds {p} is closed-interval Subset of REAL
proof
  let p be real number;
A1: p is Real by XREAL_0:def 1;
  {p} = [.p,p.] by XXREAL_1:17;
  hence thesis by A1,INTEGRA1:def 1;
end;

theorem Th46:
  for A being non empty connected Subset of I[01],
  a, b, c being Point of I[01] st
  a <= b & b <= c & a in A & c in A holds b in A
proof
  let A be non empty connected Subset of I[01], a, b, c be Point of I[01];
  assume
A1: a <= b & b <= c & a in A & c in A;
  per cases by A1,XXREAL_0:1;
  suppose a = b or b = c;
    hence thesis by A1;
  end;
  suppose
A2: a < b & b < c & a in A & c in A;
    assume not b in A;
    then
A3: A c= [. 0,1 .] \ {b} by BORSUK_1:83,ZFMISC_1:40;
A4: 0 <= a & c <= 1 by BORSUK_1:86;
    then
A5: 0 < b & b < 1 by A2,XXREAL_0:2;
    then
A6: A c= [. 0,b .[ \/ ]. b,1 .] by A3,XXREAL_1:201;
A7: [.0,b.[ c= [.0,b.] & ].b,1 .] c= [.b,1 .] by XXREAL_1:23,24;
    b in [. 0,1 .] & 0 in [.0,1 .] & 1 in [.0,1 .] by A5,XXREAL_1:1;
    then [.0,b.] c= [. 0,1 .] & [. b,1 .] c= [.0,1 .] by XXREAL_2:def 12;
    then reconsider B = [.0,b.[, C = ].b,1 .] as non empty Subset of I[01]
    by A2,A4,A7,BORSUK_1:83,XBOOLE_1:1,XXREAL_1:2,3;
    now per cases by A5,A6,Th33,CONNSP_1:17;
      suppose A c= B;
        hence contradiction by A2,XXREAL_1:3;
      end;
      suppose A c= C;
        hence contradiction by A2,XXREAL_1:2;
      end;
    end;
    hence thesis;
  end;
end;

theorem Th47:
  for A being non empty connected Subset of I[01],
  a, b being real number st a in A & b in A holds [.a,b.] c= A
proof
  let A be non empty connected Subset of I[01], a, b be real number;
  assume
A1: a in A & b in A;
  then
A2: 0 <= a & b <= 1 by BORSUK_1:86;
  let x be set;
  assume x in [.a,b.];
  then x in { y where y is Real : a <= y & y <= b } by RCOMP_1:def 1;
  then consider z being Real such that
A3: z = x & a <= z & z <= b;
  0 <= z & z <= 1 by A2,A3,XXREAL_0:2;
  then reconsider z as Point of I[01] by BORSUK_1:86;
  z in A by A1,A3,Th46;
  hence thesis by A3;
end;

theorem Th48:
  for a, b being real number, A being Subset of I[01] st A = [.a,b.] holds
  A is closed
proof
  let a, b be real number, A be Subset of I[01];
  assume
A1: A = [.a,b.];
  per cases;
  suppose
A2: a <= b;
    then 0 <= a & b <= 1 by A1,Th32;
    then
A3: Closed-Interval-TSpace(a,b) is closed SubSpace of
    Closed-Interval-TSpace(0,1) by A2,TREAL_1:6;
    then reconsider BA = the carrier of Closed-Interval-TSpace(a,b) as
    Subset of Closed-Interval-TSpace(0,1) by BORSUK_1:35;
    BA is closed by A3,BORSUK_1:def 14;
    hence thesis by A1,A2,TOPMETR:25,27;
  end;
  suppose a > b;
    then A = {}I[01] by A1,XXREAL_1:29;
    hence thesis;
  end;
end;

theorem Th49:
  for p1, p2 being Point of I[01] st p1 <= p2 holds
  [. p1, p2 .] is non empty compact connected Subset of I[01]
proof
  let p1, p2 be Point of I[01];
  assume
A1: p1 <= p2;
A2: 0 <= p1 & p2 <= 1 by BORSUK_1:86;
  set S = [. p1, p2 .];
  reconsider S as Subset of I[01] by BORSUK_1:83,XXREAL_2:def 12;
A3: S is closed by Th48;
A4: Closed-Interval-TSpace(p1,p2) is connected by A1,TREAL_1:23;
  I[01] | S = Closed-Interval-TSpace(p1,p2) by A1,A2,TOPMETR:31;
  hence thesis by A1,A3,A4,COMPTS_1:17,CONNSP_1:def 3,XXREAL_1:1;
end;

theorem Th50:
  for X being Subset of I[01], X' being Subset of REAL st X' = X holds
  X' is bounded_above bounded_below
proof
  let X be Subset of I[01], X' be Subset of REAL;
  assume
A1: X' = X;
  then for r being real number st r in X' holds r <= 1 by BORSUK_1:86;
  hence X' is bounded_above by SEQ_4:def 1;
  for r being real number st r in X' holds 0 <= r by A1,BORSUK_1:86;
  hence thesis by SEQ_4:def 2;
end;

theorem Th51:
  for X being Subset of I[01], X' being Subset of REAL,
  x being real number st x in X' & X' = X holds inf X' <= x & x <= sup X'
proof
  let X be Subset of I[01], X' be Subset of REAL, x be real number;
  assume
A1: x in X' & X' = X;
  then X' is bounded_above bounded_below by Th50;
  hence thesis by A1,SEQ_4:def 4,def 5;
end;

Lm1: I[01] is closed SubSpace of R^1 by TOPMETR:27,TREAL_1:5;

theorem Th52:
  for A being Subset of REAL, B being Subset of I[01] st A = B holds
  A is closed iff B is closed
proof
  let A be Subset of REAL, B be Subset of I[01];
  assume
A1: A = B;
  the carrier of I[01] c= the carrier of R^1 by BORSUK_1:35;
  then reconsider C = A as Subset of R^1 by A1,XBOOLE_1:1;
  reconsider Z = the carrier of I[01] as Subset of R^1 by BORSUK_1:35;
  Z is closed by Lm1,BORSUK_1:def 14;
  then
A2: B is closed iff C is closed by A1,TSEP_1:8;
  hereby
    assume A is closed;
    then
A3: C is closed by TOPREAL6:79;
    C /\ [#] I[01] = B by A1,XBOOLE_1:28;
    hence B is closed by A3,PRE_TOPC:43;
  end;
  assume B is closed;
  hence thesis by A2,TOPREAL6:79;
end;

theorem Th53:
  for C being closed-interval Subset of REAL holds inf C <= sup C
proof
  let C be closed-interval Subset of REAL;
  consider c being Element of C;
  inf C <= c & c <= sup C by INTEGRA2:1;
  hence thesis by XXREAL_0:2;
end;

theorem Th54:
  for C being non empty compact connected Subset of I[01],
  C' being Subset of REAL st C = C' & [. inf C', sup C' .] c= C' holds
  [. inf C', sup C' .] = C'
proof
  let C be non empty compact connected Subset of I[01], C' be Subset of REAL;
  assume
A1: C = C' & [. inf C', sup C' .] c= C';
  assume [. inf C', sup C' .] <> C';
  then not C' c= [. inf C', sup C' .] by A1,XBOOLE_0:def 10;
  then consider c being set such that
A2: c in C' & not c in [. inf C', sup C' .] by TARSKI:def 3;
  reconsider c as real number by A2;
  inf C' <= c & c <= sup C' by A1,A2,Th51;
  hence thesis by A2,XXREAL_1:1;
end;

theorem Th55:
  for C being non empty compact connected Subset of I[01] holds
  C is closed-interval Subset of REAL
proof
  let C be non empty compact connected Subset of I[01];
  reconsider C' = C as Subset of REAL by BORSUK_1:83,XBOOLE_1:1;
A1: C' is closed by Th52;
A2: C' is bounded_below bounded_above by Th50;
  then
A3: inf C' in C' by A1,RCOMP_1:31;
  sup C' in C' by A1,A2,RCOMP_1:30;
  then
A4: [. inf C', sup C' .] = C' by A3,Th47,Th54;
  C' is bounded by A2;
  then lower_bound C' <= upper_bound C' by SEQ_4:24;
  hence thesis by A4,INTEGRA1:def 1;
end;

theorem Th56:
  for C being non empty compact connected Subset of I[01]
  ex p1, p2 being Point of I[01] st p1 <= p2 & C = [. p1, p2 .]
proof
  let C be non empty compact connected Subset of I[01];
  reconsider C' = C as closed-interval Subset of REAL by Th55;
A1: C' = [. inf C', sup C' .] by INTEGRA1:5;
  inf C' <= sup C' by Th53;
  then inf C' in C & sup C' in C by A1,XXREAL_1:1;
  then reconsider p1 = inf C', p2 = sup C' as Point of I[01];
  take p1, p2;
  thus p1 <= p2 by Th53;
  thus thesis by INTEGRA1:5;
end;

begin :: Decompositions of Simple Closed Curves

definition
  func I(01) -> strict non empty SubSpace of I[01] means
  :Def1:
  the carrier of it = ]. 0,1 .[;
  existence
  proof
    reconsider E = ]. 0,1 .[ as non empty Subset of I[01]
    by BORSUK_1:83,XXREAL_1:25,33;
    take I[01] | E;
    thus thesis by PRE_TOPC:29;
  end;
  uniqueness by TSEP_1:5;
end;

theorem
  for A being Subset of I[01] st A = the carrier of I(01) holds
  I(01) = I[01] | A by PRE_TOPC:29,TSEP_1:5;

theorem Th58:
  the carrier of I(01) = (the carrier of I[01]) \ {0,1}
proof
A1: the carrier of I(01) = ]. 0,1 .[ by Def1;
  [.0,1.] = ].0,1.[ \/ {0,1} by XXREAL_1:128;
  hence thesis by A1,BORSUK_1:83,XBOOLE_1:88,XXREAL_1:86;
end;

theorem Th59:
  I(01) is open SubSpace of I[01] by Th58,JORDAN6:41,TSEP_1:def 1;

theorem Th60:
  for r being real number holds r in the carrier of I(01) iff 0 < r & r < 1
proof
  let r be real number;
  hereby
    assume r in the carrier of I(01);
    then r in ]. 0, 1 .[ by Def1;
    hence 0 < r & r < 1 by XXREAL_1:4;
  end;
  assume 0 < r & r < 1;
  then r in ]. 0, 1 .[ by XXREAL_1:4;
  hence thesis by Def1;
end;

theorem Th61:
  for a, b being Point of I[01] st a < b & b <> 1 holds
  ]. a, b .] is non empty Subset of I(01)
proof
  let a, b be Point of I[01];
  assume
A1: a < b & b <> 1;
  0 <= a & b <= 1 by BORSUK_1:86;
  then
A2: b < 1 by A1,XXREAL_0:1;
  ]. a, b .] c= the carrier of I(01)
  proof
    let x be set;
    assume x in ]. a, b .];
    then x in { r where r is Real : a < r & r <= b } by RCOMP_1:def 9;
    then consider r being Real such that
A3: x = r & a < r & r <= b;
    0 < r & r < 1 by A2,A3,BORSUK_1:86,XXREAL_0:2;
    hence thesis by A3,Th60;
  end;
  hence thesis by A1,XXREAL_1:2;
end;

theorem Th62:
  for a, b being Point of I[01] st
  a < b & a <> 0 holds [. a, b .[ is non empty Subset of I(01)
proof
  let a, b be Point of I[01];
  assume
A1: a < b & a <> 0;
A2: 0 <= a & b <= 1 by BORSUK_1:86;
  [. a, b .[ c= the carrier of I(01)
  proof
    let x be set;
    assume x in [. a, b .[;
    then x in { r where r is Real : a <= r & r < b } by RCOMP_1:def 8;
    then consider r being Real such that
A3: x = r & a <= r & r < b;
    0 < r & r < 1 by A1,A2,A3,XXREAL_0:2;
    hence thesis by A3,Th60;
  end;
  hence thesis by A1,XXREAL_1:3;
end;

theorem
  for D being Simple_closed_curve holds
  (TOP-REAL 2) | R^2-unit_square, (TOP-REAL 2) | D are_homeomorphic
proof
  let D be Simple_closed_curve;
  ex f being Function of (TOP-REAL 2)|R^2-unit_square, (TOP-REAL 2)|D st
  f is being_homeomorphism by TOPREAL2:def 1;
  hence thesis by T_0TOPSP:def 1;
end;

theorem
  for n being Element of NAT, D being non empty Subset of TOP-REAL n,
  p1, p2 being Point of TOP-REAL n st D is_an_arc_of p1, p2 holds
  I(01), (TOP-REAL n) | (D \ {p1,p2}) are_homeomorphic
proof
  let n be Element of NAT, D be non empty Subset of TOP-REAL n,
  p1, p2 be Point of TOP-REAL n;
  assume
A1: D is_an_arc_of p1, p2;
  then consider f being Function of I[01], (TOP-REAL n)|D such that
A2: f is being_homeomorphism & f.0 = p1 & f.1 = p2 by TOPREAL1:def 2;
A3: f is continuous one-to-one by A2,TOPS_2:def 5;
  set fD = f | the carrier of I(01);
A4: dom f = the carrier of I[01] by FUNCT_2:def 1;
  then
A5: dom fD = the carrier of I(01) by BORSUK_1:35,RELAT_1:91;
  reconsider K0 = the carrier of I(01) as Subset of I[01] by BORSUK_1:35;
A6: rng f = [#] ((TOP-REAL n)|D) by A2,TOPS_2:def 5
    .= D by PRE_TOPC:29;
A7: 0 in dom f & 1 in dom f by A4,BORSUK_1:86;
A8: f .: the carrier of I(01) =
  f .: (the carrier of I[01]) \ f .: {0,1} by A3,Th58,FUNCT_1:123
    .= D \ f .: {0,1} by A4,A6,RELAT_1:146
    .= D \ {p1,p2} by A2,A7,FUNCT_1:118;
A9: rng fD = f .: the carrier of I(01) by RELAT_1:148;
  then
A10: rng fD = the carrier of ((TOP-REAL n)|(D \ {p1,p2})) by A8,PRE_TOPC:29;
A11: I(01) = I[01] | K0 by PRE_TOPC:29,TSEP_1:5;
  then reconsider fD1 = fD as Function
  of I[01]|K0, (TOP-REAL n)|D by FUNCT_2:38;
A12: fD1 is continuous by A3,TOPMETR:10;
  reconsider fD as Function of I(01), (TOP-REAL n)|(D \ {p1,p2}) by A5,A10,
FUNCT_2:3;
A13: dom fD = [#]I(01) by A4,BORSUK_1:35,RELAT_1:91;
A14: D \ {p1,p2} c= D by XBOOLE_1:36;
  reconsider D1 = D \ {p1,p2} as non empty Subset of TOP-REAL n
  by A1,JORDAN6:56;
  reconsider D0 = D \ {p1,p2} as Subset of (TOP-REAL n)|D by A14,PRE_TOPC:29;
A15: (TOP-REAL n)|D1 = ((TOP-REAL n)|D)|D0 by GOBOARD9:4;
A16: rng fD = [#]((TOP-REAL n)|(D \ {p1,p2})) by A8,A9,PRE_TOPC:29;
  f is one-to-one by A2,TOPS_2:def 5;
  then
A17: fD is one-to-one by FUNCT_1:84;
A18: fD is continuous by A11,A12,A15,TOPMETR:9;
  set g = (f") | D1;
A19: f" is continuous by A2,TOPS_2:def 5;
A20: D1 = the carrier of (TOP-REAL n)|D1 by PRE_TOPC:29;
  D1 c= the carrier of (TOP-REAL n)|D by A14,PRE_TOPC:29;
  then reconsider ff = g as Function
  of (TOP-REAL n)|D1, I[01] by A20,FUNCT_2:38;
A21: ff is continuous by A15,A19,TOPMETR:10;
  rng f = [#]((TOP-REAL n)|D) by A2,TOPS_2:def 5;
  then fD" = (fD qua Function)" & f" = (f qua Function)"
  by A3,A10,A17,TOPS_2:def 4;
  then fD" is continuous by A3,A8,A11,A21,RFUNCT_2:40,TOPMETR:9;
  then fD is being_homeomorphism by A13,A16,A17,A18,TOPS_2:def 5;
  hence thesis by T_0TOPSP:def 1;
end;

theorem Th65:
  for n being Element of NAT, D being Subset of TOP-REAL n,
  p1, p2 being Point of TOP-REAL n st D is_an_arc_of p1, p2 holds
  I[01], (TOP-REAL n) | D are_homeomorphic
proof
  let n be Element of NAT, D be Subset of TOP-REAL n,
  p1, p2 be Point of TOP-REAL n;
  assume D is_an_arc_of p1, p2;
  then ex f being Function of I[01], (TOP-REAL n)|D st
  f is being_homeomorphism & f.0 = p1 & f.1 = p2 by TOPREAL1:def 2;
  hence thesis by T_0TOPSP:def 1;
end;

theorem
  for n being Element of NAT, p1, p2 being Point of TOP-REAL n st p1 <> p2
  holds I[01], (TOP-REAL n) | LSeg (p1, p2) are_homeomorphic
  by Th65,TOPREAL1:15;

theorem Th67:
  for E being Subset of I(01) st
  (ex p1, p2 being Point of I[01] st p1 < p2 & E = [. p1,p2 .]) holds
  I[01], I(01)|E are_homeomorphic
proof
  let E be Subset of I(01);
  given p1, p2 being Point of I[01] such that
A1: p1 < p2 & E = [. p1,p2 .];
  reconsider T = I(01)|E as SubSpace of I[01] by TSEP_1:7;
  0 <= p1 & p2 <= 1 by BORSUK_1:86;
  then reconsider S = Closed-Interval-TSpace(p1,p2) as SubSpace of I[01]
  by A1,TOPMETR:27,TREAL_1:6;
  the carrier of S = E by A1,TOPMETR:25;
  then
A2: S = T by PRE_TOPC:29,TSEP_1:5;
  set f = L[01]((#)(p1,p2), (p1,p2)(#));
  f is being_homeomorphism by A1,TREAL_1:20;
  hence thesis by A2,TOPMETR:27,T_0TOPSP:def 1;
end;

theorem Th68:
  for n being Element of NAT, A being Subset of TOP-REAL n,
  p, q being Point of TOP-REAL n,
  a, b being Point of I[01] st A is_an_arc_of p, q & a < b
  ex E being non empty Subset of I[01],
  f being Function of I[01]|E, (TOP-REAL n)|A st
  E = [. a, b .] & f is being_homeomorphism & f.a = p & f.b = q
proof
  let n be Element of NAT, A be Subset of TOP-REAL n,
  p, q be Point of TOP-REAL n, a, b be Point of I[01];
  assume that
A1: A is_an_arc_of p, q and
A2: a < b;
  reconsider B = A as non empty Subset of TOP-REAL n by A1,TOPREAL1:4;
  reconsider E = [. a, b .] as non empty Subset of I[01] by A2,Th49;
  take E;
  consider f being Function of I[01], (TOP-REAL n)|B such that
A3: f is being_homeomorphism & f.0 = p & f.1 = q by A1,TOPREAL1:def 2;
  0 <= a & b <= 1 by BORSUK_1:86;
  then
A4: I[01]|E = Closed-Interval-TSpace(a,b) by A2,TOPMETR:31;
  then reconsider e = P[01](a,b,(#)(0,1),(0,1)(#)) as Function
  of I[01]|E, I[01] by TOPMETR:27;
  set g = f * e;
A5: e is being_homeomorphism by A2,A4,TOPMETR:27,TREAL_1:20;
  reconsider g as Function of I[01]|E, (TOP-REAL n)|A;
  take g;
  thus E = [. a, b .];
  thus g is being_homeomorphism by A3,A5,TOPS_2:71;
A6: 0 = (#)(0,1) & a = (#)(a,b) by A2,TREAL_1:def 1;
A7: 1 = (0,1)(#) & b = (a,b)(#) by A2,TREAL_1:def 2;
  a in E by A2,XXREAL_1:1;
  then a in the carrier of I[01]|E by PRE_TOPC:29;
  hence g.a = f.(e.a) by FUNCT_2:21
    .= p by A2,A3,A6,TREAL_1:16;
  b in E by A2,XXREAL_1:1;
  then b in the carrier of I[01]|E by PRE_TOPC:29;
  hence g.b = f.(e.b) by FUNCT_2:21
    .= q by A2,A3,A7,TREAL_1:16;
end;

theorem Th69:
  for A being TopSpace, B being non empty TopSpace, f being Function of A, B,
  C being TopSpace, X being Subset of A st f is continuous &
  C is SubSpace of B holds
  for h being Function of A|X, C st h = f|X holds h is continuous
proof
  let A be TopSpace, B be non empty TopSpace;
  let f be Function of A,B;
  let C be TopSpace, X be Subset of A;
  assume that
A1: f is continuous and
A2: C is SubSpace of B;
  let h be Function of A|X, C;
  assume
A3: h = f|X;
  the carrier of A|X = X by PRE_TOPC:29;
  then reconsider g = f|X as Function of A|X, B by FUNCT_2:38;
  g is continuous by A1,TOPMETR:10;
  hence thesis by A2,A3,PRE_TOPC:57;
end;

theorem Th70:
  for X being Subset of I[01], a, b being Point of I[01] st
  X = ]. a, b .[ holds X is open
proof
  let X be Subset of I[01], a, b be Point of I[01];
  assume that
A1: X = ]. a, b .[;
A2: 0 in the carrier of I[01] & 1 in the carrier of I[01] by BORSUK_1:86;
A3: 0 <= a by BORSUK_1:86;
  reconsider A = [. 0, a .] as Subset of I[01]
   by A2,BORSUK_1:83,XXREAL_2:def 12;
A4: b <= 1 by BORSUK_1:86;
  reconsider B = [. b, 1 .] as Subset of I[01]
   by A2,BORSUK_1:83,XXREAL_2:def 12;
A5: A is closed by Th48;
  B is closed by Th48;
  then
A6: A \/ B is closed by A5,TOPS_1:36;
  X = (A \/ B)` by A1,A3,A4,BORSUK_1:83,XXREAL_1:200;
  hence thesis by A6;
end;

theorem Th71:
  for X being Subset of I(01), a, b being Point of I[01] st
  X = ]. a, b .[ holds X is open
proof
  let X be Subset of I(01), a, b be Point of I[01];
  assume
A1: X = ]. a, b .[;
  then reconsider X' = X as Subset of I[01] by Th44;
  X' is open by A1,Th70;
  hence thesis by Th59,TSEP_1:17;
end;

theorem Th72:
  for X being Subset of I(01), a being Point of I[01] st
  X = ]. 0, a .] holds X is closed
proof
  let X be Subset of I(01), a be Point of I[01];
  assume
A1: X = ]. 0, a .];
  per cases;
  suppose
A2: 0 < a;
A3: 1 in the carrier of I[01] by BORSUK_1:86;
    [#] I(01) = ]. 0, 1 .[ by Def1;
    then [#] I(01) \ X = ]. a, 1 .[ by A1,A2,XXREAL_1:187;
    then [#] I(01) \ X is open by A3,Th71;
    hence thesis by PRE_TOPC:def 6;
  end;
  suppose 0 >= a;
    then X = {}I(01) by A1,XXREAL_1:26;
    hence thesis;
  end;
end;

theorem Th73:
  for X being Subset of I(01), a being Point of I[01] st
  X = [. a, 1 .[ holds X is closed
proof
  let X be Subset of I(01), a be Point of I[01];
  assume
A1: X = [. a, 1 .[;
A2: 1 in the carrier of I[01] & 0 in the carrier of I[01] by BORSUK_1:86;
  per cases;
  suppose X is non empty;
    then
A3: a <> 1 by A1,XXREAL_1:18;
    a <= 1 by BORSUK_1:86;
    then
A4: a < 1 by A3,XXREAL_0:1;
    [#] I(01) = ]. 0, 1 .[ by Def1;
    then [#] I(01) \ X = ]. 0, a .[ by A1,A4,XXREAL_1:195;
    then [#] I(01) \ X is open by A2,Th71;
    hence thesis by PRE_TOPC:def 6;
  end;
  suppose X is empty;
    then X = {}I(01);
    hence thesis;
  end;
end;

theorem Th74:
  for n being Element of NAT, A being Subset of TOP-REAL n,
  p, q being Point of TOP-REAL n,
  a, b being Point of I[01] st A is_an_arc_of p, q & a < b & b <> 1
  ex E being non empty Subset of I(01),
  f being Function of I(01)|E, (TOP-REAL n)|(A \ {p}) st
  E = ]. a, b .] & f is being_homeomorphism & f.b = q
proof
  let n be Element of NAT, A be Subset of TOP-REAL n,
  p, q be Point of TOP-REAL n, a, b be Point of I[01];
  assume
A1: A is_an_arc_of p, q & a < b & b <> 1;
  then reconsider B = A as non empty Subset of TOP-REAL n by TOPREAL1:4;
  consider F being non empty Subset of I[01],
  s being Function of I[01]|F, (TOP-REAL n)|B such that
A2: F = [. a, b .] & s is being_homeomorphism & s.a = p & s.b = q by A1,Th68;
  reconsider E = ]. a, b .] as non empty Subset of I(01) by A1,Th61;
A3: the carrier of I(01)|E = E by PRE_TOPC:29;
  set X = E;
A4: X = F \ {a} by A1,A2,XXREAL_1:134;
A5: E c= F by A2,XXREAL_1:23;
  then reconsider X' = E as Subset of I[01]|F by PRE_TOPC:29;
  set sX = s|E;
A6: s is continuous one-to-one by A2,TOPS_2:def 5;
A7: rng s = [#] ((TOP-REAL n)|A) by A2,TOPS_2:def 5;
  then
A8: rng s = A by PRE_TOPC:def 10;
A9: dom s = [#] (I[01]|F) by A2,TOPS_2:def 5
    .= F by PRE_TOPC:def 10;
  then
A10: X c= dom s by A2,XXREAL_1:23;
A11: a in dom s by A1,A2,A9,XXREAL_1:1;
A12: s.:X = s.:F \ Im(s,a) by A4,A6,FUNCT_1:123
    .= s.:F \ {s.a} by A11,FUNCT_1:117
    .= rng s \ {p} by A2,A9,RELAT_1:146
    .= [#] ((TOP-REAL n)|(A \ {p})) by A8,PRE_TOPC:def 10;
  then
A13: [#] ((TOP-REAL n)|(A \ {p})) = rng sX by RELAT_1:148;
A14: rng (s|E) = the carrier of (TOP-REAL n)|(A \ {p}) by A12,RELAT_1:148;
A15: dom sX = X by A10,RELAT_1:91
    .= [#] (I(01)|X) by PRE_TOPC:def 10;
  then reconsider sX as Function of I(01)|E, (TOP-REAL n)|(A \ {p}) by A14,
FUNCT_2:3;
A16: the carrier of I(01)|X = X by PRE_TOPC:29;
A17: I(01)|X is SubSpace of I[01] by TSEP_1:7;
  I[01]|F|X' is SubSpace of I[01] by TSEP_1:7;
  then
A18: I(01)|X = I[01]|F|X' by A16,A17,PRE_TOPC:29,TSEP_1:5;
A19: the carrier of (TOP-REAL n)|(A \ {p}) = A \ {p} by PRE_TOPC:29;
A20: the carrier of (TOP-REAL n)|A = A by PRE_TOPC:29;
  then the carrier of (TOP-REAL n)|(A \ {p}) c=
  the carrier of (TOP-REAL n)|A by A19,XBOOLE_1:36;
  then (TOP-REAL n)|(A \ {p}) is SubSpace of (TOP-REAL n)|A by TSEP_1:4;
  then
A21: sX is continuous by A6,A18,Th69;
A22: sX is one-to-one by A6,FUNCT_1:84;
A23: s" is continuous by A2,TOPS_2:def 5;
  the carrier of I(01)|X c= the carrier of I[01]|F by A3,A5,PRE_TOPC:29;
  then
A24: I(01)|X is SubSpace of I[01]|F by A17,TSEP_1:4;
  reconsider Ap = A \ {p} as Subset of (TOP-REAL n)|A by A20,XBOOLE_1:36;
A25: ((TOP-REAL n)|A)|Ap = (TOP-REAL n)|(A \ {p}) by PRE_TOPC:28,XBOOLE_1:36;
  sX" = (sX qua Function)" by A13,A22,TOPS_2:def 4
    .= (s qua Function)" | (s.:X) by A6,RFUNCT_2:40
    .= s" | [#] ((TOP-REAL n)|(A \ {p})) by A6,A7,A12,TOPS_2:def 4
    .= s" | Ap by PRE_TOPC:def 10;
  then sX" is continuous by A23,A24,A25,Th69;
  then
A26: sX is being_homeomorphism by A13,A15,A21,A22,TOPS_2:def 5;
  b in X by A1,XXREAL_1:2;
  then sX.b = q by A2,FUNCT_1:72;
  hence thesis by A26;
end;

theorem Th75:
  for n being Element of NAT, A being Subset of TOP-REAL n,
  p, q being Point of TOP-REAL n,
  a, b being Point of I[01] st A is_an_arc_of p, q & a < b & a <> 0
  ex E being non empty Subset of I(01),
  f being Function of I(01)|E, (TOP-REAL n)|(A \ {q}) st
  E = [. a, b .[ & f is being_homeomorphism & f.a = p
proof
  let n be Element of NAT, A be Subset of TOP-REAL n,
  p, q be Point of TOP-REAL n, a, b be Point of I[01];
  assume
A1: A is_an_arc_of p, q & a < b & a <> 0;
  then reconsider B = A as non empty Subset of TOP-REAL n by TOPREAL1:4;
  consider F being non empty Subset of I[01],
  s being Function of I[01]|F, (TOP-REAL n)|B such that
A2: F = [. a, b .] & s is being_homeomorphism & s.a = p & s.b = q by A1,Th68;
  reconsider E = [. a, b .[ as non empty Subset of I(01) by A1,Th62;
A3: the carrier of I(01)|E = E by PRE_TOPC:29;
  set X = E;
A4: X = F \ {b} by A1,A2,XXREAL_1:135;
A5: E c= F by A2,XXREAL_1:24;
  then reconsider X' = E as Subset of I[01]|F by PRE_TOPC:29;
  set sX = s|E;
A6: s is continuous one-to-one by A2,TOPS_2:def 5;
A7: rng s = [#] ((TOP-REAL n)|A) by A2,TOPS_2:def 5;
  then
A8: rng s = A by PRE_TOPC:def 10;
A9: dom s = [#] (I[01]|F) by A2,TOPS_2:def 5
    .= F by PRE_TOPC:def 10;
  then
A10: X c= dom s by A2,XXREAL_1:24;
A11: b in dom s by A1,A2,A9,XXREAL_1:1;
A12: s.:X = s.:F \ Im(s,b) by A4,A6,FUNCT_1:123
    .= s.:F \ {s.b} by A11,FUNCT_1:117
    .= rng s \ {q} by A2,A9,RELAT_1:146
    .= [#] ((TOP-REAL n)|(A \ {q})) by A8,PRE_TOPC:def 10;
  then
A13: [#] ((TOP-REAL n)|(A \ {q})) = rng sX by RELAT_1:148;
A14: rng (s|E) = the carrier of (TOP-REAL n)|(A \ {q}) by A12,RELAT_1:148;
A15: dom sX = X by A10,RELAT_1:91
    .= [#] (I(01)|X) by PRE_TOPC:def 10;
  then reconsider sX as Function of I(01)|E, (TOP-REAL n)|(A \ {q}) by A14,
FUNCT_2:3;
A16: the carrier of I(01)|X = X by PRE_TOPC:29;
A17: I(01)|X is SubSpace of I[01] by TSEP_1:7;
  I[01]|F|X' is SubSpace of I[01] by TSEP_1:7;
  then
A18: I(01)|X = I[01]|F|X' by A16,A17,PRE_TOPC:29,TSEP_1:5;
A19: the carrier of (TOP-REAL n)|(A \ {q}) = A \ {q} by PRE_TOPC:29;
A20: the carrier of (TOP-REAL n)|A = A by PRE_TOPC:29;
  then the carrier of (TOP-REAL n)|(A \ {q}) c=
  the carrier of (TOP-REAL n)|A by A19,XBOOLE_1:36;
  then (TOP-REAL n)|(A \ {q}) is SubSpace of (TOP-REAL n)|A by TSEP_1:4;
  then
A21: sX is continuous by A6,A18,Th69;
A22: sX is one-to-one by A6,FUNCT_1:84;
A23: s" is continuous by A2,TOPS_2:def 5;
  the carrier of I(01)|X c= the carrier of I[01]|F by A3,A5,PRE_TOPC:29;
  then
A24: I(01)|X is SubSpace of I[01]|F by A17,TSEP_1:4;
  reconsider Ap = A \ {q} as Subset of (TOP-REAL n)|A by A20,XBOOLE_1:36;
A25: ((TOP-REAL n)|A)|Ap = (TOP-REAL n)|(A \ {q}) by PRE_TOPC:28,XBOOLE_1:36;
  sX" = (sX qua Function)" by A13,A22,TOPS_2:def 4
    .= (s qua Function)" | (s.:X) by A6,RFUNCT_2:40
    .= s" | [#] ((TOP-REAL n)|(A \ {q})) by A6,A7,A12,TOPS_2:def 4
    .= s" | Ap by PRE_TOPC:def 10;
  then sX" is continuous by A23,A24,A25,Th69;
  then
A26: sX is being_homeomorphism by A13,A15,A21,A22,TOPS_2:def 5;
  a in X by A1,XXREAL_1:3;
  then sX.a = p by A2,FUNCT_1:72;
  hence thesis by A26;
end;

theorem Th76:
  for n being Element of NAT, A, B being Subset of TOP-REAL n,
  p, q being Point of TOP-REAL n st
  A is_an_arc_of p, q & B is_an_arc_of q, p & A /\ B = { p, q } & p <> q holds
  I(01), (TOP-REAL n) | ((A \ {p}) \/ (B \ {p})) are_homeomorphic
proof
  let n be Element of NAT, A, B be Subset of TOP-REAL n,
  p, q be Point of TOP-REAL n;
  assume that
A1: A is_an_arc_of p, q and
A2: B is_an_arc_of q, p and
A3: A /\ B = { p, q } & p <> q;
  reconsider a = 0, b = 1/2, c = 1 as Point of I[01] by BORSUK_1:86;
A4: 0 in the carrier of I[01] & 1/2 in the carrier of I[01] by BORSUK_1:86;
  consider E1 being non empty Subset of I(01),
  sx being Function of I(01)|E1, (TOP-REAL n)|(A \ {p}) such that
A5: E1 = ]. a, b .] & sx is being_homeomorphism & sx.b = q by A1,Th74;
  consider E2 being non empty Subset of I(01),
  ty being Function of I(01)|E2, (TOP-REAL n)|(B \ {p}) such that
A6: E2 = [. b, c .[ & ty is being_homeomorphism & ty.b = q by A2,Th75;
  set T1 = I(01)|E1, T2 = I(01)|E2, T = I(01),
  S = (TOP-REAL n) | ((A \ {p}) \/ (B \ {p})),
  U1 = (TOP-REAL n) | (A \ {p}), U2 = (TOP-REAL n)|(B \ {p});
A7: A \ {p} is non empty & B \ {p} is non empty by A1,A2,Th7,JORDAN5B:14;
  then (A \ {p}) \/ (B \ {p}) is non empty;
  then the carrier of S is non empty;
  then reconsider S as non empty SubSpace of TOP-REAL n;
A8: the carrier of U1 is non empty by A7;
  the carrier of U2 is non empty by A7;
  then reconsider U1, U2 as non empty SubSpace of TOP-REAL n
  by A8;
A9: [#] U1 = A \ {p} by PRE_TOPC:def 10;
A10: [#] U2 = B \ {p} by PRE_TOPC:def 10;
A11: the carrier of U1 = A \ {p} by PRE_TOPC:29;
A12: the carrier of S = (A \ {p}) \/ (B \ {p}) by PRE_TOPC:29;
  then
A13: the carrier of U1 c= the carrier of S by A11,XBOOLE_1:7;
  then
A14: U1 is SubSpace of S by TSEP_1:4;
  the carrier of U2 = B \ {p} by PRE_TOPC:29;
  then
A15: the carrier of U2 c= the carrier of S by A12,XBOOLE_1:7;
  then
A16: U2 is SubSpace of S by TSEP_1:4;
  reconsider sx' = sx as Function of T1, S by A13,FUNCT_2:9;
  sx is continuous by A5,TOPS_2:def 5;
  then
A17: sx' is continuous by A14,PRE_TOPC:56;
  reconsider ty' = ty as Function of T2, S by A15,FUNCT_2:9;
  ty is continuous by A6,TOPS_2:def 5;
  then
A18: ty' is continuous by A16,PRE_TOPC:56;
  reconsider F1 = [#] T1, F2 = [#] T2 as non empty Subset of T
  by PRE_TOPC:def 10;
A19: F1 = ]. 0, 1/2 .] by A5,PRE_TOPC:def 10;
  then
A20: F1 is closed by A4,Th72;
A21: F2 = [. 1/2, 1 .[ by A6,PRE_TOPC:def 10;
  then
A22: F2 is closed by A4,Th73;
A23: ([#] T1) \/ ([#] T2) = ]. 0, 1 .[ by A19,A21,XXREAL_1:172
    .= [#] T by Def1;
A24: ([#] T1) /\ ([#] T2) = { 1/2 } by A19,A21,XXREAL_1:138;
  for d be set st d in ([#] T1) /\ ([#] T2) holds sx.d = ty.d
  proof
    let d be set;
    assume d in ([#] T1) /\ ([#] T2);
    then d = b by A24,TARSKI:def 1;
    hence thesis by A5,A6;
  end;
  then consider F being Function of T,S such that
A25: F = sx'+*ty & F is continuous by A17,A18,A20,A22,A23,JGRAPH_2:9;
A26: dom F = [#] I(01) by FUNCT_2:def 1;
A27: sx' is one-to-one by A5,TOPS_2:def 5;
A28: ty' is one-to-one by A6,TOPS_2:def 5;
A29: dom sx' = [#] T1 & dom ty' = [#] T2 by FUNCT_2:def 1;
  then
A30: dom sx' /\ dom ty' = { b } by A19,A21,XXREAL_1:138;
A31: rng sx = [#] ((TOP-REAL n)|(A \ {p})) by A5,TOPS_2:def 5;
  then
A32: rng sx = A \ {p} by PRE_TOPC:def 10;
A33: rng ty = [#] ((TOP-REAL n)|(B \ {p})) by A6,TOPS_2:def 5;
  then
A34: rng ty = B \ {p} by PRE_TOPC:def 10;
  then
A35: rng sx' /\ rng ty' = ((A \ {p}) /\ B) \ {p} by A32,XBOOLE_1:49
    .= ((A /\ B) \ {p}) \ {p} by XBOOLE_1:49
    .= (A /\ B) \ ({p} \/ {p}) by XBOOLE_1:41
    .= { sx'.b } by A3,A5,ZFMISC_1:23;
  then
A36: F is one-to-one by A25,A27,A28,A30,Th5;
A37: sx" = (sx qua Function)" by A27,A31,TOPS_2:def 4;
A38: ty" = (ty qua Function)" by A28,A33,TOPS_2:def 4;
  sx' tolerates ty'
  proof
    let t be set;
    assume t in dom sx' /\ dom ty';
    then t = b by A30,TARSKI:def 1;
    hence thesis by A5,A6;
  end;
  then
A39: rng F = rng sx' \/ rng ty' by A25,FRECHET:39
    .= [#] S by A32,A34,PRE_TOPC:def 10;
  then F" = (F qua Function)" by A36,TOPS_2:def 4;
  then
A40: F" = sx" +* ty" by A5,A6,A25,A27,A28,A30,A35,A37,A38,Th6;
  the carrier of T1 c= the carrier of T by BORSUK_1:35;
  then reconsider f = sx" as Function of U1, T by FUNCT_2:9;
  the carrier of T2 c= the carrier of T by BORSUK_1:35;
  then reconsider g = ty" as Function of U2, T by FUNCT_2:9;
A41: [#] U1 c= (A \ {p}) \/ (B \ {p}) by A9,XBOOLE_1:7;
  [#] U2 c= (A \ {p}) \/ (B \ {p}) by A10,XBOOLE_1:7;
  then reconsider V1 = [#] U1, V2 = [#] U2 as Subset of S by A41,PRE_TOPC:29;
A42: ([#] U1) \/ ([#] U2) = [#] S by A9,A10,PRE_TOPC:def 10;
A43: V1 is closed
  proof
    reconsider A' = A as Subset of TOP-REAL n;
A44: A' is closed by A1,COMPTS_1:16,JORDAN5A:1;
A45: not p in {q} by A3,TARSKI:def 1;
    q in A by A1,TOPREAL1:4;
    then
A46: {q} c= A by ZFMISC_1:37;
A47: A /\ (B \ {p}) = A /\ B \ {p} by XBOOLE_1:49
      .= {q} by A3,ZFMISC_1:23;
    A' /\ [#] S = A' /\ ((A \ {p}) \/ (B \ {p})) by PRE_TOPC:def 10
      .= (A /\ (A \ {p})) \/ (A /\ (B \ {p})) by XBOOLE_1:23
      .= (A \ {p}) \/ (A /\ (B \ {p})) by XBOOLE_1:28,36
      .= A \ {p} by A45,A46,A47,XBOOLE_1:12,ZFMISC_1:40
      .= V1 by PRE_TOPC:def 10;
    hence thesis by A44,PRE_TOPC:43;
  end;
A48: V2 is closed
  proof
    reconsider B' = B as Subset of TOP-REAL n;
A49: B' is closed by A2,COMPTS_1:16,JORDAN5A:1;
A50: not p in {q} by A3,TARSKI:def 1;
    q in B by A2,TOPREAL1:4;
    then
A51: {q} c= B by ZFMISC_1:37;
A52: B /\ (A \ {p}) = B /\ A \ {p} by XBOOLE_1:49
      .= {q} by A3,ZFMISC_1:23;
    B' /\ [#] S = B' /\ ((A \ {p}) \/ (B \ {p})) by PRE_TOPC:def 10
      .= (B /\ (A \ {p})) \/ (B /\ (B \ {p})) by XBOOLE_1:23
      .= (B /\ (A \ {p})) \/ (B \ {p}) by XBOOLE_1:28,36
      .= B \ {p} by A50,A51,A52,XBOOLE_1:12,ZFMISC_1:40
      .= V2 by PRE_TOPC:def 10;
    hence thesis by A49,PRE_TOPC:43;
  end;
  sx" is continuous by A5,TOPS_2:def 5;
  then
A53: f is continuous by PRE_TOPC:56;
  ty" is continuous by A6,TOPS_2:def 5;
  then
A54: g is continuous by PRE_TOPC:56;
  for r being set st r in ([#] U1) /\ ([#] U2) holds f.r = g.r
  proof
    let r be set;
    assume r in ([#] U1) /\ ([#] U2);
    then
A55: r = q by A5,A31,A33,A35,TARSKI:def 1;
    b in E1 by A5,XXREAL_1:2;
    then
A56: b in dom sx by A29,PRE_TOPC:def 10;
    b in E2 by A6,XXREAL_1:3;
    then
A57: b in dom ty by A29,PRE_TOPC:def 10;
    f.q = b by A5,A27,A37,A56,FUNCT_1:56;
    hence thesis by A6,A28,A38,A55,A57,FUNCT_1:56;
  end;
  then consider h being Function of S,T such that
A58: h = f+*g & h is continuous by A14,A16,A42,A43,A48,A53,A54,JGRAPH_2:9;
  F is being_homeomorphism by A25,A26,A36,A39,A40,A58,TOPS_2:def 5;
  hence thesis by T_0TOPSP:def 1;
end;

theorem Th77:
  for D being Simple_closed_curve, p being Point of TOP-REAL 2 st p in D holds
  (TOP-REAL 2) | (D \ {p}), I(01) are_homeomorphic
proof
  let D be Simple_closed_curve, p be Point of TOP-REAL 2;
  assume
A1: p in D;
  consider q being Point of TOP-REAL 2 such that
A2: q in D & p <> q by Th4;
  consider P1, P2 being non empty Subset of TOP-REAL 2 such that
A3: P1 is_an_arc_of p,q and
A4: P2 is_an_arc_of p,q and
A5: D = P1 \/ P2 and
A6: P1 /\ P2 = {p,q} by A1,A2,TOPREAL2:5;
  not q in {p} by A2,TARSKI:def 1;
  then reconsider R2p = D \ {p} as non empty Subset of TOP-REAL 2
  by A2,XBOOLE_0:def 5;
A7: D \ {p} = (P1 \ {p}) \/ (P2 \ {p}) by A5,XBOOLE_1:42;
  P2 is_an_arc_of q, p by A4,JORDAN5B:14;
  then (TOP-REAL 2) | R2p, I(01) are_homeomorphic by A2,A3,A6,A7,Th76;
  hence thesis;
end;

theorem
  for D being Simple_closed_curve,
  p, q being Point of TOP-REAL 2 st p in D & q in D holds
  (TOP-REAL 2) | (D \ {p}), (TOP-REAL 2) | (D \ {q}) are_homeomorphic
proof
  let D be Simple_closed_curve, p, q be Point of TOP-REAL 2;
  assume
A1: p in D & q in D;
  per cases;
  suppose p = q;
    hence thesis;
  end;
  suppose p <> q;
    then reconsider Dp = D \ {p}, Dq = D \ {q} as
    non empty Subset of TOP-REAL 2 by A1,ZFMISC_1:64;
A4: (TOP-REAL 2) | Dp, I(01) are_homeomorphic by A1,Th77;
    (TOP-REAL 2) | Dq, I(01) are_homeomorphic by A1,Th77;
    hence thesis by A4,BORSUK_3:3;
  end;
end;

theorem Th79:
  for n being Element of NAT, C being non empty Subset of TOP-REAL n,
  E being Subset of I(01) st
  (ex p1, p2 being Point of I[01] st p1 < p2 & E = [. p1,p2 .]) &
  I(01)|E, (TOP-REAL n)|C are_homeomorphic holds
  ex s1, s2 being Point of TOP-REAL n st C is_an_arc_of s1,s2
proof
  let n be Element of NAT, C be non empty Subset of TOP-REAL n,
  E be Subset of I(01);
  given p1, p2 being Point of I[01] such that
A1: p1 < p2 & E = [. p1,p2 .];
  E is non empty by A1,Th49;
  then
A3: I(01)|E is non empty;
  assume
A4: I(01)|E, (TOP-REAL n)|C are_homeomorphic;
  I[01], I(01)|E are_homeomorphic by A1,Th67;
  then I[01], (TOP-REAL n)|C are_homeomorphic by A3,A4,BORSUK_3:3;
  then consider g being Function of I[01], (TOP-REAL n)|C such that
A5: g is being_homeomorphism by T_0TOPSP:def 1;
  set s1 = g.0, s2 = g.1;
A6: the carrier of (TOP-REAL n)|C c= the carrier of TOP-REAL n by BORSUK_1:35;
  0 in the carrier of I[01] by BORSUK_1:86;
  then
A7: g.0 in the carrier of (TOP-REAL n)|C by FUNCT_2:7;
  1 in the carrier of I[01] by BORSUK_1:86;
  then g.1 in the carrier of (TOP-REAL n)|C by FUNCT_2:7;
  then reconsider s1, s2 as Point of TOP-REAL n by A6,A7;
  C is_an_arc_of s1, s2 by A5,TOPREAL1:def 2;
  hence thesis;
end;

theorem Th80:
  for Dp being non empty Subset of TOP-REAL 2,
  f being Function of (TOP-REAL 2) | Dp, I(01),
  C being non empty Subset of TOP-REAL 2
   st f is being_homeomorphism & C c= Dp &
  (ex p1, p2 being Point of I[01] st p1 < p2 & f.:C = [. p1,p2 .]) holds
  ex s1, s2 being Point of TOP-REAL 2 st C is_an_arc_of s1,s2
proof
  let Dp be non empty Subset of TOP-REAL 2,
  f be Function of (TOP-REAL 2) | Dp, I(01),
  C be non empty Subset of TOP-REAL 2;
  assume
A1: f is being_homeomorphism & C c= Dp;
  given p1, p2 being Point of I[01] such that
A2: p1 < p2 & f.:C = [. p1,p2 .];
  reconsider E = [. p1,p2 .] as Subset of I(01) by A2;
  set g = f"| E;
  f" is being_homeomorphism by A1,TOPS_2:70;
  then
A3: f" is one-to-one by TOPS_2:def 5;
A4: f is continuous one-to-one by A1,TOPS_2:def 5;
A5: f"(f.:C) = C
  proof
    thus f"(f.:C) c= C by A4,FUNCT_1:152;
    dom f = the carrier of (TOP-REAL 2) | Dp by FUNCT_2:def 1;
    then C c= dom f by A1,PRE_TOPC:29;
    hence thesis by FUNCT_1:146;
  end;
A6: rng f = [#] I(01) by A1,TOPS_2:def 5;
  then
A7: f".:E = C by A2,A4,A5,TOPS_2:68;
A8: rng g = f".:E by RELAT_1:148
    .= [#] ((TOP-REAL 2)|C) by A7,PRE_TOPC:29;
  the carrier of (I(01)|E) = E by PRE_TOPC:29;
  then g is Function of the carrier of I(01)|E,
  the carrier of (TOP-REAL 2)|Dp by FUNCT_2:38;
  then reconsider g as Function of I(01)|E, (TOP-REAL 2)|C by A8,FUNCT_2:8;
  dom (f") = the carrier of I(01) by FUNCT_2:def 1;
  then dom g = E by RELAT_1:91
    .= the carrier of (I(01)|E) by PRE_TOPC:29;
  then
A9: dom g = [#] (I(01)|E);
A10: g is one-to-one by A3,FUNCT_1:84;
  the carrier of (TOP-REAL 2)|C = C & the carrier of (TOP-REAL 2)|Dp = Dp
  by PRE_TOPC:29;
  then
A11: (TOP-REAL 2)|C is SubSpace of (TOP-REAL 2) | Dp by A1,TOPMETR:4;
  f" is continuous by A1,TOPS_2:def 5;
  then
A12: g is continuous by A11,Th69;
A13: g" = (g qua Function)" by A8,A10,TOPS_2:def 4
    .= ((f" qua Function)")|((f").:E) by A3,RFUNCT_2:40
    .= ((f qua Function)"")|((f").:E) by A4,A6,TOPS_2:def 4
    .= f|C by A4,A7,FUNCT_1:65;
  then reconsider t = f|C as Function of (TOP-REAL 2) | C, I(01)|E;
  reconsider C' = C as Subset of (TOP-REAL 2) | Dp by A1,PRE_TOPC:29;
  ((TOP-REAL 2) | Dp)|C' = (TOP-REAL 2) | C by A1,PRE_TOPC:28;
  then t is continuous by A4,Th69;
  then g is being_homeomorphism by A8,A9,A10,A12,A13,TOPS_2:def 5;
  then I(01)|E, (TOP-REAL 2)|C are_homeomorphic by T_0TOPSP:def 1;
  hence thesis by A2,Th79;
end;

theorem
  for D being Simple_closed_curve,
  C being non empty compact connected Subset of TOP-REAL 2 st C c= D holds
  C = D or (ex p1, p2 being Point of TOP-REAL 2 st C is_an_arc_of p1,p2) or
  (ex p being Point of TOP-REAL 2 st C = {p})
proof
  let D be Simple_closed_curve,
  C be non empty compact connected Subset of TOP-REAL 2;
  assume
A1: C c= D;
  assume
A2: C <> D;
  per cases;
  suppose C is trivial;
    hence thesis by Th2;
  end;
  suppose C is non trivial;
    then consider d1,d2 being Point of TOP-REAL 2 such that
A3: d1 in C & d2 in C & d1 <> d2 by REALSET1:15;
    C c< D by A1,A2,XBOOLE_0:def 8;
    then consider p being Point of TOP-REAL 2 such that
A4: p in D & C c= D \ {p} by Th1;
A5: d1 in D \ {p} & d2 in D \ {p} by A3,A4;
    reconsider Dp = D \ {p} as non empty Subset of TOP-REAL 2 by A3,A4;
    (TOP-REAL 2) | Dp, I(01) are_homeomorphic by A4,Th77;
    then consider f being Function of (TOP-REAL 2) | Dp, I(01) such that
A6: f is being_homeomorphism by T_0TOPSP:def 1;
    reconsider C' = C as Subset of (TOP-REAL 2) | Dp by A4,PRE_TOPC:29;
    set fC = f.:C';
    C c= [#] ((TOP-REAL 2) | Dp) by A4,PRE_TOPC:29;
    then
A7: C' is compact by COMPTS_1:11;
A8: C' is connected by CONNSP_1:24;
A9: f is continuous by A6,TOPS_2:def 5;
    rng f = [#] I(01) by A6,TOPS_2:def 5;
    then reconsider fC as compact connected Subset of I(01)
    by A7,A8,A9,COMPTS_1:24,TOPS_2:75;
    reconsider fC' = fC as Subset of I[01] by PRE_TOPC:39;
A10: fC' c= [#] I(01);
A11: for P being Subset of I(01) st P=fC' holds P is compact;
A12: d1 in the carrier of (TOP-REAL 2) | Dp by A5,PRE_TOPC:29;
A13: f.d1 in f.:C' by A3,FUNCT_2:43;
A14: d2 in the carrier of (TOP-REAL 2) | Dp by A5,PRE_TOPC:29;
A15: f.d2 in f.:C' by A3,FUNCT_2:43;
    then reconsider fC' as non empty compact connected Subset of I[01]
    by A10,A11,COMPTS_1:11,CONNSP_1:24;
A16: d1 in dom f by A12,FUNCT_2:def 1;
A17: d2 in dom f by A14,FUNCT_2:def 1;
    consider p1, p2 being Point of I[01] such that
A18: p1 <= p2 & fC' = [. p1,p2 .] by Th56;
A19: f is one-to-one by A6,TOPS_2:def 5;
    p1 <> p2
    proof
      assume p1 = p2;
      then
A20:  fC' = {p1} by A18,XXREAL_1:17;
      then
A21:  f.d1 = p1 by A13,TARSKI:def 1;
      f.d2 = p1 by A15,A20,TARSKI:def 1;
      hence thesis by A3,A16,A17,A19,A21,FUNCT_1:def 8;
    end;
    then p1 < p2 by A18,XXREAL_0:1;
    then consider s1, s2 being Point of TOP-REAL 2 such that
A22: C is_an_arc_of s1,s2 by A4,A6,A18,Th80;
    thus thesis by A22;
  end;
end;

begin :: Addenda

:: from BORSUK_6, 2007.04.08, A,T.

definition
  let T be 1-sorted;
  attr T is real-membered means :: BORSUK_6:def 1
  :Def2:
  the carrier of T is real-membered;
end;

registration
  cluster I[01] -> real-membered;
  coherence
  proof
    thus the carrier of I[01] is real-membered by BORSUK_1:83;
  end;
end;

:: from RCOMP_3, 2007.04.08, A,T.

registration
  cluster real-membered 1-sorted;
  existence
  proof
    take I[01]; thus thesis;
  end;
end;

registration
  let S be real-membered 1-sorted;
  cluster the carrier of S -> real-membered;
  coherence by Def2;
end;

theorem Th82:
  for C being non empty compact Subset of I[01] st C c= ].0,1.[ holds
  ex D being closed-interval Subset of REAL st
  C c= D & D c= ].0,1.[ & inf C = inf D & sup C = sup D
proof
  let C be non empty compact Subset of I[01];
  assume
A1: C c= ].0,1.[;
  reconsider C' = C as Subset of REAL by BORSUK_1:83,XBOOLE_1:1;
A2: C' is closed by Th52;
A3: C' is bounded_below bounded_above by Th50;
  then
A4: inf C' in C' by A2,RCOMP_1:31;
A5: sup C' in C' by A2,A3,RCOMP_1:30;
  reconsider D = [. inf C', sup C' .] as Subset of REAL;
A6: C c= D
  proof
    let x be set;
    assume
A7: x in C;
    then x in the carrier of I[01];
    then reconsider x' = x as Real by BORSUK_1:83;
    inf C' <= x' & x' <= sup C' by A7,Th51;
    hence thesis by XXREAL_1:1;
  end;
  consider x being Element of C';
A8: C' is bounded by A3;
  then
A9: lower_bound C' <= upper_bound C' by SEQ_4:24;
A10: D is non empty compact connected Subset of I[01] by A4,A5,A8,Th49,SEQ_4:24
  ;
  then
A11: D is closed-interval Subset of REAL by Th55;
  then
A12: D = [. inf D, sup D .] by INTEGRA1:5;
  then
A13: inf C' = inf D & sup C' = sup D by A9,XXREAL_1:66;
A14: not 0 in D
  proof
    assume 0 in D;
    then inf D <= 0 by A11,INTEGRA2:1;
    hence thesis by A1,A4,A13,XXREAL_1:4;
  end;
A15: not 1 in D
  proof
    assume 1 in D;
    then sup D >= 1 by A11,INTEGRA2:1;
    hence thesis by A1,A5,A13,XXREAL_1:4;
  end;
A16: D c= ].0,1.[
  proof
    let x be set;
    assume
A17: x in D;
    thus thesis by A10,A14,A15,A17,BORSUK_1:83,XXREAL_1:5;
  end;
  reconsider D as closed-interval Subset of REAL by A9,INTEGRA1:def 1;
  take D;
  thus thesis by A6,A9,A12,A16,XXREAL_1:66;
end;

theorem Th83:
  for C being non empty compact Subset of I[01] st C c= ].0,1.[ holds
  ex p1, p2 being Point of I[01] st
  p1 <= p2 & C c= [. p1, p2 .] & [.p1,p2.] c= ]. 0, 1 .[
proof
  let C be non empty compact Subset of I[01];
  assume C c= ].0,1.[;
  then consider D being closed-interval Subset of REAL such that
A1: C c= D & D c= ].0,1.[ & inf C = inf D & sup C = sup D by Th82;
  consider p1,p2 being Real such that
A2: p1 <= p2 & D = [.p1,p2.] by INTEGRA1:def 1;
A3: ].0,1.[ c= [.0,1.] by XXREAL_1:25;
  p1 in D & p2 in D by A2,XXREAL_1:1;
  then p1 in ].0,1.[ & p2 in ].0,1.[ by A1;
  then reconsider p1, p2 as Point of I[01] by A3,BORSUK_1:83;
  take p1, p2;
  thus p1 <= p2 by A2;
  thus thesis by A1,A2;
end;

theorem
  for D being Simple_closed_curve,
  C being closed Subset of TOP-REAL 2 st C c< D
  ex p1,p2 being Point of TOP-REAL 2, B being Subset of TOP-REAL 2 st
  B is_an_arc_of p1,p2 & C c= B & B c= D
proof
  let D be Simple_closed_curve, C be closed Subset of TOP-REAL 2;
  assume
A1: C c< D;
  then
X: C c= D by XBOOLE_0:def 8;
  D is Bounded by JORDAN2C:73;
  then
A2: C is compact by X,JORDAN2C:16,TOPREAL6:88;
A4: for C being compact Subset of TOP-REAL 2
  st C is non trivial & C c< D ex p1,p2 being Point of TOP-REAL 2,
  B being Subset of TOP-REAL 2 st B is_an_arc_of p1,p2 & C c= B & B c= D
  proof
    let C be compact Subset of TOP-REAL 2;
    assume C is non trivial;
    then consider d1,d2 being Point of TOP-REAL 2 such that
A5: d1 in C & d2 in C & d1 <> d2 by REALSET1:15;
    assume C c< D;
    then consider p being Point of TOP-REAL 2 such that
A7: p in D & C c= D \ {p} by A5,Th1;
A8: d1 in D \ {p} & d2 in D \ {p} by A5,A7;
    reconsider Dp = D \ {p} as non empty Subset of TOP-REAL 2 by A5,A7;
    (TOP-REAL 2) | Dp, I(01) are_homeomorphic by A7,Th77;
    then consider f being Function of (TOP-REAL 2) | Dp, I(01) such that
A9: f is being_homeomorphism by T_0TOPSP:def 1;
A10: C c= the carrier of (TOP-REAL 2) | Dp by A7,PRE_TOPC:29;
    reconsider C' = C as Subset of (TOP-REAL 2) | Dp by A7,PRE_TOPC:29;
    set fC = f.:C';
    C c= [#] ((TOP-REAL 2) | Dp) by A7,PRE_TOPC:29;
    then
A11: C' is compact by COMPTS_1:11;
A12: f is continuous by A9,TOPS_2:def 5;
A13: rng f = [#] I(01) by A9,TOPS_2:def 5;
    then reconsider fC as compact Subset of I(01) by A11,A12,COMPTS_1:24;
    reconsider fC' = fC as Subset of I[01] by PRE_TOPC:39;
A14: fC' c= the carrier of I(01);
A15: fC' c= [#] I(01);
A16: fC' c= ].0,1.[ by A14,Def1;
A17: for P being Subset of I(01) st P=fC' holds P is compact;
A18: d1 in the carrier of (TOP-REAL 2) | Dp by A8,PRE_TOPC:29;
A19: f.d1 in f.:C' by A5,FUNCT_2:43;
A20: d2 in the carrier of (TOP-REAL 2) | Dp by A8,PRE_TOPC:29;
A21: f.d2 in f.:C' by A5,FUNCT_2:43;
    then reconsider fC' as non empty compact
    Subset of I[01] by A15,A17,COMPTS_1:11;
A22: d1 in dom f by A18,FUNCT_2:def 1;
A23: d2 in dom f by A20,FUNCT_2:def 1;
    consider p1, p2 being Point of I[01] such that
A24: p1 <= p2 & fC' c= [. p1,p2 .] & [.p1,p2.] c= ].0,1.[ by A16,Th83;
    reconsider E = [.p1,p2.] as non empty compact connected Subset of I[01]
    by A24,Th49;
A25: f is one-to-one by A9,TOPS_2:def 5;
    p1 <> p2
    proof
      assume p1 = p2;
      then
A26:  fC' c= {p1} by A24,XXREAL_1:17;
      then
A27:  f.d1 = p1 by A19,TARSKI:def 1;
      f.d2 = p1 by A21,A26,TARSKI:def 1;
      hence thesis by A5,A22,A23,A25,A27,FUNCT_1:def 8;
    end;
    then
A28: p1 < p2 by A24,XXREAL_0:1;
A29: rng f = ].0,1.[ by A13,Def1;
A30: E c= rng f by A13,A24,Def1;
A31: f " E c= dom f by RELAT_1:167;
    dom f = the carrier of (TOP-REAL 2) | Dp by FUNCT_2:def 1;
    then dom f c= the carrier of TOP-REAL 2 by BORSUK_1:35;
    then reconsider B = f " E as non empty Subset of TOP-REAL 2
    by A30,A31,RELAT_1:174,XBOOLE_1:1;
    dom f = the carrier of (TOP-REAL 2) | Dp by FUNCT_2:def 1;
    then
A32: dom f = Dp by PRE_TOPC:29;
A33: Dp c= D by XBOOLE_1:36;
    f.: (f"E) = E by A24,A29,FUNCT_1:147;
    then consider s1, s2 being Point of TOP-REAL 2 such that
A34: B is_an_arc_of s1,s2 by A9,A28,A31,A32,Th80;
    take s1, s2, B;
    thus B is_an_arc_of s1,s2 by A34;
    C c= dom f by A10,FUNCT_2:def 1;
    hence thesis by A24,A31,A32,A33,FUNCT_1:163,XBOOLE_1:1;
  end;
  per cases;
  suppose
A35: C is trivial;
    per cases;
    suppose
A36:  C = {};
      consider p,q being Point of TOP-REAL 2 such that
A37:  p <> q & p in D & q in D by TOPREAL2:4;
      reconsider CC = {p,q} as Subset of TOP-REAL 2;
      p in CC & q in CC by TARSKI:def 2;
      then
A38:  CC is non trivial by A37,REALSET1:14;
      reconsider pp = {p}, qq = {q} as Subset of TOP-REAL 2;
      CC = pp \/ qq by ENUMSET1:41;
      then
A39:  CC is compact by COMPTS_1:19;
A40:  CC c= D by A37,ZFMISC_1:38;
      CC <> D
      proof
        assume CC = D;
        then
A41:    D \ CC = {} by XBOOLE_1:37;
        not {}((TOP-REAL 2)|D) is connected by A37,A41,JORDAN6:60;
        hence thesis;
      end;
      then CC c< D by A40,XBOOLE_0:def 8;
      then consider p1,p2 being Point of TOP-REAL 2,
      B being Subset of TOP-REAL 2 such that
A42:  B is_an_arc_of p1,p2 & CC c= B & B c= D by A4,A38,A39;
      take p1, p2, B;
      thus B is_an_arc_of p1,p2 by A42;
      thus C c= B by A36,XBOOLE_1:2;
      thus thesis by A42;
    end;
    suppose C <> {};
      then consider x being Element of TOP-REAL 2 such that
A43:  C = {x} by A35,Th2;
A44:  x in C by A43,TARSKI:def 1;
      consider y being Element of D such that
A45:  x <> y by Th3;
      reconsider y' = y as Element of TOP-REAL 2;
      reconsider Y = {y'} as compact non empty Subset of TOP-REAL 2;
      reconsider Cy = C \/ Y as
      non empty compact Subset of TOP-REAL 2 by A2,COMPTS_1:19;
A46:  C c= Cy by XBOOLE_1:7;
      y in Y by TARSKI:def 1;
      then y in Cy by XBOOLE_0:def 3;
      then
A47:  Cy is non trivial by A44,A45,A46,REALSET1:14;
      {y} c= D;
      then
A48:  Cy c= D by X,XBOOLE_1:8;
      Cy <> D
      proof
        assume Cy = D;
        then
A49:    D \ Cy = {} by XBOOLE_1:37;
        Cy = {x,y} by A43,ENUMSET1:41;
        then not {}((TOP-REAL 2)|D) is connected by X,A44,A45,A49,JORDAN6:60;
        hence thesis;
      end;
      then Cy c< D by A48,XBOOLE_0:def 8;
      then consider p1,p2 being Point of TOP-REAL 2,
      B being Subset of TOP-REAL 2 such that
A50:  B is_an_arc_of p1,p2 & Cy c= B & B c= D by A4,A47;
      take p1, p2, B;
      thus B is_an_arc_of p1,p2 by A50;
      thus C c= B by A46,A50,XBOOLE_1:1;
      thus thesis by A50;
    end;
  end;
  suppose C is non trivial;
    hence thesis by A1,A2,A4;
  end;
end;

