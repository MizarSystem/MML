:: Regular Expression Quantifiers -- $m$ to $n$ Occurrences
::  by Micha{\l} Trybulec
::
:: Received June 6, 2007
:: Copyright (c) 2007 Association of Mizar Users

environ

 vocabularies BOOLE, FINSEQ_1, TARSKI, ALGSEQ_1, ARYTM_1, AFINSQ_1, MEASURE6,
      GROUP_1, ARYTM, SETFAM_1, MODAL_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, NUMBERS, XCMPLX_0, NAT_1, ORDINAL1,
      SETFAM_1, XREAL_0, XXREAL_0, AFINSQ_1, CATALAN2, FLANG_1;
 constructors XXREAL_0, NAT_1, XREAL_0, FLANG_1;
 registrations SUBSET_1, NAT_1, ORDINAL1, AFINSQ_1, XREAL_0, XBOOLE_0,
      XXREAL_0;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 definitions TARSKI;
 theorems AFINSQ_1, NAT_1, SUBSET_1, TARSKI, XBOOLE_0, XBOOLE_1,
      XREAL_1, ZFMISC_1, FLANG_1, XXREAL_0;
 schemes CARD_FIL, DOMAIN_1, NAT_1;

begin

reserve E, x, y, X, Y, Z for set;
reserve A, B, C, D for Subset of E^omega;
reserve a, a1, a2, b, c, d for Element of E^omega;
reserve e for Element of E;
reserve i, j, k, l, kl, m, n, mn, n1, n2 for Nat;
reserve p, q, r, r1, r2 for real number;

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: Preliminaries - Numbers:
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

theorem Th1:
  m + k <= i & i <= n + k implies ex mn st mn + k = i & m <= mn & mn <= n
proof
  assume
A1: m + k <= i & i <= n + k;
  then m + k <= m + i by XREAL_1:40;
  then k <= i by XREAL_1:8;
  then reconsider mn = i - k as Nat by NAT_1:21;
  take mn;
  thus mn + k = i;
  m + k <= mn + k & mn + k <= n + k by A1;
  hence m <= mn & mn <= n by XREAL_1:8;
end;

theorem Th2:
  m <= n & k <= l & m + k <= i & i <= n + l implies
  ex mn, kl st mn + kl = i & m <= mn & mn <= n & k <= kl & kl <= l
proof
  assume
A1: m <= n & k <= l & m + k <= i & i <= n + l;
  per cases;
  suppose i <= n + k;
    then consider mn such that
A2: mn + k = i & m <= mn & mn <= n by A1,Th1;
    take mn, k;
    thus mn + k = i & m <= mn & mn <= n by A2;
    thus k <= k & k <= l by A1;
  end;
  suppose i > n + k;
    then consider kl such that
A3: kl + n = i & k <= kl & kl <= l by A1,Th1;
    take n, kl;
    thus n + kl = i & m <= n & n <= n by A1,A3;
    thus k <= kl & kl <= l by A3;
  end;
end;

theorem Th3:
  m < n implies ex k st m + k = n & k > 0
proof
  assume
A1: m < n;
  then consider k such that
A2: m + k = n by NAT_1:10;
  m - m < n - m by A1,XREAL_1:16;
  hence thesis by A2;
end;

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: Preliminaries - Sequences:
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

theorem Th4:
  a ^ b = a or b ^ a = a implies b = {}
proof
  assume
A1: a ^ b = a or b ^ a = a;
  per cases by A1;
  suppose a ^ b = a;
    then len(a ^ b) = len(a) + len(b) & len(a ^ b) = len(a) by AFINSQ_1:20;
    hence thesis by AFINSQ_1:18;
  end;
  suppose b ^ a = a;
    then len(b ^ a) = len(b) + len(a) & len(b ^ a) = len(a) by AFINSQ_1:20;
    hence thesis by AFINSQ_1:18;
  end;
end;

begin

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: Addenda - FLANG_1:
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

theorem
  (x in A or x in B) & x <> <%>E implies A ^^ B <> {<%>E}
proof
  assume (x in A or x in B) & x <> <%>E;
  then A <> {<%>E} or B <> {<%>E} by TARSKI:def 1;
  hence thesis by FLANG_1:15;
end;

theorem
  <%x%> in A ^^ B iff <%>E in A & <%x%> in B or <%x%> in A & <%>E in B
proof
  thus <%x%> in A ^^ B implies <%>E in A & <%x%> in B or <%x%> in A & <%>E in
  B
  proof
    assume <%x%> in A ^^ B;
    then consider a, b such that
A1: a in A & b in B & <%x%> = a ^ b by FLANG_1:def 1;
    thus thesis by A1,FLANG_1:4;
  end;
  assume
A2: <%>E in A & <%x%> in B or <%x%> in A & <%>E in B;
  <%>E ^ <%x%> = <%x%> & <%x%> ^ <%>E = <%x%> by AFINSQ_1:32;
  hence thesis by A2,FLANG_1:def 1;
end;

theorem Th7:
  x in A & x <> <%>E & n > 0 implies A |^ n <> {<%>E}
proof
  assume that
A1: x in A & x <> <%>E and
A2: n > 0;
  A <> {<%>E} by A1,TARSKI:def 1;
  hence thesis by A2,FLANG_1:30;
end;

theorem
  <%>E in A |^ n iff n = 0 or <%>E in A
proof
  thus <%>E in A |^ n implies n = 0 or <%>E in A by FLANG_1:32;
  assume
A3: n = 0 or <%>E in A;
  per cases by A3;
  suppose n = 0;
    then A |^ n = {<%>E} by FLANG_1:30;
    hence thesis by ZFMISC_1:37;
  end;
  suppose <%>E in A;
    hence thesis by FLANG_1:31;
  end;
end;

theorem Th9:
  <%x%> in A |^ n iff <%x%> in A & ((<%>E in A & n > 1) or n = 1)
proof
  thus <%x%> in A |^ n implies <%x%> in A & ((<%>E in A & n > 1) or n = 1)
  proof
    assume
A1: <%x%> in A |^ n;
    A |^ n c= A* by FLANG_1:43;
    hence <%x%> in A by A1,FLANG_1:73;
    assume
A2: (not <%>E in A or n <= 1) & n <> 1;
    per cases by A2;
    suppose
A3:   not <%>E in A & n <> 1;
      per cases by A3,NAT_1:26;
      suppose n = 0;
        then A |^ n = {<%>E} by FLANG_1:25;
        hence contradiction by A1,TARSKI:def 1;
      end;
      suppose
A4:     n > 1;
        then consider m such that
A5:     m + 1 = n by NAT_1:6;
        <%x%> in (A |^ m) ^^ A by A1,A5,FLANG_1:24;
        then consider a, b such that
A6:     a in (A |^ m) & b in A & <%x%> = a ^ b by FLANG_1:def 1;
        per cases by A6,FLANG_1:4;
        suppose
A7:       a = <%>E & b = <%x%>;
          m + 1 > 0 + 1 by A4,A5;
          then m > 0;
          hence contradiction by A3,A6,A7,FLANG_1:32;
        end;
        suppose b = <%>E & a = <%x%>;
          hence contradiction by A3,A6;
        end;
      end;
    end;
    suppose n <= 1 & n <> 1;
      then n = 0 by NAT_1:26;
      then A |^ n = {<%>E} by FLANG_1:25;
      hence contradiction by A1,TARSKI:def 1;
    end;
  end;
  assume that
A8: <%x%> in A and
A9: (<%>E in A & n > 1) or n = 1;
  per cases by A9;
  suppose <%>E in A & n > 1;
    then A c= A |^ n by FLANG_1:36;
    hence thesis by A8;
  end;
  suppose n = 1;
    hence thesis by A8,FLANG_1:26;
  end;
end;

theorem Th10:
  m <> n & A |^ m = {x} & A |^ n = {x} implies x = <%>E
proof
  assume
A1: m <> n & A |^ m = {x} & A |^ n = {x};
  then
A2: x in A |^ m & x in A |^ n by TARSKI:def 1;
  per cases by A1,XXREAL_0:1;
  suppose m > n;
    then consider k such that
A3: n + k = m & k > 0 by Th3;
    (A |^ n) ^^ (A |^ k) = A |^ m by A3,FLANG_1:34;
    then consider a, b such that
A4: a in A |^ n & b in A |^ k & x = a ^ b by A2,FLANG_1:def 1;
    a = x by A1,A4,TARSKI:def 1;
    then b = <%>E by A4,Th4;
    then <%>E in A by A3,A4,FLANG_1:32;
    then <%>E in A |^ m & <%>E in A |^ n by FLANG_1:31;
    hence thesis by A1,TARSKI:def 1;
  end;
  suppose m < n;
    then consider k such that
A5: m + k = n & k > 0 by Th3;
    (A |^ m) ^^ (A |^ k) = A |^ n by A5,FLANG_1:34;
    then consider a, b such that
A6: a in A |^ m & b in A |^ k & x = a ^ b by A2,FLANG_1:def 1;
    a = x by A1,A6,TARSKI:def 1;
    then b = <%>E by A6,Th4;
    then <%>E in A by A5,A6,FLANG_1:32;
    then <%>E in A |^ m & <%>E in A |^ n by FLANG_1:31;
    hence thesis by A1,TARSKI:def 1;
  end;
end;

theorem
  (A |^ m) |^ n = (A |^ n) |^ m
proof
  thus (A |^ m) |^ n = A |^ (m * n) by FLANG_1:35
    .= (A |^ n) |^ m by FLANG_1:35;
end;

theorem Th12:
  (A |^ m) ^^ (A |^ n) = (A |^ n) ^^ (A |^ m)
proof
  thus (A |^ m) ^^ (A |^ n) = A |^ (m + n) by FLANG_1:34
    .= (A |^ n) ^^ (A |^ m) by FLANG_1:34;
end;

theorem
  <%>E in B implies A c= A ^^ (B |^ l) & A c= (B |^ l) ^^ A
proof
  assume <%>E in B;
  then <%>E in B |^ l by FLANG_1:31;
  hence thesis by FLANG_1:17;
end;

theorem
  A c= C |^ k & B c= C |^ l implies A ^^ B c= C |^ (k + l)
proof
  assume A c= C |^ k & B c= C |^ l;
  then A ^^ B c= (C |^ k) ^^ (C |^ l) by FLANG_1:18;
  hence thesis by FLANG_1:34;
end;

theorem
  x in A & x <> <%>E implies A* <> {<%>E}
proof
  assume x in A & x <> <%>E;
  then A <> {} & A <> {<%>E} by TARSKI:def 1;
  hence thesis by FLANG_1:48;
end;

theorem Th16:
  <%>E in A & n > 0 implies (A |^ n)* = A*
proof
  assume <%>E in A & n > 0;
  then
A1: A* c= (A |^ n)* by FLANG_1:36,62;
  (A |^ n)* c= A* by FLANG_1:65;
  hence thesis by A1,XBOOLE_0:def 10;
end;

theorem Th17:
  <%>E in A implies (A |^ n)* = (A*) |^ n
proof
  assume
A1: <%>E in A;
  per cases;
  suppose
A2: n = 0;
    hence (A |^ n)* = {<%>E}* by FLANG_1:25
      .= {<%>E} by FLANG_1:48
      .= (A*) |^ n by A2,FLANG_1:25;
  end;
  suppose
A3: n > 0;
    then (A*) |^ n = A* by FLANG_1:67;
    hence thesis by A1,A3,Th16;
  end;
end;

theorem
  A c= A ^^ (B*) & A c= (B*) ^^ A
proof
  <%>E in B* by FLANG_1:49;
  hence thesis by FLANG_1:17;
end;

begin

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: Union of a Range of Powers
:: Definition of |^ (n, m)
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

definition
  let E, A;
  let m, n;
  func A |^ (m, n) -> Subset of E^omega equals
  union { B: ex k st m <= k & k <= n & B = A |^ k };
  coherence
  proof
    defpred P[set] means ex k st m <= k & k <= n & $1 = A |^ k;
    reconsider M = { B: P[B] } as Subset-Family of E^omega from DOMAIN_1:sch 7;
    union M is Subset of E^omega;
    hence thesis;
  end;
end;

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: Union of a Range of Powers
:: Properties of |^ (n, m)
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

theorem Th19:
  x in A |^ (m, n) iff ex k st m <= k & k <= n & x in A |^ k
proof
  thus x in A |^ (m, n) implies ex k st m <= k & k <= n & x in A |^ k
  proof
    assume x in A |^ (m, n);
    then consider X such that
A1: x in X and
A2: X in { B: ex k st m <= k & k <= n & B = A |^ k } by TARSKI:def 4;
    defpred P[set] means ex k st m <= k & k <= n & $1 = A |^ k;
A3: X in { B: P[B] } by A2;
    P[X] from CARD_FIL:sch 1(A3);
    hence thesis by A1;
  end;
  given k such that
A4: m <= k & k <= n & x in A |^ k;
  defpred P[set] means ex k st m <= k & k <= n & $1 = A |^ k;
  consider B such that
A5: x in B & P[B] by A4;
  reconsider A = { C: P[C] } as Subset-Family of E^omega from DOMAIN_1:sch 7;
  B in A by A5;
  hence thesis by A5,TARSKI:def 4;
end;

theorem Th20:
  m <= k & k <= n implies A |^ k c= A |^ (m, n)
proof
  assume m <= k & k <= n;
  then for x holds x in A |^ k implies x in A |^ (m, n) by Th19;
  hence thesis by TARSKI:def 3;
end;

theorem Th21:
  A |^ (m, n) = {} iff m > n or (m > 0 & A = {})
proof
  thus A |^ (m, n) = {} implies m > n or (m > 0 & A = {})
  proof
    assume that
A1: A |^ (m, n) = {} and
A2: m <= n & (m <= 0 or A <> {});
A3: now
      assume
A4:   m <= n & m = 0;
      then {<%>E} = A |^ m by FLANG_1:30;
      then <%>E in A |^ m by TARSKI:def 1;
      hence contradiction by A1,A4,Th19;
    end;
    now
      assume
A5:   m <= n & A <> {};
      then A |^ m <> {} by FLANG_1:28;
      then ex a st a in A |^ m by SUBSET_1:10;
      hence contradiction by A1,A5,Th19;
    end;
    hence thesis by A2,A3;
  end;
A6: now
    assume
A7: m > n;
    now
      let x;
      not (ex k st m <= k & k <= n & x in A |^ k) by A7,XXREAL_0:2;
      hence not x in A |^ (m, n) by Th19;
    end;
    hence A |^ (m, n) = {} by XBOOLE_0:def 1;
  end;
  now
    assume
A8: m > 0 & A = {};
    now
      let x;
      assume x in A |^ (m, n);
      then consider k such that
A9:   m <= k & k <= n & x in A |^ k by Th19;
      thus contradiction by A8,A9,FLANG_1:28;
    end;
    hence A |^ (m, n) = {} by XBOOLE_0:def 1;
  end;
  hence thesis by A6;
end;

theorem Th22:
  A |^ (m, m) = A |^ m
proof
A1: now
    let x;
    assume x in A |^ (m, m);
    then consider k such that
A2: m <= k & k <= m & x in A |^ k by Th19;
    thus x in A |^ m by A2,XXREAL_0:1;
  end;
  for x holds x in A |^ m implies x in A |^ (m, m) by Th19;
  hence thesis by A1,TARSKI:2;
end;

theorem Th23:
  m <= k & l <= n implies A |^ (k, l) c= A |^ (m, n)
proof
  assume
A1: m <= k & l <= n;
  thus thesis
  proof
    let x;
    assume x in A |^ (k, l);
    then consider kl such that
A2: k <= kl & kl <= l & x in A |^ kl by Th19;
    m <= kl & kl <= n by A1,A2,XXREAL_0:2;
    hence x in A |^ (m, n) by A2,Th19;
  end;
end;

theorem
  m <= k & k <= n implies A |^ (m, n) = A |^ (m, k) \/ A |^ (k, n)
proof
  assume
A1: m <= k & k <= n;
A2: A |^ (m, n) c= A |^ (m, k) \/ A |^ (k, n)
  proof
    let x;
    assume x in A |^ (m, n);
    then consider l such that
A3: m <= l & l <= n & x in A |^ l by Th19;
    l <= k or l > k;
    then x in A |^ (m, k) or x in A |^ (k, n) by A3,Th19;
    hence x in A |^ (m, k) \/ A |^ (k, n) by XBOOLE_0:def 3;
  end;
  A |^ (m, k) \/ A |^ (k, n) c= A |^ (m, n)
  proof
    A |^ (m, k) c= A |^ (m, n) & A |^ (k, n) c= A |^ (m, n) by A1,Th23;
    hence thesis by XBOOLE_1:8;
  end;
  hence thesis by A2,XBOOLE_0:def 10;
end;

theorem Th25:
  m <= k & k <= n implies A |^ (m, n) = A |^ (m, k) \/ A |^ (k + 1, n)
proof
  assume
A1: m <= k & k <= n;
  per cases;
  suppose k < n;
    then m <= k & m <= k + 1 & k <= n & k + 1 <= n by A1,NAT_1:13;
    then A |^ (m, k) c= A |^ (m, n) & A |^ (k + 1, n) c= A |^ (m, n) by Th23;
    then
A2: A |^ (m, k) \/ A |^ (k + 1, n) c= A |^ (m, n) by XBOOLE_1:8;
    A |^ (m, n) c= A |^ (m, k) \/ A |^ (k + 1, n)
    proof
      let x;
      assume x in A |^ (m, n);
      then consider mn such that
A3:   m <= mn & mn <= n & x in A |^ mn by Th19;
A4:   mn <= k implies x in A |^ (m, k) by A3,Th19;
      mn >= k + 1 implies x in A |^ (k + 1, n) by A3,Th19;
      hence x in A |^ (m, k) \/ A |^ (k + 1, n) by A4,NAT_1:13,XBOOLE_0:def 3;
    end;
    hence thesis by A2,XBOOLE_0:def 10;
  end;
  suppose
A5: k >= n;
    then k + 1 > n + 0 by XREAL_1:10;
    then A |^ (k + 1, n) = {} by Th21;
    hence thesis by A1,A5,XXREAL_0:1;
  end;
end;

theorem Th26:
  m <= n + 1 implies A |^ (m, n + 1) = A |^ (m, n) \/ (A |^ (n + 1))
proof
  assume
A1: m <= n + 1;
  per cases by A1,NAT_1:8;
  suppose m <= n;
    then m <= n & n < n + 1 by NAT_1:13;
    then A |^ (m, n + 1) = A |^ (m, n) \/ A |^ (n + 1, n + 1) by Th25;
    hence thesis by Th22;
  end;
  suppose
A2: m = n + 1;
    then
A3: m > n by NAT_1:13;
    thus A |^ (m, n + 1) = {} \/ A |^ (n + 1) by A2,Th22
      .= A |^ (m, n) \/ A |^ (n + 1) by A3,Th21;
  end;
end;

theorem
  m <= n implies A |^ (m, n) = A |^ m \/ A |^ (m + 1, n)
proof
  assume
A1: m <= n;
  per cases by A1,XXREAL_0:1;
  suppose m < n;
    then A |^ (m, n) = A |^ (m, m) \/ A |^ (m + 1, n) by Th25;
    hence thesis by Th22;
  end;
  suppose
A2: m = n;
    then
A3: m + 1 > n by NAT_1:13;
    thus A |^ (m, n) = A |^ m \/ {} by A2,Th22
      .= A |^ m \/ A |^ (m + 1, n) by A3,Th21;
  end;
end;

theorem Th28:
  A |^ (n, n + 1) = A |^ n \/ A |^ (n + 1)
proof
  thus A |^ (n, n + 1) = A |^ (n, n) \/ A |^ (n + 1) by Th26,NAT_1:11
    .= A |^ n \/ A |^ (n + 1) by Th22;
end;

theorem Th29:
  A c= B implies A |^ (m, n) c= B |^ (m, n)
proof
  assume
A1: A c= B;
  thus thesis
  proof
    let x;
    assume x in A |^ (m, n);
    then consider k such that
A2: m <= k & k <= n & x in A |^ k by Th19;
    A |^ k c= B |^ k by A1,FLANG_1:38;
    hence x in B |^ (m, n) by A2,Th19;
  end;
end;

theorem Th30:
  x in A & x <> <%>E & (m > 0 or n > 0) implies A |^ (m, n) <> {<%>E}
proof
  assume that
A1: x in A & x <> <%>E and
A2: (m > 0 or n > 0);
  per cases by A2;
  suppose m > n;
    hence thesis by Th21;
  end;
  suppose
A3: m <= n & m > 0;
    then
A4: A |^ m <> {<%>E} by A1,Th7;
    A |^ m <> {} by A1,FLANG_1:28;
    then consider x such that
A5: x in A |^ m & x <> <%>E by A4,ZFMISC_1:41;
    A |^ m c= A |^ (m, n) by A3,Th20;
    hence thesis by A5,TARSKI:def 1;
  end;
  suppose
A6: m <= n & n > 0;
    then
A7: A |^ n <> {<%>E} by A1,Th7;
    A |^ n <> {} by A1,FLANG_1:28;
    then consider x such that
A8: x in A |^ n & x <> <%>E by A7,ZFMISC_1:41;
    A |^ n c= A |^ (m, n) by A6,Th20;
    hence thesis by A8,TARSKI:def 1;
  end;
end;

theorem Th31:
  A |^ (m, n) = {<%>E} iff
  (m <= n & A = {<%>E}) or (m = 0 & n = 0) or (m = 0 & A = {})
proof
  thus A |^ (m, n) = {<%>E} implies
  (m <= n & A = {<%>E}) or (m = 0 & n = 0) or (m = 0 & A = {})
  proof
    assume that
A1: A |^ (m, n) = {<%>E} and
A2: (m > n or A <> {<%>E}) & (m <> 0 or n <> 0) & (m <> 0 or A <> {});
    per cases by A2;
    suppose m > n;
      hence contradiction by A1,Th21;
    end;
    suppose
A3:   m <= n & A <> {<%>E} & (m <> 0 or (n <> 0 & A <> {}));
      per cases by A3;
      suppose
A4:     m <> 0;
        per cases;
        suppose
A5:       A = {};
          m > 0 by A4;
          hence contradiction by A1,A5,Th21;
        end;
        suppose A <> {};
          then consider x such that
A6:       x in A & x <> <%>E by A3,ZFMISC_1:41;
          m > 0 by A4;
          hence contradiction by A1,A6,Th30;
        end;
      end;
      suppose
A7:     n <> 0 & A <> {};
        then consider x such that
A8:     x in A & x <> <%>E by A3,ZFMISC_1:41;
        n > 0 by A7;
        hence contradiction by A1,A8,Th30;
      end;
    end;
  end;
  assume
A9: (m <= n & A = {<%>E}) or (m = 0 & n = 0) or (m = 0 & A = {});
  per cases by A9;
  suppose
A10: m <= n & A = {<%>E};
A11: now
      let x;
      assume x in A |^ (m, n);
      then consider k such that
A12:  m <= k & k <= n & x in {<%>E} |^ k by A10,Th19;
      thus x in {<%>E} by A12,FLANG_1:30;
    end;
    now
      let x;
      assume x in {<%>E};
      then
A13:  x in {<%>E} |^ m by FLANG_1:30;
      {<%>E} |^ m c= {<%>E} |^ (m, n) by A10,Th20;
      hence x in A |^ (m, n) by A10,A13;
    end;
    hence thesis by A11,TARSKI:2;
  end;
  suppose
A14: m = 0 & n = 0;
    A |^ (0, 0) = A |^ 0 by Th22
      .= {<%>E} by FLANG_1:30;
    hence thesis by A14;
  end;
  suppose
A15: m = 0 & A = {};
A16: A |^ (1, n) = {}
    proof
      now
        let x;
        assume x in A |^ (1, n);
        then consider k such that
A17:    1 <= k & k <= n & x in A |^ k by Th19;
        thus contradiction by A15,A17,FLANG_1:28;
      end;
      hence thesis by XBOOLE_0:def 1;
    end;
    A |^ (0, n) = A |^ (0, 0) \/ A |^ (0 + 1, n) by Th25
      .= A |^ 0 \/ A |^ (0 + 1, n) by Th22
      .= {<%>E} by A16,FLANG_1:30;
    hence thesis by A15;
  end;
end;

theorem Th32:
  A |^ (m, n) c= A*
proof
  let x;
  assume x in A |^ (m, n);
  then consider k such that
A1: m <= k & k <= n & x in A |^ k by Th19;
  thus x in A* by A1,FLANG_1:42;
end;

theorem Th33:
  <%>E in A |^ (m, n) iff m = 0 or (m <= n & <%>E in A)
proof
  thus <%>E in A |^ (m, n) implies m = 0 or (m <= n & <%>E in A)
  proof
    assume that
A1: <%>E in A |^ (m, n) and
A2: m <> 0 & (m > n or not <%>E in A);
    per cases by A2;
    suppose m <> 0 & m > n;
      hence contradiction by A1,Th21;
    end;
    suppose
A3:   m <> 0 & not <%>E in A;
      consider k such that
A4:   m <= k & k <= n & <%>E in A |^ k by A1,Th19;
      k > 0 by A3,A4;
      hence contradiction by A3,A4,FLANG_1:32;
    end;
  end;
  assume
A5: m = 0 or (m <= n & <%>E in A);
  per cases by A5;
  suppose
A6: m = 0;
    {<%>E} = A |^ 0 by FLANG_1:30;
    then {<%>E} c= A |^ (0, n) & <%>E in {<%>E} by Th20,TARSKI:def 1;
    hence <%>E in A |^ (m, n) by A6;
  end;
  suppose
A7: m <= n & <%>E in A;
    then
A8: <%>E in A |^ m by FLANG_1:31;
    A |^ m c= A |^ (m, n) by A7,Th20;
    hence <%>E in A |^ (m, n) by A8;
  end;
end;

theorem Th34:
  <%>E in A & m <= n implies A |^ (m, n) = A |^ n
proof
  assume
A1: <%>E in A & m <= n;
A2: A |^ (m, n) c= A |^ n
  proof
A3: now
      let k such that
A4:   k <= n;
      per cases by A4,XXREAL_0:1;
      suppose k < n;
        hence A |^ k c= A |^ n by A1,FLANG_1:37;
      end;
      suppose k = n;
        hence A |^ k c= A |^ n;
      end;
    end;
    let x such that
A5: x in A |^ (m, n);
    consider k such that
A6: m <= k & k <= n & x in A |^ k by A5,Th19;
    A |^ k c= A |^ n by A3,A6;
    hence x in A |^ n by A6;
  end;
  A |^ n c= A |^ (m, n) by A1,Th20;
  hence thesis by A2,XBOOLE_0:def 10;
end;

theorem Th35:
  A |^ (m, n) ^^ (A |^ k) = (A |^ k) ^^ (A |^ (m, n))
proof
A1: now
    let x;
    assume x in (A |^ (m, n)) ^^ (A |^ k);
    then consider a, b such that
A2: a in A |^ (m, n) & b in (A |^ k) & x = a ^ b by FLANG_1:def 1;
    consider mn such that
A3: m <= mn & mn <= n & a in A |^ mn by A2,Th19;
    a ^ b in (A |^ mn) ^^ (A |^ k) by A2,A3,FLANG_1:def 1;
    then
A4: a ^ b in (A |^ k) ^^ (A |^ mn) by Th12;
    A |^ mn c= A |^ (m, n) by A3,Th20;
    then (A |^ k) ^^ (A |^ mn) c= (A |^ k) ^^ (A |^ (m, n)) by FLANG_1:18;
    hence x in (A |^ k) ^^ (A |^ (m, n)) by A2,A4;
  end;
  now
    let x;
    assume x in (A |^ k) ^^ (A |^ (m, n));
    then consider a, b such that
A5: a in (A |^ k) & b in A |^ (m, n) & x = a ^ b by FLANG_1:def 1;
    consider mn such that
A6: m <= mn & mn <= n & b in A |^ mn by A5,Th19;
    a ^ b in (A |^ k) ^^ (A |^ mn) by A5,A6,FLANG_1:def 1;
    then
A7: a ^ b in (A |^ mn) ^^ (A |^ k) by Th12;
    A |^ mn c= A |^ (m, n) by A6,Th20;
    then (A |^ mn) ^^ (A |^ k) c= (A |^ (m, n)) ^^ (A |^ k) by FLANG_1:18;
    hence x in (A |^ (m, n)) ^^ (A |^ k) by A5,A7;
  end;
  hence thesis by A1,TARSKI:2;
end;

theorem Th36:
  (A |^ (m, n)) ^^ A = A ^^ (A |^ (m, n))
proof
  thus (A |^ (m, n)) ^^ A = (A |^ (m, n)) ^^ (A |^ 1) by FLANG_1:26
    .= (A |^ 1) ^^ (A |^ (m, n)) by Th35
    .= A ^^ (A |^ (m, n)) by FLANG_1:26;
end;

theorem Th37:
  m <= n & k <= l implies (A |^ (m, n)) ^^ (A |^ (k, l)) = A |^ (m + k, n + l)
proof
  assume
A1: m <= n & k <= l;
A2: now
    let x;
    assume x in (A |^ (m, n)) ^^ (A |^ (k, l));
    then consider a, b such that
A3: a in A |^ (m, n) & b in A |^ (k, l) & x = a ^ b by FLANG_1:def 1;
    consider mn such that
A4: m <= mn & mn <= n & a in A |^ mn by A3,Th19;
    consider kl such that
A5: k <= kl & kl <= l & b in A |^ kl by A3,Th19;
A6: a ^ b in A |^ (mn + kl) by A4,A5,FLANG_1:41;
A7: m + k <= mn + kl by A4,A5,XREAL_1:9;
    mn + kl <= n + l by A4,A5,XREAL_1:9;
    hence x in A |^ (m + k, n + l) by A3,A6,A7,Th19;
  end;
  now
    let x;
    assume x in A |^ (m + k, n + l);
    then consider i such that
A8: m + k <= i & i <= n + l & x in A |^ i by Th19;
    consider mn, kl such that
A9: mn + kl = i & m <= mn & mn <= n & k <= kl & kl <= l by A1,A8,Th2;
A10: A |^ mn c= A |^ (m, n) by A9,Th20;
    A |^ kl c= A |^ (k, l) by A9,Th20;
    then (A |^ mn) ^^ (A |^ kl) c=
    (A |^ (m, n)) ^^ (A |^ (k, l)) by A10,FLANG_1:18;
    then (A |^ (mn + kl)) c= (A |^ (m, n)) ^^ (A |^ (k, l)) by FLANG_1:34;
    hence x in (A |^ (m, n)) ^^ (A |^ (k, l)) by A8,A9;
  end;
  hence thesis by A2,TARSKI:2;
end;

theorem Th38:
  A |^ (m + 1, n + 1) = (A |^ (m, n)) ^^ A
proof
  per cases;
  suppose m <= n;
    hence A |^ (m + 1, n + 1) = (A |^ (m, n)) ^^ (A |^ (1, 1)) by Th37
      .= (A |^ (m, n)) ^^ (A |^ 1) by Th22
      .= (A |^ (m, n)) ^^ A by FLANG_1:26;
  end;
  suppose
A1: m > n;
    then A |^ (m, n) = {} by Th21;
    then
A2: (A |^ (m, n)) ^^ A = {} by FLANG_1:13;
    m + 1 > n + 1 by A1,XREAL_1:10;
    hence A |^ (m + 1, n + 1) = (A |^ (m, n)) ^^ A by A2,Th21;
  end;
end;

theorem Th39:
  (A |^ (m, n)) ^^ (A |^ (k, l)) = (A |^ (k, l)) ^^ (A |^ (m, n))
proof
  per cases;
  suppose
A1: m <= n & k <= l;
    hence (A |^ (m, n)) ^^ (A |^ (k, l)) = A |^ (m + k, n + l) by Th37
      .= (A |^ (k, l)) ^^ (A |^ (m, n)) by A1,Th37;
  end;
  suppose m > n;
    then A |^ (m, n) = {} by Th21;
    then (A |^ (m, n)) ^^ (A |^ (k, l)) = {} &
    (A |^ (k, l)) ^^ (A |^ (m, n)) = {} by FLANG_1:13;
    hence thesis;
  end;
  suppose k > l;
    then A |^ (k, l) = {} by Th21;
    then (A |^ (m, n)) ^^ (A |^ (k, l)) = {} &
    (A |^ (k, l)) ^^ (A |^ (m, n)) = {} by FLANG_1:13;
    hence thesis;
  end;
end;

theorem Th40:
  (A |^ (m, n)) |^ k = A |^ (m * k, n * k)
proof
  per cases;
  suppose
A1: m <= n;
    defpred P[Nat] means (A |^ (m, n)) |^ $1 = A |^ (m * $1, n * $1);
A2: P[0]
    proof
      thus (A |^ (m, n)) |^ 0 = {<%>E} by FLANG_1:25
        .= A |^ 0 by FLANG_1:25
        .= A |^ (m * 0, n * 0) by Th22;
    end;
A3: now
      let k;
A4:   m * k <= n * k by A1,XREAL_1:66;
      assume P[k];
      then (A |^ (m, n)) |^ (k + 1)
      = (A |^ (m * k, n * k)) ^^ (A |^ (m, n)) by FLANG_1:24
        .= A |^ (m * k + m, n * k + n) by A1,A4,Th37
        .= A |^ (m * (k + 1), n * (k + 1));
      hence P[k + 1];
    end;
    for k holds P[k] from NAT_1:sch 2(A2, A3);
    hence thesis;
  end;
  suppose
A5: k = 0;
    hence (A |^ (m, n)) |^ k = {<%>E} by FLANG_1:25
      .= A |^ 0 by FLANG_1:25
      .= A |^ (m * k, n * k) by A5,Th22;
  end;
  suppose
A6: m > n & k <> 0;
    then
A7: k > 0;
    A |^ (m, n) = {} by A6,Th21;
    then
A8: (A |^ (m, n)) |^ k = {} by A7,FLANG_1:28;
    m * k > n * k by A6,XREAL_1:70;
    hence thesis by A8,Th21;
  end;
end;

theorem Th41:
  (A |^ (k + 1)) |^ (m, n) c= ((A |^ k) |^ (m, n)) ^^ (A |^ (m, n))
proof
  let x;
  assume x in (A |^ (k + 1)) |^ (m, n);
  then consider mn such that
A1: m <= mn & mn <= n & x in (A |^ (k + 1)) |^ mn by Th19;
  x in A |^ ((k + 1) * mn) by A1,FLANG_1:35;
  then x in A |^ (k * mn + mn);
  then x in (A |^ (k * mn)) ^^ (A |^ mn) by FLANG_1:34;
  then
A2: x in ((A |^ k) |^ mn) ^^ (A |^ mn) by FLANG_1:35;
  (A |^ k) |^ mn c= (A |^ k) |^ (m, n) by A1,Th20;
  then ((A |^ k) |^ mn) ^^ (A |^ mn) c=
  ((A |^ k) |^ (m, n)) ^^ (A |^ mn) by FLANG_1:18;
  then
A3: x in ((A |^ k) |^ (m, n)) ^^ (A |^ mn) by A2;
  A |^ mn c= A |^ (m, n) by A1,Th20;
  then ((A |^ k) |^ (m, n)) ^^ (A |^ mn) c=
  ((A |^ k) |^ (m, n)) ^^ (A |^ (m, n)) by FLANG_1:18;
  hence x in ((A |^ k) |^ (m, n)) ^^ (A |^ (m, n)) by A3;
end;

theorem Th42:
  (A |^ k) |^ (m, n) c= A |^ (k * m, k * n)
proof
  per cases;
  suppose
A1: m <= n;
    defpred P[Nat] means (A |^ $1) |^ (m, n) c= A |^ ($1 * m, $1 * n);
A2: P[0]
    proof
      (A |^ 0) |^ (m, n) = {<%>E} |^ (m, n) by FLANG_1:25
        .= {<%>E} by A1,Th31
        .= A |^ 0 by FLANG_1:25
        .= A |^ (0 * m, 0 * n) by Th22;
      hence thesis;
    end;
A3: now
      let l;
A4:   l * m <= l * n by A1,XREAL_1:66;
      assume
A5:   P[l];
      (A |^ (l + 1)) |^ (m, n)
      c= ((A |^ l) |^ (m, n)) ^^ (A |^ (m, n)) by Th41;
      then
A6:   (A |^ (l + 1)) |^ (m, n)
      c= ((A |^ l) |^ (m, n)) ^^ ((A |^ 1) |^ (m, n)) by FLANG_1:26;
A7:   ((A |^ l) |^ (m, n)) ^^ ((A |^ 1) |^ (m, n))
      c= (A |^ (l * m, l * n)) ^^ ((A |^ 1) |^ (m, n)) by A5,FLANG_1:18;
      (A |^ (l * m, l * n)) ^^ ((A |^ 1) |^ (m, n))
      = (A |^ (l * m, l * n)) ^^ (A |^ (m, n)) by FLANG_1:26
        .= A |^ (l * m + m, l * n + n) by A1,A4,Th37
        .= A |^ ((l + 1) * m, (l + 1) * n);
      hence P[l + 1] by A6,A7,XBOOLE_1:1;
    end;
    for l holds P[l] from NAT_1:sch 2(A2, A3);
    hence thesis;
  end;
  suppose m > n;
    then (A |^ k) |^ (m, n) = {} by Th21;
    hence thesis by XBOOLE_1:2;
  end;
end;

theorem
  (A |^ k) |^ (m, n) c= (A |^ (m, n)) |^ k
proof
  (A |^ (m, n)) |^ k = A |^ (m * k, n * k) by Th40;
  hence thesis by Th42;
end;

theorem
  (A |^ (k + l)) |^ (m, n) c= ((A |^ k) |^ (m, n)) ^^ ((A |^ l) |^ (m, n))
proof
  let x;
  assume x in (A |^ (k + l)) |^ (m, n);
  then consider mn such that
A1: m <= mn & mn <= n & x in (A |^ (k + l)) |^ mn by Th19;
  x in A |^ ((k + l) * mn) by A1,FLANG_1:35;
  then x in A |^ (k * mn + l * mn);
  then x in (A |^ (k * mn)) ^^ (A |^ (l * mn)) by FLANG_1:34;
  then consider a, b such that
A2: a in A |^ (k * mn) & b in A |^ (l * mn) & x = a ^ b by FLANG_1:def 1;
  a in (A |^ k) |^ mn & b in (A |^ l) |^ mn by A2,FLANG_1:35;
  then a in (A |^ k) |^ (m, n) & b in (A |^ l) |^ (m, n) by A1,Th19;
  hence x in (A |^ k) |^ (m, n) ^^ (A |^ l) |^ (m, n) by A2,FLANG_1:def 1;
end;

theorem Th45:
  A |^ (0, 0) = {<%>E}
proof
  thus A |^ (0, 0) = A |^ 0 by Th22
    .= {<%>E} by FLANG_1:25;
end;

theorem Th46:
  A |^ (0, 1) = {<%>E} \/ A
proof
  thus A |^ (0, 1) = A |^ 0 \/ A |^ (0 + 1) by Th28
    .= {<%>E} \/ A |^ 1 by FLANG_1:25
    .= {<%>E} \/ A by FLANG_1:26;
end;

theorem
  A |^ (1, 1) = A
proof
  thus A |^ (1, 1) = A |^ 1 by Th22
    .= A by FLANG_1:26;
end;

theorem
  A |^ (0, 2) = {<%>E} \/ A \/ (A ^^ A)
proof
  thus A |^ (0, 2) = A |^ (0, 1) \/ A |^ (1 + 1) by Th26
    .= {<%>E} \/ A \/ A |^ (1 + 1) by Th46
    .= {<%>E} \/ A \/ (A ^^ A) by FLANG_1:27;
end;

theorem
  A |^ (1, 2) = A \/ (A ^^ A)
proof
  thus A |^ (1, 2) = A |^ 1 \/ A |^ (1 + 1) by Th28
    .= A \/ A |^ 2 by FLANG_1:26
    .= A \/ (A ^^ A) by FLANG_1:27;
end;

theorem
  A |^ (2, 2) = A ^^ A
proof
  thus A |^ (2, 2) = A |^ 2 by Th22
    .= A ^^ A by FLANG_1:27;
end;

theorem Th51:
  m > 0 & m <> n & A |^ (m, n) = {x} implies
  for mn st m <= mn & mn <= n holds A |^ mn = {x}
proof
  assume
A1: m > 0 & m <> n & A |^ (m, n) = {x};
  given mn such that
A2: m <= mn & mn <= n & A |^ mn <> {x};
  per cases;
  suppose A |^ mn = {};
    then
A3: A = {} by FLANG_1:28;
    x in A |^ (m, n) by A1,TARSKI:def 1;
    then consider i such that
A4: m <= i & i <= n & x in A |^ i by Th19;
    thus contradiction by A1,A3,A4,FLANG_1:28;
  end;
  suppose A |^ mn <> {};
    then consider y such that
A5: y in A |^ mn & y <> x by A2,ZFMISC_1:41;
    y in A |^ (m, n) by A2,A5,Th19;
    hence contradiction by A1,A5,TARSKI:def 1;
  end;
end;

theorem Th52:
  m <> n & A |^ (m, n) = {x} implies x = <%>E
proof
  assume
A1: m <> n & A |^ (m, n) = {x};
  per cases;
  suppose m > n;
    hence thesis by A1,Th21;
  end;
  suppose
A2: m = 0 & m <= n;
    then {<%>E} = A |^ m by FLANG_1:25;
    then <%>E in A |^ m by TARSKI:def 1;
    then <%>E in A |^ (m, n) by A2,Th19;
    hence thesis by A1,TARSKI:def 1;
  end;
  suppose m > 0 & m <= n;
    then A |^ m = {x} & A |^ n = {x} by A1,Th51;
    hence thesis by A1,Th10;
  end;
end;

theorem Th53:
  <%x%> in A |^ (m, n) iff
  <%x%> in A & m <= n & ((<%>E in A & (n > 0)) or (m <= 1 & 1 <= n))
proof
  thus <%x%> in A |^ (m, n) implies
  <%x%> in A & m <= n & ((<%>E in A & (n > 0)) or (m <= 1 & 1 <= n))
  proof
    assume
A1: <%x%> in A |^ (m, n);
    then consider mn such that
A2: m <= mn & mn <= n & <%x%> in A |^ mn by Th19;
    thus thesis by A1,A2,Th9,Th21;
  end;
  assume
A3: <%x%> in A & m <= n & ((<%>E in A & (n > 0)) or (m <= 1 & 1 <= n));
  per cases by A3;
  suppose <%>E in A & (n > 0);
    then A c= A |^ n by FLANG_1:36;
    hence thesis by A3,Th19;
  end;
  suppose
A4: m <= 1 & 1 <= n;
    <%x%> in A |^ 1 by A3,FLANG_1:26;
    hence thesis by A4,Th19;
  end;
end;

theorem
  (A /\ B) |^ (m, n) c= (A |^ (m, n)) /\ (B |^ (m, n))
proof
  let x;
  assume x in (A /\ B) |^ (m, n);
  then consider mn such that
A1: m <= mn & mn <= n & x in (A /\ B) |^ mn by Th19;
  (A /\ B) |^ mn c= (A |^ mn) /\ (B |^ mn) by FLANG_1:40;
  then x in A |^ mn & x in B |^ mn by A1,XBOOLE_0:def 4;
  then x in A |^ (m, n) & x in B |^ (m, n) by A1,Th19;
  hence x in (A |^ (m, n)) /\ (B |^ (m, n)) by XBOOLE_0:def 4;
end;

theorem
  (A |^ (m, n)) \/ (B |^ (m, n)) c= (A \/ B) |^ (m, n)
proof
  let x;
  assume
A1: x in (A |^ (m, n)) \/ (B |^ (m, n));
  per cases by A1,XBOOLE_0:def 3;
  suppose x in A |^ (m, n);
    then consider mn such that
A2: m <= mn & mn <= n & x in A |^ mn by Th19;
A3: x in (A |^ mn) \/ (B |^ mn) by A2,XBOOLE_0:def 3;
    (A |^ mn) \/ (B |^ mn) c= (A \/ B) |^ mn by FLANG_1:39;
    hence x in (A \/ B) |^ (m, n) by A2,A3,Th19;
  end;
  suppose x in B |^ (m, n);
    then consider mn such that
A4: m <= mn & mn <= n & x in B |^ mn by Th19;
A5: x in (A |^ mn) \/ (B |^ mn) by A4,XBOOLE_0:def 3;
    (A |^ mn) \/ (B |^ mn) c= (A \/ B) |^ mn by FLANG_1:39;
    hence x in (A \/ B) |^ (m, n) by A4,A5,Th19;
  end;
end;

theorem
  (A |^ (m, n)) |^ (k, l) c= A |^ (m * k, n * l)
proof
  let x;
  assume x in (A |^ (m, n)) |^ (k, l);
  then consider kl such that
A1: k <= kl & kl <= l & x in (A |^ (m, n)) |^ kl by Th19;
A2: x in A |^ (m * kl, n * kl) by A1,Th40;
  m * k <= m * kl & n * kl <= n * l by A1,NAT_1:4;
  then A |^ (m * kl, n * kl) c= A |^ (m * k, n * l) by Th23;
  hence x in A |^ (m * k, n * l) by A2;
end;

theorem
  m <= n & <%>E in B implies A c= A ^^ (B |^ (m, n)) & A c= (B |^ (m, n)) ^^ A
proof
  assume m <= n & <%>E in B;
  then <%>E in B |^ (m, n) by Th33;
  hence thesis by FLANG_1:17;
end;

theorem
  m <= n & k <= l & A c= C |^ (m, n) & B c= C |^ (k, l) implies
  A ^^ B c= C |^ (m + k, n + l)
proof
  assume
A1: m <= n & k <= l & A c= C |^ (m, n) & B c= C |^ (k, l);
  thus thesis
  proof
    let x;
    assume x in A ^^ B;
    then consider a, b such that
A2: a in A & b in B & x = a ^ b by FLANG_1:def 1;
    a ^ b in C |^ (m, n) ^^ C |^ (k, l) by A1,A2,FLANG_1:def 1;
    hence x in C |^ (m + k, n + l) by A1,A2,Th37;
  end;
end;

theorem
  (A |^ (m, n))* c= A*
proof
  let x;
  assume x in (A |^ (m, n))*;
  then consider k such that
A1: x in A |^ (m, n) |^ k by FLANG_1:42;
A2: A |^ (m, n) |^ k = A |^ (m * k, n * k) by Th40;
  A |^ (m * k, n * k) c= A* by Th32;
  hence x in A* by A1,A2;
end;

theorem
  (A*) |^ (m, n) c= A*
proof
  let x;
  assume x in (A*) |^ (m, n);
  then consider mn such that
A1: m <= mn & mn <= n & x in (A*) |^ mn by Th19;
  (A*) |^ mn c= A* by FLANG_1:66;
  hence x in A* by A1;
end;

theorem
  m <= n & n > 0 implies (A*) |^ (m, n) = A*
proof
  assume
A1: m <= n & n > 0;
  <%>E in A* by FLANG_1:49;
  hence (A*) |^ (m, n) = (A*) |^ n by A1,Th34
    .= A* by A1,FLANG_1:67;
end;

theorem
  m <= n & n > 0 & <%>E in A implies (A |^ (m, n))* = A*
proof
  assume
A1: m <= n & n > 0 & <%>E in A;
  hence (A |^ (m, n))* = (A |^ n)* by Th34
    .= A* by A1,Th16;
end;

theorem
  m <= n & <%>E in A implies (A |^ (m, n))* = (A*) |^ (m, n)
proof
  assume
A1: m <= n & <%>E in A;
  then
A2: (A |^ (m, n))* = (A |^ n)* by Th34;
  A* |^ (m, n) = A* |^ n by A1,Th34,FLANG_1:49;
  hence thesis by A1,A2,Th17;
end;

theorem Th64:
  A c= B* implies A |^ (m, n) c= B*
proof
  assume
A1: A c= B*;
  thus thesis
  proof
    let x;
    assume x in A |^ (m, n);
    then consider mn such that
A2: m <= mn & mn <= n & x in A |^ mn by Th19;
    A |^ mn c= B* by A1,FLANG_1:60;
    hence x in B* by A2;
  end;
end;

theorem
  A c= B* implies B* = (B \/ (A |^ (m, n)))* by Th64,FLANG_1:68;

theorem Th66:
  A |^ (m, n) ^^ (A*) = A* ^^ (A |^ (m, n))
proof
A1: A |^ (m, n) ^^ (A*) c= A* ^^ (A |^ (m, n))
  proof
    let x;
    assume x in A |^ (m, n) ^^ (A*);
    then consider a, b such that
A2: a in A |^ (m, n) & b in A* & x = a ^ b by FLANG_1:def 1;
    consider mn such that
A3: m <= mn & mn <= n & a in A |^ mn by A2,Th19;
    consider k such that
A4: b in A |^ k by A2,FLANG_1:42;
    a ^ b in A |^ (k + mn) by A3,A4,FLANG_1:41;
    then
A5: a ^ b in A |^ k ^^ (A |^ mn) by FLANG_1:34;
    A |^ k c= A* & A |^ mn c= A |^ (m, n) by A3,Th20,FLANG_1:43;
    then A |^ k ^^ (A |^ mn) c= A* ^^ (A |^ (m, n)) by FLANG_1:18;
    hence x in A* ^^ (A |^ (m, n)) by A2,A5;
  end;
  A* ^^ (A |^ (m, n)) c= A |^ (m, n) ^^ (A*)
  proof
    let x;
    assume x in A* ^^ (A |^ (m, n));
    then consider a, b such that
A6: a in A* & b in A |^ (m, n) & x = a ^ b by FLANG_1:def 1;
    consider mn such that
A7: m <= mn & mn <= n & b in A |^ mn by A6,Th19;
    consider k such that
A8: a in A |^ k by A6,FLANG_1:42;
    a ^ b in A |^ (mn + k) by A7,A8,FLANG_1:41;
    then
A9: a ^ b in A |^ mn ^^ (A |^ k) by FLANG_1:34;
    A |^ k c= A* & A |^ mn c= A |^ (m, n) by A7,Th20,FLANG_1:43;
    then A |^ mn ^^ (A |^ k) c= A |^ (m, n) ^^ (A*) by FLANG_1:18;
    hence x in A |^ (m, n) ^^ (A*) by A6,A9;
  end;
  hence thesis by A1,XBOOLE_0:def 10;
end;

theorem
  <%>E in A & m <= n implies A* = A* ^^ (A |^ (m, n))
proof
  assume
A1: <%>E in A & m <= n;
  defpred P[Nat] means A* = A* ^^ (A |^ (m, m + $1));
A2: P[0]
  proof
    thus A* = A* ^^ (A |^ m) by A1,FLANG_1:56
      .= A* ^^ (A |^ (m, m + 0)) by Th22;
  end;
A3: now
    let i;
    assume
A4: P[i];
    A |^ (m, m + (i + 1))
    = A |^ (m, m + i) \/ (A |^ (m + i + 1)) by Th26,NAT_1:11;
    then A* ^^ (A |^ (m, m + (i + 1)))
    = A* \/ (A* ^^ (A |^ (m + i + 1))) by A4,FLANG_1:21
      .= A* \/ A* by A1,FLANG_1:56;
    hence P[i + 1];
  end;
A5: for i holds P[i] from NAT_1:sch 2(A2, A3);
  consider k such that
A6: m + k = n by A1,NAT_1:10;
  thus thesis by A5,A6;
end;

theorem
  A |^ (m, n) |^ k c= A* by Th32,FLANG_1:60;

theorem Th69:
  A |^ k |^ (m, n) c= A*
proof
  let x;
  assume x in A |^ k |^ (m, n);
  then consider mn such that
A1: m <= mn & mn <= n & x in A |^ k |^ mn by Th19;
A2: x in A |^ (k * mn) by A1,FLANG_1:35;
  A |^ (k * mn) c= A* by FLANG_1:43;
  hence x in A* by A2;
end;

theorem
  m <= n implies (A |^ m)* c= (A |^ (m, n))* by Th20,FLANG_1:62;

theorem Th71:
  (A |^ (m, n)) |^ (k, l) c= A*
proof
  let x;
  assume x in (A |^ (m, n)) |^ (k, l);
  then consider kl such that
A1: k <= kl & kl <= l & x in (A |^ (m, n)) |^ kl by Th19;
  (A |^ (m, n)) |^ kl c= A* by Th32,FLANG_1:60;
  hence x in A* by A1;
end;

theorem Th72:
  <%>E in A & k <= n & l <= n implies A |^ (k, n) = A |^ (l, n)
proof
  assume
A1: <%>E in A & k <= n & l <= n;
  hence A |^ (k, n) = A |^ n by Th34
    .= A |^ (l, n) by A1,Th34;
end;

begin

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: Optional Occurrence
:: Definition of ?
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

definition
  let E, A;
  func A? -> Subset of E^omega equals
  union { B: ex k st k <= 1 & B = A |^ k };
  coherence
  proof
    defpred P[set] means ex k st k <= 1 & $1 = A |^ k;
    reconsider M = { B: P[B] } as Subset-Family of E^omega from DOMAIN_1:sch 7;
    union M is Subset of E^omega;
    hence thesis;
  end;
end;

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: Optional Occurrence
:: Properties of ?
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

theorem Th73:
  x in A? iff ex k st k <= 1 & x in A |^ k
proof
  thus x in A? implies ex k st k <= 1 & x in A |^ k
  proof
    assume x in A?;
    then consider X such that
A1: x in X and
A2: X in { B: ex k st k <= 1 & B = A |^ k } by TARSKI:def 4;
    defpred P[set] means ex k st k <= 1 & $1 = A |^ k;
A3: X in { B: P[B] } by A2;
    P[X] from CARD_FIL:sch 1(A3);
    hence thesis by A1;
  end;
  given k such that
A4: k <= 1 & x in A |^ k;
  defpred P[set] means ex k st k <= 1 & $1 = A |^ k;
  consider B such that
A5: x in B & P[B] by A4;
  reconsider A = { C: P[C] } as Subset-Family of E^omega from DOMAIN_1:sch 7;
  B in A by A5;
  hence thesis by A5,TARSKI:def 4;
end;

theorem
  n <= 1 implies A |^ n c= A?
proof
  assume n <= 1;
  then for x holds x in A |^ n implies x in A? by Th73;
  hence thesis by TARSKI:def 3;
end;

theorem Th75:
  A? = (A |^ 0) \/ (A |^ 1)
proof
  now
    let x;
    x in A? iff ex k st (k = 0 or k = 1) & x in A |^ k
    proof
      thus x in A? implies ex k st (k = 0 or k = 1) & x in A |^ k
      proof
        assume x in A?;
        then consider k such that
A1:     k <= 1 & x in A |^ k by Th73;
        (k = 0 or k = 1) by A1,NAT_1:26;
        hence thesis by A1;
      end;
      given k such that
A2:   (k = 0 or k = 1) & x in A |^ k;
      thus thesis by A2,Th73;
    end;
    hence x in A? iff x in A |^ 0 or x in A |^ 1;
  end;
  hence thesis by XBOOLE_0:def 3;
end;

theorem Th76:
  A? = {<%>E} \/ A
proof
  A |^ 0 = {<%>E} & A |^ 1 = A by FLANG_1:25,26;
  hence thesis by Th75;
end;

theorem
  A c= A?
proof
  A? = {<%>E} \/ A by Th76;
  hence thesis by XBOOLE_1:7;
end;

theorem Th78:
  x in A? iff x = <%>E or x in A
proof
  x in A? iff x in {<%>E} \/ A by Th76;
  then x in A? iff x in {<%>E} or x in A by XBOOLE_0:def 3;
  hence thesis by TARSKI:def 1;
end;

theorem Th79:
  A? = A |^ (0, 1)
proof
  thus A? = (A |^ 0) \/ (A |^ (0 + 1)) by Th75
    .= A |^ (0, 1) by Th28;
end;

theorem Th80:
  A? = A iff <%>E in A
proof
  thus A? = A implies <%>E in A
  proof
    assume A? = A;
    then A = {<%>E} \/ A by Th76;
    hence thesis by ZFMISC_1:45;
  end;
  assume <%>E in A;
  then A = {<%>E} \/ A by ZFMISC_1:46;
  hence thesis by Th76;
end;

registration
  let E, A;
  cluster A? -> non empty;
  coherence
  proof
    <%>E in A? by Th78;
    hence thesis;
  end;
end;

theorem
  A?? = A?
proof
  <%>E in A? by Th78;
  hence thesis by Th80;
end;

theorem
  A c= B implies A? c= B?
proof
  assume A c= B;
  then A |^ (0, 1) c= B |^ (0, 1) by Th29;
  then A? c= B |^ (0, 1) by Th79;
  hence thesis by Th79;
end;

theorem
  x in A & x <> <%>E implies A? <> {<%>E}
proof
  assume
A1: x in A & x <> <%>E;
  A? = A |^ (0, 1) by Th79;
  hence thesis by A1,Th30;
end;

theorem
  A? = {<%>E} iff A = {} or A = {<%>E}
proof
  A? = A |^ (0, 1) by Th79;
  hence thesis by Th31;
end;

theorem
  (A*)? = A* & (A?)* = A*
proof
  thus (A*)? = {<%>E} \/ (A*) by Th76
    .= A* by FLANG_1:49,ZFMISC_1:46;
  thus (A?)* = ({<%>E} \/ A)* by Th76
    .= A* by FLANG_1:49,69;
end;

theorem
  A? c= A*
proof
  A? = A |^ (0, 1) by Th79;
  hence thesis by Th32;
end;

theorem
  (A /\ B)? = (A?) /\ (B?)
proof
  thus (A /\ B)? = {<%>E} \/ (A /\ B) by Th76
    .= ({<%>E} \/ A) /\ ({<%>E} \/ B) by XBOOLE_1:24
    .= (A?) /\ ({<%>E} \/ B) by Th76
    .= (A?) /\ (B?) by Th76;
end;

theorem
  (A?) \/ (B?) = (A \/ B)?
proof
  thus (A \/ B)? = {<%>E} \/ (A \/ B) by Th76
    .= (A \/ {<%>E}) \/ (B \/ {<%>E}) by XBOOLE_1:5
    .= (A?) \/ (B \/ {<%>E}) by Th76
    .= (A?) \/ (B?) by Th76;
end;

theorem
  A? = {x} implies x = <%>E
proof
  A? = A |^ (0, 1) by Th79;
  hence thesis by Th52;
end;

theorem
  <%x%> in A? iff <%x%> in A
proof
  A? = A |^ (0, 1) by Th79;
  hence thesis by Th53;
end;

theorem
  (A?) ^^ A = A ^^ (A?)
proof
  A? = A |^ (0, 1) by Th79;
  hence thesis by Th36;
end;

theorem
  (A?) ^^ A = A |^ (1, 2)
proof
  A? = A |^ (0, 1) by Th79;
  then (A?) ^^ A = A |^ (0 + 1, 1 + 1) by Th38;
  hence thesis;
end;

theorem Th93:
  (A?) ^^ (A?) = A |^ (0, 2)
proof
  A? = A |^ (0, 1) by Th79;
  then (A?) ^^ (A?) = A |^ (0 + 0, 1 + 1) by Th37;
  hence thesis;
end;

theorem Th94:
  (A?) |^ k = (A?) |^ (0, k)
proof
  <%>E in A? by Th78;
  hence thesis by Th34;
end;

theorem Th95:
  (A?) |^ k = A |^ (0, k)
proof
  defpred P[Nat] means (A?) |^ $1 = A |^ (0, $1);
A1: P[0]
  proof
    thus (A?) |^ 0 = {<%>E} by FLANG_1:25
      .= A |^ (0, 0) by Th45;
  end;
A2: now
    let k;
    assume
A3: P[k];
    (A?) |^ (k + 1) = ((A?) |^ k) ^^ ((A?) |^ 1) by FLANG_1:34
      .= A |^ (0, k) ^^ (A?) by A3,FLANG_1:26
      .= A |^ (0, k) ^^ (A |^ (0, 1)) by Th79
      .= A |^ (0 + 0, k + 1) by Th37;
    hence P[k + 1];
  end;
  for k holds P[k] from NAT_1:sch 2(A1, A2);
  hence thesis;
end;

theorem Th96:
  m <= n implies A? |^ (m, n) = A? |^ (0, n)
proof
  assume
A1: m <= n;
  <%>E in A? by Th78;
  hence thesis by A1,Th72;
end;

theorem Th97:
  A? |^ (0, n) = A |^ (0, n)
proof
  thus A? |^ (0, n) = A? |^ n by Th94
    .= A |^ (0, n) by Th95;
end;

theorem Th98:
  m <= n implies A? |^ (m, n) = A |^ (0, n)
proof
  assume m <= n;
  hence A? |^ (m, n) = A? |^ (0, n) by Th96
    .= A |^ (0, n) by Th97;
end;

theorem
  (A |^ (1, n))? = A |^ (0, n)
proof
  thus (A |^ (1, n))? = {<%>E} \/ (A |^ (1, n)) by Th76
    .= A |^ (0, 0) \/ (A |^ (0 + 1, n)) by Th45
    .= A |^ (0, n) by Th25;
end;

theorem
  <%>E in A & <%>E in B implies A? c= A ^^ B & A? c= B ^^ A
proof
  assume
A1: <%>E in A & <%>E in B;
  then
A2: A c= A ^^ B & A c= B ^^ A by FLANG_1:17;
  <%>E in A ^^ B & <%>E in B ^^ A by A1,FLANG_1:16;
  then {<%>E} c= A ^^ B & {<%>E} c= B ^^ A by ZFMISC_1:37;
  then {<%>E} \/ A c= A ^^ B & {<%>E} \/ A c= B ^^ A by A2,XBOOLE_1:8;
  hence thesis by Th76;
end;

theorem
  A c= A ^^ (B?) & A c= (B?) ^^ A
proof
  <%>E in B? by Th78;
  hence thesis by FLANG_1:17;
end;

theorem
  A c= C? & B c= C? implies A ^^ B c= C |^ (0, 2)
proof
  assume A c= C? & B c= C?;
  then A ^^ B c= (C?) ^^ (C?) by FLANG_1:18;
  hence thesis by Th93;
end;

theorem
  <%>E in A & n > 0 implies A? c= A |^ n
proof
  assume <%>E in A & n > 0;
  then <%>E in A |^ n & A c= A |^ n by FLANG_1:31,36;
  then {<%>E} c= A |^ n & A c= A |^ n by ZFMISC_1:37;
  then {<%>E} \/ A c= A |^ n by XBOOLE_1:8;
  hence thesis by Th76;
end;

theorem
  (A?) ^^ (A |^ k) = (A |^ k) ^^ (A?)
proof
  thus (A?) ^^ (A |^ k) = ({<%>E} \/ A) ^^ (A |^ k) by Th76
    .= ({<%>E} ^^ (A |^ k)) \/ (A ^^ (A |^ k)) by FLANG_1:21
    .= ({<%>E} ^^ (A |^ k)) \/ ((A |^ k) ^^ A) by FLANG_1:33
    .= (A |^ k) \/ ((A |^ k) ^^ A) by FLANG_1:14
    .= ((A |^ k) ^^ {<%>E}) \/ ((A |^ k) ^^ A) by FLANG_1:14
    .= (A |^ k) ^^ (A \/ {<%>E}) by FLANG_1:21
    .= (A |^ k) ^^ (A?) by Th76;
end;

theorem Th105:
  A c= B* implies A? c= B*
proof
  assume A c= B*;
  then A |^ (0, 1) c= B* by Th64;
  hence thesis by Th79;
end;

theorem
  A c= B* implies B* = (B \/ (A?))* by Th105,FLANG_1:68;

theorem
  A? ^^ (A*) = A* ^^ (A?)
proof
  thus A? ^^ (A*) = A |^ (0, 1) ^^ (A*) by Th79
    .= A* ^^ (A |^ (0, 1)) by Th66
    .= A* ^^ (A?) by Th79;
end;

theorem
  A? ^^ (A*) = A*
proof
A1: A ^^ (A*) c= A* by FLANG_1:54;
  thus A? ^^ (A*) = ({<%>E} \/ A) ^^ (A*) by Th76
    .= {<%>E} ^^ (A*) \/ A ^^ (A*) by FLANG_1:21
    .= A* \/ A ^^ (A*) by FLANG_1:14
    .= A* by A1,XBOOLE_1:12;
end;

theorem
  A? |^ k c= A*
proof
  A? |^ k = (A |^ (0, 1)) |^ k by Th79;
  hence thesis by Th32,FLANG_1:60;
end;

theorem
  (A |^ k)? c= A*
proof
  (A |^ k)? = (A |^ k) |^ (0, 1) by Th79;
  hence thesis by Th69;
end;

theorem
  (A?) ^^ (A |^ (m, n)) = (A |^ (m, n)) ^^ (A?)
proof
  (A |^ (0, 1)) ^^ (A |^ (m, n)) = (A |^ (m, n)) ^^ (A |^ (0, 1)) by Th39;
  then (A?) ^^ (A |^ (m, n)) = (A |^ (m, n)) ^^ (A |^ (0, 1)) by Th79;
  hence thesis by Th79;
end;

theorem
  (A?) ^^ (A |^ k) = A |^ (k, k + 1)
proof
  (A |^ (0, 1)) ^^ (A |^ k) = (A |^ (0, 1)) ^^ (A |^ (k, k)) by Th22
    .= A |^ (0 + k, 1 + k) by Th37;
  hence thesis by Th79;
end;

theorem
  A? |^ (m, n) c= A*
proof
  per cases;
  suppose m <= n;
    then A? |^ (m, n) = A |^ (0, n) by Th98;
    hence thesis by Th32;
  end;
  suppose m > n;
    then A? |^ (m, n) = {} by Th21;
    hence thesis by XBOOLE_1:2;
  end;
end;

theorem
  (A |^ (m, n))? c= A*
proof
  (A |^ (m, n))? = (A |^ (m, n)) |^ (0, 1) by Th79;
  hence thesis by Th71;
end;

theorem
  A? = (A \ {<%>E})?
proof
  thus A? = {<%>E} \/ A by Th76
    .= {<%>E} \/ (A \ {<%>E}) by XBOOLE_1:39
    .= (A \ {<%>E})? by Th76;
end;

theorem
  A c= B? implies A? c= B?
proof
  assume
A1: A c= B?;
  <%>E in B? by Th78;
  then {<%>E} c= B? by ZFMISC_1:37;
  then {<%>E} \/ A c= B? by A1,XBOOLE_1:8;
  hence thesis by Th76;
end;

theorem
  A c= B? implies B? = (B \/ A)?
proof
  assume
A1: A c= B?;
  thus (B \/ A)? = {<%>E} \/ (B \/ A) by Th76
    .= ({<%>E} \/ B) \/ A by XBOOLE_1:4
    .= B? \/ A by Th76
    .= B? by A1,XBOOLE_1:12;
end;

