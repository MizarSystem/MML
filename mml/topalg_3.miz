:: On the Isomorphism of Fundamental Groups
::  by Artur Korni{\l}owicz
:: 
:: Received July 30, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabulary PRE_TOPC, EUCLID, RELAT_1, SUBSET_1, TOPALG_3, TOPALG_1, WELLORD1,
      ALGSTR_1, BORSUK_1, FUNCT_1, WAYBEL20, EQREL_1, BORSUK_2, ORDINAL2,
      ARYTM_1, GROUP_6, VECTSP_1, T_0TOPSP, TOPS_2, ARYTM_3, FUNCOP_1,
      REALSET1, TOPMETR, GRAPH_1, BORSUK_6, SGRAPH1, PCOMPS_1, FUNCT_4, BOOLE,
      SEQM_3, SETFAM_1, TARSKI, TOPREAL1, TOPREAL2, TAXONOM2, TEX_2, RELAT_2,
      CONNSP_1, RLVECT_3, CAT_1;
 notation TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, RELAT_1, EQREL_1, FUNCT_1,
      RELSET_1, FUNCT_2, BINOP_1, XCMPLX_0, XREAL_0, SEQM_3, FUNCOP_1,
      CQC_LANG, FUNCT_4, STRUCT_0, PRE_TOPC, VECTSP_1, GROUP_6, GRCAT_1,
      BORSUK_1, TOPS_2, COMPTS_1, CONNSP_1, TOPREAL1, TEX_2, TOPREAL2, EUCLID,
      PCOMPS_1, BORSUK_2, BORSUK_3, TAXONOM2, YELLOW_8, BORSUK_6, TOPALG_1,
      TOPALG_2;
 constructors GRCAT_1, TOPALG_1, TOPREAL1, REAL_1, TOPS_2, BORSUK_3, CONNSP_1,
      TOPALG_2, SEQM_3, COMPTS_1, YELLOW_8, TOPREAL2, TAXONOM2, REALSET2,
      SEQ_4, TEX_2, TDLAT_3, CANTOR_1, CQC_LANG;
 clusters STRUCT_0, EUCLID, RELSET_1, XREAL_0, BORSUK_1, PRE_TOPC, SUBSET_1,
      XBOOLE_0, FUNCT_1, TOPALG_1, BORSUK_2, INT_1, BORSUK_5, PCOMPS_1, TOPS_1,
      TOPREAL2, TEX_1, BORSUK_3, WAYBEL12, YELLOW12, CQC_LANG;
 requirements NUMERALS, BOOLE, SUBSET;
 definitions TARSKI, XBOOLE_0, FUNCT_1, FUNCT_2, GROUP_6, PRE_TOPC, TOPS_2,
      T_0TOPSP, BORSUK_2, BORSUK_6, CONNSP_1;
 theorems GROUP_6, PRE_TOPC, FUNCT_2, BORSUK_1, TOPALG_1, VECTSP_1, TOPS_2,
      GRCAT_1, BORSUK_3, FUNCT_1, TOPMETR, RELAT_1, FUNCOP_1, TOPREAL6,
      BORSUK_6, BORSUK_2, BINOP_1, TARSKI, PCOMPS_1, ZFMISC_1, FUNCT_4, TSEP_1,
      XBOOLE_1, COMPTS_1, TOPREAL1, TOPREAL2, JORDAN1, STRUCT_0, SETFAM_1,
      XBOOLE_0, TAXONOM2, REALSET2, TEX_2, TOPS_1, CONNSP_1, TDLAT_3, ENUMSET1,
      JCT_MISC, YELLOW12, TOPS_3, YELLOW13, YELLOW_8, CQC_LANG;
 schemes FUNCT_2;

begin :: Preliminaries

set I = the carrier of I[01];
set R = the carrier of R^1;
Lm8: the carrier of [:I[01],I[01]:] = [:I,I:] by BORSUK_1:def 5;
reconsider j0 = 0, j1 = 1 as Point of I[01] by BORSUK_1:def 17,def 18;

theorem
  for A, B, a, b being set, f being Function of A,B st a in A & b in B holds
  f +* (a .--> b) is Function of A,B
proof ::ok
  let A, B, a, b be set, f be Function of A,B such that
a1: a in A and
a2: b in B;
  set g = a .--> b;
  set f1 = f +* g;
s6: {a} c= A by a1,ZFMISC_1:37;
s5: dom f1 = dom f \/ dom g by FUNCT_4:def 1
      .= A \/ dom g by a2,FUNCT_2:def 1
      .= A \/ {a} by CQC_LANG:5
      .= A by s6,XBOOLE_1:12;
s9: rng f1 c= rng f \/ rng g by FUNCT_4:18;
  rng g = {b} by CQC_LANG:5;
  then rng g c= B by a2,ZFMISC_1:37;
  then rng f \/ rng g c= B \/ B by XBOOLE_1:13;
  then rng f1 c= B by s9,XBOOLE_1:1;
  hence f +* (a .--> b) is Function of A,B by s5,FUNCT_2:4;
end;

theorem
  for f being Function, X, x being set st f|X is one-to-one & x in rng (f|X)
  holds (f*(f|X)").x = x
proof ::ok
  let f be Function,
      X, x be set;
  set g = f|X;
  assume that
a0: g is one-to-one and
a1: x in rng g;
  consider a being set such that
a2:  a in dom g and
a3:  g.a = x by a1,FUNCT_1:def 5;
  dom g = dom f /\ X by RELAT_1:90;
  then
a6: a in X by a2,XBOOLE_0:def 3;
  dom (g") = rng g by a0,FUNCT_1:54;
  hence (f*g").x = f.(g".x) by a1,FUNCT_1:23
    .= f.a by a0,a2,a3,FUNCT_1:54
    .= x by a3,a6,FUNCT_1:72;
end;

theorem Tzb:
  for x, y, X, Y, Z being set, f being Function of [:X,Y:],Z, g being Function
    st Z <> {} & x in X & y in Y holds (g*f).(x,y) = g.(f.(x,y))
proof
  let x, y, X, Y, Z be set,
      f be Function of [:X,Y:],Z,
      g be Function such that
a1: Z <> {};
  assume x in X & y in Y;
  then
a2: [x,y] in [:X,Y:] by ZFMISC_1:106;
  thus (g*f).(x,y) = (g*f). [x,y] by BINOP_1:def 1
    .= g.(f. [x,y]) by a1,a2,FUNCT_2:21
    .= g.(f.(x,y)) by BINOP_1:def 1;
end;

theorem Tob:
  for X, a, b being set, f being Function of X,{a,b} holds X = f"{a} \/ f"{b}
proof
  let X, a, b be set;
  let f be Function of X,{a,b};
  thus X = f"{a,b} by FUNCT_2:48
    .= f"({a}\/{b}) by ENUMSET1:41
    .= f"{a} \/ f"{b} by RELAT_1:175;
end;

theorem Tstrz:
  for S, T being non empty 1-sorted,
      s being Point of S, t being Point of T holds
   (S --> t).s = t
proof
  let S, T be non empty 1-sorted,
      s be Point of S, t be Point of T;
  thus (S --> t).s = ((the carrier of S) --> t).s by BORSUK_1:def 3
     .= t by FUNCOP_1:13;
end;

:: Topological preliminaries

theorem
  for T being non empty TopStruct, t being Point of T, A being Subset of T
   st A = {t} holds Sspace(t) = T | A
proof
  let T be non empty TopStruct,
      t be Point of T,
      A be Subset of T such that
a1:  A = {t};
  the carrier of Sspace(t) = {t} by TEX_2:def 4
    .= [#](T|A) by a1,PRE_TOPC:def 10
    .= the carrier of (T|A) by PRE_TOPC:12;
  hence Sspace(t) = T | A by TSEP_1:5;
end;

theorem Tsep:
  for T being TopSpace, A, B being Subset of T,
      C, D being Subset of the TopStruct of T st A = C & B = D holds
  A,B are_separated iff C,D are_separated
proof
  let T be TopSpace;
  let A, B be Subset of T;
  let C, D be Subset of the TopStruct of T;
  assume
Z1: A = C & B = D;
  then
Z0: Cl A = Cl C & Cl B = Cl D by TOPS_3:80;
  hereby 
    assume A,B are_separated;
    then Cl A misses B & A misses Cl B by CONNSP_1:def 1;
    hence C,D are_separated by Z1,Z0,CONNSP_1:def 1;
  end;
  assume C,D are_separated;
  then Cl C misses D & C misses Cl D by CONNSP_1:def 1;
  hence A,B are_separated by Z1,Z0,CONNSP_1:def 1;
end;

theorem Tconiff:
  for T being non empty TopSpace holds T is connected iff
  not ex f being map of T, 1TopSp{0,1} st f is continuous onto
proof
  let T be non empty TopSpace;
  set J = 1TopSp{0,1};
  set X = {0}, Y = {1};
xx: [#]J = the carrier of J by PRE_TOPC:12;
b2: the carrier of J = {0,1} by PCOMPS_1:8;
  then reconsider X, Y as non empty open Subset of J by TDLAT_3:17,ZFMISC_1:12;
a2: [#]T = the carrier of T by PRE_TOPC:12;
m1: X misses Y by ZFMISC_1:17;
  thus T is connected implies
  not ex f being map of T,J st f is continuous onto
  proof
    assume
a0:   T is connected;
    given f being map of T,J such that
a1:   f is continuous and
a5:   f is onto;
    set A = f"X, B = f"Y;
d0: the carrier of T = A \/ B by b2,Tob;
a3: A is open & B is open by a1,TOPS_2:55;
    rng f = the carrier of J by a5,FUNCT_2:def 3;
    then
a6: A <> {}T & B <> {}T by RELAT_1:174;
    A misses B by m1,JCT_MISC:4;
    hence contradiction by a0,a2,a3,a6,d0,CONNSP_1:12;
  end;
  assume
cc:  not ex f being map of T,J st f is continuous onto;
  assume not thesis;
  then consider A, B being Subset of T such that
C1:  [#]T = A \/ B and
c2:  A <> {}T and
c3:  B <> {}T and
c4:  A is open and
c5:  B is open and
c6:  A misses B by CONNSP_1:12;
  defpred C[set] means $1 in A;
  reconsider j0 = 0, j1 = 1 as Element of the carrier of J by b2,TARSKI:def 2;
  deffunc F(set) = j0;
  deffunc G(set) = j1;
c0: for x being set st x in the carrier of T holds
     (C[x] implies F(x) in the carrier of J) &
     (not C[x] implies G(x) in the carrier of J);
  consider f being Function of the carrier of T, the carrier of J
    such that
c1:  for x being set st x in the carrier of T holds
      (C[x] implies f.x = F(x)) & (not C[x] implies f.x = G(x))
      from FUNCT_2:sch 9(c0);
  reconsider f as map of T,J;
w1: dom f = the carrier of T by FUNCT_2:def 1;
d3: f"X = A
   proof
     hereby
       let x be set;
       assume
s0:      x in f"X;
       then x in dom f & f.x in X by FUNCT_1:def 13;
       then f.x = 0 by TARSKI:def 1;
       hence x in A by c1,s0;
     end;
     let x be set;
     assume
t1:    x in A;
     f.x = 0 by t1,c1;
     then f.x in X by TARSKI:def 1;
     hence x in f"X by t1,w1,FUNCT_1:def 13;
   end;
d4: f"Y = B
   proof
     hereby
       let x be set;
       assume
s0:      x in f"Y;
       then x in dom f & f.x in Y by FUNCT_1:def 13;
       then f.x = 1 by TARSKI:def 1;
       then not C[x] by c1;
       hence x in B by s0,C1,a2,XBOOLE_0:def 2;
     end;
     let x be set;
     assume
t1:    x in B;
     not x in A by t1,c6,XBOOLE_0:3;
     then f.x = 1 by t1,c1;
     then f.x in Y by TARSKI:def 1;
     hence x in f"Y by t1,w1,FUNCT_1:def 13;
   end;
w2: rng f = the carrier of J
   proof
     thus rng f c= the carrier of J;
     let y be set;
     assume
ww:    y in the carrier of J;
     per cases by ww,b2,TARSKI:def 2;
     suppose
s1:    y = 0;
     consider x being set such that
k1:    x in f"X by c2,d3,XBOOLE_0:def 1;
s2:  x in dom f & f.x in X by k1,FUNCT_1:def 13;
     then f.x = 0 by TARSKI:def 1;
     hence y in rng f by s1,s2,FUNCT_1:def 5;
     end;
     suppose
s1:    y = 1;
     consider x being set such that
k1:    x in f"Y by c3,d4,XBOOLE_0:def 1;
s2:  x in dom f & f.x in Y by k1,FUNCT_1:def 13;
     then f.x = 1 by TARSKI:def 1;
     hence y in rng f by s1,s2,FUNCT_1:def 5;
     end;
   end;
   then
c7: f is onto by FUNCT_2:def 3;
d1: f"{}J = {}T by RELAT_1:171;
d2: f"[#]J = [#]T by xx,a2,w1,w2,RELAT_1:169;
  for P being Subset of J st P is open holds f"P is open
    by d1,d2,d3,d4,c4,c5,b2,xx,ZFMISC_1:42;
  then f is continuous by TOPS_2:55;
  hence thesis by cc,c7;
end;

registration
  cluster empty -> connected TopStruct;
coherence
proof
  let T be TopStruct such that
a1:  T is empty;
  let A be Subset of T;
  reconsider T as empty TopStruct by a1;
  the carrier of T is empty;  
  hence thesis by XBOOLE_1:3;
end;
end;

theorem
  for T being TopSpace st the TopStruct of T is connected holds T is connected
proof
  let T be TopSpace;
  set G = the TopStruct of T;
  assume
a0:  G is connected;
  let A, B be Subset of T such that
a1: [#]T = A \/ B and
a2: A,B are_separated;
  reconsider A1 = A, B1 = B as Subset of G;
a3: [#]G = the carrier of G by PRE_TOPC:12
        .= A1 \/ B1 by a1,PRE_TOPC:12;
  A1,B1 are_separated by a2,Tsep;
  then A1 = {}G or B1 = {}G by a0,a3,CONNSP_1:def 2;
  hence A = {}T or B = {}T;
end;

registration
  let T be connected TopSpace;
  cluster the TopStruct of T -> connected;
coherence
proof
  set G = the TopStruct of T;
  let A, B be Subset of G such that
a1: [#]G = A \/ B and
a2: A,B are_separated;
  reconsider A1 = A, B1 = B as Subset of T;
a3: [#]T = the carrier of T by PRE_TOPC:12
        .= A1 \/ B1 by a1,PRE_TOPC:12;
  A1,B1 are_separated by a2,Tsep;
  then A1 = {}T or B1 = {}T by a3,CONNSP_1:def 2;
  hence A = {}G or B = {}G;
end;
end;

theorem
  for S, T be non empty TopSpace st
   S,T are_homeomorphic & S is arcwise_connected holds T is arcwise_connected
proof
  let S, T be non empty TopSpace;
  given h being map of S,T such that
a1: h is_homeomorphism;
  assume
a2: for a, b being Point of S ex f being map of I[01], S st
      f is continuous & f.0 = a & f.1 = b;
  let a, b being Point of T;
  consider f being map of I[01], S such that      
a3: f is continuous and
a4: f.0 = h".a and
a5: f.1 = h".b by a2;
  take g = h*f;
  h is continuous by a1,TOPS_2:def 5;
  hence g is continuous by a3,TOPS_2:58;
a6: h is one-to-one by a1,TOPS_2:def 5;
a8: rng h = [#]T by a1,TOPS_2:def 5;
a9: [#]T = the carrier of T by PRE_TOPC:12;
  thus g.0 = h.(h".a) by a4,FUNCT_2:21,BORSUK_1:def 17
    .= (h*h").a by FUNCT_2:21
    .= (id rng h).a by a6,a8,TOPS_2:65
    .= (id T).a by a8,a9,GRCAT_1:def 11
    .= a by GRCAT_1:11;
  thus g.1 = h.(h".b) by a5,FUNCT_2:21,BORSUK_1:def 18
    .= (h*h").b by FUNCT_2:21
    .= (id rng h).b by a6,a8,TOPS_2:65
    .= (id T).b by a8,a9,GRCAT_1:def 11
    .= b by GRCAT_1:11;
end;

registration
  cluster trivial -> arcwise_connected (non empty TopSpace);
coherence
proof
  let T be non empty TopSpace;
  assume
a0: T is trivial;
  let a, b be Point of T;
  take f = I[01] --> a;
  thus f is continuous by BORSUK_1:36;
  a = b by a0,REALSET2:def 7;
  hence f.0 = a & f.1 = b by Tstrz,BORSUK_1:def 17,def 18;
end;
end;

theorem
  for T being SubSpace of TOP-REAL 2 st
   the carrier of T is Simple_closed_curve holds T is arcwise_connected
proof
  let T be SubSpace of TOP-REAL 2;
  assume
Z1: the carrier of T is Simple_closed_curve;
  then reconsider P = the carrier of T as Simple_closed_curve;
  let a, b be Point of T;
  a in P & b in P;
  then reconsider p1 = a, p2 = b as Point of TOP-REAL 2;
  per cases;
  suppose p1 <> p2;
  then consider P1, P2 being non empty Subset of TOP-REAL 2 such that
a2: P1 is_an_arc_of p1,p2 and P2 is_an_arc_of p1,p2 and
a4: P = P1 \/ P2 and P1 /\ P2 = {p1,p2} by TOPREAL2:5;
  consider f1 being map of I[01], (TOP-REAL 2)|P1 such that
a6:  f1 is_homeomorphism and
a7:  f1.0 = p1 & f1.1 = p2 by a2,TOPREAL1:def 2;
  the carrier of ((TOP-REAL 2)|P1) = P1 by JORDAN1:1;
  then
b1: the carrier of ((TOP-REAL 2)|P1) c= P by a4,XBOOLE_1:7;
  then reconsider f = f1 as map of I[01],T by FUNCT_2:9;
  take f;
a9: (TOP-REAL 2)|P1 is SubSpace of T by b1,TSEP_1:4;
  f1 is continuous by a6,TOPS_2:def 5;
  hence f is continuous by a9,TOPMETR:7;
  thus thesis by a7;
  end;
  suppose
s1: p1 = p2;
  reconsider T1 = T as non empty SubSpace of TOP-REAL 2 by Z1,STRUCT_0:def 1;
  reconsider a1 = a as Point of T1;
  reconsider f = I[01] --> a1 as map of I[01],T;
  take f;
  thus f is continuous by BORSUK_1:36;
s2: f = I --> a by BORSUK_1:def 3;
  thus f.0 = f.j0 .= a by s2,FUNCOP_1:13;
  thus f.1 = f.j1 .= b by s1,s2,FUNCOP_1:13;
  end;
end;

theorem
  for T being TopSpace
   ex F being Subset-Family of T st F = {the carrier of T} &
    F is_a_cover_of T & F is open
proof
  let T be TopSpace;
  set F = {the carrier of T};
  F c= bool the carrier of T
  proof
    let a be set;
    assume a in F;
    then a = the carrier of T by TARSKI:def 1;
    hence thesis by ZFMISC_1:def 1;
  end;
  then reconsider F as Subset-Family of T by SETFAM_1:def 7;
  take F;
  thus F = {the carrier of T};
a1: [#]T = the carrier of T by PRE_TOPC:12;
  thus F is_a_cover_of T
  proof
    thus [#]T = union F by a1,ZFMISC_1:31;
  end;
  let P be Subset of T;
  thus thesis by a1,TARSKI:def 1;
end;

registration
  let T be TopSpace;
  cluster non empty mutually-disjoint open closed Subset-Family of T;
existence
proof
  set F = {{}T};
  F c= bool the carrier of T
  proof
    let a be set;
    assume a in F;
    then a = {}T by TARSKI:def 1;
    hence thesis;
  end;
  then reconsider F as Subset-Family of T by SETFAM_1:def 7;
  take F;
  thus F is non empty mutually-disjoint by TAXONOM2:10;
  thus F is open
  proof
    let P be Subset of T;
    thus thesis by TARSKI:def 1;
  end;
  let P be Subset of T;
  thus thesis by TARSKI:def 1;
end;
end;

theorem
  for T being TopSpace, D being mutually-disjoint open Subset-Family of T,
      A being Subset of T, X being set st 
   A is connected & X in D & X meets A & D is_a_cover_of A
  holds A c= X
proof
  let T be TopSpace;
  let D be mutually-disjoint open Subset-Family of T;
  let A be Subset of T;
  let X be set such that
Z0:  T|A is connected and
Z1:  X in D and
Z2:  X meets A;
  assume D is_a_cover_of A;
  then
Z3: A c= union D by COMPTS_1:def 1;
  let a be set;
  assume
W1: a in A;
  then consider Z being set such that
Z4:  a in Z and
Z5:  Z in D by Z3,TARSKI:def 4;
  consider x being set such that
A6:  x in X and
A7:  x in A by Z2,XBOOLE_0:3;
  set D1 = {K /\ A where K is Subset of T: K in D & a in K};
  set D2 = {K /\ A where K is Subset of T: K in D & not a in K};
x17: the carrier of T|A = A by JORDAN1:1;
x18: [#](T|A) = A by PRE_TOPC:def 10;
  D1 c= bool A
  proof
    let d be set;
    assume d in D1;
    then ex K1 being Subset of T st d = K1 /\ A & K1 in D & a in K1;
    then d c= A by XBOOLE_1:17;
    hence d in bool A;
  end;
  then reconsider D1 as Subset-Family of T|A by x17,SETFAM_1:def 7;
  D2 c= bool A
  proof
    let d be set;
    assume d in D2;
    then ex K1 being Subset of T st d = K1 /\ A & K1 in D & not a in K1;
    then d c= A by XBOOLE_1:17;
    hence d in bool A;
  end;
  then reconsider D2 as Subset-Family of T|A by x17,SETFAM_1:def 7;
  D1 is open
  proof
    let P be Subset of T|A;
    assume P in D1;
    then consider K1 being Subset of T such that
x20:  P = K1 /\ A and
x21:  K1 in D and a in K1;
    K1 is open by x21,TOPS_2:def 1;
    hence P is open by x20,x18,TOPS_2:32;
  end;
  then
x1: union D1 is open by TOPS_2:26;
  D2 is open
  proof
    let P be Subset of T|A;
    assume P in D2;
    then consider K1 being Subset of T such that
x20:  P = K1 /\ A and
x21:  K1 in D and not a in K1;
    K1 is open by x21,TOPS_2:def 1;
    hence P is open by x20,x18,TOPS_2:32;
  end;
  then
x2: union D2 is open by TOPS_2:26;
   a in Z /\ A & Z /\ A in D1 by Z4,W1,Z5,XBOOLE_0:def 3;
   then
x3: union D1 <> {}(T|A) by TARSKI:def 4;
x30: A = union D1 \/ union D2
  proof
    thus A c= union D1 \/ union D2
    proof
      let b be set;
      assume
W2:     b in A;
      then consider Z being set such that
Z4:     b in Z and
Z5:     Z in D by Z3,TARSKI:def 4;
      reconsider Z as Subset of T by Z5; 
W3:   b in Z /\ A by W2,Z4,XBOOLE_0:def 3;
      per cases;
      suppose a in Z;
      then Z /\ A in D1 by Z5;
      then b in union D1 by W3,TARSKI:def 4;
      hence thesis by XBOOLE_0:def 2;
      end;
      suppose not a in Z;
      then Z /\ A in D2 by Z5;
      then b in union D2 by W3,TARSKI:def 4;
      hence thesis by XBOOLE_0:def 2;
      end;
    end;
    thus thesis by x17;
  end;
  assume not a in X;
  then x in X /\ A & X /\ A in D2 by A6,A7,Z1,XBOOLE_0:def 3;
  then x in union D2 by TARSKI:def 4;
  then
  union D1 meets union D2 by Z0,x1,x2,x3,x18,x30,CONNSP_1:12;
  then consider y being set such that
x5:  y in union D1 and
x6:  y in union D2 by XBOOLE_0:3;
  consider Y1 being set such that
x7:  y in Y1 and
x8:  Y1 in D1 by x5,TARSKI:def 4;
  consider Y2 being set such that
x9:  y in Y2 and
x10:  Y2 in D2 by x6,TARSKI:def 4;
  consider K1 being Subset of T such that
x11:  Y1 = K1 /\ A and
x12:  K1 in D and
x13:  a in K1 by x8;
  consider K2 being Subset of T such that
x14:  Y2 = K2 /\ A and
x15:  K2 in D and
x16:  not a in K2 by x10;
  y in K1 & y in K2 by x7,x9,x11,x14,XBOOLE_0:def 3;
  then K1 meets K2 by XBOOLE_0:3;
  hence thesis by x13,x16,x12,x15,TAXONOM2:def 5;
end;

begin :: On the product of topologies

theorem Tprtop:
  for S, T being TopSpace holds
  the TopStruct of [:S,T:] = [:the TopStruct of S,the TopStruct of T:]
proof
  let S, T be TopSpace;
a1: the carrier of [:S,T:] = [:the carrier of S,the carrier of T:]
    by BORSUK_1:def 5
    .= the carrier of [:the TopStruct of S,the TopStruct of T:]
    by BORSUK_1:def 5;
  for C1 being Subset of [:S,T:],
  C2 being Subset of [:the TopStruct of S,the TopStruct of T:] st C1 = C2 holds
    C1 is open iff C2 is open
  proof
    let C1 be Subset of [:S,T:];
    let C2 be Subset of [:the TopStruct of S,the TopStruct of T:];
    assume
Z0:   C1 = C2;
    hereby 
      assume C1 is open;
      then consider A being Subset-Family of [:S,T:] such that
a2:     C1 = union A and
a3:     for e being set st e in A
         ex X1 being Subset of S,
            Y1 being Subset of T st
         e = [:X1,Y1:] & X1 is open & Y1 is open by BORSUK_1:45;
      reconsider AA = A as
        Subset-Family of [:the TopStruct of S,the TopStruct of T:] by a1;
      for e being set st e in AA
        ex X1 being Subset of the TopStruct of S,
           Y1 being Subset of the TopStruct of T st
        e = [:X1,Y1:] & X1 is open & Y1 is open
      proof
        let e be set;
        assume e in AA;
        then consider X1 being Subset of S, Y1 being Subset of T such that
a5:       e = [:X1,Y1:] & X1 is open & Y1 is open by a3;
        reconsider X2 = X1 as Subset of the TopStruct of S;
        reconsider Y2 = Y1 as Subset of the TopStruct of T;
        take X2, Y2;
        thus e = [:X2,Y2:] & X2 is open & Y2 is open by a5,TOPS_3:76;
      end;
      hence C2 is open by a2,Z0,BORSUK_1:45;
    end;
    assume C2 is open;
    then consider A being Subset-Family of
      [:the TopStruct of S,the TopStruct of T:] such that
a2:   C2 = union A and
a3:   for e being set st e in A
       ex X1 being Subset of the TopStruct of S,
          Y1 being Subset of the TopStruct of T st
       e = [:X1,Y1:] & X1 is open & Y1 is open by BORSUK_1:45;
    reconsider AA = A as Subset-Family of [:S,T:] by a1;
    for e being set st e in AA
      ex X1 being Subset of S,
         Y1 being Subset of T st
      e = [:X1,Y1:] & X1 is open & Y1 is open
    proof
      let e be set;
      assume e in AA;
      then consider X1 being Subset of the TopStruct of S,
                    Y1 being Subset of the TopStruct of T such that
a5:     e = [:X1,Y1:] & X1 is open & Y1 is open by a3;
      reconsider X2 = X1 as Subset of S;
      reconsider Y2 = Y1 as Subset of T;
      take X2, Y2;
      thus e = [:X2,Y2:] & X2 is open & Y2 is open by a5,TOPS_3:76;
    end;
    hence C1 is open by a2,Z0,BORSUK_1:45;
  end;
  hence the TopStruct of [:S,T:] = [:the TopStruct of S,the TopStruct of T:]
    by a1,TOPS_3:72;
end;

theorem Tcl1:
  for S, T being TopSpace,
      A being Subset of S, B being Subset of T holds
  Cl [:A,B:] = [:Cl A,Cl B:]
proof
  let S, T be TopSpace,
      A be Subset of S,
      B be Subset of T;
  hereby
    let x be set;
    assume
a1:   x in Cl [:A,B:];
    the carrier of [:S,T:] = [:the carrier of S, the carrier of T:]
     by BORSUK_1:def 5;
    then ex x1, x2 being set st x1 in the carrier of S & x2 in the carrier of T
      & x = [x1,x2] by a1,ZFMISC_1:def 2;
    then reconsider S1 = S, T1 = T as non empty TopSpace by STRUCT_0:def 1;
    reconsider p = x as Point of [:S1,T1:] by a1;
    consider K being Basis of p such that
a2:   for Q being Subset of [:S1,T1:] st Q in K holds [:A,B:] meets Q
        by a1,YELLOW13:17;
    consider p1 being Point of S1,
             p2 being Point of T1 such that
a3:   p = [p1,p2] by BORSUK_1:50;
    for G being Subset of S1 st G is open & p1 in G holds A meets G
    proof
      let G be Subset of S1 such that
a5:     G is open and
a6:     p1 in G;
      [#]T1 = the carrier of T1 by PRE_TOPC:12;
      then
a7:   [p1,p2] in [:G,[#]T1:] by a6,ZFMISC_1:106;
      [:G,[#]T1:] is open by a5,BORSUK_1:46;
      then consider V being Subset of [:S1,T1:] such that
a8:     V in K and
a9:     V c= [:G,[#]T1:] by a3,a7,YELLOW_8:def 2;
      [:A,B:] meets V by a2,a8;
      then consider a being set such that
a10:    a in [:A,B:] & a in V by XBOOLE_0:3;
      a in [:A,B:] /\ [:G,[#]T1:] by a9,a10,XBOOLE_0:def 3;
      then a in [:A/\G,B/\[#]T1:] by ZFMISC_1:123;
      then ex a1, a2 being set st a1 in A/\G & a2 in B/\[#]T1 & a = [a1,a2]
        by ZFMISC_1:def 2;
      hence A meets G by XBOOLE_0:def 7;
    end;
    then
a4: p1 in Cl A by PRE_TOPC:def 13;
    for G being Subset of T1 st G is open & p2 in G holds B meets G
    proof
      let G be Subset of T1 such that
a5:     G is open and
a6:     p2 in G;
      [#]S1 = the carrier of S1 by PRE_TOPC:12;
      then
a7:   [p1,p2] in [:[#]S1,G:] by a6,ZFMISC_1:106;
      [:[#]S1,G:] is open by a5,BORSUK_1:46;
      then consider V being Subset of [:S1,T1:] such that
a8:     V in K and
a9:     V c= [:[#]S1,G:] by a3,a7,YELLOW_8:def 2;
      [:A,B:] meets V by a2,a8;
      then consider a being set such that
a10:    a in [:A,B:] & a in V by XBOOLE_0:3;
      a in [:A,B:] /\ [:[#]S1,G:] by a9,a10,XBOOLE_0:def 3;
      then a in [:A/\[#]S1,B/\G:] by ZFMISC_1:123;
      then ex a1, a2 being set st a1 in A/\[#]S1 & a2 in B/\G & a = [a1,a2]
        by ZFMISC_1:def 2;
      hence B meets G by XBOOLE_0:def 7;
    end;
    then p2 in Cl B by PRE_TOPC:def 13;
    hence x in [:Cl A,Cl B:] by a3,a4,ZFMISC_1:106;
  end;
  let x be set;
  assume x in [:Cl A,Cl B:];
  then consider x1, x2 being set such that
b1: x1 in Cl A and
b2: x2 in Cl B and
b3: x = [x1,x2] by ZFMISC_1:def 2;
  reconsider S1 = S, T1 = T as non empty TopSpace by b1,b2,STRUCT_0:def 1;
  reconsider x1 as Point of S1 by b1;
  reconsider x2 as Point of T1 by b2;
  consider K1 being Basis of x1 such that
b4:  for Q being Subset of S1 st Q in K1 holds A meets Q by b1,YELLOW13:17;
  consider K2 being Basis of x2 such that
b5:  for Q being Subset of T1 st Q in K2 holds B meets Q by b2,YELLOW13:17;
  for G being Subset of [:S1,T1:] st G is open & [x1,x2] in G holds
   [:A,B:] meets G
  proof
    let G be Subset of [:S1,T1:] such that
b6:   G is open and
b7:   [x1,x2] in G;
    consider F being Subset-Family of [:S1,T1:] such that
b8:   G = union F and
b9:   for e being set st e in F
       ex X1 being Subset of S1, Y1 being Subset of T1 st
        e = [:X1,Y1:] & X1 is open & Y1 is open by b6,BORSUK_1:45;
    consider Z being set such that
b10:  [x1,x2] in Z and
b11:  Z in F by b7,b8,TARSKI:def 4;
    consider X1 being Subset of S1, Y1 being Subset of T1 such that
b12:  Z = [:X1,Y1:] and
b13:  X1 is open and
b14:  Y1 is open by b9,b11;
    x1 in X1 by b10,b12,ZFMISC_1:106;
    then consider V1 being Subset of S1 such that
b15:  V1 in K1 and
b16:  V1 c= X1 by b13,YELLOW_8:def 2;
    A meets V1 by b4,b15;
    then consider a1 being set such that
b17:  a1 in A and
b18:  a1 in V1 by XBOOLE_0:3;
    x2 in Y1 by b10,b12,ZFMISC_1:106;
    then consider V2 being Subset of T1 such that
b19:  V2 in K2 and
b20:  V2 c= Y1 by b14,YELLOW_8:def 2;
    B meets V2 by b5,b19;
    then consider a2 being set such that
b21:  a2 in B and
b22:  a2 in V2 by XBOOLE_0:3;
    [a1,a2] in Z by b12,b16,b18,b20,b22,ZFMISC_1:106;
    then
b23: [a1,a2] in union F by b11,TARSKI:def 4;
    [a1,a2] in [:A,B:] by b17,b21,ZFMISC_1:106;
    hence [:A,B:] meets G by b8,b23,XBOOLE_0:3;
  end;
  hence thesis by b3,PRE_TOPC:def 13;
end; 

theorem Tclosed:
  for S, T being TopSpace,
      A being closed Subset of S, B being closed Subset of T holds
  [:A,B:] is closed
proof
  let S, T be TopSpace;
  let A be closed Subset of S;
  let B be closed Subset of T;
A1: Cl A = A & Cl B = B by PRE_TOPC:52;
  Cl [:A,B:] = [:Cl A,Cl B:] by Tcl1;
  hence thesis by A1;
end;

registration
  let A, B be connected TopSpace;
  cluster [:A,B:] ->  connected;
coherence
proof
  set S = the TopStruct of A,
      T = the TopStruct of B;
ee: the TopStruct of [:A,B:] = [:S,T:] by Tprtop;
  per cases;
  suppose
a1: [:S,T:] is non empty;
  now
    set J = 1TopSp{0,1};
    given f being map of [:S,T:], J such that
a2:   f is continuous and
a3:   f is onto;
b2: the carrier of J = {0,1} by PCOMPS_1:8;
    then reconsider j0 = 0, j1 = 1 as Element of the carrier of J
      by TARSKI:def 2;
c3: dom f = the carrier of [:S,T:] by FUNCT_2:def 1;
c7: the carrier of [:S,T:] = [:the carrier of S, the carrier of T:]
      by BORSUK_1:def 5;
d4: rng f = the carrier of J by a3,FUNCT_2:def 3;
    then consider xy0 being set such that
c1:   xy0 in dom f and
c2:   f.xy0 = j0 by FUNCT_1:def 5;
    consider x0, y0 being set such that
c4:   x0 in the carrier of S and
c5:   y0 in the carrier of T and
c6:   xy0 = [x0,y0] by c1,c7,ZFMISC_1:def 2;
    consider xy1 being set such that
d2:   xy1 in dom f and
d3:   f.xy1 = j1 by d4,FUNCT_1:def 5;
    consider x1, y1 being set such that
c8:   x1 in the carrier of S and
c9:   y1 in the carrier of T and
d1:   xy1 = [x1,y1] by d2,c7,ZFMISC_1:def 2;
    reconsider x0, x1 as Point of S by c4,c8;
    reconsider y0, y1 as Point of T by c5,c9;
    reconsider S, T as non empty TopSpace by a1,BORSUK_3:4;
    reconsider X0 = {x0} as non empty Subset of S by ZFMISC_1:37;
    reconsider Y1 = {y1} as non empty Subset of T by ZFMISC_1:37;
    T, [:S|X0,T:] are_homeomorphic by BORSUK_3:15;
    then
e2: [:S|X0,T:] is connected by TOPREAL6:25;
    S, [:T|Y1,S:] are_homeomorphic by BORSUK_3:15;
    then
b3: [:T|Y1,S:] is connected by TOPREAL6:25;
    [:S,T|Y1:], [:T|Y1,S:] are_homeomorphic by YELLOW12:44;
    then
E2: [:S,T|Y1:] is connected by b3,TOPREAL6:25;
e8: [#]T = the carrier of T by PRE_TOPC:12;
E8: [#]S = the carrier of S by PRE_TOPC:12;
    set g = f | [:X0,[#]T:];
r3: dom g = [:X0,[#]T:] by c3,RELAT_1:91;
r4: the carrier of [:S|X0,T:] = [:the carrier of S|X0,the carrier of T:]
      by BORSUK_1:def 5;
r1: dom g = the carrier of [:S|X0,T:] by r3,r4,e8,JORDAN1:1;
    rng g c= the carrier of J;
    then reconsider g as map of [:S|X0,T:],J by r1,FUNCT_2:4;
    [:S|X0,T:] = [:S|X0,T| [#]T:] by TSEP_1:3
       .= [:S,T:] | [:X0,[#]T:] by BORSUK_3:26;
    then
e1: g is continuous by a2,TOPMETR:10;
    set h = f | [:[#]S,Y1:];
R3: dom h = [:[#]S,Y1:] by c3,RELAT_1:91;
R4: the carrier of [:S,T|Y1:] = [:the carrier of S,the carrier of T|Y1:]
      by BORSUK_1:def 5;
r2: dom h = the carrier of [:S,T|Y1:] by R3,R4,E8,JORDAN1:1;
    rng h c= the carrier of J;
    then reconsider h as map of [:S,T|Y1:],J by r2,FUNCT_2:4;
    [:S,T|Y1:] = [:S| [#]S,T|Y1:] by TSEP_1:3
       .= [:S,T:] | [:[#]S,Y1:] by BORSUK_3:26;
    then
E1: h is continuous by a2,TOPMETR:10;
b4: now
      assume
ff:     f. [x0,y1] <> 0;
      g is onto
      proof
        thus rng g c= the carrier of J;
        let y be set;
        assume
e3:       y in the carrier of J;
e7:     x0 in X0 by TARSKI:def 1;
        per cases by b2,e3,TARSKI:def 2;
        suppose
s1:       y = 0;
e6:     [x0,y0] in dom g by r3,e8,e7,ZFMISC_1:106;
        then
        g. [x0,y0] = y by s1,c6,c2,r3,FUNCT_1:72;
        hence y in rng g by e6,FUNCT_1:def 5;
        end;
        suppose
s1:       y = 1;
        [x0,y1] in dom f by c3,c7,c4,c9,ZFMISC_1:106;
        then
f1:     f. [x0,y1] in rng f by FUNCT_1:def 5;
e9:     [x0,y1] in dom g by r3,e8,e7,ZFMISC_1:106;
        then
        g. [x0,y1] = f. [x0,y1] by r3,FUNCT_1:72
          .= y by ff,s1,f1,b2,TARSKI:def 2;
        hence y in rng g by e9,FUNCT_1:def 5;
        end;
      end;
      hence contradiction by e1,e2,Tconiff;
    end;
    now
      assume
ff:     f. [x0,y1] <> 1;
      h is onto
      proof
        thus rng h c= the carrier of J;
        let y be set;
        assume
e3:       y in the carrier of J;
e7:     y1 in Y1 by TARSKI:def 1;
        per cases by b2,e3,TARSKI:def 2;
        suppose
s1:       y = 1;
e6:     [x1,y1] in dom h by R3,E8,e7,ZFMISC_1:106;
        then
        h. [x1,y1] = y by s1,R3,d3,d1,FUNCT_1:72;
        hence y in rng h by e6,FUNCT_1:def 5;
        end;
        suppose
s1:       y = 0;
        [x0,y1] in dom f by c3,c7,c4,c9,ZFMISC_1:106;
        then
f1:     f. [x0,y1] in rng f by FUNCT_1:def 5;
e9:     [x0,y1] in dom h by R3,E8,e7,ZFMISC_1:106;
        then
        h. [x0,y1] = f. [x0,y1] by R3,FUNCT_1:72
          .= y by ff,s1,f1,b2,TARSKI:def 2;
        hence y in rng h by e9,FUNCT_1:def 5;
        end;
      end;
      hence contradiction by E1,E2,Tconiff;
    end;
    hence contradiction by b4;
  end;
  hence [:A,B:] is connected by ee,a1,Tconiff;
  end;
  suppose
a1:  [:S,T:] is empty;
  for T being empty TopStruct holds T is connected;
  hence [:A,B:] is connected by a1,ee;
  end;
end;
end;

theorem
  for S, T being TopSpace, A being Subset of S, B being Subset of T
  st A is connected & B is connected holds
  [:A,B:] is connected
proof
  let S, T be TopSpace;
  let A be Subset of S;
  let B be Subset of T;
  assume S|A is connected & T|B is connected;
  then reconsider SA = S|A, TB = T|B as connected TopSpace;
  [:SA,TB:] is connected;
  hence [:S,T:] | [:A,B:] is connected by BORSUK_3:26;
end;

theorem
  for S, T be TopSpace, Y being non empty TopSpace, A being Subset of S,
      f being map of [:S,T:],Y, g being map of [:S|A,T:],Y st
    g = f | [:A,the carrier of T:] & f is continuous holds g is continuous
proof
  let S, T be TopSpace,
      Y be non empty TopSpace;
  let A be Subset of S;
  let f be map of [:S,T:],Y;
  let g be map of [:S|A,T:],Y;
  assume that
Z0: g = f | [:A,the carrier of T:] and
Z1: f is continuous;
  set TT = the TopStruct of T;
Z7: the TopStruct of [:S,T:] = [:the TopStruct of S,the TopStruct of T:]
     by Tprtop;
Z6: the carrier of [:S,TT:] = [:the carrier of S,the carrier of TT:]
    by BORSUK_1:def 5;
  the carrier of [:S,T:] = [:the carrier of S,the carrier of T:]
    by BORSUK_1:def 5;
  then reconsider ff = f as map of [:S,TT:],Y by Z6;
Z9: the carrier of [:S|A,TT:] = [:the carrier of S|A,the carrier of TT:]
    by BORSUK_1:def 5;
  the carrier of [:S|A,T:] = [:the carrier of S|A,the carrier of T:]
    by BORSUK_1:def 5;
  then reconsider gg = g as map of [:S|A,TT:],Y by Z9;
Z3: the TopStruct of [:S,TT:] = the TopStruct of [:S,T:] by Z7,Tprtop;
Z4: gg = ff| [:A,[#]TT:] by Z0,PRE_TOPC:12;
Z12:  [:S|A,TT:] = [:S|A,TT| [#]TT:] by TSEP_1:3
    .= [:S,TT:]| [:A,[#]TT:] by BORSUK_3:26;
  the TopStruct of [:S|A,TT:] = the TopStruct of [:S|A,T:] by Tprtop;
  hence g is continuous by Z12,Z1,Z3,Z4,TOPMETR:10;
end;

theorem
  for S, T be TopSpace, Y being non empty TopSpace, A being Subset of T,
      f being map of [:S,T:],Y, g being map of [:S,T|A:],Y st
    g = f | [:the carrier of S,A:] & f is continuous holds g is continuous
proof
  let S, T be TopSpace,
      Y be non empty TopSpace;
  let A be Subset of T;
  let f be map of [:S,T:],Y;
  let g be map of [:S,T|A:],Y;
  assume that
Z0: g = f | [:the carrier of S,A:] and
Z1: f is continuous;
  set SS = the TopStruct of S;
Z7: the TopStruct of [:S,T:] = [:the TopStruct of S,the TopStruct of T:]
     by Tprtop;
Z6: the carrier of [:SS,T:] = [:the carrier of SS,the carrier of T:]
    by BORSUK_1:def 5;
  the carrier of [:S,T:] = [:the carrier of S,the carrier of T:]
    by BORSUK_1:def 5;
  then reconsider ff = f as map of [:SS,T:],Y by Z6;
Z9: the carrier of [:SS,T|A:] = [:the carrier of SS,the carrier of T|A:]
    by BORSUK_1:def 5;
  the carrier of [:S,T|A:] = [:the carrier of S,the carrier of T|A:]
    by BORSUK_1:def 5;
  then reconsider gg = g as map of [:SS,T|A:],Y by Z9;
Z3: the TopStruct of [:SS,T:] = the TopStruct of [:S,T:] by Z7,Tprtop;
Z4: gg = ff| [:[#]SS,A:] by Z0,PRE_TOPC:12;
Z12:  [:SS,T|A:] = [:SS| [#]SS,T|A:] by TSEP_1:3
    .= [:SS,T:]| [:[#]SS,A:] by BORSUK_3:26;
  the TopStruct of [:SS,T|A:] = the TopStruct of [:S,T|A:] by Tprtop;
  hence g is continuous by Z12,Z1,Z3,Z4,TOPMETR:10;
end;

theorem
  for S, T, T1, T2, Y being non empty TopSpace,
      f being map of [:Y,T1:],S,
      g being map of [:Y,T2:],S,
      F1, F2 being closed Subset of T st
   T1 is SubSpace of T & T2 is SubSpace of T & F1 = [#]T1 & F2 = [#]T2 &
   ([#]T1) \/ ([#]T2) = [#]T &
    f is continuous & g is continuous &
   (for p being set st p in ([#][:Y,T1:]) /\ ([#][:Y,T2:]) holds f.p = g.p)
  ex h being map of [:Y,T:],S st h = f+*g & h is continuous
proof
  let S, T, T1, T2, Y be non empty TopSpace,
      f be map of [:Y,T1:],S,
      g be map of [:Y,T2:],S,
      F1, F2 be closed Subset of T;
  assume that
A1: T1 is SubSpace of T and
r1: T2 is SubSpace of T and
A2: F1 = [#]T1 and
R2: F2 = [#]T2 and
A3: ([#]T1) \/ ([#]T2) = [#]T and
A6: f is continuous and
A7: g is continuous and
A8: for p being set st p in ([#][:Y,T1:]) /\ ([#][:Y,T2:]) holds f.p = g.p;
a1: [:Y,T1:] is SubSpace of [:Y,T:] & [:Y,T2:] is SubSpace of [:Y,T:]
      by A1,r1,BORSUK_3:17;
    set h = f+*g;
xt: the carrier of [:Y,T:] = [#][:Y,T:] by PRE_TOPC:12; 
x1: the carrier of [:Y,T1:] = [#][:Y,T1:] by PRE_TOPC:12; 
x2: the carrier of [:Y,T2:] = [#][:Y,T2:] by PRE_TOPC:12; 
ct: the carrier of T = [#]T by PRE_TOPC:12; 
c1: the carrier of T1 = [#]T1 by PRE_TOPC:12; 
c2: the carrier of T2 = [#]T2 by PRE_TOPC:12; 
tt: the carrier of [:Y,T:] = [:the carrier of Y,the carrier of T:]
      by BORSUK_1:def 5;
t1: the carrier of [:Y,T1:] = [:the carrier of Y,the carrier of T1:]
      by BORSUK_1:def 5;
t2: the carrier of [:Y,T2:] = [:the carrier of Y,the carrier of T2:]
      by BORSUK_1:def 5;
A9: dom f = the carrier of [:Y,T1:] by FUNCT_2:def 1;
A10: dom g = the carrier of [:Y,T2:] by FUNCT_2:def 1;
A11: dom h = dom f \/ dom g by FUNCT_4:def 1;
    then
a11: dom h = [:the carrier of Y,the carrier of T:]
        by A3,A9,A10,t1,t2,c1,c2,ct,ZFMISC_1:120;
    rng f \/ rng g c= the carrier of S & rng h c= rng f \/ rng g
     by FUNCT_4:18;  then
    rng h c= the carrier of S by XBOOLE_1:1;
    then reconsider h as map of [:Y,T:],S by tt,a11,FUNCT_2:4;
    take h;
    thus h = f+*g;
    for P being Subset of S st P is closed holds h"P is closed
    proof
    let P be Subset of S;
    assume
A12:  P is closed;
A14: h"P = h"P /\ ([#][:Y,T1:] \/ [#][:Y,T2:])
       by x1,x2,A9,A10,tt,a11,A11,XBOOLE_1:28
        .= (h"P /\ [#][:Y,T1:]) \/ (h"P /\ [#][:Y,T2:]) by XBOOLE_1:23;
A15: for x being set st x in [#][:Y,T1:] holds h.x = f.x
      proof
       let x be set such that
A16:     x in [#][:Y,T1:];
       now per cases;
        suppose
A17:      x in [#][:Y,T2:];
        then x in [#][:Y,T1:] /\ [#][:Y,T2:] by A16,XBOOLE_0:def 3;
        then f.x = g.x by A8;
        hence thesis by A10,A17,FUNCT_4:14;
        end;
        suppose not x in [#][:Y,T2:];
        hence h.x = f.x by x2,A10,FUNCT_4:12;
        end;
       end;
       hence thesis;
      end;
      now let x be set;
      thus x in h"P /\ [#][:Y,T1:] implies x in f"P
       proof
        assume x in h"P /\ [#][:Y,T1:];
        then x in h"P & x in [#][:Y,T1:] by XBOOLE_0:def 3;
        then h.x in P & x in dom f & f.x = h.x by A9,A15,FUNCT_1:def 13;
        hence x in f"P by FUNCT_1:def 13;
       end;
      assume x in f"P;
      then x in dom f & f.x in P by FUNCT_1:def 13;
      then x in dom h & x in [#][:Y,T1:] & h.x in P
        by x1,A11,A15,XBOOLE_0:def 2;
      then x in h"P & x in [#][:Y,T1:] by FUNCT_1:def 13;
      hence x in h"P /\ [#][:Y,T1:] by XBOOLE_0:def 3;
     end;
     then
A18:  h"P /\ [#][:Y,T1:] = f"P by TARSKI:2;
     now let x be set;
      thus x in h"P /\ [#][:Y,T2:] implies x in g"P
       proof
        assume x in h"P /\ [#][:Y,T2:];
        then x in h"P & x in [#][:Y,T2:] by XBOOLE_0:def 3;
        then h.x in P & x in dom g & g.x = h.x
          by A10,FUNCT_1:def 13,FUNCT_4:14;
        hence x in g"P by FUNCT_1:def 13;
       end;
      assume x in g"P;
      then x in dom g & g.x in P by FUNCT_1:def 13;
      then x in dom h & x in [#][:Y,T2:] & h.x in P
        by x2,A11,FUNCT_4:14,XBOOLE_0:def 2;
      then x in h"P & x in [#][:Y,T2:] by FUNCT_1:def 13;
      hence x in h"P /\ [#][:Y,T2:] by XBOOLE_0:def 3;
     end;
     then
A19: h"P = f"P \/ g"P by A14,A18,TARSKI:2;
     the carrier of T1 is Subset of the carrier of T by A1,TSEP_1:1;
     then f"P c= [#][:Y,T1:] & [#][:Y,T1:] c= [#][:Y,T:]
       by t1,x1,tt,xt,ZFMISC_1:118;
    then f"P c= [#][:Y,T:] by XBOOLE_1:1;
    then reconsider P1 = f"P as Subset of [:Y,T:] by PRE_TOPC:12;
    the carrier of T2 is Subset of the carrier of T by r1,TSEP_1:1;
    then g"P c= [#][:Y,T2:] & [#][:Y,T2:] c= [#][:Y,T:]
      by t2,x2,tt,xt,ZFMISC_1:118;
    then g"P c= [#][:Y,T:] by XBOOLE_1:1;
    then reconsider P2 = g"P as Subset of [:Y,T:] by PRE_TOPC:12;
     f"P is closed by A6,A12,PRE_TOPC:def 12;
     then consider F01 being Subset of [:Y,T:] such that
A21:  F01 is closed & f"P = F01 /\ [#][:Y,T1:] by a1,PRE_TOPC:43;
    reconsider M = [:[#]Y,F1:] as Subset of [:Y,T:];
b1: M is closed by Tclosed;
    [#][:Y,T1:] = [:[#]Y,F1:] by A2,BORSUK_3:1;
    then
A22: P1 is closed by A21,b1,TOPS_1:35;
    g"P is closed by A7,A12,PRE_TOPC:def 12;
    then consider F02 being Subset of [:Y,T:] such that
A23:  F02 is closed & g"P = F02 /\ [#][:Y,T2:] by a1,PRE_TOPC:43;
    reconsider M = [:[#]Y,F2:] as Subset of [:Y,T:];
b1: M is closed by Tclosed;
    [#][:Y,T2:] = [:[#]Y,F2:] by R2,BORSUK_3:1;
    then
    P2 is closed by A23,b1,TOPS_1:35;
    hence h"P is closed by A19,A22,TOPS_1:36;
  end;
  hence thesis by PRE_TOPC:def 12;
end;

theorem
  for S, T, T1, T2, Y being non empty TopSpace,
      f being map of [:T1,Y:],S,
      g being map of [:T2,Y:],S,
      F1, F2 being closed Subset of T st
   T1 is SubSpace of T & T2 is SubSpace of T & F1 = [#]T1 & F2 = [#]T2 &
   ([#]T1) \/ ([#]T2) = [#]T &
    f is continuous & g is continuous &
   (for p being set st p in ([#][:T1,Y:]) /\ ([#][:T2,Y:]) holds f.p = g.p)
  ex h being map of [:T,Y:],S st h = f+*g & h is continuous
proof
  let S, T, T1, T2, Y be non empty TopSpace,
      f be map of [:T1,Y:],S,
      g be map of [:T2,Y:],S,
      F1, F2 be closed Subset of T;
  assume that
A1: T1 is SubSpace of T and
r1: T2 is SubSpace of T and
A2: F1 = [#]T1 and
R2: F2 = [#]T2 and
A3: ([#]T1) \/ ([#]T2) = [#]T and
A6: f is continuous and
A7: g is continuous and
A8: for p being set st p in ([#][:T1,Y:]) /\ ([#][:T2,Y:]) holds f.p = g.p;
    Y is SubSpace of Y by TSEP_1:2;
    then
a1: [:T1,Y:] is SubSpace of [:T,Y:] & [:T2,Y:] is SubSpace of [:T,Y:]
      by A1,r1,BORSUK_3:25;
    set h = f+*g;
xt: the carrier of [:T,Y:] = [#][:T,Y:] by PRE_TOPC:12; 
x1: the carrier of [:T1,Y:] = [#][:T1,Y:] by PRE_TOPC:12; 
x2: the carrier of [:T2,Y:] = [#][:T2,Y:] by PRE_TOPC:12; 
ct: the carrier of T = [#]T by PRE_TOPC:12; 
c1: the carrier of T1 = [#]T1 by PRE_TOPC:12; 
c2: the carrier of T2 = [#]T2 by PRE_TOPC:12; 
tt: the carrier of [:T,Y:] = [:the carrier of T,the carrier of Y:]
      by BORSUK_1:def 5;
t1: the carrier of [:T1,Y:] = [:the carrier of T1,the carrier of Y:]
      by BORSUK_1:def 5;
t2: the carrier of [:T2,Y:] = [:the carrier of T2,the carrier of Y:]
      by BORSUK_1:def 5;
A9: dom f = the carrier of [:T1,Y:] by FUNCT_2:def 1;
A10: dom g = the carrier of [:T2,Y:] by FUNCT_2:def 1;
A11: dom h = dom f \/ dom g by FUNCT_4:def 1;
    then
a11: dom h = [:the carrier of T,the carrier of Y:]
        by A3,A9,A10,t1,t2,c1,c2,ct,ZFMISC_1:120;
    rng f \/ rng g c= the carrier of S & rng h c= rng f \/ rng g
     by FUNCT_4:18;  then
    rng h c= the carrier of S by XBOOLE_1:1;
    then reconsider h as map of [:T,Y:],S by tt,a11,FUNCT_2:4;
    take h;
    thus h = f+*g;
    for P being Subset of S st P is closed holds h"P is closed
    proof
    let P be Subset of S;
    assume
A12:  P is closed;
A14: h"P = h"P /\ ([#][:T1,Y:] \/ [#][:T2,Y:])
       by x1,x2,A9,A10,tt,a11,A11,XBOOLE_1:28
        .= (h"P /\ [#][:T1,Y:]) \/ (h"P /\ [#][:T2,Y:]) by XBOOLE_1:23;
A15: for x being set st x in [#][:T1,Y:] holds h.x = f.x
      proof
       let x be set such that
A16:     x in [#][:T1,Y:];
       now per cases;
        suppose
A17:      x in [#][:T2,Y:];
        then x in [#][:T1,Y:] /\ [#][:T2,Y:] by A16,XBOOLE_0:def 3;
        then f.x = g.x by A8;
        hence thesis by A10,A17,FUNCT_4:14;
        end;
        suppose not x in [#][:T2,Y:];
        hence h.x = f.x by x2,A10,FUNCT_4:12;
        end;
       end;
       hence thesis;
      end;
      now let x be set;
      thus x in h"P /\ [#][:T1,Y:] implies x in f"P
       proof
        assume x in h"P /\ [#][:T1,Y:];
        then x in h"P & x in [#][:T1,Y:] by XBOOLE_0:def 3;
        then h.x in P & x in dom f & f.x = h.x by A9,A15,FUNCT_1:def 13;
        hence x in f"P by FUNCT_1:def 13;
       end;
      assume x in f"P;
      then x in dom f & f.x in P by FUNCT_1:def 13;
      then x in dom h & x in [#][:T1,Y:] & h.x in P
        by x1,A11,A15,XBOOLE_0:def 2;
      then x in h"P & x in [#][:T1,Y:] by FUNCT_1:def 13;
      hence x in h"P /\ [#][:T1,Y:] by XBOOLE_0:def 3;
     end;
     then
A18:  h"P /\ [#][:T1,Y:] = f"P by TARSKI:2;
     now let x be set;
      thus x in h"P /\ [#][:T2,Y:] implies x in g"P
       proof
        assume x in h"P /\ [#][:T2,Y:];
        then x in h"P & x in [#][:T2,Y:] by XBOOLE_0:def 3;
        then h.x in P & x in dom g & g.x = h.x
          by A10,FUNCT_1:def 13,FUNCT_4:14;
        hence x in g"P by FUNCT_1:def 13;
       end;
      assume x in g"P;
      then x in dom g & g.x in P by FUNCT_1:def 13;
      then x in dom h & x in [#][:T2,Y:] & h.x in P
        by x2,A11,FUNCT_4:14,XBOOLE_0:def 2;
      then x in h"P & x in [#][:T2,Y:] by FUNCT_1:def 13;
      hence x in h"P /\ [#][:T2,Y:] by XBOOLE_0:def 3;
     end;
     then
A19: h"P = f"P \/ g"P by A14,A18,TARSKI:2;
     the carrier of T1 is Subset of the carrier of T by A1,TSEP_1:1;
     then f"P c= [#][:T1,Y:] & [#][:T1,Y:] c= [#][:T,Y:]
       by t1,x1,tt,xt,ZFMISC_1:118;
    then f"P c= [#][:T,Y:] by XBOOLE_1:1;
    then reconsider P1 = f"P as Subset of [:T,Y:] by PRE_TOPC:12;
    the carrier of T2 is Subset of the carrier of T by r1,TSEP_1:1;
    then g"P c= [#][:T2,Y:] & [#][:T2,Y:] c= [#][:T,Y:]
      by t2,x2,tt,xt,ZFMISC_1:118;
    then g"P c= [#][:T,Y:] by XBOOLE_1:1;
    then reconsider P2 = g"P as Subset of [:T,Y:] by PRE_TOPC:12;
     f"P is closed by A6,A12,PRE_TOPC:def 12;
     then consider F01 being Subset of [:T,Y:] such that
A21:  F01 is closed & f"P = F01 /\ [#][:T1,Y:] by a1,PRE_TOPC:43;
    reconsider M = [:F1,[#]Y:] as Subset of [:T,Y:];
b1: M is closed by Tclosed;
    [#][:T1,Y:] = [:F1,[#]Y:] by A2,BORSUK_3:1;
    then
A22: P1 is closed by A21,b1,TOPS_1:35;
    g"P is closed by A7,A12,PRE_TOPC:def 12;
    then consider F02 being Subset of [:T,Y:] such that
A23:  F02 is closed & g"P = F02 /\ [#][:T2,Y:] by a1,PRE_TOPC:43;
    reconsider M = [:F2,[#]Y:] as Subset of [:T,Y:];
b1: M is closed by Tclosed;
    [#][:T2,Y:] = [:F2,[#]Y:] by R2,BORSUK_3:1;
    then
    P2 is closed by A23,b1,TOPS_1:35;
    hence h"P is closed by A19,A22,TOPS_1:36;
  end;
  hence thesis by PRE_TOPC:def 12;
end;

begin :: On the fundamental groups

registration
  let T be non empty TopSpace, t be Point of T;
  cluster -> continuous Loop of t;
coherence
proof
  set f = I[01] --> t;
a1: f is continuous by BORSUK_1:36;
  f.j0 = t & f.j1 = t by Tstrz;
  hence thesis by a1,BORSUK_2:def 1;
end;
end;

theorem
  for T being non empty TopSpace, t being Point of T,
      x being Point of I[01], P being constant Loop of t holds
  P.x = t
proof
  let T be non empty TopSpace,
      t be Point of T,
      x be Point of I[01],
      P be constant Loop of t;
  P = I[01] --> t by BORSUK_2:6;
  hence P.x = t by Tstrz;
end;

theorem Tlo1:
  for T being non empty TopSpace, t being Point of T, P being Loop of t holds
  P.0 = t & P.1 = t
proof
  let T be non empty TopSpace,
      t be Point of T,
      P be Loop of t;
  set f = I[01] --> t;
a1: f is continuous by BORSUK_1:36;
  f.j0 = t & f.j1 = t by Tstrz;
  hence thesis by a1,BORSUK_2:def 1;
end;

theorem T120:
  for S, T being non empty TopSpace,
      f being continuous map of S,T,
      a, b being Point of S st a,b are_connected holds
   f.a,f.b are_connected
proof
  let S, T be non empty TopSpace;
  let f be continuous map of S,T;
  let a, b be Point of S;
  given g being map of I[01], S such that
a1: g is continuous and
a2: g.0 = a and
a3: g.1 = b;
  take h = f*g;
  thus h is continuous by a1,TOPS_2:58;
  thus h.0 = f.(g.j0) by FUNCT_2:21
    .= f.a by a2;
  thus h.1 = f.(g.j1) by FUNCT_2:21
    .= f.b by a3;
end;

theorem
  for S, T being non empty TopSpace,
      f being continuous map of S,T,
      a, b being Point of S,
      P being Path of a,b st a,b are_connected
   holds f*P is Path of f.a,f.b
proof
  let S, T be non empty TopSpace;
  let f be continuous map of S,T;
  let a, b be Point of S;
  let P be Path of a,b;
  assume a,b are_connected;
  then
a1: ex f being map of I[01], S st f is continuous & f.0 = a & f.1 = b
    by BORSUK_6:def 3;
  then P is continuous by BORSUK_2:def 1;
  then
B1: f*P is continuous by TOPS_2:58;
B2:(f*P).0 = f.(P.j0) by FUNCT_2:21
          .= f.a by a1,BORSUK_2:def 1;
  (f*P).1 = f.(P.j1) by FUNCT_2:21
         .= f.b by a1,BORSUK_2:def 1;
  hence thesis by B1,B2,BORSUK_2:def 1;
end;

theorem 
  for S being non empty arcwise_connected TopSpace,
      T being non empty TopSpace,
      f being continuous map of S,T,
      a, b being Point of S,
      P being Path of a,b
   holds f*P is Path of f.a,f.b
proof
  let S be non empty arcwise_connected TopSpace;
  let T be non empty TopSpace;
  let f be continuous map of S,T;
  let a, b be Point of S;
  let P be Path of a,b;
  a,b are_connected by BORSUK_6:47;
  then
a1: ex f being map of I[01], S st f is continuous & f.0 = a & f.1 = b
    by BORSUK_6:def 3;
B2:(f*P).0 = f.(P.j0) by FUNCT_2:21
          .= f.a by a1,BORSUK_2:def 1;
  (f*P).1 = f.(P.j1) by FUNCT_2:21
         .= f.b by a1,BORSUK_2:def 1;
  hence thesis by B2,BORSUK_2:def 1;
end;

theorem T127a:
  for S, T being non empty TopSpace,
      f being continuous map of S,T,
      a being Point of S,
      P being Loop of a
   holds f*P is Loop of f.a
proof
  let S, T be non empty TopSpace;
  let f be continuous map of S,T;
  let a be Point of S;
  let P be Loop of a;
B2:(f*P).0 = f.(P.j0) by FUNCT_2:21
          .= f.a by Tlo1;
  (f*P).1 = f.(P.j1) by FUNCT_2:21
         .= f.a by Tlo1;
  hence thesis by B2,BORSUK_2:def 1;
end;

theorem T123:
  for S, T being non empty TopSpace,
      f being continuous map of S,T,
      a, b being Point of S,
      P, Q being Path of a,b,
      P1, Q1 being Path of f.a,f.b st P,Q are_homotopic & P1 = f*P & Q1 = f*Q
  holds P1,Q1 are_homotopic
proof
  let S, T be non empty TopSpace;
  let f be continuous map of S,T;
  let a, b be Point of S;
  let P, Q be Path of a,b;
  let P1, Q1 be Path of f.a,f.b;
  assume that
Z0: P,Q are_homotopic and
Z1: P1 = f*P and
Z2: Q1 = f*Q;
  consider F being Homotopy of P,Q;
  take G = f*F;
  F is continuous by Z0,BORSUK_6:def 13;
  hence G is continuous by TOPS_2:58;
  let s be Point of I[01];
  thus G.(s,0) = f.(F.(s,j0)) by Lm8,Tzb
    .= f.(P.s) by Z0,BORSUK_6:def 13
    .= P1.s by Z1,FUNCT_2:21;
  thus G.(s,1) = f.(F.(s,j1)) by Lm8,Tzb
    .= f.(Q.s) by Z0,BORSUK_6:def 13
    .= Q1.s by Z2,FUNCT_2:21;
  let t be Point of I[01];
  thus G.(0,t) = f.(F.(j0,t)) by Lm8,Tzb
    .= f.a by Z0,BORSUK_6:def 13;
  thus G.(1,t) = f.(F.(j1,t)) by Lm8,Tzb
    .= f.b by Z0,BORSUK_6:def 13;
end;

theorem
  for S, T being non empty TopSpace,
      f being continuous map of S,T,
      a, b being Point of S,
      P, Q being Path of a,b,
      P1, Q1 being Path of f.a,f.b,
      F being Homotopy of P,Q st P,Q are_homotopic & P1 = f*P & Q1 = f*Q
  holds f*F is Homotopy of P1,Q1
proof
  let S, T be non empty TopSpace;
  let f be continuous map of S,T;
  let a, b be Point of S;
  let P, Q be Path of a,b;
  let P1, Q1 be Path of f.a,f.b;
  let F be Homotopy of P,Q;
  assume that
Z0: P,Q are_homotopic and
Z1: P1 = f*P and
Z2: Q1 = f*Q;
  set G = f*F;
  thus P1,Q1 are_homotopic by Z0,Z1,Z2,T123;
  F is continuous by Z0,BORSUK_6:def 13;
  hence G is continuous by TOPS_2:58;
  let s be Point of I[01];
  thus G.(s,0) = f.(F.(s,j0)) by Lm8,Tzb
    .= f.(P.s) by Z0,BORSUK_6:def 13
    .= P1.s by Z1,FUNCT_2:21;
  thus G.(s,1) = f.(F.(s,j1)) by Lm8,Tzb
    .= f.(Q.s) by Z0,BORSUK_6:def 13
    .= Q1.s by Z2,FUNCT_2:21;
  let t be Point of I[01];
  thus G.(0,t) = f.(F.(j0,t)) by Lm8,Tzb
    .= f.a by Z0,BORSUK_6:def 13;
  thus G.(1,t) = f.(F.(j1,t)) by Lm8,Tzb
    .= f.b by Z0,BORSUK_6:def 13;
end;

theorem T121:
  for S, T being non empty TopSpace,
      f being continuous map of S,T,
      a, b, c being Point of S,
      P being Path of a,b,
      Q being Path of b,c,
      P1 being Path of f.a,f.b,
      Q1 being Path of f.b,f.c
  st a,b are_connected & b,c are_connected & P1 = f*P & Q1 = f*Q
  holds P1+Q1 = f*(P+Q)
proof
  let S, T be non empty TopSpace;
  let f be continuous map of S,T;
  let a, b, c be Point of S;
  let P be Path of a,b;
  let Q be Path of b,c;
  let P1 be Path of f.a,f.b;
  let Q1 be Path of f.b,f.c;
  assume that
Z0: a,b are_connected and
Q1: b,c are_connected and
Z1: P1 = f*P and
Z2: Q1 = f*Q;
  for x being Point of I[01] holds (P1+Q1).x = (f*(P+Q)).x
  proof
    let x be Point of I[01];
a0: ex h being map of I[01], S st h is continuous & h.0 = a & h.1 = b
      by Z0,BORSUK_6:def 3;
a3: ex h being map of I[01], S st h is continuous & h.0 = b & h.1 = c
      by Q1,BORSUK_6:def 3;
    f.a,f.b are_connected by Z0,T120;
    then
a1: ex h being map of I[01], T st h is continuous & h.0 = f.a & h.1 = f.b
      by BORSUK_6:def 3;
    f.b,f.c are_connected by Q1,T120;
    then
a4: ex h being map of I[01], T st h is continuous & h.0 = f.b & h.1 = f.c
      by BORSUK_6:def 3;
    per cases;
    suppose
s1:   x <= 1/2;
    then
a2: 2*x is Point of I[01] by BORSUK_6:6;
    thus (P1+Q1).x = P1.(2*x) by a1,a4,s1,BORSUK_2:def 4
      .= f.(P.(2*x)) by Z1,a2,FUNCT_2:21
      .= f.((P+Q).x) by a0,a3,s1,BORSUK_2:def 4
      .= (f*(P+Q)).x by FUNCT_2:21;
    end;
    suppose
s1:   x >= 1/2;
    then
a2: 2*x-1 is Point of I[01] by BORSUK_6:7;
    thus (P1+Q1).x = Q1.(2*x-1) by a1,a4,s1,BORSUK_2:def 4
      .= f.(Q.(2*x-1)) by Z2,a2,FUNCT_2:21
      .= f.((P+Q).x) by a0,a3,s1,BORSUK_2:def 4
      .= (f*(P+Q)).x by FUNCT_2:21;
    end;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Tzlpl:
  for S being non empty TopSpace, s being Point of S
  for x, y being Element of pi_1(S,s)
  for P, Q being Loop of s st x = Class(EqRel(S,s),P) & y = Class(EqRel(S,s),Q)
    holds x*y = Class(EqRel(S,s),P+Q)
proof
  let S be non empty TopSpace;
  let s be Point of S;
  set X = pi_1(S,s);
  let x, y be Element of X;
  let P, Q be Loop of s;
  assume
Z0: x = Class(EqRel(S,s),P) & y = Class(EqRel(S,s),Q);
  consider P1, Q1 being Loop of s such that
Z2: x = Class(EqRel(S,s),P1) & y = Class(EqRel(S,s),Q1) and
Z4: (the mult of X).(x,y) = Class(EqRel(S,s),P1+Q1) by TOPALG_1:def 3;
  P,P1 are_homotopic & Q,Q1 are_homotopic by Z0,Z2,TOPALG_1:47;
  then
Z5: P+Q,P1+Q1 are_homotopic by BORSUK_6:83;
  thus x*y = (the mult of X).(x,y) by VECTSP_1:def 10
    .= Class(EqRel(S,s),P+Q) by Z4,Z5,TOPALG_1:47;
end;

definition
  let S, T be non empty TopSpace,
      s be Point of S,
      f be map of S,T such that
C1: f is continuous;
  set pS = pi_1(S,s);
  set pT = pi_1(T,f.s);
  func FundGrIso(f,s) -> map of pi_1(S,s),pi_1(T,f.s) means :dhi:
  for x being Element of pi_1(S,s)
   ex ls being Loop of s, lt being Loop of f.s st
     x = Class(EqRel(S,s),ls) & lt = f*ls & it.x = Class(EqRel(T,f.s),lt);
existence
proof
  defpred P[set,set] means
  ex ls being Loop of s, lt being Loop of f.s st
    $1 = Class(EqRel(S,s),ls) & lt = f*ls & $2 = Class(EqRel(T,f.s),lt);
A0: for x being Element of pS ex y being Element of pT st P[x,y]
    proof
      let x be Element of pS;
      consider ls being Loop of s such that
A1:     x = Class(EqRel(S,s),ls) by TOPALG_1:48;
      reconsider lt = f*ls as Loop of f.s by C1,T127a;
      reconsider y = Class(EqRel(T,f.s),lt) as Element of pT by TOPALG_1:48;
      take y, ls, lt;
      thus thesis by A1;
    end;
  ex h being map of pS,pT st for x being Element of pS holds P[x,h.x]
    from FUNCT_2:sch 3(A0);
  hence thesis;
end;
uniqueness
proof
  let g, h be map of pS,pT such that
a1: for x being Element of pS
     ex ls being Loop of s, lt being Loop of f.s st
      x = Class(EqRel(S,s),ls) & lt = f*ls & g.x = Class(EqRel(T,f.s),lt) and
a2: for x being Element of pS
     ex ls being Loop of s, lt being Loop of f.s st
      x = Class(EqRel(S,s),ls) & lt = f*ls & h.x = Class(EqRel(T,f.s),lt);
  now
    let x be Element of pS;
    consider lsg being Loop of s, ltg being Loop of f.s such that
a3:   x = Class(EqRel(S,s),lsg) and
a4:   ltg = f*lsg and
a5:   g.x = Class(EqRel(T,f.s),ltg) by a1;
    consider lsh being Loop of s, lth being Loop of f.s such that
a6:   x = Class(EqRel(S,s),lsh) and
a7:   lth = f*lsh and
a8:   h.x = Class(EqRel(T,f.s),lth) by a2;
    lsg,lsh are_homotopic by a3,a6,TOPALG_1:47;
    then ltg,lth are_homotopic by C1,a4,a7,T123;
    hence g.x = h.x by a5,a8,TOPALG_1:47;
  end;
  hence thesis by FUNCT_2:113;
end;
end;

theorem
  for S, T being non empty TopSpace, s being Point of S,
      f being continuous map of S,T,
      x being Element of pi_1(S,s),
      ls being Loop of s, lt being Loop of f.s
  st x = Class(EqRel(S,s),ls) & lt = f*ls
  holds FundGrIso(f,s).x = Class(EqRel(T,f.s),lt)
proof
  let S, T be non empty TopSpace;
  let s be Point of S;
  let f be continuous map of S,T;
  let x be Element of pi_1(S,s);
  let ls be Loop of s;
  let lt be Loop of f.s;
  assume that
Z0: x = Class(EqRel(S,s),ls) and
Z1: lt = f*ls;
  consider ls1 being Loop of s, lt1 being Loop of f.s such that
a1:  x = Class(EqRel(S,s),ls1) and
a2:  lt1 = f*ls1 and
a3:  FundGrIso(f,s).x = Class(EqRel(T,f.s),lt1) by dhi;
  ls,ls1 are_homotopic by Z0,a1,TOPALG_1:47;
  then lt,lt1 are_homotopic by Z1,a2,T123;
  hence FundGrIso(f,s).x = Class(EqRel(T,f.s),lt) by a3,TOPALG_1:47;
end;

definition
  let S, T be non empty TopSpace,
      s be Point of S,
      f be continuous map of S,T;
  redefine func FundGrIso(f,s) -> Homomorphism of pi_1(S,s),pi_1(T,f.s);
coherence
proof
  set pS = pi_1(S,s);
  set pT = pi_1(T,f.s);
  set h = FundGrIso(f,s);
  let a, b be Element of pS; 
  consider lsa being Loop of s, lta being Loop of f.s such that
Z2: a = Class(EqRel(S,s),lsa) and
A1: lta = f*lsa and
Z3: h.a = Class(EqRel(T,f.s),lta) by dhi;
  consider lsb being Loop of s, ltb being Loop of f.s such that
Z4: b = Class(EqRel(S,s),lsb) and
A2: ltb = f*lsb and
Z5: h.b = Class(EqRel(T,f.s),ltb) by dhi;
  consider lsab being Loop of s, ltab being Loop of f.s such that
Z6: a*b = Class(EqRel(S,s),lsab) and
A3: ltab = f*lsab and
Z7: h.(a*b) = Class(EqRel(T,f.s),ltab) by dhi;
C4: lta+ltb = f*(lsa+lsb) by A1,A2,T121;
  a*b = Class(EqRel(S,s),lsa+lsb) by Z2,Z4,Tzlpl;
  then lsab,lsa+lsb are_homotopic by Z6,TOPALG_1:47;
  then ltab,lta+ltb are_homotopic by A3,C4,T123;
  hence h.(a * b) = Class(EqRel(T,f.s),lta+ltb) by Z7,TOPALG_1:47
     .= h.a * h.b by Z3,Z5,Tzlpl;
end;
end;

theorem Thomoiso1:
  for S, T being non empty TopSpace, s being Point of S,
      f being continuous map of S,T st f is_homeomorphism 
  holds FundGrIso(f,s) is_isomorphism
proof
  let S, T be non empty TopSpace;
  let s be Point of S;
  let f be continuous map of S,T such that
Z0:  f is_homeomorphism;
  set pS = pi_1(S,s);
  set pT = pi_1(T,f.s);
c9: f" is continuous by Z0,TOPS_2:def 5;
    set h = FundGrIso(f,s);
b4: dom h = the carrier of pS by FUNCT_2:def 1;
C6: dom f = [#]S by Z0,TOPS_2:def 5;
c6: rng f = [#]T by Z0,TOPS_2:def 5;
B4: [#]S = the carrier of S by PRE_TOPC:12;
d2: [#]T = the carrier of T by PRE_TOPC:12;
c7: f is one-to-one by Z0,TOPS_2:def 5;
    then
c4: f qua Function".(f.s) = s by FUNCT_2:32;
c5: f qua Function" = f" by c6,c7,TOPS_2:def 4;
b1: rng h = the carrier of pT
  proof
    thus rng h c= the carrier of pT;
    let y be set;
    assume y in the carrier of pT;
    then consider lt being Loop of f.s such that
b2:   y = Class(EqRel(T,f.s),lt) by TOPALG_1:48;
    reconsider ls = f"*lt as Loop of s by c4,c5,c9,T127a;
    set x = Class(EqRel(S,s),ls);
b3: x in the carrier of pS by TOPALG_1:48;
    then consider ls1 being Loop of s, lt1 being Loop of f.s such that
b5:   x = Class(EqRel(S,s),ls1) and
b6:   lt1 = f*ls1 and
b7:   h.x = Class(EqRel(T,f.s),lt1) by dhi;
d1: f*ls = (f*f")*lt by RELAT_1:55
        .= id rng f * lt by c6,c7,TOPS_2:65
        .= lt by c6,d2,FUNCT_2:23;
    ls,ls1 are_homotopic by b5,TOPALG_1:47;
    then lt,lt1 are_homotopic by b6,d1,T123;
    then h.x = y by b2,b7,TOPALG_1:47;
    hence thesis by b4,b3,FUNCT_1:def 5;
  end;
  h is one-to-one
  proof
    let x1, x2 be set;
    assume x1 in dom h;
    then consider ls1 being Loop of s, lt1 being Loop of f.s such that
d6:   x1 = Class(EqRel(S,s),ls1) and
d7:   lt1 = f*ls1 and
d8:   h.x1 = Class(EqRel(T,f.s),lt1) by dhi;
    assume x2 in dom h;
    then consider ls2 being Loop of s, lt2 being Loop of f.s such that
e6:   x2 = Class(EqRel(S,s),ls2) and
e7:   lt2 = f*ls2 and
e8:   h.x2 = Class(EqRel(T,f.s),lt2) by dhi;
    reconsider a1 = f"*lt1, a2 = f"*lt2 as Loop of s by c4,c5,c9,T127a;
f3: f"*f = id dom f by c6,c7,TOPS_2:65;
f1: f"*lt1 = (f"*f)*ls1 by d7,RELAT_1:55
        .= ls1 by B4,C6,f3,FUNCT_2:23;
f2: f"*lt2 = (f"*f)*ls2 by e7,RELAT_1:55
          .= ls2 by B4,C6,f3,FUNCT_2:23;
    assume h.x1 = h.x2;
    then lt1,lt2 are_homotopic by d8,e8,TOPALG_1:47;
    then a1,a2 are_homotopic by c4,c5,c9,T123;
    hence x1 = x2 by d6,e6,f1,f2,TOPALG_1:47;
  end;
  hence h is_isomorphism by b1,GROUP_6:70;
end;

theorem
  for S, T being non empty TopSpace, s being Point of S, t being Point of T,
      f being continuous map of S,T,
      P being Path of t,f.s,
      h being Homomorphism of pi_1(S,s),pi_1(T,t)
  st f is_homeomorphism & f.s,t are_connected &
     h = pi_1-iso(P) * FundGrIso(f,s)
  holds h is_isomorphism
proof
  let S, T be non empty TopSpace;
  let s be Point of S;
  let t be Point of T;
  let f be continuous map of S,T;
  let P be Path of t,f.s;
  let h be Homomorphism of pi_1(S,s),pi_1(T,t);
  assume f is_homeomorphism;
  then
a2: FundGrIso(f,s) is_isomorphism by Thomoiso1;
  assume that
Z1: f.s,t are_connected and
Z2: h = pi_1-iso(P) * FundGrIso(f,s);
  reconsider G = pi_1-iso(P) as Homomorphism of pi_1(T,f.s),pi_1(T,t)
    by Z1,TOPALG_1:51;
  G is_isomorphism by Z1,TOPALG_1:56;
  hence thesis by a2,Z2,GROUP_6:74;
end;

theorem
  for S being non empty TopSpace,
      T being non empty arcwise_connected TopSpace,
      s being Point of S, t being Point of T st
  S,T are_homeomorphic holds pi_1(S,s),pi_1(T,t) are_isomorphic
proof
  let S be non empty TopSpace;
  let T be non empty arcwise_connected TopSpace;
  let s be Point of S;
  let t be Point of T;
  given f being map of S,T such that
Z0:  f is_homeomorphism;
  reconsider f as continuous map of S,T by Z0,TOPS_2:def 5;
a2: FundGrIso(f,s) is_isomorphism by Z0,Thomoiso1;
  consider P being Path of t,f.s;
  take pi_1-iso(P) * FundGrIso(f,s);
  pi_1-iso(P) is_isomorphism by TOPALG_1:57;
  hence thesis by a2,GROUP_6:74;
end;
