:: On the Isomorphism of Fundamental Groups
::  by Artur Korni{\l}owicz
::
:: Received July 30, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies PRE_TOPC, EUCLID, RELAT_1, SUBSET_1, TOPALG_3, TOPALG_1,
      WELLORD1, ALGSTR_1, BORSUK_1, FUNCT_1, WAYBEL20, EQREL_1, BORSUK_2,
      ORDINAL2, ARYTM_1, GROUP_6, VECTSP_1, T_0TOPSP, TOPS_2, ARYTM_3,
      FUNCOP_1, REALSET1, TOPMETR, GRAPH_1, BORSUK_6, SGRAPH1, PCOMPS_1,
      FUNCT_4, BOOLE, SEQM_3, SETFAM_1, TARSKI, TOPREAL1, TOPREAL2, TAXONOM2,
      TEX_2, RELAT_2, CONNSP_1, RLVECT_3, CAT_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, RELAT_1, EQREL_1, FUNCT_1,
      RELSET_1, FUNCT_2, BINOP_1, XCMPLX_0, XREAL_0, SEQM_3, FUNCOP_1,
      CQC_LANG, FUNCT_4, STRUCT_0, PRE_TOPC, GROUP_1, GROUP_6, GRCAT_1,
      BORSUK_1, TOPS_2, COMPTS_1, CONNSP_1, TOPREAL1, TEX_2, TOPREAL2, EUCLID,
      PCOMPS_1, BORSUK_2, BORSUK_3, TAXONOM2, YELLOW_8, BORSUK_6, TOPALG_1,
      TOPALG_2;
 constructors GRCAT_1, TOPALG_1, TOPREAL1, REAL_1, TOPS_2, BORSUK_3, CONNSP_1,
      TOPALG_2, SEQM_3, COMPTS_1, YELLOW_8, TOPREAL2, TAXONOM2, REALSET2,
      SEQ_4, TEX_2, TDLAT_3, CANTOR_1, CQC_LANG;
 registrations STRUCT_0, EUCLID, RELSET_1, XREAL_0, BORSUK_1, PRE_TOPC,
      SUBSET_1, XBOOLE_0, FUNCT_1, TOPALG_1, BORSUK_2, INT_1, BORSUK_5,
      PCOMPS_1, TOPS_1, TOPREAL2, TEX_1, BORSUK_3, WAYBEL12, YELLOW12,
      CQC_LANG;
 requirements NUMERALS, BOOLE, SUBSET;
 definitions TARSKI, XBOOLE_0, FUNCT_1, FUNCT_2, GROUP_6, PRE_TOPC, TOPS_2,
      T_0TOPSP, BORSUK_2, BORSUK_6, CONNSP_1;
 theorems GROUP_6, PRE_TOPC, FUNCT_2, BORSUK_1, TOPALG_1, TOPS_2, GRCAT_1,
      BORSUK_3, FUNCT_1, TOPMETR, RELAT_1, FUNCOP_1, TOPREAL6, BORSUK_6,
      BORSUK_2, BINOP_1, TARSKI, PCOMPS_1, ZFMISC_1, FUNCT_4, TSEP_1, XBOOLE_1,
      COMPTS_1, TOPREAL1, TOPREAL2, JORDAN1, STRUCT_0, SETFAM_1, XBOOLE_0,
      TAXONOM2, REALSET2, TEX_2, TOPS_1, CONNSP_1, TDLAT_3, ENUMSET1, JCT_MISC,
      YELLOW12, TOPS_3, YELLOW13, YELLOW_8, CQC_LANG, GROUP_1;
 schemes FUNCT_2;

begin :: Preliminaries

set I = the carrier of I[01];
set R = the carrier of R^1;
Lm1: the carrier of [:I[01],I[01]:] = [:I,I:] by BORSUK_1:def 5;
reconsider j0 = 0, j1 = 1 as Point of I[01] by BORSUK_1:def 17,def 18;

theorem
  for A, B, a, b being set, f being Function of A,B st a in A & b in B holds
  f +* (a .--> b) is Function of A,B
proof ::ok
  let A, B, a, b be set, f be Function of A,B such that
A1: a in A and
A2: b in B;
  set g = a .--> b;
  set f1 = f +* g;
A3: {a} c= A by A1,ZFMISC_1:37;
A4: dom f1 = dom f \/ dom g by FUNCT_4:def 1
      .= A \/ dom g by A2,FUNCT_2:def 1
      .= A \/ {a} by CQC_LANG:5
      .= A by A3,XBOOLE_1:12;
A5: rng f1 c= rng f \/ rng g by FUNCT_4:18;
  rng g = {b} by CQC_LANG:5;
  then rng g c= B by A2,ZFMISC_1:37;
  then rng f \/ rng g c= B \/ B by XBOOLE_1:13;
  then rng f1 c= B by A5,XBOOLE_1:1;
  hence f +* (a .--> b) is Function of A,B by A4,FUNCT_2:4;
end;

theorem
  for f being Function, X, x being set st f|X is one-to-one & x in rng (f|X)
  holds (f*(f|X)").x = x
proof ::ok
  let f be Function,
      X, x be set;
  set g = f|X;
  assume that
A1: g is one-to-one and
A2: x in rng g;
  consider a being set such that
A3:  a in dom g and
A4:  g.a = x by A2,FUNCT_1:def 5;
  dom g = dom f /\ X by RELAT_1:90;
  then
A5: a in X by A3,XBOOLE_0:def 3;
  dom (g") = rng g by A1,FUNCT_1:54;
  hence (f*g").x = f.(g".x) by A2,FUNCT_1:23
    .= f.a by A1,A3,A4,FUNCT_1:54
    .= x by A4,A5,FUNCT_1:72;
end;

theorem Th3:
  for x, y, X, Y, Z being set, f being Function of [:X,Y:],Z, g being Function
    st Z <> {} & x in X & y in Y holds (g*f).(x,y) = g.(f.(x,y))
proof
  let x, y, X, Y, Z be set,
      f be Function of [:X,Y:],Z,
      g be Function such that
A1: Z <> {};
  assume x in X & y in Y;
  then
A2: [x,y] in [:X,Y:] by ZFMISC_1:106;
  thus (g*f).(x,y) = (g*f). [x,y] by BINOP_1:def 1
    .= g.(f. [x,y]) by A1,A2,FUNCT_2:21
    .= g.(f.(x,y)) by BINOP_1:def 1;
end;

theorem Th4:
  for X, a, b being set, f being Function of X,{a,b} holds X = f"{a} \/ f"{b}
proof
  let X, a, b be set;
  let f be Function of X,{a,b};
  thus X = f"{a,b} by FUNCT_2:48
    .= f"({a}\/{b}) by ENUMSET1:41
    .= f"{a} \/ f"{b} by RELAT_1:175;
end;

theorem Th5:
  for S, T being non empty 1-sorted,
      s being Point of S, t being Point of T holds
   (S --> t).s = t
proof
  let S, T be non empty 1-sorted,
      s be Point of S, t be Point of T;
  thus (S --> t).s = ((the carrier of S) --> t).s by BORSUK_1:def 3
     .= t by FUNCOP_1:13;
end;

:: Topological preliminaries

theorem
  for T being non empty TopStruct, t being Point of T, A being Subset of T
   st A = {t} holds Sspace(t) = T | A
proof
  let T be non empty TopStruct,
      t be Point of T,
      A be Subset of T such that
A1:  A = {t};
  the carrier of Sspace(t) = {t} by TEX_2:def 4
    .= [#](T|A) by A1,PRE_TOPC:def 10
    .= the carrier of (T|A) by PRE_TOPC:12;
  hence Sspace(t) = T | A by TSEP_1:5;
end;

theorem Th7:
  for T being TopSpace, A, B being Subset of T,
      C, D being Subset of the TopStruct of T st A = C & B = D holds
  A,B are_separated iff C,D are_separated
proof
  let T be TopSpace;
  let A, B be Subset of T;
  let C, D be Subset of the TopStruct of T;
  assume
A1: A = C & B = D;
  then
A2: Cl A = Cl C & Cl B = Cl D by TOPS_3:80;
  hereby
    assume A,B are_separated;
    then Cl A misses B & A misses Cl B by CONNSP_1:def 1;
    hence C,D are_separated by A1,A2,CONNSP_1:def 1;
  end;
  assume C,D are_separated;
  then Cl C misses D & C misses Cl D by CONNSP_1:def 1;
  hence A,B are_separated by A1,A2,CONNSP_1:def 1;
end;

theorem Th8:
  for T being non empty TopSpace holds T is connected iff
  not ex f being map of T, 1TopSp{0,1} st f is continuous onto
proof
  let T be non empty TopSpace;
  set J = 1TopSp{0,1};
  set X = {0}, Y = {1};
A1: [#]J = the carrier of J by PRE_TOPC:12;
A2: the carrier of J = {0,1} by PCOMPS_1:8;
  then reconsider X, Y as non empty open Subset of J by TDLAT_3:17,ZFMISC_1:12;
A3: [#]T = the carrier of T by PRE_TOPC:12;
A4: X misses Y by ZFMISC_1:17;
  thus T is connected implies
  not ex f being map of T,J st f is continuous onto
  proof
    assume
A5:   T is connected;
    given f being map of T,J such that
A6:   f is continuous and
A7:   f is onto;
    set A = f"X, B = f"Y;
A8: the carrier of T = A \/ B by A2,Th4;
A9: A is open & B is open by A6,TOPS_2:55;
    rng f = the carrier of J by A7,FUNCT_2:def 3;
    then
A10: A <> {}T & B <> {}T by RELAT_1:174;
    A misses B by A4,JCT_MISC:4;
    hence contradiction by A3,A5,A8,A9,A10,CONNSP_1:12;
  end;
  assume
A11:  not ex f being map of T,J st f is continuous onto;
  assume not thesis;
  then consider A, B being Subset of T such that
A12:  [#]T = A \/ B and
A13:  A <> {}T and
A14:  B <> {}T and
A15:  A is open and
A16:  B is open and
A17:  A misses B by CONNSP_1:12;
  defpred C[set] means $1 in A;
  reconsider j0 = 0, j1 = 1 as Element of the carrier of J by A2,TARSKI:def 2;
  deffunc F(set) = j0;
  deffunc G(set) = j1;
A18: for x being set st x in the carrier of T holds
     (C[x] implies F(x) in the carrier of J) &
     (not C[x] implies G(x) in the carrier of J);
  consider f being Function of the carrier of T, the carrier of J
    such that
A19:  for x being set st x in the carrier of T holds
      (C[x] implies f.x = F(x)) & (not C[x] implies f.x = G(x))
      from FUNCT_2:sch 9(A18);
  reconsider f as map of T,J;
A20: dom f = the carrier of T by FUNCT_2:def 1;
A21: f"X = A
   proof
     hereby
       let x be set;
       assume
A22:      x in f"X;
       then x in dom f & f.x in X by FUNCT_1:def 13;
       then f.x = 0 by TARSKI:def 1;
       hence x in A by A19,A22;
     end;
     let x be set;
     assume
A23:    x in A;
     f.x = 0 by A19,A23;
     then f.x in X by TARSKI:def 1;
     hence x in f"X by A20,A23,FUNCT_1:def 13;
   end;
A24: f"Y = B
   proof
     hereby
       let x be set;
       assume
A25:      x in f"Y;
       then x in dom f & f.x in Y by FUNCT_1:def 13;
       then f.x = 1 by TARSKI:def 1;
       then not C[x] by A19;
       hence x in B by A3,A12,A25,XBOOLE_0:def 2;
     end;
     let x be set;
     assume
A26:    x in B;
     not x in A by A17,A26,XBOOLE_0:3;
     then f.x = 1 by A19,A26;
     then f.x in Y by TARSKI:def 1;
     hence x in f"Y by A20,A26,FUNCT_1:def 13;
   end;
A27: rng f = the carrier of J
   proof
     thus rng f c= the carrier of J;
     let y be set;
     assume
A28:    y in the carrier of J;
     per cases by A2,A28,TARSKI:def 2;
     suppose
A29:    y = 0;
     consider x being set such that
A30:    x in f"X by A13,A21,XBOOLE_0:def 1;
A31:  x in dom f & f.x in X by A30,FUNCT_1:def 13;
     then f.x = 0 by TARSKI:def 1;
     hence y in rng f by A29,A31,FUNCT_1:def 5;
     end;
     suppose
A32:    y = 1;
     consider x being set such that
A33:    x in f"Y by A14,A24,XBOOLE_0:def 1;
A34:  x in dom f & f.x in Y by A33,FUNCT_1:def 13;
     then f.x = 1 by TARSKI:def 1;
     hence y in rng f by A32,A34,FUNCT_1:def 5;
     end;
   end;
   then
A35: f is onto by FUNCT_2:def 3;
A36: f"{}J = {}T by RELAT_1:171;
A37: f"[#]J = [#]T by A1,A3,A20,A27,RELAT_1:169;
  for P being Subset of J st P is open holds f"P is open
    by A1,A2,A15,A16,A21,A24,A36,A37,ZFMISC_1:42;
  then f is continuous by TOPS_2:55;
  hence thesis by A11,A35;
end;

registration
  cluster empty -> connected TopStruct;
coherence
proof
  let T be TopStruct such that
A1:  T is empty;
  let A be Subset of T;
  reconsider T as empty TopStruct by A1;
  the carrier of T is empty;
  hence thesis by XBOOLE_1:3;
end;
end;

theorem
  for T being TopSpace st the TopStruct of T is connected holds T is connected
proof
  let T be TopSpace;
  set G = the TopStruct of T;
  assume
A1:  G is connected;
  let A, B be Subset of T such that
A2: [#]T = A \/ B and
A3: A,B are_separated;
  reconsider A1 = A, B1 = B as Subset of G;
A4: [#]G = the carrier of G by PRE_TOPC:12
        .= A1 \/ B1 by A2,PRE_TOPC:12;
  A1,B1 are_separated by A3,Th7;
  then A1 = {}G or B1 = {}G by A1,A4,CONNSP_1:def 2;
  hence A = {}T or B = {}T;
end;

registration
  let T be connected TopSpace;
  cluster the TopStruct of T -> connected;
coherence
proof
  set G = the TopStruct of T;
  let A, B be Subset of G such that
A1: [#]G = A \/ B and
A2: A,B are_separated;
  reconsider A1 = A, B1 = B as Subset of T;
A3: [#]T = the carrier of T by PRE_TOPC:12
        .= A1 \/ B1 by A1,PRE_TOPC:12;
  A1,B1 are_separated by A2,Th7;
  then A1 = {}T or B1 = {}T by A3,CONNSP_1:def 2;
  hence A = {}G or B = {}G;
end;
end;

theorem
  for S, T be non empty TopSpace st
   S,T are_homeomorphic & S is arcwise_connected holds T is arcwise_connected
proof
  let S, T be non empty TopSpace;
  given h being map of S,T such that
A1: h is_homeomorphism;
  assume
A2: for a, b being Point of S holds a,b are_connected;
  let a, b being Point of T;
  h".a,h".b are_connected by A2;
  then consider f being map of I[01], S such that
A3: f is continuous and
A4: f.0 = h".a and
A5: f.1 = h".b by BORSUK_2:def 1;
  take g = h*f;
  h is continuous by A1,TOPS_2:def 5;
  hence g is continuous by A3,TOPS_2:58;
A6: h is one-to-one by A1,TOPS_2:def 5;
A7: rng h = [#]T by A1,TOPS_2:def 5;
A8: [#]T = the carrier of T by PRE_TOPC:12;
  thus g.0 = h.(h".a) by A4,BORSUK_1:def 17,FUNCT_2:21
    .= (h*h").a by FUNCT_2:21
    .= (id rng h).a by A6,A7,TOPS_2:65
    .= (id T).a by A7,A8,GRCAT_1:def 11
    .= a by GRCAT_1:11;
  thus g.1 = h.(h".b) by A5,BORSUK_1:def 18,FUNCT_2:21
    .= (h*h").b by FUNCT_2:21
    .= (id rng h).b by A6,A7,TOPS_2:65
    .= (id T).b by A7,A8,GRCAT_1:def 11
    .= b by GRCAT_1:11;
end;

registration
  cluster trivial -> arcwise_connected (non empty TopSpace);
coherence
proof
  let T be non empty TopSpace;
  assume
A1: T is trivial;
  let a, b be Point of T;
  take f = I[01] --> a;
  thus f is continuous;
  a = b by A1,REALSET2:def 7;
  hence f.0 = a & f.1 = b by Th5,BORSUK_1:def 17,def 18;
end;
end;

theorem
  for T being SubSpace of TOP-REAL 2 st
   the carrier of T is Simple_closed_curve holds T is arcwise_connected
proof
  let T be SubSpace of TOP-REAL 2;
  assume
A1: the carrier of T is Simple_closed_curve;
  then reconsider P = the carrier of T as Simple_closed_curve;
  let a, b be Point of T;
  a in P & b in P;
  then reconsider p1 = a, p2 = b as Point of TOP-REAL 2;
  per cases;
  suppose p1 <> p2;
  then consider P1, P2 being non empty Subset of TOP-REAL 2 such that
A2: P1 is_an_arc_of p1,p2 and P2 is_an_arc_of p1,p2 and
A3: P = P1 \/ P2 and P1 /\ P2 = {p1,p2} by TOPREAL2:5;
  consider f1 being map of I[01], (TOP-REAL 2)|P1 such that
A4:  f1 is_homeomorphism and
A5:  f1.0 = p1 & f1.1 = p2 by A2,TOPREAL1:def 2;
  the carrier of ((TOP-REAL 2)|P1) = P1 by JORDAN1:1;
  then
A6: the carrier of ((TOP-REAL 2)|P1) c= P by A3,XBOOLE_1:7;
  then reconsider f = f1 as map of I[01],T by FUNCT_2:9;
  take f;
A7: (TOP-REAL 2)|P1 is SubSpace of T by A6,TSEP_1:4;
  f1 is continuous by A4,TOPS_2:def 5;
  hence f is continuous by A7,TOPMETR:7;
  thus thesis by A5;
  end;
  suppose
A8: p1 = p2;
  reconsider T1 = T as non empty SubSpace of TOP-REAL 2 by A1,STRUCT_0:def 1;
  reconsider a1 = a as Point of T1;
  reconsider f = I[01] --> a1 as map of I[01],T;
  take f;
  thus f is continuous;
A9: f = I --> a by BORSUK_1:def 3;
  thus f.0 = f.j0 .= a by A9,FUNCOP_1:13;
  thus f.1 = f.j1 .= b by A8,A9,FUNCOP_1:13;
  end;
end;

theorem
  for T being TopSpace
   ex F being Subset-Family of T st F = {the carrier of T} &
    F is_a_cover_of T & F is open
proof
  let T be TopSpace;
  set F = {the carrier of T};
  F c= bool the carrier of T
  proof
    let a be set;
    assume a in F;
    then a = the carrier of T by TARSKI:def 1;
    hence thesis by ZFMISC_1:def 1;
  end;
  then reconsider F as Subset-Family of T by SETFAM_1:def 7;
  take F;
  thus F = {the carrier of T};
A1: [#]T = the carrier of T by PRE_TOPC:12;
  thus F is_a_cover_of T
  proof
    thus [#]T = union F by A1,ZFMISC_1:31;
  end;
  let P be Subset of T;
  thus thesis by A1,TARSKI:def 1;
end;

registration
  let T be TopSpace;
  cluster non empty mutually-disjoint open closed Subset-Family of T;
existence
proof
  set F = {{}T};
  F c= bool the carrier of T
  proof
    let a be set;
    assume a in F;
    then a = {}T by TARSKI:def 1;
    hence thesis;
  end;
  then reconsider F as Subset-Family of T by SETFAM_1:def 7;
  take F;
  thus F is non empty mutually-disjoint by TAXONOM2:10;
  thus F is open
  proof
    let P be Subset of T;
    thus thesis by TARSKI:def 1;
  end;
  let P be Subset of T;
  thus thesis by TARSKI:def 1;
end;
end;

theorem
  for T being TopSpace, D being mutually-disjoint open Subset-Family of T,
      A being Subset of T, X being set st
   A is connected & X in D & X meets A & D is_a_cover_of A
  holds A c= X
proof
  let T be TopSpace;
  let D be mutually-disjoint open Subset-Family of T;
  let A be Subset of T;
  let X be set such that
A1:  T|A is connected and
A2:  X in D and
A3:  X meets A;
  assume D is_a_cover_of A;
  then
A4: A c= union D by COMPTS_1:def 1;
  let a be set;
  assume
A5: a in A;
  then consider Z being set such that
A6:  a in Z and
A7:  Z in D by A4,TARSKI:def 4;
  consider x being set such that
A8:  x in X and
A9:  x in A by A3,XBOOLE_0:3;
  set D1 = {K /\ A where K is Subset of T: K in D & a in K};
  set D2 = {K /\ A where K is Subset of T: K in D & not a in K};
A10: the carrier of T|A = A by JORDAN1:1;
A11: [#](T|A) = A by PRE_TOPC:def 10;
  D1 c= bool A
  proof
    let d be set;
    assume d in D1;
    then ex K1 being Subset of T st d = K1 /\ A & K1 in D & a in K1;
    then d c= A by XBOOLE_1:17;
    hence d in bool A;
  end;
  then reconsider D1 as Subset-Family of T|A by A10,SETFAM_1:def 7;
  D2 c= bool A
  proof
    let d be set;
    assume d in D2;
    then ex K1 being Subset of T st d = K1 /\ A & K1 in D & not a in K1;
    then d c= A by XBOOLE_1:17;
    hence d in bool A;
  end;
  then reconsider D2 as Subset-Family of T|A by A10,SETFAM_1:def 7;
  D1 is open
  proof
    let P be Subset of T|A;
    assume P in D1;
    then consider K1 being Subset of T such that
A12:  P = K1 /\ A and
A13:  K1 in D and a in K1;
    K1 is open by A13,TOPS_2:def 1;
    hence P is open by A11,A12,TOPS_2:32;
  end;
  then
A14: union D1 is open by TOPS_2:26;
  D2 is open
  proof
    let P be Subset of T|A;
    assume P in D2;
    then consider K1 being Subset of T such that
A15:  P = K1 /\ A and
A16:  K1 in D and not a in K1;
    K1 is open by A16,TOPS_2:def 1;
    hence P is open by A11,A15,TOPS_2:32;
  end;
  then
A17: union D2 is open by TOPS_2:26;
   a in Z /\ A & Z /\ A in D1 by A5,A6,A7,XBOOLE_0:def 3;
   then
A18: union D1 <> {}(T|A) by TARSKI:def 4;
A19: A = union D1 \/ union D2
  proof
    thus A c= union D1 \/ union D2
    proof
      let b be set;
      assume
A20:     b in A;
      then consider Z being set such that
A21:     b in Z and
A22:     Z in D by A4,TARSKI:def 4;
      reconsider Z as Subset of T by A22;
A23:   b in Z /\ A by A20,A21,XBOOLE_0:def 3;
      per cases;
      suppose a in Z;
      then Z /\ A in D1 by A22;
      then b in union D1 by A23,TARSKI:def 4;
      hence thesis by XBOOLE_0:def 2;
      end;
      suppose not a in Z;
      then Z /\ A in D2 by A22;
      then b in union D2 by A23,TARSKI:def 4;
      hence thesis by XBOOLE_0:def 2;
      end;
    end;
    thus thesis by A10;
  end;
  assume not a in X;
  then x in X /\ A & X /\ A in D2 by A2,A8,A9,XBOOLE_0:def 3;
  then x in union D2 by TARSKI:def 4;
  then
  union D1 meets union D2 by A1,A11,A14,A17,A18,A19,CONNSP_1:12;
  then consider y being set such that
A24:  y in union D1 and
A25:  y in union D2 by XBOOLE_0:3;
  consider Y1 being set such that
A26:  y in Y1 and
A27:  Y1 in D1 by A24,TARSKI:def 4;
  consider Y2 being set such that
A28:  y in Y2 and
A29:  Y2 in D2 by A25,TARSKI:def 4;
  consider K1 being Subset of T such that
A30:  Y1 = K1 /\ A and
A31:  K1 in D and
A32:  a in K1 by A27;
  consider K2 being Subset of T such that
A33:  Y2 = K2 /\ A and
A34:  K2 in D and
A35:  not a in K2 by A29;
  y in K1 & y in K2 by A26,A28,A30,A33,XBOOLE_0:def 3;
  then K1 meets K2 by XBOOLE_0:3;
  hence thesis by A31,A32,A34,A35,TAXONOM2:def 5;
end;

begin :: On the product of topologies

theorem Th14:
  for S, T being TopSpace holds
  the TopStruct of [:S,T:] = [:the TopStruct of S,the TopStruct of T:]
proof
  let S, T be TopSpace;
A1: the carrier of [:S,T:] = [:the carrier of S,the carrier of T:]
    by BORSUK_1:def 5
    .= the carrier of [:the TopStruct of S,the TopStruct of T:]
    by BORSUK_1:def 5;
  for C1 being Subset of [:S,T:],
  C2 being Subset of [:the TopStruct of S,the TopStruct of T:] st C1 = C2 holds
    C1 is open iff C2 is open
  proof
    let C1 be Subset of [:S,T:];
    let C2 be Subset of [:the TopStruct of S,the TopStruct of T:];
    assume
A2:   C1 = C2;
    hereby
      assume C1 is open;
      then consider A being Subset-Family of [:S,T:] such that
A3:     C1 = union A and
A4:     for e being set st e in A
         ex X1 being Subset of S,
            Y1 being Subset of T st
         e = [:X1,Y1:] & X1 is open & Y1 is open by BORSUK_1:45;
      reconsider AA = A as
        Subset-Family of [:the TopStruct of S,the TopStruct of T:] by A1;
      for e being set st e in AA
        ex X1 being Subset of the TopStruct of S,
           Y1 being Subset of the TopStruct of T st
        e = [:X1,Y1:] & X1 is open & Y1 is open
      proof
        let e be set;
        assume e in AA;
        then consider X1 being Subset of S, Y1 being Subset of T such that
A5:       e = [:X1,Y1:] & X1 is open & Y1 is open by A4;
        reconsider X2 = X1 as Subset of the TopStruct of S;
        reconsider Y2 = Y1 as Subset of the TopStruct of T;
        take X2, Y2;
        thus e = [:X2,Y2:] & X2 is open & Y2 is open by A5,TOPS_3:76;
      end;
      hence C2 is open by A2,A3,BORSUK_1:45;
    end;
    assume C2 is open;
    then consider A being Subset-Family of
      [:the TopStruct of S,the TopStruct of T:] such that
A6:   C2 = union A and
A7:   for e being set st e in A
       ex X1 being Subset of the TopStruct of S,
          Y1 being Subset of the TopStruct of T st
       e = [:X1,Y1:] & X1 is open & Y1 is open by BORSUK_1:45;
    reconsider AA = A as Subset-Family of [:S,T:] by A1;
    for e being set st e in AA
      ex X1 being Subset of S,
         Y1 being Subset of T st
      e = [:X1,Y1:] & X1 is open & Y1 is open
    proof
      let e be set;
      assume e in AA;
      then consider X1 being Subset of the TopStruct of S,
                    Y1 being Subset of the TopStruct of T such that
A8:     e = [:X1,Y1:] & X1 is open & Y1 is open by A7;
      reconsider X2 = X1 as Subset of S;
      reconsider Y2 = Y1 as Subset of T;
      take X2, Y2;
      thus e = [:X2,Y2:] & X2 is open & Y2 is open by A8,TOPS_3:76;
    end;
    hence C1 is open by A2,A6,BORSUK_1:45;
  end;
  hence the TopStruct of [:S,T:] = [:the TopStruct of S,the TopStruct of T:]
    by A1,TOPS_3:72;
end;

theorem Th15:
  for S, T being TopSpace,
      A being Subset of S, B being Subset of T holds
  Cl [:A,B:] = [:Cl A,Cl B:]
proof
  let S, T be TopSpace,
      A be Subset of S,
      B be Subset of T;
  hereby
    let x be set;
    assume
A1:   x in Cl [:A,B:];
    the carrier of [:S,T:] = [:the carrier of S, the carrier of T:]
     by BORSUK_1:def 5;
    then ex x1, x2 being set st x1 in the carrier of S & x2 in the carrier of T
      & x = [x1,x2] by A1,ZFMISC_1:def 2;
    then reconsider S1 = S, T1 = T as non empty TopSpace by STRUCT_0:def 1;
    reconsider p = x as Point of [:S1,T1:] by A1;
    consider K being Basis of p such that
A2:   for Q being Subset of [:S1,T1:] st Q in K holds [:A,B:] meets Q
        by A1,YELLOW13:17;
    consider p1 being Point of S1,
             p2 being Point of T1 such that
A3:   p = [p1,p2] by BORSUK_1:50;
    for G being Subset of S1 st G is open & p1 in G holds A meets G
    proof
      let G be Subset of S1 such that
A4:     G is open and
A5:     p1 in G;
      [#]T1 = the carrier of T1 by PRE_TOPC:12;
      then
A6:   [p1,p2] in [:G,[#]T1:] by A5,ZFMISC_1:106;
      [:G,[#]T1:] is open by A4,BORSUK_1:46;
      then consider V being Subset of [:S1,T1:] such that
A7:     V in K and
A8:     V c= [:G,[#]T1:] by A3,A6,YELLOW_8:def 2;
      [:A,B:] meets V by A2,A7;
      then consider a being set such that
A9:    a in [:A,B:] & a in V by XBOOLE_0:3;
      a in [:A,B:] /\ [:G,[#]T1:] by A8,A9,XBOOLE_0:def 3;
      then a in [:A/\G,B/\[#]T1:] by ZFMISC_1:123;
      then ex a1, a2 being set st a1 in A/\G & a2 in B/\[#]T1 & a = [a1,a2]
        by ZFMISC_1:def 2;
      hence A meets G by XBOOLE_0:def 7;
    end;
    then
A10: p1 in Cl A by PRE_TOPC:def 13;
    for G being Subset of T1 st G is open & p2 in G holds B meets G
    proof
      let G be Subset of T1 such that
A11:     G is open and
A12:     p2 in G;
      [#]S1 = the carrier of S1 by PRE_TOPC:12;
      then
A13:   [p1,p2] in [:[#]S1,G:] by A12,ZFMISC_1:106;
      [:[#]S1,G:] is open by A11,BORSUK_1:46;
      then consider V being Subset of [:S1,T1:] such that
A14:     V in K and
A15:     V c= [:[#]S1,G:] by A3,A13,YELLOW_8:def 2;
      [:A,B:] meets V by A2,A14;
      then consider a being set such that
A16:    a in [:A,B:] & a in V by XBOOLE_0:3;
      a in [:A,B:] /\ [:[#]S1,G:] by A15,A16,XBOOLE_0:def 3;
      then a in [:A/\[#]S1,B/\G:] by ZFMISC_1:123;
      then ex a1, a2 being set st a1 in A/\[#]S1 & a2 in B/\G & a = [a1,a2]
        by ZFMISC_1:def 2;
      hence B meets G by XBOOLE_0:def 7;
    end;
    then p2 in Cl B by PRE_TOPC:def 13;
    hence x in [:Cl A,Cl B:] by A3,A10,ZFMISC_1:106;
  end;
  let x be set;
  assume x in [:Cl A,Cl B:];
  then consider x1, x2 being set such that
A17: x1 in Cl A and
A18: x2 in Cl B and
A19: x = [x1,x2] by ZFMISC_1:def 2;
  reconsider S1 = S, T1 = T as non empty TopSpace by A17,A18,STRUCT_0:def 1;
  reconsider x1 as Point of S1 by A17;
  reconsider x2 as Point of T1 by A18;
  consider K1 being Basis of x1 such that
A20:  for Q being Subset of S1 st Q in K1 holds A meets Q by A17,YELLOW13:17;
  consider K2 being Basis of x2 such that
A21:  for Q being Subset of T1 st Q in K2 holds B meets Q by A18,YELLOW13:17;
  for G being Subset of [:S1,T1:] st G is open & [x1,x2] in G holds
   [:A,B:] meets G
  proof
    let G be Subset of [:S1,T1:] such that
A22:   G is open and
A23:   [x1,x2] in G;
    consider F being Subset-Family of [:S1,T1:] such that
A24:   G = union F and
A25:   for e being set st e in F
       ex X1 being Subset of S1, Y1 being Subset of T1 st
        e = [:X1,Y1:] & X1 is open & Y1 is open by A22,BORSUK_1:45;
    consider Z being set such that
A26:  [x1,x2] in Z and
A27:  Z in F by A23,A24,TARSKI:def 4;
    consider X1 being Subset of S1, Y1 being Subset of T1 such that
A28:  Z = [:X1,Y1:] and
A29:  X1 is open and
A30:  Y1 is open by A25,A27;
    x1 in X1 by A26,A28,ZFMISC_1:106;
    then consider V1 being Subset of S1 such that
A31:  V1 in K1 and
A32:  V1 c= X1 by A29,YELLOW_8:def 2;
    A meets V1 by A20,A31;
    then consider a1 being set such that
A33:  a1 in A and
A34:  a1 in V1 by XBOOLE_0:3;
    x2 in Y1 by A26,A28,ZFMISC_1:106;
    then consider V2 being Subset of T1 such that
A35:  V2 in K2 and
A36:  V2 c= Y1 by A30,YELLOW_8:def 2;
    B meets V2 by A21,A35;
    then consider a2 being set such that
A37:  a2 in B and
A38:  a2 in V2 by XBOOLE_0:3;
    [a1,a2] in Z by A28,A32,A34,A36,A38,ZFMISC_1:106;
    then
A39: [a1,a2] in union F by A27,TARSKI:def 4;
    [a1,a2] in [:A,B:] by A33,A37,ZFMISC_1:106;
    hence [:A,B:] meets G by A24,A39,XBOOLE_0:3;
  end;
  hence thesis by A19,PRE_TOPC:def 13;
end;

theorem Th16:
  for S, T being TopSpace,
      A being closed Subset of S, B being closed Subset of T holds
  [:A,B:] is closed
proof
  let S, T be TopSpace;
  let A be closed Subset of S;
  let B be closed Subset of T;
A1: Cl A = A & Cl B = B by PRE_TOPC:52;
  Cl [:A,B:] = [:Cl A,Cl B:] by Th15;
  hence thesis by A1;
end;

registration
  let A, B be connected TopSpace;
  cluster [:A,B:] ->  connected;
coherence
proof
  set S = the TopStruct of A,
      T = the TopStruct of B;
A1: the TopStruct of [:A,B:] = [:S,T:] by Th14;
  per cases;
  suppose
A2: [:S,T:] is non empty;
  now
    set J = 1TopSp{0,1};
    given f being map of [:S,T:], J such that
A3:   f is continuous and
A4:   f is onto;
A5: the carrier of J = {0,1} by PCOMPS_1:8;
    then reconsider j0 = 0, j1 = 1 as Element of the carrier of J
      by TARSKI:def 2;
A6: dom f = the carrier of [:S,T:] by FUNCT_2:def 1;
A7: the carrier of [:S,T:] = [:the carrier of S, the carrier of T:]
      by BORSUK_1:def 5;
A8: rng f = the carrier of J by A4,FUNCT_2:def 3;
    then consider xy0 being set such that
A9:   xy0 in dom f and
A10:   f.xy0 = j0 by FUNCT_1:def 5;
    consider x0, y0 being set such that
A11:   x0 in the carrier of S and
A12:   y0 in the carrier of T and
A13:   xy0 = [x0,y0] by A7,A9,ZFMISC_1:def 2;
    consider xy1 being set such that
A14:   xy1 in dom f and
A15:   f.xy1 = j1 by A8,FUNCT_1:def 5;
    consider x1, y1 being set such that
A16:   x1 in the carrier of S and
A17:   y1 in the carrier of T and
A18:   xy1 = [x1,y1] by A7,A14,ZFMISC_1:def 2;
    reconsider x0, x1 as Point of S by A11,A16;
    reconsider y0, y1 as Point of T by A12,A17;
    reconsider S, T as non empty TopSpace by A2,BORSUK_3:4;
    reconsider X0 = {x0} as non empty Subset of S by ZFMISC_1:37;
    reconsider Y1 = {y1} as non empty Subset of T by ZFMISC_1:37;
    T, [:S|X0,T:] are_homeomorphic by BORSUK_3:15;
    then
A19: [:S|X0,T:] is connected by TOPREAL6:25;
    S, [:T|Y1,S:] are_homeomorphic by BORSUK_3:15;
    then
A20: [:T|Y1,S:] is connected by TOPREAL6:25;
    [:S,T|Y1:], [:T|Y1,S:] are_homeomorphic by YELLOW12:44;
    then
A21: [:S,T|Y1:] is connected by A20,TOPREAL6:25;
A22: [#]T = the carrier of T by PRE_TOPC:12;
A23: [#]S = the carrier of S by PRE_TOPC:12;
    set g = f | [:X0,[#]T:];
A24: dom g = [:X0,[#]T:] by A6,RELAT_1:91;
A25: the carrier of [:S|X0,T:] = [:the carrier of S|X0,the carrier of T:]
      by BORSUK_1:def 5;
A26: dom g = the carrier of [:S|X0,T:] by A22,A24,A25,JORDAN1:1;
    rng g c= the carrier of J;
    then reconsider g as map of [:S|X0,T:],J by A26,FUNCT_2:4;
    [:S|X0,T:] = [:S|X0,T| [#]T:] by TSEP_1:3
       .= [:S,T:] | [:X0,[#]T:] by BORSUK_3:26;
    then
A27: g is continuous by A3,TOPMETR:10;
    set h = f | [:[#]S,Y1:];
A28: dom h = [:[#]S,Y1:] by A6,RELAT_1:91;
A29: the carrier of [:S,T|Y1:] = [:the carrier of S,the carrier of T|Y1:]
      by BORSUK_1:def 5;
A30: dom h = the carrier of [:S,T|Y1:] by A23,A28,A29,JORDAN1:1;
    rng h c= the carrier of J;
    then reconsider h as map of [:S,T|Y1:],J by A30,FUNCT_2:4;
    [:S,T|Y1:] = [:S| [#]S,T|Y1:] by TSEP_1:3
       .= [:S,T:] | [:[#]S,Y1:] by BORSUK_3:26;
    then
A31: h is continuous by A3,TOPMETR:10;
A32: now
      assume
A33:     f. [x0,y1] <> 0;
      g is onto
      proof
        thus rng g c= the carrier of J;
        let y be set;
        assume
A34:       y in the carrier of J;
A35:     x0 in X0 by TARSKI:def 1;
        per cases by A5,A34,TARSKI:def 2;
        suppose
A36:       y = 0;
A37:     [x0,y0] in dom g by A22,A24,A35,ZFMISC_1:106;
        then
        g. [x0,y0] = y by A10,A13,A24,A36,FUNCT_1:72;
        hence y in rng g by A37,FUNCT_1:def 5;
        end;
        suppose
A38:       y = 1;
        [x0,y1] in dom f by A6,A7,A11,A17,ZFMISC_1:106;
        then
A39:     f. [x0,y1] in rng f by FUNCT_1:def 5;
A40:     [x0,y1] in dom g by A22,A24,A35,ZFMISC_1:106;
        then
        g. [x0,y1] = f. [x0,y1] by A24,FUNCT_1:72
          .= y by A5,A33,A38,A39,TARSKI:def 2;
        hence y in rng g by A40,FUNCT_1:def 5;
        end;
      end;
      hence contradiction by A19,A27,Th8;
    end;
    now
      assume
A41:     f. [x0,y1] <> 1;
      h is onto
      proof
        thus rng h c= the carrier of J;
        let y be set;
        assume
A42:       y in the carrier of J;
A43:     y1 in Y1 by TARSKI:def 1;
        per cases by A5,A42,TARSKI:def 2;
        suppose
A44:       y = 1;
A45:     [x1,y1] in dom h by A23,A28,A43,ZFMISC_1:106;
        then
        h. [x1,y1] = y by A15,A18,A28,A44,FUNCT_1:72;
        hence y in rng h by A45,FUNCT_1:def 5;
        end;
        suppose
A46:       y = 0;
        [x0,y1] in dom f by A6,A7,A11,A17,ZFMISC_1:106;
        then
A47:     f. [x0,y1] in rng f by FUNCT_1:def 5;
A48:     [x0,y1] in dom h by A23,A28,A43,ZFMISC_1:106;
        then
        h. [x0,y1] = f. [x0,y1] by A28,FUNCT_1:72
          .= y by A5,A41,A46,A47,TARSKI:def 2;
        hence y in rng h by A48,FUNCT_1:def 5;
        end;
      end;
      hence contradiction by A21,A31,Th8;
    end;
    hence contradiction by A32;
  end;
  hence [:A,B:] is connected by A1,A2,Th8;
  end;
  suppose
A49:  [:S,T:] is empty;
  for T being empty TopStruct holds T is connected;
  hence [:A,B:] is connected by A1,A49;
  end;
end;
end;

theorem
  for S, T being TopSpace, A being Subset of S, B being Subset of T
  st A is connected & B is connected holds
  [:A,B:] is connected
proof
  let S, T be TopSpace;
  let A be Subset of S;
  let B be Subset of T;
  assume S|A is connected & T|B is connected;
  then reconsider SA = S|A, TB = T|B as connected TopSpace;
  [:SA,TB:] is connected;
  hence [:S,T:] | [:A,B:] is connected by BORSUK_3:26;
end;

theorem
  for S, T be TopSpace, Y being non empty TopSpace, A being Subset of S,
      f being map of [:S,T:],Y, g being map of [:S|A,T:],Y st
    g = f | [:A,the carrier of T:] & f is continuous holds g is continuous
proof
  let S, T be TopSpace,
      Y be non empty TopSpace;
  let A be Subset of S;
  let f be map of [:S,T:],Y;
  let g be map of [:S|A,T:],Y;
  assume that
A1: g = f | [:A,the carrier of T:] and
A2: f is continuous;
  set TT = the TopStruct of T;
A3: the TopStruct of [:S,T:] = [:the TopStruct of S,the TopStruct of T:]
     by Th14;
A4: the carrier of [:S,TT:] = [:the carrier of S,the carrier of TT:]
    by BORSUK_1:def 5;
  the carrier of [:S,T:] = [:the carrier of S,the carrier of T:]
    by BORSUK_1:def 5;
  then reconsider ff = f as map of [:S,TT:],Y by A4;
A5: the carrier of [:S|A,TT:] = [:the carrier of S|A,the carrier of TT:]
    by BORSUK_1:def 5;
  the carrier of [:S|A,T:] = [:the carrier of S|A,the carrier of T:]
    by BORSUK_1:def 5;
  then reconsider gg = g as map of [:S|A,TT:],Y by A5;
A6: the TopStruct of [:S,TT:] = the TopStruct of [:S,T:] by A3,Th14;
A7: gg = ff| [:A,[#]TT:] by A1,PRE_TOPC:12;
A8:  [:S|A,TT:] = [:S|A,TT| [#]TT:] by TSEP_1:3
    .= [:S,TT:]| [:A,[#]TT:] by BORSUK_3:26;
  the TopStruct of [:S|A,TT:] = the TopStruct of [:S|A,T:] by Th14;
  hence g is continuous by A2,A6,A7,A8,TOPMETR:10;
end;

theorem
  for S, T be TopSpace, Y being non empty TopSpace, A being Subset of T,
      f being map of [:S,T:],Y, g being map of [:S,T|A:],Y st
    g = f | [:the carrier of S,A:] & f is continuous holds g is continuous
proof
  let S, T be TopSpace,
      Y be non empty TopSpace;
  let A be Subset of T;
  let f be map of [:S,T:],Y;
  let g be map of [:S,T|A:],Y;
  assume that
A1: g = f | [:the carrier of S,A:] and
A2: f is continuous;
  set SS = the TopStruct of S;
A3: the TopStruct of [:S,T:] = [:the TopStruct of S,the TopStruct of T:]
     by Th14;
A4: the carrier of [:SS,T:] = [:the carrier of SS,the carrier of T:]
    by BORSUK_1:def 5;
  the carrier of [:S,T:] = [:the carrier of S,the carrier of T:]
    by BORSUK_1:def 5;
  then reconsider ff = f as map of [:SS,T:],Y by A4;
A5: the carrier of [:SS,T|A:] = [:the carrier of SS,the carrier of T|A:]
    by BORSUK_1:def 5;
  the carrier of [:S,T|A:] = [:the carrier of S,the carrier of T|A:]
    by BORSUK_1:def 5;
  then reconsider gg = g as map of [:SS,T|A:],Y by A5;
A6: the TopStruct of [:SS,T:] = the TopStruct of [:S,T:] by A3,Th14;
A7: gg = ff| [:[#]SS,A:] by A1,PRE_TOPC:12;
A8:  [:SS,T|A:] = [:SS| [#]SS,T|A:] by TSEP_1:3
    .= [:SS,T:]| [:[#]SS,A:] by BORSUK_3:26;
  the TopStruct of [:SS,T|A:] = the TopStruct of [:S,T|A:] by Th14;
  hence g is continuous by A2,A6,A7,A8,TOPMETR:10;
end;

theorem
  for S, T, T1, T2, Y being non empty TopSpace,
      f being map of [:Y,T1:],S,
      g being map of [:Y,T2:],S,
      F1, F2 being closed Subset of T st
   T1 is SubSpace of T & T2 is SubSpace of T & F1 = [#]T1 & F2 = [#]T2 &
   ([#]T1) \/ ([#]T2) = [#]T &
    f is continuous & g is continuous &
   (for p being set st p in ([#][:Y,T1:]) /\ ([#][:Y,T2:]) holds f.p = g.p)
  ex h being map of [:Y,T:],S st h = f+*g & h is continuous
proof
  let S, T, T1, T2, Y be non empty TopSpace,
      f be map of [:Y,T1:],S,
      g be map of [:Y,T2:],S,
      F1, F2 be closed Subset of T;
  assume that
A1: T1 is SubSpace of T and
A2: T2 is SubSpace of T and
A3: F1 = [#]T1 and
A4: F2 = [#]T2 and
A5: ([#]T1) \/ ([#]T2) = [#]T and
A6: f is continuous and
A7: g is continuous and
A8: for p being set st p in ([#][:Y,T1:]) /\ ([#][:Y,T2:]) holds f.p = g.p;
A9: [:Y,T1:] is SubSpace of [:Y,T:] & [:Y,T2:] is SubSpace of [:Y,T:]
      by A1,A2,BORSUK_3:17;
    set h = f+*g;
A10: the carrier of [:Y,T:] = [#][:Y,T:] by PRE_TOPC:12;
A11: the carrier of [:Y,T1:] = [#][:Y,T1:] by PRE_TOPC:12;
A12: the carrier of [:Y,T2:] = [#][:Y,T2:] by PRE_TOPC:12;
A13: the carrier of T = [#]T by PRE_TOPC:12;
A14: the carrier of T1 = [#]T1 by PRE_TOPC:12;
A15: the carrier of T2 = [#]T2 by PRE_TOPC:12;
A16: the carrier of [:Y,T:] = [:the carrier of Y,the carrier of T:]
      by BORSUK_1:def 5;
A17: the carrier of [:Y,T1:] = [:the carrier of Y,the carrier of T1:]
      by BORSUK_1:def 5;
A18: the carrier of [:Y,T2:] = [:the carrier of Y,the carrier of T2:]
      by BORSUK_1:def 5;
A19: dom f = the carrier of [:Y,T1:] by FUNCT_2:def 1;
A20: dom g = the carrier of [:Y,T2:] by FUNCT_2:def 1;
A21: dom h = dom f \/ dom g by FUNCT_4:def 1;
    then
A22: dom h = [:the carrier of Y,the carrier of T:]
        by A5,A13,A14,A15,A17,A18,A19,A20,ZFMISC_1:120;
    rng f \/ rng g c= the carrier of S & rng h c= rng f \/ rng g
     by FUNCT_4:18;  then
    rng h c= the carrier of S by XBOOLE_1:1;
    then reconsider h as map of [:Y,T:],S by A16,A22,FUNCT_2:4;
    take h;
    thus h = f+*g;
    for P being Subset of S st P is closed holds h"P is closed
    proof
    let P be Subset of S;
    assume
A23:  P is closed;
A24: h"P = h"P /\ ([#][:Y,T1:] \/ [#][:Y,T2:])
       by A11,A12,A16,A19,A20,A21,A22,XBOOLE_1:28
        .= (h"P /\ [#][:Y,T1:]) \/ (h"P /\ [#][:Y,T2:]) by XBOOLE_1:23;
A25: for x being set st x in [#][:Y,T1:] holds h.x = f.x
      proof
       let x be set such that
A26:     x in [#][:Y,T1:];
       now per cases;
        suppose
A27:      x in [#][:Y,T2:];
        then x in [#][:Y,T1:] /\ [#][:Y,T2:] by A26,XBOOLE_0:def 3;
        then f.x = g.x by A8;
        hence thesis by A20,A27,FUNCT_4:14;
        end;
        suppose not x in [#][:Y,T2:];
        hence h.x = f.x by A12,A20,FUNCT_4:12;
        end;
       end;
       hence thesis;
      end;
      now let x be set;
      thus x in h"P /\ [#][:Y,T1:] implies x in f"P
       proof
        assume x in h"P /\ [#][:Y,T1:];
        then x in h"P & x in [#][:Y,T1:] by XBOOLE_0:def 3;
        then h.x in P & x in dom f & f.x = h.x by A19,A25,FUNCT_1:def 13;
        hence x in f"P by FUNCT_1:def 13;
       end;
      assume x in f"P;
      then x in dom f & f.x in P by FUNCT_1:def 13;
      then x in dom h & x in [#][:Y,T1:] & h.x in P
        by A11,A21,A25,XBOOLE_0:def 2;
      then x in h"P & x in [#][:Y,T1:] by FUNCT_1:def 13;
      hence x in h"P /\ [#][:Y,T1:] by XBOOLE_0:def 3;
     end;
     then
A28:  h"P /\ [#][:Y,T1:] = f"P by TARSKI:2;
     now let x be set;
      thus x in h"P /\ [#][:Y,T2:] implies x in g"P
       proof
        assume x in h"P /\ [#][:Y,T2:];
        then x in h"P & x in [#][:Y,T2:] by XBOOLE_0:def 3;
        then h.x in P & x in dom g & g.x = h.x
          by A20,FUNCT_1:def 13,FUNCT_4:14;
        hence x in g"P by FUNCT_1:def 13;
       end;
      assume x in g"P;
      then x in dom g & g.x in P by FUNCT_1:def 13;
      then x in dom h & x in [#][:Y,T2:] & h.x in P
        by A12,A21,FUNCT_4:14,XBOOLE_0:def 2;
      then x in h"P & x in [#][:Y,T2:] by FUNCT_1:def 13;
      hence x in h"P /\ [#][:Y,T2:] by XBOOLE_0:def 3;
     end;
     then
A29: h"P = f"P \/ g"P by A24,A28,TARSKI:2;
     the carrier of T1 is Subset of the carrier of T by A1,TSEP_1:1;
     then f"P c= [#][:Y,T1:] & [#][:Y,T1:] c= [#][:Y,T:]
       by A10,A11,A16,A17,ZFMISC_1:118;
    then f"P c= [#][:Y,T:] by XBOOLE_1:1;
    then reconsider P1 = f"P as Subset of [:Y,T:] by PRE_TOPC:12;
    the carrier of T2 is Subset of the carrier of T by A2,TSEP_1:1;
    then g"P c= [#][:Y,T2:] & [#][:Y,T2:] c= [#][:Y,T:]
      by A10,A12,A16,A18,ZFMISC_1:118;
    then g"P c= [#][:Y,T:] by XBOOLE_1:1;
    then reconsider P2 = g"P as Subset of [:Y,T:] by PRE_TOPC:12;
     f"P is closed by A6,A23,PRE_TOPC:def 12;
     then consider F01 being Subset of [:Y,T:] such that
A30:  F01 is closed & f"P = F01 /\ [#][:Y,T1:] by A9,PRE_TOPC:43;
    reconsider M = [:[#]Y,F1:] as Subset of [:Y,T:];
A31: M is closed by Th16;
    [#][:Y,T1:] = [:[#]Y,F1:] by A3,BORSUK_3:1;
    then
A32: P1 is closed by A30,A31,TOPS_1:35;
    g"P is closed by A7,A23,PRE_TOPC:def 12;
    then consider F02 being Subset of [:Y,T:] such that
A33:  F02 is closed & g"P = F02 /\ [#][:Y,T2:] by A9,PRE_TOPC:43;
    reconsider M = [:[#]Y,F2:] as Subset of [:Y,T:];
A34: M is closed by Th16;
    [#][:Y,T2:] = [:[#]Y,F2:] by A4,BORSUK_3:1;
    then
    P2 is closed by A33,A34,TOPS_1:35;
    hence h"P is closed by A29,A32,TOPS_1:36;
  end;
  hence thesis by PRE_TOPC:def 12;
end;

theorem
  for S, T, T1, T2, Y being non empty TopSpace,
      f being map of [:T1,Y:],S,
      g being map of [:T2,Y:],S,
      F1, F2 being closed Subset of T st
   T1 is SubSpace of T & T2 is SubSpace of T & F1 = [#]T1 & F2 = [#]T2 &
   ([#]T1) \/ ([#]T2) = [#]T &
    f is continuous & g is continuous &
   (for p being set st p in ([#][:T1,Y:]) /\ ([#][:T2,Y:]) holds f.p = g.p)
  ex h being map of [:T,Y:],S st h = f+*g & h is continuous
proof
  let S, T, T1, T2, Y be non empty TopSpace,
      f be map of [:T1,Y:],S,
      g be map of [:T2,Y:],S,
      F1, F2 be closed Subset of T;
  assume that
A1: T1 is SubSpace of T and
A2: T2 is SubSpace of T and
A3: F1 = [#]T1 and
A4: F2 = [#]T2 and
A5: ([#]T1) \/ ([#]T2) = [#]T and
A6: f is continuous and
A7: g is continuous and
A8: for p being set st p in ([#][:T1,Y:]) /\ ([#][:T2,Y:]) holds f.p = g.p;
    Y is SubSpace of Y by TSEP_1:2;
    then
A9: [:T1,Y:] is SubSpace of [:T,Y:] & [:T2,Y:] is SubSpace of [:T,Y:]
      by A1,A2,BORSUK_3:25;
    set h = f+*g;
A10: the carrier of [:T,Y:] = [#][:T,Y:] by PRE_TOPC:12;
A11: the carrier of [:T1,Y:] = [#][:T1,Y:] by PRE_TOPC:12;
A12: the carrier of [:T2,Y:] = [#][:T2,Y:] by PRE_TOPC:12;
A13: the carrier of T = [#]T by PRE_TOPC:12;
A14: the carrier of T1 = [#]T1 by PRE_TOPC:12;
A15: the carrier of T2 = [#]T2 by PRE_TOPC:12;
A16: the carrier of [:T,Y:] = [:the carrier of T,the carrier of Y:]
      by BORSUK_1:def 5;
A17: the carrier of [:T1,Y:] = [:the carrier of T1,the carrier of Y:]
      by BORSUK_1:def 5;
A18: the carrier of [:T2,Y:] = [:the carrier of T2,the carrier of Y:]
      by BORSUK_1:def 5;
A19: dom f = the carrier of [:T1,Y:] by FUNCT_2:def 1;
A20: dom g = the carrier of [:T2,Y:] by FUNCT_2:def 1;
A21: dom h = dom f \/ dom g by FUNCT_4:def 1;
    then
A22: dom h = [:the carrier of T,the carrier of Y:]
        by A5,A13,A14,A15,A17,A18,A19,A20,ZFMISC_1:120;
    rng f \/ rng g c= the carrier of S & rng h c= rng f \/ rng g
     by FUNCT_4:18;  then
    rng h c= the carrier of S by XBOOLE_1:1;
    then reconsider h as map of [:T,Y:],S by A16,A22,FUNCT_2:4;
    take h;
    thus h = f+*g;
    for P being Subset of S st P is closed holds h"P is closed
    proof
    let P be Subset of S;
    assume
A23:  P is closed;
A24: h"P = h"P /\ ([#][:T1,Y:] \/ [#][:T2,Y:])
       by A11,A12,A16,A19,A20,A21,A22,XBOOLE_1:28
        .= (h"P /\ [#][:T1,Y:]) \/ (h"P /\ [#][:T2,Y:]) by XBOOLE_1:23;
A25: for x being set st x in [#][:T1,Y:] holds h.x = f.x
      proof
       let x be set such that
A26:     x in [#][:T1,Y:];
       now per cases;
        suppose
A27:      x in [#][:T2,Y:];
        then x in [#][:T1,Y:] /\ [#][:T2,Y:] by A26,XBOOLE_0:def 3;
        then f.x = g.x by A8;
        hence thesis by A20,A27,FUNCT_4:14;
        end;
        suppose not x in [#][:T2,Y:];
        hence h.x = f.x by A12,A20,FUNCT_4:12;
        end;
       end;
       hence thesis;
      end;
      now let x be set;
      thus x in h"P /\ [#][:T1,Y:] implies x in f"P
       proof
        assume x in h"P /\ [#][:T1,Y:];
        then x in h"P & x in [#][:T1,Y:] by XBOOLE_0:def 3;
        then h.x in P & x in dom f & f.x = h.x by A19,A25,FUNCT_1:def 13;
        hence x in f"P by FUNCT_1:def 13;
       end;
      assume x in f"P;
      then x in dom f & f.x in P by FUNCT_1:def 13;
      then x in dom h & x in [#][:T1,Y:] & h.x in P
        by A11,A21,A25,XBOOLE_0:def 2;
      then x in h"P & x in [#][:T1,Y:] by FUNCT_1:def 13;
      hence x in h"P /\ [#][:T1,Y:] by XBOOLE_0:def 3;
     end;
     then
A28:  h"P /\ [#][:T1,Y:] = f"P by TARSKI:2;
     now let x be set;
      thus x in h"P /\ [#][:T2,Y:] implies x in g"P
       proof
        assume x in h"P /\ [#][:T2,Y:];
        then x in h"P & x in [#][:T2,Y:] by XBOOLE_0:def 3;
        then h.x in P & x in dom g & g.x = h.x
          by A20,FUNCT_1:def 13,FUNCT_4:14;
        hence x in g"P by FUNCT_1:def 13;
       end;
      assume x in g"P;
      then x in dom g & g.x in P by FUNCT_1:def 13;
      then x in dom h & x in [#][:T2,Y:] & h.x in P
        by A12,A21,FUNCT_4:14,XBOOLE_0:def 2;
      then x in h"P & x in [#][:T2,Y:] by FUNCT_1:def 13;
      hence x in h"P /\ [#][:T2,Y:] by XBOOLE_0:def 3;
     end;
     then
A29: h"P = f"P \/ g"P by A24,A28,TARSKI:2;
     the carrier of T1 is Subset of the carrier of T by A1,TSEP_1:1;
     then f"P c= [#][:T1,Y:] & [#][:T1,Y:] c= [#][:T,Y:]
       by A10,A11,A16,A17,ZFMISC_1:118;
    then f"P c= [#][:T,Y:] by XBOOLE_1:1;
    then reconsider P1 = f"P as Subset of [:T,Y:] by PRE_TOPC:12;
    the carrier of T2 is Subset of the carrier of T by A2,TSEP_1:1;
    then g"P c= [#][:T2,Y:] & [#][:T2,Y:] c= [#][:T,Y:]
      by A10,A12,A16,A18,ZFMISC_1:118;
    then g"P c= [#][:T,Y:] by XBOOLE_1:1;
    then reconsider P2 = g"P as Subset of [:T,Y:] by PRE_TOPC:12;
     f"P is closed by A6,A23,PRE_TOPC:def 12;
     then consider F01 being Subset of [:T,Y:] such that
A30:  F01 is closed & f"P = F01 /\ [#][:T1,Y:] by A9,PRE_TOPC:43;
    reconsider M = [:F1,[#]Y:] as Subset of [:T,Y:];
A31: M is closed by Th16;
    [#][:T1,Y:] = [:F1,[#]Y:] by A3,BORSUK_3:1;
    then
A32: P1 is closed by A30,A31,TOPS_1:35;
    g"P is closed by A7,A23,PRE_TOPC:def 12;
    then consider F02 being Subset of [:T,Y:] such that
A33:  F02 is closed & g"P = F02 /\ [#][:T2,Y:] by A9,PRE_TOPC:43;
    reconsider M = [:F2,[#]Y:] as Subset of [:T,Y:];
A34: M is closed by Th16;
    [#][:T2,Y:] = [:F2,[#]Y:] by A4,BORSUK_3:1;
    then
    P2 is closed by A33,A34,TOPS_1:35;
    hence h"P is closed by A29,A32,TOPS_1:36;
  end;
  hence thesis by PRE_TOPC:def 12;
end;

begin :: On the fundamental groups

registration
  let T be non empty TopSpace, t be Point of T;
  cluster -> continuous Loop of t;
coherence
proof
  t,t are_connected;
  hence thesis by BORSUK_2:def 2;
end;
end;

theorem
  for T being non empty TopSpace, t being Point of T,
      x being Point of I[01], P being constant Loop of t holds
  P.x = t
proof
  let T be non empty TopSpace,
      t be Point of T,
      x be Point of I[01],
      P be constant Loop of t;
  P = I[01] --> t by BORSUK_2:6;
  hence P.x = t by Th5;
end;

theorem Th23:
  for T being non empty TopSpace, t being Point of T, P being Loop of t holds
  P.0 = t & P.1 = t
proof
  let T be non empty TopSpace,
      t be Point of T,
      P be Loop of t;
  t,t are_connected;
  hence thesis by BORSUK_2:def 2;
end;

theorem Th24:
  for S, T being non empty TopSpace,
      f being continuous map of S,T,
      a, b being Point of S st a,b are_connected holds
   f.a,f.b are_connected
proof
  let S, T be non empty TopSpace;
  let f be continuous map of S,T;
  let a, b be Point of S;
  given g being map of I[01], S such that
A1: g is continuous and
A2: g.0 = a and
A3: g.1 = b;
  take h = f*g;
  thus h is continuous by A1,TOPS_2:58;
  thus h.0 = f.(g.j0) by FUNCT_2:21
    .= f.a by A2;
  thus h.1 = f.(g.j1) by FUNCT_2:21
    .= f.b by A3;
end;

theorem
  for S, T being non empty TopSpace,
      f being continuous map of S,T,
      a, b being Point of S,
      P being Path of a,b st a,b are_connected
   holds f*P is Path of f.a,f.b
proof
  let S, T be non empty TopSpace;
  let f be continuous map of S,T;
  let a, b be Point of S;
  let P be Path of a,b;
  assume
A1:  a,b are_connected;
  then P is continuous by BORSUK_2:def 2;
  then
A2: f*P is continuous by TOPS_2:58;
A3:(f*P).0 = f.(P.j0) by FUNCT_2:21
          .= f.a by A1,BORSUK_2:def 2;
A4:(f*P).1 = f.(P.j1) by FUNCT_2:21
         .= f.b by A1,BORSUK_2:def 2;
  f.a,f.b are_connected by A1,Th24;
  hence thesis by A2,A3,A4,BORSUK_2:def 2;
end;

theorem
  for S being non empty arcwise_connected TopSpace,
      T being non empty TopSpace,
      f being continuous map of S,T,
      a, b being Point of S,
      P being Path of a,b
   holds f*P is Path of f.a,f.b
proof
  let S be non empty arcwise_connected TopSpace;
  let T be non empty TopSpace;
  let f be continuous map of S,T;
  let a, b be Point of S;
  let P be Path of a,b;
A1: a,b are_connected by BORSUK_2:def 3;
A2:(f*P).0 = f.(P.j0) by FUNCT_2:21
          .= f.a by A1,BORSUK_2:def 2;
A3:(f*P).1 = f.(P.j1) by FUNCT_2:21
         .= f.b by A1,BORSUK_2:def 2;
  f.a,f.b are_connected by A1,Th24;
  hence thesis by A2,A3,BORSUK_2:def 2;
end;

theorem Th27:
  for S, T being non empty TopSpace,
      f being continuous map of S,T,
      a being Point of S,
      P being Loop of a
   holds f*P is Loop of f.a
proof
  let S, T be non empty TopSpace;
  let f be continuous map of S,T;
  let a be Point of S;
  let P be Loop of a;
A1:(f*P).0 = f.(P.j0) by FUNCT_2:21
          .= f.a by Th23;
A2: (f*P).1 = f.(P.j1) by FUNCT_2:21
         .= f.a by Th23;
  f.a,f.a are_connected;
  hence thesis by A1,A2,BORSUK_2:def 2;
end;

theorem Th28:
  for S, T being non empty TopSpace,
      f being continuous map of S,T,
      a, b being Point of S,
      P, Q being Path of a,b,
      P1, Q1 being Path of f.a,f.b st P,Q are_homotopic & P1 = f*P & Q1 = f*Q
  holds P1,Q1 are_homotopic
proof
  let S, T be non empty TopSpace;
  let f be continuous map of S,T;
  let a, b be Point of S;
  let P, Q be Path of a,b;
  let P1, Q1 be Path of f.a,f.b;
  assume that
A1: P,Q are_homotopic and
A2: P1 = f*P and
A3: Q1 = f*Q;
  consider F being Homotopy of P,Q;
  take G = f*F;
  F is continuous by A1,BORSUK_6:def 13;
  hence G is continuous by TOPS_2:58;
  let s be Point of I[01];
  thus G.(s,0) = f.(F.(s,j0)) by Lm1,Th3
    .= f.(P.s) by A1,BORSUK_6:def 13
    .= P1.s by A2,FUNCT_2:21;
  thus G.(s,1) = f.(F.(s,j1)) by Lm1,Th3
    .= f.(Q.s) by A1,BORSUK_6:def 13
    .= Q1.s by A3,FUNCT_2:21;
  let t be Point of I[01];
  thus G.(0,t) = f.(F.(j0,t)) by Lm1,Th3
    .= f.a by A1,BORSUK_6:def 13;
  thus G.(1,t) = f.(F.(j1,t)) by Lm1,Th3
    .= f.b by A1,BORSUK_6:def 13;
end;

theorem
  for S, T being non empty TopSpace,
      f being continuous map of S,T,
      a, b being Point of S,
      P, Q being Path of a,b,
      P1, Q1 being Path of f.a,f.b,
      F being Homotopy of P,Q st P,Q are_homotopic & P1 = f*P & Q1 = f*Q
  holds f*F is Homotopy of P1,Q1
proof
  let S, T be non empty TopSpace;
  let f be continuous map of S,T;
  let a, b be Point of S;
  let P, Q be Path of a,b;
  let P1, Q1 be Path of f.a,f.b;
  let F be Homotopy of P,Q;
  assume that
A1: P,Q are_homotopic and
A2: P1 = f*P and
A3: Q1 = f*Q;
  set G = f*F;
  thus P1,Q1 are_homotopic by A1,A2,A3,Th28;
  F is continuous by A1,BORSUK_6:def 13;
  hence G is continuous by TOPS_2:58;
  let s be Point of I[01];
  thus G.(s,0) = f.(F.(s,j0)) by Lm1,Th3
    .= f.(P.s) by A1,BORSUK_6:def 13
    .= P1.s by A2,FUNCT_2:21;
  thus G.(s,1) = f.(F.(s,j1)) by Lm1,Th3
    .= f.(Q.s) by A1,BORSUK_6:def 13
    .= Q1.s by A3,FUNCT_2:21;
  let t be Point of I[01];
  thus G.(0,t) = f.(F.(j0,t)) by Lm1,Th3
    .= f.a by A1,BORSUK_6:def 13;
  thus G.(1,t) = f.(F.(j1,t)) by Lm1,Th3
    .= f.b by A1,BORSUK_6:def 13;
end;

theorem Th30:
  for S, T being non empty TopSpace,
      f being continuous map of S,T,
      a, b, c being Point of S,
      P being Path of a,b,
      Q being Path of b,c,
      P1 being Path of f.a,f.b,
      Q1 being Path of f.b,f.c
  st a,b are_connected & b,c are_connected & P1 = f*P & Q1 = f*Q
  holds P1+Q1 = f*(P+Q)
proof
  let S, T be non empty TopSpace;
  let f be continuous map of S,T;
  let a, b, c be Point of S;
  let P be Path of a,b;
  let Q be Path of b,c;
  let P1 be Path of f.a,f.b;
  let Q1 be Path of f.b,f.c;
  assume that
A1: a,b are_connected and
A2: b,c are_connected and
A3: P1 = f*P and
A4: Q1 = f*Q;
  for x being Point of I[01] holds (P1+Q1).x = (f*(P+Q)).x
  proof
    let x be Point of I[01];
A5: f.a,f.b are_connected by A1,Th24;
A6: f.b,f.c are_connected by A2,Th24;
    per cases;
    suppose
A7:   x <= 1/2;
    then
A8: 2*x is Point of I[01] by BORSUK_6:6;
    thus (P1+Q1).x = P1.(2*x) by A5,A6,A7,BORSUK_2:def 5
      .= f.(P.(2*x)) by A3,A8,FUNCT_2:21
      .= f.((P+Q).x) by A1,A2,A7,BORSUK_2:def 5
      .= (f*(P+Q)).x by FUNCT_2:21;
    end;
    suppose
A9:   x >= 1/2;
    then
A10: 2*x-1 is Point of I[01] by BORSUK_6:7;
    thus (P1+Q1).x = Q1.(2*x-1) by A5,A6,A9,BORSUK_2:def 5
      .= f.(Q.(2*x-1)) by A4,A10,FUNCT_2:21
      .= f.((P+Q).x) by A1,A2,A9,BORSUK_2:def 5
      .= (f*(P+Q)).x by FUNCT_2:21;
    end;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th31:
  for S being non empty TopSpace, s being Point of S
  for x, y being Element of pi_1(S,s)
  for P, Q being Loop of s st x = Class(EqRel(S,s),P) & y = Class(EqRel(S,s),Q)
    holds x*y = Class(EqRel(S,s),P+Q)
proof
  let S be non empty TopSpace;
  let s be Point of S;
  set X = pi_1(S,s);
  let x, y be Element of X;
  let P, Q be Loop of s;
  assume
A1: x = Class(EqRel(S,s),P) & y = Class(EqRel(S,s),Q);
  consider P1, Q1 being Loop of s such that
A2: x = Class(EqRel(S,s),P1) & y = Class(EqRel(S,s),Q1) and
A3: (the mult of X).(x,y) = Class(EqRel(S,s),P1+Q1) by TOPALG_1:def 3;
  P,P1 are_homotopic & Q,Q1 are_homotopic by A1,A2,TOPALG_1:47;
  then
A4: P+Q,P1+Q1 are_homotopic by BORSUK_6:83;
  thus x*y = (the mult of X).(x,y) by GROUP_1:def 1
    .= Class(EqRel(S,s),P+Q) by A3,A4,TOPALG_1:47;
end;

definition
  let S, T be non empty TopSpace,
      s be Point of S,
      f be map of S,T such that
A1: f is continuous;
  set pS = pi_1(S,s);
  set pT = pi_1(T,f.s);
  func FundGrIso(f,s) -> map of pi_1(S,s),pi_1(T,f.s) means :Def1:
  for x being Element of pi_1(S,s)
   ex ls being Loop of s, lt being Loop of f.s st
     x = Class(EqRel(S,s),ls) & lt = f*ls & it.x = Class(EqRel(T,f.s),lt);
existence
proof
  defpred P[set,set] means
  ex ls being Loop of s, lt being Loop of f.s st
    $1 = Class(EqRel(S,s),ls) & lt = f*ls & $2 = Class(EqRel(T,f.s),lt);
A2: for x being Element of pS ex y being Element of pT st P[x,y]
    proof
      let x be Element of pS;
      consider ls being Loop of s such that
A3:     x = Class(EqRel(S,s),ls) by TOPALG_1:48;
      reconsider lt = f*ls as Loop of f.s by A1,Th27;
      reconsider y = Class(EqRel(T,f.s),lt) as Element of pT by TOPALG_1:48;
      take y, ls, lt;
      thus thesis by A3;
    end;
  ex h being map of pS,pT st for x being Element of pS holds P[x,h.x]
    from FUNCT_2:sch 3(A2);
  hence thesis;
end;
uniqueness
proof
  let g, h be map of pS,pT such that
A4: for x being Element of pS
     ex ls being Loop of s, lt being Loop of f.s st
      x = Class(EqRel(S,s),ls) & lt = f*ls & g.x = Class(EqRel(T,f.s),lt) and
A5: for x being Element of pS
     ex ls being Loop of s, lt being Loop of f.s st
      x = Class(EqRel(S,s),ls) & lt = f*ls & h.x = Class(EqRel(T,f.s),lt);
  now
    let x be Element of pS;
    consider lsg being Loop of s, ltg being Loop of f.s such that
A6:   x = Class(EqRel(S,s),lsg) and
A7:   ltg = f*lsg and
A8:   g.x = Class(EqRel(T,f.s),ltg) by A4;
    consider lsh being Loop of s, lth being Loop of f.s such that
A9:   x = Class(EqRel(S,s),lsh) and
A10:   lth = f*lsh and
A11:   h.x = Class(EqRel(T,f.s),lth) by A5;
    lsg,lsh are_homotopic by A6,A9,TOPALG_1:47;
    then ltg,lth are_homotopic by A1,A7,A10,Th28;
    hence g.x = h.x by A8,A11,TOPALG_1:47;
  end;
  hence thesis by FUNCT_2:113;
end;
end;

theorem
  for S, T being non empty TopSpace, s being Point of S,
      f being continuous map of S,T,
      x being Element of pi_1(S,s),
      ls being Loop of s, lt being Loop of f.s
  st x = Class(EqRel(S,s),ls) & lt = f*ls
  holds FundGrIso(f,s).x = Class(EqRel(T,f.s),lt)
proof
  let S, T be non empty TopSpace;
  let s be Point of S;
  let f be continuous map of S,T;
  let x be Element of pi_1(S,s);
  let ls be Loop of s;
  let lt be Loop of f.s;
  assume that
A1: x = Class(EqRel(S,s),ls) and
A2: lt = f*ls;
  consider ls1 being Loop of s, lt1 being Loop of f.s such that
A3:  x = Class(EqRel(S,s),ls1) and
A4:  lt1 = f*ls1 and
A5:  FundGrIso(f,s).x = Class(EqRel(T,f.s),lt1) by Def1;
  ls,ls1 are_homotopic by A1,A3,TOPALG_1:47;
  then lt,lt1 are_homotopic by A2,A4,Th28;
  hence FundGrIso(f,s).x = Class(EqRel(T,f.s),lt) by A5,TOPALG_1:47;
end;

definition
  let S, T be non empty TopSpace,
      s be Point of S,
      f be continuous map of S,T;
  redefine func FundGrIso(f,s) -> Homomorphism of pi_1(S,s),pi_1(T,f.s);
coherence
proof
  set pS = pi_1(S,s);
  set pT = pi_1(T,f.s);
  set h = FundGrIso(f,s);
  let a, b be Element of pS;
  consider lsa being Loop of s, lta being Loop of f.s such that
A1: a = Class(EqRel(S,s),lsa) and
A2: lta = f*lsa and
A3: h.a = Class(EqRel(T,f.s),lta) by Def1;
  consider lsb being Loop of s, ltb being Loop of f.s such that
A4: b = Class(EqRel(S,s),lsb) and
A5: ltb = f*lsb and
A6: h.b = Class(EqRel(T,f.s),ltb) by Def1;
  consider lsab being Loop of s, ltab being Loop of f.s such that
A7: a*b = Class(EqRel(S,s),lsab) and
A8: ltab = f*lsab and
A9: h.(a*b) = Class(EqRel(T,f.s),ltab) by Def1;
A10: lta+ltb = f*(lsa+lsb) by A2,A5,Th30;
  a*b = Class(EqRel(S,s),lsa+lsb) by A1,A4,Th31;
  then lsab,lsa+lsb are_homotopic by A7,TOPALG_1:47;
  then ltab,lta+ltb are_homotopic by A8,A10,Th28;
  hence h.(a * b) = Class(EqRel(T,f.s),lta+ltb) by A9,TOPALG_1:47
     .= h.a * h.b by A3,A6,Th31;
end;
end;

theorem Th33:
  for S, T being non empty TopSpace, s being Point of S,
      f being continuous map of S,T st f is_homeomorphism
  holds FundGrIso(f,s) is_isomorphism
proof
  let S, T be non empty TopSpace;
  let s be Point of S;
  let f be continuous map of S,T such that
A1:  f is_homeomorphism;
  set pS = pi_1(S,s);
  set pT = pi_1(T,f.s);
A2: f" is continuous by A1,TOPS_2:def 5;
    set h = FundGrIso(f,s);
A3: dom h = the carrier of pS by FUNCT_2:def 1;
A4: dom f = [#]S by A1,TOPS_2:def 5;
A5: rng f = [#]T by A1,TOPS_2:def 5;
A6: [#]S = the carrier of S by PRE_TOPC:12;
A7: [#]T = the carrier of T by PRE_TOPC:12;
A8: f is one-to-one by A1,TOPS_2:def 5;
    then
A9: f qua Function".(f.s) = s by FUNCT_2:32;
A10: f qua Function" = f" by A5,A8,TOPS_2:def 4;
A11: rng h = the carrier of pT
  proof
    thus rng h c= the carrier of pT;
    let y be set;
    assume y in the carrier of pT;
    then consider lt being Loop of f.s such that
A12:   y = Class(EqRel(T,f.s),lt) by TOPALG_1:48;
    reconsider ls = f"*lt as Loop of s by A2,A9,A10,Th27;
    set x = Class(EqRel(S,s),ls);
A13: x in the carrier of pS by TOPALG_1:48;
    then consider ls1 being Loop of s, lt1 being Loop of f.s such that
A14:   x = Class(EqRel(S,s),ls1) and
A15:   lt1 = f*ls1 and
A16:   h.x = Class(EqRel(T,f.s),lt1) by Def1;
A17: f*ls = (f*f")*lt by RELAT_1:55
        .= id rng f * lt by A5,A8,TOPS_2:65
        .= lt by A5,A7,FUNCT_2:23;
    ls,ls1 are_homotopic by A14,TOPALG_1:47;
    then lt,lt1 are_homotopic by A15,A17,Th28;
    then h.x = y by A12,A16,TOPALG_1:47;
    hence thesis by A3,A13,FUNCT_1:def 5;
  end;
  h is one-to-one
  proof
    let x1, x2 be set;
    assume x1 in dom h;
    then consider ls1 being Loop of s, lt1 being Loop of f.s such that
A18:   x1 = Class(EqRel(S,s),ls1) and
A19:   lt1 = f*ls1 and
A20:   h.x1 = Class(EqRel(T,f.s),lt1) by Def1;
    assume x2 in dom h;
    then consider ls2 being Loop of s, lt2 being Loop of f.s such that
A21:   x2 = Class(EqRel(S,s),ls2) and
A22:   lt2 = f*ls2 and
A23:   h.x2 = Class(EqRel(T,f.s),lt2) by Def1;
    reconsider a1 = f"*lt1, a2 = f"*lt2 as Loop of s by A2,A9,A10,Th27;
A24: f"*f = id dom f by A5,A8,TOPS_2:65;
A25: f"*lt1 = (f"*f)*ls1 by A19,RELAT_1:55
        .= ls1 by A4,A6,A24,FUNCT_2:23;
A26: f"*lt2 = (f"*f)*ls2 by A22,RELAT_1:55
          .= ls2 by A4,A6,A24,FUNCT_2:23;
    assume h.x1 = h.x2;
    then lt1,lt2 are_homotopic by A20,A23,TOPALG_1:47;
    then a1,a2 are_homotopic by A2,A9,A10,Th28;
    hence x1 = x2 by A18,A21,A25,A26,TOPALG_1:47;
  end;
  hence h is_isomorphism by A11,GROUP_6:70;
end;

theorem
  for S, T being non empty TopSpace, s being Point of S, t being Point of T,
      f being continuous map of S,T,
      P being Path of t,f.s,
      h being Homomorphism of pi_1(S,s),pi_1(T,t)
  st f is_homeomorphism & f.s,t are_connected &
     h = pi_1-iso(P) * FundGrIso(f,s)
  holds h is_isomorphism
proof
  let S, T be non empty TopSpace;
  let s be Point of S;
  let t be Point of T;
  let f be continuous map of S,T;
  let P be Path of t,f.s;
  let h be Homomorphism of pi_1(S,s),pi_1(T,t);
  assume f is_homeomorphism;
  then
A1: FundGrIso(f,s) is_isomorphism by Th33;
  assume that
A2: f.s,t are_connected and
A3: h = pi_1-iso(P) * FundGrIso(f,s);
  reconsider G = pi_1-iso(P) as Homomorphism of pi_1(T,f.s),pi_1(T,t)
    by A2,TOPALG_1:51;
  G is_isomorphism by A2,TOPALG_1:56;
  hence thesis by A1,A3,GROUP_6:74;
end;

theorem
  for S being non empty TopSpace,
      T being non empty arcwise_connected TopSpace,
      s being Point of S, t being Point of T st
  S,T are_homeomorphic holds pi_1(S,s),pi_1(T,t) are_isomorphic
proof
  let S be non empty TopSpace;
  let T be non empty arcwise_connected TopSpace;
  let s be Point of S;
  let t be Point of T;
  given f being map of S,T such that
A1:  f is_homeomorphism;
  reconsider f as continuous map of S,T by A1,TOPS_2:def 5;
A2: FundGrIso(f,s) is_isomorphism by A1,Th33;
  consider P being Path of t,f.s;
  take pi_1-iso(P) * FundGrIso(f,s);
  pi_1-iso(P) is_isomorphism by TOPALG_1:57;
  hence thesis by A2,GROUP_6:74;
end;
