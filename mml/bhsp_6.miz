:: On Some Properties of Real {H}ilbert Space, {I}
::  by Hiroshi Yamazaki , Yasumasa Suzuki , Takao Inou\'e and Yasunari Shidama
::
:: Received February 25, 2003
:: Copyright (c) 2003 Association of Mizar Users
 
environ

 vocabularies NUMBERS, BHSP_1, PRE_TOPC, SUBSET_1, ALGSTR_0, BINOP_1, SETWISEO,
      FINSET_1, FINSEQ_1, STRUCT_0, FUNCT_1, RELAT_1, FINSOP_1, FUNCT_2,
      TARSKI, BHSP_5, XBOOLE_0, ARYTM_3, REAL_1, XXREAL_0, CARD_1, NORMSP_1,
      ARYTM_1, SEMI_AF1, SUPINF_2, HAHNBAN, JORDAN2C, COMPLEX1, BINOP_2,
      XREAL_0, ORDINAL1, RLVECT_1, BHSP_3, ZFMISC_1, NAT_1, SEQ_2, BHSP_6,
      RSSPACE2;
 notations TARSKI, XBOOLE_0, SUBSET_1, ZFMISC_1, ORDINAL1, CARD_1, NUMBERS,
      XXREAL_0, XREAL_0, COMPLEX1, REAL_1, NAT_1, RELAT_1, FUNCT_1, FUNCT_2,
      RELSET_1, FINSET_1, BINOP_2, PARTFUN1, STRUCT_0, ALGSTR_0, PRE_TOPC,
      RLVECT_1, BHSP_1, BHSP_2, BHSP_3, BINOP_1, SETWISEO, HAHNBAN, FINSEQ_1,
      RSSPACE2, FINSOP_1, BHSP_5;
 constructors BINOP_1, SETWISEO, REAL_1, NAT_1, NAT_D, BINOP_2, FINSOP_1,
      BHSP_2, BHSP_3, HAHNBAN, BHSP_5, SUPINF_1, RELSET_1;
 registrations XBOOLE_0, SUBSET_1, FUNCT_1, ORDINAL1, RELSET_1, FUNCT_2,
      FINSET_1, NUMBERS, XXREAL_0, XREAL_0, NAT_1, BINOP_2, MEMBERED, STRUCT_0,
      CARD_1;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;
 definitions ALGSTR_0;
 theorems XBOOLE_0, XBOOLE_1, TARSKI, ABSVALUE, FINSEQ_1, INT_1, FINSEQ_4,
      FUNCT_1, NAT_1, FUNCT_2, RLVECT_1, VECTSP_1, SEQ_4, BHSP_1, BHSP_2,
      BHSP_3, BHSP_5, HAHNBAN, FINSOP_1, FINSEQ_3, XCMPLX_0, XCMPLX_1, BINOP_2,
      XREAL_1, COMPLEX1, XXREAL_0, XREAL_0;
 schemes NAT_1, FUNCT_2;

begin :: Preliminaries

reserve X for RealUnitarySpace;
reserve x for Point of X;
reserve i, n for Element of NAT;

definition
  let X such that
A1: the addF of X is commutative associative & the addF of X is having_a_unity;
  let Y be finite Subset of X;
  func setsum Y -> Element of X means
  :Def1:
  ex p being FinSequence of the
  carrier of X st p is one-to-one & rng p = Y & it = (the addF of X) "**" p;
  existence
  proof
    consider p being FinSequence such that
A2: rng p = Y and
A3: p is one-to-one by FINSEQ_4:73;
    reconsider q = p as FinSequence of the carrier of X by A2,FINSEQ_1:def 4;
    ex p being FinSequence of the carrier of X st p is one-to-one & rng p
    = Y & (the addF of X) "**" q = (the addF of X) "**" p by A2,A3;
    hence thesis;
  end;
  uniqueness
  proof
    let X1, X2 be Element of X such that
A4: ex p1 being FinSequence of the carrier of X st p1 is one-to-one &
    rng p1 = Y & X1 = (the addF of X) "**" p1 and
A5: ex p2 being FinSequence of the carrier of X st p2 is one-to-one &
    rng p2 = Y & X2 = (the addF of X) "**" p2;
    consider p2 being FinSequence of the carrier of X such that
A6: p2 is one-to-one & rng p2 = Y and
A7: X2 = (the addF of X) "**" p2 by A5;
    consider p1 being FinSequence of the carrier of X such that
A8: p1 is one-to-one & rng p1 = Y and
A9: X1 = (the addF of X) "**" p1 by A4;
    ex P being Permutation of dom p1 st p2 = p1*P & dom P = dom p1 & rng
    P = dom p1 by A8,A6,BHSP_5:1;
    hence thesis by A1,A9,A7,FINSOP_1:8;
  end;
end;

theorem Th1:
  for X st the addF of X is commutative associative & the addF of X
is having_a_unity for Y be finite Subset of X for I be Function of the carrier
of X,the carrier of X st Y c= dom I & for x be set st x in dom I holds I.x = x
holds setsum(Y) = setopfunc(Y, the carrier of X, the carrier of X, I, the addF
  of X)
proof
  let X such that
A1: the addF of X is commutative associative & the addF of X is having_a_unity;
  let Y be finite Subset of X;
  consider p being FinSequence of the carrier of X such that
A2: p is one-to-one and
A3: rng p = Y and
A4: setsum(Y) = (the addF of X) "**" p by A1,Def1;
  let I be Function of the carrier of X,the carrier of X such that
A5: Y c= dom I and
A6: for x be set st x in dom I holds I.x = x;
  now
    let xd be set;
A7: xd in dom p implies p.xd in rng(p) by FUNCT_1:12;
    xd in dom(Func_Seq(I,p)) iff xd in dom(I*p) by BHSP_5:def 4;
    hence xd in dom(Func_Seq(I,p)) iff xd in dom p by A5,A3,A7,FUNCT_1:21;
  end;
  then
A8: dom Func_Seq(I,p)=dom p by TARSKI:2;
  now
    let s be set;
    assume
A9: s in dom Func_Seq(I,p);
    then reconsider i = s as Element of NAT;
A10: p.i in rng(p) by A8,A9,FUNCT_1:12;
    Func_Seq(I,p).s = (I*p).i by BHSP_5:def 4
      .= I.(p.i) by A8,A9,FUNCT_1:23
      .= p.i by A5,A6,A3,A10;
    hence Func_Seq(I,p).s = p.s;
  end;
  then Func_Seq(I,p) =p by A8,FUNCT_1:9;
  hence thesis by A1,A5,A2,A3,A4,BHSP_5:def 5;
end;

theorem Th2:
  for X st the addF of X is commutative associative & the addF of X
  is having_a_unity for Y1, Y2 be finite Subset of X st Y1 misses Y2 for Z be
  finite Subset of X st Z = Y1 \/ Y2 holds setsum(Z) = setsum(Y1) + setsum(Y2)
proof
  let X such that
A1: the addF of X is commutative associative & the addF of X is having_a_unity;
  reconsider I = id the carrier of X as Function of the carrier of X, the
  carrier of X;
  let Y1, Y2 be finite Subset of X such that
A2: Y1 misses Y2;
A3: dom I = the carrier of X by FUNCT_2:def 1;
  let Z be finite Subset of X;
  assume Z = Y1 \/ Y2;
  then
A4: setopfunc(Z,the carrier of X,the carrier of X,I,the addF of X) =
setopfunc(Y1,the carrier of X,the carrier of X,I,the addF of X) + setopfunc(Y2,
  the carrier of X,the carrier of X,I,the addF of X) by A1,A2,A3,BHSP_5:14;
A5: for x be set st x in the carrier of X holds I.x = x by FUNCT_1:35;
  then setsum(Y1) = setopfunc(Y1,the carrier of X,the carrier of X,I,the addF
of X) & setsum(Y2) = setopfunc(Y2,the carrier of X,the carrier of X,I,the addF
  of X) by A1,A3,Th1;
  hence thesis by A1,A5,A3,A4,Th1;
end;

begin :: Summability

definition
  let X;
  let Y be Subset of X;
  attr Y is summable_set means
  :Def2:
  ex x st for e be Real st e > 0 ex Y0 be
finite Subset of X st Y0 is non empty & Y0 c= Y & for Y1 be finite Subset of X
  st Y0 c= Y1 & Y1 c= Y holds ||.x-setsum(Y1).|| < e;
end;

definition
  let X;
  let Y be Subset of X;
  assume
A1: Y is summable_set;
  func sum Y -> Point of X means
  for e be Real st e > 0 ex Y0 be finite Subset
of X st Y0 is non empty & Y0 c= Y & for Y1 be finite Subset of X st Y0 c= Y1 &
  Y1 c= Y holds ||.it-setsum(Y1).|| < e;
  existence by A1,Def2;
  uniqueness
  proof
    let y1, y2 be Point of X such that
A2: for e1 be Real st e1 > 0 ex Y0 be finite Subset of X st Y0 is non
empty & Y0 c= Y & for Y1 be finite Subset of X st Y0 c= Y1 & Y1 c= Y holds ||.
    y1-setsum(Y1).|| < e1 and
A3: for e2 be Real st e2 > 0 ex Y0 be finite Subset of X st Y0 is non
empty & Y0 c= Y & for Y1 be finite Subset of X st Y0 c= Y1 & Y1 c= Y holds ||.
    y2-setsum(Y1).|| < e2;
A4: now
      let e3 be Real such that
A5:   e3 >0;
      set e4 = e3/2;
      consider Y01 be finite Subset of X such that
      Y01 is non empty and
A6:   Y01 c= Y and
A7:   for Y1 be finite Subset of X st Y01 c= Y1 & Y1 c= Y holds ||.y1-
      setsum(Y1).|| < e4 by A2,A5,XREAL_1:141;
      consider Y02 be finite Subset of X such that
      Y02 is non empty and
A8:   Y02 c= Y and
A9:   for Y1 be finite Subset of X st Y02 c= Y1 & Y1 c= Y holds ||.y2-
      setsum(Y1).|| < e4 by A3,A5,XREAL_1:141;
      set Y00 = Y01 \/ Y02;
A10:  e3/2 + e3/2 = e3 & Y01 c= Y00 by XBOOLE_1:7;
A11:  Y00 c= Y by A6,A8,XBOOLE_1:8;
      then ||.y2-setsum(Y00).|| < e4 by A9,XBOOLE_1:7;
      then ||.-(y2-setsum(Y00)).|| < e4 by BHSP_1:37;
      then ||.y1-setsum(Y00).|| + ||.-(y2-setsum(Y00)).|| < e3 by A7,A11,A10,
XREAL_1:10;
      then (||.y1-setsum(Y00) + -(y2-setsum(Y00)).||) + (||.y1-setsum(Y00).||
+ ||.-(y2-setsum(Y00)).||) < (||.y1-setsum(Y00).|| + ||.-(y2-setsum(Y00)).||) +
      e3 by BHSP_1:36,XREAL_1:10;
      then
A12:  ||.y1-setsum(Y00) + -(y2-setsum(Y00)).|| + (||.y1-setsum(Y00).|| +
||.-(y2-setsum(Y00)).||) + - (||.y1-setsum(Y00).|| + ||.-(y2-setsum(Y00)).||) <
e3 + (||.y1-setsum(Y00).|| + ||.-(y2-setsum(Y00)).||) + - (||.y1-setsum(Y00).||
      + ||.-(y2-setsum(Y00)).||) by XREAL_1:10;
      ||.y1 - y2.|| = ||.(y1 - y2) + 0.X.|| by RLVECT_1:def 7
        .= ||.(y1 - y2) + (setsum(Y00) - setsum(Y00)).|| by RLVECT_1:16
        .= ||.((y1 - y2) + setsum(Y00)) - setsum(Y00).|| by RLVECT_1:def 6
        .= ||.(y1 - (y2 - setsum(Y00))) - setsum(Y00).|| by RLVECT_1:43
        .= ||.y1 - (setsum(Y00) + (y2 - setsum(Y00))).|| by RLVECT_1:41
        .= ||.(y1 - setsum(Y00)) - (y2 - setsum(Y00)).|| by RLVECT_1:41
        .= ||.(y1 - setsum(Y00)) + - (y2 - setsum(Y00)).||;
      hence ||.y1 - y2.|| < e3 by A12;
    end;
    y1 = y2
    proof
      assume y1 <> y2;
      then y1 - y2 <> 0.X by VECTSP_1:66;
      then
A13:  ||.y1 - y2.|| <> 0 by BHSP_1:32;
      0 <= ||.y1 - y2.|| by BHSP_1:34;
      hence contradiction by A4,A13;
    end;
    hence thesis;
  end;
end;

definition
  let X;
  let L be linear-Functional of X;
  attr L is Bounded means
  :Def4:
  ex K be Real st K > 0 & for x holds abs(L.x) <= K * ||.x.||;
end;

definition
  let X;
  let Y be Subset of X;
  attr Y is weakly_summable_set means
  :Def5:
  ex x st for L be
  linear-Functional of X st L is Bounded for e be Real st e > 0 ex Y0 be finite
  Subset of X st Y0 is non empty & Y0 c= Y & for Y1 be finite Subset of X st Y0
  c= Y1 & Y1 c= Y holds abs(L.(x-setsum(Y1))) < e;
end;

definition
  let X;
  let Y be Subset of X;
  let L be Functional of X;
  pred Y is_summable_set_by L means
  :Def6:
  ex r be Real st for e be Real st e
> 0 ex Y0 be finite Subset of X st Y0 is non empty & Y0 c= Y & for Y1 be finite
  Subset of X st Y0 c= Y1 & Y1 c= Y holds abs(r-setopfunc(Y1,the carrier of X,
  REAL, L, addreal)) < e;
end;

definition
  let X;
  let Y be Subset of X;
  let L be Functional of X;
  assume
A1: Y is_summable_set_by L;
  func sum_byfunc(Y,L) -> Real means
  for e be Real st e > 0 ex Y0 be finite
  Subset of X st Y0 is non empty & Y0 c= Y & for Y1 be finite Subset of X st Y0
c= Y1 & Y1 c= Y holds abs(it-setopfunc(Y1,the carrier of X,REAL, L, addreal)) <
  e;
  existence by A1,Def6;
  uniqueness
  proof
    let r1, r2 be Real such that
A2: for e1 be Real st e1 > 0 ex Y0 be finite Subset of X st Y0 is non
empty & Y0 c= Y & for Y1 be finite Subset of X st Y0 c= Y1 & Y1 c= Y holds abs(
    r1-setopfunc(Y1,the carrier of X,REAL, L, addreal)) < e1 and
A3: for e2 be Real st e2 > 0 ex Y0 be finite Subset of X st Y0 is non
empty & Y0 c= Y & for Y1 be finite Subset of X st Y0 c= Y1 & Y1 c= Y holds abs(
    r2-setopfunc(Y1,the carrier of X,REAL, L, addreal)) < e2;
A4: now
      let e3 be real number such that
A5:   e3 >0;
      set e4 = e3/2;
      consider Y01 be finite Subset of X such that
      Y01 is non empty and
A6:   Y01 c= Y and
A7:   for Y1 be finite Subset of X st Y01 c= Y1 & Y1 c= Y holds abs(r1
- setopfunc(Y1,the carrier of X,REAL, L, addreal)) < e4 by A2,A5,XREAL_1:141;
      consider Y02 be finite Subset of X such that
      Y02 is non empty and
A8:   Y02 c= Y and
A9:   for Y1 be finite Subset of X st Y02 c= Y1 & Y1 c= Y holds abs(r2
- setopfunc(Y1,the carrier of X,REAL, L, addreal)) < e4 by A3,A5,XREAL_1:141;
      set Y00 = Y01 \/ Y02;
A10:  e3/2 + e3/2 = e3 & Y01 c= Y00 by XBOOLE_1:7;
A11:  Y00 c= Y by A6,A8,XBOOLE_1:8;
      then
      abs(r2-setopfunc(Y00,the carrier of X,REAL, L, addreal)) < e4 by A9,
XBOOLE_1:7;
      then abs((r1-setopfunc(Y00,the carrier of X,REAL, L, addreal)) - (r2-
setopfunc(Y00,the carrier of X,REAL, L, addreal))) <= abs(r1-setopfunc(Y00,the
carrier of X, REAL, L, addreal)) + abs(r2-setopfunc(Y00,the carrier of X,REAL,
L, addreal)) & abs(r1-setopfunc(Y00,the carrier of X,REAL, L, addreal)) + abs(
      r2-setopfunc( Y00,the carrier of X,REAL, L, addreal)) < e3 by A7,A11,A10,
COMPLEX1:143,XREAL_1:10;
      hence abs(r1-r2) < e3 by XXREAL_0:2;
    end;
    r1 = r2
    proof
      assume
A12:  r1 <> r2;
A13:  abs(r1-r2) <> 0
      proof
        assume abs(r1-r2) = 0;
        then r1-r2 = 0 by ABSVALUE:7;
        hence contradiction by A12;
      end;
      0 <= abs(r1-r2) by COMPLEX1:132;
      hence contradiction by A4,A13;
    end;
    hence thesis;
  end;
end;

theorem Th3:
  for Y be Subset of X st Y is summable_set holds Y is weakly_summable_set
proof
  let Y be Subset of X;
  assume Y is summable_set;
  then consider x such that
A1: for e be Real st 0 < e ex Y0 be finite Subset of X st Y0 is non
empty & Y0 c= Y & for Y1 be finite Subset of X st Y0 c= Y1 & Y1 c= Y holds ||.x
  - setsum(Y1).|| < e by Def2;
  now
    let L be linear-Functional of X;
    assume L is Bounded;
    then consider K be Real such that
A2: 0 < K and
A3: for x holds abs(L.x) <= K * ||.x.|| by Def4;
    now
      let e1 be Real such that
A4:   0 < e1;
      set e = e1/K;
      consider Y0 be finite Subset of X such that
A5:   Y0 is non empty & Y0 c= Y and
A6:   for Y1 be finite Subset of X st Y0 c= Y1 & Y1 c=Y holds ||.x -
      setsum(Y1).|| < e by A1,A2,A4,XREAL_1:141;
A7:   e * K = e1 by A2,XCMPLX_1:88;
      now
        let Y1 be finite Subset of X;
        assume Y0 c= Y1 & Y1 c=Y;
        then K * ||.x - setsum(Y1).|| < e1 by A2,A7,A6,XREAL_1:70;
        then
        abs (L.(x-setsum(Y1))) + K*||.(x-setsum(Y1)).|| < K*||.(x-setsum(
        Y1)).|| + e1 by A3,XREAL_1:10;
        then
        abs (L.(x-setsum(Y1))) + K*||.(x-setsum(Y1)).|| - K*||.(x-setsum(
Y1)).|| < K*||.(x-setsum(Y1)).|| + e1 - K*||.(x-setsum(Y1)).|| by XREAL_1:16;
        hence abs (L.(x-setsum(Y1))) < e1;
      end;
      hence ex Y0 be finite Subset of X st Y0 is non empty & Y0 c= Y & for Y1
be finite Subset of X st Y0 c= Y1 & Y1 c=Y holds abs (L.(x-setsum(Y1))) < e1
by A5;
    end;
    hence for e1 be Real st 0 < e1 ex Y0 be finite Subset of X st Y0 is non
empty & Y0 c= Y & for Y1 be finite Subset of X st Y0 c= Y1 & Y1 c=Y holds abs (
    L.(x-setsum(Y1))) < e1;
  end;
  hence thesis by Def5;
end;

theorem Th4:
  for L be linear-Functional of X for p be FinSequence of the
carrier of X st len p >=1 for q be FinSequence of REAL st dom p = dom q & (for
  i st i in dom q holds q.i = L.(p.i) ) holds L.((the addF of X) "**" p) =
  addreal "**" q
proof
  let L be linear-Functional of X;
  let p be FinSequence of the carrier of X such that
A1: len p >= 1;
  consider f be Function of NAT,the carrier of X such that
A2: f.1 = p.1 and
A3: for n be Element of NAT st 0 <> n & n < len p holds f.(n + 1) = (the
  addF of X).(f.n,p.(n + 1)) and
A4: (the addF of X) "**" p = f.(len p) by A1,FINSOP_1:2;
  let q be FinSequence of REAL such that
A5: dom p = dom q and
A6: for i st i in dom q holds q.i = L.(p.i);
  Seg (len q) = dom p by A5,FINSEQ_1:def 3
    .= Seg (len p) by FINSEQ_1:def 3;
  then
A7: len q = len p by FINSEQ_1:8;
  then consider g be Function of NAT, REAL such that
A8: g.1 = q.1 and
A9: for n be Element of NAT st 0 <> n & n < len q holds g.(n + 1) =
  addreal.(g.n,q.(n + 1)) and
A10: addreal "**" q = g.(len q) by A1,FINSOP_1:2;
  defpred P[Element of NAT] means 1 <= $1 & $1 <= len q implies g.$1 = L.(f.$1
  );
A11: now
    let n;
    assume
A12: P[n];
    now
A13:  n <= n+1 by NAT_1:11;
      assume that
A14:  1 <=n+1 and
A15:  n+1 <= len q;
      per cases;
      suppose
A16:    n=0;
        1 in dom p by A1,FINSEQ_3:27;
        hence g.(n+1) = L.(f.(n+1)) by A5,A6,A2,A8,A16;
      end;
      suppose
A17:    n<>0;
        then
A18:    0+1 <= n by INT_1:20;
        reconsider z=f.n as Element of X;
        reconsider z1=z as VECTOR of X;
A19:    n+1 in dom q by A14,A15,FINSEQ_3:27;
        then p.(n+1) in rng p by A5,FUNCT_1:12;
        then reconsider y=p.(n+1) as Element of X;
        reconsider y1=y as VECTOR of X;
        set Lz=L.z1, Ly=L.y1;
A20:    n+1-1 < len q-0 by A15,XREAL_1:17;
        hence g.(n+1) = addreal.(g.n,q.(n + 1)) by A9,A17
          .= addreal.(L.(f.n), L.y) by A6,A12,A15,A13,A18,A19,XXREAL_0:2
          .= Lz + Ly by BINOP_2:def 9
          .= L .(z1+y1) by HAHNBAN:def 4
          .= L.(f.(n + 1)) by A3,A7,A17,A20;
      end;
    end;
    hence P[n+1];
  end;
A21: P[0];
  for n holds P[n] from NAT_1:sch 1(A21,A11);
  hence thesis by A1,A4,A7,A10;
end;

theorem Th5:
  for X st the addF of X is commutative associative & the addF of X
  is having_a_unity for S be finite Subset of X st S is non empty for L be
linear-Functional of X holds L.(setsum(S)) = setopfunc(S,the carrier of X,REAL,
  L, addreal)
proof
  let X such that
A1: the addF of X is commutative associative & the addF of X is having_a_unity;
  let S be finite Subset of X;
  assume S is non empty;
  then
A2: 0+1 <= card S by INT_1:20;
  let L be linear-Functional of X;
  consider p be FinSequence of the carrier of X such that
A3: p is one-to-one & rng p = S and
A4: setsum(S) = (the addF of X) "**" p by A1,Def1;
  reconsider q1 = Func_Seq(L,p) as FinSequence of REAL;
A5: for i st i in dom p holds q1.i = L.(p.i)
  proof
    let i such that
A6: i in dom p;
    q1.i = (L*p).i by BHSP_5:def 4
      .= L.(p.i) by A6,FUNCT_1:23;
    hence thesis;
  end;
A7: dom L = the carrier of X by FUNCT_2:def 1;
  now
    let xd be set;
A8: xd in dom p implies p.xd in rng(p) by FUNCT_1:12;
    xd in dom(Func_Seq(L,p)) iff xd in dom(L*p) by BHSP_5:def 4;
    hence xd in dom(Func_Seq(L,p)) iff xd in dom p by A7,A8,FUNCT_1:21;
  end;
  then
A9: dom Func_Seq(L,p)=dom p by TARSKI:2;
  len p >=1 by A3,A2,FINSEQ_4:77;
  then L.((the addF of X) "**" p) = addreal "**" q1 by A9,A5,Th4;
  hence thesis by A7,A3,A4,BHSP_5:def 5;
end;

theorem Th6:
  for X st the addF of X is commutative associative & the addF of X
  is having_a_unity for Y be Subset of X holds Y is weakly_summable_set implies
ex x st for L be linear-Functional of X st L is Bounded for e be Real st e > 0
  ex Y0 be finite Subset of X st Y0 is non empty & Y0 c= Y & for Y1 be finite
Subset of X st Y0 c= Y1 & Y1 c= Y holds abs((L.x)-(setopfunc(Y1,the carrier of
  X,REAL, L, addreal))) < e
proof
  let X such that
A1: the addF of X is commutative associative & the addF of X is having_a_unity;
  let Y be Subset of X;
  assume Y is weakly_summable_set;
  then consider x such that
A2: for L be linear-Functional of X st L is Bounded for e be Real st e >
  0 ex Y0 be finite Subset of X st Y0 is non empty & Y0 c= Y & for Y1 be finite
  Subset of X st Y0 c= Y1 & Y1 c= Y holds abs(L.(x-setsum(Y1))) < e by Def5;
  take x;
  now
    let L be linear-Functional of X such that
A3: L is Bounded;
    now
      let e be Real;
      assume e > 0;
      then consider Y0 be finite Subset of X such that
A4:   Y0 is non empty and
A5:   Y0 c= Y and
A6:   for Y1 be finite Subset of X st Y0 c= Y1 & Y1 c= Y holds abs(L.
      (x- setsum(Y1))) < e by A2,A3;
      take Y0;
      now
        set x1 = x;
        let Y1 be finite Subset of X such that
A7:     Y0 c= Y1 and
A8:     Y1 c= Y;
        set y1 = setsum(Y1);
        set r = L.(x-setsum(Y1));
        Y1 <> {} by A4,A7;
        then r = L.x1 - L.y1 & L.y1=setopfunc(Y1,the carrier of X,REAL, L,
        addreal) by A1,Th5,HAHNBAN:32;
        hence abs((L.x)-(setopfunc(Y1,the carrier of X,REAL, L, addreal))) < e
        by A6,A7,A8;
      end;
      hence Y0 is non empty & Y0 c= Y & for Y1 be finite Subset of X st Y0 c=
Y1 & Y1 c= Y holds abs((L.x)-(setopfunc(Y1,the carrier of X,REAL, L, addreal)))
      < e by A4,A5;
    end;
    hence
    for e be Real st e > 0 ex Y0 be finite Subset of X st Y0 is non empty
& Y0 c= Y & for Y1 be finite Subset of X st Y0 c= Y1 & Y1 c= Y holds abs((L.x)-
    (setopfunc(Y1,the carrier of X,REAL, L, addreal))) < e;
  end;
  hence thesis;
end;

theorem Th7:
  for X st the addF of X is commutative associative & the addF of X
  is having_a_unity for Y be Subset of X st Y is weakly_summable_set for L be
  linear-Functional of X st L is Bounded holds Y is_summable_set_by L
proof
  let X such that
A1: the addF of X is commutative associative & the addF of X is having_a_unity;
  let Y be Subset of X;
  assume Y is weakly_summable_set;
  then consider x such that
A2: for L be linear-Functional of X st L is Bounded for e be Real st e >
  0 ex Y0 be finite Subset of X st Y0 is non empty & Y0 c= Y & for Y1 be finite
Subset of X st Y0 c= Y1 & Y1 c= Y holds abs((L.x)-(setopfunc(Y1,the carrier of
  X,REAL, L, addreal))) < e by A1,Th6;
  let L be linear-Functional of X;
  assume L is Bounded;
  then
  for e be Real st e > 0 ex Y0 be finite Subset of X st Y0 is non empty &
Y0 c= Y & for Y1 be finite Subset of X st Y0 c= Y1 & Y1 c= Y holds abs((L.x)-(
  setopfunc(Y1,the carrier of X,REAL, L, addreal))) < e by A2;
  hence thesis by Def6;
end;

theorem
  for X st the addF of X is commutative associative & the addF of X is
  having_a_unity for Y be Subset of X st Y is summable_set for L be
linear-Functional of X st L is Bounded holds Y is_summable_set_by L by Th3,Th7;

theorem
  for Y be finite Subset of X st Y is non empty holds Y is summable_set
proof
  let Y be finite Subset of X such that
A1: Y is non empty;
  set x = setsum Y;
  now
    let e be Real such that
A2: e >0;
    ex Y0 be finite Subset of X st Y0 is non empty & Y0 c= Y & for Y1 be
    finite Subset of X st Y0 c= Y1 & Y1 c= Y holds ||.x-setsum(Y1).|| < e
    proof
      take Y;
      now
        let Y1 be finite Subset of X;
        assume Y c= Y1 & Y1 c= Y;
        then Y1 = Y by XBOOLE_0:def 10;
        then x - setsum(Y1) = 0.X by RLVECT_1:28;
        hence ||.x-setsum(Y1).|| < e by A2,BHSP_1:32;
      end;
      hence thesis by A1;
    end;
    hence ex Y0 be finite Subset of X st Y0 is non empty & Y0 c= Y & for Y1 be
    finite Subset of X st Y0 c= Y1 & Y1 c= Y holds ||.x-setsum(Y1).|| < e;
  end;
  hence thesis by Def2;
end;

begin :: Necessary and sufficient condition for summability

theorem
  for X being RealHilbertSpace
   st the addF of X is commutative associative & the addF of X is
having_a_unity for Y be Subset of X holds Y is summable_set iff
  for e be Real st e > 0 holds ex Y0 be finite Subset of X st Y0 is non empty &
Y0 c= Y & for Y1 be finite Subset of X st Y1 is non empty & Y1 c= Y & Y0 misses
  Y1 holds ||.setsum(Y1).|| < e
proof
  let X be RealHilbertSpace such that
A1: the addF of X is commutative associative & the addF of X is having_a_unity;
  let Y be Subset of X;
A3: now
    defpred P[set,set] means ( $2 is finite Subset of X & $2 is non empty & $2
c= Y & for z be Real st z=$1 for Y1 be finite Subset of X st Y1 is non empty &
    Y1 c= Y & $2 misses Y1 holds ||.setsum(Y1).|| < 1/(z+1));
    assume
A4: for e be Real st e > 0 holds ex Y0 be finite Subset of X st Y0 is
non empty & Y0 c= Y & for Y1 be finite Subset of X st Y1 is non empty & Y1 c= Y
    & Y0 misses Y1 holds ||.setsum(Y1).|| < e;
A5: now
      let x be set;
      assume x in NAT;
      then reconsider xx= x as Element of NAT;
      reconsider e = 1/(xx+1) as Real by XREAL_0:def 1;
      0/(xx + 1) < 1/(xx + 1) by XREAL_1:76;
      then consider Y0 be finite Subset of X such that
A6:   Y0 is non empty & Y0 c= Y and
A7:   for Y1 be finite Subset of X st Y1 is non empty & Y1 c= Y & Y0
      misses Y1 holds ||.setsum(Y1).|| < e by A4;
      for z be Real st z=x for Y1 be finite Subset of X st Y1 is non
      empty & Y1 c= Y & Y0 misses Y1 holds ||.setsum(Y1).|| < 1/(z+1) by A7;
      hence ex y be set st y in bool Y & P[x,y] by A6;
    end;
A8: ex B being Function of NAT, bool Y st for x be set st x in NAT holds
    P[x,B .x] from FUNCT_2:sch 1(A5);
    ex A be Function of NAT, bool Y st for i be Element of NAT holds A.i
is finite Subset of X & A.i is non empty & A.i c= Y & (for Y1 be finite Subset
of X st Y1 is non empty & Y1 c= Y & A.i misses Y1 holds ||.setsum(Y1).|| < 1/(i
    +1)) & for j be Element of NAT st i <= j holds A.i c= A.j
    proof
      consider B being Function of NAT,bool Y such that
A9:   for x be set st x in NAT holds B.x is finite Subset of X & B.x
is non empty & B.x c= Y & for z be Real st z=x for Y1 be finite Subset of X st
Y1 is non empty & Y1 c= Y & B.x misses Y1 holds ||.setsum(Y1).|| < 1/(z+1) by
A8;
      deffunc G(Nat,set) = B.($1+1) \/ $2;
      ex A being Function st dom A = NAT & A.0 = B.0 & for n being Nat
      holds A.(n+1) = G(n,A.n) from NAT_1:sch 11;
      then consider A being Function such that
A10:  dom A = NAT and
A11:  A.0 = B.0 and
A12:  for n being Nat holds A.(n+1) = B.(n+1) \/ A.n;
      defpred R[Element of NAT] means A.$1 is finite Subset of X & A.$1 is non
empty & A.$1 c= Y & (for Y1 be finite Subset of X st Y1 is non empty & Y1 c= Y
& A.$1 misses Y1 holds ||.setsum(Y1).|| < 1/($1+1)) & for j be Element of NAT
      st $1 <= j holds A.$1 c= A.j;
A13:  now
        let n be Element of NAT such that
A14:    R[n];
A15:    for Y1 be finite Subset of X st Y1 is non empty & Y1 c= Y & A.(n+
        1) misses Y1 holds ||.setsum(Y1).|| < 1/((n+1)+1)
        proof
          let Y1 be finite Subset of X such that
A16:      Y1 is non empty & Y1 c= Y and
A17:      A.(n+1) misses Y1;
          A.(n+1) = B.(n+1) \/ A.n by A12;
          then B.(n+1) misses Y1 by A17,XBOOLE_1:7,63;
          hence thesis by A9,A16;
        end;
        defpred P[Element of NAT] means (n+1) <= $1 implies A.(n+1) c= A.$1;
A18:    for j being Element of NAT st P[j] holds P[j+1]
        proof
          let j be Element of NAT such that
A19:      n+1 <= j implies A.(n+1) c= A.j and
A20:      n+1 <= j+1;
          per cases;
          suppose
            n = j;
            hence thesis;
          end;
          suppose
A21:        n <> j;
            A.(j+1) = (B.(j+1) \/ A.j) by A12;
            then
A22:        A.j c= A.(j+1) by XBOOLE_1:7;
            n <= j by A20,XREAL_1:8;
            then n < j by A21,XXREAL_0:1;
            hence thesis by A19,A22,NAT_1:13,XBOOLE_1:1;
          end;
        end;
A23:    P[0];
A24:    for j be Element of NAT holds P[j] from NAT_1:sch 1(A23, A18);
        A.(n+1) = B.(n+1) \/ A.n & B.(n+1) is finite Subset of X by A9,A12;
        hence R[n+1] by A14,A15,A24,XBOOLE_1:8;
      end;
      for j0 be Element of NAT st j0=0 holds for j be Element of NAT st
      j0 <= j holds A.j0 c= A.j
      proof
        let j0 be Element of NAT such that
A25:    j0 = 0;
        defpred P[Element of NAT] means (j0 <= $1 implies A.j0 c= A.$1);
A26:    now
          let j be Element of NAT such that
A27:      P[j];
          A.(j+1) = (B.(j+1) \/ A.j) by A12;
          then A.j c= A.(j+1) by XBOOLE_1:7;
          hence P[j+1] by A25,A27,XBOOLE_1:1;
        end;
A28:    P[0];
        thus for j be Element of NAT holds P[j] from NAT_1:sch 1(A28, A26);
      end;
      then
A29:  R[0] by A9,A11;
A30:  for i be Element of NAT holds R[i] from NAT_1:sch 1(A29,A13);
      now
        let y be set;
        assume y in rng A;
        then consider x be set such that
A31:    x in dom A and
A32:    y = A.x by FUNCT_1:def 5;
        reconsider i = x as Element of NAT by A10,A31;
        A.i c= Y by A30;
        hence y in bool Y by A32;
      end;
      then rng A c= bool Y by TARSKI:def 3;
      then A is Function of NAT, bool Y by A10,FUNCT_2:4;
      hence thesis by A30;
    end;
    then consider A be Function of NAT, bool Y such that
A33: for i be Element of NAT holds A.i is finite Subset of X & A.i is
non empty & A.i c= Y & (for Y1 be finite Subset of X st Y1 is non empty & Y1 c=
Y & A.i misses Y1 holds ||.setsum(Y1).|| < 1/(i+1)) & for j be Element of NAT
    st i <= j holds A.i c= A.j;
    defpred P[set,set] means ex Y1 be finite Subset of X st Y1 is non empty &
    A.$1 = Y1 & $2=setsum(Y1);
A34: for x be set st x in NAT ex y be set st y in the carrier of X & P[x,y ]
    proof
      let x be set;
      assume x in NAT;
      then reconsider i=x as Element of NAT;
      A.i is finite Subset of X by A33;
      then reconsider Y1 = A.x as finite Subset of X;
      reconsider y = setsum(Y1) as set;
      A.i is non empty by A33;
      then
      ex Y1 be finite Subset of X st Y1 is non empty set & A.x = Y1 & y =
      setsum(Y1);
      hence thesis;
    end;
    ex F being Function of NAT, the carrier of X st for x be set st x in
    NAT holds P[x,F.x] from FUNCT_2:sch 1(A34);
    then consider F being Function of NAT, the carrier of X such that
A35: for x be set st x in NAT holds ex Y1 be finite Subset of X st Y1
    is non empty & A.x = Y1 & F.x = setsum(Y1);
    reconsider seq = F as sequence of X;
    now
      let e be Real such that
A36:  e > 0;
A37:  e/2 > 0/2 by A36,XREAL_1:76;
      ex k be Element of NAT st 1/(k+1) < e/2
      proof
        set p = e/2;
        consider k1 be Element of NAT such that
A38:    p"<k1 by SEQ_4:10;
        take k = k1;
        p"+0 < k1+1 by A38,XREAL_1:10;
        then 1/(k1+1) < 1/p" by A37,XREAL_1:78;
        then 1/(k+1) < 1 * p"" by XCMPLX_0:def 9;
        hence thesis;
      end;
      then consider k be Element of NAT such that
A39:  1/(k+1) < e/2;
      now
        let n, m be Element of NAT such that
A40:    n >= k and
A41:    m >= k;
        consider Y2 be finite Subset of X such that
        Y2 is non empty and
A42:    A.m = Y2 and
A43:    seq.m = setsum(Y2) by A35;
        consider Y0 be finite Subset of X such that
        Y0 is non empty and
A44:    A.k = Y0 and
A45:    seq.k = setsum(Y0) by A35;
A46:    Y0 c= Y2 by A33,A41,A44,A42;
        consider Y1 be finite Subset of X such that
        Y1 is non empty and
A47:    A.n = Y1 and
A48:    seq.n = setsum(Y1) by A35;
A49:    Y0 c= Y1 by A33,A40,A44,A47;
        now
          per cases;
          case
A50:        Y0 = Y1;
            now
              per cases;
              case
                Y0 = Y2;
                then (seq.n) - (seq.m) = 0.X by A48,A43,A50,RLVECT_1:16;
                hence ||.(seq.n) - (seq.m).|| < e by A36,BHSP_1:32;
              end;
              case
A51:            Y0 <> Y2;
                ex Y02 be finite Subset of X st Y02 is non empty & Y02
                c= Y & Y02 misses Y0 & Y0 \/ Y02 = Y2
                proof
                  take Y02 = Y2 \ Y0;
A52:              Y2 \ Y0 c= Y2 by XBOOLE_1:36;
                  Y0 \/ Y02 = Y0 \/ Y2 by XBOOLE_1:39
                    .= Y2 by A46,XBOOLE_1:12;
                  hence thesis by A42,A51,A52,XBOOLE_1:1,79;
                end;
                then consider Y02 be finite Subset of X such that
A53:            Y02 is non empty & Y02 c= Y and
A54:            Y02 misses Y0 and
A55:            Y0 \/ Y02 = Y2;
                ||.setsum(Y02).|| < 1/(k+1) by A33,A44,A53,A54;
                then
A56:            ||.setsum(Y02).|| < e/2 by A39,XXREAL_0:2;
                setsum(Y2) = setsum(Y0) + setsum(Y02) by A1,A54,A55,Th2;
                then
A57:            ||.(seq.n) - (seq.m).|| = ||.(setsum(Y0) - setsum(Y0)) -
                setsum(Y02).|| by A48,A43,A50,RLVECT_1:41
                  .= ||.0.X - setsum(Y02).|| by RLVECT_1:28
                  .= ||. - setsum(Y02).|| by RLVECT_1:27
                  .= ||.setsum(Y02).|| by BHSP_1:37;
                e*(1/2) < e*1 by A36,XREAL_1:70;
                hence ||.(seq.n) - (seq.m).|| < e by A56,A57,XXREAL_0:2;
              end;
            end;
            hence ||.(seq.n) - (seq.m).|| < e;
          end;
          case
A58:        Y0 <> Y1;
            now
              per cases;
              case
                Y0 = Y2;
                then
A59:            seq.k - seq.m = 0.X by A45,A43,RLVECT_1:16;
                ex Y01 be finite Subset of X st Y01 is non empty & Y01
                c= Y & Y01 misses Y0 & Y0 \/ Y01 = Y1
                proof
                  take Y01 = Y1 \ Y0;
A60:              Y1 \ Y0 c= Y1 by XBOOLE_1:36;
                  Y0 \/ Y01 = Y0 \/ Y1 by XBOOLE_1:39
                    .= Y1 by A49,XBOOLE_1:12;
                  hence thesis by A47,A58,A60,XBOOLE_1:1,79;
                end;
                then consider Y01 be finite Subset of X such that
A61:            Y01 is non empty & Y01 c= Y and
A62:            Y01 misses Y0 and
A63:            Y0 \/ Y01 = Y1;
                seq.n = seq.k + setsum(Y01) by A1,A45,A48,A62,A63,Th2;
                then
A64:            seq.n - seq.k = seq.k + (setsum(Y01) +- seq.k) by
RLVECT_1:def 6
                  .= seq.k - (seq.k - setsum(Y01)) by RLVECT_1:47
                  .= (setsum(Y0) - setsum(Y0)) + setsum(Y01) by A45,RLVECT_1:43
                  .= 0.X + setsum(Y01) by RLVECT_1:16
                  .= setsum(Y01) by RLVECT_1:10;
                seq.n - seq.m = seq.n - seq.m + 0.X by RLVECT_1:10
                  .= seq.n - seq.m + (seq.k - seq.k) by RLVECT_1:16
                  .= seq.n - (seq.m - (seq.k - seq.k)) by RLVECT_1:43
                  .= seq.n - ((seq.m - seq.k) + seq.k) by RLVECT_1:43
                  .= seq.n - (seq.k - (seq.k - seq.m)) by RLVECT_1:47
                  .= setsum(Y01) + 0.X by A64,A59,RLVECT_1:43;
                then ||.(seq.n) - (seq.m).|| <= ||.setsum(Y01).|| + ||.0.X.||
                by BHSP_1:36;
                then
A65:            ||.(seq.n) - (seq.m).|| <= ||.setsum( Y01).|| + 0 by BHSP_1:32;
                ||. setsum(Y01).|| < 1/(k+1) by A33,A44,A61,A62;
                then ||. setsum(Y01).|| < e/2 by A39,XXREAL_0:2;
                then ||.(seq.n) - (seq.m).|| < e/2 by A65,XXREAL_0:2;
                then ||.(seq.n) - (seq.m).|| + 0 < e/2 + e/2 by A36,XREAL_1:10;
                hence ||.(seq.n) - (seq.m).|| < e;
              end;
              case
A66:            Y0<>Y2;
                ex Y02 be finite Subset of X st Y02 is non empty & Y02
                c= Y & Y02 misses Y0 & Y0 \/ Y02 = Y2
                proof
                  take Y02 = Y2 \ Y0;
A67:              Y2 \ Y0 c= Y2 by XBOOLE_1:36;
                  Y0 \/ Y02 = Y0 \/ Y2 by XBOOLE_1:39
                    .= Y2 by A46,XBOOLE_1:12;
                  hence thesis by A42,A66,A67,XBOOLE_1:1,79;
                end;
                then consider Y02 be finite Subset of X such that
A68:            Y02 is non empty & Y02 c= Y and
A69:            Y02 misses Y0 and
A70:            Y0 \/ Y02 = Y2;
                ||.setsum(Y02).|| < 1/(k+1) by A33,A44,A68,A69;
                then
A71:            ||.setsum(Y02).|| < e/2 by A39,XXREAL_0:2;
                ex Y01 be finite Subset of X st Y01 is non empty & Y01
                c= Y & Y01 misses Y0 & Y0 \/ Y01 = Y1
                proof
                  take Y01 = Y1 \ Y0;
A72:              Y1 \ Y0 c= Y1 by XBOOLE_1:36;
                  Y0 \/ Y01 = Y0 \/ Y1 by XBOOLE_1:39
                    .= Y1 by A49,XBOOLE_1:12;
                  hence thesis by A47,A58,A72,XBOOLE_1:1,79;
                end;
                then consider Y01 be finite Subset of X such that
A73:            Y01 is non empty & Y01 c= Y and
A74:            Y01 misses Y0 and
A75:            Y0 \/ Y01 = Y1;
                setsum(Y1) = setsum(Y0) + setsum(Y01) by A1,A74,A75,Th2;
                then
A76:            seq.n - seq.k = setsum(Y01) + (seq.k +- seq.k) by A45,A48,
RLVECT_1:def 6
                  .= setsum(Y01) + 0.X by RLVECT_1:16
                  .= setsum(Y01) by RLVECT_1:10;
                setsum(Y2) = setsum(Y0) + setsum(Y02) by A1,A69,A70,Th2;
                then
A77:            seq.m - seq.k = setsum(Y02) + (seq.k +- seq.k) by A45,A43,
RLVECT_1:def 6
                  .= setsum(Y02) + 0.X by RLVECT_1:16
                  .= setsum(Y02) by RLVECT_1:10;
                seq.n - seq.m = seq.n - seq.m + 0.X by RLVECT_1:10
                  .= seq.n - seq.m + (seq.k - seq.k) by RLVECT_1:16
                  .= seq.n - (seq.m - (seq.k - seq.k)) by RLVECT_1:43
                  .= seq.n - ((seq.m - seq.k) + seq.k) by RLVECT_1:43
                  .= seq.n - (seq.k - (seq.k - seq.m)) by RLVECT_1:47
                  .= (seq.n - seq.k) + (seq.k +- seq.m) by RLVECT_1:43
                  .= setsum(Y01) +- setsum(Y02) by A76,A77,RLVECT_1:47;
                then ||.(seq.n) - (seq.m).|| <= ||. setsum(Y01).|| + ||. -
                setsum(Y02).|| by BHSP_1:36;
                then
A78:            ||.(seq.n) - (seq.m).|| <= ||. setsum(Y01).|| + ||.
                setsum(Y02).|| by BHSP_1:37;
                ||. setsum(Y01).|| < 1/(k+1) by A33,A44,A73,A74;
                then ||. setsum(Y01).|| < e/2 by A39,XXREAL_0:2;
                then ||.setsum(Y01).|| + ||. setsum(Y02).|| < e/2 + e/2 by A71,
XREAL_1:10;
                hence ||.(seq.n) - (seq.m).|| < e by A78,XXREAL_0:2;
              end;
            end;
            hence ||.(seq.n) - (seq.m).|| < e;
          end;
        end;
        hence ||.(seq.n) - (seq.m).|| < e;
      end;
      hence ex k be Element of NAT st for n, m be Element of NAT st n >= k & m
      >= k holds ||.(seq.n) - (seq.m).|| < e;
    end;
    then
 seq is Cauchy by BHSP_3:2;
    then seq is convergent by BHSP_3:def 6;
    then consider x being Point of X such that
A80: for r be Real st r > 0 ex m be Element of NAT st for n be
    Element of NAT st n >= m holds ||.seq.n - x.|| < r by BHSP_2:9;
    now
      let e be Real such that
A81:  e >0;
A82:  e/2 > 0/2 by A81,XREAL_1:76;
      then consider m be Element of NAT such that
A83:  for n be Element of NAT st n >= m holds ||. (seq.n)-x .|| < e/ 2 by A80;
      ex i be Element of NAT st 1/(i+1) < e/2 & i >= m
      proof
        set p = e/2;
        consider k1 be Element of NAT such that
A84:    p"<k1 by SEQ_4:10;
        take i = k1+m;
        k1 <= k1 + m by NAT_1:11;
        then p" < i by A84,XXREAL_0:2;
        then p"+0<i+1 by XREAL_1:10;
        then 1/(i+1)<1/p" by A82,XREAL_1:78;
        then 1/(i+1)<1 * p"" by XCMPLX_0:def 9;
        hence thesis by NAT_1:11;
      end;
      then consider i be Element of NAT such that
A85:  1/(i+1) < e/2 and
A86:  i >= m;
      consider Y0 be finite Subset of X such that
A87:  Y0 is non empty and
A88:  A.i = Y0 and
A89:  seq.i=setsum(Y0) by A35;
A90:  ||.setsum(Y0) - x.|| < e/2 by A83,A86,A89;
      now
        let Y1 be finite Subset of X such that
A91:    Y0 c= Y1 and
A92:    Y1 c= Y;
        now
          per cases;
          case
            Y0 = Y1;
            then ||.x - setsum(Y1).|| = ||.-(x - setsum(Y0)).|| by BHSP_1:37
              .= ||.setsum(Y0) - x.|| by RLVECT_1:47;
            then ||.x - setsum(Y1).|| < e/2 by A83,A86,A89;
            then ||.x-setsum(Y1).|| + 0 < e/2 + e/2 by A81,XREAL_1:10;
            hence ||.x-setsum(Y1).|| < e;
          end;
          case
A93:        Y0 <> Y1;
            ex Y2 be finite Subset of X st Y2 is non empty & Y2 c= Y &
            Y0 misses Y2 & Y0 \/ Y2 = Y1
            proof
              take Y2 = Y1 \ Y0;
A94:          Y1 \ Y0 c= Y1 by XBOOLE_1:36;
              Y0 \/ Y2 = Y0 \/ Y1 by XBOOLE_1:39
                .= Y1 by A91,XBOOLE_1:12;
              hence thesis by A92,A93,A94,XBOOLE_1:1,79;
            end;
            then consider Y2 be finite Subset of X such that
A95:        Y2 is non empty & Y2 c= Y and
A96:        Y0 misses Y2 and
A97:        Y0 \/ Y2 = Y1;
            setsum(Y1) - x = setsum(Y0) + setsum(Y2) - x by A1,A96,A97,Th2
              .= setsum(Y0) - x + setsum(Y2) by RLVECT_1:def 6;
            then ||.setsum(Y1) - x.|| <= ||.setsum(Y0) - x.|| + ||.setsum(Y2)
            .|| by BHSP_1:36;
            then ||.-(setsum(Y1) - x).|| <= ||.setsum(Y0) - x.|| + ||.setsum(
            Y2).|| by BHSP_1:37;
            then
A98:        ||.x +- setsum(Y1).|| <= ||.setsum(Y0) - x.|| + ||.setsum(Y2
            ).|| by RLVECT_1:47;
            ||.setsum(Y2).|| < 1/(i+1) by A33,A88,A95,A96;
            then ||.setsum(Y2).|| < e/2 by A85,XXREAL_0:2;
            then ||.setsum(Y0)-x.|| + ||.setsum(Y2).|| < e/2 + e/2 by A90,
XREAL_1:10;
            hence ||.x - setsum(Y1).|| < e by A98,XXREAL_0:2;
          end;
        end;
        hence ||.x-setsum(Y1).|| < e;
      end;
      hence ex Y0 be finite Subset of X st Y0 is non empty & Y0 c= Y & for Y1
be finite Subset of X st Y0 c= Y1 & Y1 c= Y holds ||.x-setsum(Y1).|| < e by A87
,A88;
    end;
    hence Y is summable_set by Def2;
  end;
  now
    assume Y is summable_set;
    then consider x being Point of X such that
A99: for e be Real st e > 0 holds ex Y0 be finite Subset of X st Y0 is
non empty & Y0 c= Y & for Y1 be finite Subset of X st Y0 c= Y1 & Y1 c= Y holds
    ||.x-setsum(Y1).|| < e by Def2;
    now
      let e be Real;
      assume e > 0;
      then consider Y0 be finite Subset of X such that
A100: Y0 is non empty and
A101: Y0 c= Y and
A102: for Y1 be finite Subset of X st Y0 c= Y1 & Y1 c= Y holds ||.x-
      setsum (Y1).|| < e/2 by A99,XREAL_1:141;
      reconsider Y0 as finite non empty Subset of X by A100;
      now
        let Y1 be finite Subset of X such that
        Y1 is non empty and
A103:   Y1 c= Y and
A104:   Y0 misses Y1;
        set Z = Y0 \/ Y1;
        Y0 c= Z by XBOOLE_1:7;
        then ||.x - setsum(Z).|| < e/2 by A101,A102,A103,XBOOLE_1:8;
        then
A105:   ||.(x - setsum(Z)).|| + ||.(x - setsum(Y0)).|| < e/2 + e/2 by A101,A102
,XREAL_1:10;
        ||.(x-setsum(Z))-(x-setsum(Y0)).|| <= ||.(x-setsum(Z)).|| + ||.-(
        x-setsum(Y0)).|| by BHSP_1:36;
        then
A106:   ||.(x-setsum(Z))-(x-setsum(Y0)).|| <= ||.(x-setsum(Z)).|| + ||.(x
        -setsum(Y0)).|| by BHSP_1:37;
        setsum(Z) - setsum(Y0) = setsum(Y1) + setsum(Y0) - setsum(Y0) by A1
,A104,Th2
          .= setsum(Y1) + (setsum(Y0) +- setsum(Y0)) by RLVECT_1:def 6
          .= setsum(Y1) + 0.X by RLVECT_1:16
          .= setsum(Y1) by RLVECT_1:10;
        then ||.setsum(Y1).|| = ||.-(setsum(Z) - setsum(Y0)).|| by BHSP_1:37
          .= ||.setsum(Y0) - setsum(Z).|| by RLVECT_1:47
          .= ||.0.X + (setsum(Y0) - setsum(Z)).|| by RLVECT_1:10
          .= ||.(x - x) + (setsum(Y0) - setsum(Z)).|| by RLVECT_1:16
          .= ||.x - (x - (setsum(Y0) - setsum(Z))).|| by RLVECT_1:43
          .= ||.x - ((x - setsum(Y0)) + setsum(Z)).|| by RLVECT_1:43
          .= ||.(x - setsum(Z)) - (x - setsum(Y0)).|| by RLVECT_1:41;
        hence ||.setsum(Y1).|| < e by A106,A105,XXREAL_0:2;
      end;
      hence ex Y0 be finite Subset of X st Y0 is non empty & Y0 c= Y & for Y1
be finite Subset of X st Y1 is non empty & Y1 c= Y & Y0 misses Y1 holds ||.
      setsum(Y1).|| < e by A101;
    end;
    hence
    for e be Real st e > 0 holds ex Y0 be finite Subset of X st Y0 is non
empty & Y0 c= Y & for Y1 be finite Subset of X st Y1 is non empty & Y1 c= Y &
    Y0 misses Y1 holds ||.setsum(Y1).|| < e;
  end;
  hence thesis by A3;
end;
