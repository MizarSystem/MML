:: Real Normed Space
::  by Jan Popio{\l}ek
::
:: Received September 20, 1990
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies NUMBERS, RLVECT_1, STRUCT_0, SUBSET_1, ALGSTR_0, BINOP_1,
      FUNCT_1, ZFMISC_1, XBOOLE_0, REAL_1, PRE_TOPC, SUPINF_2, RLSUB_1,
      FUNCOP_1, CARD_1, RELAT_1, COMPLEX1, ARYTM_3, XXREAL_0, XREAL_0,
      ORDINAL1, ARYTM_1, NAT_1, TARSKI, SEQ_2, ORDINAL2, NORMSP_1, NORMSP_0,
      RELAT_2, METRIC_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0,
      XREAL_0, COMPLEX1, REAL_1, NAT_1, FUNCT_1, FUNCOP_1, FUNCT_2, BINOP_1,
      RELAT_1, SEQ_1, SEQ_2, DOMAIN_1, STRUCT_0, ALGSTR_0, PRE_TOPC, RLVECT_1,
      RLSUB_1, XXREAL_0, NORMSP_0;
 constructors BINOP_1, DOMAIN_1, FUNCOP_1, XXREAL_0, REAL_1, NAT_1, COMPLEX1,
      SEQ_2, RLSUB_1, SEQ_1, VALUED_1, RELSET_1, NORMSP_0;
 registrations XBOOLE_0, ORDINAL1, RELSET_1, NUMBERS, XXREAL_0, XREAL_0,
      MEMBERED, STRUCT_0, RLVECT_1, ALGSTR_0, NORMSP_0;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions RLVECT_1, STRUCT_0, FUNCT_1, ALGSTR_0, NORMSP_0;
 theorems TARSKI, NAT_1, FUNCT_1, SEQ_2, ABSVALUE, RLVECT_1, RLSUB_1, FUNCT_2,
      RELSET_1, XREAL_0, XBOOLE_0, XCMPLX_0, FUNCOP_1, XREAL_1, COMPLEX1,
      XXREAL_0, ORDINAL1, ALGSTR_0, RELAT_1, NORMSP_0;
 schemes FUNCT_2, NAT_1;

begin

definition
  struct(RLSStruct, N-ZeroStr) NORMSTR (# carrier -> set,
   ZeroF -> Element of the carrier,
   addF -> BinOp of the carrier, Mult -> Function of [:REAL, the carrier:], the
    carrier, normF -> Function of the carrier, REAL #);
end;

registration
  cluster non empty strict NORMSTR;
  existence
  proof
    consider A being non empty set, Z being Element of A, a being BinOp of A,
    M being Function of [:REAL,A:],A, n being Function of A,REAL;
    take NORMSTR(#A,Z,a,M,n#);
    thus the carrier of NORMSTR(#A,Z,a,M,n#) is non empty;
    thus thesis;
  end;
end;


reserve a, b for Real;


deffunc 09(NORMSTR) = 0.$1;
consider V being RealLinearSpace;

Lm1: the carrier of (0).V = {0.V} by RLSUB_1:def 3;

reconsider niltonil = (the carrier of (0).V) --> 0 as Function of the carrier
of (0).V, REAL by FUNCOP_1:57;

0.V is VECTOR of (0).V by Lm1,TARSKI:def 1;
then

Lm2: niltonil.(0.V) = 0 by FUNCOP_1:13;

Lm3: for u being VECTOR of (0).V, a holds niltonil.(a * u) = abs(a) * niltonil
.u
proof
  let u be VECTOR of (0).V;
  let a;
  niltonil.u = 0 by FUNCOP_1:13;
  hence thesis by FUNCOP_1:13;
end;

Lm4: for u, v being VECTOR of (0).V holds niltonil.(u + v) <= niltonil.u +
niltonil.v
proof
  let u, v be VECTOR of (0).V;
  u = 0.V & v = 0.V by Lm1,TARSKI:def 1;
  hence thesis by Lm1,Lm2,TARSKI:def 1;
end;

reconsider X = NORMSTR (# the carrier of (0).V, 0.(0).V, the addF of (0).V,
  the Mult of (0).V, niltonil #) as non empty NORMSTR;

Lm5: now
  let x, y be Point of X;
  let a;
  reconsider u = x, w = y as VECTOR of (0).V;
  09(X) = 0.V by RLSUB_1:19;
  hence ||.x.|| = 0 iff x = 09(X) by Lm1,FUNCOP_1:13,TARSKI:def 1;
  a * x = a * u;
  hence ||.a * x.|| = abs(a) * ||.x.|| by Lm3;
  x + y = u + w;
  hence ||.x + y.|| <= ||.x.|| + ||.y.|| by Lm4;
end;

definition
 canceled;
  let IT be non empty NORMSTR;
  attr IT is RealNormSpace-like means
  :Def2:
  for x, y being Point of IT, a holds ||.a * x.|| = abs(a) * ||.x.|| & ||.x + y
  .|| <= ||.x.|| + ||.y.||;
end;

registration
  cluster reflexive discerning RealNormSpace-like vector-distributive
  scalar-distributive scalar-associative scalar-unital Abelian add-associative
    right_zeroed right_complementable strict (non empty NORMSTR);
  existence
  proof
    take X;
    thus X is reflexive
     proof
      thus ||.0.X.|| = 0 by Lm5;
     end;
    thus X is discerning by Lm5,NORMSP_0:def 5;
    thus X is RealNormSpace-like by Def2,Lm5;
    thus X is vector-distributive scalar-distributive scalar-associative
    scalar-unital
    proof
      thus for a being real number for v,w being VECTOR of X holds a * (v + w)
      = a * v + a * w
      proof
        let a be real number;
        let v,w be VECTOR of X;
        reconsider v9= v, w9 = w as VECTOR of (0).V;
        thus a * (v + w) = a *( v9 + w9) .= a * v9 + a * w9 by RLVECT_1:def 8
          .= a * v + a * w;
      end;
      thus for a,b being real number for v being VECTOR of X holds (a + b) * v
      = a * v + b * v
      proof
        let a,b be real number;
        let v be VECTOR of X;
        reconsider v9= v as VECTOR of (0).V;
        thus (a + b) * v = (a + b) * v9 .= a * v9 + b * v9 by RLVECT_1:def 9
          .= a * v + b * v;
      end;
      thus for a,b being real number for v being VECTOR of X holds (a * b) * v
      = a * (b * v)
      proof
        let a,b be real number;
        let v be VECTOR of X;
        reconsider v9= v as VECTOR of (0).V;
        thus (a * b) * v = (a * b) * v9
          .= a * (b * v9) by RLVECT_1:def 10
          .= a * (b * v);
      end;
      let v be VECTOR of X;
      reconsider v9= v as VECTOR of (0).V;
      thus 1 * v = 1 * v9 .= v by RLVECT_1:def 11;
    end;
A1: for x,y be VECTOR of X for x9,y9 be VECTOR of (0).V st x = x9 & y = y9
    holds x + y= x9 + y9 & for a holds a * x = a * x9;
    thus for v,w being VECTOR of X holds v + w = w + v
    proof
      let v,w be VECTOR of X;
      reconsider v9= v, w9= w as VECTOR of (0).V;
      thus v + w = w9+ v9 by A1
        .= w + v;
    end;
    thus for u,v,w being VECTOR of X holds (u + v) + w = u + (v + w)
    proof
      let u,v,w be VECTOR of X;
      reconsider u9= u, v9= v, w9= w as VECTOR of (0).V;
      thus (u + v) + w = (u9 + v9) + w9 .= u9 + (v9 + w9) by RLVECT_1:def 6
        .= u + (v + w);
    end;
    thus for v being VECTOR of X holds v + 0.X = v
    proof
      let v be VECTOR of X;
      reconsider v9= v as VECTOR of (0).V;
      thus v + 0.X = v9+ 0.(0).V .=v by RLVECT_1:10;
    end;
    thus X is right_complementable
    proof
      let v be VECTOR of X;
      reconsider v9= v as VECTOR of (0).V;
      consider w9 be VECTOR of (0).V such that
A2:   v9 + w9 = 0.(0).V by ALGSTR_0:def 11;
      reconsider w = w9 as VECTOR of X;
      take w;
      thus thesis by A2;
    end;
    thus thesis;
  end;
end;

definition
  mode RealNormSpace is reflexive discerning RealNormSpace-like
  vector-distributive scalar-distributive scalar-associative scalar-unital
  Abelian add-associative right_zeroed right_complementable
   (non empty NORMSTR);
end;

reserve RNS for RealNormSpace;
reserve x, y, z, g, g1, g2 for Point of RNS;

canceled 4;

theorem
  ||.0.RNS.|| = 0;

theorem Th6:
  ||.-x.|| = ||.x.||
proof
A1: abs(-1 ) = -(-1) by ABSVALUE:def 1
    .= 1;
  ||.-x.|| = ||.(-1) * x.|| by RLVECT_1:29
    .= abs(-1 ) * ||.x.|| by Def2;
  hence thesis by A1;
end;

theorem Th7:
  ||.x - y.|| <= ||.x.|| + ||.y.||
proof
  ||.x - y.|| <= ||.x.|| + ||.(-y).|| by Def2;
  hence thesis by Th6;
end;

theorem Th8:
  0 <= ||.x.||
proof
  ||.x - x.|| = ||.09(RNS).|| by RLVECT_1:28
    .= 0;
  then 0 <= (||.x.|| + ||.x.||)/2 by Th7;
  hence thesis;
end;

registration
  let RNS,x;
  cluster ||.x.|| -> non negative;
  coherence by Th8;
end;

theorem
  ||.a * x + b * y.|| <= abs(a) * ||.x.|| + abs(b) * ||.y.||
proof
  ||.a * x + b * y.|| <= ||.a * x.|| + ||.b * y.|| by Def2;
  then ||.a * x + b * y.|| <= abs(a) * ||.x.|| + ||.b * y.|| by Def2;
  hence thesis by Def2;
end;

theorem Th10:
  ||.x - y.|| = 0 iff x = y
proof
  ||.x - y.|| = 0 iff x - y = 09(RNS) by NORMSP_0:def 5;
  hence thesis by RLVECT_1:28,35;
end;

theorem Th11:
  ||.x - y.|| = ||.y - x.||
proof
  x - y = - (y - x) by RLVECT_1:47;
  hence thesis by Th6;
end;

theorem Th12:
  ||.x.|| - ||.y.|| <= ||.x - y.||
proof
  (x - y) + y = x - (y - y) by RLVECT_1:43
    .= x - 09(RNS) by RLVECT_1:28
    .= x by RLVECT_1:26;
  then ||.x.|| <= ||.x - y.|| + ||.y.|| by Def2;
  hence thesis by XREAL_1:22;
end;

theorem Th13:
  abs(||.x.|| - ||.y.||) <= ||.x - y.||
proof
  (y - x) + x = y - (x - x) by RLVECT_1:43
    .= y - 09(RNS) by RLVECT_1:28
    .= y by RLVECT_1:26;
  then ||.y.|| <= ||.y - x.|| + ||.x.|| by Def2;
  then ||.y.|| - ||.x.|| <= ||.y - x.|| by XREAL_1:22;
  then ||.y.|| - ||.x.|| <= ||.x - y.|| by Th11;
  then
A1: -(||.y.|| - ||.x.||) >= -||.x - y.|| by XREAL_1:26;
  ||.x.|| - ||.y.|| <= ||.x - y.|| by Th12;
  hence thesis by A1,ABSVALUE:12;
end;

theorem Th14:
  ||.x - z.|| <= ||.x - y.|| + ||.y - z.||
proof
  x - z = x + (09(RNS) + (-z)) by RLVECT_1:10
    .= x + (((-y) + y) + (-z)) by RLVECT_1:16
    .= x + ((-y) + (y + (-z))) by RLVECT_1:def 6
    .= (x - y) + (y - z) by RLVECT_1:def 6;
  hence thesis by Def2;
end;

theorem
  x <> y implies ||.x - y.|| <> 0 by Th10;

reserve S, S1, S2 for sequence of RNS;
reserve k, n, m, m1, m2 for Element of NAT;
reserve r for Real;
reserve f for Function;
reserve d, s, t for set;

canceled;

theorem
  for RNS being non empty 1-sorted, x being Element of RNS holds f is
  sequence of RNS iff ( dom f = NAT & for d st d in NAT holds f.d is Element of
  RNS )
proof
  let RNS be non empty 1-sorted;
  let x be Element of RNS;
  thus f is sequence of RNS implies ( dom f = NAT & for d st d in NAT holds f.
  d is Element of RNS )
  proof
    assume
A1: f is sequence of RNS;
    then
A2: rng f c= the carrier of RNS by RELAT_1:def 19;
A3: dom f = NAT by A1,FUNCT_2:def 1;
    for d st d in NAT holds f.d is Element of RNS
    proof
      let d;
      assume d in NAT;
      then f.d in rng f by A3,FUNCT_1:def 5;
      hence thesis by A2;
    end;
    hence thesis by A1,FUNCT_2:def 1;
  end;
  thus ( dom f = NAT & for d st d in NAT holds f.d is Element of RNS ) implies
  f is sequence of RNS
  proof
    assume that
A4: dom f = NAT and
A5: for d st d in NAT holds f.d is Element of RNS;
    for s st s in rng f holds s in the carrier of RNS
    proof
      let s;
      assume s in rng f;
      then consider d such that
A6:   d in dom f and
A7:   s = f.d by FUNCT_1:def 5;
      f.d is Element of RNS by A4,A5,A6;
      hence thesis by A7;
    end;
    then rng f c= the carrier of RNS by TARSKI:def 3;
    hence thesis by A4,FUNCT_2:def 1,RELSET_1:11;
  end;
end;

canceled;

theorem
  for RNS being non empty 1-sorted, x being Element of RNS ex S being
  sequence of RNS st rng S = {x}
proof
  let RNS be non empty 1-sorted;
  let x be Element of RNS;
  consider f such that
A1: dom f = NAT and
A2: rng f = {x} by FUNCT_1:15;
  reconsider f as sequence of RNS by A1,A2,FUNCT_2:def 1,RELSET_1:11;
  take f;
  thus thesis by A2;
end;

theorem
  for RNS being non empty 1-sorted, S being sequence of RNS st (ex x
  being Element of RNS st for n being Nat holds S.n = x) ex x being Element of
  RNS st rng S={x}
proof
  let RNS be non empty 1-sorted;
  let S be sequence of RNS;
  given z being Element of RNS such that
A1: for n being Nat holds S.n = z;
  take x = z;
  now
    let s;
    assume s in {x}; then
A2: s = x by TARSKI:def 1;
    now
      assume
A3:   not s in rng S;
      now
        let n;
        n in NAT;
        then n in dom S by FUNCT_2:def 1;
        then S.n <> x by A2,A3,FUNCT_1:def 5;
        hence contradiction by A1;
      end;
      hence contradiction;
    end;
    hence s in rng S;
  end;
  then
A4: {x} c= rng S by TARSKI:def 3;
  now
    let t;
    assume t in rng S;
    then consider d such that
A5: d in dom S and
A6: S.d = t by FUNCT_1:def 5;
    d in NAT by A5,FUNCT_2:def 1;
    then t = z by A1,A6;
    hence t in {x} by TARSKI:def 1;
  end;
  then rng S c= {x} by TARSKI:def 3;
  hence thesis by A4,XBOOLE_0:def 10;
end;

theorem
  for RNS being non empty 1-sorted, S being sequence of RNS st ex x
  being Element of RNS st rng S = {x} holds for n holds S.n = S.(n+1)
proof
  let RNS be non empty 1-sorted;
  let S be sequence of RNS;
  given z being Element of RNS such that
A1: rng S = {z};
  let n;
  n in NAT;
  then n in dom S by FUNCT_2:def 1;
  then S.n in {z} by A1,FUNCT_1:def 5;
  then
A2: S.n = z by TARSKI:def 1;
  (n+1) in NAT;
  then (n+1) in dom S by FUNCT_2:def 1;
  then S.(n+1) in {z} by A1,FUNCT_1:def 5;
  hence thesis by A2,TARSKI:def 1;
end;

theorem
  for RNS being non empty 1-sorted, S being sequence of RNS st for n
  holds S.n = S.(n+1) holds for n, k holds S.n = S.(n+k)
proof
  let RNS be non empty 1-sorted;
  let S be sequence of RNS;
  assume
A1: for n holds S.n = S.(n+1);
  let n;
  defpred P[Element of NAT] means S.n = S.(n+$1);
A2: now
    let k such that
A3: P[k];
    S.(n+k) = S.(n+k+1) by A1;
    hence P[k+1] by A3;
  end;
A4: P[0];
  thus for k holds P[k] from NAT_1:sch 1(A4,A2);
end;

theorem
  for RNS being non empty 1-sorted, S being sequence of RNS st for n, k
  holds S.n = S.(n+k) holds for n, m holds S.n = S.m
proof
  let RNS be non empty 1-sorted;
  let S be sequence of RNS;
  assume
A1: for n, k holds S.n = S.(n+k);
  let n, m;
A2: now
    assume m <= n;
    then consider k being Nat such that
A3: n = m + k by NAT_1:10;
    reconsider k as Element of NAT by ORDINAL1:def 13;
    n=m+k by A3;
    hence thesis by A1;
  end;
  now
    assume n <= m;
    then consider k being Nat such that
A4: m = n + k by NAT_1:10;
    reconsider k as Element of NAT by ORDINAL1:def 13;
    m=n+k by A4;
    hence thesis by A1;
  end;
  hence thesis by A2;
end;

theorem
  for RNS being non empty 1-sorted, S being sequence of RNS st for n, m
  holds S.n = S.m ex x being Element of RNS st for n being Nat holds S.n = x
proof
  let RNS be non empty 1-sorted;
  let S be sequence of RNS;
  assume that
A1: for n, m holds S.n = S.m and
A2: for x being Element of RNS ex n being Nat st S.n <> x;
  now
    let n;
    consider z being Element of RNS such that
A3: S.n = z;
    consider k being Nat such that
A4: S.k <> z by A2;
    k in NAT by ORDINAL1:def 13;
    hence contradiction by A1,A3,A4;
  end;
  hence thesis;
end;

canceled 3;

Lm6: for RNS being non empty 1-sorted for S being sequence of RNS for n holds
S.n is Element of RNS;

definition
  canceled;
  let RNS be non empty 1-sorted;
  let S be sequence of RNS;
  let n;
  redefine func S.n -> Element of RNS;
  coherence by Lm6;
end;

definition
  let RNS be non empty addLoopStr;
  let S1, S2 be sequence of RNS;
  canceled;
  func S1 + S2 -> sequence of RNS means
  :Def5:
  for n holds it.n = S1.n + S2.n;
  existence
  proof
    deffunc F(Element of NAT) = S1.$1 + S2.$1;
    thus ex S being sequence of RNS st for n holds S.n = F(n) from FUNCT_2:sch
    4;
  end;
  uniqueness
  proof
    let S, T be sequence of RNS;
    assume that
A1: for n holds S.n = S1.n + S2.n and
A2: for n holds T.n = S1.n + S2.n;
    for n holds S.n = T.n
    proof
      let n;
      S.n = S1.n + S2.n by A1;
      hence thesis by A2;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

definition
  let RNS be non empty addLoopStr;
  let S1, S2 be sequence of RNS;
  func S1 - S2 -> sequence of RNS means
  :Def6:
  for n holds it.n = S1.n - S2.n;
  existence
  proof
    deffunc F(Element of NAT) = S1.$1 - S2.$1;
    thus ex S being sequence of RNS st for n holds S.n = F(n) from FUNCT_2:sch
    4;
  end;
  uniqueness
  proof
    let S, T be sequence of RNS;
    assume that
A1: for n holds S.n = S1.n - S2.n and
A2: for n holds T.n = S1.n - S2.n;
    for n holds S.n = T.n
    proof
      let n;
      S.n = S1.n - S2.n by A1;
      hence thesis by A2;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

definition
  let RNS be non empty addLoopStr;
  let S be sequence of RNS;
  let x be Element of RNS;
  func S - x -> sequence of RNS means
  :Def7:
  for n holds it.n = S.n - x;
  existence
  proof
    deffunc F(Element of NAT) = S.$1 - x;
    thus ex S being sequence of RNS st for n holds S.n = F(n) from FUNCT_2:sch
    4;
  end;
  uniqueness
  proof
    let S1, S2 be sequence of RNS;
    assume that
A1: for n holds S1.n = S.n - x and
A2: for n holds S2.n = S.n - x;
    for n holds S1.n = S2.n
    proof
      let n;
      S1.n = S.n - x by A1;
      hence thesis by A2;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

definition
  let RNS be non empty RLSStruct;
  let S be sequence of RNS;
  let a;
  func a * S -> sequence of RNS means
  :Def8:
  for n holds it.n = a * S.n;
  existence
  proof
    deffunc F(Element of NAT) = a * S.$1;
    thus ex S being sequence of RNS st for n holds S.n = F(n) from FUNCT_2:sch
    4;
  end;
  uniqueness
  proof
    let S1, S2 be sequence of RNS;
    assume that
A1: for n holds S1.n = a * S.n and
A2: for n holds S2.n = a * S.n;
    for n holds S1.n = S2.n
    proof
      let n;
      S1.n = a * S.n by A1;
      hence thesis by A2;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

definition
  let RNS, S;
  attr S is convergent means
  :Def9:
  ex g st for r st 0 < r ex m st for n st m <= n holds ||.(S.n) - g.|| < r;
end;

canceled 6;

theorem Th34:
  S1 is convergent & S2 is convergent implies S1 + S2 is convergent
proof
  assume that
A1: S1 is convergent and
A2: S2 is convergent;
  consider g1 such that
A3: for r st 0 < r ex m st for n st m <= n holds ||.(S1.n) - g1.|| < r
  by A1,Def9;
  consider g2 such that
A4: for r st 0 < r ex m st for n st m <= n holds ||.(S2.n) - g2.|| < r
  by A2,Def9;
  take g = g1 + g2;
  let r;
  assume
A5: 0 < r;
  then consider m1 such that
A6: for n st m1 <= n holds ||.(S1.n) - g1.|| < r/2 by A3,XREAL_1:217;
  consider m2 such that
A7: for n st m2 <= n holds ||.(S2.n) - g2.|| < r/2 by A4,A5,XREAL_1:217;
  take k = m1 + m2;
  let n such that
A8: k <= n;
  m2 <= k by NAT_1:12;
  then m2 <= n by A8,XXREAL_0:2;
  then
A9: ||.(S2.n) - g2.|| < r/2 by A7;
A10: ||.(S1 + S2).n - g.|| = ||.-(g1+g2) + ((S1.n) + (S2.n)).|| by Def5
    .= ||.((-g1)+(-g2)) + ((S1.n) + (S2.n)).|| by RLVECT_1:45
    .= ||.(S1.n) + ((-g1)+(-g2)) + (S2.n).|| by RLVECT_1:def 6
    .= ||.(S1.n) - g1 + (-g2) + (S2.n).|| by RLVECT_1:def 6
    .= ||.(S1.n) - g1 + ((S2.n) - g2).|| by RLVECT_1:def 6;
A11: ||.(S1.n) - g1 + ((S2.n) - g2).|| <= ||.(S1.n) - g1.|| + ||.(S2.n) - g2
  .|| by Def2;
  m1 <= m1 + m2 by NAT_1:12;
  then m1 <= n by A8,XXREAL_0:2;
  then ||.(S1.n) - g1.|| < r/2 by A6;
  then ||.(S1.n) - g1.|| + ||.(S2.n) - g2.|| < r/2 + r/2 by A9,XREAL_1:10;
  hence thesis by A10,A11,XXREAL_0:2;
end;

theorem Th35:
  S1 is convergent & S2 is convergent implies S1 - S2 is convergent
proof
  assume that
A1: S1 is convergent and
A2: S2 is convergent;
  consider g1 such that
A3: for r st 0 < r ex m st for n st m <= n holds ||.(S1.n) - g1.|| < r
  by A1,Def9;
  consider g2 such that
A4: for r st 0 < r ex m st for n st m <= n holds ||.(S2.n) - g2.|| < r
  by A2,Def9;
  take g = g1 - g2;
  let r;
  assume
A5: 0 < r;
  then consider m1 such that
A6: for n st m1 <= n holds ||.(S1.n) - g1.|| < r/2 by A3,XREAL_1:217;
  consider m2 such that
A7: for n st m2 <= n holds ||.(S2.n) - g2.|| < r/2 by A4,A5,XREAL_1:217;
  take k = m1 + m2;
  let n such that
A8: k <= n;
  m2 <= k by NAT_1:12;
  then m2 <= n by A8,XXREAL_0:2;
  then
A9: ||.(S2.n) - g2.|| < r/2 by A7;
A10: ||.(S1 - S2).n - g.|| = ||.((S1.n) - (S2.n)) - (g1 - g2).|| by Def6
    .= ||.(((S1.n) - (S2.n)) - g1) + g2.|| by RLVECT_1:43
    .= ||. ((S1.n) - (g1 + (S2.n))) + g2.|| by RLVECT_1:41
    .= ||. (((S1.n) - g1 ) - (S2.n)) + g2.|| by RLVECT_1:41
    .= ||. ((S1.n) - g1 ) - ((S2.n) - g2).|| by RLVECT_1:43;
A11: ||.((S1.n) - g1) - ((S2.n) - g2).|| <= ||.(S1.n) - g1.|| + ||.(S2.n) -
  g2.|| by Th7;
  m1 <= m1 + m2 by NAT_1:12;
  then m1 <= n by A8,XXREAL_0:2;
  then ||.(S1.n) - g1.|| < r/2 by A6;
  then ||.(S1.n) - g1.|| + ||.(S2.n) - g2.|| < r/2 + r/2 by A9,XREAL_1:10;
  hence thesis by A10,A11,XXREAL_0:2;
end;

theorem Th36:
  S is convergent implies S - x is convergent
proof
  assume S is convergent;
  then consider g such that
A1: for r st 0 < r ex m st for n st m <= n holds ||.(S.n) - g.|| < r by Def9;
  take h = g - x;
  let r;
  assume 0 < r;
  then consider m1 such that
A2: for n st m1 <= n holds ||.(S.n) - g.|| < r by A1;
  take k = m1;
  let n;
  assume k <= n;
  then
A3: ||.(S.n) - g.|| < r by A2;
  ||.(S.n) - g.|| = ||.((S.n) - 09(RNS)) - g.|| by RLVECT_1:26
    .= ||.((S.n) - (x - x)) - g.|| by RLVECT_1:28
    .= ||.(((S.n) - x) + x) - g.|| by RLVECT_1:43
    .= ||.((S.n) - x) + ((-g) + x).|| by RLVECT_1:def 6
    .= ||.((S.n) - x) - h.|| by RLVECT_1:47;
  hence thesis by A3,Def7;
end;

theorem Th37:
  S is convergent implies a * S is convergent
proof
  assume S is convergent;
  then consider g such that
A1: for r st 0 < r ex m st for n st m <= n holds ||.(S.n) - g.|| < r by Def9;
  take h = a * g;
A2: now
A3: 0/abs(a) =0;
    assume
A4: a <> 0; then
A5: 0 < abs a by COMPLEX1:133;
    let r;
    assume 0 < r;
    then consider m1 such that
A6: for n st m1 <= n holds ||.(S.n) - g.|| < r/abs(a) by A1,A5,A3,XREAL_1:76;
    take k = m1;
    let n;
    assume k <= n; then
A7: ||.(S.n) - g.|| < r/abs(a) by A6;
A8: 0 <> abs(a) by A4,COMPLEX1:133;
A9: abs(a) * (r/abs(a)) = abs(a) * (abs(a)" * r) by XCMPLX_0:def 9
      .= abs(a) *abs(a)" * r
      .= 1 * r by A8,XCMPLX_0:def 7
      .= r;
    ||.(a * (S.n)) - (a * g).|| = ||.a * ((S.n) - g).|| by RLVECT_1:48
      .= abs(a) * ||.(S.n) - g.|| by Def2;
    then ||.(a *(S.n)) - h.|| < r by A5,A7,A9,XREAL_1:70;
    hence ||.(a * S).n - h.|| < r by Def8;
  end;
  now
    assume
A10: a = 0;
    let r;
    assume 0 < r;
    then consider m1 such that
A11: for n st m1 <= n holds ||.(S.n) - g.|| < r by A1;
    take k = m1;
    let n;
    assume k <= n; then
A12: ||.(S.n) - g.|| < r by A11;
    ||.a * (S.n) - a * g.|| = ||.0 * (S.n) - 09(RNS).|| by A10,RLVECT_1:23
      .= ||.09(RNS) - 09(RNS).|| by RLVECT_1:23
      .= ||.09(RNS).|| by RLVECT_1:26
      .= 0;
    then ||.a * (S.n) - h.|| < r by A12;
    hence ||.(a * S).n - h.|| < r by Def8;
  end;
  hence thesis by A2;
end;

canceled;

theorem Th39:
  S is convergent implies ||.S.|| is convergent
proof
  assume S is convergent;
  then consider g such that
A1: for r st 0 < r ex m st for n st m <= n holds ||.(S.n) - g.|| < r by Def9;
  now
    let r be real number;
    assume
A2: 0 < r;
    r is Real by XREAL_0:def 1;
    then consider m1 such that
A3: for n st m1 <= n holds ||.(S.n) - g.|| < r by A1,A2;
    take k = m1;
    now
      let n;
      assume k <= n; then
A4:   ||.(S.n) - g.|| < r by A3;
      abs(||.(S.n).|| - ||.g.||) <= ||.(S.n) - g.|| by Th13;
      then abs(||.(S.n).|| - ||.g.||) < r by A4,XXREAL_0:2;
      hence abs(||.S.||.n - ||.g.||) < r by NORMSP_0:def 4;
    end;
    hence for n st k <= n holds abs(||.S.||.n - ||.g.||) < r;
  end;
  hence thesis by SEQ_2:def 6;
end;

definition
 canceled;
  let RNS, S;
  assume
A1: S is convergent;
  func lim S -> Point of RNS means
  :Def11:
  for r st 0 < r ex m st for n st m <= n holds ||.(S.n) - it.|| < r;
  existence by A1,Def9;
  uniqueness
  proof
    for x, y st ( for r st 0 < r ex m st for n st m <= n holds ||.(S.n) -
x.|| < r ) & ( for r st 0 < r ex m st for n st m <= n holds ||.(S.n) - y.|| < r
    ) holds x = y
    proof
      given x, y such that
A2:   for r st 0 < r ex m st for n st m <= n holds ||.(S.n) - x.|| < r and
A3:   for r st 0 < r ex m st for n st m <= n holds ||.(S.n) - y.|| < r and
A4:   x <> y;
A6:   ||.x - y.|| <> 0 by A4,Th10;
      then
A7:   0/4 < ||.x - y.||/4 by XREAL_1:76;
      then consider m1 such that
A8:   for n st m1 <= n holds ||.(S.n) - x.|| < ||.x - y.||/4 by A2;
      consider m2 such that
A9:   for n st m2 <= n holds ||.(S.n) - y.|| < ||.x - y.||/4 by A3,A7;
A10:  now
        ||.x - y.|| <= ||.x - (S.m1).|| + ||.(S.m1) - y.|| by Th14;
        then
A11:    ||.x - y.|| <= ||.(S.m1) - x.|| + ||.(S.m1) - y.|| by Th11;
        assume m2 <= m1; then
A12:    ||.(S.m1) - y.|| < ||.x - y.||/4 by A9;
        ||.(S.m1) - x.|| < ||.x - y.||/4 by A8;
        then
        ||.(S.m1) - x.|| + ||.(S.m1) - y.|| < ||.x - y.||/4 + ||.x - y.||
        /4 by A12,XREAL_1:10;
        then not ||.x - y.||/2 < ||.x - y.|| by A11,XXREAL_0:2;
        hence contradiction by A6,XREAL_1:218;
      end;
      now
        ||.x - y.|| <= ||.x - (S.m2).|| + ||.(S.m2) - y.|| by Th14;
        then
A13:    ||.x - y.|| <= ||.(S.m2) - x.|| + ||.(S.m2) - y.|| by Th11;
        assume m1 <= m2;
        then
A14:    ||.(S.m2) - x.|| < ||.x - y.||/4 by A8;
        ||.(S.m2) - y.|| < ||.x - y.||/4 by A9;
        then
        ||.(S.m2) - x.|| + ||.(S.m2) - y.|| < ||.x - y.||/4 + ||.x - y.||
        /4 by A14,XREAL_1:10;
        then not ||.x - y.||/2 < ||.x - y.|| by A13,XXREAL_0:2;
        hence contradiction by A6,XREAL_1:218;
      end;
      hence contradiction by A10;
    end;
    hence thesis;
  end;
end;

canceled;

theorem
  S is convergent & lim S = g implies ||.S - g.|| is convergent & lim
  ||.S - g.|| = 0
proof
  assume that
A1: S is convergent and
A2: lim S = g;
A3: now
    let r be real number;
    assume
A4: 0 < r;
    r is Real by XREAL_0:def 1;
    then consider m1 such that
A5: for n st m1 <= n holds ||.(S.n) - g.|| < r by A1,A2,A4,Def11;
    take k = m1;
    let n;
    assume k <= n;
    then ||.(S.n) - g.|| < r by A5;
    then
A6: ||.((S.n) - g ) - 09(RNS).|| < r by RLVECT_1:26;
    abs(||.(S.n) - g.|| - ||.09(RNS).||) <= ||.((S.n) - g ) - 09(RNS).||
    by Th13;
    then abs(||.(S.n) - g.|| - ||.09(RNS).||) < r by A6,XXREAL_0:2;
    then abs((||.(S - g).n.|| - 0)) < r by Def7;
    hence abs((||.S - g.||.n - 0)) < r by NORMSP_0:def 4;
  end;
  ||.S - g.|| is convergent by A1,Th36,Th39;
  hence thesis by A3,SEQ_2:def 7;
end;

theorem
  S1 is convergent & S2 is convergent implies lim (S1 + S2) = (lim S1) +
  (lim S2)
proof
  assume that
A1: S1 is convergent and
A2: S2 is convergent;
  set g2 = lim S2;
  set g1 = lim S1;
  set g = g1 + g2;
A3: now
    let r;
    assume 0 < r; then
A4: 0< r/2 by XREAL_1:217;
    then consider m1 such that
A5: for n st m1 <= n holds ||.(S1.n) - g1.|| < r/2 by A1,Def11;
    consider m2 such that
A6: for n st m2 <= n holds ||.(S2.n) - g2.|| < r/2 by A2,A4,Def11;
    take k = m1 + m2;
    let n such that
A7: k <= n;
    m2 <= k by NAT_1:12;
    then m2 <= n by A7,XXREAL_0:2;
    then
A8: ||.(S2.n) - g2.|| < r/2 by A6;
A9: ||.(S1 + S2).n - g.|| = ||.-(g1+g2) + ((S1.n) + (S2.n)).|| by Def5
      .= ||.((-g1)+(-g2)) + ((S1.n) + (S2.n)).|| by RLVECT_1:45
      .= ||.(S1.n) + ((-g1)+(-g2)) + (S2.n).|| by RLVECT_1:def 6
      .= ||.(S1.n) - g1 + (-g2) + (S2.n).|| by RLVECT_1:def 6
      .= ||.(S1.n) - g1 + ((S2.n) - g2).|| by RLVECT_1:def 6;
A10: ||.(S1.n) - g1 + ((S2.n) - g2).|| <= ||.(S1.n) - g1.|| + ||.(S2.n) -
    g2.|| by Def2;
    m1 <= m1 + m2 by NAT_1:12;
    then m1 <= n by A7,XXREAL_0:2;
    then ||.(S1.n) - g1.|| < r/2 by A5;
    then ||.(S1.n) - g1.|| + ||.(S2.n) - g2.|| < r/2 + r/2 by A8,XREAL_1:10;
    hence ||.(S1 + S2).n - g.|| < r by A9,A10,XXREAL_0:2;
  end;
  S1 + S2 is convergent by A1,A2,Th34;
  hence thesis by A3,Def11;
end;

theorem
  S1 is convergent & S2 is convergent implies lim (S1 - S2) = (lim S1) -
  (lim S2)
proof
  assume that
A1: S1 is convergent and
A2: S2 is convergent;
  set g2 = lim S2;
  set g1 = lim S1;
  set g = g1 - g2;
A3: now
    let r;
    assume 0 < r; then
A4: 0< r/2 by XREAL_1:217;
    then consider m1 such that
A5: for n st m1 <= n holds ||.(S1.n) - g1.|| < r/2 by A1,Def11;
    consider m2 such that
A6: for n st m2 <= n holds ||.(S2.n) - g2.|| < r/2 by A2,A4,Def11;
    take k = m1 + m2;
    let n such that
A7: k <= n;
    m2 <= k by NAT_1:12;
    then m2 <= n by A7,XXREAL_0:2;
    then
A8: ||.(S2.n) - g2.|| < r/2 by A6;
A9: ||.(S1 - S2).n - g.|| = ||.((S1.n) - (S2.n)) - (g1 - g2).|| by Def6
      .= ||.(((S1.n) - (S2.n)) - g1) + g2.|| by RLVECT_1:43
      .= ||. ((S1.n) - (g1 + (S2.n))) + g2.|| by RLVECT_1:41
      .= ||. (((S1.n) - g1 ) - (S2.n)) + g2.|| by RLVECT_1:41
      .= ||. ((S1.n) - g1 ) - ((S2.n) - g2).|| by RLVECT_1:43;
A10: ||.((S1.n) - g1) - ((S2.n) - g2).|| <= ||.(S1.n) - g1.|| + ||.(S2.n)
    - g2.|| by Th7;
    m1 <= m1 + m2 by NAT_1:12;
    then m1 <= n by A7,XXREAL_0:2;
    then ||.(S1.n) - g1.|| < r/2 by A5;
    then ||.(S1.n) - g1.|| + ||.(S2.n) - g2.|| < r/2 + r/2 by A8,XREAL_1:10;
    hence ||.(S1 - S2).n - g.|| < r by A9,A10,XXREAL_0:2;
  end;
  S1 - S2 is convergent by A1,A2,Th35;
  hence thesis by A3,Def11;
end;

theorem
  S is convergent implies lim (S - x) = (lim S) - x
proof
  set g = lim S;
  set h = g - x;
  assume
A1: S is convergent;
A2: now
    let r;
    assume 0 < r;
    then consider m1 such that
A3: for n st m1 <= n holds ||.(S.n) - g.|| < r by A1,Def11;
    take k = m1;
    let n;
    assume k <= n; then
A4: ||.(S.n) - g.|| < r by A3;
    ||.(S.n) - g.|| = ||.((S.n) - 09(RNS)) - g.|| by RLVECT_1:26
      .= ||.((S.n) - (x - x)) - g.|| by RLVECT_1:28
      .= ||.(((S.n) - x) + x) - g.|| by RLVECT_1:43
      .= ||.((S.n) - x) + ((-g) + x).|| by RLVECT_1:def 6
      .= ||.((S.n) - x) - h.|| by RLVECT_1:47;
    hence ||.(S - x).n - h.|| < r by A4,Def7;
  end;
  S - x is convergent by A1,Th36;
  hence thesis by A2,Def11;
end;

theorem
  S is convergent implies lim (a * S) = a * (lim S)
proof
  set g = lim S;
  set h = a * g;
  assume
A1: S is convergent;
A2: now
    assume
A3: a = 0;
    let r;
    assume 0 < r;
    then consider m1 such that
A4: for n st m1 <= n holds ||.(S.n) - g.|| < r by A1,Def11;
    take k = m1;
    let n;
    assume k <= n; then
A5: ||.(S.n) - g.|| < r by A4;
    ||.a * (S.n) - a * g.|| = ||.0 * (S.n) - 09(RNS).|| by A3,RLVECT_1:23
      .= ||.09(RNS) - 09(RNS).|| by RLVECT_1:23
      .= ||.09(RNS).|| by RLVECT_1:26
      .= 0;
    then ||.a * (S.n) - h.|| < r by A5;
    hence ||.(a * S).n - h.|| < r by Def8;
  end;
A6: now
A7: 0/abs(a) = 0;
    assume
A8: a <> 0; then
A9: 0 < abs(a) by COMPLEX1:133;
    let r;
    assume 0 < r;
    then 0 < r/abs(a) by A9,A7,XREAL_1:76;
    then consider m1 such that
A10: for n st m1 <= n holds ||.(S.n) - g.|| < r/abs(a) by A1,Def11;
    take k = m1;
    let n;
    assume k <= n; then
A11: ||.(S.n) - g.|| < r/abs(a) by A10;
A12: 0 <> abs(a) by A8,COMPLEX1:133;
A13: abs(a) * (r/abs(a)) = abs(a) * (abs(a)" * r) by XCMPLX_0:def 9
      .= abs(a) *abs(a)" * r
      .= 1 * r by A12,XCMPLX_0:def 7
      .= r;
    ||.(a * (S.n)) - (a * g).|| = ||.a * ((S.n) - g).|| by RLVECT_1:48
      .= abs(a) * ||.(S.n) - g.|| by Def2;
    then ||.(a *(S.n)) - h.|| < r by A9,A11,A13,XREAL_1:70;
    hence ||.(a * S).n - h.|| < r by Def8;
  end;
  a * S is convergent by A1,Th37;
  hence thesis by A2,A6,Def11;
end;
