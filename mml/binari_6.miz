:: On the Calculus of Binary Arithmetics, Part {II}
::  by Shunichi Kobayashi
::
:: Received November 23, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies MARGREL1, ZF_LANG, BINARITH, BVFUNC_1, BINARI_5;
 notations MARGREL1, BINARITH, BVFUNC_1, BINARI_5;
 constructors BINARITH, BVFUNC_1, BINARI_5;
 registrations MARGREL1, BINARITH, BVFUNC_1, BINARI_5;
 definitions BINARI_5, BVFUNC_1, MARGREL1;
 theorems MARGREL1, BINARITH, BINARI_5;

begin

 reserve x,y,z,w for boolean set;

theorem Th1:
  TRUE 'imp' x = x
proof
  thus TRUE 'imp' x = FALSE 'or' x by MARGREL1:def 16
    .= x by BINARITH:7;
end;

theorem Th2:
  FALSE 'imp' x = TRUE
proof
  thus FALSE 'imp' x = TRUE 'or' x by MARGREL1:def 16
    .= TRUE by BINARITH:19;
end;

theorem Th3:
  x 'imp' x = TRUE &
   'not' (x 'imp' x) = FALSE
proof
  x 'imp' x = TRUE by BINARITH:18;
  hence thesis by MARGREL1:41;
end;

theorem
  'not' (x 'imp' y) = x '&' 'not' y
proof
  thus 'not' (x 'imp' y) = 'not' 'not' x '&' 'not' y by BINARITH:10
    .= x '&' 'not' y;
end;

theorem Th5:
  x 'imp' 'not' x = 'not' x &
    'not' (x 'imp' 'not' x) = x
proof
  x 'imp' 'not' x = 'not' x by BINARITH:21;
  hence thesis;
end;

theorem Th6:
  'not' x 'imp' x = x
proof
  thus 'not' x 'imp' x = x 'or' x
   .= x by BINARITH:21;
end;

theorem
  TRUE 'eqv' x = x
proof
  TRUE 'eqv' x = (TRUE 'imp' x) '&' (x 'imp' TRUE) by BINARI_5:23
   .= x '&' (x 'imp' TRUE) by Th1
   .= x '&' TRUE by BINARITH:19
   .= x by MARGREL1:50;
  hence thesis;
end;

theorem
  FALSE 'eqv' x = 'not' x
proof
  FALSE 'eqv' x = (FALSE 'imp' x) '&' (x 'imp' FALSE) by BINARI_5:23
   .= TRUE '&' (x 'imp' FALSE) by Th2
   .= TRUE '&' 'not' x by BINARITH:7
   .= 'not' x by MARGREL1:50;
  hence thesis;
end;

theorem
  x 'eqv' x = TRUE &
    'not' (x 'eqv' x) = FALSE
proof
  x 'eqv' x = (x 'imp' x) '&' (x 'imp' x) by BINARI_5:23
   .= (x 'imp' x) by BINARITH:16
   .= TRUE by Th3;
  hence thesis by MARGREL1:41;
end;

theorem
  'not' x 'eqv' x = FALSE
proof
  'not' x 'eqv' x
     = ('not' x 'imp' x) '&' (x 'imp' 'not' x) by BINARI_5:23
    .= x '&' (x 'imp' 'not' x) by Th6
    .= x '&' 'not' x by Th5
    .= FALSE by MARGREL1:46;
  hence thesis;
end;

theorem
  x '&' (y 'eqv' z) = x '&' ('not' y 'or' z) '&' ('not' z 'or' y)
proof
  thus x '&' (y 'eqv' z) = x '&' ((y 'imp' z) '&' (z 'imp' y)) by BINARI_5:23
    .= x '&' ('not' y 'or' z) '&' ('not' z 'or' y) by MARGREL1:52;
end;

theorem Th12:
  x '&' (y 'nand' z) = x '&' 'not' y 'or' x '&' 'not' z
proof
  thus x '&' (y 'nand' z) = x '&' ('not' y 'or' 'not' z) by BINARITH:9
    .=x '&' 'not' y 'or' x '&' 'not' z by BINARITH:22;
end;

theorem Th13:
  x '&' (y 'nor' z) = x '&' 'not' y '&' 'not' z
proof
  thus x '&' (y 'nor' z) = x '&' ('not' y '&' 'not' z) by BINARITH:10
    .=x '&' 'not' y '&' 'not' z by MARGREL1:52;
end;

theorem
  x '&' (x '&' y) = x '&' y
proof
  thus x '&' (x '&' y) = x '&' x '&' y by MARGREL1:52
    .= x '&' y by BINARITH:16;
end;

theorem
  x '&' (x 'or' y) = x 'or' (x '&' y)
proof
  thus x '&' (x 'or' y) = x '&' x 'or' x '&' y by BINARITH:22
      .=x 'or' x '&' y by BINARITH:16;
end;

theorem
  x '&' (x 'xor' y) = x '&' 'not' y
proof
 thus x '&' (x 'xor' y)
 =x '&' (('not' x '&' y) 'or' (x '&' 'not' y)) by BINARITH:def 2
.=x '&' ('not' x '&' y) 'or' x '&' (x '&' 'not' y) by BINARITH:22
.=x '&' 'not' x '&' y 'or' x '&' (x '&' 'not' y) by MARGREL1:52
.=x '&' 'not' x '&' y 'or' x '&' x '&' 'not' y by MARGREL1:52
.=FALSE '&' y 'or' x '&' x '&' 'not' y by MARGREL1:46
.=FALSE 'or' x '&' x '&' 'not' y by MARGREL1:49
.=x '&' x '&' 'not' y by BINARITH:7
.=x '&' 'not' y by BINARITH:16;
end;

theorem
  x '&' (x 'imp' y) = x '&' y
proof
  thus x '&' (x 'imp' y) = x '&' 'not' x 'or' x '&' y by BINARITH:22
    .=FALSE 'or' x '&' y by MARGREL1:46
    .=x '&' y by BINARITH:7;
end;

theorem Th18:
  x '&' (x 'eqv' y) = x '&' y
proof
  thus x '&' (x 'eqv' y)
        =x '&' ((x 'imp' y) '&' (y 'imp' x)) by BINARI_5:23
       .=x '&' ('not' x 'or' y) '&' ('not' y 'or' x) by MARGREL1:52
       .=(x '&' 'not' x 'or' x '&' y) '&' ('not' y 'or' x) by BINARITH:22
       .=(FALSE 'or' x '&' y) '&' ('not' y 'or' x) by MARGREL1:46
       .=(x '&' y) '&' ('not' y 'or' x) by BINARITH:7
       .=(x '&' y) '&' 'not' y 'or' (x '&' y) '&' x by BINARITH:22
       .=x '&' (y '&' 'not' y) 'or' (x '&' y) '&' x by MARGREL1:52
       .=x '&' FALSE 'or' (x '&' y) '&' x by MARGREL1:46
       .=FALSE 'or' (x '&' y) '&' x by MARGREL1:49
       .=(x '&' y) '&' x by BINARITH:7
       .=y '&' (x '&' x) by MARGREL1:52
       .=x '&' y by BINARITH:16;
end;

theorem
  x '&' (x 'nand' y) = x '&' 'not' y
proof
  thus x '&' (x 'nand' y) = x '&' 'not' x 'or' x '&' 'not' y by Th12
   .=FALSE 'or' x '&' 'not' y by MARGREL1:46
   .=x '&' 'not' y by BINARITH:7;
end;

theorem
  x '&' (x 'nor' y) = FALSE
proof
  thus x '&' (x 'nor' y) = x '&' 'not' x '&' 'not' y by Th13
   .=FALSE '&' 'not' y by MARGREL1:46
   .=FALSE by MARGREL1:49;
end;

theorem
  x 'or' (y 'xor' z) = x 'or' ('not' y '&' z) 'or' (y '&' 'not' z)
proof
  thus x 'or' (y 'xor' z)
     =x 'or' (('not' y '&' z) 'or' (y '&' 'not' z)) by BINARITH:def 2
    .=x 'or' ('not' y '&' z) 'or' (y '&' 'not' z) by BINARITH:20;
end;

theorem Th22:
  x 'or' (y 'eqv' z) = (x 'or' 'not' y 'or' z) '&' (x 'or' 'not' z 'or' y)
proof
  thus x 'or' (y 'eqv' z)
     =x 'or' ((y 'imp' z) '&' (z 'imp' y)) by BINARI_5:23
    .=(x 'or' (y 'imp' z)) '&' (x 'or' (z 'imp' y)) by BINARITH:23
    .=(x 'or' 'not' y 'or' z) '&' (x 'or' (z 'imp' y)) by BINARITH:20
    .=(x 'or' 'not' y 'or' z) '&' (x 'or' 'not' z 'or' y) by BINARITH:20;
end;

theorem Th23:
  x 'or' (y 'nand' z) = x 'or' 'not' y 'or' 'not' z
proof
  thus x 'or' (y 'nand' z) = x 'or' ('not' y 'or' 'not' z) by BINARITH:9
    .= x 'or' 'not' y 'or' 'not' z by BINARITH:20;
end;

theorem Th24:
  x 'or' (y 'nor' z) = (x 'or' 'not' y) '&' (x 'or' 'not' z) &
    x 'or' (y 'nor' z) = (y 'imp' x) '&' (z 'imp' x)
proof
  x 'or' (y 'nor' z) = x 'or' ('not' y '&' 'not' z) by BINARITH:10
    .= (x 'or' 'not' y) '&' (x 'or' 'not' z) by BINARITH:23;
  hence thesis;
end;

theorem
  x 'or' (x 'or' y) = x 'or' y
proof
  thus x 'or' (x 'or' y) = (x 'or' x) 'or' y by BINARITH:20
    .= x 'or' y by BINARITH:21;
end;

theorem
  x 'or' (x 'imp' y) = TRUE
proof
  thus x 'or' (x 'imp' y) = x 'or' 'not' x 'or' y by BINARITH:20
    .= TRUE 'or' y by BINARITH:18
    .= TRUE by BINARITH:19;
end;

theorem
  x 'or' (x 'eqv' y) = y 'imp' x
proof
  x 'or' (x 'eqv' y)
     =(x 'or' 'not' x 'or' y) '&' (x 'or' 'not' y 'or' x) by Th22
    .=(TRUE 'or' y) '&' (x 'or' 'not' y 'or' x) by BINARITH:18
    .=TRUE '&' (x 'or' 'not' y 'or' x) by BINARITH:19
    .=x 'or' 'not' y 'or' x by MARGREL1:50
    .='not' y 'or' (x 'or' x) by BINARITH:20
    .='not' y 'or' x by BINARITH:21;
  hence thesis;
end;

theorem
  x 'or' (x 'nand' y) = TRUE
proof
  thus x 'or' (x 'nand' y) = x 'or' 'not' x 'or' 'not' y by Th23
    .= TRUE 'or' 'not' y by BINARITH:18
    .= TRUE by BINARITH:19;
end;

theorem
  x 'or' (x 'nor' y) = y 'imp' x
proof
  x 'or' (x 'nor' y) = (x 'or' 'not' x) '&' (x 'or' 'not' y) by Th24
   .= TRUE '&' (x 'or' 'not' y) by BINARITH:18
   .= x 'or' 'not' y by MARGREL1:50;
  hence thesis;
end;

theorem Th30:
  x 'imp' (y 'xor' z) = 'not' x 'or' ('not' y '&' z) 'or' (y '&' 'not' z)
proof
  thus x 'imp' (y 'xor' z)
      = 'not' x 'or' (('not' y '&' z) 'or' (y '&' 'not' z)) by BINARITH:def 2
     .= 'not' x 'or' ('not' y '&' z) 'or' (y '&' 'not' z) by BINARITH:20;
end;

theorem Th31:
  x 'imp' (y 'eqv' z) =
    ('not' x 'or' 'not' y 'or' z) '&' ('not' x 'or' y 'or' 'not' z)
proof
 thus x 'imp' (y 'eqv' z)
 = 'not' x 'or' ((y 'imp' z) '&' (z 'imp' y)) by BINARI_5:23
.= ('not' x 'or' ('not' y 'or' z)) '&' ('not' x 'or' ('not' z 'or' y))
 by BINARITH:23
.= ('not' x 'or' 'not' y 'or' z) '&' ('not' x 'or' ('not' z 'or' y))
 by BINARITH:20
.= ('not' x 'or' 'not' y 'or' z) '&' ('not' x 'or' y 'or' 'not' z)
 by BINARITH:20;
end;

theorem Th32:
  x 'imp' (y 'nand' z) = 'not' x 'or' 'not' y 'or' 'not' z
proof
  thus x 'imp' (y 'nand' z) = 'not' x 'or' ('not' y 'or' 'not' z)
       by BINARITH:9
    .= 'not' x 'or' 'not' y 'or' 'not' z by BINARITH:20;
end;

theorem Th33:
  x 'imp' (y 'nor' z) = ('not' x 'or' 'not' y) '&' ('not' x 'or' 'not' z) &
    x 'imp' (y 'nor' z) = (x 'imp' 'not' y) '&' (x 'imp' 'not' z)
proof
  x 'imp' (y 'nor' z) = 'not' x 'or' ('not' y '&' 'not' z) by BINARITH:10
     .= ('not' x 'or' 'not' y) '&' ('not' x 'or' 'not' z) by BINARITH:23;
  hence thesis;
end;

theorem Th34:
  x 'imp' (x '&' y) = x 'imp' y
proof
  x 'imp' (x '&' y) = ('not' x 'or' x) '&' ('not' x 'or' y) by BINARITH:23
    .= TRUE '&' ('not' x 'or' y) by BINARITH:18
    .= 'not' x 'or' y by MARGREL1:50;
  hence thesis;
end;

theorem
  x 'imp' (x 'or' y) = TRUE
proof
  thus x 'imp' (x 'or' y) = 'not' x 'or' x 'or' y by BINARITH:20
    .= TRUE 'or' y by BINARITH:18
    .= TRUE by BINARITH:19;
end;

theorem
  x 'imp' (x 'xor' y) = 'not' x 'or' 'not' y
proof
 thus x 'imp' (x 'xor' y)
 = 'not' x 'or' (x '&' 'not' y) 'or' ('not' x '&' y) by Th30
.= ('not' x 'or' x) '&' ('not' x 'or' 'not' y) 'or' ('not' x '&' y)
 by BINARITH:23
.= TRUE '&' ('not' x 'or' 'not' y) 'or' ('not' x '&' y)
 by BINARITH:18
.= ('not' x 'or' 'not' y) 'or' ('not' x '&' y) by MARGREL1:50
.= 'not' x 'or' ('not' y 'or' ('not' x '&' y)) by BINARITH:20
.= 'not' x 'or' (('not' y 'or' 'not' x) '&' ('not' y 'or' y)) by BINARITH:23
.= 'not' x 'or' (('not' y 'or' 'not' x) '&' TRUE) by BINARITH:18
.= 'not' x 'or' ('not' x 'or' 'not' y) by MARGREL1:50
.= 'not' x 'or' 'not' x 'or' 'not' y by BINARITH:20
.= 'not' x 'or' 'not' y by BINARITH:21;
end;

theorem Th37:
  x 'imp' (x 'imp' y) = x 'imp' y
proof
  x 'imp' (x 'imp' y) = 'not' x 'or' 'not' x 'or' y by BINARITH:20
    .= 'not' x 'or' y by BINARITH:21;
  hence thesis;
end;

theorem
  x 'imp' (x 'eqv' y) = x 'imp' y &
    x 'imp' (x 'eqv' y) = x 'imp' (x 'imp' y)
proof
  x 'imp' (x 'eqv' y)
 = ('not' x 'or' 'not' x 'or' y) '&' ('not' x 'or' x 'or' 'not' y) by Th31
.= ('not' x 'or' y) '&' ('not' x 'or' x 'or' 'not' y) by BINARITH:21
.= ('not' x 'or' y) '&' (TRUE 'or' 'not' y) by BINARITH:18
.= ('not' x 'or' y) '&' TRUE by BINARITH:19
.= 'not' x 'or' y by MARGREL1:50;
 hence thesis by Th37;
end;

theorem
  x 'imp' (x 'nand' y) = 'not' (x '&' y)
proof
  x 'imp' (x 'nand' y) = 'not' x 'or' 'not' x 'or' 'not' y by Th32
    .= 'not' x 'or' 'not' y by BINARITH:21;
  hence thesis by BINARITH:9;
end;

theorem
  x 'imp' (x 'nor' y) = 'not' x
proof
 thus x 'imp' (x 'nor' y)
 = ('not' x 'or' 'not' x) '&' ('not' x 'or' 'not' y) by Th33
.= 'not' x '&' ('not' x 'or' 'not' y) by BINARITH:21
.= 'not' x by BINARITH:25;
end;

theorem Th41:
  x 'nand' (y 'imp' z) = ('not' x 'or' y) '&' ('not' x 'or' 'not' z) &
    x 'nand' (y 'imp' z) = (x 'imp' y) '&' (x 'imp' 'not' z)
proof
  x 'nand' (y 'imp' z)
      = 'not' x 'or' 'not' ('not' y 'or' z) by BINARITH:9
     .= 'not' x 'or' ('not' 'not' y '&' 'not' z) by BINARITH:10
     .= 'not' x 'or' (y '&' 'not' z)
     .= ('not' x 'or' y) '&' ('not' x 'or' 'not' z) by BINARITH:23;
  hence thesis;
end;

theorem Th42:
  x 'nand' (y 'eqv' z) =
    'not' ((x '&' ('not' y 'or' z)) '&' ('not' z 'or' y))
proof
  thus x 'nand' (y 'eqv' z)
     = 'not' (x '&' ((y 'imp' z) '&' (z 'imp' y))) by BINARI_5:23
    .= 'not' ((x '&' ('not' y 'or' z)) '&' ('not' z 'or' y)) by MARGREL1:52;
end;

theorem Th43:
  x 'nand' (y 'nand' z) = ('not' x 'or' y) '&' ('not' x 'or' z) &
    x 'nand' (y 'nand' z) = (x 'imp' y) '&' (x 'imp' z)
proof
  x 'nand' (y 'nand' z)
     = 'not' x 'or' 'not'('not'(y '&' z)) by BINARITH:9
    .= 'not' x 'or' (y '&' z)
    .= ('not' x 'or' y) '&' ('not' x 'or' z) by BINARITH:23;
 hence thesis;
end;

theorem Th44:
  x 'nand' (y 'nor' z) = 'not' x 'or' y 'or' z
proof
  thus x 'nand' (y 'nor' z)
     = 'not' x 'or' 'not'('not'(y 'or' z)) by BINARITH:9
    .= 'not' x 'or' (y 'or' z)
    .= 'not' x 'or' y 'or' z by BINARITH:20;
end;

theorem
  x 'nand' (x '&' y) = 'not' (x '&' y)
proof
  thus x 'nand' (x '&' y) = 'not' (x '&' x '&' y) by MARGREL1:52
    .= 'not' (x '&' y) by BINARITH:16;
end;

theorem
  x 'nand' (x 'xor' y) = x 'imp' y
proof
  x 'nand' (x 'xor' y)
        = (x '&' x) 'eqv' (x '&' y) by BINARI_5:9
       .= x 'eqv' (x '&' y) by BINARITH:16
       .= (x 'imp' (x '&' y)) '&' ((x '&' y) 'imp' x) by BINARI_5:23
       .= (x 'imp' y) '&' ((x '&' y) 'imp' x) by Th34
       .= ('not' x 'or' y) '&' (('not' x 'or' 'not' y) 'or' x) by BINARITH:9
       .= ('not' x 'or' y) '&' ('not' y 'or' ('not' x 'or' x)) by BINARITH:20
       .= ('not' x 'or' y) '&' ('not' y 'or' TRUE) by BINARITH:18
       .= ('not' x 'or' y) '&' TRUE by BINARITH:19
       .= 'not' x 'or' y by MARGREL1:50;
  hence thesis;
end;

theorem
  x 'nand' (x 'imp' y) = 'not' (x '&' y)
proof
  thus x 'nand' (x 'imp' y)
      = ('not' x 'or' x) '&' ('not' x 'or' 'not' y) by Th41
     .= TRUE '&' ('not' x 'or' 'not' y) by BINARITH:18
     .= 'not' x 'or' 'not' y by MARGREL1:50
     .= 'not' (x '&' y) by BINARITH:9;
end;

theorem
  x 'nand' (x 'eqv' y) = 'not' (x '&' y)
proof
 thus x 'nand' (x 'eqv' y)
 = 'not' ((x '&' ('not' x 'or' y)) '&' ('not' y 'or' x)) by Th42
.= 'not' ((x '&' 'not' x 'or' x '&' y) '&' ('not' y 'or' x)) by BINARITH:22
.= 'not' ((FALSE 'or' x '&' y) '&' ('not' y 'or' x)) by MARGREL1:46
.= 'not' ((x '&' y) '&' (x 'or' 'not' y)) by BINARITH:7
.= 'not' ((x '&' y) '&' x 'or' (x '&' y) '&' 'not' y) by BINARITH:22
.= 'not' ((x '&' y) '&' x 'or' (x '&' (y '&' 'not' y))) by MARGREL1:52
.= 'not' ((x '&' y) '&' x 'or' (x '&' FALSE)) by MARGREL1:46
.= 'not' ((x '&' y) '&' x 'or' FALSE) by MARGREL1:49
.= 'not' ((x '&' y) '&' x) by BINARITH:7
.= 'not' (y '&' (x '&' x)) by MARGREL1:52
.= 'not' (x '&' y) by BINARITH:16;
end;

theorem
  x 'nand' (x 'nand' y) = x 'imp' y
proof
  x 'nand' (x 'nand' y) = ('not' x 'or' x) '&' ('not' x 'or' y) by Th43
    .= TRUE '&' ('not' x 'or' y) by BINARITH:18
    .= 'not' x 'or' y by MARGREL1:50;
  hence thesis;
end;

theorem
  x 'nand' (x 'nor' y) = TRUE
proof
  thus x 'nand' (x 'nor' y) = 'not' x 'or' x 'or' y by Th44
    .= TRUE 'or' y by BINARITH:18
    .= TRUE by BINARITH:19;
end;

theorem Th51:
  x 'nor' (y 'xor' z) = 'not' (x 'or' ('not' y '&' z) 'or' (y '&' 'not' z))
proof
  thus x 'nor' (y 'xor' z)
     = 'not' (x 'or' (('not' y '&' z) 'or' (y '&' 'not' z))) by BINARITH:def 2
    .= 'not' (x 'or' ('not' y '&' z) 'or' (y '&' 'not' z)) by BINARITH:20;
end;

theorem Th52:
  x 'nor' (y 'eqv' z) =
    'not' ((x 'or' 'not' y 'or' z) '&' (x 'or' 'not' z 'or' y))
proof
  thus x 'nor' (y 'eqv' z)
 = 'not' (x 'or' ((y 'imp' z) '&' (z 'imp' y))) by BINARI_5:23
.= 'not' ((x 'or' (y 'imp' z)) '&' (x 'or' (z 'imp' y))) by BINARITH:23
.= 'not' ((x 'or' 'not' y 'or' z) '&' (x 'or' (z 'imp' y))) by BINARITH:20
.= 'not' ((x 'or' 'not' y 'or' z) '&' (x 'or' 'not' z 'or' y)) by BINARITH:20;
end;

theorem Th53:
  x 'nor' (y 'nand' z) = 'not' x '&' y '&' z
proof
  thus x 'nor' (y 'nand' z)
      = 'not' x '&' 'not' 'not' (y '&' z) by BINARITH:10
     .= 'not' x '&' (y '&' z)
     .= 'not' x '&' y '&' z by MARGREL1:52;
end;

theorem Th54:
  x 'nor' (y 'nor' z) = 'not' x '&' y 'or' 'not' x '&' z
proof
  thus x 'nor' (y 'nor' z)
     = 'not' x '&' 'not' 'not' (y 'or' z) by BINARITH:10
    .= 'not' x '&' (y 'or' z)
    .= 'not' x '&' y 'or' 'not' x '&' z by BINARITH:22;
end;

theorem
  x 'nor' (x '&' y) = 'not' x
proof
  thus x 'nor' (x '&' y)
      = 'not' (x 'or' x) 'or' 'not' (x 'or' y) by BINARI_5:15
     .= 'not' x 'or' 'not' (x 'or' y) by BINARITH:21
     .= 'not' x 'or' ('not' x '&' 'not' y) by BINARITH:10
     .= ('not' x 'or' 'not' x) '&' ('not' x 'or' 'not' y) by BINARITH:23
     .= 'not' x '&' ('not' x 'or' 'not' y) by BINARITH:21
     .= 'not' x by BINARITH:25;
end;

theorem
  x 'nor' (x 'or' y) = 'not' x '&' 'not' y
proof
  thus x 'nor' (x 'or' y) = 'not' (x 'or' x 'or' y) by BINARITH:20
    .= 'not' (x 'or' y) by BINARITH:21
    .= 'not' x '&' 'not' y by BINARITH:10;
end;

theorem
  x 'nor' (x 'xor' y) = 'not' x '&' 'not' y
proof
 thus x 'nor' (x 'xor' y)
 = 'not' (x 'or' ('not' x '&' y) 'or' (x '&' 'not' y)) by Th51
.= 'not' (((x 'or' 'not' x) '&' (x 'or' y)) 'or' (x '&' 'not' y))
 by BINARITH:23
.= 'not' ((TRUE '&' (x 'or' y)) 'or' (x '&' 'not' y)) by BINARITH:18
.= 'not' ((x 'or' y) 'or' (x '&' 'not' y)) by MARGREL1:50
.= 'not' (((x 'or' y) 'or' x) '&' ((x 'or' y) 'or' 'not' y)) by BINARITH:23
.= 'not' (((x 'or' y) 'or' x) '&' (x 'or' (y 'or' 'not' y))) by BINARITH:20
.= 'not' (((x 'or' y) 'or' x) '&' (x 'or' TRUE)) by BINARITH:18
.= 'not' (((x 'or' y) 'or' x) '&' TRUE) by BINARITH:19
.= 'not' ((x 'or' y) 'or' x) by MARGREL1:50
.= 'not' (y 'or' (x 'or' x)) by BINARITH:20
.= 'not' (x 'or' y) by BINARITH:21
.= 'not' x '&' 'not' y by BINARITH:10;
end;

theorem
  x 'nor' (x 'imp' y) = FALSE
proof
  thus x 'nor' (x 'imp' y)
      = 'not' (x 'or' 'not' x 'or' y) by BINARITH:20
     .= 'not' (TRUE 'or' y) by BINARITH:18
     .= 'not' TRUE by BINARITH:19
     .= FALSE by MARGREL1:41;
end;

theorem
  x 'nor' (x 'eqv' y) = 'not' x '&' y
proof
  thus x 'nor' (x 'eqv' y)
     = 'not' ((x 'or' 'not' x 'or' y) '&' (x 'or' 'not' y 'or' x)) by Th52
    .= 'not' ((TRUE 'or' y) '&' (x 'or' 'not' y 'or' x)) by BINARITH:18
    .= 'not' (TRUE '&' (x 'or' 'not' y 'or' x)) by BINARITH:19
    .= 'not' (x 'or' 'not' y 'or' x) by MARGREL1:50
    .= 'not' ('not' y 'or' (x 'or' x)) by BINARITH:20
    .= 'not' ('not' y 'or' x) by BINARITH:21
    .= 'not' x '&' 'not' 'not' y by BINARITH:10
    .= 'not' x '&' y;
end;

theorem
  x 'nor' (x 'nand' y) = FALSE
proof
  thus x 'nor' (x 'nand' y) = 'not' x '&' x '&' y by Th53
   .= FALSE '&' y by MARGREL1:46
   .= FALSE by MARGREL1:49;
end;

theorem
  x 'nor' (x 'nor' y) = 'not' x '&' y
proof
  thus x 'nor' (x 'nor' y) = 'not' x '&' x 'or' 'not' x '&' y by Th54
    .= FALSE 'or' 'not' x '&' y by MARGREL1:46
    .= 'not' x '&' y by BINARITH:7;
end;

theorem Th62:
  x 'xor' (y '&' z) = (x 'or' (y '&' z)) '&' ('not' x 'or' 'not' (y '&' z))
proof
 thus x 'xor' (y '&' z)
 = ('not' x '&' (y '&' z)) 'or' (x '&' 'not' (y '&' z)) by BINARITH:def 2
.= (('not' x '&' (y '&' z)) 'or' x) '&'
   (('not' x '&' (y '&' z)) 'or' 'not' (y '&' z)) by BINARITH:23
.= (('not' x 'or' x) '&' ((y '&' z) 'or' x)) '&'
   (('not' x '&' (y '&' z)) 'or' 'not' (y '&' z)) by BINARITH:23
.= (TRUE '&' ((y '&' z) 'or' x)) '&'
   (('not' x '&' (y '&' z)) 'or' 'not' (y '&' z)) by BINARITH:18
.= ((y '&' z) 'or' x) '&'
   (('not' x '&' (y '&' z)) 'or' 'not' (y '&' z)) by MARGREL1:50
.= ((y '&' z) 'or' x) '&' (('not' x 'or' 'not' (y '&' z)) '&'
   ((y '&' z) 'or' 'not' (y '&' z))) by BINARITH:23
.= ((y '&' z) 'or' x) '&' (('not' x 'or' 'not' (y '&' z)) '&' TRUE)
 by BINARITH:18
.= (x 'or' (y '&' z)) '&' ('not' x 'or' 'not' (y '&' z)) by MARGREL1:50;
end;

theorem
  x 'xor' (x '&' y) = x '&' 'not' y
proof
 thus x 'xor' (x '&' y)
 = (x 'or' (x '&' y)) '&' ('not' x 'or' 'not' (x '&' y)) by Th62
.= ((x 'or' x) '&' (x 'or' y)) '&' ('not' x 'or' 'not' (x '&' y))
 by BINARITH:23
.= (x '&' (x 'or' y)) '&' ('not' x 'or' 'not' (x '&' y)) by BINARITH:21
.= x '&' ('not' x 'or' 'not' (x '&' y)) by BINARITH:25
.= (x '&' 'not' x) 'or' (x '&' 'not' (x '&' y)) by BINARITH:22
.= FALSE 'or' (x '&' 'not' (x '&' y)) by MARGREL1:46
.= x '&' 'not' (x '&' y) by BINARITH:7
.= x '&' ('not' x 'or' 'not' y) by BINARITH:9
.= x '&' 'not' x 'or' x '&' 'not' y by BINARITH:22
.= FALSE 'or' x '&' 'not' y by MARGREL1:46
.= x '&' 'not' y by BINARITH:7;
end;

theorem
  x 'xor' (x 'or' y) = 'not' x '&' y
proof
 thus x 'xor' (x 'or' y)
 = ('not' x '&' (x 'or' y)) 'or' (x '&' 'not' (x 'or' y)) by BINARITH:def 2
.= (('not' x '&' x) 'or' ('not' x '&' y)) 'or' (x '&' 'not' (x 'or' y))
 by BINARITH:22
.= (FALSE 'or' ('not' x '&' y)) 'or' (x '&' 'not' (x 'or' y))
 by MARGREL1:46
.= ('not' x '&' y) 'or' (x '&' 'not' (x 'or' y)) by BINARITH:7
.= ('not' x '&' y) 'or' (x '&' ('not' x '&' 'not' y)) by BINARITH:10
.= ('not' x '&' y) 'or' (x '&' 'not' x '&' 'not' y) by MARGREL1:52
.= ('not' x '&' y) 'or' (FALSE '&' 'not' y) by MARGREL1:46
.= ('not' x '&' y) 'or' FALSE by MARGREL1:49
.= 'not' x '&' y by BINARITH:7;
end;

theorem Th65:
  'not' x '&' (x 'xor' y) = 'not' x '&' y
proof
 thus 'not' x '&' (x 'xor' y)
 = 'not' x '&' (('not' x '&' y) 'or' (x '&' 'not' y)) by BINARITH:def 2
.= ('not' x '&' ('not' x '&' y)) 'or' ('not' x '&' (x '&' 'not' y))
 by BINARITH:22
.= ('not' x '&' 'not' x '&' y) 'or' ('not' x '&' (x '&' 'not' y))
 by MARGREL1:52
.= ('not' x '&' y) 'or' ('not' x '&' (x '&' 'not' y)) by BINARITH:16
.= ('not' x '&' y) 'or' ('not' x '&' x '&' 'not' y) by MARGREL1:52
.= ('not' x '&' y) 'or' (FALSE '&' 'not' y) by MARGREL1:46
.= ('not' x '&' y) 'or' FALSE by MARGREL1:49
.= 'not' x '&' y by BINARITH:7;
end;

theorem Th66:
  x '&' 'not' (x 'xor' y) = x '&' y
proof
 thus x '&' 'not' (x 'xor' y)
 = x '&' 'not' (('not' x '&' y) 'or' (x '&' 'not' y)) by BINARITH:def 2
.= x '&' ('not' ('not' x '&' y) '&' 'not' (x '&' 'not' y)) by BINARITH:10
.= x '&' (('not' 'not' x 'or' 'not' y) '&' 'not' (x '&' 'not' y)) by BINARITH:9
.= x '&' ((x 'or' 'not' y) '&' 'not' (x '&' 'not' y))
.= x '&' ((x 'or' 'not' y) '&' ('not' x 'or' 'not' 'not' y)) by BINARITH:9
.= x '&' ((x 'or' 'not' y) '&' ('not' x 'or' y))
.= x '&' ('not' x 'or' y) '&' (x 'or' 'not' y) by MARGREL1:52
.= (x '&' 'not' x 'or' x '&' y) '&' (x 'or' 'not' y) by BINARITH:22
.= (FALSE 'or' x '&' y) '&' (x 'or' 'not' y) by MARGREL1:46
.= (x '&' y) '&' (x 'or' 'not' y) by BINARITH:7
.= ((x '&' y) '&' x 'or' (x '&' y) '&' 'not' y) by BINARITH:22
.= ((x '&' y) '&' x 'or' x '&' (y '&' 'not' y)) by MARGREL1:52
.= ((x '&' y) '&' x 'or' x '&' FALSE) by MARGREL1:46
.= ((x '&' y) '&' x 'or' FALSE) by MARGREL1:49
.= (x '&' y) '&' x by BINARITH:7
.= y '&' (x '&' x) by MARGREL1:52
.= x '&' y by BINARITH:16;
end;

theorem
  x 'xor' (x 'xor' y) = y
proof
 thus x 'xor' (x 'xor' y)
 = ('not' x '&' (x 'xor' y)) 'or' (x '&' 'not' (x 'xor' y)) by BINARITH:def 2
.= ('not' x '&' y) 'or' (x '&' 'not' (x 'xor' y)) by Th65
.= ('not' x '&' y) 'or' (x '&' y) by Th66
.= (('not' x '&' y) 'or' x) '&' (('not' x '&' y) 'or' y) by BINARITH:23
.= (('not' x 'or' x) '&' (y 'or' x)) '&' (('not' x '&' y) 'or' y)
 by BINARITH:23
.= (TRUE '&' (y 'or' x)) '&' (('not' x '&' y) 'or' y) by BINARITH:18
.= (y 'or' x) '&' (('not' x '&' y) 'or' y) by MARGREL1:50
.= (y 'or' x) '&' (('not' x 'or' y) '&' (y 'or' y)) by BINARITH:23
.= (y 'or' x) '&' (('not' x 'or' y) '&' y) by BINARITH:21
.= (y 'or' x) '&' y by BINARITH:25
.= y by BINARITH:25;
end;

theorem Th68:
  x '&' 'not' (x 'imp' y) = x '&' 'not' y
proof
  thus x '&' 'not' (x 'imp' y)
       = x '&' ('not' 'not' x '&' 'not' y) by BINARITH:10
      .= x '&' (x '&' 'not' y)
      .= x '&' x '&' 'not' y by MARGREL1:52
      .= x '&' 'not' y by BINARITH:16;
end;

theorem
  x 'xor' (x 'imp' y) = 'not' x 'or' 'not' y
proof
 thus x 'xor' (x 'imp' y)
 = ('not' x '&' (x 'imp' y)) 'or' (x '&' 'not' (x 'imp' y)) by BINARITH:def 2
.= 'not' x 'or' (x '&' 'not' (x 'imp' y)) by BINARITH:25
.= 'not' x 'or' (x '&' 'not' y) by Th68
.= ('not' x 'or' x) '&' ('not' x 'or' 'not' y) by BINARITH:23
.= TRUE '&' ('not' x 'or' 'not' y) by BINARITH:18
.= 'not' x 'or' 'not' y by MARGREL1:50;
end;

theorem Th70:
  'not' x '&' (x 'eqv' y) = 'not' x '&' 'not' y
proof
  thus 'not' x '&' (x 'eqv' y)
      = 'not' x '&' ((x 'imp' y) '&' (y 'imp' x)) by BINARI_5:23
     .= 'not' x '&' (x 'imp' y) '&' (y 'imp' x) by MARGREL1:52
     .= 'not' x '&' (y 'imp' x) by BINARITH:25
     .= ('not' x '&' 'not' y) 'or' ('not' x '&' x) by BINARITH:22
     .= ('not' x '&' 'not' y) 'or' FALSE by MARGREL1:46
     .= 'not' x '&' 'not' y by BINARITH:7;
end;

theorem Th71:
  x '&' 'not' (x 'eqv' y) = x '&' 'not' y
proof
 thus x '&' 'not' (x 'eqv' y)
 = x '&' 'not' ((x 'imp' y) '&' (y 'imp' x)) by BINARI_5:23
.= x '&' ('not' (x 'imp' y) 'or' 'not' (y 'imp' x)) by BINARITH:9
.= x '&' ('not' (x 'imp' y) 'or' ('not' 'not' y '&' 'not' x)) by BINARITH:10
.= x '&' ('not' (x 'imp' y) 'or' (y '&' 'not' x))
.= x '&' (('not' 'not' x '&' 'not' y) 'or' (y '&' 'not' x)) by BINARITH:10
.= x '&' ((x '&' 'not' y) 'or' (y '&' 'not' x))
.= (x '&' (x '&' 'not' y)) 'or' (x '&' (y '&' 'not' x)) by BINARITH:22
.= (x '&' x '&' 'not' y) 'or' (x '&' (y '&' 'not' x)) by MARGREL1:52
.= (x '&' 'not' y) 'or' (x '&' (y '&' 'not' x)) by BINARITH:16
.= (x '&' 'not' y) 'or' (x '&' 'not' x '&' y) by MARGREL1:52
.= (x '&' 'not' y) 'or' (FALSE '&' y) by MARGREL1:46
.= (x '&' 'not' y) 'or' FALSE by MARGREL1:49
.= x '&' 'not' y by BINARITH:7;
end;

theorem
  x 'xor' (x 'eqv' y) = 'not' y
proof
 thus x 'xor' (x 'eqv' y)
 = ('not' x '&' (x 'eqv' y)) 'or' (x '&' 'not' (x 'eqv' y)) by BINARITH:def 2
.= ('not' x '&' 'not' y) 'or' (x '&' 'not' (x 'eqv' y)) by Th70
.= ('not' x '&' 'not' y) 'or' (x '&' 'not' y) by Th71
.= ('not' x 'or' x) '&' 'not' y by BINARITH:22
.= TRUE '&' 'not' y by BINARITH:18
.= 'not' y by MARGREL1:50;
end;

theorem
  x 'xor' (x 'nand' y) = x 'imp' y
proof
 x 'xor' (x 'nand' y)
 = ('not' x '&' (x 'nand' y)) 'or' (x '&' 'not' (x 'nand' y)) by BINARITH:def 2
.= ('not' x '&' ('not' x 'or' 'not' y)) 'or' (x '&' 'not' (x 'nand' y))
 by BINARITH:9
.= 'not' x 'or' (x '&' 'not' (x 'nand' y)) by BINARITH:25
.= 'not' x 'or' (x '&' (x '&' y))
.= 'not' x 'or' (x '&' x '&' y) by MARGREL1:52
.= 'not' x 'or' (x '&' y) by BINARITH:16
.= ('not' x 'or' x) '&' ('not' x 'or' y) by BINARITH:23
.= TRUE '&' ('not' x 'or' y) by BINARITH:18
.= 'not' x 'or' y by MARGREL1:50;
  hence thesis;
end;

theorem
  x 'xor' (x 'nor' y) = y 'imp' x
proof
 x 'xor' (x 'nor' y)
 = ('not' x '&' (x 'nor' y)) 'or' (x '&' 'not' (x 'nor' y)) by BINARITH:def 2
.= ('not' x '&' 'not' (x 'or' y)) 'or' (x '&' (x 'or' y))
.= ('not' x '&' ('not' x '&' 'not' y)) 'or' (x '&' (x 'or' y)) by BINARITH:10
.= ('not' x '&' 'not' x '&' 'not' y) 'or' (x '&' (x 'or' y)) by MARGREL1:52
.= ('not' x '&' 'not' y) 'or' (x '&' (x 'or' y)) by BINARITH:16
.= ('not' x '&' 'not' y) 'or' x by BINARITH:25
.= ('not' x 'or' x) '&' ('not' y 'or' x) by BINARITH:23
.= TRUE '&' ('not' y 'or' x) by BINARITH:18
.= 'not' y 'or' x by MARGREL1:50;
  hence thesis;
end;

theorem
  'not' x '&' (x 'imp' y) = 'not' x 'or' 'not' x '&' y
proof
  thus 'not' x '&' (x 'imp' y)
      = 'not' x '&' 'not' x 'or' 'not' x '&' y by BINARITH:22
     .= 'not' x 'or' 'not' x '&' y by BINARITH:16;
end;

theorem Th76:
  'not' x '&' (y 'eqv' z) = 'not' x '&' ('not' y 'or' z) '&' ('not' z 'or' y)
proof
  thus 'not' x '&' (y 'eqv' z)
      = 'not' x '&' ((y 'imp' z) '&' (z 'imp' y)) by BINARI_5:23
     .= 'not' x '&' ('not' y 'or' z) '&' ('not' z 'or' y) by MARGREL1:52;
end;

theorem
  'not' x '&' (x 'eqv' y) = ('not' x '&' 'not' y) '&' ('not' x 'or' y)
proof
 thus 'not' x '&' (x 'eqv' y)
 = 'not' x '&' (x 'or' 'not' y) '&' ('not' x 'or' y) by Th76
.= ('not' x '&' x 'or' 'not' x '&' 'not' y) '&' ('not' x 'or' y) by BINARITH:22
.= (FALSE 'or' 'not' x '&' 'not' y) '&' ('not' x 'or' y) by MARGREL1:46
.= ('not' x '&' 'not' y) '&' ('not' x 'or' y) by BINARITH:7;
end;

theorem
  'not' x '&' (x 'nand' y) = 'not' x 'or' 'not' x '&' 'not' y
proof
  thus 'not' x '&' (x 'nand' y)
     ='not' x '&' ('not' x 'or' 'not' y) by BINARITH:9
    .='not' x '&' 'not' x 'or' 'not' x '&' 'not' y by BINARITH:22
    .='not' x 'or' 'not' x '&' 'not' y by BINARITH:16;
end;

theorem
  'not' x '&' (x 'nor' y) = 'not' x '&' 'not' y
proof
  thus 'not' x '&' (x 'nor' y)
      = 'not' x '&' ('not' x '&' 'not' y) by BINARITH:10
     .= 'not' x '&' 'not' x '&' 'not' y by MARGREL1:52
     .= 'not' x '&' 'not' y by BINARITH:16;
end;

theorem
  'not' x 'or' (x 'imp' y) = 'not' x 'or' y
proof
  thus 'not' x 'or' (x 'imp' y)
      = 'not' x 'or' 'not' x 'or' y by BINARITH:20
     .= 'not' x 'or' y by BINARITH:21;
end;

theorem
  'not' x 'or' (x 'eqv' y) = 'not' x 'or' y
proof
 thus 'not' x 'or' (x 'eqv' y)
 = 'not' x 'or' ((x 'imp' y) '&' (y 'imp' x)) by BINARI_5:23
.= ('not' x 'or' ('not' x 'or' y)) '&' ('not' x 'or' (x 'or' 'not' y))
 by BINARITH:23
.= ('not' x 'or' 'not' x 'or' y) '&' ('not' x 'or' (x 'or' 'not' y))
 by BINARITH:20
.= ('not' x 'or' y) '&' ('not' x 'or' (x 'or' 'not' y)) by BINARITH:21
.= ('not' x 'or' y) '&' ('not' x 'or' x 'or' 'not' y) by BINARITH:20
.= ('not' x 'or' y) '&' (TRUE 'or' 'not' y) by BINARITH:18
.= ('not' x 'or' y) '&' TRUE by BINARITH:19
.= 'not' x 'or' y by MARGREL1:50;
end;

theorem
  'not' x 'or' (x 'nand' y) = 'not' x 'or' 'not' y
proof
  thus 'not' x 'or' (x 'nand' y)
       ='not' x 'or' ('not' x 'or' 'not' y) by BINARITH:9
      .='not' x 'or' 'not' x 'or' 'not' y by BINARITH:20
      .='not' x 'or' 'not' y by BINARITH:21;
end;

theorem
  'not' x 'xor' (x 'imp' y) = x '&' y
proof
  thus 'not' x 'xor' (x 'imp' y)
 = ('not' 'not' x '&' ('not' x 'or' y)) 'or'
   ('not' x '&' 'not' ('not' x 'or' y)) by BINARITH:def 2
.= (x '&' ('not' x 'or' y)) 'or' ('not' x '&' 'not' ('not' x 'or' y))
.= ((x '&' 'not' x) 'or' (x '&' y)) 'or' ('not' x '&' 'not' ('not' x 'or' y))
 by BINARITH:22
.= (FALSE 'or' (x '&' y)) 'or' ('not' x '&' 'not' ('not' x 'or' y))
 by MARGREL1:46
.= (x '&' y) 'or' ('not' x '&' 'not' ('not' x 'or' y)) by BINARITH:7
.= (x '&' y) 'or' ('not' x '&' ('not' 'not' x '&' 'not' y)) by BINARITH:10
.= (x '&' y) 'or' ('not' x '&' (x '&' 'not' y))
.= (x '&' y) 'or' ('not' x '&' x '&' 'not' y) by MARGREL1:52
.= (x '&' y) 'or' (FALSE '&' 'not' y) by MARGREL1:46
.= (x '&' y) 'or' FALSE by MARGREL1:49
.= x '&' y by BINARITH:7;
end;

theorem
  'not' x 'xor' (y 'imp' x) = (x '&' (x 'or' 'not' y)) 'or' ('not' x '&' y)
proof
 thus 'not' x 'xor' (y 'imp' x)
 = ('not' 'not' x '&' ('not' y 'or' x)) 'or'
   ('not' x '&' 'not' ('not' y 'or' x)) by BINARITH:def 2
.= (x '&' (x 'or' 'not' y)) 'or' ('not' x '&' 'not' ('not' y 'or' x))
.= (x '&' (x 'or' 'not' y)) 'or' ('not' x '&' ('not' 'not' y '&' 'not' x))
 by BINARITH:10
.= (x '&' (x 'or' 'not' y)) 'or' ('not' x '&' ('not' x '&' y))
.= (x '&' (x 'or' 'not' y)) 'or' ('not' x '&' 'not' x '&' y)
 by MARGREL1:52
.= (x '&' (x 'or' 'not' y)) 'or' ('not' x '&' y) by BINARITH:16;
end;

theorem Th85:
  'not' (x 'imp' y) = x '&' 'not' y
proof
  thus 'not' (x 'imp' y) = 'not' 'not' x '&' 'not' y by BINARITH:10
    .= x '&' 'not' y;
end;

theorem Th86:
  'not' (x 'eqv' y) = (x '&' 'not' y) 'or' (y '&' 'not' x)
proof
  thus 'not' (x 'eqv' y)
      = 'not' ((x 'imp' y) '&' (y 'imp' x)) by BINARI_5:23
     .= 'not' (x 'imp' y) 'or' 'not' (y 'imp' x) by BINARITH:9
     .= (x '&' 'not' y) 'or' 'not' (y 'imp' x) by Th85
     .= (x '&' 'not' y) 'or' (y '&' 'not' x) by Th85;
end;

theorem
  'not' x 'xor' (x 'eqv' y) = y
proof
  thus 'not' x 'xor' (x 'eqv' y) = ('not' 'not' x '&' (x 'eqv' y)) 'or'
       ('not' x '&' 'not' (x 'eqv' y)) by BINARITH:def 2
    .= (x '&' (x 'eqv' y)) 'or'
       ('not' x '&' 'not' (x 'eqv' y))
    .= (x '&' y) 'or' ('not' x '&' 'not' (x 'eqv' y)) by Th18
    .= (x '&' y) 'or' ('not' x '&' ((x '&' 'not' y) 'or' (y '&' 'not' x)))
         by Th86
    .= (x '&' y) 'or'
       (('not' x '&' (x '&' 'not' y)) 'or' ('not' x '&' ('not' x '&' y)))
         by BINARITH:22
    .= (x '&' y) 'or'
       (('not' x '&' x '&' 'not' y) 'or' ('not' x '&' ('not' x '&' y)))
         by MARGREL1:52
    .= (x '&' y) 'or'
       ((FALSE '&' 'not' y) 'or' ('not' x '&' ('not' x '&' y)))
         by MARGREL1:46
    .= (x '&' y) 'or' (FALSE 'or' ('not' x '&' ('not' x '&' y)))
         by MARGREL1:49
    .= (x '&' y) 'or' ('not' x '&' ('not' x '&' y)) by BINARITH:7
    .= (x '&' y) 'or' ('not' x '&' 'not' x '&' y) by MARGREL1:52
    .= (x '&' y) 'or' ('not' x '&' y) by BINARITH:16
    .= (x 'or' 'not' x) '&' y by BINARITH:22
    .= TRUE '&' y by BINARITH:18
    .= y by MARGREL1:50;
end;
