:: On Nowhere and Everywhere Dense Subspaces of Topological Spaces
::  by Zbigniew Karno
::
:: Received November 9, 1993
:: Copyright (c) 1993 Association of Mizar Users

environ

 vocabulary PRE_TOPC, TSEP_2, COLLSP, TOPS_3, BOOLE, TOPS_1, REALSET1,
      SUBSET_1, TARSKI, SETFAM_1, NATTRA_1, TDLAT_3;
 notation TARSKI, XBOOLE_0, SUBSET_1, STRUCT_0, PRE_TOPC, TOPS_1, BORSUK_1,
      TSEP_1, TSEP_2, TDLAT_3, TOPS_3, TEX_1, TEX_2;
 constructors REALSET1, TOPS_1, BORSUK_1, TSEP_1, TSEP_2, TDLAT_3, TOPS_3,
      TEX_2, MEMBERED;
 clusters PRE_TOPC, BORSUK_1, TDLAT_3, TEX_1, TEX_2, STRUCT_0, MEMBERED,
      ZFMISC_1;
 requirements BOOLE, SUBSET;


begin
:: 1. Some Properties of Subsets of a Topological Space.
reserve X for non empty TopSpace, A,B for Subset of X;

theorem :: TEX_3:1
 A,B constitute_a_decomposition implies (A is non empty iff B is proper);

theorem :: TEX_3:2
 A,B constitute_a_decomposition implies (A is dense iff B is boundary);

theorem :: TEX_3:3
   A,B constitute_a_decomposition implies (A is boundary iff B is dense)
;

theorem :: TEX_3:4
 A,B constitute_a_decomposition implies
  (A is everywhere_dense iff B is nowhere_dense);

theorem :: TEX_3:5
   A,B constitute_a_decomposition implies
  (A is nowhere_dense iff B is everywhere_dense);

reserve Y1,Y2 for non empty SubSpace of X;

theorem :: TEX_3:6
 Y1,Y2 constitute_a_decomposition implies Y1 is proper & Y2 is proper;

theorem :: TEX_3:7
   for X being non trivial (non empty TopSpace),
     D being non empty proper Subset of X
  ex Y0 being proper strict non empty SubSpace of X st D = the carrier of Y0;

theorem :: TEX_3:8
 for X being non trivial (non empty TopSpace),
     Y1 being proper non empty SubSpace of X
  ex Y2 being proper strict non empty SubSpace of X st
    Y1,Y2 constitute_a_decomposition;


begin
:: 2. Dense and Everywhere Dense Subspaces.

definition let X be non empty TopSpace;
  let IT be SubSpace of X;
 attr IT is dense means
:: TEX_3:def 1
 for A being Subset of X st
  A = the carrier of IT holds A is dense;
end;


theorem :: TEX_3:9
 for X0 being SubSpace of X, A being Subset of X
   st A = the carrier of X0 holds
  X0 is dense iff A is dense;

registration let X be non empty TopSpace;
 cluster dense closed -> non proper SubSpace of X;
 coherence
  proof let X0 be SubSpace of X;
   assume A1: X0 is dense closed;
      the carrier of X0 is Subset of X by TSEP_1:1;
    then reconsider A = the carrier of X0 as Subset of X;
         A is dense & A is closed by A1,Th9,TSEP_1:11;
      then Cl A = the carrier of X & Cl A = A by PRE_TOPC:52,TOPS_3:def 2;
     then A is non proper by TEX_2:5;
   hence thesis by TEX_2:13;
  end;
 cluster dense proper -> non closed SubSpace of X;
 coherence
  proof let X0 be SubSpace of X;
   assume A2: X0 is dense proper;
   assume A3: X0 is closed;
      the carrier of X0 is Subset of X by TSEP_1:1;
    then reconsider A = the carrier of X0 as Subset of X;
         A is dense & A is closed by A2,A3,Th9,TSEP_1:11;
      then Cl A = the carrier of X & Cl A = A by PRE_TOPC:52,TOPS_3:def 2;
     then A is non proper by TEX_2:5;
   hence contradiction by A2,TEX_2:13;
  end;
 cluster proper closed -> non dense SubSpace of X;
 coherence
  proof let X0 be SubSpace of X;
   assume A4: X0 is proper closed;
   assume A5: X0 is dense;
      the carrier of X0 is Subset of X by TSEP_1:1;
    then reconsider A = the carrier of X0 as Subset of X;
         A is dense & A is closed by A4,A5,Th9,TSEP_1:11;
      then Cl A = the carrier of X & Cl A = A by PRE_TOPC:52,TOPS_3:def 2;
     then A is non proper by TEX_2:5;
   hence contradiction by A4,TEX_2:13;
  end;
end;


registration let X be non empty TopSpace;
 cluster dense strict non empty SubSpace of X;
 existence
  proof
       X is SubSpace of X by TSEP_1:2;
    then reconsider A0 = the carrier of X as Subset of X
    by TSEP_1:1;
   consider X0 being strict non empty SubSpace of X such that
    A1: A0 = the carrier of X0 by TSEP_1:10;
   take X0;
      now let A be Subset of X;
     assume A = the carrier of X0;
      then A = [#]X by A1,PRE_TOPC:12;
     hence A is dense by TOPS_3:16;
    end;
   hence thesis by Def1;
  end;
end;


::Properties of Dense Subspaces.
theorem :: TEX_3:10
 for A0 being non empty Subset of X st A0 is dense
  ex X0 being dense strict non empty SubSpace of X st A0 = the carrier of X0;

theorem :: TEX_3:11
   for X0 being dense non empty SubSpace of X,
     A being Subset of X,
     B being Subset of X0
  st A = B holds B is dense iff A is dense;

theorem :: TEX_3:12
   for X1 being dense SubSpace of X, X2 being SubSpace of X holds
  X1 is SubSpace of X2 implies X2 is dense;

theorem :: TEX_3:13
   for X1 being dense non empty SubSpace of X,
     X2 being non empty SubSpace of X holds
  X1 is SubSpace of X2 implies X1 is dense SubSpace of X2;

theorem :: TEX_3:14
   for X1 being dense non empty SubSpace of X,
     X2 being dense non empty SubSpace of X1
 holds X2 is dense non empty SubSpace of X;

theorem :: TEX_3:15
   for Y1, Y2 being non empty TopSpace st Y2 = the TopStruct of Y1 holds
  Y1 is dense SubSpace of X iff Y2 is dense SubSpace of X;

definition let X be non empty TopSpace;
  let IT be SubSpace of X;
 attr IT is everywhere_dense means
:: TEX_3:def 2
 for A being Subset of X st
   A = the carrier of IT holds A is everywhere_dense;
end;


theorem :: TEX_3:16
 for X0 being SubSpace of X, A being Subset of X
   st A = the carrier of X0 holds
  X0 is everywhere_dense iff A is everywhere_dense;

registration let X be non empty TopSpace;
 cluster everywhere_dense -> dense SubSpace of X;
 coherence
  proof let X0 be SubSpace of X;
   assume A1: X0 is everywhere_dense;
      now let A be Subset of X;
     assume A = the carrier of X0;
      then A is everywhere_dense by A1,Def2;
     hence A is dense by TOPS_3:33;
    end;
   hence thesis by Def1;
  end;
 cluster non dense -> non everywhere_dense SubSpace of X;
 coherence
  proof let X0 be SubSpace of X;
   assume A2: X0 is non dense;
   assume A3: X0 is everywhere_dense;
      now let A be Subset of X;
     assume A = the carrier of X0;
      then A is everywhere_dense by A3,Def2;
     hence A is dense by TOPS_3:33;
    end;
   hence contradiction by A2,Def1;
  end;
 cluster non proper -> everywhere_dense SubSpace of X;
 coherence
  proof let X0 be SubSpace of X;
   assume A4: X0 is non proper;
      now let A be Subset of X;
     assume A = the carrier of X0;
      then A is non proper by A4,TEX_2:13;
      then A = the carrier of X by TEX_2:5;
      then A = [#]X by PRE_TOPC:12;
     hence A is everywhere_dense by TOPS_3:31;
    end;
   hence thesis by Def2;
  end;
 cluster non everywhere_dense -> proper SubSpace of X;
 coherence
  proof let X0 be SubSpace of X;
   assume A5: X0 is non everywhere_dense;
   assume A6: X0 is non proper;
      now let A be Subset of X;
     assume A = the carrier of X0;
      then A is non proper by A6,TEX_2:13;
      then A = the carrier of X by TEX_2:5;
      then A = [#]X by PRE_TOPC:12;
     hence A is everywhere_dense by TOPS_3:31;
    end;
   hence contradiction by A5,Def2;
  end;
end;


registration let X be non empty TopSpace;
 cluster everywhere_dense strict non empty SubSpace of X;
 existence
  proof
       X is SubSpace of X by TSEP_1:2;
    then reconsider A0 = the carrier of X
     as Subset of X by TSEP_1:1;
   consider X0 being strict non empty SubSpace of X such that
    A1: A0 = the carrier of X0 by TSEP_1:10;
   take X0;
      now let A be Subset of X;
     assume A = the carrier of X0;
      then A = [#]X by A1,PRE_TOPC:12;
     hence A is everywhere_dense by TOPS_3:31;
    end;
   hence thesis by Def2;
  end;
end;


::Properties of Everywhere Dense Subspaces.
theorem :: TEX_3:17
 for A0 being non empty Subset of X st A0 is everywhere_dense
  ex X0 being everywhere_dense strict non empty SubSpace of X
    st A0 = the carrier of X0;

theorem :: TEX_3:18
   for X0 being everywhere_dense non empty SubSpace of X,
   A being Subset of X, B being Subset of X0
   st A = B holds
  B is everywhere_dense iff A is everywhere_dense;

theorem :: TEX_3:19
   for X1 being everywhere_dense SubSpace of X, X2 being SubSpace of X holds
  X1 is SubSpace of X2 implies X2 is everywhere_dense;

theorem :: TEX_3:20
   for X1 being everywhere_dense non empty SubSpace of X,
     X2 being non empty SubSpace of X holds
  X1 is SubSpace of X2 implies X1 is everywhere_dense SubSpace of X2;

theorem :: TEX_3:21
   for X1 being everywhere_dense non empty SubSpace of X,
      X2 being everywhere_dense non empty SubSpace of X1 holds
  X2 is everywhere_dense SubSpace of X;

theorem :: TEX_3:22
   for Y1, Y2 being non empty TopSpace st Y2 = the TopStruct of Y1 holds
 Y1 is everywhere_dense SubSpace of X iff Y2 is everywhere_dense SubSpace of X;

registration let X be non empty TopSpace;
 cluster dense open -> everywhere_dense SubSpace of X;
 coherence
  proof let X0 be SubSpace of X;
   assume A1: X0 is dense open;
      now let A be Subset of X;
     assume A = the carrier of X0;
      then A is dense & A is open by A1,Th9,TSEP_1:16;
     hence A is everywhere_dense by TOPS_3:36;
    end;
   hence thesis by Def2;
  end;
 cluster dense non everywhere_dense -> non open SubSpace of X;
 coherence
  proof let X0 be SubSpace of X;
   assume A2: X0 is dense non everywhere_dense;
   assume A3: X0 is open;
      now let A be Subset of X;
     assume A = the carrier of X0;
      then A is dense & A is open by A2,A3,Th9,TSEP_1:16;
     hence A is everywhere_dense by TOPS_3:36;
    end;
   hence contradiction by A2,Def2;
  end;
 cluster open non everywhere_dense -> non dense SubSpace of X;
 coherence
  proof let X0 be SubSpace of X;
   assume A4: X0 is open non everywhere_dense;
   assume A5: X0 is dense;
      now let A be Subset of X;
     assume A = the carrier of X0;
      then A is dense & A is open by A4,A5,Th9,TSEP_1:16;
     hence A is everywhere_dense by TOPS_3:36;
    end;
   hence contradiction by A4,Def2;
  end;
end;


registration let X be non empty TopSpace;
 cluster dense open strict non empty SubSpace of X;
 existence
  proof
      X is SubSpace of X by TSEP_1:2;
    then the carrier of X is Subset of X by TSEP_1:1;
    then reconsider A0 = the carrier of X as Subset of X;
       A0 = [#]X by PRE_TOPC:12;
    then A0 is dense by TOPS_3:16;
    then consider X0 being dense strict non empty SubSpace of X such that
            A1: A0 = the carrier of X0 by Th10;
   take X0;
      now let A be Subset of X;
     assume A = the carrier of X0;
      then A = [#]X by A1,PRE_TOPC:12;
     hence A is open by TOPS_1:53;
    end;
   hence thesis by TSEP_1:def 1;
  end;
end;


::Properties of Dense Open Subspaces.
theorem :: TEX_3:23
 for A0 being non empty Subset of X st A0 is dense open
  ex X0 being dense open strict non empty SubSpace of X
   st A0 = the carrier of X0;

theorem :: TEX_3:24
   for X0 being SubSpace of X holds X0 is everywhere_dense iff
  ex X1 being dense open strict SubSpace of X st X1 is SubSpace of X0;

reserve X1, X2 for non empty SubSpace of X;

theorem :: TEX_3:25
    X1 is dense or X2 is dense implies
   X1 union X2 is dense SubSpace of X;

theorem :: TEX_3:26
    X1 is everywhere_dense or X2 is everywhere_dense implies
   X1 union X2 is everywhere_dense SubSpace of X;

theorem :: TEX_3:27
   X1 is everywhere_dense & X2 is everywhere_dense implies
  X1 meet X2 is everywhere_dense SubSpace of X;

theorem :: TEX_3:28
    X1 is everywhere_dense & X2 is dense or
       X1 is dense & X2 is everywhere_dense implies
  X1 meet X2 is dense SubSpace of X;

begin
:: 3. Boundary and Nowhere Dense Subspaces.

definition let X be non empty TopSpace;
  let IT be SubSpace of X;
 attr IT is boundary means
:: TEX_3:def 3
 for A being Subset of X st
  A = the carrier of IT holds A is boundary;
end;


theorem :: TEX_3:29
 for X0 being SubSpace of X, A being Subset of X
 st A = the carrier of X0 holds
   X0 is boundary iff A is boundary;

registration let X be non empty TopSpace;
 cluster open -> non boundary (non empty SubSpace of X);
 coherence
  proof let X0 be non empty SubSpace of X;
   assume A1: X0 is open;
      now assume A2: X0 is boundary;
        the carrier of X0 is Subset of X by TSEP_1:1;
      then reconsider A = the carrier of X0 as Subset of X;
         A is boundary & A is open by A1,A2,Def3,TSEP_1:16;
      then Int A = {} & Int A = A by TOPS_1:55,84;
     hence contradiction;
    end;
   hence thesis;
  end;
 cluster boundary -> non open (non empty SubSpace of X);
 coherence
  proof let X0 be non empty SubSpace of X;
   assume A3: X0 is boundary;
      now assume A4: X0 is open;
        the carrier of X0 is Subset of X by TSEP_1:1;
      then reconsider A = the carrier of X0 as Subset of X;
         A is boundary & A is open by A3,A4,Def3,TSEP_1:16;
      then Int A = {} & Int A = A by TOPS_1:55,84;
     hence contradiction;
    end;
   hence thesis;
  end;
 cluster everywhere_dense -> non boundary SubSpace of X;
 coherence
  proof let X0 be SubSpace of X;
   assume A5: X0 is everywhere_dense;
      now assume A6: X0 is boundary;
        the carrier of X0 is Subset of X by TSEP_1:1;
      then reconsider A = the carrier of X0 as Subset of X;
          A is everywhere_dense by A5,Def2;
       then A is not boundary & A is boundary by A6,Def3,TOPS_3:37;
     hence contradiction;
    end;
   hence thesis;
  end;
 cluster boundary -> non everywhere_dense SubSpace of X;
 coherence
  proof let X0 be SubSpace of X;
   assume A7: X0 is boundary;
   assume A8: X0 is everywhere_dense;
        the carrier of X0 is Subset of X by TSEP_1:1;
      then reconsider A = the carrier of X0 as Subset of X;
          A is everywhere_dense by A8,Def2;
      then A is not boundary & A is boundary by A7,Def3,TOPS_3:37;
   hence contradiction;
  end;
end;


::Properties of Boundary Subspaces.
theorem :: TEX_3:30
   for A0 being non empty Subset of X st A0 is boundary
  ex X0 being strict SubSpace of X st X0 is boundary & A0 = the carrier of X0;

theorem :: TEX_3:31
 for X1,X2 being SubSpace of X st X1,X2 constitute_a_decomposition holds
  X1 is dense iff X2 is boundary;

theorem :: TEX_3:32
  for X1,X2 being non empty SubSpace of X st X1,X2 constitute_a_decomposition
 holds X1 is boundary iff X2 is dense;

theorem :: TEX_3:33
 for X0 being SubSpace of X st X0 is boundary
  for A being Subset of X st A c= the carrier of X0 holds A is boundary;

theorem :: TEX_3:34
 for X1,X2 being SubSpace of X st X1 is boundary holds
  X2 is SubSpace of X1 implies X2 is boundary;

definition let X be non empty TopSpace;
  let IT be SubSpace of X;
 attr IT is nowhere_dense means
:: TEX_3:def 4
 for A being Subset of X st
  A = the carrier of IT holds A is nowhere_dense;
end;


theorem :: TEX_3:35
 for X0 being SubSpace of X, A being Subset of X
 st A = the carrier of X0 holds
   X0 is nowhere_dense iff A is nowhere_dense;

registration let X be non empty TopSpace;
 cluster nowhere_dense -> boundary SubSpace of X;
 coherence
  proof let X0 be SubSpace of X;
   assume A1: X0 is nowhere_dense;
      now let A be Subset of X;
     assume A = the carrier of X0;
      then A is nowhere_dense by A1,Def4;
     hence A is boundary by TOPS_1:92;
    end;
   hence thesis by Def3;
  end;
 cluster non boundary -> non nowhere_dense SubSpace of X;
 coherence
  proof let X0 be SubSpace of X;
   assume A2: X0 is non boundary;
      now assume A3: X0 is nowhere_dense;
           now let A be Subset of X;
          assume A = the carrier of X0;
           then A is nowhere_dense by A3,Def4;
          hence A is boundary by TOPS_1:92;
         end;
     hence contradiction by A2,Def3;
    end;
   hence thesis;
  end;
 cluster nowhere_dense -> non dense SubSpace of X;
 coherence
  proof let X0 be SubSpace of X;
   assume A4: X0 is nowhere_dense;
   assume A5: X0 is dense;
       the carrier of X0 is Subset of X by TSEP_1:1;
     then reconsider A = the carrier of X0 as Subset of X;
         A is nowhere_dense by A4,Def4;
      then A is not dense & A is dense by A5,Def1,TOPS_3:25;
   hence contradiction;
  end;
 cluster dense -> non nowhere_dense SubSpace of X;
 coherence
  proof let X0 be SubSpace of X;
   assume A6: X0 is dense;
   assume A7: X0 is nowhere_dense;
       the carrier of X0 is Subset of X by TSEP_1:1;
     then reconsider A = the carrier of X0 as Subset of X;
         A is nowhere_dense by A7,Def4;
      then A is not dense & A is dense by A6,Def1,TOPS_3:25;
   hence contradiction;
  end;
end;


reserve X for non empty TopSpace;

::Properties of Nowhere Dense Subspaces.
theorem :: TEX_3:36
   for A0 being non empty Subset of X st A0 is nowhere_dense
  ex X0 being strict SubSpace of X st
      X0 is nowhere_dense & A0 = the carrier of X0;

theorem :: TEX_3:37
 for X1,X2 being SubSpace of X st X1,X2 constitute_a_decomposition holds
  X1 is everywhere_dense iff X2 is nowhere_dense;

theorem :: TEX_3:38
  for X1,X2 being non empty SubSpace of X st X1,X2 constitute_a_decomposition
 holds X1 is nowhere_dense iff X2 is everywhere_dense;

theorem :: TEX_3:39
 for X0 being SubSpace of X st X0 is nowhere_dense
  for A being Subset of X st A c= the carrier of X0
  holds A is nowhere_dense;

theorem :: TEX_3:40
 for X1,X2 being SubSpace of X st X1 is nowhere_dense holds
  X2 is SubSpace of X1 implies X2 is nowhere_dense;

registration let X be non empty TopSpace;
 cluster boundary closed -> nowhere_dense SubSpace of X;
 coherence
  proof let X0 be SubSpace of X;
   assume A1: X0 is boundary closed;
      now let A be Subset of X;
     assume A = the carrier of X0;
      then A is boundary & A is closed by A1,Th29,TSEP_1:11;
     hence A is nowhere_dense by TOPS_1:93;
    end;
   hence thesis by Def4;
  end;
 cluster boundary non nowhere_dense -> non closed SubSpace of X;
 coherence
  proof let X0 be SubSpace of X;
   assume A2: X0 is boundary non nowhere_dense;
      now assume A3: X0 is closed;
            now let A be Subset of X;
           assume A = the carrier of X0;
            then A is boundary & A is closed by A2,A3,Th29,TSEP_1:11;
           hence A is nowhere_dense by TOPS_1:93;
          end;
     hence contradiction by A2,Def4;
    end;
   hence thesis;
  end;
 cluster closed non nowhere_dense -> non boundary SubSpace of X;
 coherence
  proof let X0 be SubSpace of X;
   assume A4: X0 is closed non nowhere_dense;
      now assume A5: X0 is boundary;
            now let A be Subset of X;
           assume A = the carrier of X0;
            then A is boundary & A is closed by A4,A5,Th29,TSEP_1:11;
           hence A is nowhere_dense by TOPS_1:93;
          end;
     hence contradiction by A4,Def4;
    end;
   hence thesis;
  end;
end;


::Properties of Boundary Closed Subspaces.
theorem :: TEX_3:41
 for A0 being non empty Subset of X st A0 is boundary closed
  ex X0 being closed strict non empty SubSpace of X st
      X0 is boundary & A0 = the carrier of X0;

theorem :: TEX_3:42
   for X0 being non empty SubSpace of X holds X0 is nowhere_dense iff
  ex X1 being closed strict non empty SubSpace of X st
      X1 is boundary & X0 is SubSpace of X1;

reserve X1, X2 for non empty SubSpace of X;

theorem :: TEX_3:43
   (X1 is boundary or X2 is boundary) & X1 meets X2 implies
   X1 meet X2 is boundary;

theorem :: TEX_3:44
   X1 is nowhere_dense & X2 is nowhere_dense implies
   X1 union X2 is nowhere_dense;

theorem :: TEX_3:45
   X1 is nowhere_dense & X2 is boundary or X1 is boundary & X2 is nowhere_dense
   implies X1 union X2 is boundary;

theorem :: TEX_3:46
   (X1 is nowhere_dense or X2 is nowhere_dense) & X1 meets X2 implies
    X1 meet X2 is nowhere_dense;


begin
:: 4. Dense and Boundary Subspaces of non Discrete Spaces.

theorem :: TEX_3:47
   for X being non empty TopSpace holds
   (for X0 being SubSpace of X holds X0 is non boundary) implies
  X is discrete;

theorem :: TEX_3:48
   for X being non trivial (non empty TopSpace) holds
   (for X0 being proper SubSpace of X holds X0 is non dense) implies
  X is discrete;

registration let X be discrete non empty TopSpace;
 cluster -> non boundary (non empty SubSpace of X);
 coherence;
 cluster proper -> non dense SubSpace of X;
 coherence
  proof let X0 be SubSpace of X;
      the carrier of X0 is Subset of X by TSEP_1:1;
    then reconsider A = the carrier of X0 as Subset of X;
   assume X0 is proper;
    then A is proper by TEX_2:13;
    then A <> the carrier of X by TEX_2:5;
    then A is not dense by TEX_1:35;
   hence X0 is not dense by Th9;
  end;
 cluster dense -> non proper SubSpace of X;
 coherence
  proof let X0 be SubSpace of X;
      the carrier of X0 is Subset of X by TSEP_1:1;
    then reconsider A = the carrier of X0 as Subset of X;
   assume A1: X0 is dense;
   assume X0 is proper;
    then A is proper by TEX_2:13;
    then A <> the carrier of X by TEX_2:5;
    then A is not dense by TEX_1:35;
   hence contradiction by A1,Th9;
  end;
end;


registration let X be discrete non empty TopSpace;
 cluster non boundary strict non empty SubSpace of X;
 existence
  proof
    consider X0 being strict non empty SubSpace of X;
   take X0;
   thus thesis;
  end;
end;


registration let X be discrete non trivial (non empty TopSpace);
 cluster non dense strict SubSpace of X;
 existence
  proof
    consider X0 being proper strict SubSpace of X;
   take X0;
   thus thesis;
  end;
end;


theorem :: TEX_3:49
   for X being non empty TopSpace holds
   (ex X0 being non empty SubSpace of X st X0 is boundary) implies
  X is non discrete;

theorem :: TEX_3:50
   for X being non empty TopSpace holds
   (ex X0 being non empty SubSpace of X st X0 is dense proper) implies
  X is non discrete;

registration let X be non discrete non empty TopSpace;
 cluster boundary strict non empty SubSpace of X;
 existence
  proof
    consider A0 being non empty Subset of X such that
           A1: A0 is boundary by TEX_1:def 6;
    consider X0 being strict non empty SubSpace of X such that
           A2: A0 = the carrier of X0 by TSEP_1:10;
   take X0;
      for A be Subset of X st A = the carrier of X0
     holds A is boundary by A1,A2;
   hence thesis by Def3;
  end;
 cluster dense proper strict non empty SubSpace of X;
 existence
  proof
    consider A0 being Subset of X such that
           A3: A0 <> the carrier of X and A4: A0 is dense by TEX_1:35;
      A0 is non empty by A4,TOPS_3:17;
    then consider X0 being dense strict non empty SubSpace of X such that
           A5: A0 = the carrier of X0 by A4,Th10;
   take X0;
       A0 is proper by A3,TEX_2:5;
   hence thesis by A5,TEX_2:13;
  end;
end;


reserve X for non discrete non empty TopSpace;

theorem :: TEX_3:51
   for A0 being non empty Subset of X st A0 is boundary
  ex X0 being boundary strict SubSpace of X st A0 = the carrier of X0;

theorem :: TEX_3:52
   for A0 being non empty proper Subset of X st A0 is dense
  ex X0 being dense proper strict SubSpace of X st A0 = the carrier of X0;

theorem :: TEX_3:53
   for X1 being boundary non empty SubSpace of X
  ex X2 being dense proper strict non empty SubSpace of X st
    X1,X2 constitute_a_decomposition;

theorem :: TEX_3:54
   for X1 being dense proper non empty SubSpace of X
  ex X2 being boundary strict non empty SubSpace of X st
    X1,X2 constitute_a_decomposition;

theorem :: TEX_3:55
   for Y1, Y2 being non empty TopSpace st Y2 = the TopStruct of Y1 holds
  Y1 is boundary SubSpace of X iff Y2 is boundary SubSpace of X;


begin
:: 5. Everywhere and Nowhere Dense Subspaces of non Almost Discrete Spaces.

theorem :: TEX_3:56
   for X being non empty TopSpace holds
   (for X0 being SubSpace of X holds X0 is non nowhere_dense) implies
  X is almost_discrete;

theorem :: TEX_3:57
   for X being non trivial (non empty TopSpace) holds
   (for X0 being proper SubSpace of X holds X0 is non everywhere_dense) implies
  X is almost_discrete;

registration let X be almost_discrete non empty TopSpace;
 cluster -> non nowhere_dense (non empty SubSpace of X);
 coherence
  proof let X0 be non empty SubSpace of X;
     the carrier of X0 is Subset of X by TSEP_1:1;
   then reconsider A = the carrier of X0 as Subset of X;
       A is not nowhere_dense by TEX_1:def 7;
   hence X0 is non nowhere_dense by Th35;
  end;
 cluster proper -> non everywhere_dense SubSpace of X;
 coherence
  proof let X0 be SubSpace of X;
     the carrier of X0 is Subset of X by TSEP_1:1;
   then reconsider A = the carrier of X0 as Subset of X;
   assume X0 is proper;
    then A is proper by TEX_2:13;
    then A <> the carrier of X by TEX_2:5;
    then A is not everywhere_dense by TEX_1:36;
   hence X0 is not everywhere_dense by Th16;
  end;
 cluster everywhere_dense -> non proper SubSpace of X;
 coherence
  proof let X0 be SubSpace of X;
     the carrier of X0 is Subset of X by TSEP_1:1;
   then reconsider A = the carrier of X0 as Subset of X;
   assume A1: X0 is everywhere_dense;
   assume X0 is proper;
    then A is proper by TEX_2:13;
    then A <> the carrier of X by TEX_2:5;
    then A is not everywhere_dense by TEX_1:36;
   hence contradiction by A1,Th16;
  end;
 cluster boundary -> non closed (non empty SubSpace of X);
 coherence
  proof let X0 be non empty SubSpace of X;
     the carrier of X0 is non empty Subset of X by TSEP_1:1;
   then reconsider A = the carrier of X0 as non empty Subset of X;
   assume A2: X0 is boundary;
   assume A3: X0 is closed;
      now
     take A;
     thus A is boundary closed by A2,A3,Th29,TSEP_1:11;
    end;
   hence contradiction by TEX_1:37;
  end;
 cluster closed -> non boundary (non empty SubSpace of X);
 coherence
  proof let X0 be non empty SubSpace of X;
     the carrier of X0 is non empty Subset of X by TSEP_1:1;
   then reconsider A = the carrier of X0 as non empty Subset of X;
   assume A4: X0 is closed;
   assume A5: X0 is boundary;
      now
     take A;
     thus A is boundary closed by A4,A5,Th29,TSEP_1:11;
    end;
   hence contradiction by TEX_1:37;
  end;
 cluster dense proper -> non open SubSpace of X;
 coherence
  proof let X0 be SubSpace of X;
     the carrier of X0 is Subset of X by TSEP_1:1;
   then reconsider A = the carrier of X0 as Subset of X;
   reconsider B = the carrier of X0 as Subset of X by TSEP_1:1;
   assume A6: X0 is dense proper;
     then A7: A is dense & B is proper by Th9,TEX_2:13;
   assume A8: X0 is open;
      now
     take A;
     thus A <> the carrier of X by A7,TEX_2:5;
     thus A is dense open by A6,A8,Th9,TSEP_1:16;
    end;
   hence contradiction by TEX_1:38;
  end;
 cluster dense open -> non proper SubSpace of X;
 coherence
  proof let X0 be SubSpace of X;
     the carrier of X0 is Subset of X by TSEP_1:1;
   then reconsider A = the carrier of X0 as Subset of X;
   reconsider B = the carrier of X0 as Subset of X by TSEP_1:1;
   assume A9: X0 is dense open;
   assume X0 is proper;
     then A10: B is proper by TEX_2:13;
      now
     take A;
     thus A <> the carrier of X by A10,TEX_2:5;
     thus A is dense open by A9,Th9,TSEP_1:16;
    end;
   hence contradiction by TEX_1:38;
  end;
 cluster open proper -> non dense SubSpace of X;
 coherence
  proof let X0 be SubSpace of X;
     the carrier of X0 is Subset of X by TSEP_1:1;
   then reconsider A = the carrier of X0 as Subset of X;
   reconsider B = the carrier of X0 as Subset of X by TSEP_1:1;
   assume A11: X0 is open proper;
     then A12: A is open & B is proper by TEX_2:13,TSEP_1:16;
   assume A13: X0 is dense;
      now
     take A;
     thus A <> the carrier of X by A12,TEX_2:5;
     thus A is dense open by A11,A13,Th9,TSEP_1:16;
    end;
   hence contradiction by TEX_1:38;
  end;
end;


registration let X be almost_discrete non empty TopSpace;
 cluster non nowhere_dense strict non empty SubSpace of X;
 existence
  proof
    consider X0 being strict non empty SubSpace of X;
   take X0;
   thus thesis;
  end;
end;


registration let X be almost_discrete non trivial (non empty TopSpace);
 cluster non everywhere_dense strict SubSpace of X;
 existence
  proof
    consider X0 being proper strict SubSpace of X;
   take X0;
   thus thesis;
  end;
end;


theorem :: TEX_3:58
   for X being non empty TopSpace holds
   (ex X0 being non empty SubSpace of X st X0 is nowhere_dense) implies
  X is non almost_discrete;

theorem :: TEX_3:59
   for X being non empty TopSpace holds
   (ex X0 being non empty SubSpace of X st X0 is boundary closed) implies
  X is non almost_discrete;

theorem :: TEX_3:60
   for X being non empty TopSpace holds
   (ex X0 being non empty SubSpace of X st X0 is everywhere_dense proper)
   implies
  X is non almost_discrete;

theorem :: TEX_3:61
   for X being non empty TopSpace holds
   (ex X0 being non empty SubSpace of X st X0 is dense open proper) implies
  X is non almost_discrete;

registration let X be non almost_discrete non empty TopSpace;
 cluster nowhere_dense strict non empty SubSpace of X;
 existence
  proof
    consider A0 being non empty Subset of X such that
           A1: A0 is nowhere_dense by TEX_1:def 7;
    consider X0 being strict non empty SubSpace of X such that
           A2: A0 = the carrier of X0 by TSEP_1:10;
   take X0;
     for A be Subset of X st A = the carrier of X0 holds
    A is nowhere_dense by A1,A2;
   hence thesis by Def4;
  end;
 cluster everywhere_dense proper strict non empty SubSpace of X;
 existence
  proof
    consider A0 being Subset of X such that
       A3: A0 <> the carrier of X and A4: A0 is everywhere_dense by TEX_1:36;
      A0 is non empty by A4,TOPS_3:34;
    then consider X0 being everywhere_dense strict non empty SubSpace of X
    such that
           A5: A0 = the carrier of X0 by A4,Th17;
   take X0;
       A0 is proper by A3,TEX_2:5;
   hence thesis by A5,TEX_2:13;
  end;
end;


reserve X for non almost_discrete non empty TopSpace;

theorem :: TEX_3:62
 for A0 being non empty Subset of X st A0 is nowhere_dense
  ex X0 being nowhere_dense strict non empty SubSpace of X
    st A0 = the carrier of X0;

theorem :: TEX_3:63
   for A0 being non empty proper Subset of X
 st A0 is everywhere_dense
  ex X0 being everywhere_dense proper strict SubSpace of X st
     A0 = the carrier of X0;

theorem :: TEX_3:64
   for X1 being nowhere_dense non empty SubSpace of X
  ex X2 being everywhere_dense proper strict non empty SubSpace of X st
    X1,X2 constitute_a_decomposition;

theorem :: TEX_3:65
   for X1 being everywhere_dense proper non empty SubSpace of X
  ex X2 being nowhere_dense strict non empty SubSpace of X st
    X1,X2 constitute_a_decomposition;

theorem :: TEX_3:66
   for Y1, Y2 being non empty TopSpace st Y2 = the TopStruct of Y1 holds
  Y1 is nowhere_dense SubSpace of X iff Y2 is nowhere_dense SubSpace of X;

registration let X be non almost_discrete non empty TopSpace;
 cluster boundary closed strict non empty SubSpace of X;
 existence
  proof
    consider A being non empty Subset of X such that
           A1: A is nowhere_dense by TEX_1:def 7;
     consider C being Subset of X such that
         A2: A c= C and A3: C is closed and A4: C is boundary by A1,TOPS_3:27;
         C is non empty by A2,XBOOLE_1:3;
      then consider X0 being strict non empty SubSpace of X such that
           A5: C = the carrier of X0 by TSEP_1:10;
   take X0;
A6: for C be Subset of X st C = the carrier of X0
      holds C is boundary by A4,A5;
       for C be Subset of X st C = the carrier of X0
      holds C is closed by A3,A5;
   hence thesis by A6,Def3,BORSUK_1:def 14;
  end;
 cluster dense open proper strict non empty SubSpace of X;
 existence
  proof
    consider A0 being Subset of X such that
       A7: A0 <> the carrier of X and
       A8: A0 is dense open by TEX_1:38;
      A0 is non empty by A8,TOPS_3:17;
    then consider X0 being dense open strict non empty SubSpace of X such that
           A9: A0 = the carrier of X0 by A8,Th23;
   take X0;
       A0 is proper by A7,TEX_2:5;
   hence thesis by A9,TEX_2:13;
  end;
end;


theorem :: TEX_3:67
 for A0 being non empty Subset of X st A0 is boundary closed
  ex X0 being boundary closed strict non empty SubSpace of X
    st A0 = the carrier of X0;

theorem :: TEX_3:68
   for A0 being non empty proper Subset of X st A0 is dense open
  ex X0 being dense open proper strict SubSpace of X st
     A0 = the carrier of X0;

theorem :: TEX_3:69
   for X1 being boundary closed non empty SubSpace of X
  ex X2 being dense open proper strict non empty SubSpace of X st
    X1,X2 constitute_a_decomposition;

theorem :: TEX_3:70
   for X1 being dense open proper non empty SubSpace of X
  ex X2 being boundary closed strict non empty SubSpace of X st
    X1,X2 constitute_a_decomposition;

theorem :: TEX_3:71
   for X0 being non empty SubSpace of X holds X0 is nowhere_dense iff
  ex X1 being boundary closed strict non empty SubSpace of X
    st X0 is SubSpace of X1;

theorem :: TEX_3:72
   for X0 being nowhere_dense non empty SubSpace of X holds
  X0 is boundary closed or
   ex X1 being everywhere_dense proper strict non empty SubSpace of X,
       X2 being boundary closed strict non empty SubSpace of X st
     X1 meet X2 = the TopStruct of X0 & X1 union X2 = the TopStruct of X;

theorem :: TEX_3:73
   for X0 being everywhere_dense non empty SubSpace of X holds
  X0 is dense open or
   ex X1 being dense open proper strict non empty SubSpace of X,
       X2 being nowhere_dense strict non empty SubSpace of X st
     X1 misses X2 & X1 union X2 = the TopStruct of X0;

theorem :: TEX_3:74
   for X0 being nowhere_dense non empty SubSpace of X
  ex X1 being dense open proper strict non empty SubSpace of X,
      X2 being boundary closed strict non empty SubSpace of X st
    X1,X2 constitute_a_decomposition & X0 is SubSpace of X2;

theorem :: TEX_3:75
   for X0 being everywhere_dense proper SubSpace of X
  ex X1 being dense open proper strict SubSpace of X,
      X2 being boundary closed strict SubSpace of X st
    X1,X2 constitute_a_decomposition & X1 is SubSpace of X0;


