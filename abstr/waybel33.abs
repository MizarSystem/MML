:: Compactness of Lim-inf Topology
::  by Grzegorz Bancerek and Noboru Endou
::
:: Received July 29, 2001
:: Copyright (c) 2001 Association of Mizar Users

environ

 vocabulary ORDERS_1, FILTER_0, YELLOW_1, ORDINAL2, LATTICES, BHSP_3, WAYBEL_0,
      YELLOW_0, WAYBEL_9, PRE_TOPC, WAYBEL28, YELLOW_6, SETFAM_1, TARSKI,
      BOOLE, REALSET1, FUNCT_1, SUBSET_1, QUANTAL1, RELAT_2, YELLOW_9,
      LATTICE3, RELAT_1, YELLOW_2, COLLSP, YELLOW19, MCART_1, CLASSES1,
      ZF_LANG, SUB_METR, FINSET_1, PROB_1, CANTOR_1, PRELAMB, DIRAF, WAYBEL19,
      WAYBEL11, WAYBEL_7, COMPTS_1, URYSOHN1, WAYBEL33, RLVECT_3;
 notation TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, MCART_1, ORDERS_1, FINSET_1,
      RELAT_1, RELSET_1, REALSET1, FUNCT_1, FUNCT_2, SETFAM_1, STRUCT_0,
      PRE_TOPC, TEX_2, LATTICE3, YELLOW_0, CARD_FIL, CLASSES1, CLASSES2,
      CANTOR_1, WAYBEL_0, YELLOW_1, YELLOW_6, YELLOW_2, WAYBEL_3, WAYBEL_7,
      WAYBEL_9, WAYBEL11, COMPTS_1, URYSOHN1, YELLOW_9, WAYBEL19, WAYBEL28,
      YELLOW19;
 constructors WAYBEL_3, TOLER_1, WAYBEL_1, CANTOR_1, WAYBEL_7, TEX_2, REALSET1,
      URYSOHN1, YELLOW_9, WAYBEL19, WAYBEL17, CARD_FIL, CLASSES1, WAYBEL28,
      YELLOW19, GROUP_1, TDLAT_3, CLASSES2, MEMBERED, TOPS_2;
 clusters SUBSET_1, STRUCT_0, WAYBEL_0, WAYBEL_3, RELSET_1, TOPS_1, FUNCT_1,
      LATTICE3, WAYBEL_7, FINSET_1, YELLOW11, YELLOW13, PUA2MSS1, YELLOW_6,
      YELLOW_9, WAYBEL19, CLASSES2, WAYBEL28, WAYBEL29, YELLOW19, MEMBERED,
      RELAT_1, ZFMISC_1;
 requirements BOOLE, SUBSET;


begin

reserve x for set;

definition
 let L be non empty Poset;
 let X be non empty Subset of L;
 let F be Filter of BoolePoset X;
 func lim_inf F -> Element of L equals
:: WAYBEL33:def 1
  "\/"({inf B where B is Subset of L: B in F},L);
end;


theorem :: WAYBEL33:1
   for L1, L2 being complete LATTICE st the RelStr of L1 = the RelStr of L2
 for X1 being non empty Subset of L1
 for X2 being non empty Subset of L2
 for F1 being Filter of BoolePoset X1, F2 being Filter of BoolePoset X2
  st F1 = F2
  holds lim_inf F1 = lim_inf F2;

definition
 let L be non empty TopRelStr;
 attr L is lim-inf means
:: WAYBEL33:def 2

  the topology of L = xi L;
end;


registration
 cluster lim-inf -> TopSpace-like (non empty TopRelStr);
 coherence
proof
   let L be non empty TopRelStr;
   assume A1:L is lim-inf;
   set T = ConvergenceSpace lim_inf-Convergence L;
A2:the topology of L = xi L by A1,Def2 .= the topology of T by WAYBEL28:def 4;
 the carrier of L = the carrier of T by YELLOW_6:def 27;
then A3:the carrier of L in the topology of L by A2,PRE_TOPC:def 1;
A4:(for a being Subset-Family of L st a c= the topology of L
    holds (union a) in the topology of L)
   proof
    let a being Subset-Family of L;
    assume A5:a c= the topology of L;
    reconsider b = a as Subset-Family of T by YELLOW_6:def 27;
      (union b) in the topology of T by A2,A5,PRE_TOPC:def 1;
    hence (union a) in the topology of L by A2;
   end;
     (for a,b being Subset of L st
    a in the topology of L & b in the topology of L
    holds a /\ b in the topology of L) by A2,PRE_TOPC:def 1;
   hence thesis by A3,A4,PRE_TOPC:def 1;
 end;
end;


registration
 cluster trivial -> lim-inf TopLattice;
 coherence
  proof let L be TopLattice;
   assume L is trivial;
   then reconsider L' = L as trivial TopLattice;

   consider x being Point of L';
      the carrier of ConvergenceSpace lim_inf-Convergence L = the carrier of L'
     by YELLOW_6:def 27;
   then reconsider S = ConvergenceSpace lim_inf-Convergence L as
      trivial non empty TopSpace by REALSET1:def 13;
   reconsider y = x as Point of S by YELLOW_6:def 27;
   thus the topology of L = {{},{y}} by YELLOW_9:9
      .= the topology of S by YELLOW_9:9 .= xi L by WAYBEL28:def 4;
  end;
end;


registration
 cluster lim-inf continuous complete TopLattice;
 existence
 proof
   consider L being trivial non empty TopLattice;
   take L; thus thesis;
 end;
end;


theorem :: WAYBEL33:2
 for L1, L2 being non empty 1-sorted
  st the carrier of L1 = the carrier of L2
 for N1 being NetStr over L1
 ex N2 being strict NetStr over L2 st
   the RelStr of N1 = the RelStr of N2 &
   the mapping of N1 = the mapping of N2;

theorem :: WAYBEL33:3
 for L1, L2 being non empty 1-sorted
  st the carrier of L1 = the carrier of L2
 for N1 being NetStr over L1 st N1 in NetUniv L1
 ex N2 being strict net of L2 st N2 in NetUniv L2 &
   the RelStr of N1 = the RelStr of N2 &
   the mapping of N1 = the mapping of N2;

theorem :: WAYBEL33:4
 for L1, L2 being /\-complete up-complete Semilattice
  st the RelStr of L1 = the RelStr of L2
 for N1 being net of L1, N2 being net of L2
  st the RelStr of N1 = the RelStr of N2 &
     the mapping of N1 = the mapping of N2
  holds lim_inf N1 = lim_inf N2;

theorem :: WAYBEL33:5
 for L1, L2 being non empty 1-sorted
  st the carrier of L1 = the carrier of L2
 for N1 being net of L1, N2 being net of L2
  st the RelStr of N1 = the RelStr of N2 &
     the mapping of N1 = the mapping of N2
 for S1 being subnet of N1
 ex S2 being strict subnet of N2 st
   the RelStr of S1 = the RelStr of S2 &
   the mapping of S1 = the mapping of S2;

theorem :: WAYBEL33:6
 for L1, L2 being /\-complete up-complete Semilattice
  st the RelStr of L1 = the RelStr of L2
 for N1 being NetStr over L1, a being set st [N1,a] in lim_inf-Convergence L1
 ex N2 being strict net of L2 st [N2,a] in lim_inf-Convergence L2 &
   the RelStr of N1 = the RelStr of N2 &
   the mapping of N1 = the mapping of N2;

theorem :: WAYBEL33:7
 for L1, L2 being non empty 1-sorted
 for N1 being non empty NetStr over L1
 for N2 being non empty NetStr over L2
  st the RelStr of N1 = the RelStr of N2 &
     the mapping of N1 = the mapping of N2
 for X being set st N1 is_eventually_in X
  holds N2 is_eventually_in X;

theorem :: WAYBEL33:8
   for L1, L2 being /\-complete up-complete Semilattice
  st the RelStr of L1 = the RelStr of L2
  holds ConvergenceSpace lim_inf-Convergence L1
      = ConvergenceSpace lim_inf-Convergence L2;

theorem :: WAYBEL33:9
 for L1, L2 being /\-complete up-complete Semilattice
  st the RelStr of L1 = the RelStr of L2
  holds xi L1 = xi L2;

registration
 let R be /\-complete (non empty reflexive RelStr);
 cluster -> /\-complete TopAugmentation of R;
 coherence
  proof let T be TopAugmentation of R;
   let X be non empty Subset of T;
A1: the RelStr of T = the RelStr of R by YELLOW_9:def 4;
   then reconsider Y = X as non empty Subset of R;
   consider x being Element of R such that
A2: x is_<=_than Y and
A3: for y being Element of R st y is_<=_than Y holds x >= y by WAYBEL_0:def 40;
   reconsider y = x as Element of T by A1;
   take y; thus y is_<=_than X by A1,A2,YELLOW_0:2;
   let z be Element of T;
   reconsider v = z as Element of R by A1;
   assume z is_<=_than X;
    then v is_<=_than Y by A1,YELLOW_0:2;
    then x >= v by A3;
   hence y >= z by A1,YELLOW_0:1;
  end;
end;


registration
 let R be Semilattice;
 cluster -> with_infima TopAugmentation of R;
 coherence
  proof let T be TopAugmentation of R;
   let x,y be Element of T;
A1: the RelStr of T = the RelStr of R by YELLOW_9:def 4;
   then reconsider x' = x, y' = y as Element of R;
   consider z' being Element of R such that
A2: z' <= x' & z' <= y' and
A3: for v' being Element of R st v' <= x' & v' <= y' holds v' <= z'
     by LATTICE3:def 11;
   reconsider z = z' as Element of T by A1;
   take z; thus z <= x & z <= y by A1,A2,YELLOW_0:1;
   let v be Element of T;
   reconsider v' = v as Element of R by A1;
   assume v <= x & v <= y;
    then v' <= x' & v' <= y' by A1,YELLOW_0:1;
    then v' <= z' by A3;
   hence v <= z by A1,YELLOW_0:1;
  end;
end;


registration
 let L be /\-complete up-complete Semilattice;
 cluster strict lim-inf TopAugmentation of L;
 existence
  proof set T = TopRelStr(# the carrier of L, the InternalRel of L, xi L #);
A1: the RelStr of T = the RelStr of L;
   then reconsider T as TopAugmentation of L by YELLOW_9:def 4;
   take T; thus T is strict;
   thus the topology of T = xi T by A1,Th9;
  end;
end;


theorem :: WAYBEL33:10
 for L being /\-complete up-complete Semilattice
 for X being lim-inf TopAugmentation of L
  holds xi L = the topology of X;

definition
 let L be /\-complete up-complete Semilattice;
 func Xi L -> strict TopAugmentation of L means
:: WAYBEL33:def 3

  it is lim-inf;
end;


registration
 let L be /\-complete up-complete Semilattice;
 cluster Xi L -> lim-inf;
 coherence by Def3;
end;


theorem :: WAYBEL33:11
 for L being complete LATTICE, N being net of L holds
  lim_inf N = "\/"({inf (N|i) where i is Element of N: not contradiction}, L);

theorem :: WAYBEL33:12
 for L being complete LATTICE,
     F being proper Filter of BoolePoset [#]L,
     f being Subset of L st f in F
 for i being Element of a_net F st i`2 = f
  holds inf f = inf ((a_net F)|i);

theorem :: WAYBEL33:13
 for L being complete LATTICE,
     F being proper Filter of BoolePoset [#]L
  holds lim_inf F = lim_inf a_net F;

theorem :: WAYBEL33:14
for L being complete LATTICE,
    F being proper Filter of BoolePoset [#]L
holds a_net F in NetUniv L;

theorem :: WAYBEL33:15
 for L being complete LATTICE,
     F being ultra Filter of BoolePoset [#]L
 for p being greater_or_equal_to_id map of a_net F, a_net F
  holds lim_inf F >= inf ((a_net F) * p);

theorem :: WAYBEL33:16
for L being complete LATTICE,
    F being ultra Filter of BoolePoset [#]L
holds for M being subnet of a_net F holds lim_inf F = lim_inf M;

theorem :: WAYBEL33:17
 for L being non empty 1-sorted
 for N being net of L
 for A being set st N is_often_in A
 ex N' being strict subnet of N st
    rng the mapping of N' c= A & N' is SubNetStr of N;

theorem :: WAYBEL33:18    :: III. 3.4. LEMMA, p. 168(?)
for L being complete lim-inf TopLattice, A being non empty Subset of L holds
 A is closed iff
 for F being ultra Filter of BoolePoset [#]L st A in F holds lim_inf F in A;

theorem :: WAYBEL33:19
 for L being non empty reflexive RelStr holds sigma L c= xi L;

theorem :: WAYBEL33:20
 for T1, T2 being non empty TopSpace, B being prebasis of T1
  st B c= the topology of T2 &
     the carrier of T1 in the topology of T2
  holds the topology of T1 c= the topology of T2;

theorem :: WAYBEL33:21
 for L being complete LATTICE holds omega L c= xi L;

theorem :: WAYBEL33:22
 for T1,T2 being TopSpace, T being non empty TopSpace
   st T is TopExtension of T1 & T is TopExtension of T2
 for R being Refinement of T1,T2
   holds T is TopExtension of R;

theorem :: WAYBEL33:23
 for T1 being TopSpace, T2 being TopExtension of T1
 for A being Subset of T1 holds
   (A is open implies A is open Subset of T2) &
   (A is closed implies A is closed Subset of T2);

theorem :: WAYBEL33:24  :: III. 3.5. LEMMA, p. 168(?)
 for L being complete LATTICE holds lambda L c= xi L;

theorem :: WAYBEL33:25  :: 3.6. PROPOSITION (B), p. 169(?)
 for L being complete LATTICE
 for T being lim-inf TopAugmentation of L
 for S being Lawson correct TopAugmentation of L
  holds T is TopExtension of S;

theorem :: WAYBEL33:26
 for L being complete lim-inf TopLattice
 for F being ultra Filter of BoolePoset [#]L
  holds lim_inf F is_a_convergence_point_of F, L;

theorem :: WAYBEL33:27 :: 3.6. PROPOSITION (A), p. 169(?)
   for L being complete lim-inf TopLattice
  holds L is compact being_T1;
