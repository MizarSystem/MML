:: Gauges
::  by Czes\law Byli\'nski
::
:: Received January 22, 1999
:: Copyright (c) 1999 Association of Mizar Users

environ

 vocabulary FINSEQ_1, MATRIX_1, RELAT_1, FINSEQ_4, BOOLE, FUNCT_1, GOBOARD1,
      ABSVALUE, ARYTM_1, RFINSEQ, PRE_TOPC, EUCLID, GOBOARD5, TOPREAL1,
      GOBOARD2, TREES_1, MCART_1, SEQM_3, COMPTS_1, SPPOL_1, PSCOMP_1, ARYTM_3,
      GROUP_1, INCSP_1, JORDAN8;
 notation TARSKI, XBOOLE_0, ZFMISC_1, XREAL_0, REAL_1, NAT_1, STRUCT_0,
      BINARITH, ABSVALUE, RELAT_1, FUNCT_1, CARD_4, FINSEQ_1, FINSEQ_4,
      RFINSEQ, MATRIX_1, PRE_TOPC, COMPTS_1, EUCLID, TOPREAL1, GOBOARD1,
      GOBOARD2, SPPOL_1, PSCOMP_1, GOBOARD5;
 constructors REAL_1, ABSVALUE, FINSEQ_4, CARD_4, RFINSEQ, SEQM_3, BINARITH,
      COMPTS_1, GOBOARD2, SPPOL_1, PSCOMP_1, GOBOARD5;
 clusters STRUCT_0, RELAT_1, RELSET_1, EUCLID, GOBOARD2, FINSEQ_5, SPRECT_1,
      NAT_1, MEMBERED;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;


begin

reserve i,i1,i2,i',i1',j,j1,j2,j',j1',k,l,m,n for Nat;
reserve r,s,r',s' for Real;

reserve D for set,
        f for FinSequence of D,
        G for Matrix of D;

theorem :: JORDAN8:1
    len f >= 2 implies f|2 = <*f/.1,f/.2*>;

theorem :: JORDAN8:2
    k+1 <= len f implies f|(k+1) = (f|k)^<*f/.(k+1)*>;

theorem :: JORDAN8:3
    <*>D is_sequence_on G;

canceled;

theorem :: JORDAN8:5
   for D being non empty set, f being FinSequence of D, G being Matrix of D
   st f is_sequence_on G
  holds f/^m is_sequence_on G;

theorem :: JORDAN8:6
  1 <= k & k+1 <= len f & f is_sequence_on G
   implies ex i1,j1,i2,j2 being Nat st
     [i1,j1] in Indices G & f/.k = G*(i1,j1) &
     [i2,j2] in Indices G & f/.(k+1) = G*(i2,j2) &
     (i1 = i2 & j1+1 = j2 or i1+1 = i2 & j1 = j2 or
      i1 = i2+1 & j1 = j2 or i1 = i2 & j1 = j2+1);

reserve G for Go-board,
        p for Point of TOP-REAL 2;

theorem :: JORDAN8:7
   for f being non empty FinSequence of TOP-REAL 2 st f is_sequence_on G
   holds f is standard special;

theorem :: JORDAN8:8
   for f being non empty FinSequence of TOP-REAL 2 st
    len f >= 2 & f is_sequence_on G
   holds f is non constant;

theorem :: JORDAN8:9
   for f being non empty FinSequence of TOP-REAL 2
   st f is_sequence_on G &
     (ex i,j st [i,j] in Indices G & p = G*(i,j)) &
     (for i1,j1,i2,j2
      st [i1,j1] in Indices G & [i2,j2] in Indices G &
         f/.len f = G*(i1,j1) & p = G*(i2,j2)
      holds abs(i2-i1)+abs(j2-j1) = 1)
  holds f^<*p*> is_sequence_on G;

theorem :: JORDAN8:10
    i+k < len G & 1 <= j & j < width G & cell(G,i,j) meets cell(G,i+k,j)
    implies k <= 1;

theorem :: JORDAN8:11
 for C being non empty compact Subset of TOP-REAL 2
   holds C is vertical iff E-bound C <= W-bound C;

theorem :: JORDAN8:12
 for C being non empty compact Subset of TOP-REAL 2
   holds C is horizontal iff N-bound C <= S-bound C;

definition let C be Subset of TOP-REAL 2, n be Nat;

 func Gauge (C,n) -> Matrix of TOP-REAL 2 means
:: JORDAN8:def 1
 len it = 2|^n + 3 & len it = width it &
   for i,j st [i,j] in Indices it holds
      it*(i,j) = |[(W-bound C)+(((E-bound C)-(W-bound C))/(2|^n))*(i-2),
                   (S-bound C)+(((N-bound C)-(S-bound C))/(2|^n))*(j-2)]|;
end;


registration
 let C be non empty Subset of TOP-REAL 2, n be Nat;
 cluster Gauge (C,n) -> non empty-yielding X_equal-in-line Y_equal-in-column;
  coherence
 proof
  set M = Gauge (C,n);
   len M = 2|^n + 3 & len M = width M by Def1;
  hence M is non empty-yielding by GOBOARD1:def 5;
A1:  Indices M = [:dom M,Seg width M:] by MATRIX_1:def 5;
  thus M is X_equal-in-line
   proof
    let i; assume
A2:   i in dom M;
    set l= Line(M,i), f = X_axis(l);
    let j1,j2 such that
A3:   j1 in dom f and
A4:   j2 in dom f;
      len l = width M by MATRIX_1:def 8;
then A5:   dom l = Seg width M by FINSEQ_1:def 3;
A6:   dom f = dom l by SPRECT_2:19;
then A7:   l/.j1 = l.j1 by A3,FINSEQ_4:def 4
             .= M*(i,j1) by A3,A5,A6,MATRIX_1:def 8;
A8:   [i,j1] in Indices M by A1,A2,A3,A5,A6,ZFMISC_1:106;
A9:   l/.j2 = l.j2 by A4,A6,FINSEQ_4:def 4
             .= M*(i,j2) by A4,A5,A6,MATRIX_1:def 8;
A10:   [i,j2] in Indices M by A1,A2,A4,A5,A6,ZFMISC_1:106;
    set x = (W-bound C)+(((E-bound C)-(W-bound C))/(2|^n))*(i-2);
    set d = ((N-bound C)-(S-bound C))/(2|^n);
   thus f.j1 = (l/.j1)`1 by A3,GOBOARD1:def 3
            .= |[x,(S-bound C)+d*(j1-2)]|`1 by A7,A8,Def1
            .= x by EUCLID:56
            .= |[x,(S-bound C)+d*(j2-2)]|`1 by EUCLID:56
            .= (l/.j2)`1 by A9,A10,Def1
            .= f.j2 by A4,GOBOARD1:def 3;
   end;
  thus M is Y_equal-in-column
   proof
    let j; assume
A11:   j in Seg width M;
    set c = Col(M,j), f = Y_axis(c);
    let i1,i2 such that
A12:   i1 in dom f and
A13:   i2 in dom f;
      len c = len M by MATRIX_1:def 9;
then A14:   dom c = dom M by FINSEQ_3:31;
A15:   dom f = dom c by SPRECT_2:20;
then A16:   c/.i1 = c.i1 by A12,FINSEQ_4:def 4
             .= M*(i1,j) by A12,A14,A15,MATRIX_1:def 9;
A17:   [i1,j] in Indices M by A1,A11,A12,A14,A15,ZFMISC_1:106;
A18:   c/.i2 = c.i2 by A13,A15,FINSEQ_4:def 4
             .= M*(i2,j) by A13,A14,A15,MATRIX_1:def 9;
A19:   [i2,j] in Indices M by A1,A11,A13,A14,A15,ZFMISC_1:106;
    set y = (S-bound C)+(((N-bound C)-(S-bound C))/(2|^n))*(j-2);
    set d = ((E-bound C)-(W-bound C))/(2|^n);
    thus f.i1 = (c/.i1)`2 by A12,GOBOARD1:def 4
             .= |[(W-bound C)+d*(i1-2),y]|`2 by A16,A17,Def1
             .= y by EUCLID:56
             .= |[(W-bound C)+d*(i2-2),y]|`2 by EUCLID:56
             .= (c/.i2)`2 by A18,A19,Def1
             .= f.i2 by A13,GOBOARD1:def 4;
   end;
 end;
end;


registration
  let C be compact non vertical non horizontal non empty Subset of TOP-REAL 2,
      n;
 cluster Gauge (C,n) -> Y_increasing-in-line X_increasing-in-column;
  coherence
 proof
  set M = Gauge (C,n);
A1:  Indices M = [:dom M,Seg width M:] by MATRIX_1:def 5;
  thus M is Y_increasing-in-line
   proof
    let i; assume
A2:    i in dom M;
    set l = Line(M,i), f = Y_axis(l);
    let j1,j2 such that
A3:   j1 in dom f and
A4:   j2 in dom f and
A5:   j1 < j2;
      len l = width M by MATRIX_1:def 8;
then A6:   dom l = Seg width M by FINSEQ_1:def 3;
A7:   dom f = dom l by SPRECT_2:20;
then A8:   l/.j1 = l.j1 by A3,FINSEQ_4:def 4
             .= M*(i,j1) by A3,A6,A7,MATRIX_1:def 8;
A9:   [i,j1] in Indices M by A1,A2,A3,A6,A7,ZFMISC_1:106;
A10:   l/.j2 = l.j2 by A4,A7,FINSEQ_4:def 4
             .= M*(i,j2) by A4,A6,A7,MATRIX_1:def 8;
A11:   [i,j2] in Indices M by A1,A2,A4,A6,A7,ZFMISC_1:106;
    set x = (W-bound C)+(((E-bound C)-(W-bound C))/(2|^n))*(i-2);
    set d = ((N-bound C)-(S-bound C))/(2|^n);
A12:   f.j1 = (l/.j1)`2 by A3,GOBOARD1:def 4
          .= |[x,(S-bound C)+d*(j1-2)]|`2 by A8,A9,Def1
          .= (S-bound C)+d*(j1-2) by EUCLID:56;
A13:   f.j2 = (l/.j2)`2 by A4,GOBOARD1:def 4
          .= |[x,(S-bound C)+d*(j2-2)]|`2 by A10,A11,Def1
          .= (S-bound C)+d*(j2-2) by EUCLID:56;
      N-bound C > S-bound C by Th12;
    then (N-bound C)-(S-bound C) > 0 & (2|^n) > 0 by HEINE:5,SQUARE_1:11;
then A14:    d > 0 by REAL_2:127;
      j1-2 < j2 -2 by A5,REAL_1:54;
    then d*(j1-2) < d*(j2-2) by A14,REAL_1:70;
    hence f.j1 < f.j2 by A12,A13,REAL_1:67;
   end;
  let j; assume
A15:   j in Seg width M;
  set c = Col(M,j), f = X_axis(c);
  let i1,i2 such that
A16:   i1 in dom f and
A17:   i2 in dom f and
A18:   i1 < i2;
     len c = len M by MATRIX_1:def 9;
then A19:   dom c = dom M by FINSEQ_3:31;
A20:   dom f = dom c by SPRECT_2:19;
then A21:   c/.i1 = c.i1 by A16,FINSEQ_4:def 4
             .= M*(i1,j) by A16,A19,A20,MATRIX_1:def 9;
A22:   [i1,j] in Indices M by A1,A15,A16,A19,A20,ZFMISC_1:106;
A23:   c/.i2 = c.i2 by A17,A20,FINSEQ_4:def 4
             .= M*(i2,j) by A17,A19,A20,MATRIX_1:def 9;
A24:   [i2,j] in Indices M by A1,A15,A17,A19,A20,ZFMISC_1:106;
    set y = (S-bound C)+(((N-bound C)-(S-bound C))/(2|^n))*(j-2);
    set d = ((E-bound C)-(W-bound C))/(2|^n);
A25:   f.i1 = (c/.i1)`1 by A16,GOBOARD1:def 3
          .= |[(W-bound C)+d*(i1-2),y]|`1 by A21,A22,Def1
          .= (W-bound C)+d*(i1-2) by EUCLID:56;
A26:   f.i2 = (c/.i2)`1 by A17,GOBOARD1:def 3
          .= |[(W-bound C)+d*(i2-2),y]|`1 by A23,A24,Def1
          .= (W-bound C)+d*(i2-2) by EUCLID:56;
      E-bound C > W-bound C by Th11;
    then (E-bound C)-(W-bound C) > 0 & (2|^n) > 0 by HEINE:5,SQUARE_1:11;
then A27:   d > 0 by REAL_2:127;
      i1-2 < i2 -2 by A18,REAL_1:54;
    then d*(i1-2) < d*(i2-2) by A27,REAL_1:70;
    hence f.i1 < f.i2 by A25,A26,REAL_1:67;
 end;
end;


reserve T for non empty Subset of TOP-REAL 2;

theorem :: JORDAN8:13
 len Gauge(T,n) >= 4;

theorem :: JORDAN8:14
    1 <= j & j <= len Gauge(T,n) implies Gauge(T,n)*(2,j)`1 = W-bound T;

theorem :: JORDAN8:15
    1 <= j & j <= len Gauge(T,n) implies
    Gauge(T,n)*(len Gauge(T,n)-'1,j)`1 = E-bound T;

theorem :: JORDAN8:16
    1 <= i & i <= len Gauge(T,n) implies Gauge(T,n)*(i,2)`2 = S-bound T;

theorem :: JORDAN8:17
    1 <= i & i <= len Gauge(T,n) implies
    Gauge(T,n)*(i,len Gauge(T,n)-'1)`2 = N-bound T;

reserve C for
   compact non vertical non horizontal non empty Subset of TOP-REAL 2;

theorem :: JORDAN8:18
    i <= len Gauge(C,n) implies cell(Gauge(C,n),i,len Gauge(C,n)) misses C;

theorem :: JORDAN8:19
    j <= len Gauge(C,n) implies cell(Gauge(C,n),len Gauge(C,n),j) misses C;

theorem :: JORDAN8:20
    i <= len Gauge(C,n) implies cell(Gauge(C,n),i,0) misses C;

theorem :: JORDAN8:21
    j <= len Gauge(C,n) implies cell(Gauge(C,n),0,j) misses C;
