:: Algebraic Lattices
::  by Robert Milewski
::
:: Received December 1, 1996
:: Copyright (c) 1996 Association of Mizar Users

environ

 vocabulary RELAT_2, ORDERS_1, CAT_1, YELLOW_0, COMPTS_1, WELLORD1, LATTICES,
      BHSP_3, WAYBEL_0, WAYBEL_3, WAYBEL_6, FILTER_0, LATTICE3, ORDINAL2,
      BOOLE, QUANTAL1, FINSET_1, REALSET1, WAYBEL_4, COHSP_1, WAYBEL_7,
      WAYBEL_1, PRE_TOPC, GROUP_6, RELAT_1, SUBSET_1, BINOP_1, SEQM_3, FUNCT_1,
      YELLOW_1, FILTER_1, SETFAM_1, TARSKI, WAYBEL_8;
 notation TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, SETFAM_1, FINSET_1, RELAT_1,
      REALSET1, STRUCT_0, ORDERS_1, TOLER_1, FUNCT_1, LATTICE3, PRE_TOPC,
      QUANTAL1, YELLOW_0, YELLOW_1, YELLOW_2, WAYBEL_0, WAYBEL_1, WAYBEL_3,
      WAYBEL_4, WAYBEL_6, WAYBEL_7, GRCAT_1;
 constructors TOLER_1, ORDERS_3, QUANTAL1, YELLOW_3, WAYBEL_1, WAYBEL_3,
      WAYBEL_4, WAYBEL_6, WAYBEL_7, GRCAT_1;
 clusters STRUCT_0, RELSET_1, FINSET_1, LATTICE3, YELLOW_0, YELLOW_2, WAYBEL_0,
      WAYBEL_1, WAYBEL_2, WAYBEL_3, WAYBEL_4, WAYBEL_6, SUBSET_1, XBOOLE_0;
 requirements SUBSET, BOOLE;


begin :: The Subset of All Compact Elements

  definition  :: DEFINITION 4.1
   let L be non empty reflexive RelStr;
   func CompactSublatt L -> strict full SubRelStr of L means
:: WAYBEL_8:def 1
    for x be Element of L holds x in the carrier of it iff x is compact;
  end;


  registration
   let L be lower-bounded non empty reflexive antisymmetric RelStr;
   cluster CompactSublatt L -> non empty;
   coherence
   proof
      Bottom L is compact by WAYBEL_3:15;
    then the carrier of CompactSublatt L is non empty by Def1;
    hence CompactSublatt L is non empty by STRUCT_0:def 1;
   end;
  end;


  theorem :: WAYBEL_8:1
     for L be complete LATTICE
   for x,y,k be Element of L holds
    x <= k & k <= y & k in the carrier of CompactSublatt L implies x << y;

  theorem :: WAYBEL_8:2  :: REMARK 4.2
     for L be complete LATTICE
   for x be Element of L holds
    uparrow x is Open Filter of L iff x is compact;

  theorem :: WAYBEL_8:3  :: REMARK 4.3
     for L be lower-bounded with_suprema non empty Poset holds
    CompactSublatt L is join-inheriting &
     Bottom L in the carrier of CompactSublatt L;

  definition
   let L be non empty reflexive RelStr;
   let x be Element of L;
   func compactbelow x -> Subset of L equals
:: WAYBEL_8:def 2
     {y where y is Element of L: x >= y & y is compact};
  end;


  theorem :: WAYBEL_8:4
   for L be non empty reflexive RelStr
   for x,y be Element of L holds
    y in compactbelow x iff x >= y & y is compact;

  theorem :: WAYBEL_8:5
   for L be non empty reflexive RelStr
   for x be Element of L holds
    compactbelow x = downarrow x /\ the carrier of CompactSublatt L;

  theorem :: WAYBEL_8:6
   for L be non empty reflexive transitive RelStr
   for x be Element of L holds
    compactbelow x c= waybelow x;

  registration
   let L be non empty lower-bounded reflexive antisymmetric RelStr;
   let x be Element of L;
   cluster compactbelow x -> non empty;
   coherence
   proof
    A1: x >= Bottom L by YELLOW_0:44;
      Bottom L is compact by WAYBEL_3:15;
    then Bottom L in {y where y is Element of L: x >= y & y is compact} by A1;
    hence compactbelow x is non empty by Def2;
   end;
  end;


begin :: Algebraic Lattices

  definition
   let L be non empty reflexive RelStr;
   attr L is satisfying_axiom_K means
:: WAYBEL_8:def 3
    for x be Element of L holds x = sup compactbelow x;
  end;


  definition :: DEFINITION 4.4
   let L be non empty reflexive RelStr;
   attr L is algebraic means
:: WAYBEL_8:def 4
    (for x being Element of L holds compactbelow x is non empty directed) &
    L is up-complete satisfying_axiom_K;
  end;


  theorem :: WAYBEL_8:7   :: PROPOSITION 4.5
   for L be LATTICE holds
    L is algebraic iff
     ( L is continuous & for x,y be Element of L st x << y
        ex k be Element of L st k in the carrier of CompactSublatt L &
         x <= k & k <= y );

  registration
   cluster algebraic -> continuous LATTICE;
   coherence by Th7;
  end;


  registration
   cluster algebraic ->
                 up-complete satisfying_axiom_K (non empty reflexive RelStr);
   coherence by Def4;
  end;


  registration
   let L be non empty with_suprema Poset;
   cluster CompactSublatt L -> join-inheriting;
   coherence
   proof
      now let x,y be Element of L;
     assume that
      A1: x in the carrier of CompactSublatt L and
      A2: y in the carrier of CompactSublatt L and
      A3: ex_sup_of {x,y},L;
     A4: x <= x "\/" y & y <= x "\/" y by A3,YELLOW_0:18;
       x is compact & y is compact by A1,A2,Def1;
     then x << x & y << y by WAYBEL_3:def 2;
     then x << x "\/" y & y << x "\/" y by A4,WAYBEL_3:2;
     then x "\/" y << x "\/" y by WAYBEL_3:3;
     then x "\/" y is compact by WAYBEL_3:def 2;
     then sup {x,y} is compact by YELLOW_0:41;
     hence sup {x,y} in the carrier of CompactSublatt L by Def1;
    end;
    hence CompactSublatt L is join-inheriting by YELLOW_0:def 17;
   end;
  end;


  definition
   let L be non empty reflexive RelStr;
   attr L is arithmetic means
:: WAYBEL_8:def 5
    L is algebraic & CompactSublatt L is meet-inheriting;
  end;


begin :: Arithmetic Lattices

  registration
   cluster arithmetic -> algebraic LATTICE;
   coherence by Def5;
  end;


  registration
   cluster trivial -> arithmetic LATTICE;
   coherence
   proof
    let L be LATTICE;
    assume L is trivial;
    then reconsider L' = L as trivial LATTICE;
      for x,y be Element of L' st x << y
     ex k be Element of L' st k in the carrier of CompactSublatt L' &
      x <= k & k <= y
    proof
     let x,y be Element of L';
     A1: x = y by REALSET1:def 20;
     assume A2: x << y;
     take k = x;
       k is compact by A1,A2,WAYBEL_3:def 2;
     hence k in the carrier of CompactSublatt L' by Def1;
     thus x <= k & k <= y by REALSET1:def 20;
    end;
    then A3: L' is algebraic by Th7;
      for z,v be Element of L' st
     (z in the carrier of CompactSublatt L' &
      v in the carrier of CompactSublatt L' &
      ex_inf_of {z,v},L' ) holds inf {z,v} in the carrier of CompactSublatt L'
                                                         by REALSET1:def 20;
    then CompactSublatt L' is meet-inheriting by YELLOW_0:def 16;
    hence thesis by A3,Def5;
   end;
  end;


  registration
   cluster trivial strict LATTICE;
   existence
   proof
    consider B be strict trivial (non empty reflexive RelStr);
    take B;
    thus thesis;
   end;
  end;


  theorem :: WAYBEL_8:8
   for L1,L2 be non empty reflexive antisymmetric RelStr
      st the RelStr of L1 = the RelStr of L2 & L1 is up-complete
   for x1,y1 be Element of L1
   for x2,y2 be Element of L2 st x1 = x2 & y1 = y2 & x1 << y1 holds
    x2 << y2;

  theorem :: WAYBEL_8:9
   for L1,L2 be non empty reflexive antisymmetric RelStr
      st the RelStr of L1 = the RelStr of L2 & L1 is up-complete
   for x be Element of L1
   for y be Element of L2 st x = y & x is compact holds
    y is compact;

  theorem :: WAYBEL_8:10
   for L1,L2 be up-complete (non empty reflexive antisymmetric RelStr)
      st the RelStr of L1 = the RelStr of L2
   for x be Element of L1
   for y be Element of L2 st x = y holds
    compactbelow x = compactbelow y;

  theorem :: WAYBEL_8:11
   for L1,L2 be RelStr
    st the RelStr of L1 = the RelStr of L2 & L1 is non empty
    holds L2 is non empty;

  theorem :: WAYBEL_8:12
   for L1,L2 be non empty RelStr
    st the RelStr of L1 = the RelStr of L2 & L1 is reflexive
    holds L2 is reflexive;

  theorem :: WAYBEL_8:13
   for L1,L2 be RelStr
    st the RelStr of L1 = the RelStr of L2 & L1 is transitive
    holds L2 is transitive;

  theorem :: WAYBEL_8:14
   for L1,L2 be RelStr
    st the RelStr of L1 = the RelStr of L2 & L1 is antisymmetric
    holds L2 is antisymmetric;

  theorem :: WAYBEL_8:15
   for L1,L2 be non empty reflexive RelStr
    st the RelStr of L1 = the RelStr of L2 & L1 is up-complete
    holds L2 is up-complete;

  theorem :: WAYBEL_8:16
   for L1,L2 be up-complete (non empty reflexive antisymmetric RelStr)
    st the RelStr of L1 = the RelStr of L2 & L1 is satisfying_axiom_K &
     for x be Element of L1 holds compactbelow x is non empty directed
    holds L2 is satisfying_axiom_K;

  theorem :: WAYBEL_8:17
   for L1,L2 be non empty reflexive antisymmetric RelStr
    st the RelStr of L1 = the RelStr of L2 & L1 is algebraic
    holds L2 is algebraic;

  theorem :: WAYBEL_8:18
   for L1,L2 be LATTICE
    st the RelStr of L1 = the RelStr of L2 & L1 is arithmetic
    holds L2 is arithmetic;

  registration
   let L be non empty RelStr;
   cluster the RelStr of L -> non empty;
   coherence by Th11;
  end;


  registration
   let L be non empty reflexive RelStr;
   cluster the RelStr of L -> reflexive;
   coherence by Th12;
  end;


  registration
   let L be transitive RelStr;
   cluster the RelStr of L -> transitive;
   coherence by Th13;
  end;


  registration
   let L be antisymmetric RelStr;
   cluster the RelStr of L -> antisymmetric;
   coherence by Th14;
  end;


  registration
   let L be with_infima RelStr;
   cluster the RelStr of L -> with_infima;
   coherence by YELLOW_7:14;
  end;


  registration
   let L be with_suprema RelStr;
   cluster the RelStr of L -> with_suprema;
   coherence by YELLOW_7:15;
  end;


  registration
   let L be up-complete (non empty reflexive RelStr);
   cluster the RelStr of L -> up-complete;
   coherence by Th15;
  end;


  registration
   let L be algebraic (non empty reflexive antisymmetric RelStr);
   cluster the RelStr of L -> algebraic;
   coherence by Th17;
  end;


  registration
   let L be arithmetic LATTICE;
   cluster the RelStr of L -> arithmetic;
   coherence by Th18;
  end;


  theorem :: WAYBEL_8:19
   for L be non empty transitive RelStr
   for S be non empty full SubRelStr of L
   for X be Subset of S st ex_sup_of X,L & "\/"(X,L) is Element of S holds
    ex_sup_of X,S & sup X = "\/"(X,L);

  theorem :: WAYBEL_8:20
     for L be non empty transitive RelStr
   for S be non empty full SubRelStr of L
   for X be Subset of S st ex_inf_of X,L & "/\"(X,L) is Element of S holds
    ex_inf_of X,S & inf X = "/\"(X,L);

  theorem :: WAYBEL_8:21  :: PROPOSITION 4.7 a)
     for L be algebraic LATTICE holds
    L is arithmetic iff CompactSublatt L is LATTICE;

  theorem :: WAYBEL_8:22  :: PROPOSITION 4.7 b)
   for L be algebraic lower-bounded LATTICE holds
    L is arithmetic iff L-waybelow is multiplicative;

  theorem :: WAYBEL_8:23  :: COROLLARY 4.8.a)
     for L be arithmetic lower-bounded LATTICE,
       p be Element of L holds
    p is pseudoprime implies p is prime;

  theorem :: WAYBEL_8:24  :: COROLLARY 4.8.b)
     for L be algebraic distributive lower-bounded LATTICE
    st for p being Element of L st p is pseudoprime holds p is prime
    holds L is arithmetic;

  registration
   let L be algebraic LATTICE;
   let c be closure map of L,L;
   cluster non empty directed Subset of Image c;
   existence
   proof
    consider x be Element of Image c;
    take downarrow x;
    thus thesis;
   end;
  end;


  theorem :: WAYBEL_8:25
   for L be algebraic LATTICE
   for c be closure map of L,L st c is directed-sups-preserving holds
    c.:([#]CompactSublatt L) c= [#]CompactSublatt Image c;

  theorem :: WAYBEL_8:26  :: PROPOSITION 4.9.(i)
     for L be algebraic lower-bounded LATTICE
   for c be closure map of L,L st c is directed-sups-preserving holds
    Image c is algebraic LATTICE;

  theorem :: WAYBEL_8:27  :: PROPOSITION 4.9.(ii)
     for L be algebraic lower-bounded LATTICE,
       c be closure map of L,L st c is directed-sups-preserving holds
    c.:([#]CompactSublatt L) = [#]CompactSublatt Image c;

begin :: Boolean Posets as Algebraic Lattices

  theorem :: WAYBEL_8:28
   for X,x be set holds
    x is Element of BoolePoset X iff x c= X;

  theorem :: WAYBEL_8:29
   for X be set
   for x,y be Element of BoolePoset X holds
    x << y
      iff
    for Y be Subset-Family of X st y c= union Y
    ex Z be finite Subset of Y st x c= union Z;

  theorem :: WAYBEL_8:30
   for X be set
   for x be Element of BoolePoset X holds
    x is finite iff x is compact;

  theorem :: WAYBEL_8:31
   for X be set
   for x be Element of BoolePoset X holds
    compactbelow x = {y where y is finite Subset of x : not contradiction};

  theorem :: WAYBEL_8:32  :: EXAMPLES 4.11.(1a)
     for X be set
   for F be Subset of X holds
    F in the carrier of CompactSublatt BoolePoset X iff F is finite;

  registration
   let X be set;
   let x be Element of BoolePoset X;
   cluster compactbelow x -> lower directed;
   coherence
   proof
      now let a,b be set;
     assume A1: a c= b & b in compactbelow x;
     then b in {y where y is finite Subset of x : not contradiction} by Th31;
     then consider b1 be finite Subset of x such that
      A2: b = b1;
       a is finite Subset of x by A1,A2,FINSET_1:13,XBOOLE_1:1;
     then a in {y where y is finite Subset of x : not contradiction};
     hence a in compactbelow x by Th31;
    end;
    hence A3: compactbelow x is lower by WAYBEL_7:10;
      now let a,b be set;
     assume a in compactbelow x & b in compactbelow x;
     then A4: a in {y where y is finite Subset of x : not contradiction} &
      b in {y where y is finite Subset of x : not contradiction} by Th31;
     then consider a1 be finite Subset of x such that
      A5: a = a1;
     consider b1 be finite Subset of x such that
      A6: b = b1 by A4;
       a \/ b is finite Subset of x by A5,A6,FINSET_1:14,XBOOLE_1:8;
     then a \/ b in {y where y is finite Subset of x : not contradiction};
     hence a \/ b in compactbelow x by Th31;
    end;
    hence compactbelow x is directed by A3,WAYBEL_7:12;
   end;
  end;


  theorem :: WAYBEL_8:33  :: EXAMPLES 4.11.(1b)
   for X be set holds BoolePoset X is algebraic;

  registration
   let X be set;
   cluster BoolePoset X -> algebraic;
   coherence by Th33;
  end;


