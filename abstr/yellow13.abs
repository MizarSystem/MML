:: Introduction to Meet-Continuous Topological Lattices
::  by Artur Korni{\l}owicz
::
:: Received November 3, 1998
:: Copyright (c) 1998 Association of Mizar Users

environ

 vocabulary FINSET_1, REALSET1, ORDERS_1, WAYBEL_9, URYSOHN1, PRE_TOPC, BOOLE,
      COMPTS_1, SUBSET_1, NATTRA_1, TSP_1, RELAT_2, WAYBEL_0, RELAT_1, SEQM_3,
      FUNCT_1, YELLOW_2, YELLOW_1, FILTER_2, ORDINAL2, YELLOW_0, WELLORD1,
      WAYBEL_1, SGRAPH1, BHSP_3, LATTICES, LATTICE3, TDLAT_3, CANTOR_1,
      SETFAM_1, RLVECT_3, TOPS_1, CONNSP_2, PRELAMB, TARSKI, WAYBEL_2,
      YELLOW13;
 notation TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, RELAT_1, FUNCT_1, FUNCT_2,
      FINSET_1, SETFAM_1, STRUCT_0, ORDERS_1, PRE_TOPC, TOPS_1, GROUP_1,
      COMPTS_1, TDLAT_2, TDLAT_3, TSP_1, LATTICE3, CANTOR_1, REALSET1,
      CONNSP_2, BORSUK_1, URYSOHN1, YELLOW_0, WAYBEL_0, YELLOW_1, ORDERS_3,
      WAYBEL_1, YELLOW_2, YELLOW_3, WAYBEL_2, YELLOW_8, WAYBEL_9;
 constructors CANTOR_1, TOPS_1, TDLAT_2, TDLAT_3, TSP_1, URYSOHN1, YELLOW_8,
      WAYBEL_1, WAYBEL_3, YELLOW_9, ORDERS_3, GROUP_1, LATTICE3, XCMPLX_0,
      MEMBERED;
 clusters STRUCT_0, TOPS_1, FINSET_1, REALSET1, TDLAT_3, TEX_1, YELLOW_0,
      YELLOW_1, YELLOW_2, WAYBEL_0, WAYBEL_1, WAYBEL_3, YELLOW_8, YELLOW_9,
      WAYBEL11, WAYBEL12, YELLOW11, YELLOW12, BORSUK_1, RELSET_1, SUBSET_1,
      RLVECT_2, XREAL_0, MEMBERED, ZFMISC_1;
 requirements SUBSET, BOOLE;


begin  :: Preliminaries

registration let S be finite 1-sorted;
 cluster the carrier of S -> finite;
coherence by GROUP_1:def 13;
end;


registration let S be trivial 1-sorted;
 cluster the carrier of S -> trivial;
coherence by REALSET1:def 13;
end;


registration
 cluster trivial -> finite set;
coherence
  proof
    let S be set such that
A1:   S is trivial;
    per cases by A1,REALSET1:def 12;
    suppose S is empty;
    then reconsider T = S as empty set;
      T is finite;
    hence thesis;
    end;
    suppose ex x being set st S = {x};
    hence thesis;
  end;
  end;
end;


registration
 cluster trivial -> finite 1-sorted;
coherence
  proof
    let S be 1-sorted such that
A1:   S is trivial;
    per cases;
    suppose S is empty;
    then reconsider R = S as empty 1-sorted;
      the carrier of R is finite;
    hence the carrier of S is finite;
    end;
    suppose S is non empty;
    then reconsider R = S as non empty trivial 1-sorted by A1;
      the carrier of R is finite;
    hence the carrier of S is finite;
  end;
  end;
end;


registration
 cluster non trivial -> non empty 1-sorted;
coherence
  proof
    let S be 1-sorted;
    assume S is non trivial;
    then reconsider A = the carrier of S as non trivial set by REALSET1:def 13;
      A is non empty;
    hence thesis by STRUCT_0:def 1;
  end;
end;


registration
 cluster strict non empty trivial 1-sorted;
existence
  proof
    consider A being strict non empty trivial 1-sorted;
    take A;
    thus thesis;
  end;

 cluster strict non empty trivial RelStr;
existence
  proof
    consider A being strict non empty trivial RelStr;
    take A;
    thus thesis;
  end;

 cluster strict non empty trivial TopRelStr;
existence
  proof
    consider A being strict non empty trivial TopRelStr;
    take A;
    thus thesis;
  end;
end;


theorem :: YELLOW13:1
 for T being being_T1 (non empty TopSpace),
     A being finite Subset of T holds A is closed;

registration let T be being_T1 (non empty TopSpace);
 cluster finite -> closed Subset of T;
coherence by Th1;
end;


registration let T be compact TopStruct;
 cluster [#]T -> compact;
coherence by COMPTS_1:10;
end;


registration
 cluster strict non empty trivial TopSpace;
existence
  proof
    consider T being strict non empty trivial TopSpace;
    take T;
    thus thesis;
  end;
end;


registration
 cluster finite being_T1 -> discrete (non empty TopSpace);
coherence
  proof
    let T be non empty TopSpace such that
A1:   T is finite being_T1;
      now
      let A be Subset of T;
      reconsider W = T as finite (non empty TopSpace) by A1;
      reconsider K = A as Subset of W;
        K is closed by A1,Th1;
      hence A is closed;
    end;
    hence thesis by TDLAT_3:18;
  end;
end;


registration
 cluster finite -> compact TopSpace;
coherence
  proof
    let T be TopSpace;
    assume T is finite;
    then the carrier of T is finite by GROUP_1:def 13;
    hence thesis by HEINE:10;
  end;
end;


theorem :: YELLOW13:2
 for T being discrete non empty TopSpace holds T is_T4;

theorem :: YELLOW13:3
 for T being discrete non empty TopSpace holds T is_T3;

theorem :: YELLOW13:4
 for T being discrete non empty TopSpace holds T is_T2;

theorem :: YELLOW13:5
 for T being discrete non empty TopSpace holds T is_T1;

registration
 cluster discrete non empty -> being_T4 being_T3 being_T2 being_T1 TopSpace;
coherence by Th2,Th3,Th4,Th5;
end;


registration
 cluster being_T4 being_T1 -> being_T3 (non empty TopSpace);
coherence
  proof
    let T be non empty TopSpace such that
A1:   T is being_T4 being_T1;
    let p be Point of T,
        P be Subset of T such that
A2:   P <> {} & P is closed & not p in P;
A3: {p} is closed by A1,URYSOHN1:27;
      P /\ {p} c= {}
    proof
      let a be set;
      assume a in P /\ {p};
then A4:   a in P & a in {p} by XBOOLE_0:def 3;
      assume not a in {};
      thus contradiction by A2,A4,TARSKI:def 1;
    end;
    then P /\ {p} = {} by XBOOLE_1:3;
    then P misses {p} by XBOOLE_0:def 7;
    then consider R, Q being Subset of T such that
A5:   R is open & Q is open & {p} c= R & P c= Q & R misses Q
       by A1,A2,A3,COMPTS_1:def 6;
    take R, Q;
      p in {p} by TARSKI:def 1;
    hence thesis by A5;
  end;
end;


registration
 cluster being_T3 being_T1 -> being_T2 (non empty TopSpace);
coherence
  proof
    let T be non empty TopSpace such that
A1:   T is being_T3 being_T1;
    let p, q be Point of T;
    assume p <> q;
then A2: not p in {q} by TARSKI:def 1;
      {q} is closed by A1,URYSOHN1:27;
    then consider W, V being Subset of T such that
A3:   W is open & V is open & p in W & {q} c= V & W misses V
       by A1,A2,COMPTS_1:def 5;
    take W, V;
      q in {q} by TARSKI:def 1;
    hence thesis by A3;
  end;
end;


registration
 cluster being_T2 -> being_T1 TopSpace;
coherence
  proof
    let T be TopSpace such that
A1:   for p, q being Point of T st not p = q
       ex W, V being Subset of T st W is open & V is open &
        p in W & q in V & W misses V;
    let p, q be Point of T;
    assume p <> q;
    then consider W, V being Subset of T such that
A2:   W is open & V is open & p in W & q in V & W misses V by A1;
    take W, V;
    thus thesis by A2,XBOOLE_0:3;
  end;
end;


registration
 cluster being_T1 -> T_0 TopSpace;
coherence
  proof
    let T be TopSpace such that
A1:  for p, q being Point of T st not p = q
      ex W, V being Subset of T st W is open & V is open &
       p in W & not q in W & q in V & not p in V;
      for p, q being Point of T st p <> q holds
     (ex V being Subset of T st V is open & p in V & not q in V)
     or
     (ex W being Subset of T st W is open & not p in W & q in W)
    proof
      let p, q be Point of T;
      assume p <> q;
      then ex W, V being Subset of T st W is open & V is open &
       p in W & not q in W & q in V & not p in V by A1;
      hence thesis;
    end;
    hence thesis by TSP_1:def 3;
  end;
end;


theorem :: YELLOW13:6
 for S being reflexive RelStr, T being reflexive transitive RelStr,
     f being map of S, T, X being Subset of S
  holds downarrow (f.:X) c= downarrow (f.:downarrow X);

theorem :: YELLOW13:7
   for S being reflexive RelStr, T being reflexive transitive RelStr,
     f being map of S, T, X being Subset of S st f is monotone
 holds downarrow (f.:X) = downarrow (f.:downarrow X);

theorem :: YELLOW13:8
 for N being non empty Poset holds IdsMap N is one-to-one;

registration let N be non empty Poset;
 cluster IdsMap N -> one-to-one;
coherence by Th8;
end;


theorem :: YELLOW13:9
 for N being finite LATTICE holds SupMap N is one-to-one;

registration let N be finite LATTICE;
 cluster SupMap N -> one-to-one;
coherence by Th9;
end;


theorem :: YELLOW13:10
   for N being finite LATTICE holds N, InclPoset Ids N are_isomorphic;

theorem :: YELLOW13:11
 for N being complete (non empty Poset), x being Element of N,
     X being non empty Subset of N holds x"/\" preserves_inf_of X;

theorem :: YELLOW13:12
 for N being complete (non empty Poset), x being Element of N holds
  x"/\" is meet-preserving;

registration let N be complete (non empty Poset),
               x be Element of N;
 cluster x"/\" -> meet-preserving;
coherence by Th12;
end;



begin  :: On the basis of topological spaces

theorem :: YELLOW13:13
   for T being anti-discrete non empty TopStruct, p being Point of T holds
  {the carrier of T} is Basis of p;

theorem :: YELLOW13:14
   for T being anti-discrete non empty TopStruct, p being Point of T,
     D being Basis of p holds D = {the carrier of T};

theorem :: YELLOW13:15
   for T being non empty TopSpace, P being Basis of T, p being Point of T holds
  {A where A is Subset of T: A in P & p in A} is Basis of p;

theorem :: YELLOW13:16
   for T being non empty TopStruct, A being Subset of T, p being Point of T
   holds
  p in Cl A iff
  for K being Basis of p, Q being Subset of T st Q in K holds A meets Q;

theorem :: YELLOW13:17
   for T being non empty TopStruct, A being Subset of T, p being Point of T
   holds
  p in Cl A iff
  ex K being Basis of p st for Q being Subset of T st Q in K holds A meets Q;

definition let T be TopStruct, p be Point of T;
 mode basis of p -> Subset-Family of T means
:: YELLOW13:def 1
  for A being Subset of T st p in Int A ex P being Subset of T st
   P in it & p in Int P & P c= A;
end;


definition let T be non empty TopSpace, p be Point of T;
 redefine mode basis of p means
:: YELLOW13:def 2
    for A being a_neighborhood of p ex P being a_neighborhood of p st
   P in it & P c= A;
end;


theorem :: YELLOW13:18
 for T being TopStruct, p being Point of T holds
  bool the carrier of T is basis of p;

theorem :: YELLOW13:19
 for T being non empty TopSpace, p being Point of T, P being basis of p holds
  P is non empty;

registration let T be non empty TopSpace, p be Point of T;
 cluster -> non empty basis of p;
coherence by Th19;
end;


registration let T be TopStruct, p be Point of T;
 cluster non empty basis of p;
existence
  proof
      bool the carrier of T is basis of p by Th18;
    hence thesis;
  end;
end;


definition let T be TopStruct, p be Point of T, P be basis of p;
 attr P is correct means
:: YELLOW13:def 3
  for A being Subset of T holds A in P iff p in Int A;
end;

registration let T be TopStruct, p be Point of T;
 cluster correct basis of p;
existence
  proof
    reconsider P = {A where A is Subset of T: p in Int A} as basis of p by Lm4;
    take P;
    thus thesis by Lm5;
  end;
end;


theorem :: YELLOW13:20
   for T being TopStruct, p being Point of T holds
  {A where A is Subset of T: p in Int A} is correct basis of p;

registration let T be non empty TopSpace, p be Point of T;
 cluster non empty correct basis of p;
existence
  proof
    reconsider K = {A where A is Subset of T: p in Int A} as correct basis of p
     by Lm4,Lm5;
    take K;
    thus thesis;
  end;
end;


theorem :: YELLOW13:21
   for T being anti-discrete non empty TopStruct, p being Point of T holds
  {the carrier of T} is correct basis of p;

theorem :: YELLOW13:22
   for T being anti-discrete non empty TopStruct, p being Point of T,
     D being correct basis of p holds D = {the carrier of T};

theorem :: YELLOW13:23
   for T being non empty TopSpace, p being Point of T,
     P being Basis of p holds P is basis of p;

definition let T be TopStruct;
 mode basis of T -> Subset-Family of T means
:: YELLOW13:def 4
  for p being Point of T holds it is basis of p;
end;


theorem :: YELLOW13:24
 for T being TopStruct holds bool the carrier of T is basis of T;

theorem :: YELLOW13:25
 for T being non empty TopSpace, P being basis of T holds P is non empty;

registration let T be non empty TopSpace;
 cluster -> non empty basis of T;
coherence by Th25;
end;


registration let T be TopStruct;
 cluster non empty basis of T;
existence
  proof
      bool the carrier of T is basis of T by Th24;
    hence thesis;
  end;
end;


theorem :: YELLOW13:26
   for T being non empty TopSpace, P being basis of T holds
  the topology of T c= UniCl Int P;

theorem :: YELLOW13:27
   for T being TopSpace, P being Basis of T holds P is basis of T;

definition let T be non empty TopSpace-like TopRelStr;
 attr T is topological_semilattice means
:: YELLOW13:def 5
  for f being map of [:T,T qua TopSpace:], T st f = inf_op T
   holds f is continuous;
end;


registration
 cluster reflexive trivial -> topological_semilattice
                                 (non empty TopSpace-like TopRelStr);
coherence
  proof
    let T be non empty TopSpace-like TopRelStr;
    assume T is reflexive trivial;
    then reconsider W = T as reflexive trivial non empty TopSpace-like
TopRelStr;
    consider d being Element of W such that
A1:   the carrier of W = {d} by TEX_1:def 1;
    let f be map of [:T,T qua TopSpace:], T such that
A2:   f = inf_op T;
      now
      let P1 be Subset of T such that
A3:     P1 is closed;
      per cases by A3,TDLAT_3:21;
      suppose P1 = {};
      then f"P1 = {}[:T,T qua TopSpace:] by RELAT_1:171;
      hence f"P1 is closed;
      end;
      suppose
A4:     P1 = the carrier of W;
A5:   dom f = the carrier of [:T,T qua TopSpace:] by FUNCT_2:def 1
           .= [:the carrier of T,the carrier of T:] by BORSUK_1:def 5;
A6:   f"P1 = [:{d},{d}:]
      proof
          thus for x being set st x in f"P1 holds x in [:{d},{d}:]
            by A1,A5,FUNCT_1:def 13;
        let x be set;
        assume x in [:{d},{d}:];
        then x in {[d,d]} by ZFMISC_1:35;
then A7:     x = [d,d] by TARSKI:def 1;
A8:     f. [d,d] = d "/\" d by A2,WAYBEL_2:def 4
                .= d by YELLOW_0:25;
          [d,d] in [:the carrier of T,the carrier of T:] by ZFMISC_1:106;
        hence thesis by A4,A5,A7,A8,FUNCT_1:def 13;
      end;
        [#][:T,T qua TopSpace:]
          = the carrier of [:T,T qua TopSpace:] by PRE_TOPC:12
         .= [:{d},{d}:] by A1,BORSUK_1:def 5;
      hence f"P1 is closed by A6;
    end;
    end;
    hence thesis by PRE_TOPC:def 12;
  end;
end;


registration
 cluster reflexive trivial non empty TopSpace-like TopRelStr;
existence
  proof
    consider A being reflexive trivial non empty TopSpace-like TopRelStr;
    take A;
    thus thesis;
  end;
end;


theorem :: YELLOW13:28
 for T being topological_semilattice (non empty TopSpace-like TopRelStr),
     x being Element of T holds
  x"/\" is continuous;

registration
let T be topological_semilattice(non empty TopSpace-like TopRelStr),
               x be Element of T;
 cluster x"/\" -> continuous;
coherence by Th28;
end;


