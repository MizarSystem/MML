:: Group and Field Definitions
::  by J\'ozef Bia{\l}as
::
:: Received October 27, 1989
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies FUNCT_1, RELAT_1, BOOLE, BINOP_1, FUNCT_3, QC_LANG1, REALSET1,
      REALSET2, FINSET_1, CARD_1, CAT_1, ARYTM;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, RELAT_1, FUNCT_1, FUNCT_2,
      BINOP_1, DOMAIN_1, FUNCOP_1, FUNCT_3, FINSET_1, CARD_1, XXREAL_0;
 constructors BINOP_1, DOMAIN_1, FUNCT_3, FUNCOP_1, XXREAL_0, CARD_1, MEMBERED,
      NUMBERS;
 registrations XBOOLE_0, FUNCT_1, RELSET_1, FINSET_1, MEMBERED;
 requirements SUBSET, BOOLE, NUMERALS, REAL;


begin

theorem :: REALSET1:1
  for X,x being set holds for F being Function of [:X,X:],X holds
  x in [:X,X:] implies F.x in X;

theorem :: REALSET1:2
  for X being set, F being BinOp of X ex A being Subset of X st
  for x being set holds x in [:A,A:] implies F.x in A;

definition
  let X be set;
  let F be BinOp of X;
  let A be Subset of X;
  pred F is_in A means
:: REALSET1:def 1

  for x being set holds x in [:A,A:] implies F.x in A;
end;

definition
  let X be set;
  let F be BinOp of X;
  mode Preserv of F -> Subset of X means
:: REALSET1:def 2

    for x being set holds x in [:it,it:] implies F.x in it;
end;

definition
  let R be Relation;
  let A be set;
  func R||A equals
:: REALSET1:def 3

  R | [:A,A:];
end;

registration
  let R be Relation;
  let A be set;
  cluster R||A -> Relation-like;
end;

registration
  let R be Function;
  let A be set;
  cluster R||A -> Function-like;
end;

theorem :: REALSET1:3
  for X being set, F being BinOp of X,
  A being Preserv of F holds F||A is BinOp of A;

definition
  let X be set;
  let F be BinOp of X;
  let A be Preserv of F;
  redefine
  func F||A -> BinOp of A;
end;

definition
  let IT be set;
  attr IT is trivial means
:: REALSET1:def 4

  IT is empty or ex x being set st IT = {x};
end;

registration
  cluster trivial non empty set;
  cluster non trivial non empty set;
  cluster non trivial -> non empty set;
end;

registration
  let y be set;
  cluster {y} -> trivial;
end;

theorem :: REALSET1:4
  for X being non empty set holds
  X is non trivial iff for x being set holds X\{x} is non empty set;

theorem :: REALSET1:5
  ex A being non empty set st for z being Element of A holds
  A \ {z} is non empty set;

theorem :: REALSET1:6
  for X being non empty set
  st for x being Element of X holds X\{x} is non empty set
  holds X is non trivial;

theorem :: REALSET1:7
  for A being non empty set st
  (for x being Element of A holds A\{x} is non empty set) holds
  A is non trivial set;

definition
  let X be non trivial set;
  let F be BinOp of X;
  let x be Element of X;
  pred F is_Bin_Op_Preserv x means
:: REALSET1:def 5

  (X\{x} is Preserv of F) &
  F||X\{x} is BinOp of X\{x};
end;

theorem :: REALSET1:8
  for X being set holds for A being Subset of X holds ex F being BinOp of X st
  for x being set holds x in [:A,A:] implies F.x in A;

definition
  let X be set;
  let A be Subset of X;
  mode Presv of X,A -> BinOp of X means
:: REALSET1:def 6

    for x being set holds x in [:A,A:] implies it.x in A;
end;

theorem :: REALSET1:9
  for X being set, A being Subset of X, F being Presv of X,A holds
  F||A is BinOp of A;

definition
  let X be set;
  let A be Subset of X;
  let F be Presv of X,A;
  func F|||A -> BinOp of A equals
:: REALSET1:def 7

  F||A;
end;

canceled;

definition
  let A be set;
  let x be Element of A;
  mode DnT of x,A -> BinOp of A means
:: REALSET1:def 8

    for y being set holds y in [:A\{x},A\{x}:] implies it.y in A\{x};
end;

theorem :: REALSET1:11
  for A being non trivial set holds for x being Element of A holds
  for F being DnT of x,A holds F||(A\{x}) is BinOp of A\{x};

definition
  let A be non trivial set;
  let x be Element of A;
  let F be DnT of x,A;
  func F!(A,x) -> BinOp of A\{x} equals
:: REALSET1:def 9

  F||(A\{x});
end;

registration
  let F be non empty set;
  cluster trivial non empty Subset of F;
end;

definition
  let F be non empty set;
  mode OnePoint of F is trivial non empty Subset of F;
end;

theorem :: REALSET1:12
  for F being non trivial set holds for A being OnePoint of F holds
  F\A is non empty set;

registration
  let F be non trivial set;
  let A be OnePoint of F;
  cluster F\A -> non empty;
end;

begin :: Addenda

:: from SPPOL_1

theorem :: REALSET1:13
  for A being finite set holds A is trivial iff card A < 2;

theorem :: REALSET1:14
  for A being set holds A is non trivial iff
  ex a1,a2 being set st a1 in A & a2 in A & a1 <> a2;

theorem :: REALSET1:15
  for D being set, A being Subset of D holds A is non trivial iff
  ex d1,d2 being Element of D st d1 in A & d2 in A & d1 <> d2;

theorem :: REALSET1:16
  for F being non trivial set, A being OnePoint of F
  ex x being Element of F st A = {x};

begin :: Addenda

:: from AMISTD_1, 2006.03.15, A.T.

registration
  let x, y be set;
  cluster x .--> y -> trivial;
end;

:: from FINSEQ_5, 2007.04.12, A.T.

registration
  cluster empty -> trivial set;
end;

:: from REALSET1, 2007.04.12, A.T.

registration
  cluster trivial -> finite set;
end;

registration
  cluster infinite -> non trivial set;
end;

