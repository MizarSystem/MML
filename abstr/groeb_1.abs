:: Characterization and Existence of {G}r\"obner Bases
::  by Christoph Schwarzweller
::
:: Received June 11, 2003
:: Copyright (c) 2003 Association of Mizar Users

environ

 vocabulary POLYNOM1, POLYNOM7, BOOLE, FUNCT_1, RELAT_1, VECTSP_1, RLVECT_1,
      ORDINAL1, LATTICES, ANPROJ_1, VECTSP_2, ALGSTR_1, ARYTM_1, REALSET1,
      GROUP_1, BINOP_1, ARYTM_3, TERMORD, IDEAL_1, RELAT_2, DICKSON, MCART_1,
      FINSEQ_1, FINSEQ_4, BHSP_3, REWRITE1, ORDERS_1, INT_1, BINOM, TARSKI,
      FINSET_1, FUNCOP_1, FILTER_2, PBOOLE, POLYRED, RLVECT_2, CARD_3, CAT_3,
      SQUARE_1, FUNCT_4, CARD_1, BAGORDER, GROEB_1, PARTFUN1;
 notation TARSKI, RELAT_1, XBOOLE_0, RELAT_2, XREAL_0, RELSET_1, FUNCT_1,
      ORDINAL1, ALGSTR_1, RLVECT_1, PARTFUN1, FINSEQ_4, FINSET_1, DICKSON,
      CARD_3, CARD_1, SUBSET_1, MCART_1, REALSET1, FINSEQ_1, YELLOW_1, PRALG_1,
      VECTSP_2, VECTSP_1, POLYNOM7, REAL_1, BINOM, PBOOLE, PRE_CIRC, ORDERS_1,
      POLYNOM1, IDEAL_1, REWRITE1, BAGORDER, STRUCT_0, TERMORD, GROUP_1,
      NUMBERS, NAT_1, POLYRED;
 constructors REWRITE1, REAL_1, IDEAL_1, DICKSON, TERMORD, YELLOW_1, PRE_CIRC,
      MONOID_0, DOMAIN_1, POLYRED, BAGORDER;
 clusters STRUCT_0, RELAT_1, FINSET_1, RELSET_1, VECTSP_1, ORDINAL1, VECTSP_2,
      CARD_1, GCD_1, ALGSTR_1, POLYNOM1, POLYNOM2, NAT_1, INT_1, REWRITE1,
      BINOM, ORDERS_1, POLYNOM7, TERMORD, IDEAL_1, YELLOW_1, SUBSET_1, DICKSON,
      POLYRED, MEMBERED, NUMBERS, ORDINAL2;
 requirements NUMERALS, REAL, SUBSET, BOOLE, ARITHM;


begin :: Preliminaries

definition
let n be Ordinal,
    L be right_zeroed add-associative right_complementable
         unital distributive (non trivial doubleLoopStr),
    p be Polynomial of n,L;
redefine func {p} -> non empty finite Subset of Polynom-Ring(n,L);
end;


theorem :: GROEB_1:1
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Field-like (non trivial doubleLoopStr),
    f,p,g being Polynomial of n,L
holds f reduces_to g,p,T implies
      ex m being Monomial of n,L st g = f - m *' p;

theorem :: GROEB_1:2
 for n being Ordinal,
    T being admissible connected TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Abelian Field-like non degenerated (non empty doubleLoopStr),
    f,p,g being Polynomial of n,L
holds f reduces_to g,p,T implies
      ex m being Monomial of n,L
      st g = f - m *' p &
         not(HT(m*'p,T) in Support g) & HT(m*'p,T) <= HT(f,T),T;

theorem :: GROEB_1:3
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Field-like (non trivial doubleLoopStr),
    f,g being Polynomial of n,L,
    P,Q being Subset of Polynom-Ring(n,L) st P c= Q
holds f reduces_to g,P,T implies f reduces_to g,Q,T;

theorem :: GROEB_1:4
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Field-like (non trivial doubleLoopStr),
    P,Q being Subset of Polynom-Ring(n,L)
holds P c= Q implies PolyRedRel(P,T) c= PolyRedRel(Q,T);

theorem :: GROEB_1:5
for n being Ordinal,
    L being right_zeroed add-associative right_complementable
            (non empty doubleLoopStr),
    p being Polynomial of n,L
holds Support(-p) = Support(p);

theorem :: GROEB_1:6
for n being Ordinal,
    T being connected TermOrder of n,
    L being right_zeroed add-associative right_complementable
            unital distributive non trivial (non empty doubleLoopStr),
    p being Polynomial of n,L
holds HT(-p,T) = HT(p,T);

theorem :: GROEB_1:7
for n being Ordinal,
    T being admissible connected TermOrder of n,
    L being right_zeroed add-associative right_complementable
            unital distributive non trivial (non empty doubleLoopStr),
    p,q being Polynomial of n,L
holds HT(p-q,T) <= max(HT(p,T),HT(q,T),T), T;

theorem :: GROEB_1:8
for n being Ordinal,
    T being admissible connected TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Field-like (non trivial doubleLoopStr),
    p,q being Polynomial of n,L
holds Support(q) c= Support(p) implies q <= p,T;

theorem :: GROEB_1:9
for n being Ordinal,
    T being connected admissible TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Field-like non degenerated (non empty doubleLoopStr),
    f,p being non-zero Polynomial of n,L
holds f is_reducible_wrt p,T implies HT(p,T) <= HT(f,T),T;

begin :: Characterization of Groebner Bases

theorem :: GROEB_1:10
::: proposition 5.33, p. 205
for n being Nat,
    T being connected admissible TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Abelian Field-like (non trivial doubleLoopStr),
    p being Polynomial of n,L
holds PolyRedRel({p},T) is locally-confluent;

theorem :: GROEB_1:11
::: corollary 5.34, p. 205
 for n being Nat,
    T being connected admissible TermOrder of n,
    L being Abelian add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Field-like non degenerated (non empty doubleLoopStr),
    P being Subset of Polynom-Ring(n,L) st
      ex p being Polynomial of n,L st p in P & P-Ideal = {p}-Ideal
holds PolyRedRel(P,T) is locally-confluent;

definition
let n be Ordinal,
    T be connected TermOrder of n,
    L be right_zeroed add-associative right_complementable
         unital distributive non trivial (non empty doubleLoopStr),
    P be Subset of Polynom-Ring(n,L);
func HT(P,T) -> Subset of Bags n equals
:: GROEB_1:def 1
  { HT(p,T) where p is Polynomial of n,L : p in P & p <> 0_(n,L) };
end;


definition
let n be Ordinal,
    S be Subset of Bags n;
func multiples(S) -> Subset of Bags n equals
:: GROEB_1:def 2
  { b where b is Element of Bags n :
         ex b' being bag of n st b' in S & b' divides b };
end;


theorem :: GROEB_1:12
::: theorem 5.35 (i) ==> (ii), p. 206
for n being Nat,
    T being connected admissible TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative well-unital distributive Abelian
            Field-like non degenerated (non empty doubleLoopStr),
    P being Subset of Polynom-Ring(n,L)
holds PolyRedRel(P,T) is locally-confluent implies
      PolyRedRel(P,T) is confluent;

theorem :: GROEB_1:13
::: theorem 5.35 (ii) ==> (iii), p. 206
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Field-like (non trivial doubleLoopStr),
    P being Subset of Polynom-Ring(n,L)
holds PolyRedRel(P,T) is confluent implies
      PolyRedRel(P,T) is with_UN_property;

theorem :: GROEB_1:14
::: theorem 5.35 (iii) ==> (iv), p. 206
for n being Nat,
    T being connected admissible TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative well-unital distributive Abelian
            Field-like non degenerated (non empty doubleLoopStr),
    P being Subset of Polynom-Ring(n,L)
holds PolyRedRel(P,T) is with_UN_property implies
      PolyRedRel(P,T) is with_Church-Rosser_property;

theorem :: GROEB_1:15
::: theorem 5.35 (iv) ==> (v), p. 206
for n being Nat,
    T being connected admissible TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative well-unital distributive Abelian
            Field-like non degenerated (non empty doubleLoopStr),
    P being non empty Subset of Polynom-Ring(n,L)
holds PolyRedRel(P,T) is with_Church-Rosser_property implies
      (for f being Polynomial of n,L
       st f in P-Ideal holds PolyRedRel(P,T) reduces f,0_(n,L));

theorem :: GROEB_1:16
::: theorem 5.35 (v) ==> (vi), p. 206
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Field-like (non trivial doubleLoopStr),
    P being Subset of Polynom-Ring(n,L)
holds (for f being Polynomial of n,L
       st f in P-Ideal holds PolyRedRel(P,T) reduces f,0_(n,L)) implies
      (for f being non-zero Polynomial of n,L
       st f in P-Ideal holds f is_reducible_wrt P,T);

theorem :: GROEB_1:17
::: theorem 5.35 (vi) ==> (vii), p. 206
for n being Nat,
    T being admissible connected TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative well-unital distributive Abelian
            Field-like non degenerated (non empty doubleLoopStr),
    P being Subset of Polynom-Ring(n,L)
holds (for f being non-zero Polynomial of n,L
       st f in P-Ideal holds f is_reducible_wrt P,T) implies
      (for f being non-zero Polynomial of n,L
       st f in P-Ideal holds f is_top_reducible_wrt P,T);

theorem :: GROEB_1:18
::: theorem 5.35 (vii) ==> (viii), p. 206
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Field-like (non trivial doubleLoopStr),
    P being Subset of Polynom-Ring(n,L)
holds (for f being non-zero Polynomial of n,L
       st f in P-Ideal holds f is_top_reducible_wrt P,T) implies
      (for b being bag of n st b in HT(P-Ideal,T)
       ex b' being bag of n st b' in HT(P,T) & b' divides b);

theorem :: GROEB_1:19
::: theorem 5.35 (viii) ==> (ix), p. 206
 for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Field-like (non trivial doubleLoopStr),
    P being Subset of Polynom-Ring(n,L)
holds (for b being bag of n st b in HT(P-Ideal,T)
       ex b' being bag of n st b' in HT(P,T) & b' divides b) implies
      HT(P-Ideal,T) c= multiples(HT(P,T));

theorem :: GROEB_1:20
::: theorem 5.35 (ix) ==> (i), p. 206
 for n being Nat,
    T being connected admissible TermOrder of n,
    L being Abelian add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Field-like non degenerated (non empty doubleLoopStr),
    P being Subset of Polynom-Ring(n,L)
holds HT(P-Ideal,T) c= multiples(HT(P,T)) implies
      PolyRedRel(P,T) is locally-confluent;

definition
::: definition 5.37, p. 207
let n be Ordinal,
    T be connected TermOrder of n,
    L be add-associative right_complementable right_zeroed
         commutative associative well-unital distributive
         Field-like (non trivial doubleLoopStr),
    G be Subset of Polynom-Ring(n,L);
pred G is_Groebner_basis_wrt T means
:: GROEB_1:def 3
  PolyRedRel(G,T) is locally-confluent;
end;


definition
::: definition 5.37, p. 207
let n be Ordinal,
    T be connected TermOrder of n,
    L be add-associative right_complementable right_zeroed
         commutative associative well-unital distributive
         Field-like (non trivial doubleLoopStr),
    G,I be Subset of Polynom-Ring(n,L);
pred G is_Groebner_basis_of I,T means
:: GROEB_1:def 4
  G-Ideal = I & PolyRedRel(G,T) is locally-confluent;
end;

theorem :: GROEB_1:21
 for n being Nat,
    T being connected admissible TermOrder of n,
    L being Abelian add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Field-like non degenerated (non empty doubleLoopStr),
    G,P being non empty Subset of Polynom-Ring(n,L)
       st G is_Groebner_basis_of P,T
holds PolyRedRel(G,T) is Completion of PolyRedRel(P,T);

theorem :: GROEB_1:22
 for n being Nat,
    T being connected admissible TermOrder of n,
    L being Abelian add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Field-like non degenerated (non empty doubleLoopStr),
    p,q being Element of Polynom-Ring(n,L),
    G being non empty Subset of Polynom-Ring(n,L) st G is_Groebner_basis_wrt T
holds p,q are_congruent_mod G-Ideal iff
      nf(p,PolyRedRel(G,T)) = nf(q,PolyRedRel(G,T));

theorem :: GROEB_1:23
 for n being Nat,
    T being connected admissible TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative well-unital distributive Abelian
            Field-like non degenerated (non empty doubleLoopStr),
    f being Polynomial of n,L,
    P being non empty Subset of Polynom-Ring(n,L) st P is_Groebner_basis_wrt T
holds f in P-Ideal iff PolyRedRel(P,T) reduces f,0_(n,L);

theorem :: GROEB_1:24
::: theorem 5.38 (o) ==> (i), p. 207
for n being Nat,
    T being connected admissible TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative well-unital distributive Abelian
            Field-like non degenerated (non empty doubleLoopStr),
    I being Subset of Polynom-Ring(n,L),
    G being non empty Subset of Polynom-Ring(n,L)
holds G is_Groebner_basis_of I,T implies
      (for f being Polynomial of n,L
       st f in I holds PolyRedRel(G,T) reduces f,0_(n,L));

theorem :: GROEB_1:25
::: theorem 5.38 (i) ==> (ii), p. 207
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Field-like (non trivial doubleLoopStr),
    G,I being Subset of Polynom-Ring(n,L)
holds (for f being Polynomial of n,L
       st f in I holds PolyRedRel(G,T) reduces f,0_(n,L)) implies
      (for f being non-zero Polynomial of n,L
       st f in I holds f is_reducible_wrt G,T);

theorem :: GROEB_1:26
::: theorem 5.38 (ii) ==> (iii), p. 207
for n being Nat,
    T being admissible connected TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative well-unital distributive Abelian
            Field-like non degenerated (non empty doubleLoopStr),
    I being add-closed left-ideal Subset of Polynom-Ring(n,L),
    G being Subset of Polynom-Ring(n,L) st G c= I
holds (for f being non-zero Polynomial of n,L
       st f in I holds f is_reducible_wrt G,T) implies
      (for f being non-zero Polynomial of n,L
       st f in I holds f is_top_reducible_wrt G,T);

theorem :: GROEB_1:27
::: theorem 5.38 (iii) ==> (iv), p. 207
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Field-like (non trivial doubleLoopStr),
    G,I being Subset of Polynom-Ring(n,L)
holds (for f being non-zero Polynomial of n,L
       st f in I holds f is_top_reducible_wrt G,T) implies
      (for b being bag of n st b in HT(I,T)
       ex b' being bag of n st b' in HT(G,T) & b' divides b);

theorem :: GROEB_1:28
::: theorem 5.38 (iv) ==> (v), p. 207
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Field-like (non trivial doubleLoopStr),
    G,I being Subset of Polynom-Ring(n,L)
holds (for b being bag of n st b in HT(I,T)
       ex b' being bag of n st b' in HT(G,T) & b' divides b) implies
      HT(I,T) c= multiples(HT(G,T));

theorem :: GROEB_1:29
::: theorem 5.38 (v) ==> (o), p. 207
for n being Nat,
    T being connected admissible TermOrder of n,
    L being Abelian add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Field-like non degenerated (non empty doubleLoopStr),
    I being add-closed left-ideal non empty Subset of Polynom-Ring(n,L),
    G being non empty Subset of Polynom-Ring(n,L) st G c= I
holds HT(I,T) c= multiples(HT(G,T)) implies
      G is_Groebner_basis_of I,T;

begin :: Existence of Groebner Bases

theorem :: GROEB_1:30
for n being Nat,
    T being connected admissible TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Abelian Field-like (non trivial doubleLoopStr)
holds {0_(n,L)} is_Groebner_basis_of {0_(n,L)},T;

theorem :: GROEB_1:31
 for n being Nat,
    T being connected admissible TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Abelian Field-like (non trivial doubleLoopStr),
    p being Polynomial of n,L
holds {p} is_Groebner_basis_of {p}-Ideal,T;

theorem :: GROEB_1:32
 for T being admissible connected TermOrder of {},
    L being add-associative right_complementable right_zeroed
            commutative associative well-unital distributive Abelian
            Field-like non degenerated (non empty doubleLoopStr),
    I being add-closed left-ideal non empty Subset of Polynom-Ring({},L),
    P being non empty Subset of Polynom-Ring({},L) st P c= I & P <> {0_({},L)}
holds P is_Groebner_basis_of I,T;

theorem :: GROEB_1:33
 for n being non empty Ordinal,
    T being admissible connected TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Field-like non degenerated (non empty doubleLoopStr)
ex P be Subset of Polynom-Ring(n,L) st not(P is_Groebner_basis_wrt T);

definition
::: definition preceeding 5.1, p. 189
let n be Ordinal;
func DivOrder(n) -> Order of Bags n means
:: GROEB_1:def 5
  for b1,b2 being bag of n holds [b1,b2] in it iff b1 divides b2;
end;


registration
::: theorem 5.2, p. 189
let n be Nat;
cluster RelStr(#Bags n, DivOrder n#) -> Dickson;
coherence
proof
set P = product (n --> OrderedNAT), J = n --> OrderedNAT;
set S = product Carrier (n --> OrderedNAT), SJ = Carrier (n --> OrderedNAT);
A1: for x being set st x in S
    for g being Function st x = g
    holds dom g = n & for y being set st y in dom g holds g.y in NAT
    proof
    let x be set;
    assume A2: x in S;
    let g' be Function;
    assume A3: x = g';
    consider g being Function such that
    A4: x = g & dom g = dom SJ &
        for y being set st y in dom SJ holds g.y in SJ.y by A2,CARD_3:def 5;
    thus dom g' = n by A3,A4,PBOOLE:def 3;
    thus for y being set st y in dom g' holds g'.y in NAT
      proof
      let y be set;
      assume A5: y in dom g';
      then A6: y in n by A3,A4,PBOOLE:def 3;
      then consider R being 1-sorted such that
      A7: R = J.y & SJ.y = the carrier of R by PRALG_1:def 13;
       g.y in the carrier of R by A3,A4,A5,A7;
      hence thesis by A3,A4,A6,A7,DICKSON:def 15,FUNCOP_1:13;
      end;
    end;
defpred Q[set,set] means
     $1 in S & (ex b being bag of n st b = $2 & b = $1);
A8: for x,y1,y2 being set st x in S & Q[x,y1] & Q[x,y2] holds y1 = y2;
A9: for x being set st x in S ex y being set st Q[x,y]
   proof
   let x be set;
   assume A10: x in S;
   then consider g being Function such that
   A11: x = g & dom g = dom SJ &
        for y being set st y in dom SJ holds g.y in SJ.y
        by CARD_3:def 5;
   A12: x = g & dom g = n &
       for y being set st y in dom g holds g.y in NAT by A1,A10,A11;
   defpred QQ[set,set] means $2 = g.$1;
   A13: for x,y1,y2 being set st x in n & QQ[x,y1] & QQ[x,y2]
       holds y1 = y2;
   A14: for x being set st x in n ex y being set st QQ[x,y];
   consider b being Function such that
   A15: dom b = n & for x being set st x in n holds QQ[x,b.x]
     from FUNCT_1:sch 2(A13,A14);
   reconsider b as ManySortedSet of n by A15,PBOOLE:def 3;
    now let u be set;
     assume u in rng b;
     then consider x being set such that
     A16: x in dom b & u = b.x by FUNCT_1:def 5;
     A17: u = g.x by A15,A16;
      x in dom g by A12,A16,PBOOLE:def 3;
     hence u in NAT by A1,A10,A11,A17;
     end;
   then rng b c= NAT by TARSKI:def 3;
   then reconsider b as bag of n by SEQM_3:def 8;
   take b;
   thus x in S by A10;
   take b;
   thus b = b;
   thus b = x by A12,A15,FUNCT_1:9;
   end;
consider i being Function such that
A18: dom i = S & for x being set st x in S holds Q[x,i.x]
   from FUNCT_1:sch 2(A8,A9);
set R = RelStr(#Bags n, DivOrder n#);
A19: now let v be set;
    assume v in rng i;
    then consider u being set such that
    A20: u in dom i & v = i.u by FUNCT_1:def 5;
     u in S & (ex b being bag of n st b = i.u & b = u) by A18,A20;
    hence v in Bags n by A20,POLYNOM1:def 14;
    end;
A21: for x being Element of R
    holds ex u being Element of P st u in dom i & i.u = x
    proof
    let x be Element of R;
    reconsider g = x as bag of n by POLYNOM1:def 14;
    A22: dom g = n by PBOOLE:def 3 .= dom(Carrier J) by PBOOLE:def 3;
    A23: now let x be set;
        assume x in dom(Carrier J);
        then A24: x in n by PBOOLE:def 3;
        then consider L being 1-sorted such that
        A25: L = J.x & (Carrier J).x = the carrier of L by PRALG_1:def 13;
         the carrier of L = NAT by A24,A25,DICKSON:def 15,FUNCOP_1:13;
        hence g.x in (Carrier J).x by A25;
        end;
    then A26: g in product Carrier J by A22,CARD_3:def 5;
    then g in the carrier of P by YELLOW_1:def 4;
    then reconsider g as Element of P;
    take g;
    thus g in dom i by A18,A22,A23,CARD_3:def 5;
     Q[g,i.g] by A18,A26;
    then consider g' being Function such that
    A27: g' = g & g' in S &
        ex b being bag of n st b = i.g & b = g;
    thus i.g = x by A27;
    end;
 now let N be Subset of R;
  set N' = i"N;
  A28: N' c= S by A18,RELAT_1:167;
  then N' c= the carrier of P by YELLOW_1:def 4;
  then reconsider N' as Subset of P;
  consider B' being set such that
  A29: B' is_Dickson-basis_of N',P & B' is finite
     by DICKSON:def 10;
  A30: B' c= N' &
      for a being Element of P st a in N'
      ex b being Element of P st b in B' & b <= a by A29,DICKSON:def 9;
  set B = i.:B';
  A31: B is finite by A29,FINSET_1:17;
   now let u be set;
    assume u in B;
    then consider v being set such that
    A32: v in dom i & v in B' & u = i.v by FUNCT_1:def 12;
    thus u in N by A30,A32,FUNCT_1:def 13;
    end;
  then A33: B c= N by TARSKI:def 3;
  A34: for a,b being Element of P,
         ta,tb being Element of Bags n st a = ta & b = tb & a in S
         holds a <= b implies ta divides tb
     proof
     let a,b being Element of P,
         ta,tb being Element of Bags n;
     assume A35: a = ta & b = tb & a in S;
     assume a <= b;
     then consider f,g being Function such that
     A36: f = a & g = b &
         for i be set st i in n
         ex R being RelStr, ai,bi being Element of R
           st R = J.i & ai = f.i & bi = g.i & ai <= bi
         by A35,YELLOW_1:def 4;
      now let k be set;
       assume A37: k in n;
       then consider R being RelStr, ak,bk being Element of R such that
       A38: R = J.k & ak = f.k & bk = g.k & ak <= bk by A36;
        J.k = OrderedNAT by A37,FUNCOP_1:13;
       then [ak,bk] in NATOrd by A38,DICKSON:def 15,ORDERS_1:def 9;
       then consider a',b' being Nat such that
       A39: [a',b'] = [ak,bk] & a' <= b' by DICKSON:def 14;
       A40: a' = [ak,bk]`1 by A39,MCART_1:def 1 .= ak by MCART_1:def 1;
        b' = [ak,bk]`2 by A39,MCART_1:def 2 .= bk by MCART_1:def 2;
       hence ta.k <= tb.k by A35,A36,A38,A39,A40;
       end;
     hence thesis by POLYNOM1:50;
     end;
   for a being Element of R st a in N
                 ex b being Element of R st b in B & b <= a
    proof
    let a be Element of R;
    assume A41: a in N;
    consider a' being Element of P such that
    A42: a' in dom i & i.a' = a by A21;
    A43: a' in S & (ex b being bag of n st b = i.a' & b = a') by A18,A42;
     a' in N' by A41,A42,FUNCT_1:def 13;
    then consider b' being Element of P such that
    A44: b' in B' & b' <= a' by A29,DICKSON:def 9;
    set b = i.b';
    A45: B' c= S by A28,A30,XBOOLE_1:1;
    then consider b1 being bag of n such that
    A46: b1 = i.b' & b1 = b' by A18,A44;
     b in rng i by A18,A44,A45,FUNCT_1:def 5;
    then reconsider b as Element of Bags n by A19;
    reconsider b as Element of R;
    take b;
    thus b in B by A18,A44,A45,FUNCT_1:def 12;
    reconsider aa = a, bb = b as Element of Bags n;
     bb divides aa by A34,A42,A43,A44,A45,A46;
    then [b,a] in DivOrder(n) by Def5;
    hence b <= a by ORDERS_1:def 9;
    end;
  then B is_Dickson-basis_of N,R by A33,DICKSON:def 9;
  hence ex B being set st B is_Dickson-basis_of N,R & B is finite by A31;
  end;
hence thesis by DICKSON:def 10;
end;
end;


theorem :: GROEB_1:34
::: theorem 5.2, p. 189
for n being Nat,
    N being Subset of RelStr(#Bags n, DivOrder n#)
ex B being finite Subset of Bags n
  st B is_Dickson-basis_of N,RelStr(#Bags n, DivOrder n#);

theorem :: GROEB_1:35
::: theorem 5.41, p. 208
for n being Nat,
    T being connected admissible TermOrder of n,
    L being Abelian add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Field-like non degenerated (non empty doubleLoopStr),
    I being add-closed left-ideal non empty Subset of Polynom-Ring(n,L)
ex G being finite Subset of Polynom-Ring(n,L) st G is_Groebner_basis_of I,T;

theorem :: GROEB_1:36
 for n being Nat,
    T being connected admissible TermOrder of n,
    L being Abelian add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Field-like non degenerated (non empty doubleLoopStr),
    I being add-closed left-ideal non empty Subset of Polynom-Ring(n,L)
     st I <> {0_(n,L)}
ex G being finite Subset of Polynom-Ring(n,L)
  st G is_Groebner_basis_of I,T & not(0_(n,L) in G);

definition
let n be Ordinal,
    T be connected TermOrder of n,
    L be non empty multLoopStr_0,
    p be Polynomial of n,L;
pred p is_monic_wrt T means
:: GROEB_1:def 6
  HC(p,T) = 1_ L;
end;


definition
::: definition 5.29, p. 203
let n be Ordinal,
    T be connected TermOrder of n,
    L be right_zeroed add-associative right_complementable
         commutative associative well-unital distributive
         Field-like non trivial (non empty doubleLoopStr),
    P be Subset of Polynom-Ring(n,L);
pred P is_reduced_wrt T means
:: GROEB_1:def 7
  for p being Polynomial of n,L st p in P
  holds p is_monic_wrt T & p is_irreducible_wrt P\{p},T;
end;
notation
::: definition 5.29, p. 203
let n be Ordinal,
    T be connected TermOrder of n,
    L be right_zeroed add-associative right_complementable
         commutative associative well-unital distributive
         Field-like non trivial (non empty doubleLoopStr),
    P be Subset of Polynom-Ring(n,L);
synonym P is_autoreduced_wrt T for P is_reduced_wrt T ; end;


theorem :: GROEB_1:37
::: lemma 5.42, p. 208
for n being Ordinal,
    T being admissible connected TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative well-unital distributive Abelian
            Field-like non degenerated (non empty doubleLoopStr),
    I being add-closed left-ideal Subset of Polynom-Ring(n,L),
    m being Monomial of n,L,
    f,g being Polynomial of n,L st f in I & g in I & HM(f,T) = m & HM(g,T) = m
holds not(ex p being Polynomial of n,L st p in I & p < f,T & HM(p,T) = m) &
      not(ex p being Polynomial of n,L st p in I & p < g,T & HM(p,T) = m)
implies f = g;

theorem :: GROEB_1:38
 for n being Nat,
    T being connected admissible TermOrder of n,
    L being Abelian add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Field-like non degenerated (non empty doubleLoopStr),
    I being add-closed left-ideal non empty Subset of Polynom-Ring(n,L),
    G being Subset of Polynom-Ring(n,L),
    p being Polynomial of n,L,
    q being non-zero Polynomial of n,L
      st p in G & q in G & p <> q & HT(q,T) divides HT(p,T)
holds G is_Groebner_basis_of I,T implies G\{p} is_Groebner_basis_of I,T;

theorem :: GROEB_1:39
::: theorem 5.43, p. 209
 for n being Nat,
    T being connected admissible TermOrder of n,
    L being Abelian add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Field-like non degenerated (non empty doubleLoopStr),
    I being add-closed left-ideal non empty Subset of Polynom-Ring(n,L)
     st I <> {0_ (n,L)}
ex G being finite Subset of Polynom-Ring(n,L)
  st G is_Groebner_basis_of I,T & G is_reduced_wrt T;

theorem :: GROEB_1:40
::: theorem 5.43, p. 209
 for n being Nat,
    T being connected admissible TermOrder of n,
    L being Abelian add-associative right_complementable right_zeroed
            commutative associative well-unital distributive
            Field-like non degenerated (non empty doubleLoopStr),
    I being add-closed left-ideal non empty Subset of Polynom-Ring(n,L),
    G1,G2 being non empty finite Subset of Polynom-Ring(n,L)
     st G1 is_Groebner_basis_of I,T & G1 is_reduced_wrt T &
        G2 is_Groebner_basis_of I,T & G2 is_reduced_wrt T
holds G1 = G2;
