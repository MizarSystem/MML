:: Introduction to Homotopy Theory
::  by Adam Grabowski
::
:: Received September 10, 1997
:: Copyright (c) 1997 Association of Mizar Users

environ

 vocabulary PRE_TOPC, CARD_1, FUNCT_1, RELAT_1, SUBSET_1, BOOLE, COMPTS_1,
      ORDINAL2, FUNCT_4, FUNCOP_1, TOPS_2, BORSUK_1, GRAPH_1, RCOMP_1, RELAT_2,
      ARYTM_3, ARYTM_1, TREAL_1, SEQ_1, TOPMETR, SEQM_3, SETFAM_1, TARSKI,
      TSP_1, METRIC_1, URYSOHN1, PCOMPS_1, EUCLID, MCART_1, CONNSP_2, TOPS_1,
      BORSUK_2;
 notation TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, XREAL_0, NAT_1, REAL_1,
      RELAT_1, SETFAM_1, FUNCT_1, PARTFUN1, FUNCT_2, STRUCT_0, PRE_TOPC,
      EUCLID, TOPS_2, MCART_1, RCOMP_1, PCOMPS_1, CARD_1, TOPS_1, COMPTS_1,
      CONNSP_1, CONNSP_2, TREAL_1, FUNCT_4, SEQM_3, BORSUK_1, T_0TOPSP,
      TOPMETR, BINOP_1, FUNCT_3, GRCAT_1, URYSOHN1, TSP_1, WEIERSTR;
 constructors EUCLID, TOPS_2, CONNSP_1, REAL_1, T_0TOPSP, TREAL_1, SEQM_3,
      TOPS_1, WEIERSTR, RCOMP_1, COMPTS_1, YELLOW_8, URYSOHN1, GRCAT_1,
      MEMBERED;
 clusters SUBSET_1, STRUCT_0, TOPMETR, FUNCT_1, PRE_TOPC, BORSUK_1, TSP_1,
      YELLOW_6, YELLOW_8, WAYBEL10, METRIC_1, RELSET_1, MEMBERED, FUNCT_2;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;


begin :: Preliminaries

 reserve T,T1,T2,S for non empty TopSpace;

scheme :: BORSUK_2:sch 1
 FrCard { A() -> non empty set, X() -> set,
       F(set) -> set, P[set] }:
 Card { F(w) where w is Element of A(): w in X() & P[w] } <=` Card X()
proof
     deffunc G(set) = F($1);
     consider f be Function such that
A1:  dom f = X() & for x be set st x in X() holds f.x = G(x) from FUNCT_1:sch 3
;
      { F(w) where w is Element of A(): w in X() & P[w]} c= rng f
     proof let x be set;
     assume x in { F(w) where w is Element of A(): w in X() & P[w]};
      then consider w being Element of A() such that
A2:     x = F(w) & w in X() & P[w];
         f.w = x by A1,A2;
      hence thesis by A1,A2,FUNCT_1:def 5;
     end;
   hence thesis by A1,CARD_1:28;
  end;

theorem :: BORSUK_2:1
   for f being map of T1,S, g being map of T2,S st
  T1 is SubSpace of T & T2 is SubSpace of T &
   ([#] T1) \/ ([#] T2) = [#] T &
    T1 is compact & T2 is compact & T is_T2 &
     f is continuous & g is continuous &
  ( for p be set st p in ([#] T1) /\ ([#] T2) holds f.p = g.p )
  ex h being map of T,S st h = f+*g & h is continuous;

registration
  let S, T be non empty TopSpace;
  cluster continuous map of S, T;
  existence
  proof
   consider a be Point of T;
     S --> a is continuous by BORSUK_1:36;
   hence thesis;
  end;
end;


registration let T be non empty TopStruct;
  cluster id T -> open continuous;
  coherence
  proof
   thus id T is open
   proof
    let A be Subset of T;
    assume A1: A is open;
      (id the carrier of T).:A = A by BORSUK_1:3;
    hence thesis by A1,GRCAT_1:def 11;
   end;
   let V be Subset of T such that
A2:    V is closed;
     (id T)"V = (id the carrier of T)"V by GRCAT_1:def 11
           .= V by BORSUK_1:4;
   hence (id T)"V is closed by A2;
  end;
end;


registration
  let T be non empty TopStruct;
  cluster continuous one-to-one map of T, T;
  existence
  proof
   take id T;
   thus thesis;
  end;
end;


canceled;

theorem :: BORSUK_2:3
    for S, T being non empty TopSpace,
      f being map of S, T st f is_homeomorphism holds
   f" is open;

begin :: Paths and arcwise connected spaces

definition let T be TopStruct; let a, b be Point of T;
 given f be map of I[01], T such that
  f is continuous & f.0 = a & f.1 = b;
 mode Path of a, b -> map of I[01], T means
:: BORSUK_2:def 1
   it is continuous & it.0 = a & it.1 = b;
end;


theorem :: BORSUK_2:4
 for T be non empty TopSpace, a be Point of T
  ex f be map of I[01], T st f is continuous & f.0 = a & f.1 = a;

registration let T be non empty TopSpace; let a be Point of T;
 cluster continuous Path of a, a;
 existence
 proof
  set IT = I[01] --> a;
A1:  IT = (the carrier of I[01]) --> a by BORSUK_1:def 3;
  reconsider IT as map of I[01], T;
A2:  IT.0 = a & IT.1 = a by A1,BORSUK_1:def 17,def 18,FUNCOP_1:13;
    IT is continuous by BORSUK_1:36;
  then reconsider IT as Path of a, a by A2,Def1;
    IT is continuous by BORSUK_1:36;
  hence thesis;
 end;
end;


definition let T be TopStruct;
  attr T is arcwise_connected means
:: BORSUK_2:def 2
   for a, b being Point of T ex f being map of I[01], T st
          f is continuous & f.0 = a & f.1 = b;
end;


registration
 cluster arcwise_connected non empty TopSpace;
 existence
 proof
  set T = I[01] | { 0[01] };
A1: the carrier of T = { 0[01] } by JORDAN1:1;
     0[01] in the carrier of I[01] & 0[01] in { 0[01] }
    by TARSKI:def 1;
  then reconsider nl = 0[01] as Point of T by JORDAN1:1;
    for a, b being Point of T ex f being map of I[01], T st
          f is continuous & f.0 = a & f.1 = b
  proof
   let a, b be Point of T;
A2: a = nl & b = nl by A1,TARSKI:def 1;
   reconsider f = I[01] --> nl as map of I[01], T;
A3: f is continuous by BORSUK_1:36;
     f = (the carrier of I[01]) --> nl by BORSUK_1:def 3;
   then f.0 = a & f.1 = b by A2,BORSUK_1:def 17,def 18,FUNCOP_1:13;
   hence thesis by A3;
  end;
  then T is arcwise_connected by Def2;
  hence thesis;
 end;
end;


definition let T be arcwise_connected TopStruct;
  let a, b be Point of T;
  redefine mode Path of a, b means
:: BORSUK_2:def 3
     it is continuous & it.0 = a & it.1 = b;
end;


registration let T be arcwise_connected TopStruct;
  let a, b be Point of T;
  cluster -> continuous Path of a, b;
 coherence by Def3;
end;


reserve GY for non empty TopSpace,
        r,s for Real;

theorem :: BORSUK_2:5
 for GX being non empty TopSpace st
  GX is arcwise_connected holds GX is connected;

registration
  cluster arcwise_connected -> connected (non empty TopSpace);
  coherence by Th5;
end;


begin :: Basic operations on paths

definition let T be non empty TopSpace;
  let a, b, c be Point of T;
  let P be Path of a, b,
      Q be Path of b, c;
 given f, g be map of I[01], T such that
  f is continuous & f.0 = a & f.1 = b &
    g is continuous & g.0 = b & g.1 = c;
  func P + Q -> Path of a, c means
:: BORSUK_2:def 4
   for t being Point of I[01], t' being Real st t = t' holds
    ( 0 <= t' & t' <= 1/2 implies it.t = P.(2*t') ) &
    ( 1/2 <= t' & t' <= 1 implies it.t = Q.(2*t'-1) );
end;


registration let T be non empty TopSpace;
 let a be Point of T;
 cluster constant Path of a, a;
  existence
  proof
  set IT = I[01] --> a;
A1:  IT = (the carrier of I[01]) --> a by BORSUK_1:def 3;
  reconsider IT as map of I[01], T;
A2:  IT.0 = a & IT.1 = a by A1,BORSUK_1:def 17,def 18,FUNCOP_1:13;
    IT is continuous by BORSUK_1:36;
  then reconsider IT as Path of a, a by A2,Def1;
    for n1,n2 being set st n1 in dom IT & n2 in dom IT holds IT.n1=IT.n2
  proof
   let n1,n2 be set;
   assume n1 in dom IT & n2 in dom IT;
then A3: n1 in the carrier of I[01] & n2 in the carrier of I[01] by FUNCT_2:def
1;
   then IT.n1 = a by A1,FUNCOP_1:13
        .= IT.n2 by A1,A3,FUNCOP_1:13;
   hence thesis;
  end;
  then IT is constant by SEQM_3:def 5;
  hence thesis;
 end;
end;


theorem :: BORSUK_2:6
   for T being non empty TopSpace,
     a being Point of T,
     P being constant Path of a, a holds P = I[01] --> a;

theorem :: BORSUK_2:7
  for T being non empty TopSpace,
      a being Point of T,
      P being constant Path of a, a holds P + P = P;

registration let T be non empty TopSpace,
               a be Point of T,
               P be constant Path of a, a;
 cluster P + P -> constant;
 coherence by Th7;
end;


definition let T be non empty TopSpace;
  let a, b be Point of T;
  let P be Path of a, b;
  given f be map of I[01], T such that
f is continuous & f.0 = a & f.1 = b;
  func - P -> Path of b, a means
:: BORSUK_2:def 5
   for t being Point of I[01], t' being Real st t = t' holds
    it.t = P.(1-t');
end;

theorem :: BORSUK_2:8
  for T being non empty TopSpace,
      a being Point of T,
      P being constant Path of a, a holds - P = P;

registration let T be non empty TopSpace,
               a be Point of T,
               P be constant Path of a, a;
 cluster - P -> constant;
 coherence by Th8;
end;


begin :: The product of two topological spaces

theorem :: BORSUK_2:9
 for X, Y being non empty TopSpace
  for A being Subset-Family of Y
   for f being map of X, Y holds
    f"(union A) = union (f"A);

definition let S1, S2, T1, T2 be non empty TopSpace;
 let f be map of S1, S2, g be map of T1, T2;
 redefine func [:f, g:] -> map of [:S1, T1:], [:S2, T2:];
end;


theorem :: BORSUK_2:10
 for S1, S2, T1, T2 be non empty TopSpace,
     f be continuous map of S1, T1,
     g be continuous map of S2, T2,
     P1, P2 being Subset of [:T1, T2:] holds
       (P2 in Base-Appr P1 implies [:f,g:]"P2 is open);

theorem :: BORSUK_2:11
 for S1, S2, T1, T2 be non empty TopSpace,
     f be continuous map of S1, T1,
     g be continuous map of S2, T2,
     P2 being Subset of [:T1, T2:] holds
       (P2 is open implies [:f,g:]"P2 is open);

theorem :: BORSUK_2:12
 for S1, S2, T1, T2 be non empty TopSpace,
     f be continuous map of S1, T1,
     g be continuous map of S2, T2 holds
   [:f,g:] is continuous;

registration
  cluster empty -> T_0 TopStruct;
  coherence by T_0TOPSP:def 7;
end;


registration let T1, T2 be discerning (non empty TopSpace);
  cluster [:T1, T2:] -> discerning;
  coherence
  proof
  set T = [:T1,T2:];
    now let p,q be Point of T; assume A1: p <> q;
     p in the carrier of T & q in the carrier of T;
then A2: p in [:the carrier of T1, the carrier of T2:] &
    q in [:the carrier of T1, the carrier of T2:] by BORSUK_1:def 5;
   then consider x,y be set such that
A3: x in the carrier of T1 & y in
 the carrier of T2 & p = [x,y] by ZFMISC_1:def 2;
   consider z,v be set such that
A4: z in the carrier of T1 & v in the carrier of T2 & q = [z,v]
      by A2,ZFMISC_1:def 2;
  reconsider x, z as Point of T1 by A3,A4;
  reconsider y, v as Point of T2 by A3,A4;
  per cases;
  suppose x <> z;
   then consider V1 being Subset of T1 such that
A5:   V1 is open &
     ((x in V1 & not z in V1) or (z in V1 & not x in V1)) by T_0TOPSP:def 7;
A6: [#]T2 is open by TOPS_1:53;
A7: y in [#]T2 & v in [#]T2 by A3,A4,PRE_TOPC:12;
   set X = [:V1, [#]T2:];
A8:  X is open by A5,A6,BORSUK_1:46;
       now per cases by A5;
      suppose x in V1 & not z in V1;
      hence (p in X & not q in X) or (q in X & not p in
 X) by A3,A4,A7,ZFMISC_1:106;
      end;
      suppose z in V1 & not x in V1;
      hence (p in X & not q in X) or (q in X & not p in
 X) by A3,A4,A7,ZFMISC_1:106;
     end;
     end;
   hence ex X being Subset of T st X is open &
     ((p in X & not q in X) or (q in X & not p in X)) by A8;
  end;
  suppose x = z;
   then consider V1 being Subset of T2 such that
A9:   V1 is open &
     ((y in V1 & not v in V1) or (v in V1 & not y in V1))
       by A1,A3,A4,T_0TOPSP:def 7;
A10: [#]T1 is open by TOPS_1:53;
A11: x in [#]T1 & z in [#]T1 by A3,A4,PRE_TOPC:12;
   set X = [:[#]T1, V1:];
A12:  X is open by A9,A10,BORSUK_1:46;
       now per cases by A9;
      suppose y in V1 & not v in V1;
hence (p in X & not q in X) or (q in X & not p in X)
        by A3,A4,A11,ZFMISC_1:106;
      end;
      suppose v in V1 & not y in V1;
hence (p in X & not q in X) or (q in X & not p in X)
        by A3,A4,A11,ZFMISC_1:106;
     end;
     end;
   hence ex X being Subset of T st X is open &
     ((p in X & not q in X) or (q in X & not p in X)) by A12;
  end;
  end;
  hence thesis by T_0TOPSP:def 7;
end;
end;


canceled;

theorem :: BORSUK_2:14
  for T1, T2 be non empty TopSpace holds
   T1 is_T1 & T2 is_T1 implies [:T1, T2:] is_T1;

registration let T1, T2 be being_T1 (non empty TopSpace);
 cluster [:T1, T2:] -> being_T1;
 coherence by Th14;
end;

registration let T1, T2 be being_T2 (non empty TopSpace);
 cluster [:T1, T2:] -> being_T2;
 coherence by Lm5;
end;


registration
 cluster I[01] -> compact being_T2;
 coherence
 proof
    I[01] = TopSpaceMetr (Closed-Interval-MSpace(0,1)) by TOPMETR:27,def 8;
  hence thesis by HEINE:11,PCOMPS_1:38,TOPMETR:27;
 end;
end;


registration let n be Nat;
 cluster TOP-REAL n -> being_T2;
 coherence by SPPOL_1:26;
end;


definition let T be non empty arcwise_connected TopSpace;
  let a, b be Point of T;
  let P, Q be Path of a, b;
 pred P, Q are_homotopic means
:: BORSUK_2:def 6
     ex f being map of [:I[01],I[01]:], T st
    f is continuous &
    for s being Point of I[01] holds f.(s,0) = P.s & f.(s,1) = Q.s &
    for t being Point of I[01] holds f.(0,t) = a & f.(1,t) = b;
  reflexivity;
  symmetry;
end;


