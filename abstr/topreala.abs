:: Some Properties of Rectangles on the Plane
::  by Artur Korni{\l}owicz and Yasunari Shidama
::
:: Received October 18, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabulary PRE_TOPC, EUCLID, RELAT_1, ARYTM, ZF_LANG, SUBSET_1, CARD_3,
      TOPS_1, INT_1, FUNCT_1, ORDINAL2, ARYTM_1, MCART_1, T_0TOPSP, TOPS_2,
      ARYTM_3, RCOMP_1, ASYMPT_0, FUNCOP_1, TOPMETR, SGRAPH1, BOOLE, TOPREALA,
      FCONT_1, FINSEQ_1, PARTFUN1, TMAP_1, CONNSP_2, JGRAPH_6, NATTRA_1,
      TDLAT_3, PARTFUN2, FINSEQ_4, MONOID_0;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, ORDINAL1, RELAT_1, FUNCT_1,
      RELSET_1, PARTFUN1, FUNCT_2, SEQ_1, ORDINAL2, NUMBERS, XCMPLX_0, XREAL_0,
      REAL_1, NAT_1, FUNCOP_1, FINSEQ_4, PARTFUN2, INT_1, CARD_3, FINSEQ_1,
      FUNCT_4, MONOID_0, RCOMP_1, RCOMP_2, FCONT_1, STRUCT_0, PRE_TOPC, TOPS_1,
      BORSUK_1, TSEP_1, TOPS_2, TDLAT_3, CONNSP_2, T_0TOPSP, TMAP_1, TOPMETR,
      EUCLID, JGRAPH_6, TOPALG_2;
 constructors GRCAT_1, PARTFUN2, REAL_1, TOPS_2, TOPS_1, BORSUK_3, TOPALG_2,
      COMPTS_1, FCONT_1, REALSET2, CARD_3, TMAP_1, RCOMP_2, FINSEQ_4, JGRAPH_6,
      INTEGRA2, PSCOMP_1, TDLAT_3, MONOID_0, YELLOW_8;
 registrations STRUCT_0, RELSET_1, XREAL_0, REAL_1, BORSUK_1, PRE_TOPC, INT_1,
      TOPALG_2, TOPS_1, YELLOW13, TOPREAL6, BORSUK_6, FINSEQ_1, FUNCT_4,
      TOPGRP_1, TSEP_1, PSCOMP_1, YELLOW12, MONOID_0, NAT_1;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM, REAL;


begin

reserve
  n for Nat,
  i for Integer,
  a, b, r, s for real number;

theorem :: TOPREALA:1
  frac(r+i) = frac r;

theorem :: TOPREALA:2
  r <= a & a < [\r/]+1 implies [\a/] = [\r/];

theorem :: TOPREALA:3
  r <= a & a < [\r/]+1 implies frac r <= frac a;

theorem :: TOPREALA:4
  r < a & a < [\r/]+1 implies frac r < frac a;

theorem :: TOPREALA:5
  a >= [\r/]+1 & a <= r+1 implies [\a/] = [\r/]+1;

theorem :: TOPREALA:6
  a >= [\r/]+1 & a < r+1 implies frac a < frac r;

theorem :: TOPREALA:7
  r <= a & a < r+1 & r <= b & b < r+1 & frac a = frac b implies a = b;

begin :: Subsets of REAL

registration
  let r be real number,
      s be positive (real number);
  cluster ].r,r+s.[ -> non empty;
  cluster [.r,r+s.[ -> non empty;
  cluster ].r,r+s.] -> non empty;
  cluster [.r,r+s.] -> non empty;
  cluster ].r-s,r.[ -> non empty;
  cluster [.r-s,r.[ -> non empty;
  cluster ].r-s,r.] -> non empty;
  cluster [.r-s,r.] -> non empty;
end;

registration
  let r be non positive (real number),
      s be positive (real number);
  cluster ].r,s.[ -> non empty;
  cluster [.r,s.[ -> non empty;
  cluster ].r,s.] -> non empty;
  cluster [.r,s.] -> non empty;
end;

registration
  let r be negative (real number),
      s be non negative (real number);
  cluster ].r,s.[ -> non empty;
  cluster [.r,s.[ -> non empty;
  cluster ].r,s.] -> non empty;
  cluster [.r,s.] -> non empty;
end;

:: r <= a & b <= s implies [.a,b.] c= [.r,s.];  TREAL_1:1
:: r <= a & b <= s implies ].a,b.[ c= ].r,s.[;  JGRAPH_3:9

theorem :: TOPREALA:8
  r <= a & b < s implies [.a,b.] c= [.r,s.[;

theorem :: TOPREALA:9
  r < a & b <= s implies [.a,b.] c= ].r,s.];

theorem :: TOPREALA:10
  r < a & b < s implies [.a,b.] c= ].r,s.[;

theorem :: TOPREALA:11
  r <= a & b <= s implies [.a,b.[ c= [.r,s.];

theorem :: TOPREALA:12
  r <= a & b <= s implies [.a,b.[ c= [.r,s.[;

theorem :: TOPREALA:13
  r < a & b <= s implies [.a,b.[ c= ].r,s.];

theorem :: TOPREALA:14
  r < a & b <= s implies [.a,b.[ c= ].r,s.[;

theorem :: TOPREALA:15
  r <= a & b <= s implies ].a,b.] c= [.r,s.];

theorem :: TOPREALA:16
  r <= a & b < s implies ].a,b.] c= [.r,s.[;

theorem :: TOPREALA:17
  r <= a & b <= s implies ].a,b.] c= ].r,s.];

theorem :: TOPREALA:18
  r <= a & b < s implies ].a,b.] c= ].r,s.[;

theorem :: TOPREALA:19
  r <= a & b <= s implies ].a,b.[ c= [.r,s.];

theorem :: TOPREALA:20
  r <= a & b <= s implies ].a,b.[ c= [.r,s.[;

theorem :: TOPREALA:21
  r <= a & b <= s implies ].a,b.[ c= ].r,s.];

begin :: Functions

theorem :: TOPREALA:22
  for f being Function, x, X being set st
    x in dom f & f.x in f.:X & f is one-to-one
  holds x in X;

theorem :: TOPREALA:23
  for f being FinSequence, i being natural number st i+1 in dom f holds
  i in dom f or i = 0;

theorem :: TOPREALA:24
  for x, y, X, Y being set, f being Function
    st x <> y & f in product ((x,y) --> (X,Y)) holds
  f.x in X & f.y in Y;

theorem :: TOPREALA:25
  for a, b being set holds <*a,b*> = (1,2) --> (a,b);

begin :: General topology

registration
  cluster constituted-FinSeqs non empty strict TopSpace;
end;

registration
  let T be constituted-FinSeqs TopSpace;
  cluster -> constituted-FinSeqs SubSpace of T;
end;

theorem :: TOPREALA:26
  for T being non empty TopSpace, Z being non empty SubSpace of T,
      t being Point of T, z being Point of Z,
      N being open a_neighborhood of t,
      M being Subset of Z st t = z & M = N /\ [#]Z
   holds M is open a_neighborhood of z;

registration
  cluster empty -> discrete anti-discrete TopSpace;
end;

registration
  let X be discrete TopSpace, Y be TopSpace;
  cluster -> continuous map of X,Y;
end;

theorem :: TOPREALA:27
  for X being TopSpace, Y being TopStruct, f being map of X,Y st f is empty
  holds f is continuous;

registration
  let X be TopSpace, Y be TopStruct;
  cluster empty -> continuous map of X,Y;
end;

theorem :: TOPREALA:28
  for X being TopStruct, Y being non empty TopStruct,
      Z being non empty SubSpace of Y,
      f being map of X,Z
  holds f is map of X,Y;

theorem :: TOPREALA:29
  for S, T being non empty TopSpace,
      X being Subset of S, Y being Subset of T,
      f being continuous map of S,T,
      g being map of S|X,T|Y st g = f|X
  holds g is continuous;

theorem :: TOPREALA:30
  for S, T being non empty TopSpace, Z being non empty SubSpace of T,
      f being map of S,T,
      g being map of S,Z st f = g & f is open holds
   g is open;

theorem :: TOPREALA:31
  for S, T being non empty TopSpace,
      S1 being Subset of S, T1 being Subset of T,
      f being map of S,T, g being map of S|S1,T|T1
  st g = f|S1 & g is onto & f is open one-to-one
  holds g is open;

theorem :: TOPREALA:32
  for X, Y, Z being non empty TopSpace,
      f being map of X,Y, g being map of Y,Z st f is open & g is open
   holds g*f is open;

theorem :: TOPREALA:33
  for X, Y being TopSpace, Z being open SubSpace of Y,
      f being map of X, Y,
      g being map of X, Z st f = g & g is open
  holds f is open;

theorem :: TOPREALA:34
  for S, T being non empty TopSpace,
      f being map of S,T st f is one-to-one onto
  holds f is continuous iff f" is open;

theorem :: TOPREALA:35
  for S, T being non empty TopSpace,
      f being map of S,T st f is one-to-one onto
  holds f is open iff f" is continuous;

theorem :: TOPREALA:36
  for S being TopSpace, T being non empty TopSpace holds
  S,T are_homeomorphic iff
  the TopStruct of S, the TopStruct of T are_homeomorphic;

theorem :: TOPREALA:37
  for S, T being non empty TopSpace,
      f being map of S,T st f is one-to-one onto continuous open
  holds f is_homeomorphism;

begin :: R^1

theorem :: TOPREALA:38
  for f being PartFunc of REAL,REAL st f = REAL --> r holds
  f is_continuous_on REAL;

theorem :: TOPREALA:39
  for f, f1, f2 being PartFunc of REAL,REAL st
   dom f = dom f1 \/ dom f2 & dom f1 is open & dom f2 is open &
   f1 is_continuous_on dom f1 & f2 is_continuous_on dom f2 &
   (for z being set st z in dom f1 holds f.z = f1.z) &
   (for z being set st z in dom f2 holds f.z = f2.z) holds
  f is_continuous_on dom f;

theorem :: TOPREALA:40
  for x being Point of R^1,
      N being Subset of REAL, M being Subset of R^1 st M = N holds
   N is Neighbourhood of x implies M is a_neighborhood of x;

theorem :: TOPREALA:41
  for x being Point of R^1, M being a_neighborhood of x
   ex N being Neighbourhood of x st N c= M;

theorem :: TOPREALA:42
  for f being map of R^1,R^1, g being PartFunc of REAL,REAL,
      x being Point of R^1 st f = g & g is_continuous_in x holds
   f is_continuous_at x;

theorem :: TOPREALA:43
  for f being map of R^1,R^1, g being Function of REAL,REAL st
      f = g & g is_continuous_on REAL holds
   f is continuous;

theorem :: TOPREALA:44
  a <= r & s <= b implies
  [.r,s.] is closed Subset of Closed-Interval-TSpace(a,b);

theorem :: TOPREALA:45
  a <= r & s <= b implies
  ].r,s.[ is open Subset of Closed-Interval-TSpace(a,b);

theorem :: TOPREALA:46
  a <= b & a <= r implies
  ].r,b.] is open Subset of Closed-Interval-TSpace(a,b);

theorem :: TOPREALA:47
  a <= b & r <= b implies
  [.a,r.[ is open Subset of Closed-Interval-TSpace(a,b);

theorem :: TOPREALA:48
  a <= b & r <= s implies
  the carrier of [:Closed-Interval-TSpace(a,b),Closed-Interval-TSpace(r,s):]
    = [: [.a,b.], [.r,s.] :];

begin :: TOP-REAL 2

theorem :: TOPREALA:49
  |[a,b]| = (1,2) --> (a,b);

theorem :: TOPREALA:50
  |[a,b]|.1 = a & |[a,b]|.2 = b;

theorem :: TOPREALA:51
  closed_inside_of_rectangle(a,b,r,s) = product ((1,2)-->([.a,b.],[.r,s.]));

theorem :: TOPREALA:52
  a <= b & r <= s implies
  |[a,r]| in closed_inside_of_rectangle(a,b,r,s);

definition
  let a, b, c, d be real number;
  func Trectangle(a,b,c,d) -> SubSpace of TOP-REAL 2 equals
:: TOPREALA:def 1
  (TOP-REAL 2) | closed_inside_of_rectangle(a,b,c,d);
end;

theorem :: TOPREALA:53
  the carrier of Trectangle(a,b,r,s) = closed_inside_of_rectangle(a,b,r,s);

theorem :: TOPREALA:54
  a <= b & r <= s implies Trectangle(a,b,r,s) is non empty;

registration
  let a, c be non positive (real number);
  let b, d be non negative (real number);
  cluster Trectangle(a,b,c,d) -> non empty;
end;

definition
  func R2Homeomorphism -> map of [:R^1,R^1:], TOP-REAL 2 means
:: TOPREALA:def 2
  for x, y being real number holds it. [x,y] = <*x,y*>;
end;

theorem :: TOPREALA:55
  for A, B being Subset of REAL holds
  R2Homeomorphism.:[:A,B:] = product ((1,2) --> (A,B));

theorem :: TOPREALA:56
  R2Homeomorphism is_homeomorphism;

theorem :: TOPREALA:57
  a <= b & r <= s implies
  R2Homeomorphism | the carrier of
      [:Closed-Interval-TSpace(a,b),Closed-Interval-TSpace(r,s):]
  is map of [:Closed-Interval-TSpace(a,b),Closed-Interval-TSpace(r,s):],
    Trectangle(a,b,r,s);

theorem :: TOPREALA:58
  a <= b & r <= s implies
  for h being map of
    [:Closed-Interval-TSpace(a,b),Closed-Interval-TSpace(r,s):],
    Trectangle(a,b,r,s) st
  h = R2Homeomorphism | the carrier of
      [:Closed-Interval-TSpace(a,b),Closed-Interval-TSpace(r,s):]
  holds h is_homeomorphism;

theorem :: TOPREALA:59
  a <= b & r <= s implies
  [:Closed-Interval-TSpace(a,b),Closed-Interval-TSpace(r,s):],
  Trectangle(a,b,r,s) are_homeomorphic;

theorem :: TOPREALA:60
  a <= b & r <= s implies
  for A being Subset of Closed-Interval-TSpace(a,b),
      B being Subset of Closed-Interval-TSpace(r,s) holds
  product ((1,2)-->(A,B)) is Subset of Trectangle(a,b,r,s);

theorem :: TOPREALA:61
  a <= b & r <= s implies
  for A being open Subset of Closed-Interval-TSpace(a,b),
      B being open Subset of Closed-Interval-TSpace(r,s) holds
  product ((1,2)-->(A,B)) is open Subset of Trectangle(a,b,r,s);

theorem :: TOPREALA:62
  a <= b & r <= s implies
  for A being closed Subset of Closed-Interval-TSpace(a,b),
      B being closed Subset of Closed-Interval-TSpace(r,s) holds
  product ((1,2)-->(A,B)) is closed Subset of Trectangle(a,b,r,s);

