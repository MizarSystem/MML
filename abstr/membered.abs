:: On the Sets Inhabited by Numbers
::  by Andrzej Trybulec
::
:: Received August 23, 2003
:: Copyright (c) 2003 Association of Mizar Users

environ

 vocabulary MEMBERED, XCMPLX_0, ARYTM, INT_1, ORDINAL2, BOOLE, COMPLEX1, RAT_1,
      ORDINAL1, TARSKI, SETFAM_1;
 notation TARSKI, XBOOLE_0, ENUMSET1, SUBSET_1, SETFAM_1, NUMBERS, XCMPLX_0,
      XREAL_0, RAT_1, INT_1, ORDINAL1, ORDINAL2;
 constructors XCMPLX_0, RAT_1, ENUMSET1, SETFAM_1;
 clusters INT_1, NUMBERS, ARYTM_3, XREAL_0, RAT_1, XBOOLE_0;
 requirements NUMERALS, BOOLE, SUBSET;


begin

reserve x, X, F for set;

definition let X be set;
 attr X is complex-membered means
:: MEMBERED:def 1
 x in X implies x is complex;
 attr X is real-membered means
:: MEMBERED:def 2
 x in X implies x is real;
 attr X is rational-membered means
:: MEMBERED:def 3
 x in X implies x is rational;
 attr X is integer-membered means
:: MEMBERED:def 4
 x in X implies x is integer;
 attr X is natural-membered means
:: MEMBERED:def 5
 x in X implies x is natural;
end;


registration
 cluster natural-membered -> integer-membered set;
 coherence
  proof let X;
   assume
A1:  X is natural-membered;
   let x;
   assume x in X;
    then x is natural by A1,Def5;
    then x in omega by ORDINAL2:def 21;
   hence x is Element of INT by INT_1:14;
  end;
 cluster integer-membered -> rational-membered set;
 coherence
  proof let X;
   assume
A2:  X is integer-membered;
   let x;
   assume x in X;
    then x is integer by A2,Def4;
    then x is Element of INT by INT_1:def 2;
    then x in INT;
   hence x in RAT by RAT_1:11;
  end;
 cluster rational-membered -> real-membered set;
 coherence
  proof let X;
   assume
A3:  X is rational-membered;
   let x;
   assume x in X;
    then x is rational by A3,Def3;
    then x in RAT by RAT_1:def 2;
   hence x in REAL by RAT_1:4;
  end;
 cluster real-membered -> complex-membered set;
 coherence
  proof let X;
   assume
A4:  X is real-membered;
   let x;
   assume x in X;
    then x is real by A4,Def2;
    then x in REAL by XREAL_0:def 1;
   hence x in COMPLEX by NUMBERS:11;
  end;
end;


registration
 cluster non empty natural-membered set;
 existence
  proof take 1;
    thus 1 is non empty;
   let x;
   assume x in 1;
   hence thesis by Lm1,TARSKI:def 1;
  end;
end;


registration
 cluster -> complex-membered Subset of COMPLEX;
 coherence
  proof let S be Subset of COMPLEX;
   let x;
   assume x in S;
   hence x is complex by XCMPLX_0:def 2;
  end;
 cluster -> real-membered Subset of REAL;
 coherence
  proof let S be Subset of REAL;
   let x;
   thus thesis by XREAL_0:def 1;
  end;
 cluster -> rational-membered Subset of RAT;
 coherence
  proof let S be Subset of RAT;
   let x;
   thus thesis by RAT_1:def 2;
  end;
 cluster -> integer-membered Subset of INT;
 coherence
  proof let S be Subset of INT;
   let x;
   thus thesis by INT_1:def 2;
  end;
 cluster -> natural-membered Subset of NAT;
 coherence
  proof let S be Subset of NAT;
   let x;
   thus thesis by ORDINAL2:def 21;
  end;
end;


registration
 cluster COMPLEX -> complex-membered;
 coherence
  proof let x;
   thus thesis by XCMPLX_0:def 2;
  end;
 cluster REAL -> real-membered;
 coherence
  proof let x;
   thus thesis by XREAL_0:def 1;
  end;
 cluster RAT -> rational-membered;
 coherence
  proof let x;
   thus thesis by RAT_1:def 2;
  end;
 cluster INT -> integer-membered;
 coherence
  proof let x;
   thus thesis by INT_1:def 2;
  end;
 cluster NAT -> natural-membered;
 coherence
  proof let x;
   thus thesis by ORDINAL2:def 21;
  end;
end;


theorem :: MEMBERED:1
 X is complex-membered implies X c= COMPLEX;

theorem :: MEMBERED:2
 X is real-membered implies X c= REAL;

theorem :: MEMBERED:3
 X is rational-membered implies X c= RAT;

theorem :: MEMBERED:4
 X is integer-membered implies X c= INT;

theorem :: MEMBERED:5
 X is natural-membered implies X c= NAT;

registration let X be complex-membered set;
 cluster -> complex Element of X;
 coherence
  proof let e be Element of X;
   per cases;
   suppose X is empty;
    then e = 0 by SUBSET_1:def 2;
   hence e is complex;
   end;
   suppose X is non empty;
   hence e is complex by Def1;
  end;
  end;
end;


registration let X be real-membered set;
 cluster -> real Element of X;
 coherence
  proof let e be Element of X;
   per cases;
   suppose X is empty;
    then e = 0 by SUBSET_1:def 2;
   hence e is real;
   end;
   suppose X is non empty;
   hence e is real by Def2;
  end;
  end;
end;


registration let X be rational-membered set;
 cluster -> rational Element of X;
 coherence
  proof let e be Element of X;
   per cases;
   suppose X is empty;
    then e = 0 by SUBSET_1:def 2;
    then e is Element of INT by INT_1:def 2;
    then e in INT;
   hence e is rational by RAT_1:11,def 2;
   end;
   suppose X is non empty;
   hence e is rational by Def3;
  end;
  end;
end;


registration let X be integer-membered set;
 cluster -> integer Element of X;
 coherence
  proof let e be Element of X;
   per cases;
   suppose X is empty;
    then e = 0 by SUBSET_1:def 2;
   hence e is integer;
   end;
   suppose X is non empty;
   hence e is integer by Def4;
  end;
  end;
end;


registration let X be natural-membered set;
 cluster -> natural Element of X;
 coherence
  proof let e be Element of X;
   per cases;
   suppose X is empty;
    then e = 0 by SUBSET_1:def 2;
   hence e is natural;
   end;
   suppose X is non empty;
   hence e is natural by Def5;
  end;
  end;
end;


reserve c, c1, c2, c3 for complex number,
        r, r1, r2, r3 for real number,
        w, w1, w2, w3 for rational number,
        i, i1, i2, i3 for integer number,
        n, n1, n2, n3 for natural number;

theorem :: MEMBERED:6
 for X being non empty complex-membered set
  ex c st c in X;

theorem :: MEMBERED:7
 for X being non empty real-membered set
  ex r st r in X;

theorem :: MEMBERED:8
 for X being non empty rational-membered set
  ex w st w in X;

theorem :: MEMBERED:9
 for X being non empty integer-membered set
  ex i st i in X;

theorem :: MEMBERED:10
 for X being non empty natural-membered set
  ex n st n in X;

theorem :: MEMBERED:11
 for X being complex-membered set
  st for c holds c in X
 holds X = COMPLEX;

theorem :: MEMBERED:12
 for X being real-membered set
  st for r holds r in X
 holds X = REAL;

theorem :: MEMBERED:13
 for X being rational-membered set
  st for w holds w in X
 holds X = RAT;

theorem :: MEMBERED:14
 for X being integer-membered set
  st for i holds i in X
 holds X = INT;

theorem :: MEMBERED:15
 for X being natural-membered set
  st for n holds n in X
 holds X = NAT;

theorem :: MEMBERED:16
 for Y being complex-membered set st X c= Y
  holds X is complex-membered;

theorem :: MEMBERED:17
 for Y being real-membered set st X c= Y
  holds X is real-membered;

theorem :: MEMBERED:18
 for Y being rational-membered set st X c= Y
  holds X is rational-membered;

theorem :: MEMBERED:19
 for Y being integer-membered set st X c= Y
  holds X is integer-membered;

theorem :: MEMBERED:20
 for Y being natural-membered set st X c= Y
  holds X is natural-membered;

registration
 cluster {} -> natural-membered;
 coherence proof let x; thus thesis; end;
end;


registration
 cluster empty -> natural-membered set;
 coherence;
end;


registration let c;
 cluster {c} -> complex-membered;
 coherence
  proof let e be set;
   thus thesis by TARSKI:def 1;
  end;
end;


registration let r;
 cluster {r} -> real-membered;
 coherence
  proof let e be set;
   thus thesis by TARSKI:def 1;
  end;
end;


registration let w;
 cluster {w} -> rational-membered;
 coherence
  proof let e be set;
   thus thesis by TARSKI:def 1;
  end;
end;


registration let i;
 cluster {i} -> integer-membered;
 coherence
  proof let e be set;
   thus thesis by TARSKI:def 1;
  end;
end;


registration let n;
 cluster {n} -> natural-membered;
 coherence
  proof let e be set;
   thus thesis by TARSKI:def 1;
  end;
end;


registration let c1,c2;
 cluster {c1,c2} -> complex-membered;
 coherence
  proof let e be set;
   thus thesis by TARSKI:def 2;
  end;
end;


registration let r1,r2;
 cluster {r1,r2} -> real-membered;
 coherence
  proof let e be set;
   thus thesis by TARSKI:def 2;
  end;
end;


registration let w1,w2;
 cluster {w1,w2} -> rational-membered;
 coherence
  proof let e be set;
   thus thesis by TARSKI:def 2;
  end;
end;


registration let i1,i2;
 cluster {i1,i2} -> integer-membered;
 coherence
  proof let e be set;
   thus thesis by TARSKI:def 2;
  end;
end;


registration let n1,n2;
 cluster {n1,n2} -> natural-membered;
 coherence
  proof let e be set;
   thus thesis by TARSKI:def 2;
  end;
end;


registration let c1,c2,c3;
 cluster {c1,c2,c3} -> complex-membered;
 coherence
  proof let e be set;
   thus thesis by ENUMSET1:def 1;
  end;
end;


registration let r1,r2,r3;
 cluster {r1,r2,r3} -> real-membered;
 coherence
  proof let e be set;
   thus thesis by ENUMSET1:def 1;
  end;
end;


registration let w1,w2,w3;
 cluster {w1,w2,w3} -> rational-membered;
 coherence
  proof let e be set;
   thus thesis by ENUMSET1:def 1;
  end;
end;


registration let i1,i2,i3;
 cluster {i1,i2,i3} -> integer-membered;
 coherence
  proof let e be set;
   thus thesis by ENUMSET1:def 1;
  end;
end;


registration let n1,n2,n3;
 cluster {n1,n2,n3} -> natural-membered;
 coherence
  proof let e be set;
   thus thesis by ENUMSET1:def 1;
  end;
end;


registration let X be complex-membered set;
 cluster -> complex-membered Subset of X;
 coherence
  proof let S be Subset of X, x;
   thus thesis by Def1;
  end;
end;


registration let X be real-membered set;
 cluster -> real-membered Subset of X;
 coherence
  proof let S be Subset of X, x;
   thus thesis by Def2;
  end;
end;


registration let X be rational-membered set;
 cluster -> rational-membered Subset of X;
 coherence
  proof let S be Subset of X, x;
   thus thesis by Def3;
  end;
end;


registration let X be integer-membered set;
 cluster -> integer-membered Subset of X;
 coherence
  proof let S be Subset of X, x;
   thus thesis by Def4;
  end;
end;


registration let X be natural-membered set;
 cluster -> natural-membered Subset of X;
 coherence
  proof let S be Subset of X, x;
   thus thesis by Def5;
  end;
end;


registration let X,Y be complex-membered set;
 cluster X \/ Y -> complex-membered;
 coherence
  proof let x;
   assume x in X \/ Y;
    then x in X or x in Y by XBOOLE_0:def 2;
   hence thesis by Def1;
  end;
end;


registration let X,Y be real-membered set;
 cluster X \/ Y -> real-membered;
 coherence
  proof let x;
   assume x in X \/ Y;
    then x in X or x in Y by XBOOLE_0:def 2;
   hence thesis by Def2;
  end;
end;


registration let X,Y be rational-membered set;
 cluster X \/ Y -> rational-membered;
 coherence
  proof let x;
   assume x in X \/ Y;
    then x in X or x in Y by XBOOLE_0:def 2;
   hence thesis by Def3;
  end;
end;


registration let X,Y be integer-membered set;
 cluster X \/ Y -> integer-membered;
 coherence
  proof let x;
   assume x in X \/ Y;
    then x in X or x in Y by XBOOLE_0:def 2;
   hence thesis by Def4;
  end;
end;


registration let X,Y be natural-membered set;
 cluster X \/ Y -> natural-membered;
 coherence
  proof let x;
   assume x in X \/ Y;
    then x in X or x in Y by XBOOLE_0:def 2;
   hence thesis by Def5;
  end;
end;


registration let X be complex-membered set, Y be set;
 cluster X /\ Y -> complex-membered;
 coherence
  proof X /\ Y c= X by XBOOLE_1:17;
   hence thesis by Th16;
  end;
 cluster Y /\ X -> complex-membered;
 coherence
  proof Y /\ X c= X by XBOOLE_1:17;
   hence thesis by Th16;
  end;
end;


registration let X be real-membered set, Y be set;
 cluster X /\ Y -> real-membered;
 coherence
  proof X /\ Y c= X by XBOOLE_1:17;
   hence thesis by Th17;
  end;
 cluster Y /\ X -> real-membered;
 coherence
  proof Y /\ X c= X by XBOOLE_1:17;
   hence thesis by Th17;
  end;
end;


registration let X be rational-membered set, Y be set;
 cluster X /\ Y -> rational-membered;
 coherence
  proof X /\ Y c= X by XBOOLE_1:17;
   hence thesis by Th18;
  end;
 cluster Y /\ X -> rational-membered;
 coherence
  proof Y /\ X c= X by XBOOLE_1:17;
   hence thesis by Th18;
  end;
end;


registration let X be integer-membered set, Y be set;
 cluster X /\ Y -> integer-membered;
 coherence
  proof X /\ Y c= X by XBOOLE_1:17;
   hence thesis by Th19;
  end;
 cluster Y /\ X -> integer-membered;
 coherence
  proof Y /\ X c= X by XBOOLE_1:17;
   hence thesis by Th19;
  end;
end;


registration let X be natural-membered set, Y be set;
 cluster X /\ Y -> natural-membered;
 coherence
  proof X /\ Y c= X by XBOOLE_1:17;
   hence thesis by Th20;
  end;
 cluster Y /\ X -> natural-membered;
 coherence
  proof Y /\ X c= X by XBOOLE_1:17;
   hence thesis by Th20;
  end;
end;


registration let X be complex-membered set, Y be set;
 cluster X \ Y -> complex-membered;
 coherence
  proof X \ Y c= X by XBOOLE_1:36;
   hence thesis by Th16;
  end;
end;


registration let X be real-membered set, Y be set;
 cluster X \ Y -> real-membered;
 coherence
  proof X \ Y c= X by XBOOLE_1:36;
   hence thesis by Th17;
  end;
end;


registration let X be rational-membered set, Y be set;
 cluster X \ Y -> rational-membered;
 coherence
  proof X \ Y c= X by XBOOLE_1:36;
   hence thesis by Th18;
  end;
end;


registration let X be integer-membered set, Y be set;
 cluster X \ Y -> integer-membered;
 coherence
  proof X \ Y c= X by XBOOLE_1:36;
   hence thesis by Th19;
  end;
end;


registration let X be natural-membered set, Y be set;
 cluster X \ Y -> natural-membered;
 coherence
  proof X \ Y c= X by XBOOLE_1:36;
   hence thesis by Th20;
  end;
end;


registration let X,Y be complex-membered set;
 cluster X \+\ Y -> complex-membered;
 coherence
  proof X \+\ Y = (X \ Y) \/ (Y \ X) by XBOOLE_0:def 6;
   hence thesis;
  end;
end;


registration let X,Y be real-membered set;
 cluster X \+\ Y -> real-membered;
 coherence
  proof X \+\ Y = (X \ Y) \/ (Y \ X) by XBOOLE_0:def 6;
   hence thesis;
  end;
end;


registration let X,Y be rational-membered set;
 cluster X \+\ Y -> rational-membered;
 coherence
  proof X \+\ Y = (X \ Y) \/ (Y \ X) by XBOOLE_0:def 6;
   hence thesis;
  end;
end;


registration let X,Y be integer-membered set;
 cluster X \+\ Y -> integer-membered;
 coherence
  proof X \+\ Y = (X \ Y) \/ (Y \ X) by XBOOLE_0:def 6;
   hence thesis;
  end;
end;


registration let X,Y be natural-membered set;
 cluster X \+\ Y -> natural-membered;
 coherence
  proof X \+\ Y = (X \ Y) \/ (Y \ X) by XBOOLE_0:def 6;
   hence thesis;
  end;
end;


definition let X,Y be complex-membered set;
 redefine pred X c= Y means
:: MEMBERED:def 6
 c in X implies c in Y;
end;


definition let X,Y be real-membered set;
 redefine pred X c= Y means
:: MEMBERED:def 7
 r in X implies r in Y;
end;


definition let X,Y be rational-membered set;
 redefine pred X c= Y means
:: MEMBERED:def 8
 w in X implies w in Y;
end;


definition let X,Y be integer-membered set;
 redefine pred X c= Y means
:: MEMBERED:def 9
 i in X implies i in Y;
end;


definition let X,Y be natural-membered set;
 redefine pred X c= Y means
:: MEMBERED:def 10
 n in X implies n in Y;
end;


definition let X,Y be complex-membered set;
 redefine pred X = Y means
:: MEMBERED:def 11
   c in X iff c in Y;
end;


definition let X,Y be real-membered set;
 redefine pred X = Y means
:: MEMBERED:def 12
   r in X iff r in Y;
end;


definition let X,Y be rational-membered set;
 redefine pred X = Y means
:: MEMBERED:def 13
   w in X iff w in Y;
end;


definition let X,Y be integer-membered set;
 redefine pred X = Y means
:: MEMBERED:def 14
   i in X iff i in Y;
end;


definition let X,Y be natural-membered set;
 redefine pred X = Y means
:: MEMBERED:def 15
   n in X iff n in Y;
end;


definition let X,Y be complex-membered set;
 redefine pred X meets Y means
:: MEMBERED:def 16
   ex c st c in X & c in Y;
end;


definition let X,Y be real-membered set;
 redefine pred X meets Y means
:: MEMBERED:def 17
   ex r st r in X & r in Y;
end;


definition let X,Y be rational-membered set;
 redefine pred X meets Y means
:: MEMBERED:def 18
   ex w st w in X & w in Y;
end;


definition let X,Y be integer-membered set;
 redefine pred X meets Y means
:: MEMBERED:def 19
   ex i st i in X & i in Y;
end;


definition let X,Y be natural-membered set;
 redefine pred X meets Y means
:: MEMBERED:def 20
   ex n st n in X & n in Y;
end;


theorem :: MEMBERED:21
 (for X st X in F holds X is complex-membered)
   implies union F is complex-membered;

theorem :: MEMBERED:22
 (for X st X in F holds X is real-membered)
   implies union F is real-membered;

theorem :: MEMBERED:23
 (for X st X in F holds X is rational-membered)
   implies union F is rational-membered;

theorem :: MEMBERED:24
 (for X st X in F holds X is integer-membered)
   implies union F is integer-membered;

theorem :: MEMBERED:25
 (for X st X in F holds X is natural-membered)
   implies union F is natural-membered;

theorem :: MEMBERED:26
 for X st X in F & X is complex-membered
  holds meet F is complex-membered;

theorem :: MEMBERED:27
 for X st X in F & X is real-membered
  holds meet F is real-membered;

theorem :: MEMBERED:28
 for X st X in F & X is rational-membered
  holds meet F is rational-membered;

theorem :: MEMBERED:29
 for X st X in F & X is integer-membered
  holds meet F is integer-membered;

theorem :: MEMBERED:30
 for X st X in F & X is natural-membered
  holds meet F is natural-membered;

scheme :: MEMBERED:sch 1
 CM_Separation {P[set]}:
 ex X being complex-membered set st
  for c holds c in X iff P[c]
proof
  defpred _P[set] means P[$1];
   consider X being set such that
A1:  x in X iff x in COMPLEX & _P[x] from XBOOLE_0:sch 1;
   X is complex-membered
    proof let x;
     assume x in X;
      then x in COMPLEX by A1;
     hence thesis by XCMPLX_0:def 2;
    end;
   then reconsider X as complex-membered set;
  take X; let c;
   c in COMPLEX by XCMPLX_0:def 2;
  hence thesis by A1;
end;

scheme :: MEMBERED:sch 2
 RM_Separation {P[set]}:
 ex X being real-membered set st
  for r holds r in X iff P[r]
proof
  defpred _P[set] means P[$1];
   consider X being set such that
A1:  x in X iff x in REAL & _P[x] from XBOOLE_0:sch 1;
   X is real-membered
    proof let x;
     assume x in X;
      then x in REAL by A1;
     hence thesis by XREAL_0:def 1;
    end;
   then reconsider X as real-membered set;
  take X; let r;
   r in REAL by XREAL_0:def 1;
  hence thesis by A1;
end;

scheme :: MEMBERED:sch 3
 WM_Separation {P[set]}:
 ex X being rational-membered set st
  for w holds w in X iff P[w]
proof
  defpred _P[set] means P[$1];
   consider X being set such that
A1:  x in X iff x in RAT & _P[x] from XBOOLE_0:sch 1;
   X is rational-membered
    proof let x;
     assume x in X;
      then x in RAT by A1;
     hence thesis by RAT_1:def 2;
    end;
   then reconsider X as rational-membered set;
  take X; let w;
   w in RAT by RAT_1:def 2;
  hence thesis by A1;
end;

scheme :: MEMBERED:sch 4
 IM_Separation {P[set]}:
 ex X being integer-membered set st
  for i holds i in X iff P[i]
proof
  defpred _P[set] means P[$1];
   consider X being set such that
A1:  x in X iff x in INT & _P[x] from XBOOLE_0:sch 1;
   X is integer-membered
    proof let x;
     assume x in X;
      then x in INT by A1;
     hence thesis by INT_1:def 2;
    end;
   then reconsider X as integer-membered set;
  take X; let i;
  i in INT by INT_1:12;
  hence thesis by A1;
end;

scheme :: MEMBERED:sch 5
 NM_Separation {P[set]}:
 ex X being natural-membered set st
  for n holds n in X iff P[n]
proof
  defpred _P[set] means P[$1];
   consider X being set such that
A1:  x in X iff x in NAT & _P[x] from XBOOLE_0:sch 1;
   X is natural-membered
    proof let x;
     assume x in X;
      then x in NAT by A1;
     hence thesis by ORDINAL2:def 21;
    end;
   then reconsider X as natural-membered set;
  take X; let n;
   n in NAT by ORDINAL2:def 21;
  hence thesis by A1;
end;
