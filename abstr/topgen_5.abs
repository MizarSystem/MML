:: Niemytzki Plane -- an Example of {T}ychonoff Space Which Is Not $T_4$
::  by Grzegorz Bancerek
::
:: Received November 7, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies FINSEQ_1, PRE_TOPC, ARYTM, FUNCT_1, BOOLE, SETFAM_1, ORDINAL2,
      EUCLID, METRIC_1, TOPGEN_2, PROB_1, PBOOLE, CANTOR_1, RELAT_1, FINSET_1,
      ZF_REFLE, TARSKI, MCART_1, ASYMPT_0, COMPLEX1, ARYTM_1, ARYTM_3, RCOMP_1,
      ABSVALUE, ZF_LANG, SQUARE_1, RLVECT_3, SUBSET_1, TOPS_1, PARTFUN1, RAT_1,
      TOPGEN_1, TOPGEN_3, CARD_1, CARD_2, CARD_3, URYSOHN1, TOPMETR, MEMBERED,
      TOPS_2, SEQ_1, PRALG_1, FUNCT_2, CAT_1, LATTICES, SEQ_2, FUNCT_5,
      LIMFUNC1, COMPTS_1, BORSUK_1, TOPGEN_5, FUNCOP_1, FUNCT_4, PRALG_2;
 notations MEMBERED, TARSKI, XBOOLE_0, ZFMISC_1, RELAT_1, FINSET_1, SUBSET_1,
      MCART_1, SETFAM_1, RELSET_1, FUNCT_1, PARTFUN1, FUNCT_2, BINOP_1,
      FINSEQ_1, FUNCT_4, FUNCOP_1, SEQ_1, CQC_LANG, PBOOLE, ORDINAL1, CARD_1,
      CARD_2, CARD_3, FUNCT_5, FUNCT_6, NAT_1, NUMBERS, XCMPLX_0, SQUARE_1,
      REAL_1, COMPLEX1, XREAL_0, SEQ_4, RAT_1, RCOMP_1, RCOMP_2, PRE_CIRC,
      LIMFUNC1, STRUCT_0, PRE_TOPC, TOPS_1, TOPS_2, EUCLID, CANTOR_1, TOPRNS_1,
      BORSUK_1, TOPMETR, TOPREAL9, URYSOHN1, COMPTS_1, TOPGEN_1, TOPGEN_2,
      TOPGEN_3, XXREAL_0;
 constructors TOPREAL9, TOPGEN_2, FUNCT_4, PROB_1, TOPRNS_1, REAL_1, COMPLEX1,
      TOPS_1, TOPGEN_1, TOPGEN_3, RAT_1, RCOMP_2, CARD_2, WELLORD2, RCOMP_1,
      URYSOHN1, COMPTS_1, CONNSP_1, BORSUK_6, TOPS_2, FUNCT_6, CQC_LANG,
      SETFAM_1, PSCOMP_1, FUNCT_5, LIMFUNC1, REALSET1, TSP_1, XXREAL_0;
 registrations XREAL_0, STRUCT_0, PRE_TOPC, EUCLID, ASYMPT_0, RAT_1, SUBSET_1,
      XCMPLX_0, PBOOLE, COMPLSP1, FINSEQ_1, SQUARE_1, TOPS_1, TOPGRP_1,
      TOPGEN_1, CARD_1, RELSET_1, BORSUK_1, BORSUK_6, TIETZE, FUNCOP_1,
      FINSET_1, MEMBERED, RCOMP_3, FUNCT_1, CQC_LANG, FUNCT_2, RELAT_1,
      SPRECT_1, PSCOMP_1;
 requirements BOOLE, SUBSET, NUMERALS, ARITHM, REAL;


begin :: Preliminaries

 reserve x,y for Element of REAL, u,v,w for set, r for positive (real number);

theorem :: TOPGEN_5:1
  for f,g being Function
  st f tolerates g
  for A being set holds (f+*g)"A = (f"A)\/(g"A);

theorem :: TOPGEN_5:2
  for f,g being Function
  st dom f misses dom g
  for A being set holds (f+*g)"A = (f"A)\/(g"A);

registration
  let X be set;
  let Y be real-membered set;
  cluster -> real-yielding Relation of X,Y;
end;

theorem :: TOPGEN_5:3
  for x,a being set, f being Function st a in dom f
  holds (commute (x .--> f)).a = x .--> f.a;

theorem :: TOPGEN_5:4
  for b being set, f being Function
  holds b in dom commute f iff
  ex a being set, g being Function st a in dom f & g = f.a & b in dom g;

theorem :: TOPGEN_5:5
  for a,b being set, f being Function
  holds a in dom ((commute f).b) iff
  ex g being Function st a in dom f & g = f.a & b in dom g;

theorem :: TOPGEN_5:6
  for a,b being set, f,g being Function st a in dom f & g = f.a & b in dom g
  holds (commute f).b.a = g.b;

theorem :: TOPGEN_5:7
  for a being set, f,g,h being Function st h = f \/ g
  holds (commute h).a = (commute f).a \/ (commute g).a;

registration
  cluster -> bounded (finite Subset of REAL);
end;

theorem :: TOPGEN_5:8
 for a,b,c,d being real number st a < b & c <= d
      holds ].a,c.[ /\ [.b,d.] = [.b,c.[;

theorem :: TOPGEN_5:9
 for a,b,c,d being real number st a >= b & c > d
      holds ].a,c.[ /\ [.b,d.] = ].a,d.];

theorem :: TOPGEN_5:10
 for a,b,c,d being real number st a <= b & b < c & c <= d
      holds [.a,c.[ \/ ].b,d.] = [.a,d.];

theorem :: TOPGEN_5:11
 for a,b,c,d being real number st a <= b & b < c & c <= d
      holds [.a,c.[ /\ ].b,d.] = ].b,c.[;

theorem :: TOPGEN_5:12
  for X,Y being set holds
  product <*X,Y*>, [:X,Y:] are_equipotent &
  Card product <*X,Y*> = (Card X)*`(Card Y);

scheme :: TOPGEN_5:sch 1
 SCH1{P[set], A,B,C()-> non empty set, F1,F2(set)-> set}:
  ex f being Function of C(), B() st
  for a being Element of A() st a in C() holds
   (P[a] implies f.a = F1(a)) & (not P[a] implies f.a = F2(a))
provided
 C() c= A() and
 for a being Element of A() st a in C() holds
    (P[a] implies F1(a) in B()) & (not P[a] implies F2(a) in B());

scheme :: TOPGEN_5:sch 2
 SCH2{P,Q[set], A,B,C()-> non empty set, F1,F2,F3(set)-> set}:
  ex f being Function of C(), B() st
  for a being Element of A() st a in C() holds
   (P[a] implies f.a = F1(a)) &
   (not P[a] & Q[a] implies f.a = F2(a)) &
   (not P[a] & not Q[a] implies f.a = F3(a))
provided
 C() c= A() and
 for a being Element of A() st a in C() holds
    (P[a] implies F1(a) in B()) &
    (not P[a] & Q[a] implies F2(a) in B()) &
    (not P[a] & not Q[a] implies F3(a) in B());

theorem :: TOPGEN_5:13
  for a,b being real number holds |.|[a,b]|.|^2 = a^2+b^2;

theorem :: TOPGEN_5:14
  for X being TopSpace, Y being non empty TopSpace
  for A,B being closed Subset of X
  for f being continuous Function of X|A, Y
  for g being continuous Function of X|B, Y
  st f tolerates g
  holds f+*g is continuous Function of X|(A \/ B), Y;

theorem :: TOPGEN_5:15
  for X being TopSpace, Y being non empty TopSpace
  for A,B being closed Subset of X st A misses B
  for f being continuous Function of X|A, Y
  for g being continuous Function of X|B, Y
  holds f+*g is continuous Function of X|(A \/ B), Y;

theorem :: TOPGEN_5:16
  for X being TopSpace, Y being non empty TopSpace
  for A being open closed Subset of X
  for f being continuous Function of X|A, Y
  for g being continuous Function of X|A`, Y
  holds f+*g is continuous Function of X, Y;

begin :: Niemytzki plane

theorem :: TOPGEN_5:17
  for n being Nat
  for a being Point of TOP-REAL n
  for r being positive (real number)
  holds a in Ball(a,r);

definition
  func y=0-line -> Subset of TOP-REAL 2 equals
:: TOPGEN_5:def 1
  {|[x,0]|: not contradiction};
  func y>=0-plane -> Subset of TOP-REAL 2 equals
:: TOPGEN_5:def 2
  {|[x,y]|: y >= 0};
end;

theorem :: TOPGEN_5:18
  for a,b being set holds <*a,b*> in y=0-line iff a in REAL & b = 0;

theorem :: TOPGEN_5:19
  for a,b being real number holds |[a,b]| in y=0-line iff b = 0;

theorem :: TOPGEN_5:20
  Card y=0-line = continuum;

theorem :: TOPGEN_5:21
 for a,b being set holds <*a,b*> in y>=0-plane iff
   a in REAL & ex y st b = y & y >= 0;

theorem :: TOPGEN_5:22
  for a,b being real number holds |[a,b]| in y>=0-plane iff b >= 0;

registration
  cluster y=0-line -> non empty;
  cluster y>=0-plane -> non empty;
end;

theorem :: TOPGEN_5:23
  y=0-line c= y>=0-plane;

theorem :: TOPGEN_5:24
  for a,b,r being real number st r > 0 holds
  Ball(|[a,b]|,r) c= y>=0-plane iff r <= b;

theorem :: TOPGEN_5:25
  for a,b,r being real number st r > 0 & b >= 0 holds
  Ball(|[a,b]|,r) misses y=0-line iff r <= b;

theorem :: TOPGEN_5:26
  for n being Nat,
      a,b being Element of TOP-REAL n, r1,r2 being positive (real number)
  st |.a-b.| <= r1-r2
  holds Ball(b,r2) c= Ball(a,r1);

theorem :: TOPGEN_5:27
  for a being real number, r1,r2 being positive (real number) st r1 <= r2
  holds Ball(|[a,r1]|,r1) c= Ball(|[a,r2]|,r2);

theorem :: TOPGEN_5:28
  for T1,T2 being non empty TopSpace
  for B1 being Neighborhood_System of T1
  for B2 being Neighborhood_System of T2 st B1 = B2
  holds the TopStruct of T1 = the TopStruct of T2;

definition
  func Niemytzki-plane -> strict non empty TopSpace means
:: TOPGEN_5:def 3
  the carrier of it = y>=0-plane &
  ex B being Neighborhood_System of it st
  (for x holds B.(|[x,0]|) =
    {Ball(|[x,r]|,r) \/ {|[x,0]|} where r is Element of REAL: r > 0}) &
  (for x,y st y > 0 holds B.(|[x,y]|) =
    {Ball(|[x,y]|,r) /\ y>=0-plane where r is Element of REAL: r > 0});
end;

theorem :: TOPGEN_5:29
  y>=0-plane \ y=0-line is open Subset of Niemytzki-plane;

theorem :: TOPGEN_5:30
  y=0-line is closed Subset of Niemytzki-plane;

theorem :: TOPGEN_5:31
  for x being real number, r being positive (real number)
  holds Ball(|[x,r]|,r) \/ {|[x,0]|} is open Subset of Niemytzki-plane;

theorem :: TOPGEN_5:32
  for x being real number
  for y,r being positive (real number)
  holds Ball(|[x,y]|,r) /\ y>=0-plane is open Subset of Niemytzki-plane;

theorem :: TOPGEN_5:33
  for x,y being real number
  for r being positive (real number) st r <= y
  holds Ball(|[x,y]|,r) is open Subset of Niemytzki-plane;

theorem :: TOPGEN_5:34
  for p being Point of Niemytzki-plane
  for r being positive (real number)
  ex a being Point of TOP-REAL 2, U being open Subset of Niemytzki-plane
  st p in U & a in U &
  for b being Point of TOP-REAL 2 st b in U holds |.b-a.| < r;

theorem :: TOPGEN_5:35
  for x,y being real number
  for r being positive (real number)
  ex w,v being rational number
  st |[w,v]| in Ball(|[x,y]|,r) & |[w,v]| <> |[x,y]|;

theorem :: TOPGEN_5:36
  for A being Subset of Niemytzki-plane
  st A = (y>=0-plane \ y=0-line) /\ product <*RAT,RAT*>
  for x being set holds Cl (A \ {x}) = [#] Niemytzki-plane;

theorem :: TOPGEN_5:37
  for A being Subset of Niemytzki-plane
  st A = y>=0-plane \ y=0-line
  for x being set holds Cl (A \ {x}) = [#] Niemytzki-plane;

theorem :: TOPGEN_5:38
  for A being Subset of Niemytzki-plane
  st A = y>=0-plane \ y=0-line
  holds Cl A = [#] Niemytzki-plane;

theorem :: TOPGEN_5:39
  for A being Subset of Niemytzki-plane st A = y=0-line
  holds Cl A = A & Int A = {};

theorem :: TOPGEN_5:40
  (y>=0-plane \ y=0-line) /\ product <*RAT,RAT*>
  is dense Subset of Niemytzki-plane;

theorem :: TOPGEN_5:41
  (y>=0-plane \ y=0-line) /\ product <*RAT,RAT*>
  is dense-in-itself Subset of Niemytzki-plane;

theorem :: TOPGEN_5:42
  y>=0-plane \ y=0-line is dense Subset of Niemytzki-plane;

theorem :: TOPGEN_5:43
  y>=0-plane \ y=0-line is dense-in-itself Subset of Niemytzki-plane;

theorem :: TOPGEN_5:44
  y=0-line is nowhere_dense Subset of Niemytzki-plane;

theorem :: TOPGEN_5:45
  for A being Subset of Niemytzki-plane st A = y=0-line
  holds Der A is empty;

theorem :: TOPGEN_5:46
  for A being Subset of y=0-line
  holds A is closed Subset of Niemytzki-plane;

theorem :: TOPGEN_5:47
  RAT is dense Subset of Sorgenfrey-line;

theorem :: TOPGEN_5:48
  Sorgenfrey-line is separable;

theorem :: TOPGEN_5:49
  Niemytzki-plane is separable;

theorem :: TOPGEN_5:50
  Niemytzki-plane is_T1;

theorem :: TOPGEN_5:51
  Niemytzki-plane is not being_T4;

begin :: Tychonoff spaces

definition
  let T be TopSpace;
  attr T is Tychonoff means
:: TOPGEN_5:def 4
  T is_T1 &
  for A being closed Subset of T, a being Point of T st a in A`
  ex f being continuous Function of T, I[01] st f.a = 0 & f.:A c= {1};
end;

registration
  cluster Tychonoff -> being_T1 being_T3 TopSpace;
  cluster being_T1 being_T4 -> Tychonoff (non empty TopSpace);
end;

theorem :: TOPGEN_5:52
  for X being being_T1 TopSpace st X is Tychonoff
  for B being prebasis of X
  for x being Point of X
  for V being Subset of X st x in V & V in B
  ex f being continuous Function of X, I[01] st f.x = 0 & f.:V` c= {1};

registration
  let X be set;
  let Y be non empty real-membered set;
  cluster -> real-yielding Relation of X,Y;
end;

theorem :: TOPGEN_5:53
  for X being TopSpace, R being non empty SubSpace of R^1
  for f,g being continuous Function of X,R
  for A being Subset of X st
    for x being Point of X holds x in A iff f.x <= g.x
  holds A is closed;

theorem :: TOPGEN_5:54
  for X being TopSpace, R being non empty SubSpace of R^1
  for f,g being continuous Function of X,R
  ex h being continuous Function of X,R st
  for x being Point of X holds h.x = max(f.x,g.x);

theorem :: TOPGEN_5:55
  for X being non empty TopSpace, R being non empty SubSpace of R^1
  for A being finite non empty set
  for F being ManySortedFunction of A
  st for a being set st a in A holds F.a is continuous Function of X,R
  ex f being continuous Function of X,R st
  for x being Point of X, S being finite non empty Subset of REAL
  st S = rng ((commute F).x) holds f.x = max S;

theorem :: TOPGEN_5:56
  for X being being_T1 non empty TopSpace
  for B being prebasis of X
  st for x being Point of X
     for V being Subset of X st x in V & V in B
     ex f being continuous Function of X, I[01] st f.x = 0 & f.:V` c= {1}
  holds X is Tychonoff;

theorem :: TOPGEN_5:57
  Sorgenfrey-line is_T1;

theorem :: TOPGEN_5:58
  for x being real number
  holds left_open_halfline x is closed Subset of Sorgenfrey-line;

theorem :: TOPGEN_5:59
  for x being real number
  holds left_closed_halfline x is closed Subset of Sorgenfrey-line;

theorem :: TOPGEN_5:60
  for x being real number
  holds right_closed_halfline x is closed Subset of Sorgenfrey-line;

theorem :: TOPGEN_5:61
  for x,y being real number
  holds [.x,y.[ is closed Subset of Sorgenfrey-line;

::<DESC name="1.4.4. Przyk/lad funkcji ci/ag/lej ze strza/lce w [0,1]" monograph="topology"/>
theorem :: TOPGEN_5:62
  for x being real number, w being rational number st x < w
  ex f being continuous Function of Sorgenfrey-line, I[01] st
     for a being Point of Sorgenfrey-line holds
       (a in [.x,w.[ implies f.a = 0) &
       (not a in [.x,w.[ implies f.a = 1);

theorem :: TOPGEN_5:63
  Sorgenfrey-line is Tychonoff;

begin :: Niemytzki plane is Tychonoff space

::<DESC name="1.4.5. Przyk/lad funkcji ci/ag/lej z p/laszczyzny Niemytzkiego w R^1" monograph="topology"/>
definition
  let x be real number, r be positive (real number);
  func +(x,r) -> Function of Niemytzki-plane, I[01] means
:: TOPGEN_5:def 5
  it.(|[x,0]|) = 0 &
  for a being real number, b being non negative (real number) holds
   ((a <> x or b <> 0) & not |[a,b]| in Ball(|[x,r]|,r) implies
        it.(|[a,b]|) = 1) &
   (|[a,b]| in Ball(|[x,r]|,r) implies
        it.(|[a,b]|) = |.|[x,0]|-|[a,b]|.|^2/(2*r*b));
end;

theorem :: TOPGEN_5:64
  for p being Point of TOP-REAL 2 st p`2 >= 0
  for x being real number, r being positive (real number)
  st +(x,r).p = 0
  holds p = |[x,0]|;

theorem :: TOPGEN_5:65
  for x,y being real number, r being positive (real number) st x <> y
  holds +(x,r).(|[y,0]|) = 1;

theorem :: TOPGEN_5:66
  for p being Point of TOP-REAL 2
  for x being real number, a,r being positive (real number)
  st a <= 1 & |.p-|[x,r*a]|.| = r*a & p`2 <> 0
  holds +(x,r).p = a;

theorem :: TOPGEN_5:67
  for p being Point of TOP-REAL 2
  for x,a being real number, r being positive (real number)
  st 0 <= a & a <= 1 & |.p-|[x,r*a]|.| < r*a
  holds +(x,r).p < a;

theorem :: TOPGEN_5:68
  for p being Point of TOP-REAL 2 st p`2 >= 0
  for x,a being real number, r being positive (real number)
  st 0 <= a & a < 1 & |.p-|[x,r*a]|.| > r*a
  holds +(x,r).p > a;

theorem :: TOPGEN_5:69
  for p being Point of TOP-REAL 2 st p`2 >= 0
  for x,a,b being real number, r being positive (real number)
  st 0 <= a & b <= 1 & +(x,r).p in ].a,b.[
  ex r1 being positive (real number)
  st r1 <= p`2 & Ball(p,r1) c= +(x,r)"].a,b.[;

theorem :: TOPGEN_5:70
  for x being real number, a,r being positive (real number)
  holds Ball(|[x,r*a]|,r*a) c= +(x,r)"].0,a.[;

theorem :: TOPGEN_5:71
  for x being real number, a,r being positive (real number)
  holds Ball(|[x,r*a]|,r*a) \/ {|[x,0]|} c= +(x,r)"[.0,a.[;

theorem :: TOPGEN_5:72
  for p being Point of TOP-REAL 2 st p`2 >= 0
  for x,a being real number, r being positive (real number)
  st 0 < +(x,r).p & +(x,r).p < a & a <= 1
  holds p in Ball(|[x,r*a]|,r*a);

theorem :: TOPGEN_5:73
  for p being Point of TOP-REAL 2 st p`2 > 0
  for x,a being real number, r being positive (real number)
  st 0 <= a & a < +(x,r).p
  ex r1 being positive (real number)
  st r1 <= p`2 & Ball(p,r1) c= +(x,r)"].a,1.];

theorem :: TOPGEN_5:74
  for p being Point of TOP-REAL 2 st p`2 = 0
  for x being real number, r being positive (real number)
  st +(x,r).p = 1
  ex r1 being positive (real number)
  st Ball(|[p`1,r1]|,r1) \/ {p} c= +(x,r)"{1};

theorem :: TOPGEN_5:75
  for T being non empty TopSpace, S being SubSpace of T
  for B being Basis of T holds
  {A/\[#]S where A is Subset of T: A in B & A meets [#]S} is Basis of S;

theorem :: TOPGEN_5:76
  {].a,b.[ where a,b is Real: a < b} is Basis of R^1;

theorem :: TOPGEN_5:77
  for T being TopSpace
  for U,V being Subset of T
  for B being set
  st U in B & V in B & B \/ {U \/ V} is Basis of T
  holds B is Basis of T;

theorem :: TOPGEN_5:78
  {[.0,a.[ where a is Real: 0 < a & a <= 1} \/
  {].a,1.] where a is Real: 0 <= a & a < 1} \/
  {].a,b.[ where a,b is Real: 0 <= a & a < b & b <= 1} is Basis of I[01];

theorem :: TOPGEN_5:79
  for T being non empty TopSpace
  for f being Function of T, I[01] holds f is continuous iff
  for a,b being real number st 0 <= a & a < 1 & 0 < b & b <= 1
  holds f"[.0,b.[ is open & f"].a,1.] is open;

registration
  let x be real number, r be positive (real number);
  cluster +(x,r) -> continuous;
end;

theorem :: TOPGEN_5:80
  for U being Subset of Niemytzki-plane
  for x,r st U = Ball(|[x,r]|,r) \/ {|[x,0]|}
  ex f being continuous Function of Niemytzki-plane, I[01] st
     f.(|[x,0]|) = 0 &
     for a,b being real number holds
       (|[a,b]| in U` implies f.(|[a,b]|) = 1) &
       (|[a,b]| in U\{|[x,0]|} implies
        f.(|[a,b]|) = |.|[x,0]|-|[a,b]|.|^2/(2*r*b));

::<DESC name="1.4.5. Przyk/lad funkcji ci/ag/lej z p/laszczyzny Niemytzkiego w R^1" monograph="topology"/>
definition
  let x,y be real number, r be positive (real number);
  func +(x,y,r) -> Function of Niemytzki-plane, I[01] means
:: TOPGEN_5:def 6
  for a being real number, b being non negative (real number) holds
   (not |[a,b]| in Ball(|[x,y]|,r) implies it.(|[a,b]|) = 1) &
   (|[a,b]| in Ball(|[x,y]|,r) implies it.(|[a,b]|) = |.|[x,y]|-|[a,b]|.|/r);
end;

theorem :: TOPGEN_5:81
  for p being Point of TOP-REAL 2 st p`2 >= 0
  for x being real number, y being non negative (real number)
  for r being positive (real number)
  holds +(x,y,r).p = 0 iff p = |[x,y]|;

theorem :: TOPGEN_5:82
  for x being real number, y being non negative (real number)
  for r,a being positive (real number) st a <= 1
  holds +(x,y,r)"[.0,a.[ = Ball(|[x,y]|,r*a) /\ y>=0-plane;

theorem :: TOPGEN_5:83
  for p being Point of TOP-REAL 2 st p`2 > 0
  for x being real number, a being non negative (real number)
  for y,r being positive (real number) st +(x,y,r).p > a
  holds |.|[x,y]|-p.| > r*a &
  Ball(p,|.|[x,y]|-p.|-r*a) /\ y>=0-plane c= +(x,y,r)"].a,1.];

theorem :: TOPGEN_5:84
  for p being Point of TOP-REAL 2 st p`2 = 0
  for x being real number, a being non negative (real number)
  for y,r being positive (real number) st +(x,y,r).p > a
  holds |.|[x,y]|-p.| > r*a &
  ex r1 being positive (real number) st r1 = (|.|[x,y]|-p.|-r*a)/2 &
  Ball(|[p`1,r1]|,r1) \/ {p} c= +(x,y,r)"].a,1.];

registration
  let x be real number;
  let y,r be positive (real number);
  cluster +(x,y,r) -> continuous;
end;

theorem :: TOPGEN_5:85
  for U being Subset of Niemytzki-plane
  for x,y,r st y > 0 & U = Ball(|[x,y]|,r) /\ y>=0-plane
  ex f being continuous Function of Niemytzki-plane, I[01] st
     f.(|[x,y]|) = 0 &
     for a,b being real number holds
       (|[a,b]| in U` implies f.(|[a,b]|) = 1) &
       (|[a,b]| in U implies f.(|[a,b]|) = |.|[x,y]|-|[a,b]|.|/r);

theorem :: TOPGEN_5:86
  Niemytzki-plane is_T1;

theorem :: TOPGEN_5:87
  Niemytzki-plane is Tychonoff;
