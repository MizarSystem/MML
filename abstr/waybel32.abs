:: On the Order-consistent Topology of Complete and Uncomplete
:: Lattices
::  by Ewa Gr\c{a}dzka
::
:: Received May 23, 2000
:: Copyright (c) 2000 Association of Mizar Users

environ

 vocabulary WAYBEL_9, WAYBEL_0, CANTOR_1, WAYBEL11, BHSP_3, PRELAMB, YELLOW_9,
      PRE_TOPC, RELAT_2, ORDINAL2, CONNSP_2, REALSET1, SETFAM_1, BOOLE,
      WAYBEL19, TARSKI, FINSET_1, SUBSET_1, YELLOW_2, RELAT_1, TOPS_1,
      QUANTAL1, LATTICE3, YELLOW_0, FUNCT_1, ORDERS_1, LATTICES, SEQM_3,
      YELLOW_6, PROB_1, T_0TOPSP, FUNCT_3, WAYBEL21, CAT_1, ARYTM_3, WAYBEL_7,
      WAYBEL32, RLVECT_3;
 notation TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, RELAT_1, FUNCT_1, FINSET_1,
      STRUCT_0, REALSET1, FUNCT_2, ORDERS_1, LATTICE3, PRE_TOPC, TOPS_1,
      TOPS_2, CONNSP_2, T_0TOPSP, GROUP_1, YELLOW_0, WAYBEL_0, CANTOR_1,
      YELLOW_2, WAYBEL_2, YELLOW_6, WAYBEL_9, RELSET_1, WAYBEL11, WAYBEL19,
      YELLOW_9, WAYBEL21;
 constructors WAYBEL_1, CANTOR_1, TOPS_1, TOPS_2, WAYBEL_3, TSP_1, WAYBEL11,
      YELLOW_9, GROUP_1, TOLER_1, WAYBEL_5, WAYBEL21, BORSUK_1, MEMBERED,
      PARTFUN1;
 clusters STRUCT_0, LATTICE3, WAYBEL_0, FINSET_1, YELLOW_6, WAYBEL_3, WAYBEL11,
      WAYBEL21, YELLOW_9, RELSET_1, SUBSET_1, YELLOW14, WAYBEL29, MEMBERED,
      ZFMISC_1, FUNCT_2, PARTFUN1;
 requirements SUBSET, BOOLE;


begin

definition
 let T be non empty TopRelStr;
 attr T is upper means
:: WAYBEL32:def 1

  {(downarrow x)` where x is Element of T: not contradiction} is prebasis of T;
end;


registration
 cluster Scott up-complete strict TopLattice;
 existence
  proof consider R being complete LATTICE;
   consider T being strict Scott correct TopAugmentation of R;
   take T; thus thesis;
  end;
end;


definition
 let T be TopSpace-like non empty reflexive TopRelStr;
 attr T is order_consistent means
:: WAYBEL32:def 2

  for x being Element of T holds downarrow x = Cl {x} &
  for N being eventually-directed net of T st x = sup N
  for V being a_neighborhood of x holds N is_eventually_in V;
end;


registration
cluster trivial -> upper (non empty reflexive TopSpace-like TopRelStr);
 coherence
  proof let T be non empty reflexive TopSpace-like TopRelStr;
   assume
A1:  T is trivial;
   set BB = {(downarrow x)` where x is Element of T: not contradiction};
      BB c= bool the carrier of T
     proof let a be set; assume a in BB;
       then ex x being Element of T st a = (downarrow x)`;
      hence thesis;
     end;
   then reconsider C = BB as Subset-Family of T by SETFAM_1:def
7;
   reconsider C as Subset-Family of T;
A2: BB c= the topology of T
     proof let a be set; assume a in BB;
      then consider x being Element of T such that
A3:    a = (downarrow x)`;
         a c= {}
        proof let y be set; assume
A4:       y in a;
          then y in the carrier of T & x <= x by A3;
          then y = x & x in downarrow x by A1,REALSET1:def 20,WAYBEL_0:17;
          then x in (downarrow x) /\ a & (downarrow x) misses a
           by A3,A4,PRE_TOPC:26,XBOOLE_0:def 3;
         hence thesis by XBOOLE_0:def 7;
        end;
       then a = {} by XBOOLE_1:3;
      hence thesis by PRE_TOPC:5;
     end;
   reconsider F = {the carrier of T} as Basis of T by A1,YELLOW_9:10;
      BB = {{}}
     proof
      thus BB c= {{}}
       proof let a be set; assume a in BB;
        then consider x being Element of T such that
A5:      a = (downarrow x)`;
           x <= x;
         then the carrier of T = {x} & x in downarrow x
          by A1,WAYBEL_0:17,YELLOW_9:9;
         then a = {} or a = the carrier of T & not x in a
          by A5,YELLOW_6:10,ZFMISC_1:39;
        hence thesis by TARSKI:def 1;
       end;
      consider x being Element of T;
         x <= x;
       then the carrier of T = {x} & x in downarrow x by A1,WAYBEL_0:17,
YELLOW_9:9;
       then (downarrow x)` = {} or (downarrow x)` = the carrier of T &
       not x in (downarrow x)` by YELLOW_6:10,ZFMISC_1:39;
       then {} in BB;
      hence thesis by ZFMISC_1:37;
     end;
    then FinMeetCl C = {{}, the carrier of T} by YELLOW_9:11;
    then F c= FinMeetCl C by ZFMISC_1:12;
   hence BB is prebasis of T by A2,CANTOR_1:def 5;
 end;
end;


registration
 cluster upper trivial up-complete strict TopLattice;
  existence
   proof
    consider T being trivial up-complete strict TopLattice;
     take T;
    thus thesis;
   end;
  end;


theorem :: WAYBEL32:1
 for T being upper up-complete (non empty TopPoset)
 for A being Subset of T st A is open holds A is upper;

theorem :: WAYBEL32:2
   for T being up-complete (non empty TopPoset) holds
 T is upper implies T is order_consistent;

canceled 4;

theorem :: WAYBEL32:7
for R being up-complete (non empty reflexive transitive antisymmetric
     RelStr)
 ex T being TopAugmentation of R st T is Scott;

theorem :: WAYBEL32:8
 for R being up-complete (non empty Poset)
  for T being TopAugmentation of R holds T is Scott implies T is correct;

registration
 let R be up-complete (non empty reflexive transitive antisymmetric
     RelStr);
 cluster Scott -> correct TopAugmentation of R;
 coherence by Th8;
end;


registration
 let R be up-complete (non empty reflexive transitive antisymmetric
     RelStr);
 cluster Scott correct TopAugmentation of R;
 existence
  proof
   consider T being TopAugmentation of R such that A1: T is Scott by Th7;
   take T;
   thus thesis by A1,Th8;
  end;
end;


theorem :: WAYBEL32:9   :: Remark 1.4 (ii)
 for T being Scott up-complete (non empty reflexive transitive antisymmetric
     TopRelStr), x being Element of T
  holds Cl {x} = downarrow x;

theorem :: WAYBEL32:10
 for T being up-complete Scott (non empty TopPoset) holds
  T is order_consistent;

theorem :: WAYBEL32:11
 for R being /\-complete Semilattice, Z be net of R,
     D be Subset of R st
  D = {"/\"({Z.k where k is Element of Z: k >= j},R)
            where j is Element of Z: not contradiction}
  holds D is non empty directed;

theorem :: WAYBEL32:12
 for R being /\-complete Semilattice,
     S being Subset of R,
     a being Element of R holds
  a in S implies "/\"(S,R) <= a;

theorem :: WAYBEL32:13
 for R being /\-complete Semilattice,
     N being monotone reflexive net of R
 holds lim_inf N = sup N;

theorem :: WAYBEL32:14
 for R being /\-complete Semilattice
 for S being Subset of R
  holds S in the topology of ConvergenceSpace Scott-Convergence R
    iff S is inaccessible upper;

theorem :: WAYBEL32:15
 for R being /\-complete up-complete Semilattice,
  T being TopAugmentation of R
  st the topology of T = sigma R
  holds T is Scott;

registration
 let R be /\-complete up-complete Semilattice;
 cluster strict Scott correct TopAugmentation of R;
  existence
   proof
    set T = TopRelStr(#the carrier of R, the InternalRel of R, sigma R#);
       the RelStr of T = the RelStr of R;
     then reconsider T as TopAugmentation of R by YELLOW_9:def 4;
     take T;
    thus thesis by Th15,YELLOW_9:48;
  end;
end;


theorem :: WAYBEL32:16
  for S being up-complete /\-complete Semilattice,
    T being Scott TopAugmentation of S holds
  sigma S = the topology of T;

theorem :: WAYBEL32:17     :: Remark 1.4 (iii)
   for T being Scott up-complete (non empty reflexive transitive antisymmetric
     TopRelStr)
 holds T is T_0-TopSpace;

registration
let R be up-complete (non empty reflexive transitive antisymmetric RelStr);
cluster -> up-complete TopAugmentation of R;
coherence;
end;


theorem :: WAYBEL32:18
 for R being up-complete (non empty reflexive transitive antisymmetric
     RelStr)
  for T being Scott TopAugmentation of R,
   x being Element of T,
     A being upper Subset of T st not x in A
  holds (downarrow x)` is a_neighborhood of A;

theorem :: WAYBEL32:19     ::Remark 1.4 (iv)
   for R being up-complete (non empty reflexive transitive antisymmetric
     TopRelStr)
 for T being Scott TopAugmentation of R,
     S being upper Subset of T
  ex F being Subset-Family of T st S = meet F &
   for X being Subset of T st X in F holds X is a_neighborhood of S;

theorem :: WAYBEL32:20    ::Remark 1.4 (v)
  for T being Scott up-complete (non empty reflexive transitive antisymmetric
     TopRelStr),
     S being Subset of T
  holds S is open iff S is upper property(S);

theorem :: WAYBEL32:21
 for R being up-complete (non empty reflexive transitive antisymmetric
     TopRelStr),
     S being non empty directed Subset of R,
     a being Element of R holds
  a in S implies a <= "\/"(S, R);

::Remark 1.4 (vi)
registration let T be up-complete (non empty reflexive transitive antisymmetric
     TopRelStr);
 cluster lower -> property(S) Subset of T;
 coherence
  proof
  let S be Subset of T;
   assume
A1: S is lower;
   let D be non empty directed Subset of T such that
A2:  sup D in S;
   consider y being Element of T such that
A3:  y in D by SUBSET_1:10;
   take y;
   thus y in D by A3;
   let x be Element of T such that
A4: x in D and x >= y;
      x <= sup D by A4,Th21;
   hence x in S by A1,A2,WAYBEL_0:def 19;
  end;
end;


theorem :: WAYBEL32:22
   for T being finite up-complete (non empty Poset),
     S being Subset of T holds S is inaccessible;

theorem :: WAYBEL32:23
 for R being complete connected LATTICE,
  T being Scott TopAugmentation of R, x being Element of T holds
   (downarrow x)` is open;

theorem :: WAYBEL32:24
   for R being complete connected LATTICE,
     T being Scott TopAugmentation of R, S being Subset of T holds
     S is open iff S = the carrier of T or S in {(downarrow x)`
                              where x is Element of T: not contradiction};

registration
let R be up-complete (non empty Poset);
cluster order_consistent (correct TopAugmentation of R);
correctness
 proof
  consider T being Scott correct TopAugmentation of R;
  take T;
  thus T is order_consistent by Th10;
 end;
end;


registration
cluster order_consistent complete TopLattice;
correctness
proof
 consider T being Scott complete TopLattice;
 take T;
 thus thesis by Th10;
end;
end;


theorem :: WAYBEL32:25
for R being non empty TopRelStr
for A being Subset of R holds
 (for x being Element of R holds downarrow x = Cl {x}) implies
  (A is open implies A is upper);

theorem :: WAYBEL32:26
  for R being non empty TopRelStr
for A being Subset of R holds
 (for x being Element of R holds downarrow x = Cl {x}) implies
for A being Subset of R st A is closed holds A is lower;

theorem :: WAYBEL32:27
 for T being up-complete /\-complete LATTICE, N being net of T
 for i being Element of N holds lim_inf (N|i) = lim_inf N;

definition
let S be non empty 1-sorted,
    R be non empty RelStr,
    f be Function of the carrier of R,the carrier of S;
func R*'f -> strict non empty NetStr over S means
:: WAYBEL32:def 3

the RelStr of it = the RelStr of R &
the mapping of it = f;
end;


registration
let S be non empty 1-sorted,
    R be non empty transitive RelStr,
    f be Function of the carrier of R,the carrier of S;
cluster R*'f -> transitive;
coherence
 proof
    the RelStr of R*'f = the RelStr of R & R is transitive by Def3;
  hence R*'f is transitive by WAYBEL_8:13;
 end;
end;


registration
let S be non empty 1-sorted,
    R be non empty directed RelStr,
    f be Function of the carrier of R,the carrier of S;
cluster R*'f -> directed;
coherence
proof
A1:  the RelStr of R*'f = the RelStr of R & R is directed by Def3;
  thus [#](R*'f) is directed
   proof
A2: [#]R is directed by WAYBEL_0:def 6;
      [#](R*'f) = the carrier of R*'f by PRE_TOPC:12
            .= [#]R by A1,PRE_TOPC:12;
      hence thesis by A1,A2,WAYBEL_0:3;
   end;
end;
end;


definition
 let R be non empty RelStr,
     N be prenet of R;
func inf_net N -> strict prenet of R means
:: WAYBEL32:def 4

ex f being map of N,R st
 it = N*'f & for i being Element of N holds
  f.i = "/\"({N.k where k is Element of N: k >= i},R);
end;


registration
let R be non empty RelStr,
    N be net of R;
cluster inf_net N -> transitive;
coherence
 proof
    ex f being map of N,R st inf_net N = N*'f &
      for i being Element of N holds
  f.i = "/\"({N.k where k is Element of N: k >= i},R) by Def4;
  hence thesis;
 end;
end;


registration
let R be non empty RelStr,
    N be net of R;
cluster inf_net N -> directed;
coherence;
end;


registration
let R be /\-complete (non empty reflexive antisymmetric RelStr),
    N be net of R;
cluster inf_net N -> monotone;
coherence
proof
 let i,j be Element of inf_net N such that A1: i <= j;
 consider f being map of N,R such that A2: inf_net N = N*'f &
      for i being Element of N holds
       f.i = "/\"({N.k where k is Element of N: k >= i},R)
        by Def4;
A3:   the RelStr of inf_net N = the RelStr of N by A2,Def3;
  then reconsider i'=i,j'=j as Element of N;
  deffunc F(Element of N) = N.$1;
  defpred P[Element of N] means $1 >= j';
  defpred Q[Element of N] means $1 >= i';
set J= {F(k) where k is Element of N: P[k]};
set I= {F(k) where k is Element of N: Q[k]};
A4:     J is Subset of R from COMPLSP1:sch 3;
        consider j0 being Element of N such that
A5:      j0 >= j' & j0 >= j' by YELLOW_6:def 5;
           N.j0 in J by A5;
        then reconsider J'= J as non empty Subset of R by A4;
A6:     I is Subset of R from COMPLSP1:sch 3;
        consider j1 being Element of N such that
A7:      j1 >= i' & j1 >= i' by YELLOW_6:def 5;
           N.j1 in I by A7;
        then reconsider I'= I as non empty Subset of R by A6;
A8: ex_inf_of J',R & ex_inf_of I',R by WAYBEL_0:76;
A9:J' c= I'
     proof
      let a be set; assume A10: a in J';
      then reconsider x = a as Element of R;
       consider k being Element of N such that
A11:     x = N.k and
A12:     k >= j' by A10;
       reconsider i0 = i,j0 = j as Element of inf_net N;
       reconsider k' = k as Element of N;
         i0 <= j0 & i' = i0 & j' = j0 by A1;
       then i' <= j' & j' <= k' by A3,A12,YELLOW_0:1;
       then i' <= k' by YELLOW_0:def 2;
      hence a in I' by A11;
     end;
A13:  f.i' = "/\"(I,R) & f.j' = "/\"(J,R) by A2;
    the mapping of (inf_net N) = f by A2,Def3;
  then (inf_net N).i = f.i' & (inf_net N).j = f.j' by WAYBEL_0:def 8;
 hence (inf_net N).i <= (inf_net N).j by A8,A9,A13,YELLOW_0:35;
end;
end;


registration
let R be /\-complete (non empty reflexive antisymmetric RelStr),
    N be net of R;
cluster inf_net N -> eventually-directed;
coherence;
end;


theorem :: WAYBEL32:28
for R being non empty RelStr,
    N being net of R
    holds rng the mapping of (inf_net N) =
    {"/\"({N.i where i is Element of N: i >= j},R) where
         j is Element of N: not contradiction};

theorem :: WAYBEL32:29
for R being up-complete /\-complete LATTICE,
    N being net of R holds
sup inf_net N = lim_inf N;

theorem :: WAYBEL32:30
  for R being up-complete /\-complete LATTICE,
    N being net of R,
    i being Element of N holds
sup inf_net N = lim_inf (N|i);

theorem :: WAYBEL32:31
for R being /\-complete Semilattice,
    N being net of R,
    V being upper Subset of R holds
inf_net N is_eventually_in V implies N is_eventually_in V;

theorem :: WAYBEL32:32
  for R being /\-complete Semilattice,
    N being net of R,
    V being lower Subset of R holds
 N is_eventually_in V implies inf_net N is_eventually_in V;

theorem :: WAYBEL32:33
for R being order_consistent up-complete /\-complete (non empty TopLattice)
for N being net of R,
    x being Element of R holds
    x <= lim_inf N implies x is_a_cluster_point_of N;

theorem :: WAYBEL32:34
  for R being order_consistent up-complete /\-complete (non empty TopLattice)
for N being eventually-directed net of R,
    x being Element of R holds
    x <= lim_inf N iff x is_a_cluster_point_of N;

