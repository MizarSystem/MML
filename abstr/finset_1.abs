:: Finite Sets
::  by Agata Darmochwa\l
::
:: Received April 6, 1989
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabulary FUNCT_1, RELAT_1, ORDINAL2, BOOLE, ORDINAL1, ORDINAL3, SETFAM_1,
      TARSKI, MCART_1, FINSET_1;
 notation TARSKI, XBOOLE_0, ENUMSET1, ZFMISC_1, SUBSET_1, RELAT_1, FUNCT_1,
      MCART_1, SETFAM_1, ORDINAL1, ORDINAL2, ORDINAL3;
 constructors ENUMSET1, MCART_1, ORDINAL3, SETFAM_1, XBOOLE_0;
 clusters FUNCT_1, ORDINAL2, RELAT_1, ARYTM_3, ORDINAL1, SUBSET_1, ZFMISC_1,
      XBOOLE_0;
 requirements SUBSET, BOOLE;


begin

 definition let IT be set;
  attr IT is finite means
:: FINSET_1:def 1
    ex p being Function st rng p = IT & dom p in omega;
  end;
notation let IT be set;
  antonym IT is infinite for IT is finite ; end;


 reserve A, B, C, D, X, Y, Z, x, y for set;
 reserve f for Function;

registration
 cluster non empty finite set;
  existence proof consider x; {x} is finite by Lm1; hence thesis; end;
end;


registration
 cluster empty -> finite set;
 coherence
 proof let A be set;
  assume
A1:  A is empty;
  take {};
  thus rng {} = A by A1;
  thus thesis by ORDINAL2:def 5;
 end;
end;


registration let X be set;
 cluster empty finite Subset of X;
 existence
  proof
   take {} X;
   thus thesis;
  end;
end;


scheme :: FINSET_1:sch 1
 OLambdaC{A()->set,C[set],F(set)->set,G(set)->set}:
 ex f being Function st dom f = A() &
  for x being Ordinal st x in A() holds
   (C[x] implies f.x = F(x)) & (not C[x] implies f.x = G(x))
proof
   defpred C'[set] means C[$1];
   deffunc F'(set) = F($1);
   deffunc G'(set) = G($1);
    consider f being Function such that
A1:  dom f = A() &
     for x being set st x in A() holds
     (C'[x] implies f.x = F'(x)) & (not C'[x] implies f.x = G'(x))
     from PARTFUN1:sch 1;
    take f;
    thus thesis by A1;
 end;

registration let x be set;
 cluster {x} -> finite;
  coherence by Lm1;
end;


registration let X be non empty set;
 cluster non empty finite Subset of X;
 existence
  proof
   consider x being set such that
A1:x in X by XBOOLE_0:def 1;
   take {x};
   thus thesis by A1,ZFMISC_1:37;
  end;
end;


registration
  let x,y be set;
  cluster {x,y} -> finite;
  coherence
   proof
      {x,y} is finite
     proof
        {x,y} = {x} \/ {y} & {x} is finite & {y} is finite by ENUMSET1:41;
      hence thesis by Lm2;
     end;
    hence thesis;
   end;
end;


registration
  let x,y,z be set;
  cluster {x,y,z} -> finite;
  coherence
   proof
      {x,y,z} is finite
     proof
        {x,y,z} = {x} \/ {y,z} & {x} is finite & {y,z}is finite
       by ENUMSET1:42;
      hence thesis by Lm2;
     end;
    hence thesis;
   end;
end;


registration
  let x1,x2,x3,x4 be set;
  cluster {x1,x2,x3,x4} -> finite;
  coherence
   proof
      {x1,x2,x3,x4} is finite
     proof
        {x1,x2,x3,x4} = {x1} \/ {x2,x3,x4} & {x1} is finite & {x2,x3,x4}is
finite
       by ENUMSET1:44;
      hence thesis by Lm2;
     end;
    hence thesis;
   end;
end;


registration
  let x1,x2,x3,x4,x5 be set;
  cluster {x1,x2,x3,x4,x5} -> finite;
  coherence
   proof
      {x1,x2,x3,x4,x5} is finite
     proof
        {x1,x2,x3,x4,x5} = {x1} \/ {x2,x3,x4,x5} & {x1} is finite &
      {x2,x3,x4,x5} is finite by ENUMSET1:47;
      hence thesis by Lm2;
     end;
    hence thesis;
   end;
end;


registration
  let x1,x2,x3,x4,x5,x6 be set;
  cluster {x1,x2,x3,x4,x5,x6} -> finite;
  coherence
   proof
      {x1,x2,x3,x4,x5,x6} is finite
     proof
        {x1,x2,x3,x4,x5,x6} = {x1} \/ {x2,x3,x4,x5,x6} & {x1} is finite &
      {x2,x3,x4,x5,x6} is finite by ENUMSET1:51;
      hence thesis by Lm2;
     end;
    hence thesis;
   end;
end;


registration
  let x1,x2,x3,x4,x5,x6,x7 be set;
  cluster {x1,x2,x3,x4,x5,x6,x7} -> finite;
  coherence
   proof
      {x1,x2,x3,x4,x5,x6,x7} is finite
     proof
        {x1,x2,x3,x4,x5,x6,x7} = {x1} \/ {x2,x3,x4,x5,x6,x7} & {x1} is finite &
      {x2,x3,x4,x5,x6,x7} is finite by ENUMSET1:56;
      hence thesis by Lm2;
     end;
    hence thesis;
   end;
end;


registration
  let x1,x2,x3,x4,x5,x6,x7,x8 be set;
  cluster {x1,x2,x3,x4,x5,x6,x7,x8} -> finite;
  coherence
   proof
      {x1,x2,x3,x4,x5,x6,x7,x8} is finite
     proof
        {x1,x2,x3,x4,x5,x6,x7,x8} = {x1} \/ {x2,x3,x4,x5,x6,x7,x8} &
      {x1} is finite &
      {x2,x3,x4,x5,x6,x7,x8} is finite by ENUMSET1:62;
      hence thesis by Lm2;
     end;
    hence thesis;
   end;
end;


registration let B be finite set;
 cluster -> finite Subset of B;
 coherence
  proof let A be Subset of B;
   per cases;
   suppose A is empty;
    then A = {};
   hence thesis;
   end;
   suppose A is non empty;
    then consider x1 be set such that
A1:  x1 in A by XBOOLE_0:def 1;
    consider p be Function such that
A2:  rng p = B and
A3:  dom p in omega by Def1;
    deffunc F(set) = p.$1;
    deffunc G(set) = x1;
    defpred P[set] means p.$1 in A;
    consider q be Function such that
A4:  dom q = dom p and
A5:  for x st x in dom p holds
      (P[x] implies q.x = F(x)) &
      (not P[x] implies q.x = G(x)) from PARTFUN1:sch 1;
      now let y;
     thus y in A implies ex x st x in dom q & y = q.x
      proof assume
A6:     y in A;
       then consider x such that
A7:     x in dom p and
A8:     p.x = y by A2,FUNCT_1:def 5;
       take x;
       thus x in dom q by A4,A7;
       thus y = q.x by A5,A6,A7,A8;
      end;
     given x such that
A9:   x in dom q and
A10:   y = q.x;
     per cases;
     suppose p.x in A;
     hence y in A by A4,A5,A9,A10;
     end;
     suppose not p.x in A;
     hence y in A by A1,A4,A5,A9,A10;
    end;
    end;
    then rng q = A by FUNCT_1:def 5;
   hence thesis by A3,A4,Def1;
  end;
  end;
end;


registration let X,Y be finite set;
 cluster X \/ Y -> finite;
  coherence by Lm2;
end;


canceled 12;

theorem :: FINSET_1:13
 A c= B & B is finite implies A is finite;

theorem :: FINSET_1:14
   A is finite & B is finite implies A \/ B is finite;

registration let A be set, B be finite set;
 cluster A /\ B -> finite;
 coherence
  proof
     A /\ B is finite
   proof
      A /\ B c= B by XBOOLE_1:17;
    hence thesis by Th13;
   end;
   hence thesis;
  end;
end;


registration let A be finite set, B be set;
 cluster A /\ B -> finite;
 coherence
  proof
     A /\ B is finite
   proof
      A /\ B c= A by XBOOLE_1:17;
    hence thesis by Th13;
   end;
   hence thesis;
  end;
 cluster A \ B -> finite;
 coherence
  proof
     A \ B is finite
    proof
       A \ B c= A by XBOOLE_1:36;
     hence thesis by Th13;
    end;
   hence thesis;
  end;
end;


theorem :: FINSET_1:15
 A is finite implies A /\ B is finite;

theorem :: FINSET_1:16
   A is finite implies A \ B is finite;

theorem :: FINSET_1:17
 A is finite implies f.:A is finite;

registration let f be Function, A be finite set;
 cluster f.:A -> finite;
 coherence by Th17;
end;


reserve O for Ordinal;

theorem :: FINSET_1:18
 A is finite implies for X being Subset-Family of A st X <> {}
  ex x being set st
   x in X & for B being set st B in X holds x c= B implies B = x;

scheme :: FINSET_1:sch 2
 Finite{A()->set,P[set]} : P[A()]
  provided
  A() is finite and
  P[{}] and
  for x,B being set st x in A() & B c= A() & P[B] holds P[B \/ {x}]
proof
      now assume A() <> {};
    defpred R[set] means ex B st B=$1 & P[B];
    consider G being set such that
     A4: for x holds x in G iff x in bool A() & R[x] from XBOOLE_0:sch 1;
      G c= bool A()
     proof
      let x; assume x in G;
      hence thesis by A4;
     end;
    then reconsider GA=G as Subset-Family of A() by SETFAM_1:def 7;
      {} c= A() by XBOOLE_1:2;
    then GA <> {} by A2,A4;
    then consider B such that A5:
    B in GA & for X being set st X in GA holds B c= X implies B=X by A1,Th18;
    A6: B in bool A() & ex A st A = B & P[A] by A4,A5;
      now consider x being Element of A() \ B;
     assume B <> A();
      then not A() c= B by A5,XBOOLE_0:def 10;
      then A7:     A() \ B <> {} by XBOOLE_1:37;
      then A8: x in A() by XBOOLE_0:def 4;
      then A9: P[B \/ {x}] by A3,A6;
        {x} c= A() by A8,ZFMISC_1:37;
      then B \/ {x} c= A() by A5,XBOOLE_1:8;
      then A10: B \/ {x} in GA by A4,A9;
        not x in B by A7,XBOOLE_0:def 4;
      then not {x} c= B by ZFMISC_1:37;
      then B c= B \/ {x} & B \/ {x} <> B by XBOOLE_1:7;
     hence contradiction by A5,A10;
   end;
   hence thesis by A6;
   end;
   hence thesis by A2;
  end;

theorem :: FINSET_1:19
 A is finite & B is finite implies [:A,B:] is finite;

registration let A,B be finite set;
 cluster [:A,B:] -> finite;
 coherence by Th19;
end;


theorem :: FINSET_1:20
 A is finite & B is finite & C is finite
  implies [:A,B,C:] is finite;

registration let A,B,C be finite set;
 cluster [:A,B,C:] -> finite;
 coherence by Th20;
end;


theorem :: FINSET_1:21
 A is finite & B is finite & C is finite & D is finite
  implies [:A,B,C,D:] is finite;

registration let A,B,C,D be finite set;
 cluster [:A,B,C,D:] -> finite;
 coherence by Th21;
end;


theorem :: FINSET_1:22
   B <> {} & [:A,B:] is finite implies A is finite;

theorem :: FINSET_1:23
   A <> {} & [:A,B:] is finite implies B is finite;

theorem :: FINSET_1:24
 A is finite iff bool A is finite;

theorem :: FINSET_1:25
   A is finite & (for X st X in A holds X is finite) iff union A is finite;

theorem :: FINSET_1:26
   dom f is finite implies rng f is finite;

theorem :: FINSET_1:27
   Y c= rng f & f"Y is finite implies Y is finite;
