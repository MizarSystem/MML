:: Properties of Groups
::  by Gijs Geleijnse and Grzegorz Bancerek
::
:: Received May 31, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies CARD_1, REALSET1, GROUP_2, BOOLE, GRAPH_1, INT_1, NAT_1, ARYTM_1,
      ARYTM_3, GROUP_1, FINSET_1, GROUP_4, VECTSP_1, PBOOLE, TARSKI, RELAT_1,
      FINSEQ_1, FUNCT_1, FILTER_0, GROUP_8, SETFAM_1, LATTICES;
 notations XBOOLE_0, CARD_1, TARSKI, ZFMISC_1, SUBSET_1, XCMPLX_0, XREAL_0,
      RELAT_1, FUNCT_1, FUNCT_2, PBOOLE, BINOP_1, INT_1, NAT_1, RLVECT_1,
      FINSET_1, GROUP_2, SETFAM_1, GROUP_4, GROUP_6, GR_CY_1, FINSEQ_1,
      STRUCT_0, GROUP_1, INT_2;
 constructors REAL_1, BINOP_1, NAT_1, GROUP_4, GR_CY_1, PREPOWER, DOMAIN_1,
      PBOOLE, WELLORD2, GROUP_6, REALSET2, BINOP_2;
 registrations INT_1, GR_CY_1, STRUCT_0, XREAL_0, GROUP_2, FINSEQ_1, RELSET_1,
      GROUP_1, SUBSET_1;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;


begin

 reserve G for strict Group,
         a,b,x,y,z for Element of the carrier of G,
         H,K for strict Subgroup of G,
         h for Element of the carrier of H,
         p,n for Nat,
         A for Subset of the carrier of G;

theorem :: GROUP_8:1
  p is prime & ord G = p & G is finite implies
  ex a st ord a = p;

theorem :: GROUP_8:2
for a1,a2 being Element of the carrier of H
for b1,b2 being Element of the carrier of G st a1 = b1 & a2 = b2
 holds a1*a2 = b1*b2;

theorem :: GROUP_8:3
for a being Element of the carrier of H
for b being Element of the carrier of G st a = b
for n being Nat
 holds a|^n = b|^n;

theorem :: GROUP_8:4
for a being Element of the carrier of H
for b being Element of the carrier of G st a = b
for i being Integer
 holds a|^i = b|^i;

theorem :: GROUP_8:5
for a being Element of the carrier of H
for b being Element of the carrier of G st a = b & G is finite
 holds ord a = ord b;

theorem :: GROUP_8:6
for h being Element of the carrier of G st
    h in H holds   H  * h c= the carrier of H;

theorem :: GROUP_8:7
  for a st a <> 1.G holds gr {a} <> (1).G;

theorem :: GROUP_8:8
for m being Integer holds (1.G) |^ m = 1.G;

theorem :: GROUP_8:9
for m being Integer holds a |^ (m * ord a) = 1.G;

theorem :: GROUP_8:10
for a st a is_not_of_order_0
for m being Integer holds a |^ m = a |^(m mod ord a);

theorem :: GROUP_8:11
b is_not_of_order_0 implies gr {b} is finite;

theorem :: GROUP_8:12
b is_of_order_0 implies b" is_of_order_0;

theorem :: GROUP_8:13
b is_of_order_0 iff
for n being Integer holds b |^n = 1.G implies n = 0;

theorem :: GROUP_8:14
for G st ex a st a <> 1.G holds
(for H holds H = G or H = (1).G) iff
G is cyclic Group & G is finite & ex p being Nat st ord G = p & p is prime;

theorem :: GROUP_8:15
  for x,y,z being Element of the carrier of G
  for A being Subset of the carrier of G holds
  z in x * A * y iff ex a being Element of the carrier of G st
    z = x * a * y & a in A;

theorem :: GROUP_8:16
  for A being non empty Subset of G holds
  for x being Element of the carrier of G holds
  Card A = Card (x" * A * x);

definition let G, H, K;
  func Double_Cosets (H, K) -> Subset-Family of the carrier of G means
:: GROUP_8:def 1
    A in it iff ex a st A = H * a * K;
end;

theorem :: GROUP_8:17
  z in H * x * K iff ex g,h being Element of the carrier of G st
    z = g * x * h & g in H & h in K;

theorem :: GROUP_8:18
  for H, K holds H * x * K = H * y * K or
    not ex z st z in H * x * K & z in H * y * K;

 reserve C for strict Subgroup of G,
         B, A for strict Subgroup of G,
         D for strict Subgroup of A,
         E for strict Subgroup of B;

registration let G,A;
  cluster Left_Cosets A -> non empty;
end;

notation let G;
  let H be Subgroup of G;
  synonym index (G,H) for index H;
end;

theorem :: GROUP_8:19
  G = A "\/" B & D = A /\ B & G is finite implies
    index(G,B) >= index(A,D);

theorem :: GROUP_8:20
G is finite implies index (G,H) > 0;

theorem :: GROUP_8:21
for G being strict Group st G is finite
for C being strict Subgroup of G
for A,B being strict Subgroup of C st C = A "\/" B
for D being strict Subgroup of A st D = A /\ B
for E being strict Subgroup of B st E = A /\ B
for F being strict Subgroup of C st F = A /\ B
holds
  Left_Cosets(B) is finite & Left_Cosets(A) is finite &
  index (C,A), index (C,B) are_relative_prime
implies index (C,B) = index (A,D) & index (C,A) = index (B,E);

theorem :: GROUP_8:22
for a being Element of the carrier of G holds a in H implies
for j being Integer holds a |^ j in H;

 reserve a for Element of the carrier of G;

theorem :: GROUP_8:23
  for G being strict Group st G <> (1).G
    ex b being Element of the carrier of G st b <> 1.G;

theorem :: GROUP_8:24
for G being strict Group
for a being Element of the carrier of G st G = gr{a} & G <> (1).G
for H being strict Subgroup of G st H <> (1).G holds
ex k being Nat st 0 < k & a |^k in H;

theorem :: GROUP_8:25
  for G being strict cyclic Group st G <> (1).G
  for H being strict Subgroup of G st H <> (1).G holds
  H is cyclic Group;
