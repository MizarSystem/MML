:: Weights of Continuous Lattices
::  by Robert Milewski
::
:: Received January 6, 2000
:: Copyright (c) 2000 Association of Mizar Users

environ

 vocabulary ORDERS_1, SETFAM_1, WAYBEL_0, TARSKI, LATTICES, ORDINAL2, WAYBEL23,
      YELLOW_1, YELLOW_0, WAYBEL_8, CARD_1, ORDINAL1, SUBSET_1, PRE_TOPC,
      CANTOR_1, WAYBEL_3, BOOLE, FINSET_1, TSP_1, FUNCT_1, RELAT_1, WAYBEL11,
      YELLOW_9, WAYBEL19, PRELAMB, DIRAF, PROB_1, COMPTS_1, QUANTAL1, LATTICE3,
      REALSET1, RELAT_2, FILTER_2, FINSEQ_1, MATRIX_2, BHSP_3, FINSEQ_4,
      WAYBEL_9, WELLORD1, CAT_1, WAYBEL31, RLVECT_3;
 notation TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, SETFAM_1, STRUCT_0, FINSET_1,
      RELSET_1, GROUP_1, RELAT_1, REALSET1, FUNCT_1, FUNCT_2, FINSEQ_1,
      FINSEQ_2, FINSEQ_4, MATRIX_2, ORDINAL1, CARD_1, PRE_TOPC, TSP_1, TOPS_2,
      CANTOR_1, ORDERS_1, LATTICE3, YELLOW_0, YELLOW_1, YELLOW_2, YELLOW_9,
      WAYBEL_0, WAYBEL_1, WAYBEL_3, WAYBEL_8, WAYBEL_9, WAYBEL11, WAYBEL19,
      WAYBEL23;
 constructors NAT_1, GROUP_1, FINSEQ_4, MATRIX_2, TSP_1, TOPS_2, CANTOR_1,
      URYSOHN1, ORDERS_3, YELLOW_9, WAYBEL_1, WAYBEL_8, WAYBEL11, WAYBEL19,
      WAYBEL23, MEMBERED;
 clusters STRUCT_0, RELSET_1, FINSET_1, FINSEQ_1, CARD_1, TSP_1, SUBSET_1,
      CANTOR_1, LATTICE3, YELLOW_0, YELLOW_1, YELLOW_2, YELLOW_3, YELLOW_9,
      YELLOW11, YELLOW13, YELLOW15, WAYBEL_0, WAYBEL_3, WAYBEL_4, WAYBEL_8,
      WAYBEL14, WAYBEL19, WAYBEL23, WAYBEL25, WAYBEL30, ZFMISC_1;
 requirements NUMERALS, BOOLE, SUBSET;


begin

  scheme :: WAYBEL31:sch 1
 UparrowUnion{L1()->RelStr,P[set]}:
   for S be Subset-Family of L1() st
    S = { X where X is Subset of L1() : P[X] } holds
     uparrow union S = union { uparrow X where X is Subset of L1() : P[X] }
proof
    let S be Subset-Family of L1();
    assume A1: S = { X where X is Subset of L1() : P[X] };
    A2: uparrow union S c= union { uparrow X where X is Subset of L1() : P[X]}
    proof
     let z be set;
     assume A3: z in uparrow union S;
     then reconsider z1 = z as Element of L1();
     consider y1 be Element of L1() such that
      A4: y1 <= z1 and
      A5: y1 in union S by A3,WAYBEL_0:def 16;
     consider Z be set such that
      A6: y1 in Z and
      A7: Z in S by A5,TARSKI:def 4;
     consider X1 be Subset of L1() such that
      A8: Z = X1 and
      A9: P[X1] by A1,A7;
     A10: z in uparrow X1 by A4,A6,A8,WAYBEL_0:def 16;
       uparrow X1 in { uparrow X where X is Subset of L1() : P[X] } by A9;
     hence z in union { uparrow X where X is Subset of L1() : P[X] }
                                                         by A10,TARSKI:def 4;
    end;
      union { uparrow X where X is Subset of L1() : P[X] } c= uparrow union S
    proof
     let z be set;
     assume z in union { uparrow X where X is Subset of L1() : P[X] };
     then consider Z be set such that
      A11: z in Z and
      A12: Z in
 { uparrow X where X is Subset of L1() : P[X] } by TARSKI:def 4;
     consider X1 be Subset of L1() such that
      A13: Z = uparrow X1 and
      A14: P[X1] by A12;
     reconsider z1 = z as Element of L1() by A11,A13;
     consider y1 be Element of L1() such that
      A15: y1 <= z1 and
      A16: y1 in X1 by A11,A13,WAYBEL_0:def 16;
       X1 in S by A1,A14;
     then y1 in union S by A16,TARSKI:def 4;
     hence z in uparrow union S by A15,WAYBEL_0:def 16;
    end;
    hence uparrow union S =
     union { uparrow X where X is Subset of L1() : P[X] } by A2,XBOOLE_0:def 10
;
   end;

  scheme :: WAYBEL31:sch 2
 DownarrowUnion{L1()->RelStr,P[set]}:
   for S be Subset-Family of L1() st
    S = { X where X is Subset of L1() : P[X] } holds
     downarrow union S = union { downarrow X where X is Subset of L1() : P[X]}
proof
    let S be Subset-Family of L1();
    assume A1: S = { X where X is Subset of L1() : P[X] };
    A2: downarrow union S c=
     union { downarrow X where X is Subset of L1() : P[X] }
    proof
     let z be set;
     assume A3: z in downarrow union S;
     then reconsider z1 = z as Element of L1();
     consider y1 be Element of L1() such that
      A4: y1 >= z1 and
      A5: y1 in union S by A3,WAYBEL_0:def 15;
     consider Z be set such that
      A6: y1 in Z and
      A7: Z in S by A5,TARSKI:def 4;
     consider X1 be Subset of L1() such that
      A8: Z = X1 and
      A9: P[X1] by A1,A7;
     A10: z in downarrow X1 by A4,A6,A8,WAYBEL_0:def 15;
       downarrow X1 in { downarrow X where X is Subset of L1() : P[X] } by A9;
     hence z in union { downarrow X where X is Subset of L1() : P[X] }
                                                         by A10,TARSKI:def 4;
    end;
      union { downarrow X where X is Subset of L1(): P[X] } c= downarrow union
S
    proof
     let z be set;
     assume z in union { downarrow X where X is Subset of L1() : P[X] };
     then consider Z be set such that
      A11: z in Z and
      A12: Z in
 { downarrow X where X is Subset of L1(): P[X]} by TARSKI:def 4;
     consider X1 be Subset of L1() such that
      A13: Z = downarrow X1 and
      A14: P[X1] by A12;
     reconsider z1 = z as Element of L1() by A11,A13;
     consider y1 be Element of L1() such that
      A15: y1 >= z1 and
      A16: y1 in X1 by A11,A13,WAYBEL_0:def 15;
       X1 in S by A1,A14;
     then y1 in union S by A16,TARSKI:def 4;
     hence z in downarrow union S by A15,WAYBEL_0:def 15;
    end;
    hence downarrow union S = union { downarrow X where X is Subset of L1() :
     P[X] } by A2,XBOOLE_0:def 10;
   end;

  registration
   let L1 be lower-bounded continuous sup-Semilattice;
   let B1 be with_bottom CLbasis of L1;
   cluster InclPoset Ids subrelstr B1 -> algebraic;
   coherence by WAYBEL13:10;
  end;


  definition  :: DEFINITION 4.5
   let L1 be continuous sup-Semilattice;
   func CLweight L1 -> Cardinal equals
:: WAYBEL31:def 1
    meet {Card B1 where B1 is with_bottom CLbasis of L1 : not contradiction};
  end;


  theorem :: WAYBEL31:1
   for T be TopStruct
   for b be Basis of T holds
    weight T c= Card b;

  theorem :: WAYBEL31:2
   for T be TopStruct
   ex b be Basis of T st
    Card b = weight T;

  theorem :: WAYBEL31:3
   for L1 be continuous sup-Semilattice
   for B1 be with_bottom CLbasis of L1 holds
    CLweight L1 c= Card B1;

  theorem :: WAYBEL31:4
   for L1 be continuous sup-Semilattice
   ex B1 be with_bottom CLbasis of L1 st
    Card B1 = CLweight L1;

  theorem :: WAYBEL31:5   :: Under 4.5.
   for L1 be algebraic lower-bounded LATTICE holds
    CLweight L1 = Card the carrier of CompactSublatt L1;

  theorem :: WAYBEL31:6
   for T be non empty TopSpace
   for L1 be continuous sup-Semilattice st
    InclPoset the topology of T = L1
   for B1 be with_bottom CLbasis of L1 holds
    B1 is Basis of T;

  theorem :: WAYBEL31:7
   for T be non empty TopSpace
   for L1 be continuous lower-bounded LATTICE st
    InclPoset the topology of T = L1
   for B1 be Basis of T
   for B2 be Subset of L1 st B1 = B2 holds
    finsups B2 is with_bottom CLbasis of L1;

  theorem :: WAYBEL31:8   :: PROPOSITION 4.6 (i)
   for T be T_0 non empty TopSpace
   for L1 be continuous lower-bounded sup-Semilattice st
    InclPoset the topology of T = L1 holds
     T is infinite implies weight T = CLweight L1;

  theorem :: WAYBEL31:9   :: PROPOSITION 4.6 (ii) (a)
   for T be T_0 non empty TopSpace
   for L1 be continuous sup-Semilattice st
    InclPoset the topology of T = L1 holds
     Card the carrier of T c= Card the carrier of L1;

  theorem :: WAYBEL31:10  :: PROPOSITION 4.6 (ii) (b)
   for T be T_0 non empty TopSpace st T is finite holds
    weight T = Card the carrier of T;

  theorem :: WAYBEL31:11   :: PROPOSITION 4.6 (ii) (c)
   for T be TopStruct
   for L1 be continuous lower-bounded LATTICE st
    InclPoset the topology of T = L1 & T is finite holds
     CLweight L1 = Card the carrier of L1;

  theorem :: WAYBEL31:12
   for L1 be continuous lower-bounded sup-Semilattice
   for T1 be Scott TopAugmentation of L1
   for T2 be Lawson correct TopAugmentation of L1
   for B2 be Basis of T2 holds
    { uparrow V where V is Subset of T2 : V in B2 } is Basis of T1;

canceled;

  theorem :: WAYBEL31:14
   for L1 be up-complete (non empty Poset) st L1 is finite
   for x be Element of L1 holds
    x in compactbelow x;

  theorem :: WAYBEL31:15
   for L1 be finite LATTICE holds
    L1 is arithmetic;

  registration
   cluster finite -> arithmetic LATTICE;
   coherence by Th15;
  end;


  registration
   cluster trivial reflexive transitive antisymmetric with_suprema with_infima
    lower-bounded non empty finite strict RelStr;
   existence
   proof
      0 in {0} by TARSKI:def 1;
    then reconsider r = {[0,0]} as Relation of {0} by RELSET_1:8;
    reconsider T = RelStr(#{0},r#) as non empty RelStr;
    take T;
    thus T is trivial;
    thus T is reflexive
    proof
     let x be Element of T;
       x = 0 by TARSKI:def 1;
     then [x,x] in {[0,0]} by TARSKI:def 1;
     hence x <= x by ORDERS_1:def 9;
    end;
    then reconsider T' = T as trivial non empty reflexive RelStr;
      T' is transitive;
    hence T is transitive;
      T' is antisymmetric;
    hence T is antisymmetric;
      T' is with_suprema;
    hence T is with_suprema;
      T' is with_infima;
    hence T is with_infima;
    reconsider T'' = T' as LATTICE;
      T'' is lower-bounded;
    hence T is lower-bounded;
    thus T is non empty;
    thus T is finite;
    thus thesis;
   end;
  end;


  theorem :: WAYBEL31:16
   for L1 be finite LATTICE
   for B1 be with_bottom CLbasis of L1 holds
    Card B1 = CLweight L1 iff B1 = the carrier of CompactSublatt L1;

  definition
   let L1 be non empty reflexive RelStr;
   let A be Subset of L1;
   let a be Element of L1;
   func Way_Up(a,A) -> Subset of L1 equals
:: WAYBEL31:def 2
    wayabove a \ uparrow A;
  end;


  theorem :: WAYBEL31:17
     for L1 be non empty reflexive RelStr
   for a be Element of L1 holds
    Way_Up(a,{}(L1)) = wayabove a;

  theorem :: WAYBEL31:18
     for L1 be non empty Poset
   for A be Subset of L1
   for a be Element of L1 st a in uparrow A holds
    Way_Up(a,A) = {};

  theorem :: WAYBEL31:19
   for L1 be non empty finite reflexive transitive RelStr holds
    Ids L1 is finite;

  theorem :: WAYBEL31:20
   for L1 be continuous lower-bounded sup-Semilattice st L1 is infinite
   for B1 be with_bottom CLbasis of L1 holds
    B1 is infinite;

canceled 2;

  theorem :: WAYBEL31:23
     for L1 be complete (non empty Poset)
   for x be Element of L1 holds
    x is compact implies x = inf wayabove x;

  theorem :: WAYBEL31:24
   for L1 be continuous lower-bounded sup-Semilattice st L1 is infinite
   for B1 be with_bottom CLbasis of L1 holds
    Card { Way_Up(a,A) where a is Element of L1, A is finite Subset of L1 :
           a in B1 & A c= B1 } c= Card B1;

  theorem :: WAYBEL31:25
   for T be Lawson (complete TopLattice)
   for X be finite Subset of T holds
    (uparrow X)` is open & (downarrow X)` is open;

  theorem :: WAYBEL31:26
   for L1 be continuous lower-bounded sup-Semilattice
   for T be Lawson correct TopAugmentation of L1 holds
   for B1 be with_bottom CLbasis of L1 holds
    { Way_Up(a,A) where a is Element of L1, A is finite Subset of L1 :
           a in B1 & A c= B1 } is Basis of T;

  theorem :: WAYBEL31:27
   for L1 be continuous lower-bounded sup-Semilattice
   for T be Scott TopAugmentation of L1
   for b be Basis of T holds
    { wayabove inf u where u is Subset of T : u in b } is Basis of T;

  theorem :: WAYBEL31:28
   for L1 be continuous lower-bounded sup-Semilattice
   for T be Scott TopAugmentation of L1
   for B1 be Basis of T st B1 is infinite holds
    { inf u where u is Subset of T : u in B1 } is infinite;

  theorem :: WAYBEL31:29   :: THEOREM 4.7 (1)=(2)
   for L1 be continuous lower-bounded sup-Semilattice
   for T be Scott TopAugmentation of L1 holds
    CLweight L1 = weight T;

  theorem :: WAYBEL31:30  :: THEOREM 4.7 (1)=(3)
     for L1 be continuous lower-bounded sup-Semilattice
   for T be Lawson correct TopAugmentation of L1 holds
    CLweight L1 = weight T;

  theorem :: WAYBEL31:31
   for L1,L2 be non empty RelStr st L1,L2 are_isomorphic holds
    Card the carrier of L1 = Card the carrier of L2;

  theorem :: WAYBEL31:32  :: THEOREM 4.7 (1)=(4)
     for L1 be continuous lower-bounded sup-Semilattice
   for B1 be with_bottom CLbasis of L1 st Card B1 = CLweight L1 holds
    CLweight L1 = CLweight InclPoset Ids subrelstr B1;

  registration
   let L1 be continuous lower-bounded sup-Semilattice;
   cluster InclPoset sigma L1 -> with_suprema continuous;
   coherence
   proof
    consider S be Scott TopAugmentation of L1;
    A1: the RelStr of S = the RelStr of L1 by YELLOW_9:def 4;
      InclPoset sigma S is complete;
    hence InclPoset sigma L1 is with_suprema by A1,YELLOW_9:52;
      InclPoset sigma S is continuous by WAYBEL14:36;
    hence thesis by A1,YELLOW_9:52;
   end;
  end;


  theorem :: WAYBEL31:33  :: THEOREM 4.7 (5)
     for L1 be continuous lower-bounded sup-Semilattice holds
    CLweight L1 c= CLweight InclPoset sigma L1;

  theorem :: WAYBEL31:34  :: THEOREM 4.7 (6)
     for L1 be continuous lower-bounded sup-Semilattice st L1 is infinite holds
    CLweight L1 = CLweight InclPoset sigma L1;

