:: Uniform Boundedness Principle
::  by Hideki Sakurai , Hisayoshi Kunimune and Yasunari Shidama
::
:: Received October 9, 2007
:: Copyright (c) 2007 Association of Mizar Users

environ

 vocabularies TARSKI, RLVECT_1, FUNCT_1, PRE_TOPC, ARYTM, BOOLE, ABSVALUE,
      ARYTM_1, FCONT_1, FINSEQ_4, ARYTM_3, RELAT_1, SEQ_2, LATTICES, ORDINAL2,
      NORMSP_1, SUBSET_1, TOPS_1, METRIC_1, PCOMPS_1, NORMSP_2, PROB_1, BHSP_3,
      SEQ_1, SEQ_4, RINFSUP1, FUNCT_2, SEQM_3, RSSPACE3, LOPBAN_1, TDGROUP,
      MSUALG_3;
 notations TARSKI, XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, FUNCT_2, PARTFUN1,
      PRE_TOPC, TOPS_1, STRUCT_0, ORDINAL1, CARD_3, NUMBERS, XREAL_0, RINFSUP1,
      COMPLEX1, REAL_1, NAT_1, XXREAL_0, VALUED_1, SEQ_1, SEQ_2, SEQM_3, SEQ_4,
      PSCOMP_1, RLVECT_1, TBSP_1, METRIC_1, PCOMPS_1, NORMSP_1, NORMSP_2,
      RSSPACE3, LOPBAN_1, NFCONT_1, INTEGRA2, KURATO_2;
 constructors REAL_1, COMPLEX1, SEQ_1, SETFAM_1, TOPS_1, TBSP_1, NFCONT_1,
      FRECHET2, NAT_1, PSCOMP_1, PARTFUN1, RINFSUP1, INTEGRA2, PROB_1,
      NORMSP_2, RSSPACE3, LOPBAN_1;
 registrations NUMBERS, XREAL_0, XBOOLE_0, XXREAL_0, ORDINAL1, RELSET_1,
      STRUCT_0, MEMBERED, SUBSET_1, NAT_1, NORMSP_1, NORMSP_2, FUNCT_2,
      LOPBAN_1, VALUED_0, VALUED_1;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;


begin :: Uniform Boundedness Principle

theorem :: LOPBAN_5:1
  for seq be Real_Sequence, r be Real st
  seq is bounded & 0<=r holds lim_inf(r(#)seq)=r*(lim_inf seq);

theorem :: LOPBAN_5:2
  for seq be Real_Sequence, r be Real st
  seq is bounded & 0<=r holds lim_sup(r(#)seq)=r*(lim_sup seq);

registration
  let X be RealBanachSpace;
  cluster MetricSpaceNorm X -> complete;
end;

definition
  let X be RealBanachSpace, x0 be Point of X, r be real number;
  func Ball (x0,r) -> Subset of X equals
:: LOPBAN_5:def 1
  { x where x is Point of X : ||.x0-x.|| < r };
end;

theorem :: LOPBAN_5:3

:: Baire category theorem - Banach space version
  for X be RealBanachSpace, Y be SetSequence of X st
  union rng Y = the carrier of X &
  (for n be Element of NAT holds Y.n is closed) holds
  ex n0 be Element of NAT, r be Real, x0 be Point of X st
  0 < r & Ball (x0,r) c= Y.n0;

theorem :: LOPBAN_5:4
  for X,Y be RealNormSpace, f be bounded LinearOperator of X,Y holds
  f is_Lipschitzian_on the carrier of X &
  f is_continuous_on the carrier of X &
  for x be Point of X holds f is_continuous_in x;

theorem :: LOPBAN_5:5
  for X be RealBanachSpace, Y be RealNormSpace,
  T be Subset of R_NormSpace_of_BoundedLinearOperators(X,Y) st
  for x be Point of X ex K be real number st 0 <= K &
  for f be Point of R_NormSpace_of_BoundedLinearOperators(X,Y) st
  f in T holds ||. f.x .|| <= K holds ex L be real number st 0 <= L &
  for f be Point of R_NormSpace_of_BoundedLinearOperators(X,Y) st
  f in T holds ||.f.|| <= L;

definition
  let X, Y be RealNormSpace, H be Function of NAT,
  the carrier of R_NormSpace_of_BoundedLinearOperators(X,Y), x be Point of X;
  func H # x -> sequence of Y means
:: LOPBAN_5:def 2

  for n be Element of NAT holds it.n = (H.n).x;
end;

theorem :: LOPBAN_5:6
  for X be RealBanachSpace, Y be RealNormSpace,
  vseq be sequence of R_NormSpace_of_BoundedLinearOperators(X,Y),
  tseq be Function of X,Y st
  ( for x be Point of X holds vseq#x is convergent & tseq.x = lim(vseq#x) )
  holds tseq is bounded LinearOperator of X,Y &

(for x be Point of X holds ||.tseq.x.|| <=( lim_inf ||.vseq.|| ) * ||.x.|| ) &
  for ttseq be Point of R_NormSpace_of_BoundedLinearOperators(X,Y) st
  ttseq = tseq holds ||.ttseq.|| <= lim_inf ||.vseq.||;

begin :: Banach-Steinhaus Principle

theorem :: LOPBAN_5:7
  for X be RealBanachSpace, X0 be Subset of LinearTopSpaceNorm(X),
  Y be RealBanachSpace,
  vseq be sequence of R_NormSpace_of_BoundedLinearOperators(X,Y) st
  X0 is dense & (for x be Point of X st x in X0 holds vseq#x is convergent) &
  (for x be Point of X ex K be real number st
  0<=K & for n be Element of NAT holds ||.(vseq#x).n.|| <=K) holds
  for x be Point of X holds vseq#x is convergent;

theorem :: LOPBAN_5:8
  for X,Y be RealBanachSpace, X0 be Subset of LinearTopSpaceNorm(X),
  vseq be sequence of R_NormSpace_of_BoundedLinearOperators(X,Y) st
  X0 is dense &
  ( for x be Point of X st x in X0 holds vseq#x is convergent ) &
  ( for x be Point of X ex K be real number st 0<=K &
  ( for n be Element of NAT holds ||. (vseq#x).n .|| <= K ) ) holds
  ex tseq be Point of R_NormSpace_of_BoundedLinearOperators(X,Y) st
  ( for x be Point of X holds vseq#x is convergent & tseq.x=lim (vseq#x) &
  ||.tseq.x.|| <= lim_inf ||.vseq.|| * ||.x.|| ) &
  ||.tseq.|| <= lim_inf ||.vseq.||;

