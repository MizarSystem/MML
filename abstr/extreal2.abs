:: Some Properties of Extended Real Numbers Operations: absolute
:: value, min and max
::  by Noboru Endou , Katsumi Wasaki and Yasunari Shidama
::
:: Received September 15, 2000
:: Copyright (c) 2000 Association of Mizar Users

environ

 vocabularies NUMBERS, SUPINF_1, REAL_1, COMPLEX1, CARD_1, XXREAL_0, ARYTM_1,
      ARYTM_3, RELAT_1, MESFUNC1, MEASURE6;
 notations NUMBERS, XXREAL_0, COMPLEX1, REAL_1, SUPINF_1, SUPINF_2, MEASURE6,
      EXTREAL1, MESFUNC1;
 constructors REAL_1, COMPLEX1, SUPINF_2, MEASURE6, EXTREAL1, MESFUNC1,
      SUPINF_1, BINOP_2;
 registrations NUMBERS, XXREAL_0, XREAL_0, MEMBERED, XBOOLE_0, XXREAL_3;
 requirements NUMERALS, REAL, SUBSET, BOOLE, ARITHM;


begin :: Preliminaries

reserve x,y,w,z for R_eal,
  a,b for Real;

begin :: Basic properties of absolute value for extended real numbers

canceled 48;

theorem :: EXTREAL2:49
  x = a implies |.x.| = abs(a);

theorem :: EXTREAL2:50
  |.x.| = x or |.x.| = -x;

theorem :: EXTREAL2:51  :: extension of ABSVALUE:5
  0 <= |.x.|;

theorem :: EXTREAL2:52  :: extension of ABSVALUE:6
  x <> 0 implies 0 < |.x.|;

theorem :: EXTREAL2:53 :: extension of ABSVALUE:7
  x = 0 iff |.x.| = 0;

theorem :: EXTREAL2:54 :: extension of ABSVALUE:9
  |.x.| = -x & x <> 0 implies x < 0;

theorem :: EXTREAL2:55
  x <= 0 implies |.x.| = -x;

theorem :: EXTREAL2:56 :: extension of ABSVALUE:10
  |.x * y.| = |.x.| * |.y.|;

theorem :: EXTREAL2:57  :: extension of ABSVALUE:11
  -|.x.| <= x & x <= |.x.|;

theorem :: EXTREAL2:58
  |.x.| < y implies -y < x & x < y;

theorem :: EXTREAL2:59
  -y < x & x < y implies 0 < y & |.x.| < y;

theorem :: EXTREAL2:60  :: extension of ABSVALUE:12
  -y <= x & x <= y iff |.x.| <= y;

theorem :: EXTREAL2:61  :: extension of ABSVALUE:13
  |.x + y.| <= |.x.| + |.y.|;

theorem :: EXTREAL2:62
  -infty < x & x < +infty & x <> 0 implies |.x.|*|. 1./x .| = 1.;

theorem :: EXTREAL2:63
  x = +infty or x = -infty implies |.x.|*|. 1./x .| = 0;

theorem :: EXTREAL2:64 :: extension of ABSVALUE:15
  x <> 0 implies |. 1./x .| = 1./|.x.|;

theorem :: EXTREAL2:65 :: extension of ABSVALUE:16
  not((x=-infty or x=+infty) & (y=-infty or y=+infty)) & y<>0 implies |.
  x/y.| = |.x.|/|.y.|;

theorem :: EXTREAL2:66  :: extension of ABSVALUE:17
  |.x.| = |.-x.|;

theorem :: EXTREAL2:67
  |.+infty.| = +infty & |.-infty.| = +infty;

theorem :: EXTREAL2:68  :: extension of ABSVALUE:18
  x is Real or y is Real implies |.x.|-|.y.| <= |.x-y.|;

theorem :: EXTREAL2:69 :: extension of ABSVALUE:19
  not( x = +infty & y = +infty or x = -infty & y = -infty ) implies |.x-
  y.| <= |.x.| + |.y.|;

theorem :: EXTREAL2:70 :: extension of ABSVALUE:20
  |.|.x.|.| = |.x.|;

theorem :: EXTREAL2:71 :: extension of ABSVALUE:21
  |.x.| <= z & |.y.| <= w implies |.x+y.| <= z + w;

theorem :: EXTREAL2:72 :: extension of ABSVALUE:22
  x is Real or y is Real implies |.|.x.|-|.y.|.| <= |.x-y.|;

theorem :: EXTREAL2:73 :: extension of ABSVALUE:24
  0 <= x * y implies |.x+y.| = |.x.|+|.y.|;

begin

canceled 6;

theorem :: EXTREAL2:80 :: extension of SQUARE_1:34
  x <> +infty & y <> +infty & not ( x = +infty & y = +infty or x =
  -infty & y = -infty ) implies min(x,y) = (x + y - |.x - y.|) / R_EAL 2;

canceled 10;

theorem :: EXTREAL2:91 :: extension of SQUARE_1:45
  x <> -infty & y <> -infty & not ( x = +infty & y = +infty or x =
  -infty & y = -infty ) implies max(x,y) = (x + y + |.x - y.|) / R_EAL 2;

definition
  let x,y be R_eal;
  redefine func max(x,y) -> R_eal;
  func min(x,y) -> R_eal;
end;

canceled 8;

theorem :: EXTREAL2:100 :: extension of SQUARE_1:53
  min(x,y) + max(x,y)= x + y;

begin ::Addenda

:: missing, 2007.07.20, A.T.

theorem :: EXTREAL2:101
  |.x.| = +infty implies x = +infty or x = -infty;

theorem :: EXTREAL2:102
  |.x.| < +infty iff x in REAL;

