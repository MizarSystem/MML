:: On Kolmogorov Topological Spaces
::  by Zbigniew Karno
::
:: Received July 26, 1994
:: Copyright (c) 1994 Association of Mizar Users

environ

 vocabulary PRE_TOPC, BOOLE, SUBSET_1, METRIC_1, REALSET1, TDLAT_3, NATTRA_1,
      SETFAM_1, TARSKI, COLLSP, TSP_1;
 notation TARSKI, XBOOLE_0, SUBSET_1, STRUCT_0, PRE_TOPC, BORSUK_1, TSEP_1,
      TDLAT_3, TEX_1, TEX_2, T_0TOPSP, TEX_4;
 constructors REALSET1, TSEP_1, TDLAT_3, TEX_2, T_0TOPSP, TEX_4, BORSUK_1,
      MEMBERED;
 clusters TDLAT_3, TEX_1, TEX_2, STRUCT_0, MEMBERED, ZFMISC_1;
 requirements SUBSET, BOOLE;


begin
:: 1. Subspaces.

definition let Y be TopStruct;
 redefine mode SubSpace of Y -> TopStruct means
:: TSP_1:def 1
 the carrier of it c= the carrier of Y &
 for G0 being Subset of it holds G0 is open iff
   ex G being Subset of Y
      st G is open & G0 = G /\ the carrier of it;
end;


canceled;

theorem :: TSP_1:2
 for Y being TopStruct, Y0 being SubSpace of Y
  for G being Subset of Y st G is open holds
    ex G0 being Subset of Y0
    st G0 is open & G0 = G /\ the carrier of Y0;

definition let Y be TopStruct;
 redefine
 mode SubSpace of Y -> TopStruct means
:: TSP_1:def 2
 the carrier of it c= the carrier of Y &
 for F0 being Subset of it holds F0 is closed iff
   ex F being Subset of Y
   st F is closed & F0 = F /\ the carrier of it;
end;


canceled;

theorem :: TSP_1:4
 for Y being TopStruct, Y0 being SubSpace of Y
  for F being Subset of Y st F is closed holds
    ex F0 being Subset of Y0
    st F0 is closed & F0 = F /\ the carrier of Y0;


begin
:: 2. Kolmogorov Spaces.

notation let T be TopStruct;
 synonym T is T_0 for T is discerning ;
end;
definition let T be TopStruct;
 redefine attr T is T_0 means
:: TSP_1:def 3
 T is empty or
   for x, y being Point of T st x <> y holds
   (ex V being Subset of T st V is open & x in V & not y in V) or
   (ex W being Subset of T st W is open & not x in W & y in W);
 end;


definition let Y be TopStruct;
 redefine attr Y is T_0 means
:: TSP_1:def 4
 Y is empty or
  for x, y being Point of Y st x <> y holds
   (ex E being Subset of Y st E is closed & x in E & not y in E)
   or
   (ex F being Subset of Y st F is closed & not x in F & y in F);
end;


registration
 cluster trivial -> T_0 (non empty TopStruct);
 coherence
  proof let Y be non empty TopStruct;
   assume Y is trivial;
    then consider a being Point of Y such that
           A1: the carrier of Y = {a} by TEX_1:def 1;
       now let x, y be Point of Y;
        A2: x = a & y = a by A1,TARSKI:def 1;
      assume x <> y;
      hence (ex V being Subset of Y
        st V is open & x in V & not y in V) or
       (ex W being Subset of Y
         st W is open & not x in W & y in W) by A2;
     end;
   hence thesis by Def3;
  end;
 cluster non T_0 -> non trivial (non empty TopStruct);
 coherence
  proof let Y be non empty TopStruct;
   assume A3: Y is non T_0;
   assume Y is trivial;
    then consider a being Point of Y such that
           A4: the carrier of Y = {a} by TEX_1:def 1;
       now let x, y be Point of Y;
        A5: x = a & y = a by A4,TARSKI:def 1;
      assume x <> y;
      hence (ex V being Subset of Y
        st V is open & x in V & not y in V) or
       (ex W being Subset of Y
         st W is open & not x in W & y in W) by A5;
     end;
    hence contradiction by A3,Def3;
  end;
end;

registration
 cluster strict T_0 non empty TopSpace;
 existence
  proof
    consider X being trivial strict (non empty TopSpace);
   take X;
   thus thesis;
  end;
 cluster strict non T_0 non empty TopSpace;
 existence
  proof
    consider X being anti-discrete non trivial strict (non empty TopSpace);
   take X;
   thus thesis by Lm1;
  end;
end;


registration
 cluster discrete -> T_0 (non empty TopSpace);
 coherence
  proof let Y be non empty TopSpace;
   assume A1: Y is discrete;
       now let x, y be Point of Y;
      assume A2: x <> y;
         now
        take V = {x};
        thus V is open by A1,TDLAT_3:17;
        thus x in V by TARSKI:def 1;
        thus not y in V by A2,TARSKI:def 1;
       end;
      hence (ex V being Subset of Y
         st V is open & x in V & not y in V) or
       (ex W being Subset of Y
          st W is open & not x in W & y in W);
     end;
   hence thesis by Def3;
  end;
 cluster non T_0 -> non discrete (non empty TopSpace);
 coherence
  proof let Y be non empty TopSpace;
   assume A3: Y is non T_0;
   assume A4: Y is discrete;
       now let x, y be Point of Y;
      assume A5: x <> y;
         now
        take V = {x};
        thus V is open by A4,TDLAT_3:17;
        thus x in V by TARSKI:def 1;
        thus not y in V by A5,TARSKI:def 1;
       end;
      hence (ex V being Subset of Y
         st V is open & x in V & not y in V) or
       (ex W being Subset of Y
         st W is open & not x in W & y in W);
     end;
    hence contradiction by A3,Def3;
  end;
 cluster anti-discrete non trivial -> non T_0 (non empty TopSpace);
 coherence by Lm1;
 cluster anti-discrete T_0 -> trivial (non empty TopSpace);
 coherence by Lm1;
 cluster T_0 non trivial -> non anti-discrete (non empty TopSpace);
 coherence by Lm1;
end;

definition let X be non empty TopSpace;
 redefine attr X is T_0 means
:: TSP_1:def 5
 for x, y being Point of X st x <> y holds Cl {x} <> Cl {y};
end;


definition let X be non empty TopSpace;
 redefine attr X is T_0 means
:: TSP_1:def 6
 for x, y being Point of X st x <> y holds
            not x in Cl {y} or not y in Cl {x};
end;


definition let X be non empty TopSpace;
 redefine attr X is T_0 means
:: TSP_1:def 7
   for x, y being Point of X st x <> y & x in Cl {y} holds
                                   not Cl {y} c= Cl {x};
end;


registration
 cluster almost_discrete T_0 -> discrete (non empty TopSpace);
 coherence
  proof let Y be non empty TopSpace;
   assume A1: Y is almost_discrete T_0;
      for A being Subset of Y, x being Point of Y st A = {x}
      holds A is closed
     proof let A be Subset of Y, x be Point of Y;
      assume A2: A = {x};
            x in Cl {x} by Lm2;
         then A3: {x} c= Cl {x} by ZFMISC_1:37;
            now
           assume not Cl {x} c= {x};
            then consider a being set such that
                  A4: a in Cl {x} and A5: not a in {x} by TARSKI:def 3;
              reconsider a as Point of Y by A4;
                  a <> x by A5,TARSKI:def 1;
               then not x in Cl {a} by A1,A4,Def6;
                then x in (Cl {a})` by SUBSET_1:50;
                then {x} c= (Cl {a})` by ZFMISC_1:37;
               then A6: {x} misses Cl {a} by SUBSET_1:43;
                 Cl {a} is closed by PCOMPS_1:4;
               then Cl {a} is open by A1,TDLAT_3:24;
               then A7: (Cl {x}) misses Cl {a} by A6,TSEP_1:40;
                   a in {a} & {a} c= Cl {a} by PRE_TOPC:48,TARSKI:def 1;
                hence contradiction by A4,A7,XBOOLE_0:3;
          end;
       then Cl {x} = {x} by A3,XBOOLE_0:def 10;
      hence A is closed by A2,PCOMPS_1:4;
     end;
    hence thesis by A1,TDLAT_3:28;
  end;
 cluster almost_discrete non discrete -> non T_0 (non empty TopSpace);
 coherence
  proof let Y be non empty TopSpace;
   assume A8: Y is almost_discrete non discrete;
   assume A9: Y is T_0;
      for A being Subset of Y, x being Point of Y st A = {x}
      holds A is closed
     proof let A be Subset of Y, x be Point of Y;
      assume A10: A = {x};
            x in Cl {x} by Lm2;
         then A11: {x} c= Cl {x} by ZFMISC_1:37;
            now
           assume not Cl {x} c= {x};
            then consider a being set such that
                  A12: a in Cl {x} and A13: not a in {x} by TARSKI:def 3;
              reconsider a as Point of Y by A12;
                  a <> x by A13,TARSKI:def 1;
               then not x in Cl {a} by A9,A12,Def6;
                then x in (Cl {a})` by SUBSET_1:50;
                then {x} c= (Cl {a})` by ZFMISC_1:37;
               then A14: {x} misses Cl {a} by SUBSET_1:43;
                    Cl {a} is closed by PCOMPS_1:4;
                 then Cl {a} is open by A8,TDLAT_3:24;
               then A15: (Cl {x}) misses Cl {a} by A14,TSEP_1:40;
                   a in {a} & {a} c= Cl {a} by PRE_TOPC:48,TARSKI:def 1;
                hence contradiction by A12,A15,XBOOLE_0:3;
          end;
       then Cl {x} = {x} by A11,XBOOLE_0:def 10;
      hence A is closed by A10,PCOMPS_1:4;
     end;
    hence contradiction by A8,TDLAT_3:28;
  end;
 cluster non discrete T_0 -> non almost_discrete (non empty TopSpace);
 coherence
  proof let Y be non empty TopSpace;
   assume A16: Y is non discrete T_0;
   assume A17: Y is almost_discrete;
      for A being Subset of Y, x being Point of Y st A = {x}
      holds A is closed
     proof let A be Subset of Y, x be Point of Y;
      assume A18: A = {x};
            x in Cl {x} by Lm2;
         then A19: {x} c= Cl {x} by ZFMISC_1:37;
            now
           assume not Cl {x} c= {x};
            then consider a being set such that
                  A20: a in Cl {x} and A21: not a in {x} by TARSKI:def 3;
              reconsider a as Point of Y by A20;
                  a <> x by A21,TARSKI:def 1;
               then not x in Cl {a} by A16,A20,Def6;
                then x in (Cl {a})` by SUBSET_1:50;
                then {x} c= (Cl {a})` by ZFMISC_1:37;
               then A22:{x} misses Cl {a} by SUBSET_1:43;
                    Cl {a} is closed by PCOMPS_1:4;
                 then Cl {a} is open by A17,TDLAT_3:24;
               then A23: (Cl {x}) misses Cl {a} by A22,TSEP_1:40;
                   a in {a} & {a} c= Cl {a} by PRE_TOPC:48,TARSKI:def 1;
                hence contradiction by A20,A23,XBOOLE_0:3;
          end;
       then Cl {x} = {x} by A19,XBOOLE_0:def 10;
      hence A is closed by A18,PCOMPS_1:4;
     end;
    hence contradiction by A16,A17,TDLAT_3:28;
  end;
end;


definition
 mode Kolmogorov_space is T_0 non empty TopSpace;
 mode non-Kolmogorov_space is non T_0 non empty TopSpace;
end;


registration
 cluster non trivial strict Kolmogorov_space;
 existence
  proof
    consider X being non trivial discrete strict (non empty TopSpace);
   take X;
   thus thesis;
  end;
 cluster non trivial strict non-Kolmogorov_space;
 existence
  proof
    consider X being non trivial anti-discrete strict (non empty TopSpace);
   take X;
   thus thesis;
  end;
end;



begin
:: 3. T_{0} Subsets.

definition let Y be TopStruct;
  let IT be Subset of Y;
 attr IT is T_0 means
:: TSP_1:def 8
 for x, y being Point of Y st x in IT & y in IT & x <> y holds
   (ex V being Subset of Y st V is open & x in V & not y in V) or
       (ex W being Subset of Y st W is open & not x in W & y in W);
end;


definition let Y be non empty TopStruct;
  let A be Subset of Y;
 redefine attr A is T_0 means
:: TSP_1:def 9
 for x, y being Point of Y st x in A & y in A & x <> y holds
   (ex E being Subset of Y st E is closed & x in E & not y in E)
   or
   (ex F being Subset of Y st F is closed & not x in F & y in F);
end;


theorem :: TSP_1:5
   for Y0, Y1 being TopStruct, D0 being Subset of Y0,
     D1 being Subset of Y1 st
    the TopStruct of Y0 = the TopStruct of Y1 & D0 = D1 holds
   D0 is T_0 implies D1 is T_0;

theorem :: TSP_1:6
 for Y being non empty TopStruct, A being Subset of Y
   st A = the carrier of Y
  holds A is T_0 iff Y is T_0;

reserve Y for non empty TopStruct;

theorem :: TSP_1:7
 for A, B being Subset of Y st B c= A
   holds A is T_0 implies B is T_0;

theorem :: TSP_1:8
 for A, B being Subset of Y
    holds A is T_0 or B is T_0 implies A /\ B is T_0;

theorem :: TSP_1:9
 for A, B being Subset of Y st A is open or B is open holds
                     A is T_0 & B is T_0 implies A \/ B is T_0;

theorem :: TSP_1:10
 for A, B being Subset of Y st A is closed or B is closed holds
                         A is T_0 & B is T_0 implies A \/ B is T_0;

theorem :: TSP_1:11
 for A being Subset of Y holds A is discrete implies A is T_0;

theorem :: TSP_1:12
   for A being non empty Subset of Y holds
  A is anti-discrete & A is not trivial implies A is not T_0;

definition let X be non empty TopSpace;
  let A be Subset of X;
 redefine attr A is T_0 means
:: TSP_1:def 10
 for x, y being Point of X st x in A & y in A & x <> y holds
                                            Cl {x} <> Cl {y};
end;


definition let X be non empty TopSpace;
  let A be Subset of X;
 redefine attr A is T_0 means
:: TSP_1:def 11
 for x, y being Point of X st x in A & y in A & x <> y holds
                                  not x in Cl {y} or not y in Cl {x};
end;


definition let X be non empty TopSpace;
  let A be Subset of X;
 redefine attr A is T_0 means
:: TSP_1:def 12
   for x, y being Point of X st x in A & y in A & x <> y holds
                      x in Cl {y} implies not Cl {y} c= Cl {x};
end;


reserve X for non empty TopSpace;

theorem :: TSP_1:13
 for A being empty Subset of X holds A is T_0;

theorem :: TSP_1:14
   for x being Point of X holds {x} is T_0;


begin
:: 4. Kolmogorov Subspaces.

registration let Y be non empty TopStruct;
 cluster strict T_0 non empty SubSpace of Y;
 existence
  proof
    consider X0 being trivial strict (non empty SubSpace of Y);
   take X0;
   thus thesis;
  end;
end;

definition let Y be TopStruct;
  let Y0 be SubSpace of Y;
 redefine attr Y0 is T_0 means
:: TSP_1:def 13
   Y0 is empty or
 for x, y being Point of Y st
      x is Point of Y0 & y is Point of Y0 & x <> y holds
   (ex V being Subset of Y st V is open & x in V & not y in V) or
       (ex W being Subset of Y
          st W is open & not x in W & y in W);
end;


definition let Y be TopStruct;
  let Y0 be SubSpace of Y;
 redefine attr Y0 is T_0 means
:: TSP_1:def 14
 Y0 is empty or
   for x, y being Point of Y st
      x is Point of Y0 & y is Point of Y0 & x <> y holds
   (ex E being Subset of Y
      st E is closed & x in E & not y in E) or
       (ex F being Subset of Y
       st F is closed & not x in F & y in F);
end;


reserve Y for non empty TopStruct;

theorem :: TSP_1:15
 for Y0 being non empty SubSpace of Y, A being Subset of Y st
                          A = the carrier of Y0 holds A is T_0 iff Y0 is T_0;

theorem :: TSP_1:16
   for Y0 being non empty SubSpace of Y,
     Y1 being T_0 non empty SubSpace of Y st
                       Y0 is SubSpace of Y1 holds Y0 is T_0;

reserve X for non empty TopSpace;

theorem :: TSP_1:17
   for X1 being T_0 non empty SubSpace of X, X2 being non empty SubSpace of X
 holds X1 meets X2 implies X1 meet X2 is T_0;

theorem :: TSP_1:18
   for X1, X2 being T_0 non empty SubSpace of X holds
  X1 is open or X2 is open implies X1 union X2 is T_0;

theorem :: TSP_1:19
   for X1, X2 being T_0 non empty SubSpace of X holds
   X1 is closed or X2 is closed implies X1 union X2 is T_0;

definition let X be non empty TopSpace;
 mode Kolmogorov_subspace of X is T_0 non empty SubSpace of X;
end;


theorem :: TSP_1:20
   for X being non empty TopSpace,
    A0 being non empty Subset of X st A0 is T_0
  ex X0 being strict Kolmogorov_subspace of X st A0 = the carrier of X0;

registration let X be non trivial (non empty TopSpace);
 cluster proper strict Kolmogorov_subspace of X;
 existence
  proof
    consider X0 being trivial strict (non empty SubSpace of X);
   take X0;
   thus thesis;
  end;
end;


registration let X be Kolmogorov_space;
 cluster -> T_0 (non empty SubSpace of X);
 coherence
  proof let X0 be non empty SubSpace of X;
     the carrier of X0 is non empty Subset of X by TSEP_1:1;
  then reconsider A0 = the carrier of X0 as non empty Subset of X;
       X is SubSpace of X by TSEP_1:2;
   then the carrier of X is non empty Subset of X by TSEP_1:1;
   then reconsider A = the carrier of X as non empty Subset of X;
       A is T_0 & A0 c= A by Th6;
    then A0 is T_0 by Th7;
    hence thesis by Th15;
  end;
end;


registration let X be non-Kolmogorov_space;
 cluster non proper -> non T_0 (non empty SubSpace of X);
 coherence
  proof let X0 be non empty SubSpace of X;
      the carrier of X0 is non empty Subset of X by TSEP_1:1;
    then reconsider A0 = the carrier of X0 as non empty Subset of X
   ;
        X is SubSpace of X by TSEP_1:2;
    then reconsider A = the carrier of X
       as non empty Subset of X by TSEP_1:1;
   assume X0 is non proper;
     then A0 is not proper by TEX_2:13;
     then A0 = A by TEX_2:5;
     then A0 is not T_0 by Th6;
    hence thesis by Th15;
  end;
 cluster T_0 -> proper (non empty SubSpace of X);
 coherence
  proof let X0 be non empty SubSpace of X;
      the carrier of X0 is non empty Subset of X by TSEP_1:1;
    then reconsider A0 = the carrier of X0 as non empty Subset of X
     ;
        X is SubSpace of X by TSEP_1:2;
    then reconsider A = the carrier of X
       as non empty Subset of X by TSEP_1:1;
   assume A1: X0 is T_0;
   assume X0 is non proper;
     then A0 is not proper by TEX_2:13;
     then A0 = A by TEX_2:5;
     then A0 is not T_0 by Th6;
    hence contradiction by A1,Th15;
  end;
end;


registration let X be non-Kolmogorov_space;
 cluster strict non T_0 SubSpace of X;
 existence
  proof
    consider X0 being non proper strict (non empty SubSpace of X);
   take X0;
   thus thesis;
  end;
end;


definition let X be non-Kolmogorov_space;
 mode non-Kolmogorov_subspace of X is non T_0 SubSpace of X;
end;


theorem :: TSP_1:21
   for X being non empty non-Kolmogorov_space,
    A0 being Subset of X st A0 is not T_0
  ex X0 being strict non-Kolmogorov_subspace of X st A0 = the carrier of X0;
