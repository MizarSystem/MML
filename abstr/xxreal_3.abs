:: Basic Operations on Extended Real Numbers
::  by Andrzej Trybulec
::
:: Received September 23, 2008
:: Copyright (c) 2008 Association of Mizar Users

environ

 vocabularies SUPINF_1, ARYTM, ARYTM_1, RELAT_1, ASYMPT_0, ZF_LANG, ARYTM_3,
      XCMPLX_0;
 notations ORDINAL1, XXREAL_0, XREAL_0, XCMPLX_0, NUMBERS;
 constructors XBOOLE_0, ORDINAL1, XXREAL_0, XCMPLX_0, XREAL_0, NUMBERS;
 registrations XXREAL_0, XREAL_0, ORDINAL1, XBOOLE_0;
 requirements BOOLE, SUBSET, NUMERALS, ARITHM;


begin

definition let x,y be ext-real number;
  func x + y -> ext-real number means
:: XXREAL_3:def 1
    ex a,b being complex number st x = a & y = b & it = a + b
      if x is real & y is real,
    it = +infty if x = +infty & y <> -infty or y = +infty & x <> -infty,
    it = -infty if x = -infty & y <> +infty or y = -infty & x <> +infty
    otherwise it = 0;
 commutativity;
  func x * y -> ext-real number means
:: XXREAL_3:def 2
    ex a,b being complex number st x = a & y = b & it = a * b
      if x is real & y is real,
    it = +infty if (x is not real or y is not real) &
     (x is positive & y is positive or x is negative & y is negative),
    it = -infty if (x is not real or y is not real) &
     (x is positive & y is negative or x is negative & y is positive)
    otherwise it = 0;
 commutativity;
end;

registration let x,y be real number, a,b be complex number;
 identify x+y with a+b when x = a, y = b;
 identify x*y with a*b when x = a, y = b;
end;

definition let x be ext-real number;
  func -x -> ext-real number means
:: XXREAL_3:def 3
    ex a being complex number st x = a & it = -a if x is real,
    it = -infty if x = +infty
    otherwise it = +infty;
 involutiveness;
end;

definition let x be ext-real number;
  func x" -> ext-real number means
:: XXREAL_3:def 4
    ex a being complex number st x = a & it = a" if x is real
     otherwise it = 0;
end;

registration let x be real number, a be complex number;
 identify -x with -a when x = a;
 identify x" with a" when x = a;
end;

definition let x,y be ext-real number;
 func x - y -> ext-real number equals
:: XXREAL_3:def 5
x + -y;
 func x / y -> ext-real number equals
:: XXREAL_3:def 6
x * y";
end;

registration let x,y be real number, a,b be complex number;
 identify x-y with a-b when x = a, y = b;
 identify x/y with a/b when x = a, y = b;
end;

reserve x for ext-real number;

theorem :: XXREAL_3:1
 x * 0 = 0;
