:: Basic Operations on Extended Real Numbers
::  by Andrzej Trybulec
::
:: Received September 23, 2008
:: Copyright (c) 2008 Association of Mizar Users

environ

 vocabularies SUPINF_1, ARYTM, ARYTM_1, RELAT_1, ASYMPT_0, ZF_LANG, ARYTM_3,
      XCMPLX_0, XREAL_0, SQUARE_1;
 notations ORDINAL1, SUBSET_1, XCMPLX_0, XXREAL_0, XREAL_0, NUMBERS;
 constructors XBOOLE_0, ORDINAL1, XXREAL_0, XCMPLX_0, XREAL_0, NUMBERS,
      XXREAL_2;
 registrations XXREAL_0, XREAL_0, ORDINAL1, XBOOLE_0, XCMPLX_0, REAL_1;
 requirements BOOLE, SUBSET, NUMERALS, ARITHM, REAL;


begin

reserve x,y,z,w for ext-real number,
  r,s for real number;

definition
  let x,y;
  redefine pred x <= y means
:: XXREAL_3:def 1

  ex p,q being Element of REAL st p = x & q
  = y & p <= q if x in REAL & y in REAL otherwise x = -infty or y = +infty;
end;

registration
  cluster non real positive (ext-real number);
  cluster non real negative (ext-real number);
end;

theorem :: XXREAL_3:1
  for x being non real positive (ext-real number) holds x = +infty;

theorem :: XXREAL_3:2
  for x being non real negative (ext-real number) holds x = -infty;

registration
  cluster non real non negative -> positive (ext-real number);
  cluster non real non positive -> negative (ext-real number);
end;

theorem :: XXREAL_3:3
  0 < x implies ex y being real number st 0 < y & y < x;

begin :: addition

definition
  let x,y;
  func x + y -> ext-real number means
:: XXREAL_3:def 2

  ex a,b being complex number st x
= a & y = b & it = a + b if x is real & y is real, it = +infty if x = +infty &
  y <> -infty or y = +infty & x <> -infty, it = -infty if x = -infty & y <>
  +infty or y = -infty & x <> +infty otherwise it = 0;
  commutativity;
end;

definition
  let x;
  func -x -> ext-real number means
:: XXREAL_3:def 3

  ex a being complex number st x = a &
  it = -a if x is real, it = -infty if x = +infty otherwise it = +infty;
  involutiveness;
end;

definition
  let x,y;
  func x - y -> ext-real number equals
:: XXREAL_3:def 4
  x + -y;
end;

registration
  let x,y be real number, a,b be complex number;
  identify x+y with a+b when x = a, y = b;
end;

registration
  let x be real number, a be complex number;
  identify -x with -a when x = a;
end;

:: for ext-real numbers

registration
  let r be real number;
  cluster -r -> real;
end;

:: for ext-real numbers

registration
  let r,s be real number;
  cluster r+s -> real;
  cluster r-s -> real;
end;

registration
  let x be real number, y be non real ext-real number;
  cluster x+y -> non real number;
end;

registration
  let x,y be non real positive (ext-real number);
  cluster x+y -> non real;
end;

registration
  let x,y be non real negative (ext-real number);
  cluster x+y -> non real;
end;

registration
  let x be non real negative (ext-real number), y be non real positive (
  ext-real number);
  cluster x+y -> zero;
end;

registration
  let x,y be real number, a,b be complex number;
  identify x-y with a-b when x = a, y = b;
end;

theorem :: XXREAL_3:4
  x + 0 = x;

theorem :: XXREAL_3:5
  --infty = +infty;

theorem :: XXREAL_3:6
  -+infty = -infty;

theorem :: XXREAL_3:7
  x + -x = 0;

theorem :: XXREAL_3:8
  x+y = 0 implies x = -y;

theorem :: XXREAL_3:9
  -(x+y) = -x + -y;

reserve f,g for ext-real number;

theorem :: XXREAL_3:10  :: MEMBER_1:3
  -f = -g implies f = g;

theorem :: XXREAL_3:11  :: MEMBER_1:4
  r+f = r+g implies f = g;

theorem :: XXREAL_3:12 :: MEMBER_1:5
  r-f = r-g implies f = g;

theorem :: XXREAL_3:13
  x <> +infty implies +infty - x = +infty & x - +infty = -infty;

theorem :: XXREAL_3:14
  x <> -infty implies -infty - x = -infty & x - -infty = +infty;

theorem :: XXREAL_3:15
  x - 0 = x;

theorem :: XXREAL_3:16
  x + y = +infty implies x = +infty or y = +infty;

theorem :: XXREAL_3:17
  x + y = -infty implies (x = -infty or y = -infty);

theorem :: XXREAL_3:18
  x - y = +infty implies (x = +infty or y = -infty);

theorem :: XXREAL_3:19
  x - y = -infty implies (x = -infty or y = +infty);

theorem :: XXREAL_3:20
  not ( x = +infty & y = -infty or x = -infty & y = +infty ) & x +
  y in REAL implies x in REAL & y in REAL;

theorem :: XXREAL_3:21
  not ( x = +infty & y = +infty or x = -infty & y = -infty ) & x -
  y in REAL implies x in REAL & y in REAL;

theorem :: XXREAL_3:22
  x is real implies y - x + x = y & y + x - x = y;

theorem :: XXREAL_3:23
  (x = +infty iff -x = -infty) & (x = -infty iff -x = +infty);

theorem :: XXREAL_3:24
  z is real implies x = x + z - z;

canceled;

theorem :: XXREAL_3:26
  -(x + y) = -y - x;

theorem :: XXREAL_3:27
  -(x - y) = -x + y & -(x - y) = y - x;

theorem :: XXREAL_3:28
  -(-x + y) = x - y & -(-x + y) = x + -y;

theorem :: XXREAL_3:29
  0<= y & y < +infty implies z = z + y - y;

theorem :: XXREAL_3:30
  not (x = +infty & y = -infty) & not (x = -infty & y = +infty) &
not ( y = +infty & z = -infty or y = -infty & z = +infty ) & not ( x = +infty &
  z = -infty or x = -infty & z = +infty ) implies x + y + z = x + (y + z);

theorem :: XXREAL_3:31
  not (x = +infty & y = -infty) & not (x = -infty & y = +infty) &
not (y = +infty & z = +infty) & not (y = -infty & z = -infty) & not (x = +infty
  & z = +infty) & not (x = -infty & z = -infty) implies x + y - z = x + (y - z)
;

theorem :: XXREAL_3:32 :: extension of REAL_1:27
  not (x = +infty & y = +infty) & not (x = -infty & y = -infty) & not (y
= +infty & z = -infty) & not (y = -infty & z = +infty) & not (x = +infty & z =
  +infty) & not (x = -infty & z = -infty) implies x - y - z = x - (y + z);

theorem :: XXREAL_3:33
  not (x = +infty & y = +infty) & not (x = -infty & y = -infty) &
not (y = +infty & z = +infty) & not (y = -infty & z = -infty) & not (x = +infty
  & z = -infty) & not (x = -infty & z = +infty) implies x - y + z = x - (y - z)
;

theorem :: XXREAL_3:34
  z is real implies (z + x) - (z + y) = x - y;

theorem :: XXREAL_3:35
  y is real implies (z - y) + (y - x) = z - x;

begin

registration
  let x,y be non negative (ext-real number);
  cluster x + y -> non negative;
end;

registration
  let x,y be non positive (ext-real number);
  cluster x + y -> non positive;
end;

registration
  let x be positive (ext-real number);
  let y be non negative (ext-real number);
  cluster x + y -> positive;
  cluster y + x -> positive;
end;

registration
  let x be negative (ext-real number);
  let y be non positive (ext-real number);
  cluster x + y -> negative;
  cluster y + x -> negative;
end;

registration
  let x be non positive (ext-real number);
  cluster -x -> non negative;
end;

registration
  let x be non negative (ext-real number);
  cluster -x -> non positive;
end;

registration
  let x be positive (ext-real number);
  cluster -x -> negative;
end;

registration
  let x be negative (ext-real number);
  cluster -x -> positive;
end;

registration
  let x be non negative (ext-real number), y be non positive (ext-real number);
  cluster x - y -> non negative;
  cluster y - x -> non positive;
end;

registration
  let x be positive (ext-real number);
  let y be non positive (ext-real number);
  cluster x - y -> positive;
  cluster y - x -> negative;
end;

registration
  let x be negative (ext-real number);
  let y be non negative (ext-real number);
  cluster x - y -> negative;
  cluster y - x -> positive;
end;

theorem :: XXREAL_3:36
  x <= y implies x + z <= y + z;

canceled;

theorem :: XXREAL_3:38
  x <= y & z <= w implies x + z <= y + w;

theorem :: XXREAL_3:39
  x <= y & z <= w implies x - w <= y - z;

theorem :: XXREAL_3:40
  x <= y iff - y <= - x;

canceled;

theorem :: XXREAL_3:42
  0 <= z implies x <= x + z;

theorem :: XXREAL_3:43
  x <= y implies y-x >= 0;

theorem :: XXREAL_3:44
  (z = -infty & y = +infty implies x <= 0) & (z = +infty & y = -infty
  implies x <= 0) implies (x - y <= z implies x <= z + y);

theorem :: XXREAL_3:45
  (x = +infty & y = +infty implies 0 <= z) & (x = -infty & y = -infty
  implies 0 <= z) implies (x <= z + y implies x - y <= z);

canceled;

theorem :: XXREAL_3:47
  z in REAL & x < y implies x + z < y + z & x - z < y - z;

theorem :: XXREAL_3:48
  0<= x & 0<= y & 0<= z implies (x + y) + z = x + (y + z);

theorem :: XXREAL_3:49
  x is real implies (y + x <= z iff y <= z - x);

theorem :: XXREAL_3:50
  0 < x & x < y implies 0 < y - x;

theorem :: XXREAL_3:51
  0 <= x & 0 <= z & z + x < y implies z < y - x;

canceled;

theorem :: XXREAL_3:53
  0 <= x & 0 <= z & z + x < y implies z <= y;

canceled;

theorem :: XXREAL_3:55
  0 <= x & x < z implies ex y being real number st 0 < y & x + y <
  z;

theorem :: XXREAL_3:56
  0 < x implies ex y being real number st 0 < y & y + y < x;

theorem :: XXREAL_3:57
  x < y & x < +infty & -infty < y implies 0 < y- x;

canceled 5;

theorem :: XXREAL_3:63
  not ( x = +infty & y = -infty or x = -infty & y = +infty ) & x + y < z
  implies x <> +infty & y <> +infty & z <> -infty & x < z - y;

theorem :: XXREAL_3:64
  not ( z = +infty & y = +infty or z = -infty & y = -infty ) & x < z - y
  implies x <> +infty & y <> +infty & z <> -infty & x + y < z;

theorem :: XXREAL_3:65
  not ( x = +infty & y = +infty or x = -infty & y = -infty ) & x - y < z
  implies x <> +infty & y <> -infty & z <> -infty & x < z + y;

theorem :: XXREAL_3:66
  not ( z = +infty & y = -infty or z = -infty & y = +infty ) & x < z + y
  implies x <> +infty & y <> -infty & z <> -infty & x - y < z;

theorem :: XXREAL_3:67
  not ( x = +infty & y = -infty or x = -infty & y = +infty or y = +infty
& z = +infty or y = -infty & z = -infty ) & x + y <= z implies y <> +infty & x
  <= z - y;

theorem :: XXREAL_3:68
  not (x = +infty & y = -infty) & not (x = -infty & y = +infty) & not (y
  = +infty & z = +infty) & x <= z - y implies y <> +infty & x + y <= z;

theorem :: XXREAL_3:69
  not ( x = +infty & y = +infty or x = -infty & y = -infty or y = +infty
  & z = -infty or y = -infty & z = +infty ) & x - y <= z implies y <> -infty;

theorem :: XXREAL_3:70
  not (x = -infty & y = -infty) & not (y = -infty & z = +infty) & x <= z
  + y implies y <> -infty;

theorem :: XXREAL_3:71
  (x <= -y implies y <= -x) & (-x <= y implies -y <= x);

theorem :: XXREAL_3:72
  (for e be real number st 0 < e holds x < y + e) implies x <= y;

reserve t for ext-real number;

theorem :: XXREAL_3:73
  t <> -infty & t <> +infty & x < y implies x + t < y + t;

theorem :: XXREAL_3:74
  t <> -infty & t <> +infty & x < y implies x - t < y - t;

theorem :: XXREAL_3:75
  -infty < w & x < y & w < z implies x + w < y + z;

theorem :: XXREAL_3:76
  0 <= x & z + x <= y implies z <= y;

begin :: multiplication

definition
  let x,y be ext-real number;
  func x * y -> ext-real number means
:: XXREAL_3:def 5

  ex a,b being complex number st x
  = a & y = b & it = a * b if x is real & y is real, it = +infty if (x is not
real or y is not real) & (x is positive & y is positive or x is negative & y is
negative), it = -infty if (x is not real or y is not real) & (x is positive & y
  is negative or x is negative & y is positive) otherwise it = 0;
  commutativity;
end;

registration
  let x,y be real number, a,b be complex number;
  identify x*y with a*b when x = a, y = b;
end;

definition
  let x be ext-real number;
  func x" -> ext-real number means
:: XXREAL_3:def 6

  ex a being complex number st x = a &
  it = a" if x is real otherwise it = 0;
end;

registration
  let x be real number, a be complex number;
  identify x" with a" when x = a;
end;

definition
  let x,y be ext-real number;
  func x / y -> ext-real number equals
:: XXREAL_3:def 7
  x * y";
end;

registration
  let x,y be real number, a,b be complex number;
  identify x/y with a/b when x = a, y = b;
end;

registration
  let x be positive (ext-real number), y be negative (ext-real number);
  cluster x*y -> negative;
end;

registration
  let x,y be negative (ext-real number);
  cluster x*y -> positive;
end;

registration
  let x,y be positive (ext-real number);
  cluster x*y -> positive;
end;

registration
  let x be non positive (ext-real number), y be non negative (ext-real number);
  cluster x*y -> non positive;
end;

registration
  let x,y be non positive (ext-real number);
  cluster x*y -> non negative;
end;

registration
  let x,y be non negative (ext-real number);
  cluster x*y -> non negative;
end;

registration
  let x be non positive (ext-real number);
  cluster x" -> non positive;
end;

registration
  let x be non negative (ext-real number);
  cluster x" -> non negative;
end;

registration
  let x be non negative (ext-real number), y be non positive (ext-real number);
  cluster x/y -> non positive;
  cluster y/x -> non positive;
end;

registration
  let x,y be non negative (ext-real number);
  cluster x/y -> non negative;
end;

registration
  let x,y be non positive (ext-real number);
  cluster x/y -> non negative;
end;

registration
  let x,y be non zero (ext-real number);
  cluster x*y -> non zero;
end;

registration
  let x be zero (ext-real number), y be ext-real number;
  cluster x*y -> zero (ext-real number);
end;

theorem :: XXREAL_3:77
  x*(y*z) = (x*y)*z;
:: for ext-real numbers

registration
  let r be real number;
  cluster r" -> real;
end;
:: for ext-real numbers

registration
  let r,s be real number;
  cluster r*s -> real;
  cluster r/s -> real;
end;

registration
  cluster -infty" -> zero;
  cluster +infty" -> zero;
end;

theorem :: XXREAL_3:78 :: MEMBER_1:2
  (f*g)" = f"*g";

theorem :: XXREAL_3:79 :: MEMBER_1:6
  r <> 0 & r*f = r*g implies f = g;

theorem :: XXREAL_3:80
  x <> +infty & x <> -infty & x * y = +infty implies y = +infty or
  y = -infty;

theorem :: XXREAL_3:81
  x <> +infty & x <> -infty & x * y = -infty implies y = +infty or
  y = -infty;

theorem :: XXREAL_3:82
  x <= y & 0 <= z implies x*z <= y*z;

theorem :: XXREAL_3:83
  x < y & 0 < z & z <> +infty implies x*z < y*z;

theorem :: XXREAL_3:84
  x*y in REAL implies x in REAL & y in REAL or x*y = 0;

theorem :: XXREAL_3:85
  +infty" = 0;

theorem :: XXREAL_3:86
  -infty" = 0;

theorem :: XXREAL_3:87
  x/+infty = 0;

theorem :: XXREAL_3:88
  x/-infty = 0;

theorem :: XXREAL_3:89
  x <> -infty & x <> +infty & x <> 0 implies x / x = 1;

theorem :: XXREAL_3:90
  x <= y & 0 < z implies x/z <= y/z;

theorem :: XXREAL_3:91
  x < y & 0 < z & z <> +infty implies x/z < y/z;

theorem :: XXREAL_3:92
  1*x = x;

theorem :: XXREAL_3:93
  y" = 0 implies y = +infty or y = -infty or y = 0;

theorem :: XXREAL_3:94
  0 < y & y <> +infty implies +infty / y = +infty;

theorem :: XXREAL_3:95
  y < 0 & -infty <> y implies -infty / y = +infty;

theorem :: XXREAL_3:96
  y < 0 & -infty <> y implies +infty / y = -infty;

theorem :: XXREAL_3:97
  0 < y & y <> +infty implies -infty / y = -infty;

theorem :: XXREAL_3:98 :: extension of REAL_1:34
  x <> +infty & x <> -infty & x <> 0 implies x*(1/x) = 1 & (1/x)*x = 1;

theorem :: XXREAL_3:99
  -infty <> y & y <> +infty & y <> 0 implies x * y / y = x & x * (y / y) =
  x;

theorem :: XXREAL_3:100
  +infty * y <> 1 & -infty * y <> 1;

theorem :: XXREAL_3:101
  x * y <> +infty & x * y <> -infty implies x in REAL or y in REAL;

begin :: distributivity

theorem :: XXREAL_3:102
  (-1)*x = -x;

theorem :: XXREAL_3:103
  - x*y = (-x)*y;

theorem :: XXREAL_3:104
  y = -z implies x*(y+z) = x*y +x*z;

theorem :: XXREAL_3:105
  2*x = x+x;

theorem :: XXREAL_3:106
  x is real implies x*(y+z) = x*y +x*z;

theorem :: XXREAL_3:107
  y>=0 & z>=0 implies x*(y+z) = x*y +x*z;

canceled;

theorem :: XXREAL_3:109
  y<=0 & z<=0 implies x*(y+z) = x*y +x*z;

theorem :: XXREAL_3:110
  x*(0+z) = x*0 +x*z;

theorem :: XXREAL_3:111 :: MEMBER_1:1
  (-f)" = -f";

theorem :: XXREAL_3:112
  x is real implies x * (y - z) = x * y - x * z;

theorem :: XXREAL_3:113
  x <= y & z <= 0 implies y*z <= x*z;

theorem :: XXREAL_3:114
  x < y & z < 0 & z <> -infty implies y*z < x*z;

theorem :: XXREAL_3:115
  x <= y & z < 0 implies y/z <= x/z;

theorem :: XXREAL_3:116
  x < y & z < 0 & z <> -infty implies y/z < x/z;

theorem :: XXREAL_3:117
  x/2 + x/2 = x;

