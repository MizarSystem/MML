:: Pythagorean triples
::  by Freek Wiedijk
::
:: Received August 26, 2001
:: Copyright (c) 2001 Association of Mizar Users

environ

 vocabulary INT_1, ARYTM_3, FILTER_0, ARYTM, SQUARE_1, ORDINAL2, MATRIX_2,
      NAT_1, ARYTM_1, FINSET_1, FUNCT_1, RELAT_1, ORDINAL1, BOOLE, ABSVALUE,
      INT_2, TARSKI, PYTHTRIP;
 notation TARSKI, XBOOLE_0, SUBSET_1, FINSET_1, ORDINAL2, NUMBERS, XCMPLX_0,
      XREAL_0, REAL_1, INT_1, INT_2, NAT_1, SQUARE_1, ABIAN, PEPIN, DOMAIN_1,
      GROUP_1, RELAT_1, FUNCT_1, LIMFUNC1, ORDINAL1;
 constructors REAL_1, INT_2, NAT_1, ABIAN, PEPIN, DOMAIN_1, GROUP_1, LIMFUNC1,
      MEMBERED;
 clusters FINSET_1, SUBSET_1, NAT_LAT, ABIAN, INT_1, NAT_1, XREAL_0, MEMBERED,
      NUMBERS, ORDINAL2;
 requirements SUBSET, BOOLE, NUMERALS, REAL, ARITHM;


begin :: relative primeness

reserve a,a',b,b',c,c',k,k',m,m',m'',m''',n,n',n'',n''',mn,mn',mn'',p,p'
 for Nat;
reserve i,i' for Integer;


definition
 let m,n;
 redefine pred m,n are_relative_prime means
:: PYTHTRIP:def 1
  for k st k divides m & k divides n holds k = 1;
end;


definition
 let m,n;
 redefine pred m,n are_relative_prime means
:: PYTHTRIP:def 2
  for p being prime Nat holds not (p divides m & p divides n);
end;


begin :: squares

definition
 let n be number;
 attr n is square means
:: PYTHTRIP:def 3
  ex m st n = m^2;
end;


registration
 cluster square -> natural number;
 coherence
 proof
  let n be number;
  assume n is square;
  then ex m st n = m^2 by Def3;
  hence n is natural;
 end;
end;


registration
 let n be Nat;
 cluster n^2 -> square;
 coherence by Def3;
end;


registration
 cluster even square Nat;
 existence
 proof
  take 0;
    0 = 2*0;
  hence thesis by SQUARE_1:60;
 end;
end;


registration
 cluster odd square Nat;
 existence
 proof
  take 1;
    1 = 2*0 + 1;
  hence thesis by SQUARE_1:59;
 end;
end;


registration
 cluster even square number;
 existence
 proof
  consider n being even square Nat;
  take n;
  thus thesis;
 end;
end;


registration
 cluster odd square number;
 existence
 proof
  consider n being odd square Nat;
  take n;
  thus thesis;
 end;
end;


registration
 let m,n be square number;
 cluster m*n -> square;
 coherence
 proof
  consider m' such that
A1: m = m'^2 by Def3;
  consider n' such that
A2: n = n'^2 by Def3;
    m*n = (m'*n')^2 by A1,A2,SQUARE_1:68;
  hence thesis;
 end;
end;


theorem :: PYTHTRIP:1
 m*n is square & m,n are_relative_prime implies m is square & n is square;

registration
 let i be even Integer;
 cluster i^2 -> even;
 coherence
 proof
    i^2 = i*i by SQUARE_1:def 3;
  hence thesis;
 end;
end;


registration
 let i be odd Integer;
 cluster i^2 -> odd;
 coherence
 proof
    i^2 = i*i by SQUARE_1:def 3;
  hence thesis;
 end;
end;


theorem :: PYTHTRIP:2
 i is even iff i^2 is even;

theorem :: PYTHTRIP:3
 i is even implies i^2 mod 4 = 0;

theorem :: PYTHTRIP:4
 i is odd implies i^2 mod 4 = 1;

registration
 let m,n be odd square number;
 cluster m + n -> non square;
 coherence
 proof
  consider m' such that
A1: m = m'^2 by Def3;
A2: m' is odd by A1,Th2;
  consider n' such that
A3: n = n'^2 by Def3;
A4: n' is odd by A3,Th2;
  reconsider m'' = m as Nat by ORDINAL2:def 21;
  reconsider n'' = n as Nat by ORDINAL2:def 21;
A5: (m'' + n'') mod 4 = ((m'' mod 4) + (n'' mod 4)) mod 4 by EULER_2:8
   .= (1 + (n'' mod 4)) mod 4 by A1,A2,Th4
   .= (1 + 1) mod 4 by A3,A4,Th4
   .= 2 by GR_CY_1:4;
  hereby
   assume m + n is square;
   then consider mn' such that
A6: m + n = mn'^2 by Def3;
     mn' is even by A6,Th2;
   hence contradiction by A5,A6,Th3;
  end;
 end;
end;


theorem :: PYTHTRIP:5
 m^2 = n^2 implies m = n;

theorem :: PYTHTRIP:6
 m divides n iff m^2 divides n^2;

begin :: distributive law for hcf

theorem :: PYTHTRIP:7
 m divides n or k = 0 iff k*m divides k*n;

theorem :: PYTHTRIP:8
 (k*m) hcf (k*n) = k*(m hcf n);

begin :: unbounded sets are infinite

theorem :: PYTHTRIP:9
 for X being set st for m ex n st n >= m & n in X holds X is infinite;

begin :: Pythagorean triples

theorem :: PYTHTRIP:10
 a,b are_relative_prime implies a is odd or b is odd;

theorem :: PYTHTRIP:11
 a^2 + b^2 = c^2 & a,b are_relative_prime & a is odd implies
  ex m,n st m <= n & a = n^2 - m^2 & b = 2*m*n & c = n^2 + m^2;

theorem :: PYTHTRIP:12
 a = n^2 - m^2 & b = 2*m*n & c = n^2 + m^2 implies a^2 + b^2 = c^2;

definition
 mode Pythagorean_triple -> Subset of NAT means
:: PYTHTRIP:def 4
  ex a,b,c st a^2 + b^2 = c^2 & it = { a,b,c };
end;


reserve X for Pythagorean_triple;

registration
 cluster -> finite Pythagorean_triple;
 coherence
 proof
  let X;
    ex a,b,c st a^2 + b^2 = c^2 & X = { a,b,c } by Def4;
  hence thesis;
 end;
end;


definition
 redefine mode Pythagorean_triple means
:: PYTHTRIP:def 5
  ex k,m,n st m <= n & it = { k*(n^2 - m^2), k*(2*m*n), k*(n^2 + m^2) };
end;


definition
 let X;
 attr X is degenerate means
:: PYTHTRIP:def 6
  0 in X;
end;


theorem :: PYTHTRIP:13
   n > 2 implies ex X st X is non degenerate & n in X;

definition
 let X;
 attr X is simplified means
:: PYTHTRIP:def 7
  for k st for n st n in X holds k divides n holds k = 1;
end;


definition
 let X;
 redefine attr X is simplified means
:: PYTHTRIP:def 8
  ex m,n st m in X & n in X & m,n are_relative_prime;
end;


theorem :: PYTHTRIP:14
 n > 0 implies ex X st X is non degenerate & X is simplified & 4*n in X;

registration
 cluster non degenerate simplified Pythagorean_triple;
 existence
 proof
  consider X such that
A1: X is non degenerate & X is simplified & 4*1 in X by Th14;
  take X;
  thus thesis by A1;
 end;
end;


theorem :: PYTHTRIP:15
   { 3,4,5 } is non degenerate simplified Pythagorean_triple;

theorem :: PYTHTRIP:16
   { X: X is non degenerate & X is simplified } is infinite;

