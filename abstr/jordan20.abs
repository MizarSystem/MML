:: Behaviour of an Arc Crossing a Line
::  by Yatsuka Nakamura
::
:: Received January 26, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabulary JORDAN20, ORDINAL2, ARYTM, PRE_TOPC, EUCLID, TOPREAL1, BOOLE,
      ARYTM_1, MCART_1, FUNCT_5, RELAT_1, JORDAN3, TOPS_2, RELAT_2, SUBSET_1,
      SEQ_2, FUNCT_1, JORDAN6, COMPTS_1, METRIC_1, PCOMPS_1, ABSVALUE,
      SQUARE_1, TOPMETR, BORSUK_1, RCOMP_1, ARYTM_3, GRAPH_1, TREAL_1, SEQ_1,
      FUNCT_4, TOPREAL4, FINSEQ_4, FINSEQ_1, TARSKI, SPPOL_2;
 notation TARSKI, XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, XREAL_0, REAL_1,
      SQUARE_1, ABSVALUE, SEQ_4, STRUCT_0, CONNSP_1, RCOMP_1, COMPTS_1,
      METRIC_1, PCOMPS_1, JORDAN5C, JORDAN6, EUCLID, TOPMETR, PARTFUN1,
      PRE_TOPC, TOPS_2, NUMBERS, ORDINAL1, PSCOMP_1, BORSUK_2, RCOMP_2,
      TREAL_1, FUNCT_4, TOPREAL4, FINSEQ_4, BINARITH, FINSEQ_1, TOPREAL1,
      SPPOL_2, JORDAN3, NAT_1, FUNCT_2;
 constructors REAL_1, TOPREAL1, TOPREAL2, JORDAN1A, ARYTM_1, ARYTM_0, NUMBERS,
      ORDINAL2, JORDAN5C, TOPS_1, TOPS_2, JORDAN2C, JORDAN6, CONNSP_1, JORDAN1,
      BORSUK_2, RCOMP_1, ABSVALUE, GOBRD14, SQUARE_1, XCMPLX_0, PARTFUN1,
      RELAT_1, MEMBERED, PSCOMP_1, RCOMP_2, TREAL_1, FUNCT_4, TOPREAL4,
      FINSEQ_4, BINARITH, FINSEQ_1, SPPOL_2, JORDAN3, NAT_1, COMPTS_1, BINOP_2;
 clusters ARYTM_3, ARYTM_2, XREAL_0, EUCLID, REAL_1, STRUCT_0, XBOOLE_0,
      JORDAN1A, SUBSET_1, RELSET_1, SPRECT_4, BORSUK_1, PRE_TOPC, METRIC_1,
      SQUARE_1, TOPMETR, MEMBERED, NUMBERS, BORSUK_2, TOPREAL1, FINSEQ_1,
      SPPOL_2, NAT_1;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;


begin

theorem :: JORDAN20:1
for P being Subset of TOP-REAL 2,
p1,p2,p being Point of TOP-REAL 2
st P is_an_arc_of p1,p2 & p in P holds Segment(P,p1,p2,p,p)={p};

theorem :: JORDAN20:2
for p1,p2,p being Point of TOP-REAL 2,a being Real
st p in LSeg(p1,p2) & p1`1<=a & p2`1<=a holds p`1<=a;

theorem :: JORDAN20:3
for p1,p2,p being Point of TOP-REAL 2,a being Real
st p in LSeg(p1,p2) & p1`1>=a & p2`1>=a holds p`1>=a;

theorem :: JORDAN20:4
for p1,p2,p being Point of TOP-REAL 2,a being Real
st p in LSeg(p1,p2) & p1`1<a & p2`1<a holds p`1<a;

theorem :: JORDAN20:5
for p1,p2,p being Point of TOP-REAL 2,a being Real
st p in LSeg(p1,p2) & p1`1>a & p2`1>a holds p`1>a;

reserve s,p,q for Point of TOP-REAL 2;
reserve r for Real;
reserve j for Nat;

theorem :: JORDAN20:6
for f being S-Sequence_in_R2, p,q being Point of TOP-REAL 2 st
   1 <=j & j < len f & p in LSeg(f,j) & q in LSeg(f,j) &
   (f/.j)`2=(f/.(j+1))`2 & (f/.j)`1>(f/.(j+1))`1 &
   LE p,q,L~f,f/.1,f/.(len f)
  holds p`1>=q`1;

theorem :: JORDAN20:7
for f being S-Sequence_in_R2, p,q being Point of TOP-REAL 2 st
   1 <=j & j < len f & p in LSeg(f,j) & q in LSeg(f,j) &
   (f/.j)`2=(f/.(j+1))`2 & (f/.j)`1<(f/.(j+1))`1 &
   LE p,q,L~f,f/.1,f/.(len f)
  holds p`1<=q`1;

definition
  let P be Subset of TOP-REAL 2, p1,p2,p be Point of TOP-REAL 2, e be Real;
  pred p is_Lin P,p1,p2,e means
:: JORDAN20:def 1
    P is_an_arc_of p1,p2 & p in P & p`1=e &
    (ex p4 being Point of TOP-REAL 2 st p4`1<e & LE p4,p,P,p1,p2 &
    (for p5 being Point of TOP-REAL 2 st
    LE p4,p5,P,p1,p2 & LE p5,p,P,p1,p2 holds p5`1<=e));
  pred p is_Rin P,p1,p2,e means
:: JORDAN20:def 2
    P is_an_arc_of p1,p2 & p in P & p`1=e &
    (ex p4 being Point of TOP-REAL 2 st p4`1>e & LE p4,p,P,p1,p2 &
    (for p5 being Point of TOP-REAL 2 st
    LE p4,p5,P,p1,p2 & LE p5,p,P,p1,p2 holds p5`1>=e));
  pred p is_Lout P,p1,p2,e means
:: JORDAN20:def 3
    P is_an_arc_of p1,p2 & p in P & p`1=e &
    (ex p4 being Point of TOP-REAL 2 st p4`1<e & LE p,p4,P,p1,p2 &
    (for p5 being Point of TOP-REAL 2 st
    LE p5,p4,P,p1,p2 & LE p,p5,P,p1,p2 holds p5`1<=e));
  pred p is_Rout P,p1,p2,e means
:: JORDAN20:def 4
    P is_an_arc_of p1,p2 & p in P & p`1=e &
    (ex p4 being Point of TOP-REAL 2 st p4`1>e & LE p,p4,P,p1,p2 &
    (for p5 being Point of TOP-REAL 2 st
    LE p5,p4,P,p1,p2 & LE p,p5,P,p1,p2 holds p5`1>=e));
  pred p is_OSin P,p1,p2,e means
:: JORDAN20:def 5  ::Oscilating In
    P is_an_arc_of p1,p2 & p in P & p`1=e &
    ex p7 being Point of TOP-REAL 2 st LE p7,p,P,p1,p2 &
    (for p8 being Point of TOP-REAL 2 st LE p7,p8,P,p1,p2 & LE p8,p,P,p1,p2
    holds p8`1=e) &
    (for p4 being Point of TOP-REAL 2 st LE p4,p7,P,p1,p2 & p4<>p7 holds
    (ex p5 being Point of TOP-REAL 2 st
    LE p4,p5,P,p1,p2 & LE p5,p7,P,p1,p2 & p5`1>e) &
    (ex p6 being Point of TOP-REAL 2 st
    LE p4,p6,P,p1,p2 & LE p6,p7,P,p1,p2 & p6`1<e));
  pred p is_OSout P,p1,p2,e means
:: JORDAN20:def 6  ::Oscilating Out
    P is_an_arc_of p1,p2 & p in P & p`1=e &
    ex p7 being Point of TOP-REAL 2 st LE p,p7,P,p1,p2 &
    (for p8 being Point of TOP-REAL 2 st LE p8,p7,P,p1,p2 & LE p,p8,P,p1,p2
    holds p8`1=e) &
    (for p4 being Point of TOP-REAL 2 st LE p7,p4,P,p1,p2 & p4<>p7 holds
    (ex p5 being Point of TOP-REAL 2 st
     LE p5,p4,P,p1,p2 & LE p7,p5,P,p1,p2 & p5`1>e)&
    (ex p6 being Point of TOP-REAL 2 st
     LE p6,p4,P,p1,p2 & LE p7,p6,P,p1,p2 & p6`1<e));
end;

theorem :: JORDAN20:8
for P being Subset of TOP-REAL 2,
  p1,p2,p being Point of TOP-REAL 2,e being Real st
  P is_an_arc_of p1,p2 & p1`1<=e & p2`1>=e
  holds ex p3 being Point of TOP-REAL 2 st p3 in P & p3`1=e;

reserve s,s1,r,t for Real;

theorem :: JORDAN20:9
for P being non empty Subset of TOP-REAL 2,
  p1,p2,p being Point of TOP-REAL 2,e being Real st
  P is_an_arc_of p1,p2 & p1`1<e & p2`1>e & p in P & p`1=e
  holds p is_Lin P,p1,p2,e or p is_Rin P,p1,p2,e
  or p is_OSin P,p1,p2,e;

theorem :: JORDAN20:10
for P being non empty Subset of TOP-REAL 2,
  p1,p2,p being Point of TOP-REAL 2,e being Real st
  P is_an_arc_of p1,p2 & p1`1<e & p2`1>e & p in P & p`1=e
  holds p is_Lout P,p1,p2,e or p is_Rout P,p1,p2,e
  or p is_OSout P,p1,p2,e;

reserve n for Nat;

theorem :: JORDAN20:11
for P being Subset of I[01],s being Real st
 P=[.0,s.[ holds P is open;

theorem :: JORDAN20:12
for P being Subset of I[01],s being Real st
 P=].s,1.] holds P is open;

theorem :: JORDAN20:13
for P being non empty Subset of TOP-REAL 2,
P1 being Subset of (TOP-REAL 2)|P ,Q being Subset of I[01],
 f being map of I[01],(TOP-REAL 2)|P,s being Real st s<=1 &
  P1= {q0 where q0 is Point of TOP-REAL 2:
   ex ss being Real st 0<=ss & ss<s & q0=f.ss} &
  Q=[.0,s.[ holds f.:Q=P1;

theorem :: JORDAN20:14
for P being non empty Subset of TOP-REAL 2,
P1 being Subset of (TOP-REAL 2)|P ,Q being Subset of I[01],
 f being map of I[01],(TOP-REAL 2)|P,s being Real st s>=0 &
  P1= {q0 where q0 is Point of TOP-REAL 2:
   ex ss being Real st s<ss & ss<=1 & q0=f.ss} &
  Q=].s,1.]  holds f.:Q=P1;

theorem :: JORDAN20:15
for P being non empty Subset of TOP-REAL 2,
P1 being Subset of (TOP-REAL 2)|P,
 f being map of I[01],(TOP-REAL 2)|P,s being Real st s<=1 &
 f is_homeomorphism &
 P1= {q0 where q0 is Point of TOP-REAL 2:
     ex ss being Real st 0<=ss & ss<s & q0=f.ss}
 holds P1 is open;

theorem :: JORDAN20:16
for P being non empty Subset of TOP-REAL 2,
P1 being Subset of (TOP-REAL 2)|P,
 f being map of I[01],(TOP-REAL 2)|P,s being Real st s>=0 &
 f is_homeomorphism &
 P1= {q0 where q0 is Point of TOP-REAL 2:
     ex ss being Real st s<ss & ss<=1 & q0=f.ss}
 holds P1 is open;

theorem :: JORDAN20:17
for T being non empty TopStruct, Q1,Q2 being Subset of T,
p1,p2 being Point of T st Q1 /\ Q2={} & Q1 \/ Q2=the carrier of T &
p1 in Q1 & p2 in Q2 & Q1 is open & Q2 is open holds
not ex P being map of I[01],T st P is continuous & P.0=p1 & P.1=p2;

theorem :: JORDAN20:18
for P being non empty Subset of TOP-REAL 2,
Q being Subset of (TOP-REAL 2)|P,
p1,p2,q being Point of TOP-REAL 2
st P is_an_arc_of p1,p2 & q in P & q<>p1 & q<>p2 & Q=P\{q}
holds not Q is connected &
not ex R being map of I[01],((TOP-REAL 2)|P)|Q
 st R is continuous & R.0=p1 & R.1=p2;

theorem :: JORDAN20:19
for P being non empty Subset of TOP-REAL 2,
  p1,p2,q1,q2 being Point of TOP-REAL 2 st
  P is_an_arc_of p1,p2 & q1 in P & q2 in P holds
  LE q1,q2,P,p1,p2 or LE q2,q1,P,p1,p2;

theorem :: JORDAN20:20
for P being non empty Subset of TOP-REAL 2,
  p1,p2,q1 being Point of TOP-REAL 2 st
  P is_an_arc_of p1,p2 & q1 in P & p1<>q1 holds
  Segment(P,p1,p2,p1,q1) is_an_arc_of p1,q1;

theorem :: JORDAN20:21     :: Another version of TOPMETR3:18
for n being Nat,p1,p2 being Point of TOP-REAL n,
P,P1 being non empty Subset of TOP-REAL n st
P is_an_arc_of p1,p2 & P1 is_an_arc_of p1,p2 & P1 c= P
holds P1=P;

theorem :: JORDAN20:22
for P being non empty Subset of TOP-REAL 2,
  p1,p2,q1 being Point of TOP-REAL 2 st
  P is_an_arc_of p1,p2 & q1 in P & p2<>q1 holds
  Segment(P,p1,p2,q1,p2) is_an_arc_of q1,p2;

theorem :: JORDAN20:23
for P being non empty Subset of TOP-REAL 2,
  p1,p2,q1,q2,q3 being Point of TOP-REAL 2 st
  P is_an_arc_of p1,p2 & LE q1,q2,P,p1,p2 & LE q2,q3,P,p1,p2 holds
  Segment(P,p1,p2,q1,q2) \/ Segment(P,p1,p2,q2,q3)
  =Segment(P,p1,p2,q1,q3);

theorem :: JORDAN20:24
for P being non empty Subset of TOP-REAL 2,
  p1,p2,q1,q2,q3 being Point of TOP-REAL 2 st
  P is_an_arc_of p1,p2 & LE q1,q2,P,p1,p2 & LE q2,q3,P,p1,p2 holds
  Segment(P,p1,p2,q1,q2) /\ Segment(P,p1,p2,q2,q3) = {q2};

theorem :: JORDAN20:25
for P being non empty Subset of TOP-REAL 2,
  p1,p2 being Point of TOP-REAL 2 st
  P is_an_arc_of p1,p2 holds Segment(P,p1,p2,p1,p2)=P;

theorem :: JORDAN20:26
for T being non empty TopSpace,w1,w2,w3 being Point of T,
 h1,h2 being map of I[01],T st
 h1 is continuous & w1=h1.0 & w2=h1.1 &
 h2 is continuous & w2=h2.0 & w3=h2.1
 holds ex h3 being map of I[01],T st
 h3 is continuous & w1=h3.0 & w3=h3.1;

theorem :: JORDAN20:27
for T being non empty TopSpace,a,b,c being Point of T,
G1 being Path of a,b, G2 being Path of b,c st
G1 is continuous & G2 is continuous & G1.0=a & G1.1=b & G2.0=b & G2.1=c
holds (G1+G2) is continuous & (G1+G2).0=a & (G1+G2).1=c;

theorem :: JORDAN20:28
for P,Q1 being non empty Subset of TOP-REAL 2,
  p1,p2,q1,q2 being Point of TOP-REAL 2 st
  P is_an_arc_of p1,p2 & Q1 is_an_arc_of q1,q2 &
  LE q1,q2,P,p1,p2 & Q1 c= P holds Q1= Segment(P,p1,p2,q1,q2);

theorem :: JORDAN20:29
for P being non empty Subset of TOP-REAL 2,
  p1,p2,q1,q2,p being Point of TOP-REAL 2,e being Real st
  p1`1<e & p2`1>e &
  q1 is_Lin P,p1,p2,e & q2`1=e & LSeg(q1,q2) c= P
  & p in LSeg(q1,q2) holds p is_Lin P,p1,p2,e;

theorem :: JORDAN20:30
for P being non empty Subset of TOP-REAL 2,
  p1,p2,q1,q2,p being Point of TOP-REAL 2,e being Real st
  p1`1<e & p2`1>e &
  q1 is_Rin P,p1,p2,e & q2`1=e & LSeg(q1,q2) c= P
  & p in LSeg(q1,q2) holds p is_Rin P,p1,p2,e;

theorem :: JORDAN20:31
for P being non empty Subset of TOP-REAL 2,
  p1,p2,q1,q2,p being Point of TOP-REAL 2,e being Real st
  p1`1<e & p2`1>e &
  q1 is_Lout P,p1,p2,e & q2`1=e & LSeg(q1,q2) c= P
  & p in LSeg(q1,q2) holds p is_Lout P,p1,p2,e;

theorem :: JORDAN20:32
for P being non empty Subset of TOP-REAL 2,
  p1,p2,q1,q2,p being Point of TOP-REAL 2,e being Real st
  p1`1<e & p2`1>e &
  q1 is_Rout P,p1,p2,e & q2`1=e & LSeg(q1,q2) c= P
  & p in LSeg(q1,q2) holds p is_Rout P,p1,p2,e;

theorem :: JORDAN20:33
for P being non empty Subset of TOP-REAL 2,
  p1,p2,p being Point of TOP-REAL 2,e being Real st
  P is_S-P_arc_joining p1,p2 & p1`1<e & p2`1>e & p in P & p`1=e
  holds p is_Lin P,p1,p2,e or p is_Rin P,p1,p2,e;

theorem :: JORDAN20:34
for P being non empty Subset of TOP-REAL 2,
  p1,p2,p being Point of TOP-REAL 2,e being Real st
  P is_S-P_arc_joining p1,p2 & p1`1<e & p2`1>e & p in P & p`1=e
  holds p is_Lout P,p1,p2,e or p is_Rout P,p1,p2,e;
