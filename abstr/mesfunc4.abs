:: Linearity of {L}ebesgue Integral of Simple Valued Function
::  by Noboru Endou and Yasunari Shidama
::
:: Received September 14, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies PARTFUN1, SUPINF_1, MEASURE1, RELAT_1, FUNCT_1, COMPLEX1, BOOLE,
      ARYTM_1, TARSKI, ARYTM_3, NAT_1, RLVECT_1, GROUP_1, FINSEQ_1, INTEGRA1,
      MESFUNC2, MESFUNC3, ARYTM, ORDINAL2;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0,
      REAL_1, BINARITH, ORDINAL2, RELAT_1, FUNCT_1, PARTFUN1, FUNCT_2,
      FINSEQ_1, NAT_1, RAT_1, SUPINF_1, SUPINF_2, MEASURE1, EXTREAL1, MESFUNC1,
      MESFUNC2, MESFUNC3, CONVFUN1;
 constructors REAL_1, NAT_1, BINARITH, FINSEQOP, FINSOP_1, EXTREAL1, SUPINF_2,
      PARTFUN1, RAT_1, MESFUNC1, MESFUNC3, MEASURE6, MESFUNC2, CONVFUN1,
      MEASURE3;
 registrations SUPINF_1, XREAL_0, RELSET_1, NAT_1, FINSEQ_1, ORDINAL2,
      MEASURE1;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;


begin  :: Linearity of Lebesgue Integral of Simple Valued Function

theorem :: MESFUNC4:1
for F,G,H be FinSequence of ExtREAL
 st (for i be Nat st i in dom F holds 0. <=' F.i)
  & (for i be Nat st i in dom G holds 0. <=' G.i)
   & dom F = dom G & H = F + G
 holds
  Sum(H)=Sum(F)+Sum(G);

theorem :: MESFUNC4:2
for X be non empty set,
    S be sigma_Field_Subset of X,
    M be sigma_Measure of S
 holds
   for n be Nat,
       f be PartFunc of X,ExtREAL,
       F be Finite_Sep_Sequence of S,
       a,x be FinSequence of ExtREAL
    st f is_simple_func_in S
     & dom f <> {}
     & (for x be set st x in dom f holds 0. <=' f.x)
     & F,a are_Re-presentation_of f
     & dom x = dom F
     & (for i be Nat st i in dom x holds x.i=a.i*(M*F).i)
     & len F = n
   holds
    integral(X,S,M,f)=Sum(x);

theorem :: MESFUNC4:3
for X be non empty set,
    S be sigma_Field_Subset of X,
    f be PartFunc of X,ExtREAL,
    M be sigma_Measure of S,
    F be Finite_Sep_Sequence of S,
    a,x be FinSequence of ExtREAL
 st f is_simple_func_in S
  & dom f <> {}
  & (for x be set st x in dom f holds 0. <=' f.x)
  & F,a are_Re-presentation_of f
  & dom x = dom F
  & (for n be Nat st n in dom x holds x.n=a.n*(M*F).n)
 holds
    integral(X,S,M,f)=Sum(x);

theorem :: MESFUNC4:4
for X be non empty set,
    S be sigma_Field_Subset of X,
    f be PartFunc of X,ExtREAL,
    M be sigma_Measure of S
 st f is_simple_func_in S
  & dom f <> {}
  & (for x be set st x in dom f holds 0. <=' f.x)
 holds
    ex F be Finite_Sep_Sequence of S, a,x be FinSequence of ExtREAL
     st F,a are_Re-presentation_of f
      & dom x = dom F
      & (for n be Nat st n in dom x holds x.n=a.n*(M*F).n)
      & integral(X,S,M,f)=Sum(x);

theorem :: MESFUNC4:5
for X be non empty set,
    S be sigma_Field_Subset of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL
  st f is_simple_func_in S & dom f <> {}
   & (for x be set st x in dom f holds 0. <=' f.x)
   & g is_simple_func_in S & dom g = dom f
   & (for x be set st x in dom g holds 0. <=' g.x)
 holds
    f+g is_simple_func_in S & dom (f+g) <> {}
  & (for x be set st x in dom (f+g) holds 0. <=' (f+g).x)
  & integral(X,S,M,f+g)=integral(X,S,M,f)+integral(X,S,M,g);

theorem :: MESFUNC4:6
for X be non empty set,
    S be sigma_Field_Subset of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL, c be R_eal
  st f is_simple_func_in S
   & dom f <> {}
   & (for x be set st x in dom f holds 0. <=' f.x)
   & 0. <=' c & c <' +infty
   & dom g = dom f
   & (for x be set st x in dom g holds g.x=c*f.x)
 holds
    integral(X,S,M,g)=c*integral(X,S,M,f);
