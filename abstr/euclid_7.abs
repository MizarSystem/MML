:: The Real Vector Spaces of Finite Sequences Are Finite Dimensional
::  by Yatsuka Nakamura , Artur Korni{\l}owicz , Nagato Oya and Yasunari Shidama
:: 
:: Received September 23, 2008
:: Copyright (c) 2008 Association of Mizar Users

environ

 vocabularies PRE_TOPC, ARYTM, FUNCT_1, FINSEQ_1, EUCLID, SQUARE_1, RLVECT_1,
      RLVECT_2, RLVECT_3, RVSUM_1, FINSEQ_2, ARYTM_1, RELAT_1, ARYTM_3,
      COMPLEX1, ABSVALUE, FUNCT_2, FINSET_1, VALUED_0, EUCLID_2, BHSP_1,
      VECTSP_1, SEQ_1, REALSET1, BHSP_3, SUBSET_1, BOOLE, SETFAM_1, FINSEQ_4,
      XREAL_0, ORDINAL2, REAL_NS1, ALGSTR_0, MATRIXR2, RLSUB_1, PARTFUN1,
      CQC_LANG, CARD_1, MATRIX_7, RFINSEQ, MATRLIN, VECTSP_9, SGRAPH1,
      RFINSEQ2, FINSEQ_7, MONOID_0, EUCLID_7, SUPINF_1;
 notations XBOOLE_0, TARSKI, RELAT_1, FUNCT_1, RVSUM_1, RELSET_1, PARTFUN1,
      CARD_1, CARD_3, FUNCT_2, FUNCT_3, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0,
      SUBSET_1, STRUCT_0, MONOID_0, COMPLEX1, FINSEQ_1, FINSEQ_2, VALUED_0,
      VALUED_1, SEQ_1, FINSET_1, BINOP_1, PRE_TOPC, BINOP_2, EUCLID, SQUARE_1,
      XXREAL_0, EUCLID_2, SETFAM_1, RLSUB_1, RLVECT_2, RLVECT_3, REAL_1, NAT_1,
      NAT_D, MATRIXR2, ZFMISC_1, ALGSTR_0, RLVECT_5, REALSET1, RLVECT_1,
      MATRIX_7, RFINSEQ, RFINSEQ2, FINSEQ_7, RUSUB_3, REAL_NS1, DOMAIN_1,
      RUSUB_4, FUNCOP_1, FINSOP_1;
 constructors REAL_1, SQUARE_1, BINOP_2, MONOID_0, SEQ_1, EUCLID_2, SETFAM_1,
      RLVECT_2, RLVECT_3, REAL_NS1, NAT_1, FUNCT_3, REALSET1, MATRIXR2,
      RLVECT_5, FINSOP_1, MATRIX_7, RFINSEQ, RFINSEQ2, SETWISEO, FINSEQ_7,
      RUSUB_3, RUSUB_4, NAT_D, CARD_1, CARD_3;
 registrations REALSET1, FUNCT_1, MONOID_0, RELSET_1, NUMBERS, XREAL_0,
      STRUCT_0, FRAENKEL, FINSET_1, XXREAL_0, CARD_1, NAT_1, VALUED_1,
      MEMBERED, FINSEQ_2, RVSUM_1, EUCLID, VALUED_0, FUNCT_2, SUBSET_1,
      XBOOLE_0, RLVECT_1, REAL_NS1, FINSEQ_1, RELAT_1, ORDINAL1, FUNCOP_1;
 requirements REAL, SUBSET, NUMERALS, ARITHM, BOOLE;


begin :: 1. Preliminaries

reserve i, j, m, n for Nat,
  x, y, a for real number,
  z, B0, B1 for set,
  v for Element of n-tuples_on REAL,
  p, p1, p2, p3, q, q1, q2 for Point of TOP-REAL n,
  f, x0 for real-valued FinSequence;

theorem :: EUCLID_7:1
  for f,g being Function holds dom (f*g) = (dom g) /\ g"(dom f);

theorem :: EUCLID_7:2
  for R being Relation, Y being set st rng R c= Y holds R"Y = dom R;

theorem :: EUCLID_7:3
  for X being set, Y being non empty set, f being Function of X,Y st
  f is bijective holds Card X = Card Y;

theorem :: EUCLID_7:4
  <* z *> * <* 1 *> = <* z *>;

theorem :: EUCLID_7:5
  for x being Element of REAL 0 holds x = <*>REAL;

theorem :: EUCLID_7:6
  for a,b,c being Element of REAL n holds a - b + c + b = a + c;

registration
  let f1,f2 be FinSequence;
  cluster <:f1,f2:> -> FinSequence-like;
end;

definition
  let D be set, f1,f2 be FinSequence of D;
  redefine func <:f1,f2:> -> FinSequence of [:D,D:];
end;

definition let h be real-valued FinSequence;
  redefine attr h is increasing means
:: EUCLID_7:def 1

  for i being Nat st 1<=i & i<len h holds h.i < h.(i+1);
end;

theorem :: EUCLID_7:7
  for h being real-valued FinSequence st h is increasing holds
  for i,j being Nat st i<j & 1<=i & j<=len h
  holds h.i < h.j;

theorem :: EUCLID_7:8
  for h being real-valued FinSequence st h is increasing holds
  for i,j being Nat st i<=j & 1<=i & j<=len h
  holds h.i <= h.j;

theorem :: EUCLID_7:9
  for h being natural-valued FinSequence st h is increasing holds
  for i being Nat st 1<=i & i<=len h & 1<=h.1 holds i <= h.i;

theorem :: EUCLID_7:10
  for V being RealLinearSpace, X being Subspace of V st
  V is strict & X is strict & the carrier of X = the carrier of V holds
  X = V;

definition let D be set;let F be FinSequence of D;
  let h be Permutation of dom F;
  func F(*)h -> FinSequence of D equals
:: EUCLID_7:def 2
  F*h;
end;

theorem :: EUCLID_7:11
  for D being non empty set,f being FinSequence of D
  st 1<=i & i<=len f & 1<=j & j<=len f
  holds (Swap(f,i,j)).i=f.j & (Swap(f,i,j)).j=f.i;

theorem :: EUCLID_7:12
  {} is Permutation of {};

theorem :: EUCLID_7:13
  <* 1 *> is Permutation of {1};

theorem :: EUCLID_7:14
  for h being FinSequence of REAL holds
  h is one-to-one iff sort_a h is one-to-one;

theorem :: EUCLID_7:15
  for h being FinSequence of NAT st h is one-to-one
  ex h3 being Permutation of dom h,h2 being FinSequence of NAT
  st h2=h*h3 & h2 is increasing & dom h=dom h2 & rng h=rng h2;

begin :: 2. Orthogonal Basis of REAL n

definition let B0 be set;
  attr B0 is R-orthogonal means
:: EUCLID_7:def 3

  for x, y being real-valued FinSequence st x in B0 & y in B0 & x<>y
  holds |( x,y )| = 0;
end;

registration
  cluster {} -> R-orthogonal;
end;

theorem :: EUCLID_7:16
  B0 is R-orthogonal iff for x,y being real-valued FinSequence
  st x in B0 & y in B0 & x <> y holds x,y are_orthogonal;

definition let B0 be set;
  attr B0 is R-normal means
:: EUCLID_7:def 4

  for x being real-valued FinSequence st x in B0 holds |.x.| = 1;
end;

registration
  cluster {} -> R-normal;
end;

registration
  cluster R-normal set;
end;

registration
  let B0, B1 be R-normal set;
  cluster B0 \/ B1 -> R-normal;
end;

theorem :: EUCLID_7:17
  |.f.| = 1 implies {f} is R-normal;

theorem :: EUCLID_7:18
  B0 is R-normal & |.x0.| = 1 implies B0 \/ {x0} is R-normal;

definition let B0 be set;
  attr B0 is R-orthonormal means
:: EUCLID_7:def 5
   B0 is R-orthogonal R-normal;
end;

registration
  cluster R-orthonormal -> R-orthogonal R-normal set;
  cluster R-orthogonal R-normal -> R-orthonormal set;
end;

registration
  cluster { <*1*> } -> R-orthonormal;
end;

registration
  cluster R-orthonormal non empty set;
end;

registration
  let n;
  cluster {}(REAL n) -> R-orthonormal;
end;

registration
  let n;
  cluster R-orthonormal Subset of REAL n;
end;

definition let n be Nat; let B0 be Subset of REAL n;
  attr B0 is complete means
:: EUCLID_7:def 6
   for B being R-orthonormal Subset of REAL n st B0 c= B holds B = B0;
end;

definition let n be Nat, B0 be Subset of REAL n;
  attr B0 is orthogonal_basis means
:: EUCLID_7:def 7

  B0 is R-orthonormal complete;
end;

registration
  let n be Nat;
  cluster orthogonal_basis -> R-orthonormal complete Subset of REAL n;
  cluster R-orthonormal complete -> orthogonal_basis Subset of REAL n;
end;

theorem :: EUCLID_7:19
  for B0 being Subset of REAL 0 st B0 is orthogonal_basis holds B0 = {};

theorem :: EUCLID_7:20
  for B0 being Subset of REAL n,y being Element of REAL n st
  B0 is orthogonal_basis &
  (for x being Element of REAL n st x in B0 holds |(x,y)|=0)
  holds y=0*n;

begin :: 3. Linear Manifolds

definition let n; let X be Subset of REAL n;
  attr X is linear_manifold means
:: EUCLID_7:def 8
   for x, y being Element of REAL n, a,b being Element of REAL
  st x in X & y in X holds a*x+b*y in X;
end;

registration
  let n;
  cluster [#](REAL n) -> linear_manifold;
end;

theorem :: EUCLID_7:21
  { 0*n } is linear_manifold;

registration
  let n;
  cluster { 0*n } -> linear_manifold Subset of REAL n;
end;

registration
  let n;
  cluster {}(REAL n) -> linear_manifold;
end;

definition let n; let X be Subset of REAL n;
  func L_Span X -> Subset of REAL n equals
:: EUCLID_7:def 9
  meet {Y where Y is Subset of REAL n: Y is linear_manifold & X c= Y};
end;

registration
  let n; let X be Subset of REAL n;
  cluster L_Span X -> linear_manifold;
end;

definition let n be Nat; let f be FinSequence of REAL n;
  func Sum f -> Element of REAL n means
:: EUCLID_7:def 10

  ex g being FinSequence of REAL n st len f=len g & f.1=g.1 &
  (for i being Nat st 1<=i & i<len f holds g.(i+1)=(g/.i)+(f/.(i+1)))
  & it=g.len f if len f>0 otherwise it=0*n;
end;

definition let n be Nat,f be FinSequence of REAL n;
  func accum f -> FinSequence of REAL n means
:: EUCLID_7:def 11

  len f = len it & f.1 = it.1 &
  for i being Nat st 1<=i & i<len f holds it.(i+1) = (it/.i) + (f/.(i+1));
 end;

theorem :: EUCLID_7:22
  for f being FinSequence of REAL n st len f>0 holds (accum f).(len f)= Sum f;

theorem :: EUCLID_7:23
  for F, F2 being FinSequence of REAL n,
  h being Permutation of dom F st F2=F(*)h holds Sum F2=Sum F;

theorem :: EUCLID_7:24
  for k being Element of NAT holds Sum(k |-> 0*n) = 0*n;

theorem :: EUCLID_7:25
  for g being FinSequence of REAL n,
  h being FinSequence of NAT,F being FinSequence of REAL n st
  h is increasing & rng h c= dom g & F=g*h &
  (for i being Element of NAT st i in dom g & not i in rng h holds g.i= 0*n)
  holds Sum g=Sum F;

theorem :: EUCLID_7:26
  for g being FinSequence of REAL n,
  h being FinSequence of NAT,F being FinSequence of REAL n st
  h is one-to-one & rng h c= dom g & F=g*h &
  (for i being Element of NAT st i in dom g & not i in rng h holds g.i= 0*n)
  holds Sum g =Sum F;

begin :: 4. Standard Basis

definition let n, i be Nat;
  redefine func Base_FinSeq(n,i) -> Element of REAL n;
end;

theorem :: EUCLID_7:27
  for i1,i2 being Nat st 1<=i1 & i1<=n & 1<=i2 & i2<=n &
  Base_FinSeq(n,i1)=Base_FinSeq(n,i2) holds i1=i2;

theorem :: EUCLID_7:28
  sqr (Base_FinSeq(n,i)) = Base_FinSeq(n,i);

theorem :: EUCLID_7:29
  1<=i & i<=n implies Sum Base_FinSeq(n,i)= 1;

theorem :: EUCLID_7:30
  1<=i & i<=n implies |. Base_FinSeq(n,i) .| = 1;

theorem :: EUCLID_7:31
  1<=i & i<=n & 1<=j & j<=n & i<>j implies
  |( Base_FinSeq(n,i),Base_FinSeq(n,j) )| = 0;

theorem :: EUCLID_7:32
  for x being Element of REAL n st 1<=i & i<=n holds
  |( x, Base_FinSeq(n,i) )| = x.i;

definition let n be Nat; let x0 be Element of REAL n;
  func ProjFinSeq x0 -> FinSequence of REAL n means
:: EUCLID_7:def 12
   len it = n & for i being Nat st
  1<=i & i<=n holds it.i = |( x0,Base_FinSeq(n,i) )| * Base_FinSeq(n,i);
end;

theorem :: EUCLID_7:33
  for x0 being Element of REAL n holds x0 = Sum (ProjFinSeq x0);

definition let n be Nat;
  func RN_Base n -> Subset of REAL n equals
:: EUCLID_7:def 13
  { Base_FinSeq(n,i) where i is Element of NAT: 1<=i & i<=n };
end;

theorem :: EUCLID_7:34
  for n being non zero Nat holds RN_Base n <> {};

registration
  cluster RN_Base 0 -> empty;
end;

registration
  let n be non zero Nat;
  cluster RN_Base n -> non empty;
end;

registration
  let n;
  cluster RN_Base n -> orthogonal_basis;
 end;

registration
  let n;
  cluster orthogonal_basis Subset of REAL n;
end;

definition
  let n;
  mode Orthogonal_Basis of n is orthogonal_basis Subset of REAL n;
end;

registration
  let n be non zero Nat;
  cluster -> non empty Orthogonal_Basis of n;
end;

begin :: 5. Finite Real Unitary Spaces and Finite Real Linear Spaces

registration let n be Element of NAT;
  cluster REAL-US n -> constituted-FinSeqs;
end;

registration let n be Element of NAT;
  cluster -> real-valued Element of REAL-US n;
end;

registration
  let n be Element of NAT;
  let x, y be VECTOR of REAL-US n, a, b be real-valued Function;
  identify x+y with a+b when x = a, y = b;
end;

registration
  let n be Element of NAT, x be VECTOR of REAL-US n,
  y be real-valued Function, a, b be Element of REAL;
  identify a*x with b(#)y when x = y, a = b;
end;

registration
  let n be Element of NAT;
  let x be VECTOR of REAL-US n, a be real-valued Function;
  identify -x with -a when x = a;
end;

registration
  let n be Element of NAT;
  let x, y be VECTOR of REAL-US n, a, b be real-valued Function;
  identify x-y with a-b when x = a, y = b;
end;

theorem :: EUCLID_7:35
  for n being Element of NAT, x,y being Element of REAL n,
  u,v being Point of REAL-US n st x=u & y=v holds
  (Euclid_scalar n).[u,v] = |(x,y)|;

theorem :: EUCLID_7:36
  for n,j being Element of NAT,
  F being FinSequence of the carrier of (REAL-US n),
  Bn being Subset of REAL-US n,
  v0 being Element of REAL-US n,l being Linear_Combination of Bn
  st F is one-to-one & Bn is R-orthogonal & rng F = Carrier l &
  v0 in Bn & j in dom (l (#) F) & v0=F.j
  holds (Euclid_scalar n).[v0,Sum (l(#)F)]
  = (Euclid_scalar n).[v0,(l.(F/.j))*v0];

theorem :: EUCLID_7:37
  for n being Element of NAT, f being FinSequence of REAL n,
  g being FinSequence of the carrier of (REAL-US n) st f=g
  holds Sum f=Sum g;

definition let n be Nat;
  func REAL-L n -> RLSStruct equals
:: EUCLID_7:def 14
  RLSStruct(# REAL(n),0*(n),Euclid_add(n),Euclid_mult(n) #);
end;

registration let n;
  cluster REAL-L n -> constituted-FinSeqs;
end;

registration let n;
  cluster -> real-valued Element of REAL-L n;
end;

registration let n;
  cluster REAL-L n -> strict non empty;
end;

registration let n;
  cluster REAL-L n -> Abelian add-associative right_zeroed
  right_complementable RealLinearSpace-like;
 end;

registration
  let n;
  let x, y be VECTOR of REAL-L n, a, b be real-valued Function;
  identify x+y with a+b when x = a, y = b;
end;

registration
  let n; let x be VECTOR of REAL-L n,
  y be real-valued Function, a, b be Element of REAL;
  identify a*x with b(#)y when x = y, a = b;
end;

registration
  let n; let x be VECTOR of REAL-L n, a be real-valued Function;
  identify -x with -a when x = a;
end;

registration
  let n; let x, y be VECTOR of REAL-L n, a, b be real-valued Function;
  identify x-y with a-b when x = a, y = b;
end;

theorem :: EUCLID_7:38
  for X being Subspace of REAL-L n,
  x being Element of REAL n, a being Real st x in the carrier of X holds
  a*x in the carrier of X;

theorem :: EUCLID_7:39
  for X being Subspace of REAL-L n,
  x,y being Element of REAL n st x in the carrier of X & y in the carrier of X
  holds x+y in the carrier of X;

theorem :: EUCLID_7:40
  for X being Subspace of REAL-L n,
  x,y being Element of REAL n,a,b being Real st
  x in the carrier of X & y in the carrier of X holds
  a*x+b*y in the carrier of X;

theorem :: EUCLID_7:41
  for x,y being Element of REAL n,
  u,v being Point of REAL-L n st x=u & y=v holds
  (Euclid_scalar n).[u,v]= |(x,y)|;

theorem :: EUCLID_7:42
  for F being FinSequence of the carrier of REAL-L n,
      Bn being Subset of REAL-L n,
  v0 being Element of REAL-L n,l being Linear_Combination of Bn
  st F is one-to-one & Bn is R-orthogonal & rng F = Carrier l &
  v0 in Bn & j in dom (l (#) F) & v0=F.j
  holds (Euclid_scalar n).[v0,Sum (l(#)F)] =
  (Euclid_scalar n).[v0,(l.(F/.j))*v0];

registration
  let n;
  cluster R-orthonormal -> linearly-independent Subset of REAL-L n;
end;

registration
  let n be Element of NAT;
  cluster R-orthonormal -> linearly-independent Subset of REAL-US n;
end;

theorem :: EUCLID_7:43
  for Bn being Subset of REAL-L n,
  x,y being Element of REAL n,a being Real st
  Bn is linearly-independent & x in Bn & y in Bn & y=a*x holds x=y;

begin :: 6. Finite Dimensionality of the Spaces

registration
  let n;
  cluster RN_Base n -> finite;
end;

theorem :: EUCLID_7:44
  Card RN_Base n = n;

theorem :: EUCLID_7:45
  for f being FinSequence of REAL n,
  g being FinSequence of the carrier of REAL-L n st f=g
  holds Sum f=Sum g;

theorem :: EUCLID_7:46
  for x0 being Element of REAL-L n,
  B being Subset of REAL-L n st B=RN_Base n holds
  ex l being Linear_Combination of B st x0=Sum l;

theorem :: EUCLID_7:47
  for n being Element of NAT,x0 being Element of REAL-US n,
  B being Subset of REAL-US n st B=RN_Base n holds
  ex l being Linear_Combination of B st x0=Sum l;

theorem :: EUCLID_7:48
  for B being Subset of REAL-L n st B=RN_Base n holds
  B is Basis of REAL-L n;

registration
  let n;
  cluster REAL-L n -> finite-dimensional;
end;

theorem :: EUCLID_7:49
  dim (REAL-L n) = n;

theorem :: EUCLID_7:50
  for B being Subset of REAL-L n st B is Basis of REAL-L n holds Card B = n;

theorem :: EUCLID_7:51
  {} is Basis of REAL-L 0;

theorem :: EUCLID_7:52
  for n being Element of NAT holds RN_Base n is Basis of REAL-US n;

theorem :: EUCLID_7:53
  for D being Orthogonal_Basis of n holds D is Basis of REAL-L n;

registration
  let n be Element of NAT;
  cluster REAL-US n -> finite-dimensional;
end;

theorem :: EUCLID_7:54
  for n being Element of NAT holds dim (REAL-US n) = n;

theorem :: EUCLID_7:55
  for B being Orthogonal_Basis of n holds Card B = n;
