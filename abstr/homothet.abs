:: Homotheties and Shears in Affine Planes
::  by Henryk Oryszczyszyn and Krzysztof Pra\.zmowski
::
:: Received September 21, 1990
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies DIRAF, FUNCT_2, AFF_2, ANALOAF, INCSP_1, AFF_1, TRANSGEO,
      FUNCT_1, RELAT_1, HOMOTHET;
 notations TARSKI, RELSET_1, PARTFUN1, FUNCT_2, STRUCT_0, ANALOAF, DIRAF,
      AFF_1, AFF_2, TRANSGEO;
 constructors PARTFUN1, AFF_1, AFF_2, TRANSGEO, RELSET_1;
 registrations STRUCT_0;
 requirements SUBSET, BOOLE;


begin

reserve AFP for AffinPlane;
reserve a,a',b,b',c,c',d,d',o,p,p',q,q',r,s,t,x,y,z for Element of AFP;
reserve A,A',C,D,P,B',M,N,K for Subset of AFP;
reserve f for Permutation of the carrier of AFP;

theorem :: HOMOTHET:1
  not LIN o,a,p & LIN o,a,b & LIN o,a,x & LIN o,a,y & LIN o,p,p' &
  LIN o,p,q & LIN o,p,q' & p<>q & o<>q & o<>x & a,p // b,p' & a,q // b,q' & x,p
  // y,p' & AFP is Desarguesian implies x,q // y,q';

theorem :: HOMOTHET:2
  (for o,a,b st o<>a & o<>b & LIN o,a,b ex f st f is dilatation & f
  .o=o & f.a=b) implies AFP is Desarguesian;

theorem :: HOMOTHET:3
  AFP is Desarguesian implies for o,a,b st o<>a & o<>b & LIN o,a,b
  ex f st f is dilatation & f.o=o & f.a=b;

theorem :: HOMOTHET:4
  AFP is Desarguesian iff for o,a,b st o<>a & o<>b & LIN o,a,b ex f st f
  is dilatation & f.o=o & f.a=b;

definition
  let AFP,f,K;
  pred f is_Sc K means
:: HOMOTHET:def 1

  f is collineation & K is being_line & (for x st
  x in K holds f.x = x) & for x holds x,f.x // K;
end;

theorem :: HOMOTHET:5
  f is_Sc K & f.p=p & not p in K implies f=id the carrier of AFP;

theorem :: HOMOTHET:6
  (for a,b,K st a,b // K & not a in K ex f st f is_Sc K & f.a=b)
  implies AFP is Moufangian;

theorem :: HOMOTHET:7
  K // M & p in K & q in K & p' in K & q' in K & AFP is Moufangian
& a in M & b in M & x in M & y in M & a<>b & q<>p & p,a // p',x & p,b // p',y &
  q,a // q',x implies q,b // q',y;

theorem :: HOMOTHET:8
  a,b // K & not a in K & AFP is Moufangian implies ex f st f is_Sc
  K & f.a=b;

theorem :: HOMOTHET:9
  AFP is Moufangian iff for a,b,K st a,b // K & not a in K ex f st f
  is_Sc K & f.a=b;

