:: On the Isomorphism Between Finite Chains
::  by Marta Pruszy\'nska and Marek Dudzicz
::
:: Received June 29, 2000
:: Copyright (c) 2000 Association of Mizar Users

environ

 vocabulary ORDERS_1, RELAT_2, REALSET1, CARD_4, FINSET_1, CAT_1, YELLOW_0,
      YELLOW_1, WELLORD2, WELLORD1, BOOLE, LATTICES, PRE_TOPC, FUNCT_4,
      RELAT_1, FUNCT_1, ORDINAL1, CARD_1, FUNCOP_1, ARYTM_1, PBOOLE, ORDERS_4;
 notation TARSKI, XBOOLE_0, STRUCT_0, XCMPLX_0, XREAL_0, FINSET_1, NAT_1,
      FUNCOP_1, GROUP_1, PBOOLE, CARD_1, CARD_4, REALSET1, RELAT_2, ORDERS_1,
      PRE_TOPC, RELAT_1, FUNCT_1, FUNCT_2, YELLOW_0, WAYBEL_0, YELLOW_1,
      WAYBEL_1, FUNCT_4, WELLORD1, ORDINAL1;
 constructors CARD_4, WAYBEL_1, NAT_1, REAL_1, ORDERS_3, GROUP_6, WAYBEL_8,
      WAYBEL19, WAYBEL23, MEMBERED, TOLER_1;
 clusters FINSET_1, WAYBEL_0, STRUCT_0, YELLOW_0, YELLOW_1, WAYBEL_2, XREAL_0,
      YELLOW13, RELSET_1, WAYBEL31, FUNCOP_1, YELLOW14, YELLOW_2, NAT_1,
      MEMBERED;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM;


begin

definition
 mode Chain -> RelStr means
:: ORDERS_4:def 1
  it is connected (non empty Poset) or it is empty;
end;


registration
 cluster empty -> reflexive transitive antisymmetric RelStr;
 coherence
  proof
   let A be RelStr;
    assume A1: A is empty;
    then for x be set holds x in the carrier of A
          implies [x,x] in the InternalRel of A by STRUCT_0:def 1;
then A2:  the InternalRel of A is_reflexive_in the carrier of A by RELAT_2:def
1;
      for x,y,z be set holds
     x in the carrier of A & y in the carrier of A & z in the carrier of A &
      [x,y] in the InternalRel of A & [y,z] in the InternalRel of A implies
        [x,z] in the InternalRel of A by A1,STRUCT_0:def 1;
then A3:  the InternalRel of A is_transitive_in the carrier of A by RELAT_2:def
8;
      for x,y be set holds
     x in the carrier of A & y in the carrier of A &
      [x,y] in the InternalRel of A & [y,x] in the InternalRel of A implies
        x = y by A1,STRUCT_0:def 1;
    then the InternalRel of A is_antisymmetric_in the carrier of A
       by RELAT_2:def 4;
   hence thesis by A2,A3,ORDERS_1:def 4,def 5,def 6;
  end;
end;


registration
 cluster -> reflexive transitive antisymmetric Chain;
 coherence
  proof let A be Chain;
      A is connected (non empty Poset) or A is empty RelStr by Def1;
   hence thesis;
  end;
end;


registration
 cluster non empty Chain;
 existence
  proof
    consider A being trivial reflexive transitive antisymmetric
      non empty RelStr;
      A is Chain by Def1;
    hence thesis;
  end;
end;


registration
 cluster -> connected (non empty Chain);
 coherence by Def1;
end;


definition let L be 1-sorted;
  attr L is countable means
:: ORDERS_4:def 2
    the carrier of L is countable;
end;


registration
  cluster finite non empty Chain;
  existence
   proof
     consider A being
     trivial reflexive transitive antisymmetric non empty finite RelStr;
       A is Chain by Def1;
     hence thesis;
   end;
end;


registration
  cluster countable Chain;
  existence
  proof
    consider L being finite Chain;
    take L;
      the carrier of L is countable by CARD_4:43;
    hence L is countable by Def2;
  end;
end;


registration let A be connected (non empty RelStr);
  cluster full -> connected (non empty SubRelStr of A);
  correctness
   proof
   let S be non empty SubRelStr of A;
    assume A1: S is full;
      for x,y being Element of S holds x <= y or y <= x
     proof
      let x,y be Element of S;
A2:     the carrier of S c= the carrier of A by YELLOW_0:def 13;
A3:     x in the carrier of S;
         y in the carrier of S;
       then reconsider b=y as Element of A by A2;
       reconsider a=x as Element of A by A2,A3;
         a <= b or b <= a by WAYBEL_0:def 29;
       hence thesis by A1,YELLOW_0:61;
     end;
    hence thesis by WAYBEL_0:def 29;
   end;
end;


registration let A be finite RelStr;
  cluster -> finite SubRelStr of A;
  correctness
   proof
    let S be SubRelStr of A;
    the carrier of S c= the carrier of A by YELLOW_0:def 13;
     then the carrier of S is finite by FINSET_1:13;
    hence thesis by GROUP_1:def 13;
   end;
end;


theorem :: ORDERS_4:1
for n,m be Nat holds
 n <= m implies InclPoset(n) is full SubRelStr of InclPoset(m);

definition
 let L be RelStr;
 let A,B be set;
pred A,B form_upper_lower_partition_of L means
:: ORDERS_4:def 3
 A \/ B = the carrier of L &
  for a,b be Element of L st a in A & b in B holds
   a < b;
end;


theorem :: ORDERS_4:2
 for L be RelStr
  for A,B be set holds
   A,B form_upper_lower_partition_of L implies A misses B;

theorem :: ORDERS_4:3
for L be upper-bounded antisymmetric non empty RelStr holds
 ((the carrier of L) \ { Top L }), { Top L } form_upper_lower_partition_of L;

theorem :: ORDERS_4:4
for L1,L2 be RelStr
 for f be map of L1,L2 st f is isomorphic
  holds (the carrier of L1 <> {} iff the carrier of L2 <> {})
   & (the carrier of L2 <> {} or the carrier of L1 = {})
    & (the carrier of L1 = {} iff the carrier of L2 = {});

theorem :: ORDERS_4:5
for L1,L2 be antisymmetric RelStr
 for A1,B1 be Subset of L1 st A1,B1 form_upper_lower_partition_of L1
  for A2,B2 be Subset of L2 st A2,B2 form_upper_lower_partition_of L2
   for f be map of subrelstr A1, subrelstr A2 st f is isomorphic
    for g be map of subrelstr B1, subrelstr B2 st g is isomorphic
     ex h be map of L1,L2 st h = f +* g & h is isomorphic;

theorem :: ORDERS_4:6
  for A being finite Chain, n being Nat st Card(the carrier of A) = n holds
    A,InclPoset n are_isomorphic;
