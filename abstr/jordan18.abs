:: The Ordering of Points on a Curve, Part {IV}
::  by Artur Korni{\l}owicz
::
:: Received September 16, 2002
:: Copyright (c) 2002 Association of Mizar Users

environ

 vocabulary ORDINAL2, ARYTM, PRE_TOPC, EUCLID, TOPREAL1, BOOLE, TOPREAL2,
      INCPROJ, ARYTM_1, MCART_1, FUNCT_5, RELAT_1, JORDAN1A, JORDAN2C, JORDAN3,
      TOPS_2, RELAT_2, SUBSET_1, SEQ_2, FUNCT_1, JORDAN6, COMPTS_1, LATTICES,
      METRIC_1, PCOMPS_1, ABSVALUE, SQUARE_1, JORDAN18, SPPOL_1;
 notation TARSKI, XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, FUNCT_2, ORDINAL1,
      NUMBERS, XCMPLX_0, XREAL_0, REAL_1, SQUARE_1, ABSVALUE, SEQ_4, STRUCT_0,
      INCPROJ, PRE_TOPC, TOPS_2, CONNSP_1, RCOMP_1, COMPTS_1, METRIC_1,
      SPPOL_1, PCOMPS_1, EUCLID, TOPREAL1, PSCOMP_1, TOPREAL2, JORDAN5C,
      JORDAN2C, GOBRD14, JORDAN1A, JORDAN6;
 constructors REAL_1, TOPREAL1, TOPREAL2, INCPROJ, SPPOL_1, JORDAN1A, PSCOMP_1,
      JORDAN5C, TOPS_2, JORDAN2C, JORDAN6, CONNSP_1, JORDAN1, BORSUK_2,
      RCOMP_1, ABSVALUE, GOBRD14, SQUARE_1, MEMBERED;
 clusters XREAL_0, EUCLID, STRUCT_0, JORDAN1A, SUBSET_1, RELSET_1, BORSUK_2,
      SPRECT_4, MEMBERED, ZFMISC_1;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;


begin  :: Preliminaries

reserve n for Element of NAT,
        V for Subset of TOP-REAL n,
        s,s1,s2,t,t1,t2 for Point of TOP-REAL n,
        C for Simple_closed_curve,
        P for Subset of TOP-REAL 2,
        a,p,p1,p2,q,q1,q2 for Point of TOP-REAL 2;

theorem :: JORDAN18:1
  for a,b being real number holds (a-b)^2 = (b-a)^2;

theorem :: JORDAN18:2
    for S,T being non empty TopSpace,
      f being map of S,T,
      A being Subset of T st f is_homeomorphism & A is connected
   holds f"A is connected;

theorem :: JORDAN18:3
    for S,T being non empty TopStruct,
      f being map of S,T,
      A being Subset of T st f is_homeomorphism & A is compact
   holds f"A is compact;

theorem :: JORDAN18:4
  proj2.:(north_halfline a) is bounded_below;

theorem :: JORDAN18:5
  proj2.:(south_halfline a) is bounded_above;

theorem :: JORDAN18:6
  proj1.:(west_halfline a) is bounded_above;

theorem :: JORDAN18:7
  proj1.:(east_halfline a) is bounded_below;

registration
  let a;
  cluster proj2.:(north_halfline a) -> non empty;
  coherence by Lm4,RELAT_1:152;
  cluster proj2.:(south_halfline a) -> non empty;
  coherence by Lm4,RELAT_1:152;
  cluster proj1.:(west_halfline a) -> non empty;
  coherence by Lm3,RELAT_1:152;
  cluster proj1.:(east_halfline a) -> non empty;
  coherence by Lm3,RELAT_1:152;
end;


theorem :: JORDAN18:8
  inf(proj2.:(north_halfline a)) = a`2;

theorem :: JORDAN18:9
  sup(proj2.:(south_halfline a)) = a`2;

theorem :: JORDAN18:10
    sup(proj1.:(west_halfline a)) = a`1;

theorem :: JORDAN18:11
    inf(proj1.:(east_halfline a)) = a`1;

registration
  let a;
  cluster north_halfline a -> closed;
  coherence
  proof
    set X = north_halfline a;
      for p being Point of Euclid 2 st p in X`
      ex r being real number st r>0 & Ball(p,r) c= X`
    proof
      let p be Point of Euclid 2;
      assume
A1:     p in X`;
      then reconsider x = p as Point of TOP-REAL 2;
A2:   not p in X by A1,YELLOW_6:10;
      per cases by A2,JORDAN1A:def 2;
      suppose
A3:     x`1 <> a`1;
      take r = abs(x`1-a`1);
        x`1 - a`1 <> 0 by A3,XCMPLX_1:15;
      hence r > 0 by ABSVALUE:6;
      let b be set;
      assume
A4:     b in Ball(p,r);
      then reconsider b as Point of Euclid 2;
      reconsider c = b as Point of TOP-REAL 2 by Lm2,EUCLID:def 8;
        dist(p,b) < r by A4,METRIC_1:12;
      then A5:   dist(x,c) < r by GOBRD14:def 1;
        now
        assume
A6:       c`1 = a`1;
A7:     0 <= (x`2-c`2)^2 by SQUARE_1:72;
         A8: 0 <= (x`1-c`1)^2 by SQUARE_1:72;
        then 0+0 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A7,REAL_1:55;
        then A9:     0 <= sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) by SQUARE_1:def 4;
          sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) < abs(x`1-c`1) by A5,A6,GOBRD14:9;
        then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (abs(x`1-c`1))^2
          by A9,SQUARE_1:78;
        then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (x`1-c`1)^2
          by SQUARE_1:62;
        then (x`1-c`1)^2 + (x`2-c`2)^2 < (x`1-c`1)^2 + 0 by A8,SQUARE_1:def 4;
        hence contradiction by A7,REAL_1:55;
      end;
      then not c in X by JORDAN1A:def 2;
      hence thesis by YELLOW_6:10;

      end;
      suppose
A10:     x`2 < a`2;
      take r = a`2-x`2;
      thus r > 0 by A10,SQUARE_1:11;
      let b be set;
      assume
A11:     b in Ball(p,r);
      then reconsider b as Point of Euclid 2;
      reconsider c = b as Point of TOP-REAL 2 by Lm2,EUCLID:def 8;
        dist(p,b) < r by A11,METRIC_1:12;
      then A12:   dist(x,c) < r by GOBRD14:def 1;
        now
        assume
A13:       c`2 >= a`2;
A14:     0 <= (x`1-c`1)^2 by SQUARE_1:72;
          0 <= (x`2-c`2)^2 by SQUARE_1:72;
        then A15:     0+0 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A14,REAL_1:55;
        then A16:     0 <= sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) by SQUARE_1:def 4;
          sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) < a`2-x`2 by A12,GOBRD14:9;
        then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (a`2-x`2)^2
          by A16,SQUARE_1:78;
        then (x`1-c`1)^2 + (x`2-c`2)^2 < (a`2-x`2)^2 by A15,SQUARE_1:def 4;
        then A17:     (x`1-c`1)^2 + (x`2-c`2)^2 < (x`2-a`2)^2 by Th1;
A18:     0 <= a`2-x`2 by A10,SQUARE_1:11;
          a`2-x`2 <= c`2-x`2 by A13,REAL_1:92;
        then (a`2-x`2)^2 <= (c`2-x`2)^2 by A18,SQUARE_1:77;
        then (x`2-a`2)^2 <= (c`2-x`2)^2 by Th1;
        then A19:     (x`2-a`2)^2 <= (x`2-c`2)^2 by Th1;
          0 + (x`2-c`2)^2 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A14,REAL_1:55;
        hence contradiction by A17,A19,AXIOMS:22;
      end;
      then not c in X by JORDAN1A:def 2;
      hence thesis by YELLOW_6:10;
    end;
    end;
    then X` is open by Lm1,TOPMETR:22;
    hence thesis by TOPS_1:29;
  end;

  cluster south_halfline a -> closed;
  coherence
  proof
    set X = south_halfline a;
      for p being Point of Euclid 2 st p in X`
      ex r being real number st r>0 & Ball(p,r) c= X`
    proof
      let p be Point of Euclid 2;
      assume
A20:     p in X`;
      then reconsider x = p as Point of TOP-REAL 2;
A21:   not p in X by A20,YELLOW_6:10;
      per cases by A21,JORDAN1A:def 4;
      suppose
A22:     x`1 <> a`1;
      take r = abs(x`1-a`1);
        x`1 - a`1 <> 0 by A22,XCMPLX_1:15;
      hence r > 0 by ABSVALUE:6;
      let b be set;
      assume
A23:     b in Ball(p,r);
      then reconsider b as Point of Euclid 2;
      reconsider c = b as Point of TOP-REAL 2 by Lm2,EUCLID:def 8;
        dist(p,b) < r by A23,METRIC_1:12;
      then A24:   dist(x,c) < r by GOBRD14:def 1;
        now
        assume
A25:       c`1 = a`1;
A26:     0 <= (x`2-c`2)^2 by SQUARE_1:72;
         A27: 0 <= (x`1-c`1)^2 by SQUARE_1:72;
        then 0+0 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A26,REAL_1:55;
        then A28:     0 <= sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) by SQUARE_1:def 4;
          sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) < abs(x`1-c`1) by A24,A25,GOBRD14:9;
        then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (abs(x`1-c`1))^2
          by A28,SQUARE_1:78;
        then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (x`1-c`1)^2
          by SQUARE_1:62;
        then (x`1-c`1)^2 + (x`2-c`2)^2 < (x`1-c`1)^2 + 0 by A27,SQUARE_1:def 4
;
        hence contradiction by A26,REAL_1:55;
      end;
      then not c in X by JORDAN1A:def 4;
      hence thesis by YELLOW_6:10;

      end;
      suppose
A29:     x`2 > a`2;
      take r = x`2-a`2;
      thus r > 0 by A29,SQUARE_1:11;
      let b be set;
      assume
A30:     b in Ball(p,r);
      then reconsider b as Point of Euclid 2;
      reconsider c = b as Point of TOP-REAL 2 by Lm2,EUCLID:def 8;
        dist(p,b) < r by A30,METRIC_1:12;
      then A31:   dist(x,c) < r by GOBRD14:def 1;
        now
        assume
A32:       c`2 <= a`2;
A33:     0 <= (x`1-c`1)^2 by SQUARE_1:72;
          0 <= (x`2-c`2)^2 by SQUARE_1:72;
        then A34:     0+0 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A33,REAL_1:55;
        then A35:     0 <= sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) by SQUARE_1:def 4;
          sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) < x`2-a`2 by A31,GOBRD14:9;
        then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (x`2-a`2)^2
          by A35,SQUARE_1:78;
        then A36:     (x`1-c`1)^2 + (x`2-c`2)^2 < (x`2-a`2)^2 by A34,SQUARE_1:
def 4;
A37:     0 <= x`2-a`2 by A29,SQUARE_1:11;
          x`2-c`2 >= x`2-a`2 by A32,REAL_1:92;
        then A38:     (x`2-a`2)^2 <= (x`2-c`2)^2 by A37,SQUARE_1:77;
          0 + (x`2-c`2)^2 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A33,REAL_1:55;
        hence contradiction by A36,A38,AXIOMS:22;
      end;
      then not c in X by JORDAN1A:def 4;
      hence thesis by YELLOW_6:10;
    end;
    end;
    then X` is open by Lm1,TOPMETR:22;
    hence thesis by TOPS_1:29;
  end;

  cluster east_halfline a -> closed;
  coherence
  proof
    set X = east_halfline a;
      for p being Point of Euclid 2 st p in X`
      ex r being real number st r>0 & Ball(p,r) c= X`
    proof
      let p be Point of Euclid 2;
      assume
A39:     p in X`;
      then reconsider x = p as Point of TOP-REAL 2;
A40:   not p in X by A39,YELLOW_6:10;
      per cases by A40,JORDAN1A:def 3;
      suppose
A41:     x`2 <> a`2;
      take r = abs(x`2-a`2);
        x`2 - a`2 <> 0 by A41,XCMPLX_1:15;
      hence r > 0 by ABSVALUE:6;
      let b be set;
      assume
A42:     b in Ball(p,r);
      then reconsider b as Point of Euclid 2;
      reconsider c = b as Point of TOP-REAL 2 by Lm2,EUCLID:def 8;
        dist(p,b) < r by A42,METRIC_1:12;
      then A43:   dist(x,c) < r by GOBRD14:def 1;
        now
        assume
A44:       c`2 = a`2;
A45:     0 <= (x`1-c`1)^2 by SQUARE_1:72;
         A46: 0 <= (x`2-c`2)^2 by SQUARE_1:72;
        then 0+0 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A45,REAL_1:55;
        then A47:     0 <= sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) by SQUARE_1:def 4;
          sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) < abs(x`2-c`2) by A43,A44,GOBRD14:9;
        then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (abs(x`2-c`2))^2
          by A47,SQUARE_1:78;
        then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (x`2-c`2)^2
          by SQUARE_1:62;
        then (x`1-c`1)^2 + (x`2-c`2)^2 < (x`2-c`2)^2 + 0 by A46,SQUARE_1:def 4
;
        hence contradiction by A45,REAL_1:55;
      end;
      then not c in X by JORDAN1A:def 3;
      hence thesis by YELLOW_6:10;

      end;
      suppose
A48:     x`1 < a`1;
      take r = a`1-x`1;
      thus r > 0 by A48,SQUARE_1:11;
      let b be set;
      assume
A49:     b in Ball(p,r);
      then reconsider b as Point of Euclid 2;
      reconsider c = b as Point of TOP-REAL 2 by Lm2,EUCLID:def 8;
        dist(p,b) < r by A49,METRIC_1:12;
      then A50:   dist(x,c) < r by GOBRD14:def 1;
        now
        assume
A51:       c`1 >= a`1;
A52:     0 <= (x`2-c`2)^2 by SQUARE_1:72;
          0 <= (x`1-c`1)^2 by SQUARE_1:72;
        then A53:     0+0 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A52,REAL_1:55;
        then A54:     0 <= sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) by SQUARE_1:def 4;
          sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) < a`1-x`1 by A50,GOBRD14:9;
        then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (a`1-x`1)^2
          by A54,SQUARE_1:78;
        then (x`1-c`1)^2 + (x`2-c`2)^2 < (a`1-x`1)^2 by A53,SQUARE_1:def 4;
        then A55:     (x`1-c`1)^2 + (x`2-c`2)^2 < (x`1-a`1)^2 by Th1;
A56:     0 <= a`1-x`1 by A48,SQUARE_1:11;
          a`1-x`1 <= c`1-x`1 by A51,REAL_1:92;
        then (a`1-x`1)^2 <= (c`1-x`1)^2 by A56,SQUARE_1:77;
        then (x`1-a`1)^2 <= (c`1-x`1)^2 by Th1;
        then A57:     (x`1-a`1)^2 <= (x`1-c`1)^2 by Th1;
          (x`1-c`1)^2 + 0 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A52,REAL_1:55;
        hence contradiction by A55,A57,AXIOMS:22;
      end;
      then not c in X by JORDAN1A:def 3;
      hence thesis by YELLOW_6:10;
    end;
    end;
    then X` is open by Lm1,TOPMETR:22;
    hence thesis by TOPS_1:29;
  end;

  cluster west_halfline a -> closed;
  coherence
  proof
    set X = west_halfline a;
      for p being Point of Euclid 2 st p in X`
      ex r being real number st r>0 & Ball(p,r) c= X`
    proof
      let p be Point of Euclid 2;
      assume
A58:     p in X`;
      then reconsider x = p as Point of TOP-REAL 2;
A59:   not p in X by A58,YELLOW_6:10;
      per cases by A59,JORDAN1A:def 5;
      suppose
A60:     x`2 <> a`2;
      take r = abs(x`2-a`2);
        x`2 - a`2 <> 0 by A60,XCMPLX_1:15;
      hence r > 0 by ABSVALUE:6;
      let b be set;
      assume
A61:     b in Ball(p,r);
      then reconsider b as Point of Euclid 2;
      reconsider c = b as Point of TOP-REAL 2 by Lm2,EUCLID:def 8;
        dist(p,b) < r by A61,METRIC_1:12;
      then A62:   dist(x,c) < r by GOBRD14:def 1;
        now
        assume
A63:       c`2 = a`2;
A64:     0 <= (x`1-c`1)^2 by SQUARE_1:72;
         A65: 0 <= (x`2-c`2)^2 by SQUARE_1:72;
        then 0+0 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A64,REAL_1:55;
        then A66:     0 <= sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) by SQUARE_1:def 4;
          sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) < abs(x`2-c`2) by A62,A63,GOBRD14:9;
        then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (abs(x`2-c`2))^2
          by A66,SQUARE_1:78;
        then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (x`2-c`2)^2
          by SQUARE_1:62;
        then (x`1-c`1)^2 + (x`2-c`2)^2 < (x`2-c`2)^2 + 0 by A65,SQUARE_1:def 4
;
        hence contradiction by A64,REAL_1:55;
      end;
      then not c in X by JORDAN1A:def 5;
      hence thesis by YELLOW_6:10;

      end;
      suppose
A67:     x`1 > a`1;
      take r = x`1-a`1;
      thus r > 0 by A67,SQUARE_1:11;
      let b be set;
      assume
A68:     b in Ball(p,r);
      then reconsider b as Point of Euclid 2;
      reconsider c = b as Point of TOP-REAL 2 by Lm2,EUCLID:def 8;
        dist(p,b) < r by A68,METRIC_1:12;
      then A69:   dist(x,c) < r by GOBRD14:def 1;
        now
        assume
A70:       c`1 <= a`1;
A71:     0 <= (x`2-c`2)^2 by SQUARE_1:72;
          0 <= (x`1-c`1)^2 by SQUARE_1:72;
        then A72:     0+0 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A71,REAL_1:55;
        then A73:     0 <= sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) by SQUARE_1:def 4;
          sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) < x`1-a`1 by A69,GOBRD14:9;
        then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (x`1-a`1)^2
          by A73,SQUARE_1:78;
        then A74:     (x`1-c`1)^2 + (x`2-c`2)^2 < (x`1-a`1)^2 by A72,SQUARE_1:
def 4;
A75:     0 <= x`1-a`1 by A67,SQUARE_1:11;
          x`1-c`1 >= x`1-a`1 by A70,REAL_1:92;
        then A76:     (x`1-a`1)^2 <= (x`1-c`1)^2 by A75,SQUARE_1:77;
          0 + (x`1-c`1)^2 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A71,REAL_1:55;
        hence contradiction by A74,A76,AXIOMS:22;
      end;
      then not c in X by JORDAN1A:def 5;
      hence thesis by YELLOW_6:10;
    end;
    end;
    then X` is open by Lm1,TOPMETR:22;
    hence thesis by TOPS_1:29;
  end;
end;


theorem :: JORDAN18:12
  a in BDD P implies not north_halfline a c= UBD P;

theorem :: JORDAN18:13
  a in BDD P implies not south_halfline a c= UBD P;

theorem :: JORDAN18:14
    a in BDD P implies not east_halfline a c= UBD P;

theorem :: JORDAN18:15
    a in BDD P implies not west_halfline a c= UBD P;

theorem :: JORDAN18:16
    for P being Subset of TOP-REAL 2,
      p1,p2,q being Point of TOP-REAL 2
   st P is_an_arc_of p1,p2 & q <> p2
   holds not p2 in L_Segment(P,p1,p2,q);

theorem :: JORDAN18:17
    for P being Subset of TOP-REAL 2,
      p1,p2,q being Point of TOP-REAL 2
   st P is_an_arc_of p1,p2 & q <> p1
   holds not p1 in R_Segment(P,p1,p2,q);

theorem :: JORDAN18:18
  for C being Simple_closed_curve,
      P being Subset of TOP-REAL 2,
      p1,p2 being Point of TOP-REAL 2
   st P is_an_arc_of p1,p2 & P c= C
  ex R being non empty Subset of TOP-REAL 2 st
     R is_an_arc_of p1,p2 & P \/ R = C & P /\ R = {p1,p2};

theorem :: JORDAN18:19
  for P being Subset of TOP-REAL 2,
      p1,p2,q1,q2 being Point of TOP-REAL 2
    st P is_an_arc_of p1,p2 & q1 in P & q2 in P &
       q1 <> p1 & q1 <> p2 & q2 <> p1 & q2 <> p2 & q1 <> q2
   ex Q being non empty Subset of TOP-REAL 2 st
      Q is_an_arc_of q1,q2 & Q c= P & Q misses {p1,p2};

begin  :: Two Special Points on a Simple Closed Curve

definition let p,P;
 func North-Bound(p,P) -> Point of TOP-REAL 2 equals
:: JORDAN18:def 1
 |[p`1,inf(proj2.:(P /\ north_halfline p))]|;
 func South-Bound(p,P) -> Point of TOP-REAL 2 equals
:: JORDAN18:def 2
 |[p`1,sup(proj2.:(P /\ south_halfline p))]|;
end;


theorem :: JORDAN18:20
  North-Bound(p,P)`1 = p`1 & South-Bound(p,P)`1 = p`1;

theorem :: JORDAN18:21
  North-Bound(p,P)`2 = inf(proj2.:(P /\ north_halfline p)) &
  South-Bound(p,P)`2 = sup(proj2.:(P /\ south_halfline p));

theorem :: JORDAN18:22
  for C being compact Subset of TOP-REAL 2 holds
  p in BDD C implies
  North-Bound(p,C) in C & North-Bound(p,C) in north_halfline p &
  South-Bound(p,C) in C & South-Bound(p,C) in south_halfline p;

theorem :: JORDAN18:23
  for C being compact Subset of TOP-REAL 2 holds
  p in BDD C implies South-Bound(p,C)`2 < p`2 & p`2 < North-Bound(p,C)`2;

theorem :: JORDAN18:24
  for C being compact Subset of TOP-REAL 2 holds
  p in BDD C implies
  inf(proj2.:(C /\ north_halfline p)) > sup(proj2.:(C /\ south_halfline p));

theorem :: JORDAN18:25
    for C being compact Subset of TOP-REAL 2 holds
  p in BDD C implies South-Bound(p,C) <> North-Bound(p,C);

theorem :: JORDAN18:26
  for C being Subset of TOP-REAL 2 holds
   LSeg(North-Bound(p,C),South-Bound(p,C)) is vertical;

theorem :: JORDAN18:27
    for C being compact Subset of TOP-REAL 2 holds
   p in BDD C implies
   LSeg(North-Bound(p,C),South-Bound(p,C)) /\ C
      = { North-Bound(p,C), South-Bound(p,C) };

theorem :: JORDAN18:28
    for C being compact Subset of TOP-REAL 2 holds
  p in BDD C & q in BDD C & p`1 <> q`1 implies
   North-Bound(p,C), South-Bound(q,C), North-Bound(q,C), South-Bound(p,C)
       are_mutually_different;

begin  :: An Order of Points on a Simple Closed Curve

definition let n,V,s1,s2,t1,t2;
 pred s1,s2, V-separate t1,t2 means
:: JORDAN18:def 3
 for A being Subset of TOP-REAL n st
    A is_an_arc_of s1,s2 & A c= V
  holds A meets {t1,t2};
 end;
notation let n,V,s1,s2,t1,t2;
 antonym s1,s2 are_neighbours_wrt t1,t2, V for s1,s2, V-separate t1,t2 ; end;


theorem :: JORDAN18:29
    t,t, V-separate s1,s2;

theorem :: JORDAN18:30
    s1,s2, V-separate t1,t2 implies s2,s1, V-separate t1,t2;

theorem :: JORDAN18:31
    s1,s2, V-separate t1,t2 implies s1,s2, V-separate t2,t1;

theorem :: JORDAN18:32
  s,t1, V-separate s,t2;

theorem :: JORDAN18:33
  t1,s, V-separate t2,s;

theorem :: JORDAN18:34
  t1,s, V-separate s,t2;

theorem :: JORDAN18:35
  s,t1, V-separate t2,s;

theorem :: JORDAN18:36
    for p1,p2,q being Point of TOP-REAL 2 st
    q in C & p1 in C & p2 in C & p1 <> p2 & p1 <> q & p2 <> q holds
  p1,p2 are_neighbours_wrt q,q, C;

theorem :: JORDAN18:37
    p1 <> p2 & p1 in C & p2 in C implies
  (p1,p2, C-separate q1,q2 implies q1,q2, C-separate p1,p2);

theorem :: JORDAN18:38
    p1 in C & p2 in C & q1 in C &
  p1 <> p2 & q1 <> p1 & q1 <> p2 & q2 <> p1 & q2 <> p2
    implies p1,p2 are_neighbours_wrt q1,q2, C
         or p1,q1 are_neighbours_wrt p2,q2, C;
